
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ИспользоватьМеждународныйУчет = Ложь;
	//++ НЕ УТКА
	ИспользоватьМеждународныйУчет = ПолучитьФункциональнуюОпцию("ИспользоватьМеждународныйФинансовыйУчет");
	
	ПланСчетовМеждународный.ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ПланСчетовМеждународный.Ссылка КАК Ссылка,
	|	ПланСчетовМеждународный.Код КАК Код,
	|	ПланСчетовМеждународный.Наименование КАК Наименование,
	|	ВЫБОР
	|		КОГДА ПравилаПолученияФактаПоСтатьямБюджетов.ИсточникДанных ЕСТЬ NULL 
	|				И ПравилаПолученияФактаПоПоказателямБюджетов.ИсточникДанных ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьПравилаПолученияФакта
	|ИЗ
	|	ПланСчетов.Международный КАК ПланСчетовМеждународный
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			ПравилаПолученияФактаПоСтатьямБюджетов.ИсточникДанных КАК ИсточникДанных
	|		ИЗ
	|			Справочник.ПравилаПолученияФактаПоСтатьямБюджетов КАК ПравилаПолученияФактаПоСтатьямБюджетов
	|		ГДЕ
	|			ПравилаПолученияФактаПоСтатьямБюджетов.РазделИсточникаДанных = ЗНАЧЕНИЕ(Перечисление.РазделыИсточниковДанныхБюджетирования.МеждународныйУчет)) КАК ПравилаПолученияФактаПоСтатьямБюджетов
	|		ПО ПланСчетовМеждународный.Ссылка = ПравилаПолученияФактаПоСтатьямБюджетов.ИсточникДанных
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			ПравилаПолученияФактаПоПоказателямБюджетов.ИсточникДанных КАК ИсточникДанных
	|		ИЗ
	|			Справочник.ПравилаПолученияФактаПоПоказателямБюджетов КАК ПравилаПолученияФактаПоПоказателямБюджетов
	|		ГДЕ
	|			ПравилаПолученияФактаПоПоказателямБюджетов.РазделИсточникаДанных = ЗНАЧЕНИЕ(Перечисление.РазделыИсточниковДанныхБюджетирования.МеждународныйУчет)) КАК ПравилаПолученияФактаПоПоказателямБюджетов
	|		ПО ПланСчетовМеждународный.Ссылка = ПравилаПолученияФактаПоПоказателямБюджетов.ИсточникДанных";
	
	ПланСчетовМеждународный.ОсновнаяТаблица = "ПланСчетов.Международный";
	
	//-- НЕ УТКА
	
	Если Не ПолучитьФункциональнуюОпцию("УправлениеПредприятием") Тогда
		Элементы.ГруппаОтражениеВУчете.Видимость = Ложь;
	КонецЕсли;
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	//++ НЕ УТКА
	ПодключитьОбработчикОжидания("ПроверкаНеобходимостиОтраженияВУчете", 60);
	//-- НЕ УТКА
	
	Возврат; //Только для использования в УП
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаписаноПравилоПолученияФактаПоСтатьеБюджета" Тогда
		УправлениеФормой();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиЭлементовФормы

&НаКлиенте
Процедура РежимНастройкиПравилПриИзменении(Элемент)
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыНастройки = Новый Структура("ОбъектНастройки", ВыбраннаяСтрока);
	ОткрытьФорму("Обработка.НастройкаПравилПолученияФактаБюджетирования.Форма.НастройкаПравил",
									ПараметрыНастройки,,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьиБюджетовПриАктивизацииСтроки(Элемент)
	УстановитьДоступностьКоманд(Элементы, "СтатьиБюджетов");
КонецПроцедуры

&НаКлиенте
Процедура ПоказателиБюджетовПриАктивизацииСтроки(Элемент)
	УстановитьДоступностьКоманд(Элементы, "ПоказателиБюджетов");
КонецПроцедуры

&НаКлиенте
Процедура ХозяйственныеОперацииПриАктивизацииСтроки(Элемент)
	УстановитьДоступностьКоманд(Элементы, "ХозяйственныеОперации");
КонецПроцедуры


&НаКлиенте
Процедура СтатьиАктивовПассивовПриАктивизацииСтроки(Элемент)
	УстановитьДоступностьКоманд(Элементы, "СтатьиАктивовПассивов");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтразитьВУчете(Команда)
	
	//++ НЕ УТКА
	ОтразитьВУчетеНаСервере();
	//-- НЕ УТКА
	
	Возврат; //Только для использования в УП
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьРасписаниеНажатие(Элемент)
	
	//++ НЕ УТКА
	ПараметрыФормы = ПараметрыФормыНастройкиРегламентногоЗадания();
	ОткрытьФорму("Обработка.РегламентныеИФоновыеЗадания.Форма.РегламентноеЗадание",
		ПараметрыФормы,ЭтаФорма,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	//-- НЕ УТКА
	
	Возврат; //Только для использования в УП
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатРаботыПравил(Команда)
	Перем ЗаголовокОтчета;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыФормы.Вставить("ВидимостьКомандВариантовОтчетов", Ложь);
	
	ОпределитьОбъектНастройкиПравил(ПараметрыФормы, ЗаголовокОтчета);
	
	ОткрытьФорму("Отчет.РезультатРаботыПравилПолученияФактическихДанных.Форма",
														ПараметрыФормы, , ЗаголовокОтчета);
		
КонецПроцедуры

&НаКлиенте
Процедура НастроитьПравила(Команда)
	
	ПараметрыНастройки = Новый Структура("ОбъектНастройки", ОпределитьОбъектНастройкиПравил());
	ОткрытьФорму("Обработка.НастройкаПравилПолученияФактаБюджетирования.Форма.НастройкаПравил",
									ПараметрыНастройки,,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОбъектНастройки(Команда)
	
	ПоказатьЗначение(,ОпределитьОбъектНастройкиПравил());
	
КонецПроцедуры

&НаКлиенте
Функция МассивТабличныхЧастей()
	
	Массив = Новый Массив;
	Массив.Добавить("СтатьиБюджетов");
	Массив.Добавить("ПоказателиБюджетов");
	Массив.Добавить("ХозяйственныеОперации");
	Массив.Добавить("СтатьиАктивовПассивов");
	Массив.Добавить("ПланСчетовХозрасчетный");
	//++ НЕ УТКА
	Массив.Добавить("ПланСчетовМеждународный");
	//-- НЕ УТКА
	
	Возврат Массив;

КонецФункции

&НаКлиенте
Процедура Все(Команда)
	
	Массив = МассивТабличныхЧастей();
	Для Каждого ТабличнаяЧасть из Массив Цикл
	
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ЭтаФорма[ТабличнаяЧасть], 
																"ЕстьПравилаПолученияФакта", ,,, Ложь);
		Элементы["ТолькоСПравилами" + ТабличнаяЧасть].Заголовок = "Показывать";
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура БезПравил(Команда)
	
	Массив = МассивТабличныхЧастей();
	Для Каждого ТабличнаяЧасть из Массив Цикл
	
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ЭтаФорма[ТабличнаяЧасть], 
															"ЕстьПравилаПолученияФакта", 
															Ложь, ВидСравненияКомпоновкиДанных.Равно,,Истина);
		Элементы["ТолькоСПравилами" + ТабличнаяЧасть].Заголовок = "Только без правил";
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТолькоСПравилами(Команда)
	
	Массив = МассивТабличныхЧастей();
	Для Каждого ТабличнаяЧасть из Массив Цикл
	
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(ЭтаФорма[ТабличнаяЧасть], 
															"ЕстьПравилаПолученияФакта", 
															Истина, ВидСравненияКомпоновкиДанных.Равно,,Истина);
		Элементы["ТолькоСПравилами" + ТабличнаяЧасть].Заголовок = "Только с правилами";
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеФормой()
	Перем КоличествоДокументов;
	
	Если РежимНастройкиПравил = 0 Тогда
		Элементы.СтраницаПоСтатьямИПоказателямБюджетов.Видимость = Истина;
		Элементы.СтраницаПоИсточникамДанных.Видимость = Ложь;
	Иначе
		Элементы.СтраницаПоСтатьямИПоказателямБюджетов.Видимость = Ложь;
		Элементы.СтраницаПоИсточникамДанных.Видимость = Истина;
	КонецЕсли;
	
	Элементы.СтраницаПравилаПоСчетамМеждународногоУчета.Видимость = ИспользоватьМеждународныйУчет;
	Элементы.СтраницаПравилаПоСчетамРегламентированногоУчета.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет");
	
	//++ НЕ УТКА
	
	Если ДокументыОтражаютсяВБюджетировании() Тогда
		Элементы.ОтразитьВУчете.Видимость = Ложь;
		Элементы.ДекорацияДокументыОтражаются.Видимость = Истина;
		Элементы.ДекорацияКОтражению.Заголовок = НСтр("ru = 'Выполняется отражение документов в подсистеме ""Бюджетирование и планирование""'");
	Иначе
		
		КоличествоДокументов = 0;
		Если РегистрыСведений.ЗаданияКОтражениюВБюджетировании.ТребуетсяОтражениеВБюджетированииДляОтчетаЗаПериод(
																			Дата(1, 1, 1), , КоличествоДокументов, Истина) Тогда
		
			Элементы.ОтразитьВУчете.Видимость = Истина;
			Элементы.ДекорацияДокументыОтражаются.Видимость = Ложь;
			Элементы.ДекорацияКОтражению.Заголовок = НСтр("ru = 'К отражению в подсистеме ""Бюджетирование и планирование"" %1 документов'");
			Элементы.ДекорацияКОтражению.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
																Элементы.ДекорацияКОтражению.Заголовок, КоличествоДокументов);
		Иначе
			Элементы.ОтразитьВУчете.Видимость = Ложь;
			Элементы.ДекорацияДокументыОтражаются.Видимость = Ложь;
			Элементы.ДекорацияКОтражению.Заголовок = НСтр("ru = 'Отражение документов в подсистеме ""Бюджетирование и планирование"" не требуется'");
		КонецЕсли;
	КонецЕсли;
	
	//-- НЕ УТКА

КонецПроцедуры

//++ НЕ УТКА

&НаСервереБезКонтекста
Функция ДокументыОтражаютсяВБюджетировании()
	
	Ключ = "ОтражениеДокументовВБюджетировании";
	
	Отбор = Новый Структура();
	Отбор.Вставить("Ключ", Ключ);
	Отбор.Вставить("СостояниеЗадания", Перечисления.СостоянияЗаданий.Выполняется);
	
	АктивныеЗадания = ОчередьЗаданий.ПолучитьЗадания(Отбор);
	
	Возврат АктивныеЗадания.Количество();
	
КонецФункции

&НаКлиенте
Процедура ПроверкаНеобходимостиОтраженияВУчете()
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ОтразитьВУчетеНаСервере()
	
	ФактическиеДанныеБюджетированияСервер.ОтразитьДокументыФоновымЗаданием(,);
	УправлениеФормой();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПараметрыФормыНастройкиРегламентногоЗадания()
	Отбор = Новый Структура();
	Отбор.Вставить("Метаданные", "ОтражениеДокументовВБюджетировании");
	Задания = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
	
	УникальныйИдентификаторРегламентногоЗадания = Неопределено;
	Если Задания.Количество() > 0 Тогда
		УникальныйИдентификаторРегламентногоЗадания = Задания[0].УникальныйИдентификатор;		
	КонецЕсли;
		
	ПараметрыФормы = Новый Структура("Действие, Идентификатор","Изменить",УникальныйИдентификаторРегламентногоЗадания);
	Возврат ПараметрыФормы;
КонецФункции

//-- НЕ УТКА

&НаКлиенте
Функция ОпределитьОбъектНастройкиПравил(ДополняемыеПараметрыФормы = Неопределено, ЗаголовокОтчета = "")
	
	Если РежимНастройкиПравил = 0 Тогда
		Если Элементы.СтраницыПравилаПоСтатьямПоказателямБюджетов.ТекущаяСтраница =
											Элементы.СтраницаПравилаПоСтатьямБюджетов Тогда
			ОбъектНастройки = Элементы.СтатьиБюджетов.ТекущаяСтрока;
			ЗаголовокОтчета = "СтатьяБюджетов = " + ОбъектНастройки;
			Если ДополняемыеПараметрыФормы <> Неопределено Тогда
				ДополняемыеПараметрыФормы.Вставить("КлючВарианта", "РезультатРаботыПравилПоСтатьеБюджетов");
				ДополняемыеПараметрыФормы.Вставить("Отбор", Новый Структура("СтатьяБюджетов", ОбъектНастройки));
			КонецЕсли;
		ИначеЕсли Элементы.СтраницыПравилаПоСтатьямПоказателямБюджетов.ТекущаяСтраница =
											Элементы.СтраницаПравилаПоПоказателямБюджетов Тогда
			ОбъектНастройки = Элементы.ПоказателиБюджетов.ТекущаяСтрока;
			ЗаголовокОтчета = "ПоказательБюджетов = " + ОбъектНастройки;
			Если ДополняемыеПараметрыФормы <> Неопределено Тогда
				ДополняемыеПараметрыФормы.Вставить("КлючВарианта", "РезультатРаботыПравилПоПоказателюБюджетов");
				ДополняемыеПараметрыФормы.Вставить("Отбор", Новый Структура("ПоказательБюджетов", ОбъектНастройки));
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если Элементы.СтраницыРазделыИсточниковДанных.ТекущаяСтраница = 
											Элементы.СтраницаПравилаПоХозяйственнымОперациям Тогда
			ОбъектНастройки = Элементы.ХозяйственныеОперации.ТекущаяСтрока;
			ЗаголовокОтчета = "ХозяйственнаяОперация = " + ОбъектНастройки;
			Если ДополняемыеПараметрыФормы <> Неопределено Тогда
				ДополняемыеПараметрыФормы.Вставить("КлючВарианта", "РезультатРаботыПравилПоСтатьямБюджетов");
				ДополняемыеПараметрыФормы.Вставить("Отбор", Новый Структура("ИсточникДанных", ОбъектНастройки));
			КонецЕсли;
		ИначеЕсли Элементы.СтраницыРазделыИсточниковДанных.ТекущаяСтраница = 
											Элементы.СтраницаПравилаПоСтатьямАктивовПассивов Тогда
			ОбъектНастройки = Элементы.СтатьиАктивовПассивов.ТекущаяСтрока;
			ЗаголовокОтчета = "СтатьяАктивовПассивов = " + ОбъектНастройки;
			Если ДополняемыеПараметрыФормы <> Неопределено Тогда
				ДополняемыеПараметрыФормы.Вставить("КлючВарианта", "РезультатРаботыПравилПоПоказателямБюджетов");
				ДополняемыеПараметрыФормы.Вставить("Отбор", Новый Структура("ИсточникДанных", ОбъектНастройки));
			КонецЕсли;
		ИначеЕсли Элементы.СтраницыРазделыИсточниковДанных.ТекущаяСтраница = 
											Элементы.СтраницаПравилаПоСчетамРегламентированногоУчета Тогда
			ОбъектНастройки = Элементы.ПланСчетовХозрасчетный.ТекущаяСтрока;
			ЗаголовокОтчета = "СчетУчета = " + ОбъектНастройки;
			Если ДополняемыеПараметрыФормы <> Неопределено Тогда
				ДополняемыеПараметрыФормы.Вставить("КлючВарианта", "РезультатРаботыПравилПоСтатьямИПоказателямБюджетов");
				ДополняемыеПараметрыФормы.Вставить("Отбор", Новый Структура("ИсточникДанных", ОбъектНастройки));
			КонецЕсли;
		ИначеЕсли Элементы.СтраницыРазделыИсточниковДанных.ТекущаяСтраница = 
											Элементы.СтраницаПравилаПоСчетамМеждународногоУчета Тогда
			ОбъектНастройки = Элементы.ПланСчетовМеждународный.ТекущаяСтрока;
			ЗаголовокОтчета = "СчетУчета = " + ОбъектНастройки;
			Если ДополняемыеПараметрыФормы <> Неопределено Тогда
				ДополняемыеПараметрыФормы.Вставить("КлючВарианта", "РезультатРаботыПравилПоСтатьямИПоказателямБюджетов");
				ДополняемыеПараметрыФормы.Вставить("Отбор", Новый Структура("ИсточникДанных", ОбъектНастройки));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ОбъектНастройки;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста 
Процедура УстановитьДоступностьКоманд(Элементы, ИмяДинамическогоСписка)
	
	ИмяЭлементаФормы = ИмяДинамическогоСписка + "НастроитьПравила";
	
	Если Элементы[ИмяДинамическогоСписка].ТекущаяСтрока = Неопределено
		Или Элементы[ИмяДинамическогоСписка].ТекущиеДанные.ЭтоГруппа Тогда
		Элементы[ИмяЭлементаФормы].Доступность = Ложь;
	Иначе
		Элементы[ИмяЭлементаФормы].Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти