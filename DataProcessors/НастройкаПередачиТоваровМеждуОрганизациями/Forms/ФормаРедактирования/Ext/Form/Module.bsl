
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Организация = Параметры.Организация;
	Продавец = Параметры.Продавец;
	ТипЗапасов = Параметры.ТипЗапасов;
	Валюта = Параметры.Валюта;
	Договор = Параметры.Договор;
	ВидЦены = Параметры.ВидЦены;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВалют") И  НЕ ЗначениеЗаполнено(Параметры.Валюта) Тогда
		Валюта = ДоходыИРасходыСервер.ПолучитьВалютуУправленческогоУчета();	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.СпособПередачиТоваров) Тогда
		СпособПередачиТоваров = Параметры.СпособПередачиТоваров;
	Иначе
		СпособПередачиТоваров = Перечисления.СпособыПередачиТоваров.НеПередается;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеЭлементамиФормы();
	УстановитьПараметрыВыбораДоговора();
	УстановитьПараметрыВыбораВидаЦены();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если СпособПередачиТоваров <> Перечисления.СпособыПередачиТоваров.ПередачаНаКомиссию
	 И СпособПередачиТоваров <> Перечисления.СпособыПередачиТоваров.ПередачаНаКомиссиюВозврат Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Валюта");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(
		ПроверяемыеРеквизиты,
		МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СпособПередачиТоваровПриИзменении(Элемент)
	
	ПередачаНаКомиссию = ПредопределенноеЗначение("Перечисление.СпособыПередачиТоваров.ПередачаНаКомиссию");
	ПередачаНаКомиссиюВозврат = ПредопределенноеЗначение("Перечисление.СпособыПередачиТоваров.ПередачаНаКомиссиюВозврат");
	Если СпособПередачиТоваров <> ПередачаНаКомиссию
	 И СпособПередачиТоваров <> ПередачаНаКомиссиюВозврат Тогда
		Валюта = Неопределено;
	КонецЕсли;
	Договор = Неопределено;
	
	УправлениеЭлементамиФормы();
	УстановитьПараметрыВыбораДоговора();
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютаПриИзменении(Элемент)
	Договор = Неопределено;
	УстановитьПараметрыВыбораДоговора();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьДанные(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		ЗаписатьНастройкуПередачиТоваров();
		
		ПараметрыЗакрытия = Новый Структура();
		ПараметрыЗакрытия.Вставить("СпособПередачиТоваров", СпособПередачиТоваров);
		ПараметрыЗакрытия.Вставить("Валюта", Валюта);
		ПараметрыЗакрытия.Вставить("Договор", Договор);
		ПараметрыЗакрытия.Вставить("ВидЦены", ВидЦены);
		
		Закрыть(ПараметрыЗакрытия);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Валюта.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);

КонецПроцедуры

#Область УправлениеЭлементамиФормы

&НаКлиенте
Процедура УправлениеЭлементамиФормы()
	
	Если СпособПередачиТоваров = ПредопределенноеЗначение("Перечисление.СпособыПередачиТоваров.ПередачаНаКомиссию")
	 ИЛИ СпособПередачиТоваров = ПредопределенноеЗначение("Перечисление.СпособыПередачиТоваров.ПередачаНаКомиссиюВозврат") Тогда
		Элементы.Валюта.ТолькоПросмотр = Ложь;
		Элементы.Валюта.АвтоОтметкаНезаполненного = Истина;
		Элементы.Договор.Доступность = Истина;
		Элементы.ВидЦены.Доступность = Истина;
	ИначеЕсли СпособПередачиТоваров = ПредопределенноеЗначение("Перечисление.СпособыПередачиТоваров.НеПередается")
		ИЛИ СпособПередачиТоваров = ПредопределенноеЗначение("Перечисление.СпособыПередачиТоваров.НеТребуется")
		ИЛИ СпособПередачиТоваров = ПредопределенноеЗначение("Перечисление.СпособыПередачиТоваров.ПустаяСсылка") Тогда
		Элементы.Договор.Доступность = Ложь;
		Элементы.ВидЦены.Доступность = Ложь;
		Элементы.Валюта.ТолькоПросмотр = Истина;
		Элементы.Валюта.ОтметкаНезаполненного = Ложь;
		Элементы.Валюта.АвтоОтметкаНезаполненного = Ложь;
	Иначе
		Элементы.Валюта.ТолькоПросмотр = Истина;
		Элементы.Валюта.ОтметкаНезаполненного = Ложь;
		Элементы.Валюта.АвтоОтметкаНезаполненного = Ложь;
		Элементы.Договор.Доступность = Истина;
		Элементы.ВидЦены.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервере
Процедура ЗаписатьНастройкуПередачиТоваров()
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = РегистрыСведений.НастройкаПередачиТоваровМеждуОрганизациями.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ОрганизацияВладелец = Организация;
	МенеджерЗаписи.ОрганизацияПродавец = Продавец;
	МенеджерЗаписи.ТипЗапасов = ТипЗапасов;
	МенеджерЗаписи.Договор = Договор;
	МенеджерЗаписи.ВидЦены = ВидЦены;
	
	Если СпособПередачиТоваров = Перечисления.СпособыПередачиТоваров.ПередачаНаКомиссиюВозврат
		Или СпособПередачиТоваров = Перечисления.СпособыПередачиТоваров.ПередачаНаКомиссию Тогда
		Если Не ЗначениеЗаполнено(Валюта) И НЕ ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВалют") Тогда
			МенеджерЗаписи.Валюта = Константы.ВалютаУправленческогоУчета.Получить();
		Иначе
			МенеджерЗаписи.Валюта = Валюта;
		КонецЕсли;
	Иначе
		Валюта = Справочники.Валюты.ПустаяСсылка();
	КонецЕсли;
	
	Если СпособПередачиТоваров = Перечисления.СпособыПередачиТоваров.ПродажаВозврат Тогда
		МенеджерЗаписи.СпособПередачиТоваров = Перечисления.СпособыПередачиТоваров.Продажа;
	ИначеЕсли СпособПередачиТоваров = Перечисления.СпособыПередачиТоваров.ПередачаНаКомиссиюВозврат Тогда
		МенеджерЗаписи.СпособПередачиТоваров = Перечисления.СпособыПередачиТоваров.ПередачаНаКомиссию;
	Иначе
		МенеджерЗаписи.СпособПередачиТоваров = СпособПередачиТоваров;
	КонецЕсли;
	
	Если СпособПередачиТоваров = Перечисления.СпособыПередачиТоваров.ПродажаВозврат
	 ИЛИ СпособПередачиТоваров = Перечисления.СпособыПередачиТоваров.ПередачаНаКомиссиюВозврат Тогда
		МенеджерЗаписи.ИспользоватьПриВозвратеОтКлиента = Истина;
	КонецЕсли;
	
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПараметрыВыбораДоговора()
	
	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Организация", Организация));
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ОрганизацияПолучатель", Продавец));
	Если ЗначениеЗаполнено(Валюта) Тогда
		МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ВалютаВзаиморасчетов", Валюта));
	КонецЕсли;
	Если СпособПередачиТоваров = ПредопределенноеЗначение("Перечисление.СпособыПередачиТоваров.ПередачаНаКомиссию")
		ИЛИ СпособПередачиТоваров = ПредопределенноеЗначение("Перечисление.СпособыПередачиТоваров.ПередачаНаКомиссиюВозврат") Тогда
		МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ТипДоговора", ПредопределенноеЗначение("Перечисление.ТипыДоговоровМеждуОрганизациями.Комиссионный")));
	Иначе
		МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ТипДоговора", ПредопределенноеЗначение("Перечисление.ТипыДоговоровМеждуОрганизациями.КупляПродажа")));
	КонецЕсли;
	Элементы.Договор.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПараметрыВыбораВидаЦены()
	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.ИспользоватьПриПередачеМеждуОрганизациями", Истина));
	МассивПараметров.Добавить(Новый ПараметрВыбора("Отбор.Статус", ПредопределенноеЗначение("Перечисление.СтатусыДействияВидовЦен.Действует")));
	Элементы.ВидЦены.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
КонецПроцедуры


#КонецОбласти

#КонецОбласти
