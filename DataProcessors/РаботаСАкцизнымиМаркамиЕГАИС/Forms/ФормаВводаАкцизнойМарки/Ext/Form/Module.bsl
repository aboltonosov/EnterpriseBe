
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	Если Параметры.Редактирование Тогда
		
		АкцизныеМарки.Загрузить(ПолучитьИзВременногоХранилища(Параметры.АдресВоВременномХранилище));
		
		Заголовок = НСтр("ru = 'Редактирование акцизных марок'");
		
		Элементы.ЗавершитьРедактирование.Видимость = Истина;
		
		Элементы.Редактирование.Видимость                              = Истина;
		Элементы.СканированиеМаркиПослеСканированияШтрихкода.Видимость = Ложь;
		
		Элементы.АкцизныеМаркиСгенерироватьАкцизныеМарки.Видимость = ОбщегоНазначенияКлиентСервер.РежимОтладки();
		
	Иначе
		
		Заголовок = НСтр("ru = 'Сканирование акцизной марки'");
		
		Элементы.ЗавершитьРедактирование.Видимость = Ложь;
		
		Элементы.Редактирование.Видимость                              = Ложь;
		Элементы.СканированиеМаркиПослеСканированияШтрихкода.Видимость = Истина;
		
	КонецЕсли;
	
	Номенклатура = Параметры.Номенклатура;
	Характеристика = Параметры.Характеристика;
	
	ИспользоватьАкцизныеМарки = Истина;
	
	СобытияФормЕГАИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПоддерживаемыеТипыПодключаемогоОборудования = "СканерШтрихкода";
	
	ОповещениеПриПодключении = Новый ОписаниеОповещения("ПодключитьОборудованиеЗавершение", ЭтотОбъект);
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(
		ОповещениеПриПодключении,
		ЭтотОбъект,
		ПоддерживаемыеТипыПодключаемогоОборудования);
	
	ЭтотОбъект.ТекущийЭлемент = Элементы.ДекорацияКартинка;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	ОповещениеПриОтключении = Новый ОписаниеОповещения("ОтключитьОборудованиеЗавершение", ЭтотОбъект);
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(
		ОповещениеПриОтключении, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	СобытияФормЕГАИСКлиент.ОбработкаОповещенияПолученыШтрихкоды(
		Новый ОписаниеОповещения("Подключаемый_ПолученыШтрихкоды", ЭтотОбъект),
		ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
	СобытияФормЕГАИСКлиентПереопределяемый.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

&НаКлиенте
Процедура ВнешнееСобытие(Источник, Событие, Данные)
	
	// ПодключаемоеОборудование
	СобытияФормЕГАИСКлиент.ВнешнееСобытиеПолученыШтрихкоды(
		Новый ОписаниеОповещения("Подключаемый_ПолученыШтрихкоды", ЭтотОбъект),
		ЭтотОбъект, Источник, Событие, Данные);
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
	Отказ = Ложь;
	
	МассивКодовМарок = Новый Массив;
	Для Каждого СтрокаТЧ Из АкцизныеМарки Цикл
		Если ПустаяСтрока(СтрокаТЧ.КодАкцизнойМарки) Тогда
			Поле = "АкцизныеМарки[%1].КодАкцизнойМарки";
			Поле = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Поле, Формат(АкцизныеМарки.Индекс(СтрокаТЧ), "ЧРГ="));
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru='Не указана акцизная марка'"),, Поле, "", Отказ);
		Иначе
			МассивКодовМарок.Добавить(СтрокаТЧ.КодАкцизнойМарки);
		КонецЕсли;
	КонецЦикла;
	
	МассивОшибочныхМарок = КодыМарокНеСоответствующихФормату(МассивКодовМарок);
	
	Для Каждого КодАкцизнойМарки Из МассивОшибочныхМарок Цикл
		СтрокиТаблицы = АкцизныеМарки.НайтиСтроки(Новый Структура("КодАкцизнойМарки", КодАкцизнойМарки));
		Если СтрокиТаблицы.Количество() > 0 Тогда
			Поле = "АкцизныеМарки[%1].КодАкцизнойМарки";
			Поле = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Поле, Формат(АкцизныеМарки.Индекс(СтрокиТаблицы[0]), "ЧРГ="));
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Штрихкод не соответствует формату штрихкода акцизной марки.'"),, Поле, "", Отказ);
		КонецЕсли;
	КонецЦикла;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Закрыть(АдресТаблицыАкцизныхМаркиВоВременномХранилище(ВладелецФормы.УникальныйИдентификатор));
	
КонецПроцедуры

&НаКлиенте
Процедура СгенерироватьАкцизныеМарки(Команда)
	
	НоваяСтрока = АкцизныеМарки.Добавить();
	НоваяСтрока.КодАкцизнойМарки = СгенерироватьАкцизнуюМарку();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область АкцизныеМарки

&НаКлиенте
Процедура ВводАкцизнойМаркиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КодАкцизнойМарки           = Результат.КодАкцизнойМарки;
	СопоставленнаяНоменклатура = Результат.СопоставленнаяНоменклатура;
	
	Если СопоставленнаяНоменклатура <> Неопределено
		И ЗначениеЗаполнено(СопоставленнаяНоменклатура.Номенклатура)
		И СопоставленнаяНоменклатура.Номенклатура <> Номенклатура Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СтрШаблон(НСтр("ru = 'Акцизная марка не соответствует номенклатуре %1'"), Номенклатура));
		Возврат;
	КонецЕсли;
	
	Если Элементы.СканированиеМаркиПослеСканированияШтрихкода.Видимость Тогда
		
		ПодключитьОбработчикОжидания("ЗакрытьФормуПриСканировании", 0.1, Истина);
		
	Иначе
		
		Отбор = Новый Структура;
		Отбор.Вставить("КодАкцизнойМарки", КодАкцизнойМарки);
		
		НайденныеСтроки = АкцизныеМарки.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() = 0 Тогда
			
			НоваяСтрока = АкцизныеМарки.Добавить();
			НоваяСтрока.КодАкцизнойМарки = КодАкцизнойМарки;
			
		Иначе
			
			Элементы.АкцизныеМарки.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция КодыМарокНеСоответствующихФормату(МассивКодовМарок)
	
	Результат = Новый Массив;
	
	Для Каждого КодАкцизнойМарки Из МассивКодовМарок Цикл
		Если НЕ АкцизныеМаркиВызовСервера.ЭтоШтрихкодАкцизнойМарки(КодАкцизнойМарки) Тогда
			Результат.Добавить(КодАкцизнойМарки);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Штрихкоды

&НаКлиенте
Процедура Подключаемый_ПолученыШтрихкоды(ДанныеШтрихкодов, ДополнительныеПараметры) Экспорт
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		НСтр("ru = 'Штрихкод не соответствует формату штрихкода акцизной марки.'"));
	
КонецПроцедуры

#КонецОбласти

#Область Оборудование

&НаКлиенте
Процедура ПодключитьОборудованиеЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если НЕ РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр( "ru = 'При подключении оборудования произошла ошибка:""%ОписаниеОшибки%"".'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%" , РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьОборудованиеЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	Если НЕ РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр( "ru = 'При отключении оборудования произошла ошибка: ""%ОписаниеОшибки%"".'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%" , РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаКлиенте
Процедура ЗакрытьФормуПриСканировании()
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("КодАкцизнойМарки",           КодАкцизнойМарки);
	ВозвращаемоеЗначение.Вставить("СопоставленнаяНоменклатура", СопоставленнаяНоменклатура);
	
	Закрыть(ВозвращаемоеЗначение);
	
КонецПроцедуры

&НаСервере
Функция АдресТаблицыАкцизныхМаркиВоВременномХранилище(УникальныйИдентификатор)
	
	Возврат ПоместитьВоВременноеХранилище(АкцизныеМарки.Выгрузить(), УникальныйИдентификатор);
	
КонецФункции

&НаСервереБезКонтекста
Функция СгенерироватьАкцизнуюМарку()
	
	ГенераторСлучайныхЧисел = Новый ГенераторСлучайныхЧисел(ТекущаяУниверсальнаяДатаВМиллисекундах());
	СлучайноеЧисло = ГенераторСлучайныхЧисел.СлучайноеЧисло(1, 9999);
	
	КодАкцизнойМарки = "45300002922078110073842805150040017640901295000024" + Формат(ТекущаяДата(), "ДФ=ddMMyyyyЧЧммсс") + Формат(СлучайноеЧисло, "ЧЦ=4; ЧВН=; ЧГ=0");
	
	Возврат КодАкцизнойМарки;
	
КонецФункции

#КонецОбласти

#КонецОбласти
