#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область Печать

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	СтруктураТипов = ОбщегоНазначенияУТ.СоответствиеМассивовПоТипамОбъектов(МассивОбъектов);
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЭСЧФ") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ЭСЧФ",
			"Счет-Фактура",
			СформироватьПечатнуюФормуСчетФактура(СтруктураТипов, ОбъектыПечати, ПараметрыПечати));
		
	КонецЕсли;
	
	ФормированиеПечатныхФорм.ЗаполнитьПараметрыОтправки(ПараметрыВывода.ПараметрыОтправки, СтруктураТипов, КоллекцияПечатныхФорм);
	
КонецПроцедуры

#Область Печать_СчетФактура

Функция СформироватьПечатнуюФормуСчетФактура(СтруктураТипов, ОбъектыПечати, ПараметрыПечати)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СчетФактура";
	
	НомерТипаДокумента = 0;
	
	Для Каждого СтруктураОбъектов Из СтруктураТипов Цикл

		НомерТипаДокумента = НомерТипаДокумента + 1;
		Если НомерТипаДокумента > 1 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		Для Каждого ЭлементМассива Из СтруктураОбъектов.Значение Цикл 
			ТабличныйДокумент.Вывести(ПолучитьТабличныйДокумент(ЭлементМассива));
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;

КонецФункции

Функция ПолучитьТабличныйДокумент(СсылкаНаОбъект)
	
	ТекущийЭлемент = ЭлектронныеСчетаФактуры.ПолучитьЭДПоВладельцу(СсылкаНаОбъект);
	Если ТекущийЭлемент = Неопределено Тогда
		ШаблонСообщения = НСтр("ru='Отсутствует сформированный электронный документ'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, СсылкаНаОбъект);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат Новый ТабличныйДокумент;
	КонецЕсли;	
	
	ТабличныйДокумент = ЭлектронныеСчетаФактуры.ПолучитьДанныеДляПечати(ТекущийЭлемент);
	Если ТабличныйДокумент = Неопределено Тогда
		ШаблонСообщения = НСтр("ru='Печатная форма документа %1 не сформирована'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, ТекущийЭлемент);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат Новый ТабличныйДокумент;
	КонецЕсли;
	
	Возврат ТабличныйДокумент;
	
КонецФункции	

#КонецОбласти

#КонецОбласти

#КонецОбласти

#КонецЕсли
