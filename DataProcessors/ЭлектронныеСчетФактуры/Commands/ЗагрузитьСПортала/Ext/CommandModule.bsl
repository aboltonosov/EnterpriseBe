
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ДатаЗапроса = НачалоМесяца(ТекущаяДата());
	
	Оповещение = Новый ОписаниеОповещения("ЗагрузитьСПортала", ЭтотОбъект);
    ПоказатьВводЗначения(Оповещение, ДатаЗапроса, "Дата начала загрузки ЭСЧФ", Тип("Дата"));

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСПортала(ДатаЗапроса, ДополнительныеПараметры) Экспорт 
	
	Если ДатаЗапроса = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	EVatService = ЭлектронныеСчетаФактурыКлиент.ПодключитьEVatService();
	
	Если EVatService = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	InvList = EVatService.GetList(Формат(ДатаЗапроса, "ДФ=yyyy-MM-dd") + "T00:00:00Z");
	
	Если  InvList = Неопределено Тогда
		ШаблонСообщения = НСтр("ru='Ошибка при получении входящих документов: %1'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения,  EVatService.LastError);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;	
	
	Если InvList.Count = 0 Тогда
		ТекстСообщения = НСтр("ru='На портале vat.gov.by нет поступивших документов'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ШаблонСообщения = НСтр("ru='На сервере доступно %1 документа(ов) для скачивания'");
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения,  InvList.Count);
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	
	Для i = 0 to InvList.Count - 1   Цикл
		
		InvVatNumber = InvList.GetItemAttribute(i, "document/number");
		
		ИмяФайла = ПолучитьИмяВременногоФайла(".xml");
		
		InvVatXml = EVatService.GetEDoc(InvVatNumber); 
		Если InvVatXml = Неопределено Тогда
			ШаблонСообщения = НСтр("ru='Ошибка получения документа по номеру %1'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, InvVatNumber);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Продолжить;
		КонецЕсли;
		
		res = InvVatXml.Document.SaveToFile(ИмяФайла); 
		Если res <> 0 Тогда
			ШаблонСообщения = НСтр("ru='Ошибка сохранения документа документа: %1'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, EVatService.LastError);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Продолжить;
		КонецЕсли;
		
		Хранение = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ИмяФайла), Новый УникальныйИдентификатор);
		РезультатПомещенияФайлов = Новый Структура;
		РезультатПомещенияФайлов.Вставить("ИмяФайла", ИмяФайла);
		РезультатПомещенияФайлов.Вставить("Хранение", Хранение);
	
		ЗагрузитьИзXMLНаСервере(РезультатПомещенияФайлов);
		
	КонецЦикла;	
	
	ЭлектронныеСчетаФактурыКлиент.ОтключитьEVatService(EVatService);
	
	Оповестить("ОбновитьСостояниеЭД");
	
 КонецПроцедуры
 
 &НаСервере
Процедура ЗагрузитьИзXMLНаСервере(РезультатПомещенияФайлов)
	
	ЭлектронныеСчетаФактуры.ЗагрузитьИзXML(РезультатПомещенияФайлов);
	
КонецПроцедуры	
