
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ПараметрыДиалога = Новый Структура;
	ПараметрыДиалога.Вставить("Фильтр", "Файлы ЭСЧФ (*.xml)|*.xml");
	ПараметрыДиалога.Вставить("Расширение", "xml");

	ПолучаемыйФайл = СохранитьВXMLНаСервере(ПараметрКоманды);
	Если ПолучаемыйФайл <> Неопределено Тогда
		ОбменДаннымиКлиент.ВыбратьИСохранитьФайлНаКлиенте(ПолучаемыйФайл, ПараметрыДиалога);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СохранитьВXMLНаСервере(Знач СсылкаНаОбъект)
	
	ЭД = ЭлектронныеСчетаФактуры.ПолучитьЭДПоВладельцу(СсылкаНаОбъект);
	
	Если ЭД = Неопределено Тогда
		
		ШаблонСообщения = НСтр("ru='Отсутствует сформированный электронный документ (%1)'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, СсылкаНаОбъект);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
	Иначе
		
		ИмяФайла = ЭлектронныеСчетаФактуры.ПолучитьИмяФайлаДанныхЭД(ЭД);
		Хранение = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ИмяФайла));
		
		ПолучаемыйФайл = Новый Структура;
		ПолучаемыйФайл.Вставить("Имя", 		ЭД.НомерЭД);
		ПолучаемыйФайл.Вставить("Хранение", Хранение);
		
	КонецЕсли; 
	
	Возврат ПолучаемыйФайл;
	
КонецФункции


