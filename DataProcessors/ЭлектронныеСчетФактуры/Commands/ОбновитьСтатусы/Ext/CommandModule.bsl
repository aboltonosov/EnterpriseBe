
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ОбновитьСтатусы(ПараметрКоманды);	
	Оповестить("ОбновлениеЭСЧФ");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтатусы(МассивДокументов)
	
	ТаблицаДанных = ПолучитьТаблицуЭД(МассивДокументов);
	
	Если ТаблицаДанных = Неопределено Тогда
		ТекстСообщения = НСтр("ru='Отсутствуют документы для обновления статусов'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;	
	
	EVatService = ЭлектронныеСчетаФактурыКлиент.ПодключитьEVatService();
	
	Если EVatService = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	Попытка   
		
		Для Каждого ТекущаяСтрока Из ТаблицаДанных Цикл
			
			Если ПустаяСтрока(ТекущаяСтрока.НомерЭД) Тогда
				ТекстСообщения = НСтр("ru='Не заполнен номер электронного документа'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				Продолжить;
			КонецЕсли;
			
			EDocStatusInfo = EVatService.GetStatus(ТекущаяСтрока.НомерЭД);
			Если EDocStatusInfo = Неопределено Тогда
				
				ШаблонСообщения = НСтр("ru='Ошибка проверки документа: %1'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, EVatService.LastError);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				Продолжить;
				
			КонецЕсли;	 
			
			res = EDocStatusInfo.Verify;
			Если res <> 0 Тогда
				
				ШаблонСообщения = НСтр("ru='Ошибка проверки документа: %1'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, EVatService.LastError);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				Продолжить;
				
			Иначе	
				
				ШаблонСообщения = НСтр("ru='Получен статус: %1'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, EDocStatusInfo.Message);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				
				ОбновитьСтатусыНаСервере(EDocStatusInfo.Status, ТекущаяСтрока, EDocStatusInfo.Since);
				
			КонецЕсли;
			
		КонецЦикла;
		
		ШаблонСообщения = НСтр("ru='Обработано %1 документа(ов)'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, ТаблицаДанных.Количество());
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
	Исключение
		
		ШаблонСообщения = НСтр("ru='Ошибка обновления статусов: %1'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, ОписаниеОшибки());
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
	КонецПопытки;
	
	ЭлектронныеСчетаФактурыКлиент.ОтключитьEVatService(EVatService);
	
КонецПроцедуры

&НаСервере
Функция ОбновитьСтатусыНаСервере(Знач Статус, Знач ТекущаяСтрока, Знач ДатаИзмененияСтатуса)
	
	СтруктураСтатусов = ЭлектронныеСчетаФактуры.ПолучитьСтруктуруСтатусовЭСЧФ(Статус, Истина);
	
	Если ТекущаяСтрока.СостояниеВерсииЭД <> СтруктураСтатусов.СостояниеВерсииЭД Тогда 
		
		ЭлектронныеСчетаФактуры.ОбновитьВерсиюЭД(ТекущаяСтрока.СсылкаНаОбъект, СтруктураСтатусов, ДатаИзмененияСтатуса);
		
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ПолучитьТаблицуЭД(Знач МассивДокументов)
	
	Возврат ЭлектронныеСчетаФактурыВызовСервера.ПолучитьТаблицуЭД(МассивДокументов);
	
КонецФункции
