&НаКлиенте
Перем ФормаДлительнойОперации;
&НаКлиенте
Перем ПараметрыОжидания;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("Регистратор") Тогда
		Документ = Параметры.Отбор.Регистратор;
		ОбновитьДанныеРеквизитовДокумента();
		УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("Период, Организация", Дата, Организация));
	Иначе
	    ТекстСообщения = НСтр("ru='Непосредственное открытие этой формы не предусмотрено!'");
	    ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	ОписаниеТиповВалюта        = Новый ОписаниеТипов("СправочникСсылка.Валюты");
	ОписаниеТиповКоличество    = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3));
	ОписаниеТиповПодразделение = БухгалтерскийУчетКлиентСерверПереопределяемый.ОписаниеТиповПодразделения();
	ОписаниеТиповСумма         = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,2));
	ТипыСвязанныеСОрганизацией = БухгалтерскийУчетПереопределяемый.ТипыСвязанныеСОрганизацией();
	УведомлятьОПустомКомментарии = Истина;
	
	ДокументПроводится = (Документ.Метаданные().Проведение = Метаданные.СвойстваОбъектов.Проведение.Запретить);
	
	ОбновитьДанныеОВведенныхКорректировках();
	
	УстановитьДоступностьСубконто();
	
	УстановитьЗаголовкиСумм();
	УстановитьЗаголовкиПодразделения();
	
	ПолучитьСостояниеОтраженияДокумента();
	
	ИспользуетсяПроверкаДокументов = ПолучитьФункциональнуюОпцию("ИспользоватьПроверкуДокументовПоРегламентированномуУчету")
		И ПроверкаДокументовСервер.ЭтотТипДокументаДолженПроверяться(ТипЗнч(Документ));
	ПредставлениеДокумента = Строка(Документ) + ?(ИспользуетсяПроверкаДокументов, СтатусПроверкиДокумента(Документ), "");
	
	УстановитьВидКоманднойПанелиНабораЗаписей();
	
	УстановитьДоступностьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриПовторномОткрытии()
	
	ПовторноеОткрытиеСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.АктуализацияПроводок.Пометка = АктуализацияПроводок;
	Если АктуализацияПроводок
		И (СтатусОтраженияДокумента = ПредопределенноеЗначение("Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете")
		ИЛИ СтатусОтраженияДокумента = ПредопределенноеЗначение("Перечисление.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета"))
		И РезультатФоновогоЗаданияОтраженияПроводок.Статус = "Выполняется" Тогда
		
			ПодключитьОбработчикОжидания("ОтражениеПроводокПроверитьНаКлиенте", 0.1, Истина);

	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ЗаполнитьПредставлениеВидовСубконто();
	УстановитьДоступностьСубконто();

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	// Проверки перед записью:
	//	- Если автоматически отражаем, спрашиваем о продолжении записи с тем, что проводки будут сформированы автоматически;
	//	- Если отражаем вручную, задаем дополнительный вопрос только в том случае, когда не указан комментарий ручного отражения.
	
	ЗадаватьВопросПередЗаписью = Не Объект.РучнаяКорректировкаПроводок
		ИЛИ Объект.РучнаяКорректировкаПроводок И Не ЗначениеЗаполнено(Комментарий) И УведомлятьОПустомКомментарии;
	
	Если ПараметрыЗаписи.Свойство("ЗадаватьВопросПередЗаписью") Тогда
		ЗадаватьВопросПередЗаписью = ПараметрыЗаписи.ЗадаватьВопросПередЗаписью;
	КонецЕсли;
	
	Если ЗадаватьВопросПередЗаписью Тогда
		Отказ = Истина;
		ПараметрыЗаписи.Вставить("ЗадаватьВопросПередЗаписью", Ложь);
		Если Объект.РучнаяКорректировкаПроводок Тогда
			ПоказатьФормуКомментария(ПараметрыЗаписи);
		Иначе
			ОписаниеОповещения = Новый ОписаниеОповещения("ОбработчикОповещенияВопросПередЗаписью", ЭтотОбъект, ПараметрыЗаписи);
			ТекстВопроса = НСтр("ru = 'Проводки будут сформированы автоматически. Продолжить?'");
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	ТекущийОбъект.ДополнительныеСвойства.Вставить("ЗаписыватьПустойНабор", Истина);
	
	Если Объект.РучнаяКорректировкаПроводок Тогда
		
		СтатусОтраженияДокумента = Перечисления.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВУчетеВручную;
		ТекущийОбъект.ДополнительныеСвойства.Вставить("РучноеОтражение", Истина);
		
	Иначе
				
		ПараметрыОтражения = ПараметрыОтраженияДокументаВРеглУчете();
		АдресРезультата = ПоместитьВоВременноеХранилище(Неопределено, ЭтотОбъект.УникальныйИдентификатор);
		РеглУчетПроведениеСервер.ВернутьРезультатОтраженияДокумента(ПараметрыОтражения, АдресРезультата);
		Результат = ПолучитьИзВременногоХранилища(АдресРезультата);		
		
		СтатусОтраженияДокумента = Результат.СтатусОтражения;
		Комментарий = Результат.КомментарийОшибки;
		Если СтатусОтраженияДокумента = Перечисления.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете Тогда
			ТекущийОбъект.Загрузить(Результат.ТаблицаПроводок);
		Конецесли;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Не Отказ Тогда
		РеглУчетПроведениеСервер.ЗарегистрироватьОтражениеДокумента(Документ, СтатусОтраженияДокумента, Комментарий);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	ПолучитьСостояниеОтраженияДокумента();
	ЗаполнитьПредставлениеВидовСубконто();
	УстановитьДоступностьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_ОтражениеДокументовВРегламентированномУчете", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Документ), ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ОтражениеДокументовВРегламентированномУчете" 
			И ((ТипЗнч(Параметр) = Тип("Массив") И Не Параметр.Найти(Документ) = Неопределено)
			ИЛИ ТипЗнч(Параметр) = Тип("ДокументСсылка.ОперацияБух")) Тогда
		ПовторноеОткрытиеСервер();
	ИначеЕсли ИмяСобытия = "Запись_СтатусПроверкиДокумента" Тогда
		ПредставлениеДокумента = Строка(Документ) + СтатусПроверкиДокумента(Документ);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	Если АктуализацияПроводок
		И (СтатусОтраженияДокумента = ПредопределенноеЗначение("Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете")
		ИЛИ СтатусОтраженияДокумента = ПредопределенноеЗначение("Перечисление.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета")) Тогда
		РезультатФоновогоЗаданияОтраженияПроводок = ОтразитьДокументВРеглУчетеСервер();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РучнаяКорректировкаПроводокПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
	Если Объект.РучнаяКорректировкаПроводок Тогда 
		
		Если НаборЗаписей.Количество() = 0 Тогда
			ТекстВопроса = НСтр("ru = 'Заполнить проводки автоматически?'");
			ОписаниеОповещения = Новый ОписаниеОповещения("ОбработчикОповещенияВопросПередАвтоматическимЗаполнениемПроводок", ЭтотОбъект, Новый Структура);
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		КонецЕсли;
		
		УстановитьДоступностьЭлементовФормы("РучнаяКорректировкаПроводок");
		
	Иначе
		  
		ТекстВопроса = НСтр("ru = 'Документ будет отражен в учете автоматически. Продолжить?'");  
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработчикОповещенияПриСнятииФлагаРучнаяКорректировкаПроводок", ЭтотОбъект, Новый Структура);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеДокументаНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(Неопределено, Документ);
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьФормуКомментария();
		
КонецПроцедуры

&НаКлиенте
Процедура ТекстКорректировкиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если СписокКорректировок.Количество() = 1 Тогда
		ПоказатьЗначение(, СписокКорректировок.Получить(0).Значение);
	Иначе
		ЗаголовокФормы = НСтр("ru = 'Выберите документ корректировки для просмотра'");
		ПараметрыФормы = Новый Структура("СписокДокументов, Заголовок", СписокКорректировок, ЗаголовокФормы);
		
		ОткрытьФорму("ОбщаяФорма.ПросмотрСпискаДокументов", ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор,
		,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "РасшифровкаСтатуса" Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьРасшифровкуСтатуса();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыНаборЗаписей

&НаКлиенте
Процедура НаборЗаписейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	Если Не Объект.РучнаяКорректировкаПроводок Тогда
		Значение = Элемент.ТекущиеДанные[Поле.Имя];
		Если ЗначениеЗаполнено(Значение) 
			И ТипЗнч(Значение) <> Тип("Число") 
			И ТипЗнч(Значение) <> Тип("Дата")
			И ТипЗнч(Значение) <> Тип("Булево") Тогда
			ПоказатьЗначение(, Значение);
			СтандартнаяОбработка = Ложь;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура НаборЗаписейПередНачаломИзменения(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.НаборЗаписей.ТекущиеДанные;
	
	ПоляФормы = Новый Структура("Субконто1, Субконто2, Субконто3",
		"СубконтоДт1", "СубконтоДт2", "СубконтоДт3");
	БухгалтерскийУчетКлиентСервер.ПриВыбореСчета(ТекущиеДанные.СчетДт, ЭтотОбъект, ПоляФормы, Неопределено, Истина);
	
	ПоляФормы = Новый Структура("Субконто1, Субконто2, Субконто3",
		"СубконтоКт1", "СубконтоКт2", "СубконтоКт3");
	БухгалтерскийУчетКлиентСервер.ПриВыбореСчета(ТекущиеДанные.СчетКт, ЭтотОбъект, ПоляФормы, Неопределено, Истина);
	
	ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НаборЗаписейПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И НЕ Копирование Тогда
		Если Элемент.ТекущиеДанные.Свойство("Период") И ЗначениеЗаполнено(ДатаОтражения) Тогда
			Элемент.ТекущиеДанные.Период = ДатаОтражения;
		КонецЕсли;
		Если Элемент.ТекущиеДанные.Свойство("Организация") И ЗначениеЗаполнено(Организация) Тогда
			Элемент.ТекущиеДанные.Организация = Организация;
		КонецЕсли;
		Если Элемент.ТекущиеДанные.Свойство("Регистратор") И ЗначениеЗаполнено(Документ) Тогда
			Элемент.ТекущиеДанные.Регистратор = Документ;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНаборЗаписей

&НаКлиенте
Процедура СчетДтПриИзменении(Элемент)
	
	ОбработатьИзменениеСчета("Дт");
	
КонецПроцедуры

&НаКлиенте
Процедура СчетКтПриИзменении(Элемент)
	
	ОбработатьИзменениеСчета("Кт");
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютаДтПриИзменении(Элемент)
	
	РасчетСуммы(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютаКтПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.НаборЗаписей.ТекущиеДанные;

	Если ЗначениеЗаполнено(ТекущиеДанные.СчетДт) Тогда
		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ТекущиеДанные.СчетДт);
		Если СвойстваСчета.Валютный Тогда
			Возврат; // Если оба счета валютные, сумма пересчитывается при изменении счета Дт
		КонецЕсли;
	КонецЕсли;

	РасчетСуммы(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютнаяСуммаДтПриИзменении(Элемент)
	
	РасчетСуммы(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютнаяСуммаКтПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.НаборЗаписей.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.СчетДт) Тогда
		СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ТекущиеДанные.СчетДт);
		Если СвойстваСчета.Валютный Тогда
			Возврат; // Если оба счета валютные, сумма пересчитывается при изменении счета Дт
		КонецЕсли;
	КонецЕсли;
	
	РасчетСуммы(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаПриИзменении(Элемент)
	
	РасчетСуммы();
	
КонецПроцедуры

#Область ОбработчикиСобытийСубконто

&НаКлиенте
Процедура СубконтоДт1НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбработатьНачалоВыбораСубконто("Дт", Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоДт1ПриИзменении(Элемент)
		
	ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, "Дт");
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоДт1ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОчиститьПараметрыВыбора(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоДт2НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбработатьНачалоВыбораСубконто("Дт", Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоДт2ПриИзменении(Элемент)
		
	ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, "Дт");
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоДт2ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОчиститьПараметрыВыбора(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоДт3НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
	ОбработатьНачалоВыбораСубконто("Дт", Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоДт3ПриИзменении(Элемент)
		
	ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, "Дт");
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоДт3ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОчиститьПараметрыВыбора(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт1НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
	ОбработатьНачалоВыбораСубконто("Кт", Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт1ПриИзменении(Элемент)
		
	ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, "Кт");
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт1ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОчиститьПараметрыВыбора(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт2НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
	ОбработатьНачалоВыбораСубконто("Кт", Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт2ПриИзменении(Элемент)
		
	ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, "Кт");
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт2ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОчиститьПараметрыВыбора(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт3НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
	ОбработатьНачалоВыбораСубконто("Кт", Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт3ПриИзменении(Элемент)
		
	ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, "Кт");
	
КонецПроцедуры

&НаКлиенте
Процедура СубконтоКт3ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОчиститьПараметрыВыбора(Элемент);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ОчиститьСообщения();
	
	Если Не Модифицированность Тогда
		Закрыть();
		Возврат;
	КонецЕсли;
	
	РезультатЗаписи = Записать(Новый Структура("ЗакрыватьПриЗаписи", Истина));
	Если РезультатЗаписи И Объект.РучнаяКорректировкаПроводок Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПроводкиАвтоматически(Команда)
	
	// Для документов амортизации не делаем автозаполнение, так как они формируют проводки при проведении и только:
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.АмортизацияНМА") ИЛИ ТипЗнч(Документ) = Тип("ДокументСсылка.АмортизацияОС") Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработчикОповещенияВопросПередПроведениемАмортизации", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Данное действие приведет к перепроведению документа, перепровести документ амортизации?'");
		СписокВыбора = Новый СписокЗначений;
		СписокВыбора.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Перепровести'"));
		СписокВыбора.Добавить(КодВозвратаДиалога.Нет, НСтр("ru = 'Не перепроводить'"));
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, СписокВыбора);
		Возврат;
	КонецЕсли;
	
	Если НаборЗаписей.Количество() > 0 Тогда
		ДопПараметры = Новый Структура;
		// Некоторые документы при создании проводок по регистру Хозрасчетный опираются на свои же остатки на этом регистре,
		// и добавляют только те проводки которые необходимы. В нашем случае (когда документ не проводится,
		// а проводки соответственно не очищаются) необходимо вручную очищать проводки документа:
		Если МассивДокументовТребующихОчисткиПроводок().Найти(ТипЗнч(Документ)) = Неопределено Тогда
			// Документ не относится к списку документов, для которых необходимо очищать проводки, задаем стандартный вопрос:
			ТекстВопроса = НСтр("ru = 'Существующие проводки будут удалены. Продолжить?'");
		Иначе
			ТекстВопроса = НСтр("ru = 'Существующие проводки будут удалены без возможности отмены изменений. Продолжить?'");
			ДопПараметры.Вставить("ОчисткаПроводок");
		КонецЕсли;
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработчикОповещенияВопросПередАвтоматическимЗаполнениемПроводок", ЭтотОбъект, ДопПараметры);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ЗапуститьФоновоеЗаданиеПоЗаполнениюПроводок();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодтвердитьАктуальностьПроводок(Команда)
	
	ПодтвердитьАктуальностьПроводокСервер();
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КорректировкаПроводок(Команда)
	
	ПараметрыОперации = Новый Структура("Основание", Документ);
	ОткрытьФорму("Документ.ОперацияБух.ФормаОбъекта", ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтразитьДокументВРеглУчете(Команда)
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.АмортизацияНМА") ИЛИ ТипЗнч(Документ) = Тип("ДокументСсылка.АмортизацияОС") Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработчикОповещенияВопросПередПроведениемАмортизации", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Данное действие приведет к перепроведению документа, перепровести документ амортизации?'");
		СписокВыбора = Новый СписокЗначений;
		СписокВыбора.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Перепровести'"));
		СписокВыбора.Добавить(КодВозвратаДиалога.Нет, НСтр("ru = 'Не перепроводить'"));
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, СписокВыбора);
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	РезультатФоновогоЗаданияОтраженияПроводок = ОтразитьДокументВРеглУчетеСервер();
	Если РезультатФоновогоЗаданияОтраженияПроводок.Статус = "Выполняется" Тогда
		ПодключитьОбработчикОжидания("ОтражениеПроводокПроверитьНаКлиенте", 0.1, Истина);
	ИначеЕсли РезультатФоновогоЗаданияОтраженияПроводок.Статус = "Ошибка" Тогда
		ВызватьИсключение(НСтр("ru = 'При отражении документа в регламентированном учете произошла ошибка.
							|Подробности см. в Журнале регистрации.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ПовторноеОткрытиеСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьСчетаУчета(Команда)
	
	СтандартнаяОбработка = Ложь;
	СтруктураПараметры = Новый Структура();
	
	ПараметрыОткрытия = ПараметрыОткрытияФормыНастройкиОтражения(Документ);
	ОрганизацииДляНастройки = ПараметрыОткрытия.Организации;
	ДатаОтражения = ПараметрыОткрытия.ДатаОтражения;
	
	Если ОрганизацииДляНастройки.Количество() = 1 Тогда
		Организация = ОрганизацииДляНастройки[0]; 
		ОткрытьФормуНастройкиСчетовУчета(Организация, ОрганизацииДляНастройки, ДатаОтражения);
	Иначе
		ДопПараметры = Новый Структура;
		ДопПараметры.Вставить("ОрганизацииДляНастройки", ОрганизацииДляНастройки);
		ДопПараметры.Вставить("ДатаОтражения", ДатаОтражения);
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаВыбораОрганизацииДляНастройкиСчетов", ЭтотОбъект, ДопПараметры);
		СписокВыбораОрагнизации = Новый СписокЗначений;
		СписокВыбораОрагнизации.ЗагрузитьЗначения(ОрганизацииДляНастройки);
		СписокВыбораОрагнизации.ПоказатьВыборЭлемента(ОписаниеОповещения, НСтр("ru = 'Выберите организацию для настройки'"));  
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АктуализацияПроводок(Команда)
	АктуализацияПроводок = Не АктуализацияПроводок;
	Элементы.АктуализацияПроводок.Пометка = АктуализацияПроводок;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработкаИзмененияРеквизитов

&НаКлиентеНаСервереБезКонтекста
Процедура ИзменитьПараметрыВыбораПолейСубконто(Форма, ДтКт = "")
	
	ИдСтроки = Форма.Элементы.НаборЗаписей.ТекущаяСтрока;
	Если ИдСтроки <> Неопределено Тогда
		СтрокаТаблицы = Форма.НаборЗаписей.НайтиПоИдентификатору(ИдСтроки);
		Если ДтКт <> "Кт" Тогда
			ПараметрыДокумента = ПолучитьСписокПараметров(Форма, СтрокаТаблицы, "СубконтоДт%Индекс%", "СчетДт");
			БухгалтерскийУчетКлиентСервер.ИзменитьПараметрыВыбораПолейСубконто(
				Форма, СтрокаТаблицы, "СубконтоДт%Индекс%", "СубконтоДт%Индекс%", ПараметрыДокумента);
		КонецЕсли;
		Если ДтКт <> "Дт" Тогда
			ПараметрыДокумента = ПолучитьСписокПараметров(Форма, СтрокаТаблицы, "СубконтоКт%Индекс%", "СчетКт");
			БухгалтерскийУчетКлиентСервер.ИзменитьПараметрыВыбораПолейСубконто(
				Форма, СтрокаТаблицы, "СубконтоКт%Индекс%", "СубконтоКт%Индекс%", ПараметрыДокумента);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьСписокПараметров(Форма, ТекущиеДанные, ШаблонИмяПоляОбъекта, ИмяПоляСчетУчета)

	СписокПараметров = Новый Структура;
	СписокПараметров.Вставить("ОстаткиОбороты", "Кт");
	СписокПараметров.Вставить("Организация",    Форма.Организация);
	СписокПараметров.Вставить("СчетУчета",      ТекущиеДанные[ИмяПоляСчетУчета]);
	Для Индекс = 1 По 3 Цикл
		ИмяПоля = СтрЗаменить(ШаблонИмяПоляОбъекта, "%Индекс%", Индекс);
		Если ТипЗнч(ТекущиеДанные[ИмяПоля]) = Тип("СправочникСсылка.Контрагенты") Тогда
			СписокПараметров.Вставить("Контрагент", ТекущиеДанные[ИмяПоля]);
		ИначеЕсли ТипЗнч(ТекущиеДанные[ИмяПоля]) = Тип("СправочникСсылка.Организации") Тогда
			МассивОрганизаций = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СписокПараметров.Организация);
			МассивОрганизаций.Добавить(ТекущиеДанные[ИмяПоля]);
			СписокПараметров.Вставить("Организация", Новый ФиксированныйМассив(МассивОрганизаций));
		ИначеЕсли БухгалтерскийУчетКлиентСерверПереопределяемый.ПолучитьОписаниеТиповДоговора().СодержитТип(ТипЗнч(ТекущиеДанные[ИмяПоля])) Тогда
			СписокПараметров.Вставить("ДоговорКонтрагента", ТекущиеДанные[ИмяПоля]);
		ИначеЕсли ТипЗнч(ТекущиеДанные[ИмяПоля]) = Тип("СправочникСсылка.Номенклатура") Тогда
			СписокПараметров.Вставить("Номенклатура", ТекущиеДанные[ИмяПоля]);
		ИначеЕсли ТипЗнч(ТекущиеДанные[ИмяПоля]) = Тип("СправочникСсылка.Склады") Тогда
			СписокПараметров.Вставить("Склад", ТекущиеДанные[ИмяПоля]);
		КонецЕсли;
	КонецЦикла;

	Возврат СписокПараметров;

КонецФункции

&НаКлиенте
Процедура ОбработатьИзменениеСчета(ДтКт)

	ТекущиеДанные = Элементы.НаборЗаписей.ТекущиеДанные;
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ПредставлениеВидовСубконто(ТекущиеДанные["Счет"+ДтКт], ДтКт));
		
	ПоляФормы = Новый Структура("Субконто1, Субконто2, Субконто3",
		"Субконто"+ДтКт+"1", "Субконто"+ДтКт+"2", "Субконто"+ДтКт+"3");
	БухгалтерскийУчетКлиентСервер.ПриВыбореСчета(ТекущиеДанные["Счет"+ДтКт], ЭтотОбъект, ПоляФормы, Неопределено, Истина);
	
	ПоляОбъекта = Новый Структура("Субконто1, Субконто2, Субконто3",
		"Субконто"+ДтКт+"1", "Субконто"+ДтКт+"2", "Субконто"+ДтКт+"3");
	ПоляОбъекта.Вставить("Подразделение", "Подразделение"+ДтКт);
	ПоляОбъекта.Вставить("Организация"  , Организация);
	БухгалтерскийУчетКлиентСервер.ПриИзмененииСчета(ТекущиеДанные["Счет"+ДтКт], ТекущиеДанные, ПоляОбъекта, Истина);
	
	СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ТекущиеДанные["Счет"+ДтКт]);
	Если СвойстваСчета.Валютный Тогда
		ТекущиеДанные["Валюта" + ДтКт]        = ОписаниеТиповВалюта.ПривестиЗначение(ТекущиеДанные["Валюта" + ДтКт]);
		ТекущиеДанные["ВалютнаяСумма" + ДтКт] = ОписаниеТиповСумма.ПривестиЗначение(ТекущиеДанные["ВалютнаяСумма" + ДтКт]);
	Иначе
		ТекущиеДанные["Валюта"+ДтКт]        = NULL;
		ТекущиеДанные["ВалютнаяСумма"+ДтКт] = NULL;
	КонецЕсли;
	Если СвойстваСчета.УчетПоПодразделениям Тогда
		ТекущиеДанные["Подразделение" + ДтКт] = ОписаниеТиповПодразделение.ПривестиЗначение(ТекущиеДанные["Подразделение" + ДтКт]);
	Иначе
		ТекущиеДанные["Подразделение" + ДтКт] = NULL;
	КонецЕсли;
	Если СвойстваСчета.НалоговыйУчет Тогда
		ТекущиеДанные["СуммаНУ" + ДтКт] = ОписаниеТиповСумма.ПривестиЗначение(ТекущиеДанные["СуммаНУ" + ДтКт]);
		ТекущиеДанные["СуммаПР" + ДтКт] = ОписаниеТиповСумма.ПривестиЗначение(ТекущиеДанные["СуммаПР" + ДтКт]);
		ТекущиеДанные["СуммаВР" + ДтКт] = ОписаниеТиповСумма.ПривестиЗначение(ТекущиеДанные["СуммаВР" + ДтКт]);
	Иначе
		ТекущиеДанные["СуммаНУ" + ДтКт] = NULL;
		ТекущиеДанные["СуммаПР" + ДтКт] = NULL;
		ТекущиеДанные["СуммаВР" + ДтКт] = NULL;
	КонецЕсли;
	Если СвойстваСчета.Количественный Тогда
		ТекущиеДанные["Количество" + ДтКт] = ОписаниеТиповКоличество.ПривестиЗначение(ТекущиеДанные["Количество" + ДтКт]);
	Иначе
		ТекущиеДанные["Количество" + ДтКт] = NULL;
	КонецЕсли;
	
	ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, ДтКт);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьНачалоВыбораСубконто(ДтКт, Элемент, СтандартнаяОбработка)

	ТекущиеДанные      = Элементы.НаборЗаписей.ТекущиеДанные;
	ПараметрыДокумента = ПолучитьСписокПараметров(ЭтотОбъект, ТекущиеДанные, "Субконто" + ДтКт + "%Индекс%", "Счет" + ДтКт);
	ОбщегоНазначенияБПКлиент.НачалоВыбораЗначенияСубконто(ЭтотОбъект, Элемент, СтандартнаяОбработка, ПараметрыДокумента);

КонецПроцедуры

&НаКлиенте
Процедура ОчиститьПараметрыВыбора(Элемент)
	
	Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(Новый Массив);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПредставлениеВидовСубконто()

	Для каждого Запись Из НаборЗаписей Цикл
		ЗаполнитьЗначенияСвойств(Запись, ПредставлениеВидовСубконто(Запись.СчетДт, "Дт"));
		ЗаполнитьЗначенияСвойств(Запись, ПредставлениеВидовСубконто(Запись.СчетКт, "Кт"));
	КонецЦикла;

КонецПроцедуры

&НаСервере
Функция ПредставлениеВидовСубконто(Счет, ВидДвижения)

	ПредставлениеВидовСубконто = Новый Структура;
	МаксКоличествоСубконто = Метаданные.ПланыСчетов.Хозрасчетный.МаксКоличествоСубконто;
	КоличествоВидовСубконто = Счет.ВидыСубконто.Количество();
	Для НомерСубконто = 1 По МаксКоличествоСубконто Цикл
		ПредставлениеВидаСубконто = ?(КоличествоВидовСубконто >= НомерСубконто, "<" + Счет.ВидыСубконто[НомерСубконто - 1].ВидСубконто + ">", Неопределено);
		ПредставлениеВидовСубконто.Вставить("ПредставлениеВидСубконто" + ВидДвижения + НомерСубконто, ПредставлениеВидаСубконто);
	КонецЦикла;

	Возврат ПредставлениеВидовСубконто;

КонецФункции

&НаСервере
Процедура УстановитьДоступностьСубконто()
	
	ИспользуютсяНерекомендуемыеСчетаУчета = Ложь;
	
	Для каждого Проводка Из НаборЗаписей Цикл
		
		ПоляОбъекта = Новый Структура("Субконто1, Субконто2, Субконто3", "СубконтоДт1", "СубконтоДт2", "СубконтоДт3");
		БухгалтерскийУчетКлиентСервер.УстановитьДоступностьСубконто(Проводка.СчетДт, Проводка, ПоляОбъекта);
		ПоляОбъекта = Новый Структура("Субконто1, Субконто2, Субконто3", "СубконтоКт1", "СубконтоКт2", "СубконтоКт3");
		БухгалтерскийУчетКлиентСервер.УстановитьДоступностьСубконто(Проводка.СчетКт, Проводка, ПоляОбъекта);
		
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработкаИзмененияСуммовыхРеквизитов

&НаКлиенте
Процедура РасчетСуммы(ПересчетСуммыПоКурсуДт = Неопределено)
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные     = Элементы.НаборЗаписей.ТекущиеДанные;
	СтруктураПроводки = ПреобразоватьДанныеЭлементаФормыВСтруктуру(ТекущиеДанные);
	
	ПересчитатьСуммыПоСтрокеНаСервере(СтруктураПроводки, ПересчетСуммыПоКурсуДт);
	
	ЗаполнитьЗначенияСвойств(Элементы.НаборЗаписей.ТекущиеДанные, СтруктураПроводки);
	
КонецПроцедуры

&НаКлиенте
Функция ПреобразоватьДанныеЭлементаФормыВСтруктуру(ТекущиеДанные)

	СтруктураПроводки = Новый Структура("НомерСтроки,Период,
		|СчетДт,ПодразделениеДт,СубконтоДт1,СубконтоДт2,СубконтоДт3,
		|КоличествоДт,ВалютаДт,ВалютнаяСуммаДт,
		|СчетКт,ПодразделениеКт,СубконтоКт1,СубконтоКт2,СубконтоКт3,
		|КоличествоКт,ВалютаКт,ВалютнаяСуммаКт,
		|Сумма,Содержание,СуммаНУДт,СуммаПРДт,СуммаВРДт,СуммаНУКт,СуммаПРКт,СуммаВРКт");
	ЗаполнитьЗначенияСвойств(СтруктураПроводки, ТекущиеДанные);

	Возврат СтруктураПроводки;

КонецФункции

&НаСервере
Процедура ПересчитатьСуммыПоСтрокеНаСервере(Проводка, Знач ПересчетСуммыПоКурсуДт = Неопределено)
	
	ДатаПроводки = ?(ЗначениеЗаполнено(Проводка.Период), Проводка.Период, ДатаОтражения);
	
	Если ПересчетСуммыПоКурсуДт = Истина Тогда
		Проводка.Сумма = ПересчетСуммыПоКурсу(Проводка.ВалютнаяСуммаДт, Проводка.ВалютаДт, ДатаПроводки);
	ИначеЕсли ПересчетСуммыПоКурсуДт = Ложь Тогда
		Проводка.Сумма = ПересчетСуммыПоКурсу(Проводка.ВалютнаяСуммаКт, Проводка.ВалютаКт, ДатаПроводки);
	КонецЕсли;
	
	ОтразитьНепринимаемыеДоходыИРасходы(Проводка);
	
	Если БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетДт).НалоговыйУчет Тогда
		Проводка.СуммаНУДт = Проводка.Сумма
			- ?(Проводка.СуммаПРДт = Неопределено, 0, Проводка.СуммаПРДт)
			- ?(Проводка.СуммаВРДт = Неопределено, 0, Проводка.СуммаВРДт);
	КонецЕсли;
	
	Если БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Проводка.СчетКт).НалоговыйУчет Тогда
		Проводка.СуммаНУКт = Проводка.Сумма
			- ?(Проводка.СуммаПРКт = Неопределено, 0, Проводка.СуммаПРКт)
			- ?(Проводка.СуммаВРКт = Неопределено, 0, Проводка.СуммаВРКт);
	КонецЕсли;
	
КонецПроцедуры

// Пересчет валютной суммы в основную по курсу на указанную дату.
//
&НаСервереБезКонтекста
Функция ПересчетСуммыПоКурсу(Знач ВалютнаяСумма, Знач Валюта, Знач Дата)

	СтруктураКурса = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Валюта, Дата);

	Сумма = ?(СтруктураКурса.Кратность = 0, 0, Окр(ВалютнаяСумма * СтруктураКурса.Курс / СтруктураКурса.Кратность, 2));
	Возврат Сумма;

КонецФункции

&НаСервере
Процедура ОтразитьНепринимаемыеДоходыИРасходы(СтруктураПроводки)

	ОтразитьНеПринимаемыеДоходы = ОпределитьНеПринимаемыеДоходы(СтруктураПроводки);
	Если ОтразитьНеПринимаемыеДоходы Тогда
		СтруктураПроводки.СуммаПРКт = СтруктураПроводки.Сумма;
	КонецЕсли;

	ОтразитьНеПринимаемыеРасходы = ОпределитьНеПринимаемыеРасходы(СтруктураПроводки);
	Если ОтразитьНеПринимаемыеРасходы Тогда
		СтруктураПроводки.СуммаПРДт = СтруктураПроводки.Сумма;
		Возврат;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ОпределитьНеПринимаемыеДоходы(СтруктураПроводки)

	Если Не ЗначениеЗаполнено(СтруктураПроводки.СчетКт) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если СтруктураПроводки.СчетКт <> ПланыСчетов.Хозрасчетный.ПустаяСсылка()
		И БухгалтерскийУчетПовтИсп.СчетВИерархии(СтруктураПроводки.СчетКт, ПланыСчетов.Хозрасчетный.ПрочиеДоходы) Тогда
		
		Для НомерСубконто = 1 По 3 Цикл
			
			Субконто = СтруктураПроводки["СубконтоКт" + НомерСубконто];
			Если Не ЗначениеЗаполнено(Субконто) Тогда
				Продолжить;
			КонецЕсли;
			
			Если ТипЗнч(Субконто) = Тип("ПланВидовХарактеристикСсылка.СтатьиДоходов") Тогда
				
				РеквизитыСубконто = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Субконто, "ЭтоГруппа,ПринятиеКналоговомуУчету");
				Если РеквизитыСубконто.ПринятиеКналоговомуУчету = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				Если НЕ РеквизитыСубконто.ЭтоГруппа
					И Субконто <> ПланыВидовХарактеристик.СтатьиДоходов.ПустаяСсылка() И 
					НЕ РеквизитыСубконто.ПринятиеКналоговомуУчету Тогда
					Возврат Истина;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Ложь;

КонецФункции

&НаСервере
Функция ОпределитьНеПринимаемыеРасходы(СтруктураПроводки)
	
	Если Не ЗначениеЗаполнено(СтруктураПроводки.СчетДт) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Для НомерСубконто = 1 По 3 Цикл
		
		Субконто = СтруктураПроводки["СубконтоДт" + НомерСубконто];
		Если Не ЗначениеЗаполнено(Субконто) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТипЗнч(Субконто) = Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов") Тогда
		
			РеквизитыСубконто = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Субконто, 
				"ЭтоГруппа, ВидРасходов, Представление");
			
			Если РеквизитыСубконто.ЭтоГруппа Тогда
				Продолжить;
			КонецЕсли;
		
			Если РеквизитыСубконто.ВидРасходов = Перечисления.ВидыРасходовНУ.НеУчитываемыеВЦеляхНалогообложения Тогда
				// Расходы на строительство объектов основных средств, не относятся к расходам по производству и реализации.
				Если БухгалтерскийУчетПовтИсп.СчетВИерархии(СтруктураПроводки.СчетДт, ПланыСчетов.Хозрасчетный.СтроительствоОбъектовОсновныхСредств) Тогда
					Возврат Ложь;
				КонецЕсли;
				Возврат Истина;
			КонецЕсли;

		ИначеЕсли ТипЗнч(Субконто) = Тип("ПланВидовХарактеристикСсылка.СтатьиДоходов") Тогда
		
			РеквизитыСубконто = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Субконто,
				"ЭтоГруппа, ПринятиеКналоговомуУчету, Представление");
				
			Если РеквизитыСубконто.ЭтоГруппа Тогда
				Продолжить;
			КонецЕсли;
		
			Если Субконто <> ПланыВидовХарактеристик.СтатьиДоходов.ПустаяСсылка()
					И НЕ РеквизитыСубконто.ПринятиеКналоговомуУчету Тогда
				Возврат Истина;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область РаботаСФормой

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
#Область ОтображениеВидаСубконтоДт1
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ПредставлениеВидСубконтоДт1");
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СчетДт");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СубконтоДт1");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаПодсказкиВвода);
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ПредставлениеВидСубконтоДт1");
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СубконтоДт1");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
#КонецОбласти

#Область ОтображениеВидаСубконтоДт2
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ПредставлениеВидСубконтоДт2");
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СчетДт");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СубконтоДт2");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаПодсказкиВвода);
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ПредставлениеВидСубконтоДт2");
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СубконтоДт2");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
#КонецОбласти

#Область ОтображениеВидаСубконтоДт3
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ПредставлениеВидСубконтоДт3");
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СчетДт");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СубконтоДт3");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаПодсказкиВвода);
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ПредставлениеВидСубконтоДт3");
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СубконтоДт3");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
#КонецОбласти
	
#Область ОтображениеВидаСубконтоКт1
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ПредставлениеВидСубконтоКт1");
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СчетКт");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СубконтоКт1");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаПодсказкиВвода);
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ПредставлениеВидСубконтоКт1");
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СубконтоКт1");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
#КонецОбласти

#Область ОтображениеВидаСубконтоКт2
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ПредставлениеВидСубконтоКт2");
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СчетКт");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СубконтоКт2");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаПодсказкиВвода);
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ПредставлениеВидСубконтоКт2");
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СубконтоКт2");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
#КонецОбласти

#Область ОтображениеВидаСубконтоКт3
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ПредставлениеВидСубконтоКт3");
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СчетКт");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СубконтоКт3");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаПодсказкиВвода);
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ПредставлениеВидСубконтоКт3");
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗаписей.СубконтоКт3");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
#КонецОбласти

КонецПроцедуры

&НаСервере
Процедура ПовторноеОткрытиеСервер()

	Если Документ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДокументПроводится = (Документ.Метаданные().Проведение = Метаданные.СвойстваОбъектов.Проведение.Запретить);
	ДокументПроведен = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "Проведен");
	
	ПолучитьСостояниеОтраженияДокумента();
	УстановитьДоступностьЭлементовФормы();
	
	Если НЕ ДокументПроведен 
		ИЛИ (ДокументПроведен И НЕ Объект.РучнаяКорректировкаПроводок) Тогда
		Прочитать();
	КонецЕсли;
	
	ИспользуетсяПроверкаДокументов = ПолучитьФункциональнуюОпцию("ИспользоватьПроверкуДокументовПоРегламентированномуУчету")
		И ПроверкаДокументовСервер.ЭтотТипДокументаДолженПроверяться(ТипЗнч(Документ));
	ПредставлениеДокумента = Строка(Документ) + ?(ИспользуетсяПроверкаДокументов, СтатусПроверкиДокумента(Документ), "");
	
	ЗаполнитьПредставлениеВидовСубконто();

КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьЭлементовФормы(ИдентификаторСобытия = Неопределено)
	
	Если ИдентификаторСобытия = "РучнаяКорректировкаПроводок" ИЛИ ИдентификаторСобытия = Неопределено Тогда
		УстановитьДоступностьПриИзмененииРучнаяКорректировкаПроводок();
	КонецЕсли;

	Если ИдентификаторСобытия = Неопределено Тогда
		УстановитьДоступностьПриОткрытииФормы();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьПриИзмененииРучнаяКорректировкаПроводок()

	Элементы.ГруппаПроводкиРегламентированногоУчета.ТолькоПросмотр = НЕ (Объект.РучнаяКорректировкаПроводок И Пользователи.РолиДоступны("ПравоРучнойКорректировкиПроводок"));
	
	Элементы.КомментарийРучногоОтражения.Видимость = ЗначениеЗаполнено(Комментарий) ИЛИ Объект.РучнаяКорректировкаПроводок;
	Элементы.КомментарийРучногоОтражения.ЦветТекста = ?(Объект.РучнаяКорректировкаПроводок, ЦветаСтиля.ЦветГиперссылки, ЦветаСтиля.ПоясняющийОшибкуТекст);
	Элементы.ГруппаКомментарийАвтоматическогоОтраженияВУчете.Видимость = Не Объект.РучнаяКорректировкаПроводок
		И СтатусОтраженияДокумента = Перечисления.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета;
	Элементы.ГруппаКомментарийРучногоОтраженияВУчете.Видимость = Объект.РучнаяКорректировкаПроводок;
	
	УстановитьДоступностьАвтоматическогоОтраженияВРегламентированномУчете();
	
	Элементы.ЗаполнитьПроводкиАвтоматически.Видимость = Объект.РучнаяКорректировкаПроводок;

КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьАвтоматическогоОтраженияВРегламентированномУчете()

	СтатусыОтражения = Перечисления.СтатусыОтраженияДокументовВРеглУчете;
	
	Элементы.ФормаГруппаКомандыАвтоматическогоОтражения.Видимость = НЕ Объект.РучнаяКорректировкаПроводок;
	Элементы.ФормаОтразитьДокументВРеглУчете.Доступность = Не СтатусОтраженияДокумента = СтатусыОтражения.ОтраженоВРеглУчете
		И Не СтатусОтраженияДокумента.Пустая();
		
	Элементы.ГруппаАктуальность.Видимость = НЕ Объект.РучнаяКорректировкаПроводок
		ИЛИ СтатусОтраженияДокумента = СтатусыОтражения.ОтраженоВУчетеВручную
		ИЛИ СтатусОтраженияДокумента = СтатусыОтражения.КОтражениюВУчетеВручную;
	
	Элементы.ФормаОтразитьДокументВРеглУчете.КнопкаПоУмолчанию = Не Объект.РучнаяКорректировкаПроводок;
	Элементы.ФормаГруппаКомандыРучногоОтражения.Видимость = Объект.РучнаяКорректировкаПроводок;
	Элементы.ПодтвердитьАктуальностьПроводок.Видимость = Объект.РучнаяКорректировкаПроводок И СтатусОтраженияДокумента = СтатусыОтражения.КОтражениюВУчетеВручную;
	Элементы.ПодтвердитьАктуальностьПроводок.КнопкаПоУмолчанию = Объект.РучнаяКорректировкаПроводок И СтатусОтраженияДокумента = СтатусыОтражения.КОтражениюВУчетеВручную;
	Элементы.ФормаЗаписатьИЗакрыть.КнопкаПоУмолчанию = Объект.РучнаяКорректировкаПроводок И Не СтатусОтраженияДокумента = Перечисления.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВУчетеВручную;
	Элементы.ФормаЗаписатьИЗакрыть.Видимость = Объект.РучнаяКорректировкаПроводок И Не СтатусОтраженияДокумента = Перечисления.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВУчетеВручную;
	Элементы.КоманднаяПанельНабораЗаписей.Видимость = Объект.РучнаяКорректировкаПроводок;
	Элементы.ФормаГруппаСтандартныеКоманды.Ширина = ?(Объект.РучнаяКорректировкаПроводок И СтатусОтраженияДокумента = СтатусыОтражения.КОтражениюВУчетеВручную, 33, 22);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьПриОткрытииФормы()
	
	ПравоРучнойКорректировкиПроводок = Пользователи.РолиДоступны("ПравоРучнойКорректировкиПроводок");
	НеИзменятьПроводки = ДатаОтражения < Константы.ДатаНачалаВеденияРеглУчета.Получить() И Не ТипЗнч(Документ) = Тип("ДокументСсылка.ВводОстатков")
		И Не ТипЗнч(Документ) = Тип("ДокументСсылка.ВводОстатковВнеоборотныхАктивов");
	Элементы.СоздатьНаОсновании.Доступность = ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Документы.ОперацияБух) И (ДокументПроводится ИЛИ ДокументПроведен);
	ТолькоПросмотр = НЕ (ДокументПроводится ИЛИ ДокументПроведен) ИЛИ НеИзменятьПроводки;
	Элементы.ФормаГруппаСтандартныеКоманды.Доступность = ДокументПроводится ИЛИ ДокументПроведен;
	Элементы.ФормаЗаписатьИЗакрыть.Доступность = Элементы.ФормаЗаписатьИЗакрыть.Доступность И ПравоРучнойКорректировкиПроводок;
	Элементы.ФормаЗаписать.Доступность = Элементы.ФормаЗаписать.Доступность И ПравоРучнойКорректировкиПроводок;
	Элементы.РучнаяКорректировкаПроводок.Доступность = (ДокументПроводится ИЛИ ДокументПроведен) И ТипДокументаКорректируетсяВручную(Документ)
		И ПравоРучнойКорректировкиПроводок И Не НеИзменятьПроводки;

КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеОВведенныхКорректировках()
	
	Если ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Документы.ОперацияБух) Тогда
	
		СписокКорректировок.ЗагрузитьЗначения(ВернутьСписокКорректировок(Документ));
		
		Элементы.ГруппаКорректировкиДокументамиРеглУчета.Видимость = СписокКорректировок.Количество();
		
		Если СписокКорректировок.Количество() = 1 Тогда
			ТекстКорректировки = СписокКорректировок.Получить(0).Значение;
		Иначе
			ТекстКорректировки = НСтр("ru = 'Открыть список введенных корректировок (%Количество%)'");
			ТекстКорректировки = СтрЗаменить(ТекстКорректировки, "%Количество%", СписокКорректировок.Количество());
		КонецЕсли;
		
	Иначе
		
		Элементы.ГруппаКорректировкиДокументамиРеглУчета.Видимость = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидКоманднойПанелиНабораЗаписей()	
	
	Элементы.КоманднаяПанельНабораЗаписей.ПодчиненныеЭлементы.НаборЗаписейДобавить.Отображение = ОтображениеКнопки.КартинкаИТекст;
	
	Элементы.КоманднаяПанельНабораЗаписей.ПодчиненныеЭлементы.НаборЗаписейУдалить.ТолькоВоВсехДействиях = Ложь;
	Элементы.КоманднаяПанельНабораЗаписей.ПодчиненныеЭлементы.НаборЗаписейУдалить.Отображение = ОтображениеКнопки.Картинка;
	
	Элементы.КоманднаяПанельНабораЗаписей.ПодчиненныеЭлементы.НаборЗаписейСкопировать.ТолькоВоВсехДействиях = Ложь;
	Элементы.КоманднаяПанельНабораЗаписей.ПодчиненныеЭлементы.НаборЗаписейСкопировать.Отображение = ОтображениеКнопки.Картинка;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовкиСумм()

	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	ЗаголовокСуммы = НСтр("ru = 'Сумма (%РегламентированнаяВалюта%)'");
	Элементы.Сумма.Заголовок = СтрЗаменить(ЗаголовокСуммы, "%РегламентированнаяВалюта%", ВалютаРегламентированногоУчета);
	Если Не ПолучитьФункциональнуюОпцию("ПоддержкаПБУ18", Новый Структура("Период, Организация", Дата, Организация)) Тогда
		Элементы.СуммаНУДт.Заголовок = Нстр("ru = 'Сумма НУ Дт'");
		Элементы.СуммаНУКт.Заголовок = Нстр("ru = 'Сумма НУ Кт'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовкиПодразделения()

	ИспользоватьПодразделения = ПолучитьФункциональнуюОпцию("ИспользоватьПодразделения");
	ШаблоныЗаголовка = Новый Массив;
	Если ИспользоватьПодразделения Тогда
		ШаблоныЗаголовка.Добавить(НСтр("ru = 'Подразделение %1'"));
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетЗатратПоНаправлениямДеятельности")
		ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьУчетДоходовПоНаправлениямДеятельности")
		ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьУчетДСпоНаправлениямДеятельностиРаздельно")
		ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьУчетДСпоНаправлениямДеятельностиПоКорреспонденции")
		ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьУчетВнеоборотныхАктивовПоНаправлениямДеятельности") Тогда
		ШаблоныЗаголовка.Добавить(НСтр("ru = 'Направление %1'"));
	КонецЕсли;
	
	Если ШаблоныЗаголовка.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ШаблонЗаголовка = СтрСоединить(ШаблоныЗаголовка, ", ");
	Если ИспользоватьПодразделения Тогда
		Элементы.ПодразделениеДт.Заголовок = СтрШаблон(ШаблонЗаголовка, НСтр("ru = 'Дт'"));
		Элементы.ПодразделениеКт.Заголовок = СтрШаблон(ШаблонЗаголовка, НСтр("ru = 'Кт'"));
	Иначе
		Элементы.НаправлениеДеятельностиДт.ОтображатьВШапке = Истина;
		Элементы.НаправлениеДеятельностиКт.ОтображатьВШапке = Истина;
		Элементы.НаправлениеДеятельностиДт.Заголовок = СтрШаблон(ШаблонЗаголовка, НСтр("ru = 'Дт'"));
		Элементы.НаправлениеДеятельностиКт.Заголовок = СтрШаблон(ШаблонЗаголовка, НСтр("ru = 'Кт'"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПоказатьКомандуНастрокиСчетовУчета(Статус, Комментарий)
	
	Результат = Ложь;
	
	Если Статус <> Перечисления.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета Тогда
		Возврат Результат;
	КонецЕсли;
	
	ВидыСчетовДляНастройки = Новый Массив;
	Для каждого ЗначениеПеречисления Из Метаданные.Перечисления.ВидыСчетовРеглУчета.ЗначенияПеречисления Цикл
		Если ЗначениеПеречисления.Имя = "ПрочиеАктивыПассивы" Тогда
			Продолжить;
		КонецЕсли;
		Если Найти(Комментарий, ЗначениеПеречисления.Синоним) > 0 Тогда
			Результат = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПредставлениеОтраженияДокумента(Документ, ДатаДокумента, ЕстьПроводки, ДокументПроведен)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОтражениеДокументовВРеглУчете.Регистратор КАК Документ,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК НеУказаныСчетаУчета,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК КОтражениюВРеглУчете,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ОтраженоВРеглУчете,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВУчетеВручную)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК КОтражениюВУчетеВручную,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВУчетеВручную)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ОтраженоВУчетеВручную
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
	|ГДЕ
	|	ОтражениеДокументовВРеглУчете.Регистратор = &Документ
	|
	|СГРУППИРОВАТЬ ПО
	|	ОтражениеДокументовВРеглУчете.Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтражениеДокументовВРеглУчете.Организация КАК Организация,
	|	ОтражениеДокументовВРеглУчете.ДатаОтражения КАК ДатаОтражения
	|ПОМЕСТИТЬ ДанныеДляПроверкиРасчетовПоСебестоимости
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
	|		ПО ОтражениеДокументовВРеглУчете.Организация = СебестоимостьТоваров.Организация
	|			И ОтражениеДокументовВРеглУчете.Регистратор = СебестоимостьТоваров.Регистратор
	|			И (СебестоимостьТоваров.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
	|			И (ОтражениеДокументовВРеглУчете.Регистратор = &Документ)
	|ГДЕ
	|	ОтражениеДокументовВРеглУчете.Регистратор = &Документ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ДатаОтражения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаданияКРасчетуСебестоимости.Организация,
	|	ЗаданияКРасчетуСебестоимости.Месяц
	|ИЗ
	|	ДанныеДляПроверкиРасчетовПоСебестоимости КАК ДанныеДляПроверкиРасчетаСебестоимости
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияКРасчетуСебестоимости КАК ЗаданияКРасчетуСебестоимости
	|		ПО ДанныеДляПроверкиРасчетаСебестоимости.Организация = ЗаданияКРасчетуСебестоимости.Организация
	|			И ДанныеДляПроверкиРасчетаСебестоимости.ДатаОтражения >= ЗаданияКРасчетуСебестоимости.Месяц
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ЗаданияКРаспределениюРасчетовСПоставщиками.Месяц,
	|	ОтражениеДокументовВРеглУчете.Организация
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПоставщиками
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияКРаспределениюРасчетовСПоставщиками КАК ЗаданияКРаспределениюРасчетовСПоставщиками
	|		ПО РасчетыСПоставщиками.АналитикаУчетаПоПартнерам = ЗаданияКРаспределениюРасчетовСПоставщиками.АналитикаУчетаПоПартнерам
	|			И (РасчетыСПоставщиками.Регистратор = &Документ)
	|			И (РасчетыСПоставщиками.Активность),
	|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
	|ГДЕ
	|	РасчетыСПоставщиками.Регистратор = &Документ
	|	И РасчетыСПоставщиками.Активность
	|	И ОтражениеДокументовВРеглУчете.Регистратор = &Документ
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерам,
	|	ЗаданияКРаспределениюРасчетовСПоставщиками.Месяц,
	|	ОтражениеДокументовВРеглУчете.Организация
	|
	|ИМЕЮЩИЕ
	|	ЗаданияКРаспределениюРасчетовСПоставщиками.Месяц <= МИНИМУМ(ОтражениеДокументовВРеглУчете.ДатаОтражения)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам,
	|	ЗаданияКРаспределениюРасчетовСКлиентами.Месяц,
	|	ОтражениеДокументовВРеглУчете.Организация
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияКРаспределениюРасчетовСКлиентами КАК ЗаданияКРаспределениюРасчетовСКлиентами
	|		ПО РасчетыСКлиентами.АналитикаУчетаПоПартнерам = ЗаданияКРаспределениюРасчетовСКлиентами.АналитикаУчетаПоПартнерам
	|			И (РасчетыСКлиентами.Регистратор = &Документ)
	|			И (РасчетыСКлиентами.Активность),
	|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
	|ГДЕ
	|	РасчетыСКлиентами.Регистратор = &Документ
	|	И РасчетыСКлиентами.Активность
	|	И ОтражениеДокументовВРеглУчете.Регистратор = &Документ
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам,
	|	ЗаданияКРаспределениюРасчетовСКлиентами.Месяц,
	|	ОтражениеДокументовВРеглУчете.Организация
	|
	|ИМЕЮЩИЕ
	|	ЗаданияКРаспределениюРасчетовСКлиентами.Месяц <= МИНИМУМ(ОтражениеДокументовВРеглУчете.ДатаОтражения)";
	
	Запрос.УстановитьПараметр("Документ", Документ);
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	ВыборкаСостоянияОтражения = Результат.Получить(0).Выбрать();
	
	Если ВыборкаСостоянияОтражения.Следующий() Тогда
		Если ВыборкаСостоянияОтражения.КОтражениюВУчетеВручную Тогда
			Состояние = Новый ФорматированнаяСтрока(
							НСтр("ru = 'Документ был изменен. Скорректируйте проводки или подтвердите актуальность текущих.'"),,
							ЦветаСтиля.ИзмененноеЗначениеРеквизитаЦвет);
		ИначеЕсли ВыборкаСостоянияОтражения.ОтраженоВУчетеВручную Тогда
			Состояние = Новый ФорматированнаяСтрока(
							НСтр("ru = 'Отражен в учете вручную'"),,
							ЦветаСтиля.ТекстИнформационнойНадписи);
		ИначеЕсли ВыборкаСостоянияОтражения.НеУказаныСчетаУчета Тогда
			Состояние = 
				?(ВыборкаСостоянияОтражения.ОтраженоВРеглУчете, 
					Новый ФорматированнаяСтрока(
							НСтр("ru = 'Частично отражен в учете'"),, 
							ЦветаСтиля.ПросроченныеДанныеЦвет, , "РасшифровкаСтатуса"), 
					Новый ФорматированнаяСтрока(
							НСтр("ru = 'Не удалось отразить в учете'"),,
							ЦветаСтиля.ПоясняющийОшибкуТекст));
		ИначеЕсли ВыборкаСостоянияОтражения.КОтражениюВРеглУчете Тогда
			Состояние = ?(ВыборкаСостоянияОтражения.ОтраженоВРеглУчете, 
					Новый ФорматированнаяСтрока(
							НСтр("ru = 'Частично отражен в учете'"),, 
							ЦветаСтиля.ТекстИнформационнойНадписи, , "РасшифровкаСтатуса"), 
					Новый ФорматированнаяСтрока(
							НСтр("ru = 'Ожидает отражения в учете'"),,
							ЦветаСтиля.ТекстИнформационнойНадписи));
		Иначе
			Состояние = Новый ФорматированнаяСтрока(
							НСтр("ru = 'Отражен в учете'"),,
							ЦветаСтиля.ТекстИнформационнойНадписи);
		КонецЕсли;
	ИначеЕсли ЕстьПроводки Тогда
		
		Состояние = Новый ФорматированнаяСтрока(
							НСтр("ru = 'Отражен в учете'"),,
							ЦветаСтиля.ТекстИнформационнойНадписи);
	ИначеЕсли НЕ ДокументПроведен Тогда
		
		Состояние = Новый ФорматированнаяСтрока(
							НСтр("ru = 'Документ не проведен'"),,
							ЦветаСтиля.ТекстИнформационнойНадписи);
	Иначе
		
		ДатаНачалаОтраженияВРеглУчете = Константы.ДатаНачалаВеденияРеглУчета.Получить();
		Состояние = ?(ДатаДокумента < ДатаНачалаОтраженияВРеглУчете, 
						Новый ФорматированнаяСтрока(
							СтрШаблон(НСтр("ru = 'Регламентированный учет ведется с %1, отражение документа в учете не требуется'"), 
								Формат(ДатаНачалаОтраженияВРеглУчете, "ДЛФ=D")),,
							ЦветаСтиля.ТекстИнформационнойНадписи), 
						Новый ФорматированнаяСтрока(
							НСтр("ru = 'Не требует отражения в учете'"),,
							ЦветаСтиля.ТекстИнформационнойНадписи));
	КонецЕсли;
	
	ВзаиморасчетыАкуальны = Истина;
	СебестоимостьАкуальна = Истина;
	МассивУточнениеСостояния = Новый Массив;
	Если Не Результат.Получить(2).Пустой() Тогда
		УточнениеСостояния = Новый ФорматированнаяСтрока(
							НСтр("ru = 'Расчет себестоимости не выполнен.'")+" ",,
							ЦветаСтиля.ПоясняющийТекст);
		МассивУточнениеСостояния.Добавить(УточнениеСостояния);
		СебестоимостьАкуальна = Ложь;
	КонецЕсли;
	Если Не Результат.Получить(3).Пустой() ИЛИ Не Результат.Получить(4).Пустой() Тогда
		УточнениеСостояния = Новый ФорматированнаяСтрока(
							НСтр("ru = 'Распределение расчетов не выполнено.'")+" ",,
							ЦветаСтиля.ПоясняющийТекст);
		МассивУточнениеСостояния.Добавить(УточнениеСостояния);
		ВзаиморасчетыАкуальны = Ложь;
	КонецЕсли;
	
	Если НЕ (СебестоимостьАкуальна И ВзаиморасчетыАкуальны) 
			И ВыборкаСостоянияОтражения.ОтраженоВРеглУчете Тогда
		УточнениеСостояния = Новый ФорматированнаяСтрока(
							НСтр("ru = 'Проводки по документу могут измениться.'"),,
							ЦветаСтиля.ПоясняющийТекст);
		МассивУточнениеСостояния.Добавить(УточнениеСостояния);
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Состояние",            Состояние);
	Результат.Вставить("УточнениеСостояния",   Новый ФорматированнаяСтрока(МассивУточнениеСостояния));
	Результат.Вставить("ВзаиморасчетыАктуальны", ВзаиморасчетыАкуальны);
	Результат.Вставить("СебестоимостьАкуальна",  СебестоимостьАкуальна);
	
	Комментарий = РеглУчетПроведениеСервер.СводныйКомментарийПоДокументу(Документ);
	Результат.Вставить("Комментарий", Комментарий);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ОтражениеДокументов

&НаКлиенте
Процедура ОбработчикОповещенияВопросПередАвтоматическимЗаполнениемПроводок(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		Если ДополнительныеПараметры.Свойство("ОчисткаПроводок") Тогда
			НаборЗаписей.Очистить();
			Записать(Новый Структура("ЗадаватьВопросПередЗаписью", Ложь));
		КонецЕсли;
		
		ЗапуститьФоновоеЗаданиеПоЗаполнениюПроводок();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОповещенияПриСнятииФлагаРучнаяКорректировкаПроводок(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Записать(Новый Структура("ЗадаватьВопросПередЗаписью", Ложь));
	Иначе
		Объект.РучнаяКорректировкаПроводок = Истина;
	КонецЕсли;
	
	УстановитьДоступностьЭлементовФормы("РучнаяКорректировкаПроводок");
	
КонецПроцедуры

&НаСервере
Функция ОтразитьДокументВРеглУчетеСервер()
	
	СтруктураРеквизиты = Новый Структура("Ссылка, Дата, Организация", Документ, ДатаОтражения, Неопределено);
	
	ВыполняемыйМетод = "РеглУчетПроведениеСервер.ОтразитьДокументВФоне";
	
	НаименованиеЗадания = СтрШаблон(НСтр("ru = 'Актуализация проводок документа ""%1""'"), Документ);
	НаименованиеЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НаименованиеЗадания, ПредставлениеДокумента);
	
	ПараметрыФоновогоЗадания = ДлительныеОперации.ПараметрыВыполненияВФоне(ЭтотОбъект.УникальныйИдентификатор);
	ПараметрыФоновогоЗадания.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	ПараметрыФоновогоЗадания.КлючФоновогоЗадания = Документ.УникальныйИдентификатор();
	ПараметрыФоновогоЗадания.ОжидатьЗавершение = 1;	
	
	ПараметрыОбработки = Новый Структура("РеквизитыДокумента, ВыполнитьПересчеты", СтруктураРеквизиты, Истина);
	РезультатФоновогоЗадания = ДлительныеОперации.ВыполнитьВФоне(ВыполняемыйМетод, ПараметрыОбработки, ПараметрыФоновогоЗадания);
	РезультатФоновогоЗадания.Вставить("ВыводитьПрогрессВыполнения", Ложь);
	РезультатФоновогоЗадания.Вставить("ВыводитьСообщения", Ложь);
	
	Если РезультатФоновогоЗадания.Статус = "Выполнено" Тогда
		ПовторноеОткрытиеСервер();
	КонецЕсли;
	
	Возврат РезультатФоновогоЗадания;
	
КонецФункции

&НаСервере
Процедура ПодтвердитьАктуальностьПроводокСервер()

	СтатусРучногоОтражения = Перечисления.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВУчетеВручную;
	РеглУчетПроведениеСервер.ЗарегистрироватьОтражениеДокумента(Документ, СтатусРучногоОтражения, Комментарий);

КонецПроцедуры

&НаСервере
Функция ПараметрыОтраженияДокументаВРеглУчете()
	
	ПараметрыОтражения = Новый Структура;
	ПараметрыОтражения.Вставить("Ссылка", Документ);
	ПараметрыОтражения.Вставить("Дата", ДатаОтражения);
	ПараметрыОтражения.Вставить("Организация", Организация);
	ПараметрыОтражения.Вставить("ВыполнитьПересчеты", Истина);
	
	Возврат ПараметрыОтражения;
	
КонецФункции

&НаСервере
Функция ЗаполнитьПроводкиАвтоматическиСервер() //выполняется по кнопке набора записей

	ПараметрыОтражения = ПараметрыОтраженияДокументаВРеглУчете();
	ПараметрыОтражения.Организация = Неопределено;
	
	ВыполняемыйМетод = "РеглУчетПроведениеСервер.ВернутьРезультатОтраженияДокумента";
	
	НаименованиеЗадания = СтрШаблон(НСтр("ru = 'Актуализация проводок документа ""%1""'"), Документ);
	НаименованиеЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НаименованиеЗадания, ПредставлениеДокумента);
	
	ПараметрыФоновогоЗадания = ДлительныеОперации.ПараметрыВыполненияВФоне(ЭтотОбъект.УникальныйИдентификатор);
	ПараметрыФоновогоЗадания.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	ПараметрыФоновогоЗадания.КлючФоновогоЗадания = Документ.УникальныйИдентификатор();
	ПараметрыФоновогоЗадания.ОжидатьЗавершение = 1;	
	
	РезультатФоновогоЗадания = ДлительныеОперации.ВыполнитьВФоне(ВыполняемыйМетод, ПараметрыОтражения, ПараметрыФоновогоЗадания);
	
	Если РезультатФоновогоЗадания.Статус = "Выполнено" Тогда
		ОбновитьПроводкиПоДаннымРезультатаВыполненияФоновогоЗадания(РезультатФоновогоЗадания.АдресРезультата);
	Иначе
		РезультатФоновогоЗадания.Вставить("ВыводитьПрогрессВыполнения", Ложь);
		РезультатФоновогоЗадания.Вставить("ВыводитьСообщения", Ложь);
	КонецЕсли;
	
	Возврат РезультатФоновогоЗадания;
	
КонецФункции

&НаКлиенте
Процедура ЗапуститьФоновоеЗаданиеПоЗаполнениюПроводок()
	РезультатФоновогоЗаданияОтраженияПроводок = ЗаполнитьПроводкиАвтоматическиСервер();
	Если РезультатФоновогоЗаданияОтраженияПроводок.Статус = "Выполняется" Тогда
		ПодключитьОбработчикОжидания("ОтражениеПроводокПроверитьНаКлиенте", 0.1, Истина);
	ИначеЕсли РезультатФоновогоЗаданияОтраженияПроводок.Статус = "Ошибка" Тогда
		ВызватьИсключение(НСтр("ru = 'При автоматическом заполнении проводок документа произошла ошибка. Подробности см. в Журнале регистрации.'"));
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбновитьПроводкиПоДаннымРезультатаВыполненияФоновогоЗадания(АдресРезультата)
	
	Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	// Изначально результат получаем без колонки активности, из-за чего проводки будут записываться неактивными, добавим данную колонку:
	Результат.ТаблицаПроводок.Колонки.Добавить("Активность");
	Результат.ТаблицаПроводок.ЗаполнитьЗначения(Истина, "Активность");
	
	Проводки = РеквизитФормыВЗначение("НаборЗаписей");
	Проводки.Загрузить(Результат.ТаблицаПроводок);
	РегистрыБухгалтерии.Хозрасчетный.ВыполнитьДопОбработкуПроводок(Проводки);
	ЗначениеВРеквизитФормы(Проводки, "НаборЗаписей");
	
	Если ЗначениеЗаполнено(Результат.КомментарийОшибки) Тогда
		Комментарий = Результат.КомментарийОшибки;
		Элементы.ГруппаКомментарийАвтоматическогоОтраженияВУчете.Видимость = Истина;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	ЗаполнитьПредставлениеВидовСубконто();
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьСостояниеОтраженияДокумента()
	
	ЕстьПроводки = НаборЗаписей.Количество() > 0;
	
	ДанныеОтражения = РеглУчетПроведениеСервер.ПолучитьДанныеОтраженияДокумента(Документ);
	СтатусОтраженияДокумента = ДанныеОтражения.Статус;
	Объект.РучнаяКорректировкаПроводок = 
		ПолучитьФункциональнуюОпцию("ИспользоватьРучнуюКорректировкуПроводокПоРеглУчету") 
		И ДанныеОтражения.РучноеОтражение;
		
	ПредставлениеОтражения = ПредставлениеОтраженияДокумента(Документ, ДатаОтражения, ЕстьПроводки, ДокументПроведен);
	Состояние = ПредставлениеОтражения.Состояние;
	УточнениеСостояния = ПредставлениеОтражения.УточнениеСостояния;
	Комментарий = ?(ДанныеОтражения.РучноеОтражение, ДанныеОтражения.Комментарий, ПредставлениеОтражения.Комментарий);
	ВзаиморасчетыСПартнерамиАктуальны = ПредставлениеОтражения.ВзаиморасчетыАктуальны;
	
	СостояниеОтображенияКомментария = ПолучитьСостояниеОтображенияКомментария(Комментарий);
	
	Элементы.НастроитьСчетаУчета.Видимость = Ложь;
	
	Элементы.ГруппаДопУточнениеСостояния.Видимость = ЗначениеЗаполнено(УточнениеСостояния);
	
	Если Не СтатусОтраженияДокумента.Пустая() Тогда 
		Элементы.НастроитьСчетаУчета.Видимость = ПоказатьКомандуНастрокиСчетовУчета(СтатусОтраженияДокумента, Комментарий);
	Иначе
		Если НЕ ЕстьПроводки Тогда
			Элементы.ЗаполнитьПроводкиАвтоматически.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеРеквизитовДокумента()

	Если Не Документ.Пустая() Тогда
		РеквизитыДляПоиска = "Дата, Проведен" 
			+ ?(ОбщегоНазначения.ЕстьРеквизитОбъекта("Организация", Документ.Метаданные()), ", Организация", "")
			+ ?(ОбщегоНазначения.ЕстьРеквизитОбъекта("ДатаПроведенияБанком", Документ.Метаданные()), ", ДатаПроведенияБанком", "");
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Документ, РеквизитыДляПоиска);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЗначенияРеквизитов);
		ДокументПроведен = ЗначенияРеквизитов.Проведен;
		ДатаОтражения = ?(ЗначенияРеквизитов.Свойство("ДатаПроведенияБанком"), ЗначенияРеквизитов.ДатаПроведенияБанком, Дата);
	КонецЕсли;		

КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОповещенияВопросПередЗаписью(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Записать(ДополнительныеПараметры);
		Если ДополнительныеПараметры.Свойство("ЗакрыватьПриЗаписи") Тогда
			Закрыть();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОповещенияВопросПередПроведениемАмортизации(РезультатВопроса, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		МассивДокументовДляПроведения = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Документ);
		ОбщегоНазначенияВызовСервера.ПровестиДокументы(МассивДокументовДляПроведения);
		ЭтотОбъект.Прочитать();
		Если Объект.РучнаяКорректировкаПроводок Тогда
			ЭтотОбъект.Модифицированность = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ТипДокументаКорректируетсяВручную(Документ)
	
	МассивТиповИсключений = Новый Массив;
	МассивТиповИсключений.Добавить(Тип("ДокументСсылка.АмортизацияОС"));
	МассивТиповИсключений.Добавить(Тип("ДокументСсылка.АмортизацияНМА"));
	
	Возврат МассивТиповИсключений.Найти(ТипЗнч(Документ)) = Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ОтражениеПроводокПроверитьНаКлиенте()
	МассивЗаданий = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(РезультатФоновогоЗаданияОтраженияПроводок);
	Результат = ДлительныеОперацииВызовСервера.ОперацииВыполнены(МассивЗаданий, Новый Массив);
	РезультатЗадания = Результат.Получить(РезультатФоновогоЗаданияОтраженияПроводок.ИдентификаторЗадания);
	СтатусРезультата = ?(РезультатЗадания = Неопределено, "Ошибка", РезультатЗадания.Статус);
	Если СтатусРезультата = "Выполняется" Тогда
		Если ФормаДлительнойОперации = Неопределено Тогда
			ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтотОбъект, УникальныйИдентификатор);
			ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОжидания);
		КонецЕсли;
		ПодключитьОбработчикОжидания("ОтражениеПроводокПроверитьНаКлиенте", ПараметрыОжидания.ТекущийИнтервал, Истина);
	Иначе
		Если ФормаДлительнойОперации <> Неопределено Тогда
			ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
		КонецЕсли;
		Если СтатусРезультата = "Ошибка" Тогда
			ТекстОшибки = ?(Объект.РучнаяКорректировкаПроводок,
				НСтр("ru = 'При автоматическом заполнении проводок документа произошла ошибка.'"),
				НСтр("ru = 'При отражении документа в регламентированном учете произошла ошибка.'"));
			ДополнительныйТекст = ?(РезультатЗадания = Неопределено,
				НСтр("ru = 'Не найдено выполняемое задание.'"),
				НСтр("ru = 'Подробности см. в Журнале регистрации.'"));
			ТекстОшибки = ТекстОшибки + Символы.ПС + ДополнительныйТекст;
			ВызватьИсключение(ТекстОшибки);
		Иначе
			Если Объект.РучнаяКорректировкаПроводок Тогда
				ОбновитьПроводкиПоДаннымРезультатаВыполненияФоновогоЗадания(РезультатФоновогоЗаданияОтраженияПроводок.АдресРезультата);
			Иначе
				ПовторноеОткрытиеСервер();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;		
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервереБезКонтекста
Функция ВернутьСписокКорректировок(Документ)
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	ОперацияБухЗаполнениеДвижений.Ссылка
	                      |ИЗ
	                      |	Документ.ОперацияБух.ЗаполнениеДвижений КАК ОперацияБухЗаполнениеДвижений
	                      |ГДЕ
	                      |	ОперацияБухЗаполнениеДвижений.Документ = &Документ
	                      |	И НЕ ОперацияБухЗаполнениеДвижений.Ссылка.ПометкаУдаления");
	Запрос.УстановитьПараметр("Документ", Документ);
	
	МассивКорректировок = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	Возврат МассивКорректировок;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьФормуКомментария(ДополнительныеПараметры = Неопределено)
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ФормаКомментарияЗакрытие", ЭтотОбъект, ДополнительныеПараметры);
	ПараметрыФормы = Новый Структура("РучнаяКорректировкаПроводок, Комментарий, УведомлятьОПустомКомментарии",
								Объект.РучнаяКорректировкаПроводок, Комментарий, УведомлятьОПустомКомментарии);
	ОткрытьФорму("Обработка.ОтражениеДокументовВРеглУчете.Форма.ФормаКомментария", ПараметрыФормы, ЭтотОбъект,, ВариантОткрытияОкна.ОтдельноеОкно,, ОповещениеОЗакрытии);
	
КонецПроцедуры

&НаКлиенте
Процедура ФормаКомментарияЗакрытие(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если Не Объект.РучнаяКорректировкаПроводок Тогда
		Возврат;
	КонецЕсли;
	
	Если Не РезультатЗакрытия = Неопределено Тогда
		
		Если Не Комментарий = РезультатЗакрытия.Комментарий Тогда
			Модифицированность = Истина;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, РезультатЗакрытия);
		
		Если ДополнительныеПараметры = Неопределено Тогда
			СостояниеОтображенияКомментария = ПолучитьСостояниеОтображенияКомментария(Комментарий);
		Иначе
			Записать(ДополнительныеПараметры);
			Если ДополнительныеПараметры.Свойство("ЗакрыватьПриЗаписи") Тогда
				Закрыть();
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьСостояниеОтображенияКомментария(Комментарий)
	
	Если ЗначениеЗаполнено(Комментарий) Тогда
		Возврат СтрПолучитьСтроку(Комментарий, 1);
	Иначе
		Возврат "добавить";
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция СтатусПроверкиДокумента(Документ)
	
	ДанныеПроверки = РегистрыСведений.СтатусыПроверкиДокументов.ПолучитьДанныеПроверкиДокумента(Документ);
	
	Если ДанныеПроверки.Проверен Тогда
		СтрокаВозврата = НСтр("ru = '(проверен)'");
	Иначе
		СтрокаВозврата = НСтр("ru = '(не проверен)'");
	КонецЕсли;
	
	Возврат " " + СтрокаВозврата;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПараметрыОткрытияФормыНастройкиОтражения(Документ)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтражениеДокументовВРеглУчете.Организация
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
	|ГДЕ
	|	ОтражениеДокументовВРеглУчете.Регистратор = &Документ
	|	И ОтражениеДокументовВРеглУчете.Статус = &Статус
	|;
	|
	|////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(ОтражениеДокументовВРеглУчете.ДатаОтражения), ДАТАВРЕМЯ(1,1,1)) КАК ДатаОтражения
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
	|ГДЕ
	|	ОтражениеДокументовВРеглУчете.Регистратор = &Документ
	|	И ОтражениеДокументовВРеглУчете.Статус = &Статус
	|";
	Запрос.УстановитьПараметр("Документ", Документ);
	Запрос.УстановитьПараметр("Статус",   Перечисления.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ТаблицаОрганизации = РезультатЗапроса[0].Выгрузить();
	ВыборкаДатаОкончания = РезультатЗапроса[1].Выбрать();
	ВыборкаДатаОкончания.Следующий();
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Организации", ТаблицаОрганизации.ВыгрузитьКолонку("Организация"));
	ПараметрыОткрытия.Вставить("ДатаОтражения", КонецДня(ВыборкаДатаОкончания.ДатаОтражения));
	
	Возврат ПараметрыОткрытия;
	
КонецФункции

&НаКлиенте
Процедура ОбработкаВыбораОрганизацииДляНастройкиСчетов(Результат, ДопПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуНастройкиСчетовУчета(
		Результат.Значение, 
		ДопПараметры.ОрганизацииДляНастройки,
		ДопПараметры.ДатаОтражения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНастройкиСчетовУчета(Организация, ОрганизацииДляНастройки, ДатаОтражения)
	
	СтруктураПараметры = Новый Структура;
	СтруктураПараметры.Вставить("Организация",             Организация);
	СтруктураПараметры.Вставить("ОрганизацииДляНастройки", ОрганизацииДляНастройки);
	СтруктураПараметры.Вставить("ДатаОкончанияПериода",    Новый СтандартнаяДатаНачала(ДатаОтражения));
	СтруктураПараметры.Вставить("ПоказыватьТолькоТребующиеНастройки", Истина);
	
	ОткрытьФорму("Обработка.НастройкаОтраженияДокументовВРеглУчете.Форма.ФормаНастройки", СтруктураПараметры, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Функция МассивДокументовТребующихОчисткиПроводок()
	
	МассивВозврата = Новый Массив;
	МассивВозврата.Добавить(Тип("ДокументСсылка.ПередачаОСАрендатору"));
	МассивВозврата.Добавить(Тип("ДокументСсылка.ВозвратОСОтАрендатора"));
	МассивВозврата.Добавить(Тип("ДокументСсылка.ВыбытиеАрендованныхОС"));
	МассивВозврата.Добавить(Тип("ДокументСсылка.ИзменениеПараметровОС"));
	МассивВозврата.Добавить(Тип("ДокументСсылка.МодернизацияОС"));
	МассивВозврата.Добавить(Тип("ДокументСсылка.ПеремещениеОС"));
	МассивВозврата.Добавить(Тип("ДокументСсылка.ПереоценкаНМА"));
	МассивВозврата.Добавить(Тип("ДокументСсылка.ПереоценкаОС"));
	МассивВозврата.Добавить(Тип("ДокументСсылка.ПодготовкаКПередачеНМА"));
	МассивВозврата.Добавить(Тип("ДокументСсылка.ПодготовкаКПередачеОС"));
	МассивВозврата.Добавить(Тип("ДокументСсылка.ПринятиеКУчетуНМА"));
	МассивВозврата.Добавить(Тип("ДокументСсылка.ПринятиеКУчетуОС"));
	МассивВозврата.Добавить(Тип("ДокументСсылка.СписаниеНМА"));
	МассивВозврата.Добавить(Тип("ДокументСсылка.СписаниеОС"));
	
	Возврат МассивВозврата;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьРасшифровкуСтатуса()
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Документ", Документ);
	
	ОткрытьФорму("Обработка.ОтражениеДокументовВРеглУчете.Форма.ФормаРасшифровкиСтатуса", ПараметрыФормы, ЭтаФорма); 
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти