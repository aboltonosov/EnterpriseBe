#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет наличие отрицательных остатков товаров организаций, наличие "развернутого" сальдо по видам запасов,
// возможность оформления передач товаров между организациями.
//
// Параметры:
//	Параметры - Структура - содержит свойства
//		* ДатаНачала - Дата - начальная дата проверки остатков
//		* ДатаОкончания - Дата - конечная дата проверки остатков
//		* МассивОрганизаций - Массив - организации, для которых выполняется проверка остатков
//	АдресХранилища - Строка - адрес во временном хранилище
//
Функция ПолучитьСостояниеОстатковТоваровОрганизаций(Параметры, АдресХранилища = Неопределено) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ТоварыОрганизаций.Организация КАК Организация,
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТоварыОрганизаций.Период КАК Период
	|
	|ПОМЕСТИТЬ ВтТаблицаОтрицательныхОстатков
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.ОстаткиИОбороты(&ДатаНачала, &ДатаОкончания, Месяц,,) КАК ТоварыОрганизаций
	|ГДЕ
	|	ТоварыОрганизаций.КоличествоКонечныйОстаток < 0
	|	И ТоварыОрганизаций.КоличествоНачальныйОстаток <> ТоварыОрганизаций.КоличествоКонечныйОстаток
	//++ НЕ УТКА
	|	И (НЕ &ИспользоватьУправлениеПроизводством22
	|			ИЛИ НЕ ЕСТЬNULL(ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Склад.ЦеховаяКладовая, ЛОЖЬ))
	//-- НЕ УТКА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	АналитикаУчетаНоменклатуры,
	|	Период
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТоварыОрганизаций.Период КАК Период
	|
	|ПОМЕСТИТЬ ВтТаблицаОтсутствующихТоваров
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.ОстаткиИОбороты(&ДатаНачала, &ДатаОкончания, Месяц,,) КАК ТоварыОрганизаций
	|ГДЕ
	|	ТоварыОрганизаций.КоличествоКонечныйОстаток < 0
	|	И ТоварыОрганизаций.КоличествоНачальныйОстаток <> ТоварыОрганизаций.КоличествоКонечныйОстаток
	//++ НЕ УТКА
	|	И (НЕ &ИспользоватьУправлениеПроизводством22
	|			ИЛИ НЕ ЕСТЬNULL(ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Склад.ЦеховаяКладовая, ЛОЖЬ))
	//-- НЕ УТКА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры,
	|	Период
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|// ОТРИЦАТЕЛЬНЫЕ ОСТАТКИ ТОВАРОВ ОРГАНИЗАЦИЙ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаОтрицательныхОстатков.Период КАК Период
	|ИЗ
	|	ВтТаблицаОтрицательныхОстатков КАК ТаблицаОтрицательныхОстатков
	|ГДЕ
	|	ТаблицаОтрицательныхОстатков.Организация В (&МассивОрганизаций)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период ВОЗР
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|// РАЗВЕРНУТОЕ САЛЬДО ПО ТОВАРАМ ОРГАНИЗАЦИЙ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТоварыОрганизаций.Период КАК Период
	|ИЗ (
	|	ВЫБРАТЬ
	|		ТоварыОрганизаций.Организация КАК Организация,
	|		ТоварыОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ТоварыОрганизаций.ВидЗапасов КАК ВидЗапасов,
	|		ТоварыОрганизаций.НомерГТД КАК НомерГТД,
	|		ТоварыОрганизаций.Период
	|	ИЗ
	|		РегистрНакопления.ТоварыОрганизаций.ОстаткиИОбороты(&ДатаНачала, &ДатаОкончания, Месяц,, 
	|			Организация В (&МассивОрганизаций)
	|		) КАК ТоварыОрганизаций
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			ВтТаблицаОтрицательныхОстатков КАК ТаблицаОтрицательныхОстатков
	|		ПО
	|			ТоварыОрганизаций.Организация = ТаблицаОтрицательныхОстатков.Организация
	|			И ТоварыОрганизаций.АналитикаУчетаНоменклатуры = ТаблицаОтрицательныхОстатков.АналитикаУчетаНоменклатуры
	|			И ТоварыОрганизаций.Период = ТаблицаОтрицательныхОстатков.Период
	|	ГДЕ
	|		ТоварыОрганизаций.КоличествоКонечныйОстаток < 0
	|		И ТоварыОрганизаций.КоличествоНачальныйОстаток <> ТоварыОрганизаций.КоличествоКонечныйОстаток
	|		И ТаблицаОтрицательныхОстатков.Период ЕСТЬ NULL
	//++ НЕ УТКА
	|		И (НЕ &ИспользоватьУправлениеПроизводством22
	|			ИЛИ НЕ ЕСТЬNULL(ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Склад.ЦеховаяКладовая, ЛОЖЬ))
	//-- НЕ УТКА
	|		
	|	) КАК ТоварыОрганизаций
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период ВОЗР
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|// НЕДОСТАТОЧНО ТОВАРОВ ОРГАНИЗАЦИЙ НА СКЛАДАХ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаОтсутствующихТоваров.Период КАК Период
	|ИЗ
	|	ВтТаблицаОтсутствующихТоваров КАК ТаблицаОтсутствующихТоваров
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период ВОЗР
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|// МОЖНО ОФОРМИТЬ ПЕРЕДАЧИ ТОВАРОВ МЕЖДУ ОРГАНИЗАЦИЯМИ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТоварыОрганизаций.Период КАК Период
	|ИЗ (
	|	ВЫБРАТЬ
	|		ТаблицаОтрицательныхОстатков.Организация КАК Организация,
	|		ТаблицаОтрицательныхОстатков.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ТаблицаОтрицательныхОстатков.Период КАК Период
	|	ИЗ
	|		ВтТаблицаОтрицательныхОстатков КАК ТаблицаОтрицательныхОстатков
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			ВтТаблицаОтсутствующихТоваров КАК ТаблицаОтсутствующихТоваров
	|		ПО
	|			ТаблицаОтрицательныхОстатков.АналитикаУчетаНоменклатуры = ТаблицаОтсутствующихТоваров.АналитикаУчетаНоменклатуры
	|			И ТаблицаОтрицательныхОстатков.Период = ТаблицаОтсутствующихТоваров.Период
	|	ГДЕ
	|		ТаблицаОтсутствующихТоваров.Период ЕСТЬ NULL
	|		И ТаблицаОтрицательныхОстатков.Организация В (&МассивОрганизаций)
	|
	|	) КАК ТоварыОрганизаций
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период ВОЗР
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|");
	
	Запрос.УстановитьПараметр("ДатаНачала", 	   Параметры.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", 	   Параметры.ДатаОкончания);
	Запрос.УстановитьПараметр("МассивОрганизаций", Параметры.МассивОрганизаций);
	//++ НЕ УТКА
	Запрос.УстановитьПараметр("ИспользоватьУправлениеПроизводством22",
		ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеПроизводством2_2"));
	//-- НЕ УТКА
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Если ЗначениеЗаполнено(АдресХранилища) Тогда
		ПоместитьВоВременноеХранилище(МассивРезультатов, АдресХранилища);
	КонецЕсли;
	
	Возврат МассивРезультатов;
	
КонецФункции

#КонецОбласти

#КонецЕсли
