
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СоответствиеТипыДокументовИсключений = Новый Соответствие;
	СоответствиеТипыДокументовИсключений.Вставить(Тип("ДокументСсылка.АмортизацияОСМеждународныйУчет"), Истина);
	СоответствиеТипыДокументовИсключений.Вставить(Тип("ДокументСсылка.АмортизацияНМАМеждународныйУчет"), Истина);
	СоответствиеТипыДокументовИсключений.Вставить(Тип("ДокументСсылка.ИзменениеПараметровНМАМеждународныйУчет"), Истина);
	СоответствиеТипыДокументовИсключений.Вставить(Тип("ДокументСсылка.ИзменениеПараметровОСМеждународныйУчет"), Истина);
	СоответствиеТипыДокументовИсключений.Вставить(Тип("ДокументСсылка.ПеремещениеНМАМеждународныйУчет"), Истина);
	СоответствиеТипыДокументовИсключений.Вставить(Тип("ДокументСсылка.ПеремещениеОСМеждународныйУчет"), Истина);
	СоответствиеТипыДокументовИсключений.Вставить(Тип("ДокументСсылка.ПереводОсновныхСредствИнвестиционногоИмуществаМеждународныйУчет"), Истина);
	СоответствиеТипыДокументовИсключений.Вставить(Тип("ДокументСсылка.СписаниеНМАМеждународныйУчет"), Истина);
	СоответствиеТипыДокументовИсключений.Вставить(Тип("ДокументСсылка.СписаниеОСМеждународныйУчет"), Истина);
	
	ТипыДокументовИсключений = Новый ФиксированноеСоответствие(СоответствиеТипыДокументовИсключений);
	
	Если Параметры.Свойство("ДатаОкончанияПериода") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "Дата", Параметры.ДатаОкончанияПериода, ВидСравненияКомпоновкиДанных.МеньшеИлиРавно, , Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ОтражениеДокументовВМеждународномУчете" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПоказатьЗначение(, Элемент.ТекущиеДанные.Документ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗарегистрироватьДляОтраженияВУчете(Команда)
	
	МассивДокументов = МассивДокументов(Элементы.Список.ВыделенныеСтроки);
	Если МассивДокументов.Количество() > 0 Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработчикПоказатьВопросПриРегистрацииДляОтраженияВУчете", ЭтотОбъект, МассивДокументов);
		ТекстВопроса = Нстр("ru = 'Выбранные документы будут зарегистрированы для повторного отражения в международном учете. Продолжить?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроводкиМеждународногоУчета(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Для выполнения команды необходимо выбрать документ'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыОтбора = Новый Структура("Регистратор", ТекущиеДанные.Документ);
	ПараметрыФормы = Новый Структура("Отбор", ПараметрыОтбора);
	ОткрытьФорму("РегистрБухгалтерии.Международный.Форма.ПроводкиМеждународногоУчета", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция МассивДокументов(МассивВыделенныхСтрок)
	
	ЕстьОшибкаПереотраженияДокументовОСНМА = Ложь;
	
	МассивДокументов = Новый Массив;
	Для каждого Строка Из МассивВыделенныхСтрок Цикл
		Документ = Элементы.Список.ДанныеСтроки(Строка).Документ;
		Если ТипыДокументовИсключений.Получить(ТипЗнч(Документ)) = Неопределено Тогда
			МассивДокументов.Добавить(Документ);
		Иначе
			ЕстьОшибкаПереотраженияДокументовОСНМА = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьОшибкаПереотраженияДокументовОСНМА Тогда
		ПоказатьПредупреждение(,
			НСтр("ru='Документы учета внеоборотных активов отражаются в международном учете при проведении'"));
	КонецЕсли;
	
	Возврат МассивДокументов;
	
КонецФункции

&НаКлиенте
Процедура ОбработчикПоказатьВопросПриРегистрацииДляОтраженияВУчете(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗарегистрироватьДляОтраженияВУчетеСервер(ДополнительныеПараметры);
		ОповеститьОбИзмененииОтраженияДокументаВМеждународномУчете();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗарегистрироватьДляОтраженияВУчетеСервер(МассивДокументов)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.ДатаОтражения КАК ДатаОтражения,
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ДанныеРегистра.Организация КАК Организация
	|ПОМЕСТИТЬ ДокументыКОтражению
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК ДанныеРегистра
	|ГДЕ
	|	ДанныеРегистра.Регистратор В(&МассивДокументов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистра.Период,
	|	ДанныеРегистра.Регистратор,
	|	ДанныеРегистра.ДатаОтражения,
	|	ДанныеРегистра.Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Регистратор,
	|	Организация,
	|	ДатаОтражения";
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	Запрос.Выполнить();
	
	МеждународныйУчетПроведениеСервер.ВернутьДокументыКОтражению(Запрос.МенеджерВременныхТаблиц);
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьОбИзмененииОтраженияДокументаВМеждународномУчете()

	Оповестить("Запись_ОтражениеДокументовВМеждународномУчете");

КонецПроцедуры

#КонецОбласти
