#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает список используемых способов для обеспечения работой.
//
// Параметры:
//  Номенклатура - СправочникСсылка.Номенклатура - Номенклатура работы,
//  Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры - Характеристика работы,
//  ДоступныеТипыОбеспечения - Массив - Отбор по типам способов обеспечения.
//
// Возвращаемое значение:
//  Список значений - Список, содержащий способы обеспечения работой.
//
Функция СписокСпособовОбеспечения(Номенклатура, Характеристика, ДоступныеТипыОбеспечения = Неопределено) Экспорт

	Запрос = Новый Запрос();

	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	НаборДанных.Способ                             КАК СпособОбеспечения,
		|	ПРЕДСТАВЛЕНИЕ(НаборДанных.Способ)              КАК СпособОбеспеченияПредставление,
		|	МИНИМУМ(НаборДанных.Приоритет)                 КАК Приоритет,
		|	МИНИМУМ(НаборДанных.РеквизитДопУпорядочивания) КАК РеквизитДопУпорядочивания
		|ИЗ(
		|	ВЫБРАТЬ
		|		Способы.СпособОбеспеченияПотребностей КАК Способ,
		|		1                                     КАК Приоритет,
		|		Способы.РеквизитДопУпорядочивания     КАК РеквизитДопУпорядочивания
		|	ИЗ
		|		РегистрСведений.ВариантыОбеспеченияТоварами КАК Способы
		|	ГДЕ
		|		Способы.Характеристика            = &Характеристика
		|		И Способы.Номенклатура            = &Номенклатура
		|		И &Характеристика <> ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|		И Способы.СпособОбеспеченияПотребностей.ТипОбеспечения В(
		|			ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Покупка),
		|			ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Производство))
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		Способы.СпособОбеспеченияПотребностей КАК Способ,
		|		2                                     КАК Приоритет,
		|		Способы.РеквизитДопУпорядочивания     КАК РеквизитДопУпорядочивания
		|	ИЗ
		|		РегистрСведений.ВариантыОбеспеченияТоварами КАК Способы
		|	ГДЕ
		|		Способы.Характеристика            = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|		И Способы.Номенклатура            = &Номенклатура
		|		И Способы.СпособОбеспеченияПотребностей.ТипОбеспечения В(
		|			ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Покупка),
		|			ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Производство))
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		КонстантаОсновнойСпособ.Значение КАК Способ,
		|		3                                КАК Приоритет,
		|		1                                КАК РеквизитДопУпорядочивания
		|	ИЗ
		|		Константа.ОсновнойСпособОбеспеченияПотребностей КАК КонстантаОсновнойСпособ
		|	ГДЕ
		|		КонстантаОсновнойСпособ.Значение <> ЗНАЧЕНИЕ(Справочник.СпособыОбеспеченияПотребностей.ПустаяСсылка)
		|		И КонстантаОсновнойСпособ.Значение.ТипОбеспечения В(
		|			ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Покупка),
		|			ЗНАЧЕНИЕ(Перечисление.ТипыОбеспечения.Производство)) ) КАК НаборДанных
		|ГДЕ
		|	&ОтборПоТипуОбеспечения
		|СГРУППИРОВАТЬ ПО
		|	НаборДанных.Способ
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет,
		|	РеквизитДопУпорядочивания";

	Запрос.УстановитьПараметр("Номенклатура",   Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Характеристика);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоТипуОбеспечения", ?(ДоступныеТипыОбеспечения = Неопределено,
		"Истина", "НаборДанных.Способ.ТипОбеспечения В(&ДоступныеТипыОбеспечения)"));
	Запрос.УстановитьПараметр("ДоступныеТипыОбеспечения", ДоступныеТипыОбеспечения);

	Выборка = Запрос.Выполнить().Выбрать();

	Список = Новый СписокЗначений();
	Пока Выборка.Следующий() Цикл
		Список.Добавить(Выборка.СпособОбеспечения, Выборка.СпособОбеспеченияПредставление);
	КонецЦикла;

	Возврат Список;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СлужебныйПрограммныйИнтерфейс

Функция ЗаполнитьСпособОбеспечения(Таблица, СпособОбеспечения) Экспорт
	
	Набор = РегистрыСведений.ВариантыОбеспеченияРаботами.СоздатьНаборЗаписей();
	ОбработанныеСтроки = Новый Массив();
	
	Если ЗначениеЗаполнено(СпособОбеспечения) Тогда
		
		Набор.Добавить();
		Запись = Набор[0];
		Запись.РеквизитДопУпорядочивания = 1;
		Запись.СпособОбеспеченияПотребностей = СпособОбеспечения;
		
		Для Каждого СтрокаТаблицы Из Таблица Цикл
			
			ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицы);
			Набор.Отбор.Номенклатура.Установить(СтрокаТаблицы.Номенклатура);
			Набор.Отбор.Характеристика.Установить(СтрокаТаблицы.Характеристика);
			
			Попытка
				
				Набор.Записать();
				ОбработанныеСтроки.Добавить(СтрокаТаблицы.Идентификатор);
				
			Исключение
				
				ИмяСобытияЖурнала = НСтр("ru = 'Заполнение способа обеспечения работ'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
				КомментарийКОшибке = ИнформацияОбОшибке();
				ЗаписьЖурналаРегистрации(ИмяСобытияЖурнала,
					УровеньЖурналаРегистрации.Ошибка,
					Метаданные.РегистрыСведений.ВариантыОбеспеченияРаботами,
					,
					КомментарийКОшибке);
				
			КонецПопытки;
			
		КонецЦикла;
		
	Иначе
		
		Для Каждого СтрокаТаблицы Из Таблица Цикл
			
			Набор.Отбор.Номенклатура.Установить(СтрокаТаблицы.Номенклатура);
			Набор.Отбор.Характеристика.Установить(СтрокаТаблицы.Характеристика);
			
			Попытка
				
				Набор.Записать();
				ОбработанныеСтроки.Добавить(СтрокаТаблицы.Идентификатор);
				
			Исключение
				
				КомментарийКОшибке = ИнформацияОбОшибке();
				ИмяСобытияЖурнала = НСтр("ru = 'Заполнение способа обеспечения работ'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
				ЗаписьЖурналаРегистрации(ИмяСобытияЖурнала,
					УровеньЖурналаРегистрации.Ошибка,
					Метаданные.РегистрыСведений.ВариантыОбеспеченияРаботами,
					,
					КомментарийКОшибке);
				
			КонецПопытки;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ТекстДанныеНеЗаписаны = НСтр("ru = 'Не удалось записать изменения способа обеспечения работами.'");
	ТекстДанныеЗаписаныЧастично = НСтр("ru = 'Не все изменения способа обеспечения работами удалось записать.'");
	ОбеспечениеСервер.СообщитьОбОшибкахЗаписиПараметровОбеспеченияПотребностей(
		Таблица, ОбработанныеСтроки, ТекстДанныеНеЗаписаны, ТекстДанныеЗаписаныЧастично);
	
	Возврат ОбработанныеСтроки;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли