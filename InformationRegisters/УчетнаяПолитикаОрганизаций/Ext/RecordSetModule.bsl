#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	//++ НЕ УТ
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	УчетнаяПолитикаОрганизацийПередЗаписью();
	
	//-- НЕ УТ
	
	Если ЭтотОбъект.Количество() = 0 Тогда
		Возврат
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УточняемыеДанные.Организация КАК Организация,
	|	УточняемыеДанные.УчетнаяПолитика КАК УчетнаяПолитика,
	|	УточняемыеДанные.Период КАК Период,
	|	УточняемыеДанные.ПлательщикЕНВД КАК ПлательщикЕНВД,
	|	УточняемыеДанные.ПрименяетсяПБУ18 КАК ПрименяетсяПБУ18
	|ПОМЕСТИТЬ ВТДанныеДляУточнения
	|ИЗ
	|	&Данные КАК УточняемыеДанные";
	Запрос.УстановитьПараметр("Данные", ЭтотОбъект.Выгрузить());
	Запрос.Выполнить(); 
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УточняемыеДанные.Организация КАК Организация,
	|	УточняемыеДанные.УчетнаяПолитика КАК УчетнаяПолитика,
	|	ВЫБОР
	|		КОГДА УточняемыеДанные.Организация = УточняемыеДанные.Организация.ГоловнаяОрганизация
	|		ТОГДА УточняемыеДанные.УчетнаяПолитика.ПрименяетсяЕНВД
	|		ИНАЧЕ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.ПлательщикЕНВД, ЛОЖЬ)
	|	КОНЕЦ КАК ПлательщикЕНВД,
	|	ВЫБОР
	|		КОГДА УточняемыеДанные.Организация = УточняемыеДанные.Организация.ГоловнаяОрганизация
	|		ТОГДА УточняемыеДанные.УчетнаяПолитика.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Общая)
	|				И УточняемыеДанные.Организация.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
	|		ИНАЧЕ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.ПлательщикНалогаНаПрибыль, ЛОЖЬ)
	|	КОНЕЦ КАК ПлательщикНалогаНаПрибыль,
	|	ВЫБОР
	|		КОГДА УточняемыеДанные.Организация = УточняемыеДанные.Организация.ГоловнаяОрганизация
	|		ТОГДА УточняемыеДанные.УчетнаяПолитика.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Упрощенная)
	|		ИНАЧЕ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.ПрименяетсяУСН, ЛОЖЬ)
	|	КОНЕЦ КАК ПрименяетсяУСН,
	|	ВЫБОР
	|		КОГДА УточняемыеДанные.Организация = УточняемыеДанные.Организация.ГоловнаяОрганизация
	|		ТОГДА УточняемыеДанные.УчетнаяПолитика.ОбъектНалогообложенияУСН = ЗНАЧЕНИЕ(Перечисление.ОбъектыНалогообложенияПоУСН.ДоходыМинусРасходы)
	|		ИНАЧЕ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.ПрименяетсяУСНДоходыМинусРасходы, ЛОЖЬ)
	|	КОНЕЦ КАК ПрименяетсяУСНДоходыМинусРасходы,
	|	ВЫБОР
	|		КОГДА УточняемыеДанные.Организация = УточняемыеДанные.Организация.ГоловнаяОрганизация
	|		ТОГДА УточняемыеДанные.УчетнаяПолитика.ПрименяетсяПБУ18
	|		ИНАЧЕ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.ПрименяетсяПБУ18, ЛОЖЬ)
	|	КОНЕЦ КАК ПрименяетсяПБУ18,
	|	УточняемыеДанные.Период КАК Период
	|ИЗ
	|	ВТДанныеДляУточнения КАК УточняемыеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|		ПО УточняемыеДанные.Организация.ГоловнаяОрганизация = УчетнаяПолитикаОрганизаций.Организация
	|			И УточняемыеДанные.Период = УчетнаяПолитикаОрганизаций.Период";
	
	ЭтотОбъект.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	//++ НЕ УТ
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	СформироватьЗаданияПриИзмененииУчетнойПолитики();
	
	//-- НЕ УТ
	Если ЭтотОбъект.Количество() = 0 Тогда
		Возврат
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УточняемыеДанные.Организация КАК Организация,
	|	УточняемыеДанные.УчетнаяПолитика КАК УчетнаяПолитика,
	|	УточняемыеДанные.ПлательщикЕНВД КАК ПлательщикЕНВД,
	|	УточняемыеДанные.ПлательщикНалогаНаПрибыль КАК ПлательщикНалогаНаПрибыль,
	|	УточняемыеДанные.ПрименяетсяУСН КАК ПрименяетсяУСН,
	|	УточняемыеДанные.ПрименяетсяУСНДоходыМинусРасходы КАК ПрименяетсяУСНДоходыМинусРасходы,
	|	УточняемыеДанные.ПрименяетсяПБУ18 КАК ПрименяетсяПБУ18,
	|	УточняемыеДанные.Период КАК Период
	|ПОМЕСТИТЬ ВТНовыеДанные
	|ИЗ
	|	&Данные КАК УточняемыеДанные
	|";
	Запрос.УстановитьПараметр("Данные", ЭтотОбъект.Выгрузить());
	Запрос.Выполнить(); 
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Организации.Ссылка КАК Организация,
	|	НовыеДанные.УчетнаяПолитика КАК УчетнаяПолитика,
	|	НовыеДанные.ПлательщикЕНВД КАК ПлательщикЕНВД,
	|	НовыеДанные.ПлательщикНалогаНаПрибыль КАК ПлательщикНалогаНаПрибыль,
	|	НовыеДанные.ПрименяетсяУСН КАК ПрименяетсяУСН,
	|	НовыеДанные.ПрименяетсяУСНДоходыМинусРасходы КАК ПрименяетсяУСНДоходыМинусРасходы,
	|	НовыеДанные.ПрименяетсяПБУ18,
	|	НовыеДанные.Период КАК Период
	|ИЗ
	|	Справочник.Организации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНовыеДанные КАК НовыеДанные
	|		ПО Организации.Ссылка.ГоловнаяОрганизация = НовыеДанные.Организация
	|ГДЕ
	|	Организации.Ссылка <> Организации.ГоловнаяОрганизация
	|	И Организации.ГоловнаяОрганизация В
	|			(ВЫБРАТЬ
	|				Организации.Организация
	|			ИЗ
	|				ВТНовыеДанные КАК Организации
	|			ГДЕ
	|				Организации.Организация = Организации.Организация.ГоловнаяОрганизация)";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
	
		НаборЗаписей = РегистрыСведений.УчетнаяПолитикаОрганизаций.СоздатьНаборЗаписей();
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
		НаборЗаписей.Отбор.Период.Установить(Выборка.Период);
		ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(),Выборка);
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

//++ НЕ УТ

#Область СлужебныеПроцедурыИФункции

Процедура УчетнаяПолитикаОрганизацийПередЗаписью()

	СтруктураВременныеТаблицы = Новый Структура("МенеджерВременныхТаблиц", Новый МенеджерВременныхТаблиц);
	ДляПроведения = Новый Структура("СтруктураВременныеТаблицы", СтруктураВременныеТаблицы);
	ДополнительныеСвойства.Вставить("ДляПроведения", ДляПроведения);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период                   КАК Период,
	|	ТаблицаПередЗаписью.Организация              КАК Организация,
	|	ТаблицаПередЗаписью.УчетнаяПолитика          КАК УчетнаяПолитика
	|ПОМЕСТИТЬ УчетнаяПолитикаОрганизацийПередЗаписью
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаОрганизаций КАК ТаблицаПередЗаписью
	|//ОтборОрганизация//";
	
	Если Отбор.Организация.Использование Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
									"//ОтборОрганизация//",
									"ГДЕ
									|	ТаблицаПередЗаписью.Организация = &Организация");
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", Отбор.Организация.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СформироватьЗаданияПриИзмененииУчетнойПолитики()

	Если НЕ ДополнительныеСвойства.Свойство("ДляПроведения") 
		ИЛИ НЕ ДополнительныеСвойства.ДляПроведения.Свойство("СтруктураВременныеТаблицы") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Таблица.Период           КАК Дата,
	|	Таблица.Организация      КАК Организация
	|ИЗ
	|	(ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ) КАК Период,
	|		ТаблицаПередЗаписью.Организация КАК Организация
	|	ИЗ
	|		УчетнаяПолитикаОрганизацийПередЗаписью КАК ТаблицаПередЗаписью
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций КАК ТаблицаПриЗаписи
	|			ПО ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|		ГДЕ
	|			ТаблицаПередЗаписью.УчетнаяПолитика.СхемаУчетаСтоимостиОСвНУ = ЗНАЧЕНИЕ(Перечисление.СхемыУчетаСтоимостиОСвНУ.СФормированиемВР)
	|			И (ЕСТЬNULL(ТаблицаПриЗаписи.УчетнаяПолитика.СхемаУчетаСтоимостиОСвНУ, ЗНАЧЕНИЕ(Перечисление.СхемыУчетаСтоимостиОСвНУ.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.СхемыУчетаСтоимостиОСвНУ.СФормированиемВР)
	|				ИЛИ ТаблицаПриЗаписи.Период <> ТаблицаПередЗаписью.Период
	|				ИЛИ ТаблицаПриЗаписи.Организация ЕСТЬ NULL)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ),
	|		ТаблицаПриЗаписи.Организация
	|	ИЗ
	|		РегистрСведений.УчетнаяПолитикаОрганизаций КАК ТаблицаПриЗаписи
	|			ЛЕВОЕ СОЕДИНЕНИЕ УчетнаяПолитикаОрганизацийПередЗаписью КАК ТаблицаПередЗаписью
	|			ПО ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|		ГДЕ
	|			ТаблицаПриЗаписи.УчетнаяПолитика.СхемаУчетаСтоимостиОСвНУ = ЗНАЧЕНИЕ(Перечисление.СхемыУчетаСтоимостиОСвНУ.СФормированиемВР)
	|			И (ЕСТЬNULL(ТаблицаПередЗаписью.УчетнаяПолитика.СхемаУчетаСтоимостиОСвНУ, ЗНАЧЕНИЕ(Перечисление.СхемыУчетаСтоимостиОСвНУ.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.СхемыУчетаСтоимостиОСвНУ.СФормированиемВР)
	|				ИЛИ ТаблицаПриЗаписи.Период <> ТаблицаПередЗаписью.Период
	|				ИЛИ ТаблицаПередЗаписью.Организация ЕСТЬ NULL)
	|			//ОтборОрганизация//
	|
	|	) КАК Таблица
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ УчетнаяПолитикаОрганизацийПередЗаписью";
	
	Если Отбор.Организация.Использование Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
							"//ОтборОрганизация//", 
							"И ТаблицаПриЗаписи.Организация = &Организация");
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", Отбор.Организация.Значение);
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		УчетОСВызовСервера.СфрмироватьЗаданиеПризнаниеВНУЛизинговыхПлатежей(Выборка.Организация, Выборка.Дата, Неопределено, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

//-- НЕ УТ

#КонецЕсли