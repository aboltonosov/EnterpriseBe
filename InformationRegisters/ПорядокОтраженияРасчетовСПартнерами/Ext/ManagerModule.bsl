#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Возвращает результат выполнения анализатора отражения расчетов с партнерами и по договорам кредита и займа с настроенными счетами
//	
//	Параметры:
//		МассивОрганизаций - Массив - СправочникСсылка.Организации - отбор по организациям;
//		Период - СтандартныйПериод - период с датой начала и окончания анализа.
//	ВозвращаемоеЗначение:
//		РезультатЗапроса - список всех операций, удовлетворяющий входным параметрам с настроенными для них счетами.
//
Функция РезультатЗапросаПоНастройкамОтраженияВУчете(МассивОрганизаций, Период) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	ПараметрыАнализатора = Новый Структура("МассивОрганизаций, НачалоПериода, КонецПериода", МассивОрганизаций, Период.ДатаНачала, Период.ДатаОкончания);
	Запрос.МенеджерВременныхТаблиц = ВременнаяТаблицаДанныхПоСуществующимОперациям(ПараметрыАнализатора);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РасчетыСПартнерами.Организация КАК Организация,
	|	РасчетыСПартнерами.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	
	|	РасчетыСПартнерами.ЕстьРасчетыСКлиентами КАК ЕстьРасчетыСКлиентами,
	|	РасчетыСПартнерами.ЕстьРасчетыСКлиентамиТара КАК ЕстьРасчетыСКлиентамиТара,
	|	РасчетыСПартнерами.ЕстьРасчетыСПоставщиками КАК ЕстьРасчетыСПоставщиками,
	|	РасчетыСПартнерами.ЕстьРасчетыСПоставщикамиТара КАК ЕстьРасчетыСПоставщикамиТара,
	|	РасчетыСПартнерами.ЕстьРасчетыПоКомиссии КАК ЕстьРасчетыПоКомиссии,
	|	РасчетыСПартнерами.ЕстьРасчетыПоДепозитам КАК ЕстьРасчетыПоДепозитам,
	|	РасчетыСПартнерами.ЕстьРасчетыПоКомиссииКредитов КАК ЕстьРасчетыПоКомиссииКредитов,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовСКлиентами, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаРасчетовСКлиентами
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаРасчетовСКлиентами
	|	КОНЕЦ КАК СчетУчетаРасчетовСКлиентами,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовСКлиентами, &ПустойСчет) = &ПустойСчет 
	|				И ГруппыФинансовогоУчета.СчетУчетаРасчетовСКлиентами <> &ПустойСчет 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаРасчетовСКлиентамиПоУмолчанию,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаАвансовПолученных, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаАвансовПолученных
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаАвансовПолученных
	|	КОНЕЦ КАК СчетУчетаАвансовПолученных,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаАвансовПолученных, &ПустойСчет) = &ПустойСчет  
	|				И ГруппыФинансовогоУчета.СчетУчетаАвансовПолученных <> &ПустойСчет 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаАвансовПолученныхПоУмолчанию,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовСКлиентамиТара, &ПустойСчет) <> &ПустойСчет 
	|			ТОГДА ПорядокОтражения.СчетУчетаРасчетовСКлиентамиТара
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаРасчетовСКлиентамиТара
	|	КОНЕЦ КАК СчетУчетаРасчетовСКлиентамиТара,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовСКлиентамиТара, &ПустойСчет) = &ПустойСчет 
	|				И ГруппыФинансовогоУчета.СчетУчетаРасчетовСКлиентамиТара <> &ПустойСчет 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаРасчетовСКлиентамиТараПоУмолчанию,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовПоВознаграждению, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаРасчетовПоВознаграждению
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаРасчетовПоВознаграждению
	|	КОНЕЦ КАК СчетУчетаРасчетовПоВознаграждению,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовПоВознаграждению, &ПустойСчет) = &ПустойСчет
	|				И ГруппыФинансовогоУчета.СчетУчетаРасчетовПоВознаграждению <> &ПустойСчет 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаРасчетовПоВознаграждениюПоУмолчанию,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовСПоставщиками, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаРасчетовСПоставщиками
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаРасчетовСПоставщиками
	|	КОНЕЦ КАК СчетУчетаРасчетовСПоставщиками,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовСПоставщиками, &ПустойСчет) = &ПустойСчет
	|				И ГруппыФинансовогоУчета.СчетУчетаРасчетовСПоставщиками <> &ПустойСчет 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаРасчетовСПоставщикамиПоУмолчанию,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаАвансовВыданных, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаАвансовВыданных
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаАвансовВыданных
	|	КОНЕЦ КАК СчетУчетаАвансовВыданных,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаАвансовВыданных, &ПустойСчет) = &ПустойСчет  
	|				И ГруппыФинансовогоУчета.СчетУчетаАвансовВыданных <> &ПустойСчет 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаАвансовВыданныхПоУмолчанию,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовСПоставщикамиТара, &ПустойСчет) <> &ПустойСчет 
	|			ТОГДА ПорядокОтражения.СчетУчетаРасчетовСПоставщикамиТара
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаРасчетовСПоставщикамиТара
	|	КОНЕЦ КАК СчетУчетаРасчетовСПоставщикамиТара,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовСПоставщикамиТара, &ПустойСчет) = &ПустойСчет  
	|				И ГруппыФинансовогоУчета.СчетУчетаРасчетовСПоставщикамиТара <> &ПустойСчет 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаРасчетовСПоставщикамиТараПоУмолчанию,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовПоПретензиям, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаРасчетовПоПретензиям
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаРасчетовПоПретензиям
	|	КОНЕЦ КАК СчетУчетаРасчетовПоПретензиям,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовПоПретензиям, &ПустойСчет) = &ПустойСчет
	|				И ГруппыФинансовогоУчета.СчетУчетаРасчетовПоПретензиям <> &ПустойСчет 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаРасчетовПоПретензиямПоУмолчанию
	|
	|ИЗ
	|	ТаблицаДвижений КАК РасчетыСПартнерами
	|		ЛЕВОЕ СОЕДИНЕНИЕ 
	|			РегистрСведений.ПорядокОтраженияРасчетовСПартнерами КАК ПорядокОтражения
	|		ПО 
	|			РасчетыСПартнерами.Организация = ПорядокОтражения.Организация
	|			И РасчетыСПартнерами.ГруппаФинансовогоУчета = ПорядокОтражения.ГруппаФинансовогоУчета
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ 
	|			Справочник.ГруппыФинансовогоУчетаРасчетов КАК ГруппыФинансовогоУчета
	|		ПО
	|			РасчетыСПартнерами.ГруппаФинансовогоУчета = ГруппыФинансовогоУчета.Ссылка
	|ГДЕ
	|	НЕ РасчетыСПартнерами.ГруппаФинансовогоУчета ЕСТЬ NULL 
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПорядокОтражения.Организация,
	|	ПорядокОтражения.ГруппаФинансовогоУчета,
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	ЛОЖЬ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовСКлиентами, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаРасчетовСКлиентами
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаРасчетовСКлиентами
	|	КОНЕЦ КАК СчетУчетаРасчетовСКлиентами,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовСКлиентами, &ПустойСчет) = &ПустойСчет 
	|				И ГруппыФинансовогоУчета.СчетУчетаРасчетовСКлиентами <> &ПустойСчет 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаРасчетовСКлиентамиПоУмолчанию,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаАвансовПолученных, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаАвансовПолученных
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаАвансовПолученных
	|	КОНЕЦ КАК СчетУчетаАвансовПолученных,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаАвансовПолученных, &ПустойСчет) = &ПустойСчет  
	|				И ГруппыФинансовогоУчета.СчетУчетаАвансовПолученных <> &ПустойСчет 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаАвансовПолученныхПоУмолчанию,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовСКлиентамиТара, &ПустойСчет) <> &ПустойСчет 
	|			ТОГДА ПорядокОтражения.СчетУчетаРасчетовСКлиентамиТара
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаРасчетовСКлиентамиТара
	|	КОНЕЦ КАК СчетУчетаРасчетовСКлиентамиТара,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовСКлиентамиТара, &ПустойСчет) = &ПустойСчет 
	|				И ГруппыФинансовогоУчета.СчетУчетаРасчетовСКлиентамиТара <> &ПустойСчет 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаРасчетовСКлиентамиТараПоУмолчанию,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовПоВознаграждению, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаРасчетовПоВознаграждению
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаРасчетовПоВознаграждению
	|	КОНЕЦ КАК СчетУчетаРасчетовПоВознаграждению,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовПоВознаграждению, &ПустойСчет) = &ПустойСчет
	|				И ГруппыФинансовогоУчета.СчетУчетаРасчетовПоВознаграждению <> &ПустойСчет 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаРасчетовПоВознаграждениюПоУмолчанию,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовСПоставщиками, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаРасчетовСПоставщиками
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаРасчетовСПоставщиками
	|	КОНЕЦ КАК СчетУчетаРасчетовСПоставщиками,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовСПоставщиками, &ПустойСчет) = &ПустойСчет
	|				И ГруппыФинансовогоУчета.СчетУчетаРасчетовСПоставщиками <> &ПустойСчет 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаРасчетовСПоставщикамиПоУмолчанию,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаАвансовВыданных, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаАвансовВыданных
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаАвансовВыданных
	|	КОНЕЦ КАК СчетУчетаАвансовВыданных,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаАвансовВыданных, &ПустойСчет) = &ПустойСчет  
	|				И ГруппыФинансовогоУчета.СчетУчетаАвансовВыданных <> &ПустойСчет 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаАвансовВыданныхПоУмолчанию,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовСПоставщикамиТара, &ПустойСчет) <> &ПустойСчет 
	|			ТОГДА ПорядокОтражения.СчетУчетаРасчетовСПоставщикамиТара
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаРасчетовСПоставщикамиТара
	|	КОНЕЦ КАК СчетУчетаРасчетовСПоставщикамиТара,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовСПоставщикамиТара, &ПустойСчет) = &ПустойСчет  
	|				И ГруппыФинансовогоУчета.СчетУчетаРасчетовСПоставщикамиТара <> &ПустойСчет 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаРасчетовСПоставщикамиТараПоУмолчанию,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовПоПретензиям, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаРасчетовПоПретензиям
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаРасчетовПоПретензиям
	|	КОНЕЦ КАК СчетУчетаРасчетовПоПретензиям,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовПоПретензиям, &ПустойСчет) = &ПустойСчет
	|				И ГруппыФинансовогоУчета.СчетУчетаРасчетовПоПретензиям <> &ПустойСчет 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаРасчетовПоПретензиямПоУмолчанию
	|	
	|ИЗ
	|	РегистрСведений.ПорядокОтраженияРасчетовСПартнерами КАК ПорядокОтражения
	|		ЛЕВОЕ СОЕДИНЕНИЕ 
	|			ТаблицаДвижений КАК РасчетыСПартнерами
	|		ПО 
	|			ПорядокОтражения.Организация = РасчетыСПартнерами.Организация
	|			И ПорядокОтражения.ГруппаФинансовогоУчета = РасчетыСПартнерами.ГруппаФинансовогоУчета
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ 
	|			Справочник.ГруппыФинансовогоУчетаРасчетов КАК ГруппыФинансовогоУчета
	|		ПО
	|			ПорядокОтражения.ГруппаФинансовогоУчета = ГруппыФинансовогоУчета.Ссылка
	|		
	|ГДЕ
	|	ПорядокОтражения.Организация В(&МассивОрганизаций)
	|	И РасчетыСПартнерами.Организация ЕСТЬ NULL 
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация,
	|	ГруппаФинансовогоУчета";
	
	Запрос.УстановитьПараметр("ПустойСчет", ПланыСчетов.Хозрасчетный.ПустаяСсылка());
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса;
	
КонецФункции // РезультатЗапросаПоНастройкамОтраженияВУчете()

Функция ВременнаяТаблицаДанныхПоСуществующимОперациям(ПараметрыАнализатора)
	
	РежимАнализа = Неопределено;
	ПараметрыАнализатора.Свойство("РежимАнализа", РежимАнализа);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст =
	"
	// Есть расчеты с клиентами (в том числе по таре и по комиссии)
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Аналитика.Организация КАК Организация,
	|	ЕСТЬNULL(ДанныеРегистра.ЗаказКлиента.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка)) КАК ГруппаФинансовогоУчета,
	|	ИСТИНА КАК ЕстьРасчетыСКлиентами,
	|	ЛОЖЬ КАК ЕстьРасчетыСПоставщиками,
	|	ИспользоватьМногооборотнуюТару.Значение КАК ЕстьРасчетыСКлиентамиТара,
	|	ЛОЖЬ КАК ЕстьРасчетыСПоставщикамиТара,
	|	ДанныеРегистра.ЗаказКлиента ССЫЛКА Документ.ОтчетКомиссионера
	|		ИЛИ ДанныеРегистра.ЗаказКлиента ССЫЛКА Документ.ОтчетКомиссионераОСписании
	|		ИЛИ ДанныеРегистра.ЗаказКлиента ССЫЛКА Документ.ОтчетПоКомиссииМеждуОрганизациями
	|		ИЛИ ДанныеРегистра.ЗаказКлиента ССЫЛКА Документ.ОтчетКомитенту КАК ЕстьРасчетыПоКомиссии,
	|	ЛОЖЬ КАК ЕстьРасчетыПоДепозитам,
	|	ЛОЖЬ КАК ЕстьРасчетыПоКомиссииКредитов
	|ПОМЕСТИТЬ ТаблицаДвиженийБезГруппировки
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|		ПО ДанныеРегистра.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константа.ИспользоватьМногооборотнуюТару КАК ИспользоватьМногооборотнуюТару
	|		ПО ИСТИНА
	|ГДЕ
	|	&УсловиеДляРегистра
	|
	// Есть расчеты по депозитам и займам выданным, по оплате комиссии по кредитам и займам полученным
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Аналитика.Организация КАК Организация,
	|	ЕСТЬNULL(ДанныеРегистра.Договор.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка)) КАК ГруппаФинансовогоУчета,
	|	ЛОЖЬ КАК ЕстьРасчетыСКлиентами,
	|	ЛОЖЬ КАК ЕстьРасчетыСПоставщиками,
	|	ЛОЖЬ КАК ЕстьРасчетыСКлиентамиТара,
	|	ЛОЖЬ КАК ЕстьРасчетыСПоставщикамиТара,
	|	ЛОЖЬ КАК ЕстьРасчетыПоКомиссии,
	|	ДанныеРегистра.Договор.ХарактерДоговора <> ЗНАЧЕНИЕ(Перечисление.ХарактерДоговораКредитовИДепозитов.КредитИлиЗайм) КАК ЕстьРасчетыПоДепозитам,
	|	ДанныеРегистра.Договор.ХарактерДоговора = ЗНАЧЕНИЕ(Перечисление.ХарактерДоговораКредитовИДепозитов.КредитИлиЗайм)
	|		И ДанныеРегистра.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Комиссия) КАК ЕстьРасчетыПоКомиссииКредитов
	|ИЗ
	|	РегистрНакопления.РасчетыПоДоговорамКредитовИДепозитов КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|		ПО ДанныеРегистра.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|ГДЕ
	|	&УсловиеДляРегистра
	|	И ДанныеРегистра.ТипСуммы В (ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Комиссия),
	|								 ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Проценты))
	|
	// Есть расчеты с поставщиками (в том числе по таре и по комиссии)
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Аналитика.Организация КАК Организация,
	|	ЕСТЬNULL(ДанныеРегистра.ЗаказПоставщику.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка)) КАК ГруппаФинансовогоУчета,
	|	ЛОЖЬ КАК ЕстьРасчетыСКлиентами,
	|	ИСТИНА КАК ЕстьРасчетыСПоставщиками,
	|	ЛОЖЬ КАК ЕстьРасчетыСКлиентамиТара,
	|	ИспользоватьМногооборотнуюТару.Значение КАК ЕстьРасчетыСПоставщикамиТара,
	|	ДанныеРегистра.ЗаказПоставщику ССЫЛКА Документ.ОтчетКомитенту
	|		ИЛИ ДанныеРегистра.ЗаказПоставщику ССЫЛКА Документ.ОтчетКомиссионера
	|		ИЛИ ДанныеРегистра.ЗаказПоставщику ССЫЛКА Документ.ОтчетПоКомиссииМеждуОрганизациями
	|		ИЛИ ДанныеРегистра.ЗаказПоставщику ССЫЛКА Документ.ОтчетКомитентуОСписании КАК ЕстьРасчетыПоКомиссии,
	|	ЛОЖЬ КАК ЕстьРасчетыПоДепозитам,
	|	ЛОЖЬ КАК ЕстьРасчетыПоКомиссииКредитов
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|		ПО ДанныеРегистра.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Константа.ИспользоватьМногооборотнуюТару КАК ИспользоватьМногооборотнуюТару
	|		ПО ИСТИНА
	|ГДЕ
	|	&УсловиеДляРегистра
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьРасчетыСКлиентами) КАК ЕстьРасчетыСКлиентами,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьРасчетыСПоставщиками) КАК ЕстьРасчетыСПоставщиками,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьРасчетыСКлиентамиТара) КАК ЕстьРасчетыСКлиентамиТара,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьРасчетыСПоставщикамиТара) КАК ЕстьРасчетыСПоставщикамиТара,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьРасчетыПоКомиссии) КАК ЕстьРасчетыПоКомиссии,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьРасчетыПоДепозитам) КАК ЕстьРасчетыПоДепозитам,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьРасчетыПоКомиссииКредитов) КАК ЕстьРасчетыПоКомиссииКредитов
	|ПОМЕСТИТЬ ТаблицаДвижений
	|ИЗ ТаблицаДвиженийБезГруппировки КАК ВложенныйЗапрос
	|	
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.ГруппаФинансовогоУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|	УНИЧТОЖИТЬ ТаблицаДвиженийБезГруппировки";
	
	УсловиеВхожденияВРегистр = "ГДЕ" + Символы.ПС + Символы.Таб + "&УсловиеДляРегистра";
	ТекстСоединенияСТаблицейДокументов = "		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДокументов КАК ТаблицаДокументов
	|		ПО Аналитика.Организация = ТаблицаДокументов.Организация
	|			И ДанныеРегистра.Регистратор = ТаблицаДокументов.Документ";
	ТекстУсловийПоОрганизацииИПериоду = "ГДЕ
	|	ДанныеРегистра.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И Аналитика.Организация В (&Организации)";
	
	Если РежимАнализа = "ПоНеотраженным" Тогда
		
		Статусы = Новый Массив;
		Статусы.Добавить(Перечисления.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета);
		Статусы.Добавить(Перечисления.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете);
		РегистрыСведений.ОтражениеДокументовВРеглУчете.ДобавитьВременнуюТаблицуДокументовСОтборомПоСтатусуОтражения(
			Запрос.МенеджерВременныхТаблиц,
			ПараметрыАнализатора.МассивОрганизаций,
			Статусы);
	
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВРегистр, ТекстСоединенияСТаблицейДокументов);
	Иначе
		Запрос.УстановитьПараметр("Организации", ПараметрыАнализатора.МассивОрганизаций);
		Запрос.УстановитьПараметр("ДатаНачала", ПараметрыАнализатора.НачалоПериода);
		Запрос.УстановитьПараметр("ДатаОкончания", ПараметрыАнализатора.КонецПериода);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВРегистр, ТекстУсловийПоОрганизацииИПериоду);
	КонецЕсли;
	
	Запрос.Выполнить();
	
	Возврат Запрос.МенеджерВременныхТаблиц;
	
КонецФункции

Функция ВременнаяТаблицаДвиженийСоСчетами(ВременнаяТаблицаДвижений)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ВременнаяТаблицаДвижений;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаДвижений.Организация КАК Организация,
	|	ТаблицаДвижений.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	
	|	ТаблицаДвижений.ЕстьРасчетыСКлиентами КАК ЕстьРасчетыСКлиентами,
	|	ТаблицаДвижений.ЕстьРасчетыСКлиентамиТара КАК ЕстьРасчетыСКлиентамиТара,
	|	ТаблицаДвижений.ЕстьРасчетыСПоставщиками КАК ЕстьРасчетыСПоставщиками,
	|	ТаблицаДвижений.ЕстьРасчетыСПоставщикамиТара КАК ЕстьРасчетыСПоставщикамиТара,
	|	ТаблицаДвижений.ЕстьРасчетыПоКомиссии КАК ЕстьРасчетыПоКомиссии,
	|	ТаблицаДвижений.ЕстьРасчетыПоДепозитам КАК ЕстьРасчетыПоДепозитам,
	|	ТаблицаДвижений.ЕстьРасчетыПоКомиссииКредитов КАК ЕстьРасчетыПоКомиссииКредитов,
	|
	|	ВЫБОР КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовСКлиентами, &ПустойСчет) = &ПустойСчет
	|		ТОГДА ЕСТЬNULL(ОбщаяНастройка.СчетУчетаРасчетовСКлиентами, ЕСТЬNULL(АналитикаУчета.СчетУчетаРасчетовСКлиентами, &ПустойСчет))
	|		ИНАЧЕ ПорядокОтражения.СчетУчетаРасчетовСКлиентами КОНЕЦ КАК СчетУчетаРасчетовСКлиентами,
	|	ВЫБОР КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаАвансовПолученных, &ПустойСчет) = &ПустойСчет
	|		ТОГДА ЕСТЬNULL(ОбщаяНастройка.СчетУчетаАвансовПолученных, ЕСТЬNULL(АналитикаУчета.СчетУчетаАвансовПолученных, &ПустойСчет))
	|		ИНАЧЕ ПорядокОтражения.СчетУчетаАвансовПолученных КОНЕЦ КАК СчетУчетаАвансовПолученных,
	|	ВЫБОР КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовПоВознаграждению, &ПустойСчет) = &ПустойСчет
	|		ТОГДА ЕСТЬNULL(ОбщаяНастройка.СчетУчетаРасчетовПоВознаграждению, ЕСТЬNULL(АналитикаУчета.СчетУчетаРасчетовПоВознаграждению, &ПустойСчет))
	|		ИНАЧЕ ПорядокОтражения.СчетУчетаРасчетовПоВознаграждению КОНЕЦ КАК СчетУчетаРасчетовПоВознаграждению,
	|	ВЫБОР КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовСПоставщиками, &ПустойСчет) = &ПустойСчет
	|		ТОГДА ЕСТЬNULL(ОбщаяНастройка.СчетУчетаРасчетовСПоставщиками, ЕСТЬNULL(АналитикаУчета.СчетУчетаРасчетовСПоставщиками, &ПустойСчет))
	|		ИНАЧЕ ПорядокОтражения.СчетУчетаРасчетовСПоставщиками КОНЕЦ КАК СчетУчетаРасчетовСПоставщиками,
	|	ВЫБОР КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаАвансовВыданных, &ПустойСчет) = &ПустойСчет
	|		ТОГДА ЕСТЬNULL(ОбщаяНастройка.СчетУчетаАвансовВыданных, ЕСТЬNULL(АналитикаУчета.СчетУчетаАвансовВыданных, &ПустойСчет))
	|		ИНАЧЕ ПорядокОтражения.СчетУчетаАвансовВыданных КОНЕЦ КАК СчетУчетаАвансовВыданных,
	|	ВЫБОР КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовПоПретензиям, &ПустойСчет) = &ПустойСчет
	|		ТОГДА ЕСТЬNULL(ОбщаяНастройка.СчетУчетаРасчетовПоПретензиям, ЕСТЬNULL(АналитикаУчета.СчетУчетаРасчетовПоПретензиям, &ПустойСчет))
	|		ИНАЧЕ ПорядокОтражения.СчетУчетаРасчетовПоПретензиям КОНЕЦ КАК СчетУчетаРасчетовПоПретензиям,
	|	ВЫБОР КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовСКлиентамиТара, &ПустойСчет) = &ПустойСчет
	|		ТОГДА ЕСТЬNULL(ОбщаяНастройка.СчетУчетаРасчетовСКлиентамиТара, ЕСТЬNULL(АналитикаУчета.СчетУчетаРасчетовСКлиентамиТара, &ПустойСчет))
	|		ИНАЧЕ ПорядокОтражения.СчетУчетаРасчетовСКлиентамиТара КОНЕЦ КАК СчетУчетаРасчетовСКлиентамиТара,
	|	ВЫБОР КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовСПоставщикамиТара, &ПустойСчет) = &ПустойСчет
	|		ТОГДА ЕСТЬNULL(ОбщаяНастройка.СчетУчетаРасчетовСПоставщикамиТара, ЕСТЬNULL(АналитикаУчета.СчетУчетаРасчетовСПоставщикамиТара, &ПустойСчет))
	|		ИНАЧЕ ПорядокОтражения.СчетУчетаРасчетовСПоставщикамиТара КОНЕЦ КАК СчетУчетаРасчетовСПоставщикамиТара,
	|
	|	ЕСТЬNULL(ОбщаяНастройка.СчетУчетаРасчетовСКлиентами, ЕСТЬNULL(АналитикаУчета.СчетУчетаРасчетовСКлиентами, &ПустойСчет)) <> &ПустойСчет
	|		И ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовСКлиентами, &ПустойСчет) = &ПустойСчет
	|		ИЛИ ЕСТЬNULL(ОбщаяНастройка.СчетУчетаРасчетовСКлиентами, ЕСТЬNULL(АналитикаУчета.СчетУчетаРасчетовСКлиентами, &ПустойСчет))
	|		= ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовСКлиентами, &ПустойСчет) КАК СчетУчетаРасчетовСКлиентамиПоУмолчанию,
	|	ЕСТЬNULL(ОбщаяНастройка.СчетУчетаАвансовПолученных, ЕСТЬNULL(АналитикаУчета.СчетУчетаАвансовПолученных, &ПустойСчет)) <> &ПустойСчет
	|		И ЕСТЬNULL(ПорядокОтражения.СчетУчетаАвансовПолученных, &ПустойСчет) = &ПустойСчет
	|		ИЛИ ЕСТЬNULL(ОбщаяНастройка.СчетУчетаАвансовПолученных, ЕСТЬNULL(АналитикаУчета.СчетУчетаАвансовПолученных, &ПустойСчет))
	|		= ЕСТЬNULL(ПорядокОтражения.СчетУчетаАвансовПолученных, &ПустойСчет) КАК СчетУчетаАвансовПолученныхПоУмолчанию,
	|	ЕСТЬNULL(ОбщаяНастройка.СчетУчетаРасчетовПоВознаграждению, ЕСТЬNULL(АналитикаУчета.СчетУчетаРасчетовПоВознаграждению, &ПустойСчет)) <> &ПустойСчет
	|		И ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовПоВознаграждению, &ПустойСчет) = &ПустойСчет
	|		ИЛИ ЕСТЬNULL(ОбщаяНастройка.СчетУчетаРасчетовПоВознаграждению, ЕСТЬNULL(АналитикаУчета.СчетУчетаРасчетовПоВознаграждению, &ПустойСчет))
	|		= ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовПоВознаграждению, &ПустойСчет) КАК СчетУчетаРасчетовПоВознаграждениюПоУмолчанию,
	|	ЕСТЬNULL(ОбщаяНастройка.СчетУчетаРасчетовСПоставщиками, ЕСТЬNULL(АналитикаУчета.СчетУчетаРасчетовСПоставщиками, &ПустойСчет)) <> &ПустойСчет
	|		И ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовСПоставщиками, &ПустойСчет) = &ПустойСчет
	|		ИЛИ ЕСТЬNULL(ОбщаяНастройка.СчетУчетаРасчетовСПоставщиками, ЕСТЬNULL(АналитикаУчета.СчетУчетаРасчетовСПоставщиками, &ПустойСчет))
	|		= ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовСПоставщиками, &ПустойСчет) КАК СчетУчетаРасчетовСПоставщикамиПоУмолчанию,
	|	ЕСТЬNULL(ОбщаяНастройка.СчетУчетаАвансовВыданных, ЕСТЬNULL(АналитикаУчета.СчетУчетаАвансовВыданных, &ПустойСчет)) <> &ПустойСчет
	|		И ЕСТЬNULL(ПорядокОтражения.СчетУчетаАвансовВыданных, &ПустойСчет) = &ПустойСчет
	|		ИЛИ ЕСТЬNULL(ОбщаяНастройка.СчетУчетаАвансовВыданных, ЕСТЬNULL(АналитикаУчета.СчетУчетаАвансовВыданных, &ПустойСчет))
	|		= ЕСТЬNULL(ПорядокОтражения.СчетУчетаАвансовВыданных, &ПустойСчет) КАК СчетУчетаАвансовВыданныхПоУмолчанию,
	|	ЕСТЬNULL(ОбщаяНастройка.СчетУчетаРасчетовПоПретензиям, ЕСТЬNULL(АналитикаУчета.СчетУчетаРасчетовПоПретензиям, &ПустойСчет)) <> &ПустойСчет
	|		И ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовПоПретензиям, &ПустойСчет) = &ПустойСчет
	|		ИЛИ ЕСТЬNULL(ОбщаяНастройка.СчетУчетаРасчетовПоПретензиям, ЕСТЬNULL(АналитикаУчета.СчетУчетаРасчетовПоПретензиям, &ПустойСчет))
	|		= ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовПоПретензиям, &ПустойСчет) КАК СчетУчетаРасчетовПоПретензиямПоУмолчанию,
	|	ЕСТЬNULL(ОбщаяНастройка.СчетУчетаРасчетовСКлиентамиТара, ЕСТЬNULL(АналитикаУчета.СчетУчетаРасчетовСКлиентамиТара, &ПустойСчет)) <> &ПустойСчет
	|		И ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовСКлиентамиТара, &ПустойСчет) = &ПустойСчет
	|		ИЛИ ЕСТЬNULL(ОбщаяНастройка.СчетУчетаРасчетовСКлиентамиТара, ЕСТЬNULL(АналитикаУчета.СчетУчетаРасчетовСКлиентамиТара, &ПустойСчет))
	|		= ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовСКлиентамиТара, &ПустойСчет) КАК СчетУчетаРасчетовСКлиентамиТараПоУмолчанию,
	|	ЕСТЬNULL(ОбщаяНастройка.СчетУчетаРасчетовСПоставщикамиТара, ЕСТЬNULL(АналитикаУчета.СчетУчетаРасчетовСПоставщикамиТара, &ПустойСчет)) <> &ПустойСчет
	|		И ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовСПоставщикамиТара, &ПустойСчет) = &ПустойСчет
	|		ИЛИ ЕСТЬNULL(ОбщаяНастройка.СчетУчетаРасчетовСПоставщикамиТара, ЕСТЬNULL(АналитикаУчета.СчетУчетаРасчетовСПоставщикамиТара, &ПустойСчет))
	|		= ЕСТЬNULL(ПорядокОтражения.СчетУчетаРасчетовСПоставщикамиТара, &ПустойСчет) КАК СчетУчетаРасчетовСПоставщикамиТараПоУмолчанию
	|
	|ПОМЕСТИТЬ ТаблицаДвиженийСоСчетами
	|ИЗ
	|	ТаблицаДвижений КАК ТаблицаДвижений
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияРасчетовСПартнерами КАК ПорядокОтражения
	|		ПО ТаблицаДвижений.Организация = ПорядокОтражения.Организация
	|			И ТаблицаДвижений.ГруппаФинансовогоУчета = ПорядокОтражения.ГруппаФинансовогоУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыФинансовогоУчетаРасчетов КАК АналитикаУчета
	|		ПО ТаблицаДвижений.ГруппаФинансовогоУчета = АналитикаУчета.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияРасчетовСПартнерами КАК ОбщаяНастройка
	|		ПО ОбщаяНастройка.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			И ТаблицаДвижений.ГруппаФинансовогоУчета = ОбщаяНастройка.ГруппаФинансовогоУчета
	|			И ТаблицаДвижений.ГруппаФинансовогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаРасчетов.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("ПустойСчет", ПланыСчетов.Хозрасчетный.ПустаяСсылка());
	Запрос.Выполнить();
	
	Возврат Запрос.МенеджерВременныхТаблиц;
	
КонецФункции

// Выполняет анализ всех неотраженных операций отражения расчетов с партнерами и по договорам кредита и займа,
//	ищет для них ненастроенные счета учета и заносит эти данные в регистр ненастроенных счетов учета.
//	
//	Параметры:
//		ПараметрыАнализатора - Структура, содержащая следующие значения:
//			* МассивОрганизаций - Массив - СправочникСсылка.Организации - отбор по организациям, операции которых будут анализироваться;
//			* РежимАнализа - Строка - строка вида "ПоНеотраженным" -
//					необходимо для того, чтобы анализатор работал только по документам, неотраженным в регл. учете.
//
Процедура ЗаполнитьРегистрТребующихНастройкиНаОснованииАнализаНеотраженныхОпераций(ПараметрыАнализатора) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВременнаяТаблицаДвижений = ВременнаяТаблицаДанныхПоСуществующимОперациям(ПараметрыАнализатора);
	ВременнаяТаблицаДвиженийСоСчетами = ВременнаяТаблицаДвиженийСоСчетами(ВременнаяТаблицаДвижений);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ВременнаяТаблицаДвиженийСоСчетами;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДвиженийСоСчетами.Организация,
	|	ТаблицаДвиженийСоСчетами.ГруппаФинансовогоУчета КАК АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыПоВознаграждению) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ТаблицаДвиженийСоСчетами
	|ГДЕ
	|	ТаблицаДвиженийСоСчетами.ЕстьРасчетыПоКомиссии
	|	И ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовПоВознаграждению = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДвиженийСоСчетами.Организация,
	|	ТаблицаДвиженийСоСчетами.ГруппаФинансовогоУчета КАК АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщиками) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ТаблицаДвиженийСоСчетами
	|ГДЕ
	|	(ТаблицаДвиженийСоСчетами.ЕстьРасчетыСПоставщиками ИЛИ ТаблицаДвиженийСоСчетами.ЕстьРасчетыПоКомиссииКредитов)
	|	И ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовСПоставщиками = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДвиженийСоСчетами.Организация,
	|	ТаблицаДвиженийСоСчетами.ГруппаФинансовогоУчета КАК АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АвансыВыданные) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ТаблицаДвиженийСоСчетами
	|ГДЕ
	|	ТаблицаДвиженийСоСчетами.ЕстьРасчетыСПоставщиками
	|	И ТаблицаДвиженийСоСчетами.СчетУчетаАвансовВыданных = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДвиженийСоСчетами.Организация,
	|	ТаблицаДвиженийСоСчетами.ГруппаФинансовогоУчета КАК АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыПоПретензиям) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ТаблицаДвиженийСоСчетами
	|ГДЕ
	|	ТаблицаДвиженийСоСчетами.ЕстьРасчетыСПоставщиками
	|	И ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовПоПретензиям = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДвиженийСоСчетами.Организация,
	|	ТаблицаДвиженийСоСчетами.ГруппаФинансовогоУчета КАК АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСКлиентами) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ТаблицаДвиженийСоСчетами
	|ГДЕ
	|	(ТаблицаДвиженийСоСчетами.ЕстьРасчетыСКлиентами ИЛИ ТаблицаДвиженийСоСчетами.ЕстьРасчетыПоДепозитам)
	|	И ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовСКлиентами = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДвиженийСоСчетами.Организация,
	|	ТаблицаДвиженийСоСчетами.ГруппаФинансовогоУчета КАК АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.АвансыПолученные) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ТаблицаДвиженийСоСчетами
	|ГДЕ
	|	ТаблицаДвиженийСоСчетами.ЕстьРасчетыСКлиентами
	|	И ТаблицаДвиженийСоСчетами.СчетУчетаАвансовПолученных = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДвиженийСоСчетами.Организация,
	|	ТаблицаДвиженийСоСчетами.ГруппаФинансовогоУчета КАК АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСПоставщикамиТара) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ТаблицаДвиженийСоСчетами
	|ГДЕ
	|	ТаблицаДвиженийСоСчетами.ЕстьРасчетыСПоставщикамиТара
	|	И ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовСПоставщикамиТара = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДвиженийСоСчетами.Организация,
	|	ТаблицаДвиженийСоСчетами.ГруппаФинансовогоУчета КАК АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РасчетыСКлиентамиТара) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ТаблицаДвиженийСоСчетами
	|ГДЕ
	|	ТаблицаДвиженийСоСчетами.ЕстьРасчетыСКлиентамиТара
	|	И ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовСКлиентамиТара = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураВидовСчетовИРезультатаЗапроса = Новый Соответствие;
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.РасчетыПоВознаграждению, МассивРезультатов.Получить(0));
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.РасчетыСПоставщиками, МассивРезультатов.Получить(1));
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.АвансыВыданные, МассивРезультатов.Получить(2));
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.РасчетыПоПретензиям, МассивРезультатов.Получить(3));
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.РасчетыСКлиентами, МассивРезультатов.Получить(4));
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.АвансыПолученные, МассивРезультатов.Получить(5));
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.РасчетыСПоставщикамиТара, МассивРезультатов.Получить(6));
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.РасчетыСКлиентамиТара, МассивРезультатов.Получить(7));
	
	ИмяСобытия = НСтр("ru = 'Анализ порядка отражения расчетов с партнерами, требующего настройки'");
	РегистрыСведений.СчетаРеглУчетаТребующиеНастройки.ЗаполнитьЗаписиРегистраПоВидамСчетов(СтруктураВидовСчетовИРезультатаЗапроса, ИмяСобытия);
	
КонецПроцедуры

// Выполняет анализ операций отражения расчетов с партнерами и по договорам кредита и займа,
//	в зависимости от параметров и дополняет исходную таблицу операций новыми строками.
//	
//	Параметры:
//		ПараметрыАнализа - Структура - содержит настройки анализатора со следующими свойствами:
//			* РежимАнализа - Строка - принимает значения вида "ПоНеотраженным" и "ЗаПериод". Причем если не задана, значение по умолчанию "ЗаПериод".
//			* КонецПериода - Дата - по эту дату анализируются операции, причем если режим анализа "ПоНеотраженным" не актуальна;
//			* НачалоПериода - Дата - начиная с этой даты анализируются операции, причем если режим анализа "ПоНеотраженным" не актуальна;
//			* МассивОрганизаций - Массив - СправочникСсылка.Организации - отбор по организациям, операции которых будут анализироваться;
//		АдресХранилища - Строка - адрес сохранения результата работы метода (ТаблицаЗначений).
//
Процедура ДополнитьТаблицуНаОснованииАнализаОпераций(ПараметрыАнализа, АдресХранилища) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВременнаяТаблицаДвижений = ВременнаяТаблицаДанныхПоСуществующимОперациям(ПараметрыАнализа);
	 
	ВременнаяТаблицаДвиженийСоСчетами = ВременнаяТаблицаДвиженийСоСчетами(ВременнаяТаблицаДвижений);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Таблица", ПараметрыАнализа.ЗаполняемаяТаблица);
	Запрос.МенеджерВременныхТаблиц = ВременнаяТаблицаДвиженийСоСчетами;
	 
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТекущаяТаблицаСчетовУчета.Организация,
	|	ТекущаяТаблицаСчетовУчета.ГруппаФинансовогоУчета,
	|
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСКлиентами,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаАвансовПолученных,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовПоВознаграждению,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСПоставщиками,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаАвансовВыданных,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовПоПретензиям,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСКлиентамиТара,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСПоставщикамиТара,
	|
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСКлиентамиПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаАвансовПолученныхПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовПоВознаграждениюПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСПоставщикамиПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаАвансовВыданныхПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовПоПретензиямПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСКлиентамиТараПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСПоставщикамиТараПоУмолчанию,
	|
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаРасчетовСКлиентами,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаАвансовПолученных,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаРасчетовПоВознаграждению,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаРасчетовСПоставщиками,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаАвансовВыданных,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаРасчетовПоПретензиям,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаРасчетовСКлиентамиТара,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаРасчетовСПоставщикамиТара,
	|
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройка,
	|	ТекущаяТаблицаСчетовУчета.ИзмененныеДанные
	|
	|ПОМЕСТИТЬ ТекущаяТаблицаСчетовУчета
	|ИЗ
	|	&Таблица КАК ТекущаяТаблицаСчетовУчета;
	|
	|///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДвиженийСоСчетами.Организация,
	|	ТаблицаДвиженийСоСчетами.ГруппаФинансовогоУчета,
	|
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСКлиентами, ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовСКлиентами) КАК СчетУчетаРасчетовСКлиентами,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаАвансовПолученных, ТаблицаДвиженийСоСчетами.СчетУчетаАвансовПолученных) КАК СчетУчетаАвансовПолученных,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовПоВознаграждению, ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовПоВознаграждению) КАК СчетУчетаРасчетовПоВознаграждению,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСПоставщиками, ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовСПоставщиками) КАК СчетУчетаРасчетовСПоставщиками,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаАвансовВыданных, ТаблицаДвиженийСоСчетами.СчетУчетаАвансовВыданных) КАК СчетУчетаАвансовВыданных,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовПоПретензиям, ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовПоПретензиям) КАК СчетУчетаРасчетовПоПретензиям,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСКлиентамиТара, ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовСКлиентамиТара) КАК СчетУчетаРасчетовСКлиентамиТара,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСПоставщикамиТара, ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовСПоставщикамиТара) КАК СчетУчетаРасчетовСПоставщикамиТара,
	|
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСКлиентамиПоУмолчанию, ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовСКлиентамиПоУмолчанию) КАК СчетУчетаРасчетовСКлиентамиПоУмолчанию,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаАвансовПолученныхПоУмолчанию, ТаблицаДвиженийСоСчетами.СчетУчетаАвансовПолученныхПоУмолчанию) КАК СчетУчетаАвансовПолученныхПоУмолчанию,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовПоВознаграждениюПоУмолчанию, ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовПоВознаграждениюПоУмолчанию) КАК СчетУчетаРасчетовПоВознаграждениюПоУмолчанию,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСПоставщикамиПоУмолчанию, ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовСПоставщикамиПоУмолчанию) КАК СчетУчетаРасчетовСПоставщикамиПоУмолчанию,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаАвансовВыданныхПоУмолчанию, ТаблицаДвиженийСоСчетами.СчетУчетаАвансовВыданныхПоУмолчанию) КАК СчетУчетаАвансовВыданныхПоУмолчанию,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовПоПретензиямПоУмолчанию, ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовПоПретензиямПоУмолчанию) КАК СчетУчетаРасчетовПоПретензиямПоУмолчанию,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСКлиентамиТараПоУмолчанию, ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовСКлиентамиТараПоУмолчанию) КАК СчетУчетаРасчетовСКлиентамиТараПоУмолчанию,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСПоставщикамиТараПоУмолчанию, ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовСПоставщикамиТараПоУмолчанию) КАК СчетУчетаРасчетовСПоставщикамиТараПоУмолчанию,
	|	
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСКлиентами, ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовСКлиентами) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И (ТаблицаДвиженийСоСчетами.ЕстьРасчетыСКлиентами ИЛИ ТаблицаДвиженийСоСчетами.ЕстьРасчетыПоДепозитам) КАК ТребуетсяНастройкаСчетУчетаРасчетовСКлиентами,	
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаАвансовПолученных, ТаблицаДвиженийСоСчетами.СчетУчетаАвансовПолученных) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ТаблицаДвиженийСоСчетами.ЕстьРасчетыСКлиентами КАК ТребуетсяНастройкаСчетУчетаАвансовПолученных,	
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовПоВознаграждению, ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовПоВознаграждению) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ТаблицаДвиженийСоСчетами.ЕстьРасчетыПоКомиссии КАК ТребуетсяНастройкаСчетУчетаРасчетовПоВознаграждению,	
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСПоставщиками, ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовСПоставщиками) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И (ТаблицаДвиженийСоСчетами.ЕстьРасчетыСПоставщиками ИЛИ ТаблицаДвиженийСоСчетами.ЕстьРасчетыПоКомиссииКредитов) КАК ТребуетсяНастройкаСчетУчетаРасчетовСПоставщиками,	
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаАвансовВыданных, ТаблицаДвиженийСоСчетами.СчетУчетаАвансовВыданных) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ТаблицаДвиженийСоСчетами.ЕстьРасчетыСПоставщиками КАК ТребуетсяНастройкаСчетУчетаАвансовВыданных,	
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовПоПретензиям, ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовПоПретензиям) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ТаблицаДвиженийСоСчетами.ЕстьРасчетыСПоставщиками КАК ТребуетсяНастройкаСчетУчетаРасчетовПоПретензиям,	
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСКлиентамиТара, ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовСКлиентамиТара) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ТаблицаДвиженийСоСчетами.ЕстьРасчетыСКлиентамиТара КАК ТребуетсяНастройкаСчетУчетаРасчетовСКлиентамиТара,	
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСПоставщикамиТара, ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовСПоставщикамиТара) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ТаблицаДвиженийСоСчетами.ЕстьРасчетыСПоставщикамиТара КАК ТребуетсяНастройкаСчетУчетаРасчетовСПоставщикамиТара,
	|
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСКлиентами, ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовСКлиентами)
	|		 = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) И ТаблицаДвиженийСоСчетами.ЕстьРасчетыСКлиентами ИЛИ
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСКлиентами, ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовСКлиентами)
	|		 = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) И ТаблицаДвиженийСоСчетами.ЕстьРасчетыПоДепозитам ИЛИ
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаАвансовПолученных, ТаблицаДвиженийСоСчетами.СчетУчетаАвансовПолученных)
	|		 = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) И ТаблицаДвиженийСоСчетами.ЕстьРасчетыСКлиентами ИЛИ
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовПоВознаграждению, ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовПоВознаграждению)
	|		 = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) И ТаблицаДвиженийСоСчетами.ЕстьРасчетыПоКомиссии ИЛИ
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСПоставщиками, ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовСПоставщиками)
	|		 = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) И ТаблицаДвиженийСоСчетами.ЕстьРасчетыСПоставщиками ИЛИ
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСПоставщиками, ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовСПоставщиками)
	|		 = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) И ТаблицаДвиженийСоСчетами.ЕстьРасчетыПоКомиссииКредитов ИЛИ
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаАвансовВыданных, ТаблицаДвиженийСоСчетами.СчетУчетаАвансовВыданных)
	|		 = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) И ТаблицаДвиженийСоСчетами.ЕстьРасчетыСПоставщиками ИЛИ	
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовПоПретензиям, ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовПоПретензиям)
	|		 = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) И ТаблицаДвиженийСоСчетами.ЕстьРасчетыСПоставщиками ИЛИ	
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСКлиентамиТара, ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовСКлиентамиТара)
	|		 = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) И ТаблицаДвиженийСоСчетами.ЕстьРасчетыСКлиентамиТара ИЛИ	
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСПоставщикамиТара, ТаблицаДвиженийСоСчетами.СчетУчетаРасчетовСПоставщикамиТара)
	|		 = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) И ТаблицаДвиженийСоСчетами.ЕстьРасчетыСПоставщикамиТара КАК ТребуетсяНастройка,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.ИзмененныеДанные, ""СчетУчетаРасчетовСКлиентами, СчетУчетаАвансовПолученных, СчетУчетаРасчетовПоВознаграждению, СчетУчетаРасчетовСПоставщиками, СчетУчетаАвансовВыданных, СчетУчетаРасчетовПоПретензиям, СчетУчетаРасчетовСКлиентамиТара, СчетУчетаРасчетовСПоставщикамиТара"") КАК ИзмененныеДанные
	|
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ТаблицаДвиженийСоСчетами
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ТекущаяТаблицаСчетовУчета КАК ТекущаяТаблицаСчетовУчета
	|	ПО
	|		ТаблицаДвиженийСоСчетами.Организация = ТекущаяТаблицаСчетовУчета.Организация
	|		И ТаблицаДвиженийСоСчетами.ГруппаФинансовогоУчета = ТекущаяТаблицаСчетовУчета.ГруппаФинансовогоУчета
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТекущаяТаблицаСчетовУчета.Организация,
	|	ТекущаяТаблицаСчетовУчета.ГруппаФинансовогоУчета,
	|
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСКлиентами,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаАвансовПолученных,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовПоВознаграждению,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСПоставщиками,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаАвансовВыданных,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовПоПретензиям,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСКлиентамиТара,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСПоставщикамиТара,
	|
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСКлиентамиПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаАвансовПолученныхПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовПоВознаграждениюПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСПоставщикамиПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаАвансовВыданныхПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовПоПретензиямПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСКлиентамиТараПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаРасчетовСПоставщикамиТараПоУмолчанию,
	|
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаРасчетовСКлиентами,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаАвансовПолученных,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаРасчетовПоВознаграждению,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаРасчетовСПоставщиками,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаАвансовВыданных,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаРасчетовПоПретензиям,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаРасчетовСКлиентамиТара,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаРасчетовСПоставщикамиТара,
	|
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройка,
	|	ТекущаяТаблицаСчетовУчета.ИзмененныеДанные
	|
	|ИЗ
	|	ТекущаяТаблицаСчетовУчета КАК ТекущаяТаблицаСчетовУчета
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ТаблицаДвиженийСоСчетами КАК ТаблицаДвиженийСоСчетами
	|	ПО
	|		ТекущаяТаблицаСчетовУчета.Организация = ТаблицаДвиженийСоСчетами.Организация
	|		И ТекущаяТаблицаСчетовУчета.ГруппаФинансовогоУчета = ТаблицаДвиженийСоСчетами.ГруппаФинансовогоУчета
	|ГДЕ
	|	ТаблицаДвиженийСоСчетами.Организация ЕСТЬ NULL";
	
	Таблица = Запрос.Выполнить().Выгрузить();
	
	ПоместитьВоВременноеХранилище(Новый Структура("СчетаУчетаРасчетовСПартнерами", Таблица), АдресХранилища);
	
КонецПроцедуры

Процедура НайтиДокументыСоответствующиеНастройкам(ТаблицаНастроек, Дата, ТаблицаДокументов) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ИсходнаяТаблица.Организация КАК Организация,
	|	ИсходнаяТаблица.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета
	|
	|ПОМЕСТИТЬ ТаблицаНастроек
	|ИЗ
	|	&ИсходнаяТаблица КАК ИсходнаяТаблица
	|ГДЕ
	|	ИсходнаяТаблица.ИзмененныеДанные <> """"
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ГруппаФинансовогоУчета
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Документ,
	|	ОтражениеДокументовВРеглУчете.ДатаОтражения КАК ДатаОтражения,
	|	ОтражениеДокументовВРеглУчете.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами.Обороты(, &ДатаОкончания, Регистратор,
	|		(АналитикаУчетаПоПартнерам.Организация, ЗаказКлиента.ГруппаФинансовогоУчета) В (
	|			ВЫБРАТЬ
	|				ТаблицаНастроек.Организация,
	|				ТаблицаНастроек.ГруппаФинансовогоУчета
	|			ИЗ
	|				ТаблицаНастроек КАК ТаблицаНастроек
	|		)
	|	) КАК ДанныеРегистра
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
	|	ПО
	|		ДанныеРегистра.Регистратор = ОтражениеДокументовВРеглУчете.Регистратор
	|		И ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Документ,
	|	ОтражениеДокументовВРеглУчете.ДатаОтражения КАК ДатаОтражения,
	|	ОтражениеДокументовВРеглУчете.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками.Обороты(, &ДатаОкончания, Регистратор,
	|		(АналитикаУчетаПоПартнерам.Организация, ЗаказПоставщику.ГруппаФинансовогоУчета) В (
	|			ВЫБРАТЬ
	|				ТаблицаНастроек.Организация,
	|				ТаблицаНастроек.ГруппаФинансовогоУчета
	|			ИЗ
	|				ТаблицаНастроек КАК ТаблицаНастроек
	|		)
	|	) КАК ДанныеРегистра
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
	|	ПО
	|		ДанныеРегистра.Регистратор = ОтражениеДокументовВРеглУчете.Регистратор
	|		И ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Документ
	|;");
	ИсходнаяТаблица = ТаблицаНастроек.Выгрузить(, "Организация,ГруппаФинансовогоУчета, ИзмененныеДанные");
	Запрос.УстановитьПараметр("ИсходнаяТаблица", ИсходнаяТаблица);
	Запрос.УстановитьПараметр("ДатаОкончания", Дата);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТаблицаДокументов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли