#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	Если НЕ ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период,
	|	ТаблицаПередЗаписью.ОбъектЭксплуатации,
	|	ТаблицаПередЗаписью.ПоказательНаработки,
	|	ТаблицаПередЗаписью.Значение,
	|	ТаблицаПередЗаписью.СреднесуточноеЗначение
	|ПОМЕСТИТЬ НаработкиОбъектовЭксплуатацииПередЗаписью
	|ИЗ
	|	РегистрСведений.НаработкиОбъектовЭксплуатации КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор
	|	И ТаблицаПередЗаписью.Активность
	|	И ТаблицаПередЗаписью.ОбъектЭксплуатации ССЫЛКА Справочник.ОбъектыЭксплуатации";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	МИНИМУМ(Таблица.Период)  КАК Период,
	|	Таблица.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ НаработкиОбъектовЭксплуатацииИзменениеБезОрганизации
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПередЗаписью.Период              КАК Период,
	|		ТаблицаПередЗаписью.ОбъектЭксплуатации  КАК ОсновноеСредство
	|	ИЗ
	|		НаработкиОбъектовЭксплуатацииПередЗаписью КАК ТаблицаПередЗаписью
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НаработкиОбъектовЭксплуатации КАК ТаблицаПриЗаписи
	|			ПО ТаблицаПриЗаписи.ОбъектЭксплуатации = ТаблицаПередЗаписью.ОбъектЭксплуатации
	|				И ТаблицаПриЗаписи.ПоказательНаработки = ТаблицаПередЗаписью.ПоказательНаработки
	|				И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|		ГДЕ
	|			(НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ)
	|				ИЛИ ТаблицаПриЗаписи.Значение <> ТаблицаПередЗаписью.Значение
	|				ИЛИ ТаблицаПриЗаписи.СреднесуточноеЗначение <> ТаблицаПередЗаписью.СреднесуточноеЗначение
	|				ИЛИ ТаблицаПриЗаписи.Период ЕСТЬ NULL)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.Период,
	|		ТаблицаПриЗаписи.ОбъектЭксплуатации
	|	ИЗ
	|		РегистрСведений.НаработкиОбъектовЭксплуатации КАК ТаблицаПриЗаписи
	|			ЛЕВОЕ СОЕДИНЕНИЕ НаработкиОбъектовЭксплуатацииПередЗаписью КАК ТаблицаПередЗаписью
	|			ПО ТаблицаПриЗаписи.ОбъектЭксплуатации = ТаблицаПередЗаписью.ОбъектЭксплуатации
	|				И ТаблицаПриЗаписи.ПоказательНаработки = ТаблицаПередЗаписью.ПоказательНаработки
	|		ГДЕ
	|			(НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ)
	|				ИЛИ ТаблицаПриЗаписи.Значение <> ТаблицаПередЗаписью.Значение
	|				ИЛИ ТаблицаПриЗаписи.СреднесуточноеЗначение <> ТаблицаПередЗаписью.СреднесуточноеЗначение
	|				ИЛИ ТаблицаПередЗаписью.Период ЕСТЬ NULL)
	|			И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|
	|	) КАК Таблица
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.ОсновноеСредство
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ПервоначальныеСведения.Период) КАК ПервоначальныеСведенияПериод,
	|	ТаблицаИзменение.Период КАК Период,
	|	ТаблицаИзменение.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ ПервоначальныеСведенияПоследнее
	|ИЗ
	|	НаработкиОбъектовЭксплуатацииИзменениеБезОрганизации КАК ТаблицаИзменение
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет КАК ПервоначальныеСведения
	|		ПО (ПервоначальныеСведения.ОсновноеСредство = ТаблицаИзменение.ОсновноеСредство)
	|			И (ПервоначальныеСведения.Период <= ТаблицаИзменение.Период)
	|			И (ПервоначальныеСведения.СпособНачисленияАмортизации В (
	|					ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции),
	|					ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПоЕНАОФНа1000кмПробега)))
	|		
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменение.ОсновноеСредство,
	|	ТаблицаИзменение.Период
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаИзменение.Период КАК Период,
	|	ТаблицаИзменение.ОсновноеСредство КАК ОсновноеСредство,
	|	ПервоначальныеСведения.Организация КАК Организация,
	|	&Регистратор КАК Документ
	|ПОМЕСТИТЬ НаработкиОбъектовЭксплуатацииИзменение
	|ИЗ
	|	НаработкиОбъектовЭксплуатацииИзменениеБезОрганизации КАК ТаблицаИзменение
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведенияПоследнее КАК ПервоначальныеСведенияПоследнее
	|		ПО (ТаблицаИзменение.ОсновноеСредство = ПервоначальныеСведенияПоследнее.ОсновноеСредство)
	|			И (ТаблицаИзменение.Период = ПервоначальныеСведенияПоследнее.Период)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет КАК ПервоначальныеСведения
	|		ПО (ПервоначальныеСведения.ОсновноеСредство = ПервоначальныеСведенияПоследнее.ОсновноеСредство)
	|			И (ПервоначальныеСведения.Период = ПервоначальныеСведенияПоследнее.ПервоначальныеСведенияПериод)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НаработкиОбъектовЭксплуатацииИзменениеБезОрганизации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НаработкиОбъектовЭксплуатацииПередЗаписью";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Выборка = Запрос.ВыполнитьПакет()[2].Выбрать();
	Выборка.Следующий();
	
	СтруктураВременныеТаблицы.Вставить("НаработкиОбъектовЭксплуатацииИзменение", Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
