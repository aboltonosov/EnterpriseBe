#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрСведений.СпособыОтраженияРасходовПоИмущественнымНалогам";
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаРегистра.Регистратор
	|ИЗ
	|	РегистрСведений.СпособыОтраженияРасходовПоИмущественнымНалогам КАК ТаблицаРегистра
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РегистрацияТранспортныхСредств.ОтражениеРасходов КАК ТЧРегистрацияТранспортныхСредств
	|		ПО (ТЧРегистрацияТранспортныхСредств.Ссылка = ТаблицаРегистра.СпособОтраженияРасходов)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РегистрацияПорядкаНалогообложенияПоНалогуНаИмущество.ОтражениеРасходов КАК ТЧРегистрацияПорядкаНалогообложенияПоНалогуНаИмущество
	|		ПО (ТЧРегистрацияПорядкаНалогообложенияПоНалогуНаИмущество.Ссылка = ТаблицаРегистра.СпособОтраженияРасходов)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РегистрацияЗемельныхУчастков.ОтражениеРасходов КАК ТЧРегистрацияЗемельныхУчастков
	|		ПО (ТЧРегистрацияЗемельныхУчастков.Ссылка = ТаблицаРегистра.СпособОтраженияРасходов)
	|ГДЕ
	|	ТаблицаРегистра.СпособОтраженияРасходовЗаданДокументом
	|
	|	И (ТаблицаРегистра.Регистратор ССЫЛКА Документ.РегистрацияПорядкаНалогообложенияПоНалогуНаИмущество
	|			ИЛИ ТаблицаРегистра.Регистратор ССЫЛКА Документ.РегистрацияТранспортныхСредств
	|			ИЛИ ТаблицаРегистра.Регистратор ССЫЛКА Документ.РегистрацияЗемельныхУчастков)
	|
	|	И (ТаблицаРегистра.СпособОтраженияРасходов ССЫЛКА Документ.РегистрацияТранспортныхСредств
	|				И ТЧРегистрацияТранспортныхСредств.Ссылка ЕСТЬ NULL
	|			ИЛИ ТаблицаРегистра.СпособОтраженияРасходов ССЫЛКА Документ.РегистрацияПорядкаНалогообложенияПоНалогуНаИмущество
	|				И ТЧРегистрацияПорядкаНалогообложенияПоНалогуНаИмущество.Ссылка ЕСТЬ NULL
	|			ИЛИ ТаблицаРегистра.СпособОтраженияРасходов ССЫЛКА Документ.РегистрацияЗемельныхУчастков
	|				И ТЧРегистрацияЗемельныхУчастков.Ссылка ЕСТЬ NULL)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(Параметры, Регистраторы, ПолноеИмяРегистра);
		
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Регистраторы = Новый Массив;
	Регистраторы.Добавить("Документ.РегистрацияПорядкаНалогообложенияПоНалогуНаИмущество");
	Регистраторы.Добавить("Документ.РегистрацияТранспортныхСредств");
	Регистраторы.Добавить("Документ.РегистрацияЗемельныхУчастков");
	
	ОбработкаЗавершена = ОбновлениеИнформационнойБазыУТ.ПерезаписатьДвиженияИзОчереди(
		Регистраторы,
	    "РегистрСведений.СпособыОтраженияРасходовПоИмущественнымНалогам",
	    Параметры.Очередь);
	
	Параметры.ОбработкаЗавершена = ОбработкаЗавершена;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли