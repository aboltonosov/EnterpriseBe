#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Процедура выполняет первоначальное заполнение сведений.
Процедура НачальноеЗаполнение() Экспорт
	
	// 4D:ERP для Беларуси, Яна, 21.08.2017 11:11:36 
	// Регистр сведений "Страховые взносы", №15103
	// Результат тестирования конфигурации, №15769 
	// {
	ТаблицаТарифов = ТаблицаТарифовСтраховыхВзносов();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыТарифовСтраховыхВзносов.Ссылка
	|ИЗ
	|	Справочник.ВидыТарифовСтраховыхВзносов КАК ВидыТарифовСтраховыхВзносов
	|ГДЕ
	|	НЕ ВидыТарифовСтраховыхВзносов.ПометкаУдаления";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбновитьТарифСтраховыхВзносов(Выборка.Ссылка, ТаблицаТарифов);
	КонецЦикла;
	// }
	// 4D
	
КонецПроцедуры

// Процедура выполняет полную перезапись сведений о тарифах.
Процедура ЗаполнитьТарифы() Экспорт

	НаборЗаписей = РегистрыСведений.ТарифыСтраховыхВзносов.СоздатьНаборЗаписей();
	НаборЗаписей.ДополнительныеСвойства.Вставить("ЗаписьОбщихДанных");
	НаборЗаписей.Загрузить(ТаблицаТарифовСтраховыхВзносов());
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Переписывает ставки для указанного тарифа.
//
// Параметры: 
// 	Тариф - СправочникСсылка.ВидыТарифовСтраховыхВзносов
//  ТаблицаТарифов - ТаблицаЗначений - установленные Законом ставки по всем тарифам
//
Процедура ОбновитьТарифСтраховыхВзносов(Тариф, ТаблицаТарифов = Неопределено) Экспорт

	Если ТаблицаТарифов = Неопределено Тогда
		ТаблицаТарифов = ТаблицаТарифовСтраховыхВзносов();
		ТаблицаТарифов.Индексы.Добавить("ВидТарифа");
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ТарифыСтраховыхВзносов.СоздатьНаборЗаписей();
	НаборЗаписей.ДополнительныеСвойства.Вставить("ЗаписьОбщихДанных");
	НаборЗаписей.Отбор.ВидТарифа.Установить(Тариф);
	НаборЗаписей.Загрузить(ТаблицаТарифов.Скопировать(Новый Структура("ВидТарифа",Тариф)));
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Возвращает размеры тарифов взносов в разрезе лет и видов тарифа, 
// установленные Федеральным законом.
//
// Параметры: 
// 	нет
//
// Возвращаемое значение:
//	Таблица значений, соответствующая по структуре регистру сведений ТарифыСтраховыхВзносов.
//
Функция ТаблицаТарифовСтраховыхВзносов() Экспорт
	
	// 4D:ERP для Беларуси, Яна, 21.08.2017 11:11:36 
	// Регистр сведений "Страховые взносы", №15103
	// Результат тестирования конфигурации, №15769 
	// {
	ТаблицаТарифов = Новый ТаблицаЗначений;
	ТаблицаТарифов.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	ТаблицаТарифов.Колонки.Добавить("ВидТарифа", Новый ОписаниеТипов("СправочникСсылка.ВидыТарифовСтраховыхВзносов"));
	ТаблицаТарифов.Колонки.Добавить("ФСЗН", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(5,2)));
	ТаблицаТарифов.Колонки.Добавить("ПС", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(5,2)));
	ТаблицаТарифов.Колонки.Добавить("СС", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(5,2)));
	ТаблицаТарифов.Колонки.Добавить("ПенсионныйФонд", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(5,2)));
	ТаблицаТарифов.Колонки.Добавить("ФСЗНИнГрЕАЭС", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(5,2)));
	ТаблицаТарифов.Индексы.Добавить("ВидТарифа");
	

	СельХозПроизводители = Справочники.ВидыТарифовСтраховыхВзносов.СельХозПроизводители;	
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20170101',СельХозПроизводители,
			30, 24, 6, 1, 6);	
			
	ОрганизацияИнвалидов = Справочники.ВидыТарифовСтраховыхВзносов.ОрганизацияИнвалидов;	
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20170101',ОрганизацияИнвалидов,
			11, 5, 6, 1, 6);		
			
	ОбщийНалоговыйРежим = Справочники.ВидыТарифовСтраховыхВзносов.ОбщийНалоговыйРежим;
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20170101',ОбщийНалоговыйРежим,
			34, 28, 6, 1, 6);	
	
	КоммОргМогилевскОбл = Справочники.ВидыТарифовСтраховыхВзносов.КоммОргМогилевскОбл;
	ВписатьСтрокуРегистраТарифСтраховыхВзносов(ТаблицаТарифов,'20170101',КоммОргМогилевскОбл,
			30, 24, 6, 1, 6);
			
	Возврат ТаблицаТарифов
	// }
	// 4D
		
КонецФункции

// 4D:ERP для Беларуси, Яна, 21.08.2017 11:11:36 
// Результат тестирования конфигурации, №15769 
// {
Процедура ВписатьСтрокуРегистраТарифСтраховыхВзносов(НаборЗаписей,ДатаСведений,ВидТарифа,ФСЗН = 0,ПС = 0,СС = 0,ПенсионныйФонд = 0,ФСЗНИнГрЕАЭС = 0)

	НоваяСтрока = НаборЗаписей.Добавить();
	НоваяСтрока.Период = ДатаСведений;
	НоваяСтрока.ВидТарифа = ВидТарифа;
	НоваяСтрока.ФСЗН = ФСЗН;
	НоваяСтрока.ПС = ПС;
	НоваяСтрока.СС = СС;	
	НоваяСтрока.ПенсионныйФонд = ПенсионныйФонд;
	НоваяСтрока.ФСЗНИнГрЕАЭС = ФСЗНИнГрЕАЭС;
	
КонецПроцедуры
// }
// 4D

#КонецОбласти

#КонецЕсли