#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	Если Не ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период,
	|	ТаблицаПередЗаписью.НематериальныйАктив,
	|	ТаблицаПередЗаписью.Организация,
	|	ТаблицаПередЗаписью.АмортизацияДо2002,
	|	ТаблицаПередЗаписью.АмортизацияДо2009,
	|	ТаблицаПередЗаписью.МетодНачисленияАмортизации,
	|	ТаблицаПередЗаписью.НачислятьАмортизацию,
	|	ТаблицаПередЗаписью.ПервоначальнаяСтоимостьНУ,
	|	ТаблицаПередЗаписью.СрокПолезногоИспользования,
	|	ТаблицаПередЗаписью.СтоимостьДо2002,
	|	ТаблицаПередЗаписью.ФактическийСрокИспользованияДо2009
	|ПОМЕСТИТЬ ПервоначальныеСведенияНМАНалоговыйУчетПередЗаписью
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияНМАНалоговыйУчет КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Период           КАК Период,
	|	Таблица.Организация      КАК Организация,
	|	Таблица.НематериальныйАктив КАК НематериальныйАктив,
	|	&Регистратор             КАК Документ
	|ПОМЕСТИТЬ ПервоначальныеСведенияНМАНалоговыйУчетИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПередЗаписью.Период            КАК Период,
	|		ТаблицаПередЗаписью.Организация       КАК Организация,
	|		ТаблицаПередЗаписью.НематериальныйАктив  КАК НематериальныйАктив
	|	ИЗ
	|		ПервоначальныеСведенияНМАНалоговыйУчетПередЗаписью КАК ТаблицаПередЗаписью
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияНМАНалоговыйУчет КАК ТаблицаПриЗаписи
	|			ПО ТаблицаПриЗаписи.НематериальныйАктив = ТаблицаПередЗаписью.НематериальныйАктив
	|				И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|		ГДЕ
	|			(НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ)
	|				ИЛИ ТаблицаПриЗаписи.АмортизацияДо2002 <> ТаблицаПередЗаписью.АмортизацияДо2002
	|				ИЛИ ТаблицаПриЗаписи.АмортизацияДо2009 <> ТаблицаПередЗаписью.АмортизацияДо2009
	|				ИЛИ ТаблицаПриЗаписи.МетодНачисленияАмортизации <> ТаблицаПередЗаписью.МетодНачисленияАмортизации
	|				ИЛИ ТаблицаПриЗаписи.НачислятьАмортизацию <> ТаблицаПередЗаписью.НачислятьАмортизацию
	|				ИЛИ ТаблицаПриЗаписи.Организация <> ТаблицаПередЗаписью.Организация
	|				ИЛИ ТаблицаПриЗаписи.ПервоначальнаяСтоимостьНУ <> ТаблицаПередЗаписью.ПервоначальнаяСтоимостьНУ
	|				ИЛИ ТаблицаПриЗаписи.СрокПолезногоИспользования <> ТаблицаПередЗаписью.СрокПолезногоИспользования
	|				ИЛИ ТаблицаПриЗаписи.СтоимостьДо2002 <> ТаблицаПередЗаписью.СтоимостьДо2002
	|				ИЛИ ТаблицаПриЗаписи.ФактическийСрокИспользованияДо2009 <> ТаблицаПередЗаписью.ФактическийСрокИспользованияДо2009
	|				ИЛИ ТаблицаПриЗаписи.НематериальныйАктив ЕСТЬ NULL)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.Период,
	|		ТаблицаПриЗаписи.Организация,
	|		ТаблицаПриЗаписи.НематериальныйАктив
	|	ИЗ
	|		РегистрСведений.ПервоначальныеСведенияНМАНалоговыйУчет КАК ТаблицаПриЗаписи
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПервоначальныеСведенияНМАНалоговыйУчетПередЗаписью КАК ТаблицаПередЗаписью
	|			ПО ТаблицаПриЗаписи.НематериальныйАктив = ТаблицаПередЗаписью.НематериальныйАктив
	|		ГДЕ
	|			(НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ)
	|				ИЛИ ТаблицаПриЗаписи.АмортизацияДо2002 <> ТаблицаПередЗаписью.АмортизацияДо2002
	|				ИЛИ ТаблицаПриЗаписи.АмортизацияДо2009 <> ТаблицаПередЗаписью.АмортизацияДо2009
	|				ИЛИ ТаблицаПриЗаписи.МетодНачисленияАмортизации <> ТаблицаПередЗаписью.МетодНачисленияАмортизации
	|				ИЛИ ТаблицаПриЗаписи.НачислятьАмортизацию <> ТаблицаПередЗаписью.НачислятьАмортизацию
	|				ИЛИ ТаблицаПриЗаписи.Организация <> ТаблицаПередЗаписью.Организация
	|				ИЛИ ТаблицаПриЗаписи.ПервоначальнаяСтоимостьНУ <> ТаблицаПередЗаписью.ПервоначальнаяСтоимостьНУ
	|				ИЛИ ТаблицаПриЗаписи.СрокПолезногоИспользования <> ТаблицаПередЗаписью.СрокПолезногоИспользования
	|				ИЛИ ТаблицаПриЗаписи.СтоимостьДо2002 <> ТаблицаПередЗаписью.СтоимостьДо2002
	|				ИЛИ ТаблицаПриЗаписи.ФактическийСрокИспользованияДо2009 <> ТаблицаПередЗаписью.ФактическийСрокИспользованияДо2009
	|				ИЛИ ТаблицаПередЗаписью.НематериальныйАктив ЕСТЬ NULL)
	|			И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|
	|	) КАК Таблица
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПервоначальныеСведенияНМАНалоговыйУчетПередЗаписью";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	Выборка.Следующий();
	
	СтруктураВременныеТаблицы.Вставить("ПервоначальныеСведенияНМАНалоговыйУчетИзменение", Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
