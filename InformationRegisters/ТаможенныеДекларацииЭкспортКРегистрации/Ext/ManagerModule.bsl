#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Обновляет состояние документа "Реализация товаров и услуг" в регистре
//
// Параметры:
//   РеализацииЭкспорт - Ссылка на документ реализации
//
Процедура ОбновитьСостояние(Знач РеализацииЭкспорт) Экспорт
	
	Если НЕ ПолучитьФункциональнуюОпцию("ВестиУчетТаможенныхДекларацийНаЭкспорт") Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	РеализацииЭкспорт = ОбщегоНазначенияКлиентСервер.СвернутьМассив(РеализацииЭкспорт);
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(РеализацииЭкспорт, Документы.РеализацияТоваровУслуг.ПустаяСсылка());
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВложеннаяТаблица.Организация,
	|	ВложеннаяТаблица.РеализацияЭкспорт,
	|	ВложеннаяТаблица.ДатаРеализации,
	|	ВложеннаяТаблица.Контрагент,
	|	ВложеннаяТаблица.Валюта,
	|	СУММА(ВложеннаяТаблица.СуммаДокумента) КАК СуммаДокумента
	|ИЗ
	|	(ВЫБРАТЬ
	|		РеализацииЭкспорт.Организация               КАК Организация,
	|		РеализацииЭкспорт.Ссылка                    КАК РеализацияЭкспорт,
	|		НАЧАЛОПЕРИОДА(РеализацииЭкспорт.Дата, ДЕНЬ) КАК ДатаРеализации,
	|		РеализацииЭкспорт.Контрагент                КАК Контрагент,
	|		РеализацииЭкспорт.Валюта                    КАК Валюта,
	|		РеализацииЭкспорт.СуммаДокумента            КАК СуммаДокумента
	|	ИЗ
	|		Документ.РеализацияТоваровУслуг КАК РеализацииЭкспорт
	|		
	|	ГДЕ
	|		РеализацииЭкспорт.Ссылка В (&РеализацииЭкспорт)
	|		И РеализацииЭкспорт.НалогообложениеНДС = &ПродажаНаЭкспорт
	|		И РеализацииЭкспорт.Проведен
	|		
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РеализацииЭкспорт.Организация,
	|		РеализацииЭкспорт.Ссылка,
	|		НАЧАЛОПЕРИОДА(РеализацииЭкспорт.Дата, ДЕНЬ),
	|		РеализацииЭкспорт.Контрагент,
	|		РеализацииЭкспорт.Валюта,
	|		-РеализацииЭкспорт.СуммаДокумента
	|	ИЗ
	|		Документ.РеализацияТоваровУслуг КАК РеализацииЭкспорт
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			Документ.ТаможеннаяДекларацияЭкспорт.ДокументыОснования КАК ДекларацииЭкспорт
	|		ПО
	|			РеализацииЭкспорт.Ссылка = ДекларацииЭкспорт.ДокументОснование
	|			И ДекларацииЭкспорт.Ссылка.Проведен
	|	ГДЕ
	|		РеализацииЭкспорт.Ссылка В (&РеализацииЭкспорт)
	|		И РеализацииЭкспорт.НалогообложениеНДС = &ПродажаНаЭкспорт
	|		И РеализацииЭкспорт.Проведен) ВложеннаяТаблица
	|	
	|СГРУППИРОВАТЬ ПО
	|	ВложеннаяТаблица.Организация,
	|	ВложеннаяТаблица.РеализацияЭкспорт,
	|	ВложеннаяТаблица.ДатаРеализации,
	|	ВложеннаяТаблица.Контрагент,
	|	ВложеннаяТаблица.Валюта
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВложеннаяТаблица.СуммаДокумента) > 0
	|";
	
	Запрос.УстановитьПараметр("РеализацииЭкспорт", РеализацииЭкспорт);
	Запрос.УстановитьПараметр("ПродажаНаЭкспорт",  Перечисления.ТипыНалогообложенияНДС.ПродажаНаЭкспорт);
	
	ТаможенныеДекларацииКРегистрации = Запрос.Выполнить().Выгрузить();
	ТаможенныеДекларацииКРегистрации.Индексы.Добавить("РеализацияЭкспорт");
	
	Для каждого РеализацияЭкспорт Из РеализацииЭкспорт Цикл
		
		МенеджерЗаписи = РегистрыСведений.ТаможенныеДекларацииЭкспортКРегистрации.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.РеализацияЭкспорт = РеализацияЭкспорт;
		
		Строка = ТаможенныеДекларацииКРегистрации.Найти(РеализацияЭкспорт, "РеализацияЭкспорт");
		
		Если Строка = Неопределено Тогда
			МенеджерЗаписи.Удалить();
		Иначе
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Строка);
			МенеджерЗаписи.Записать();
		КонецЕсли;
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли