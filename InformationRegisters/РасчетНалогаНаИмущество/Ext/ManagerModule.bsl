#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбновлениеИнформационнойБазы

// Перенос Налоговой ставки из ресурсов в измерения.
//
Процедура ПеренестиНалоговуюСтавкуИзРесурсовВИзмерения() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетНалогаНаИмущество.Регистратор
	|ИЗ
	|	РегистрСведений.РасчетНалогаНаИмущество КАК РасчетНалогаНаИмущество
	|ГДЕ
	|	РасчетНалогаНаИмущество.УдалитьНалоговаяСтавка <> ДАТАВРЕМЯ(1, 1, 1)";
	
	ВыборкаЗаписи = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаЗаписи.Следующий() Цикл
	
		НаборЗаписей = РегистрыСведений.РасчетНалогаНаИмущество.СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.Регистратор.Установить(ВыборкаЗаписи.Регистратор);
		НаборЗаписей.Прочитать();
		
		Для каждого СтрокаНабора Из НаборЗаписей Цикл
			Если ЗначениеЗаполнено(СтрокаНабора.УдалитьНалоговаяСтавка) Тогда
				Если НЕ ЗначениеЗаполнено(СтрокаНабора.НалоговаяСтавка) Тогда
					СтрокаНабора.НалоговаяСтавка = СтрокаНабора.УдалитьНалоговаяСтавка;
				КонецЕсли;
				СтрокаНабора.УдалитьНалоговаяСтавка = Неопределено;
			КонецЕсли;
		КонецЦикла;
		
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		Исключение
			ШаблонСообщения	= НСтр("ru = 'Не выполнено обновление записей регистра сведений ""Расчет налога на имущество""
                                    |%1'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.РегистрыСведений.РасчетНалогаНаИмущество,, 
				ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

// Перенос налоговых льгот из ресурсов в измерения.
//
Процедура ПеренестиЛьготыИзРесурсовВИзмерения() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетНалогаНаИмущество.Регистратор
	|ИЗ
	|	РегистрСведений.РасчетНалогаНаИмущество КАК РасчетНалогаНаИмущество
	|ГДЕ
	|	РасчетНалогаНаИмущество.УдалитьПонижениеНалоговойСтавки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетНалогаНаИмущество.Регистратор
	|ИЗ
	|	РегистрСведений.РасчетНалогаНаИмущество КАК РасчетНалогаНаИмущество
	|ГДЕ
	|	РасчетНалогаНаИмущество.УдалитьКодНалоговойЛьготыОсвобождениеОтНалогообложения <> """"";
	
	ВыборкаЗаписи = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаЗаписи.Следующий() Цикл
	
		НаборЗаписей = РегистрыСведений.РасчетНалогаНаИмущество.СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.Регистратор.Установить(ВыборкаЗаписи.Регистратор);
		НаборЗаписей.Прочитать();
		
		Для каждого СтрокаНабора Из НаборЗаписей Цикл
			Если ЗначениеЗаполнено(СтрокаНабора.УдалитьКодНалоговойЛьготыОсвобождениеОтНалогообложения)
			 ИЛИ СтрокаНабора.ПонижениеНалоговойСтавки Тогда
				Если НЕ ЗначениеЗаполнено(СтрокаНабора.УдалитьКодНалоговойЛьготыОсвобождениеОтНалогообложения) Тогда
					СтрокаНабора.КодНалоговойЛьготыОсвобождениеОтНалогообложения = 
						СтрокаНабора.УдалитьКодНалоговойЛьготыОсвобождениеОтНалогообложения;
				КонецЕсли;
				Если НЕ СтрокаНабора.ПонижениеНалоговойСтавки Тогда
					СтрокаНабора.ПонижениеНалоговойСтавки = СтрокаНабора.УдалитьПонижениеНалоговойСтавки;
				КонецЕсли;
				СтрокаНабора.УдалитьКодНалоговойЛьготыОсвобождениеОтНалогообложения = Неопределено;
				СтрокаНабора.УдалитьПонижениеНалоговойСтавки = Ложь;
			КонецЕсли;
		КонецЦикла;
		
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		Исключение
			ШаблонСообщения	= НСтр("ru = 'Не выполнено обновление записей регистра сведений ""Расчет налога на имущество""
                                    |%1'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.РегистрыСведений.РасчетНалогаНаИмущество,, 
				ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьЛидирующиеНулиКодВидаИмущества() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетНалогаНаИмущество.Регистратор
	|ИЗ
	|	РегистрСведений.РасчетНалогаНаИмущество КАК РасчетНалогаНаИмущество";
	
	ВыборкаЗаписи = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаЗаписи.Следующий() Цикл
	
		КодВидаИмуществаИзменен = Ложь;
		
		НаборЗаписей = РегистрыСведений.РасчетНалогаНаИмущество.СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.Регистратор.Установить(ВыборкаЗаписи.Регистратор);
		НаборЗаписей.Прочитать();
		
		Для каждого СтрокаНабора Из НаборЗаписей Цикл
			
			КодВидаИмущества = СокрЛП(СтрокаНабора.КодВидаИмущества);
			Если СтрДлина(КодВидаИмущества) = 1 Тогда
				СтрокаНабора.КодВидаИмущества = "0" + КодВидаИмущества;
				КодВидаИмуществаИзменен = Истина;
			КонецЕсли;
			
			Если СтрокаНабора.Период >= '20170101'
			   И СтрокаНабора.ВидНалоговойБазы = Перечисления.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость Тогда
				СтрокаНабора.КодВидаИмущества = "11";
				КодВидаИмуществаИзменен = Истина;
			КонецЕсли;
			
		КонецЦикла;
		
		Если Не КодВидаИмуществаИзменен Тогда
			Продолжить;
		КонецЕсли;
		
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
		Исключение
			ШаблонСообщения	= НСтр("ru = 'Не выполнено обновление записей регистра сведений ""Расчет налога на имущество""
                                    |%1'");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,
				Метаданные.РегистрыСведений.РасчетНалогаНаИмущество,, 
				ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли