#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Возвращает результат выполнения анализатора отражения номенклатуры с настроенными счетами
//	
//	Параметры:
//		МассивОрганизаций - Массив - СправочникСсылка.Организации - отбор по организациям;
//		Период - СтандартныйПериод - период с датой начала и окончания анализа.
//	ВозвращаемоеЗначение:
//		РезультатЗапроса - список всех операций, удовлетворяющий входным параметрам с настроенными для них счетами.
//
Функция РезультатЗапросаПоНастройкамОтраженияВУчете(МассивОрганизаций, Период) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	ПараметрыАнализатора = Новый Структура("МассивОрганизаций, НачалоПериода, КонецПериода", МассивОрганизаций, Период.ДатаНачала, Период.ДатаОкончания);
	Запрос.МенеджерВременныхТаблиц = ВременнаяТаблицаДанныхПоСуществующимОперациям(ПараметрыАнализатора);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДвиженияНоменклатуры.Организация КАК Организация,
	|	ДвиженияНоменклатуры.Склад КАК Склад,
	|	ДвиженияНоменклатуры.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|
	|	ДвиженияНоменклатуры.ЕстьСобственныеТовары КАК ЕстьСобственныеТовары,
	|	ДвиженияНоменклатуры.ЕстьРеализацияБезПереходаПраваСобственности КАК ЕстьРеализацияБезПереходаПраваСобственности,
	|	ДвиженияНоменклатуры.ЕстьПродажаСобственныхТоваров КАК ЕстьПродажаСобственныхТоваров,
	|	ДвиженияНоменклатуры.ЕстьПродажаУслуг КАК ЕстьПродажаУслуг,
	|	ДвиженияНоменклатуры.ЕстьПродажаСНДС КАК ЕстьПродажаСНДС,
	|	
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаНаСкладе, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаНаСкладе
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаНаСкладе
	|	КОНЕЦ КАК СчетУчетаНаСкладе,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаНаСкладе, &ПустойСчет) = &ПустойСчет
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаНаСкладе, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаНаСкладеПоУмолчанию,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаВПути, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаВПути
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаВПути
	|	КОНЕЦ КАК СчетУчетаВПути,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаВПути, &ПустойСчет) = &ПустойСчет
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаВПути, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаВПутиПоУмолчанию,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаВыручкиОтПродаж, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаВыручкиОтПродаж
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаВыручкиОтПродаж
	|	КОНЕЦ КАК СчетУчетаВыручкиОтПродаж,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаВыручкиОтПродаж, &ПустойСчет) = &ПустойСчет
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаВыручкиОтПродаж, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаВыручкиОтПродажПоУмолчанию,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаСебестоимостиПродаж, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаСебестоимостиПродаж
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаСебестоимостиПродаж
	|	КОНЕЦ КАК СчетУчетаСебестоимостиПродаж,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаСебестоимостиПродаж, &ПустойСчет) = &ПустойСчет
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаСебестоимостиПродаж, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаСебестоимостиПродажПоУмолчанию,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаНДСПриПродаже, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаНДСПриПродаже
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаНДСПриПродаже
	|	КОНЕЦ КАК СчетУчетаНДСПриПродаже,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаНДСПриПродаже, &ПустойСчет) = &ПустойСчет
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаНДСПриПродаже, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаНДСПриПродажеПоУмолчанию,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаНДСВПути, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаНДСВПути
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаНДСВПути
	|	КОНЕЦ КАК СчетУчетаНДСВПути,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаНДСВПути, &ПустойСчет) = &ПустойСчет
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаНДСВПути, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаНДСВПутиПоУмолчанию,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СтатьяДоходовРегл, &ПустаяСтатья) <> &ПустаяСтатья
	|			ТОГДА ПорядокОтражения.СтатьяДоходовРегл
	|		ИНАЧЕ ГруппыФинансовогоУчета.СтатьяДоходовРегл
	|	КОНЕЦ КАК СтатьяДоходовРегл,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СтатьяДоходовРегл, &ПустаяСтатья) = &ПустаяСтатья
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СтатьяДоходовРегл, &ПустаяСтатья) <> &ПустаяСтатья
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СтатьяДоходовРеглПоУмолчанию
	|
	|ИЗ
	|	ДвиженияНоменклатуры КАК ДвиженияНоменклатуры
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ПорядокОтраженияНоменклатуры КАК ПорядокОтражения
	|	ПО
	|		ДвиженияНоменклатуры.Организация = ПорядокОтражения.Организация
	|		И ДвиженияНоменклатуры.Склад = ПорядокОтражения.Склад
	|		И ДвиженияНоменклатуры.ГруппаФинансовогоУчета = ПорядокОтражения.ГруппаФинансовогоУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ГруппыФинансовогоУчетаНоменклатуры КАК ГруппыФинансовогоУчета
	|	ПО
	|		ДвиженияНоменклатуры.ГруппаФинансовогоУчета = ГруппыФинансовогоУчета.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПорядокОтражения.Организация КАК Организация,
	|	ПорядокОтражения.Склад КАК Склад,
	|	ПорядокОтражения.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|
	|	Ложь КАК ЕстьСобственныеТовары,
	|	Ложь КАК ЕстьРеализацияБезПереходаПраваСобственности,
	|	Ложь КАК ЕстьПродажаСобственныхТоваров,
	|	Ложь КАК ЕстьПродажаУслуг,
	|	Ложь КАК ЕстьПродажаСНДС,
	|	
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаНаСкладе, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаНаСкладе
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаНаСкладе
	|	КОНЕЦ КАК СчетУчетаНаСкладе,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаНаСкладе, &ПустойСчет) = &ПустойСчет
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаНаСкладе, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаНаСкладеПоУмолчанию,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаВПути, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаВПути
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаВПути
	|	КОНЕЦ КАК СчетУчетаВПути,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаВПути, &ПустойСчет) = &ПустойСчет
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаВПути, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаВПутиПоУмолчанию,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаВыручкиОтПродаж, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаВыручкиОтПродаж
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаВыручкиОтПродаж
	|	КОНЕЦ КАК СчетУчетаВыручкиОтПродаж,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаВыручкиОтПродаж, &ПустойСчет) = &ПустойСчет
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаВыручкиОтПродаж, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаВыручкиОтПродажПоУмолчанию,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаСебестоимостиПродаж, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаСебестоимостиПродаж
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаСебестоимостиПродаж
	|	КОНЕЦ КАК СчетУчетаСебестоимостиПродаж,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаСебестоимостиПродаж, &ПустойСчет) = &ПустойСчет
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаСебестоимостиПродаж, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаСебестоимостиПродажПоУмолчанию,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаНДСПриПродаже, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаНДСПриПродаже
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаНДСПриПродаже
	|	КОНЕЦ КАК СчетУчетаНДСПриПродаже,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаНДСПриПродаже, &ПустойСчет) = &ПустойСчет
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаНДСПриПродаже, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаНДСПриПродажеПоУмолчанию,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаНДСВПути, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаНДСВПути
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаНДСВПути
	|	КОНЕЦ КАК СчетУчетаНДСВПути,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаНДСВПути, &ПустойСчет) = &ПустойСчет
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаНДСВПути, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаНДСВПутиПоУмолчанию,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СтатьяДоходовРегл, &ПустаяСтатья) <> &ПустаяСтатья
	|			ТОГДА ПорядокОтражения.СтатьяДоходовРегл
	|		ИНАЧЕ ГруппыФинансовогоУчета.СтатьяДоходовРегл
	|	КОНЕЦ КАК СтатьяДоходовРегл,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СтатьяДоходовРегл, &ПустаяСтатья) = &ПустаяСтатья
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СтатьяДоходовРегл, &ПустаяСтатья) <> &ПустаяСтатья
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СтатьяДоходовРеглПоУмолчанию
	|
	|ИЗ
	|	РегистрСведений.ПорядокОтраженияНоменклатуры КАК ПорядокОтражения
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ДвиженияНоменклатуры КАК ДвиженияНоменклатуры
	|	ПО
	|		ПорядокОтражения.Организация = ДвиженияНоменклатуры.Организация
	|		И ПорядокОтражения.Склад = ДвиженияНоменклатуры.Склад
	|		И ПорядокОтражения.ГруппаФинансовогоУчета = ДвиженияНоменклатуры.ГруппаФинансовогоУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ГруппыФинансовогоУчетаНоменклатуры КАК ГруппыФинансовогоУчета
	|	ПО
	|		ПорядокОтражения.ГруппаФинансовогоУчета = ГруппыФинансовогоУчета.Ссылка
	|	
	|ГДЕ
	|	ПорядокОтражения.Организация В (&Организации)
	|	И ДвиженияНоменклатуры.Организация ЕСТЬ NULL
	|	
	|УПОРЯДОЧИТЬ ПО
	|	Организация Возр,
	|	Склад Возр,
	|	ГруппаФинансовогоУчета Возр";
	
	Запрос.УстановитьПараметр("ПустойСчет", ПланыСчетов.Хозрасчетный.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустаяСтатья", ПланыВидовХарактеристик.СтатьиДоходов.ПустаяСсылка());
		
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса;
	
КонецФункции // РезультатЗапросаПоНастройкамОтраженияВУчете()

Функция ВременнаяТаблицаДанныхПоСуществующимОперациям(ПараметрыАнализатора)
	
	РежимАнализа = Неопределено;
	ПараметрыАнализатора.Свойство("РежимАнализа", РежимАнализа);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.АналитикаУчетаНоменклатуры,
	|	ДанныеРегистра.ВидЗапасов,
	|	ВЫБОР КОГДА ДанныеРегистра.КорРазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыВПути) ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ЕстьРеализацияБезПереходаПраваСобственности
	|ПОМЕСТИТЬ ДвиженияРегистраСебестоимости
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК ДанныеРегистра
	|ГДЕ
	|	&УсловиеДляРегистра
	|	И ДанныеРегистра.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.ТипЗапасов,
	|	ДанныеРегистра.ВидЗапасов,
	|	КлючиПартнеров.Организация,
	|	ВЫРАЗИТЬ(КлючиНоменклатуры.Склад КАК Справочник.Склады) КАК Склад,
	|	КлючиНоменклатуры.Номенклатура,
	|	ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности) КАК ЕстьРеализацияБезПереходаПраваСобственности,
	|	ДанныеРегистра.НалогообложениеНДС В (&НалогообложениеСНДС) КАК ЕстьПродажаСНДС
	|ПОМЕСТИТЬ ДвиженияРегистраВыручки
	|ИЗ РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ДанныеРегистра
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК КлючиПартнеров
	|			ПО КлючиПартнеров.КлючАналитики = ДанныеРегистра.АналитикаУчетаПоПартнерам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиНоменклатуры
	|			ПО КлючиНоменклатуры.КлючАналитики = ДанныеРегистра.АналитикаУчетаНоменклатуры
	|
	|ГДЕ
	|	&ОрганизацияИзРегистраСведений_УсловиеДляРегистра
	|	И (ТИПЗНАЧЕНИЯ(КлючиНоменклатуры.Склад) = ТИП(Справочник.Склады)
	|		ИЛИ ТИПЗНАЧЕНИЯ(КлючиНоменклатуры.Склад) = ТИП(Справочник.СтруктураПредприятия))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДвиженияРегистраСебестоимости.Организация КАК Организация,
	|	Склады.Ссылка КАК Склад,
	|	ВЫБОР КОГДА НЕ &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета ТОГДА
	|		НоменклатураСпр.ГруппаФинансовогоУчета
	|	ИНАЧЕ
	|		ЕСТЬNULL(ВидыЗапасовСпр.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка))
	|	КОНЕЦ КАК ГруппаФинансовогоУчета,
	|	ИСТИНА КАК ЕстьСобственныеТовары,
	|	ДвиженияРегистраСебестоимости.ЕстьРеализацияБезПереходаПраваСобственности,
	|	ЛОЖЬ КАК ЕстьПродажаСобственныхТоваров,
	|	ЛОЖЬ КАК ЕстьПродажаУслуг,
	|	ЛОЖЬ КАК ЕстьПродажаСНДС
	|ПОМЕСТИТЬ ТаблицаДвиженийБезГруппировки
	|ИЗ ДвиженияРегистраСебестоимости КАК ДвиженияРегистраСебестоимости
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|			ПО Аналитика.Ссылка = ДвиженияРегистраСебестоимости.АналитикаУчетаНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|			ПО Склады.Ссылка = Аналитика.Склад
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСпр
	|			ПО НоменклатураСпр.Ссылка = Аналитика.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасовСпр
	|			ПО ВидыЗапасовСпр.Ссылка = ДвиженияРегистраСебестоимости.ВидЗапасов
	|
	//++ НЕ УТКА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
	|	ЕСТЬNULL(Услуга.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка)) КАК ГруппаФинансовогоУчета,
	|	ЛОЖЬ КАК ЕстьСобственныеТовары,
	|	ЛОЖЬ КАК ЕстьРеализацияБезПереходаПраваСобственности,
	|	ИСТИНА КАК ЕстьПродажаСобственныхТоваров,
	|	ИСТИНА КАК ЕстьПродажаУслуг,
	|	ВЫБОР
	|		КОГДА ДанныеДокумента.НалогообложениеНДС В (&НалогообложениеСНДС)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЕстьПродажаСНДС
	|ИЗ
	|	Документ.ОтчетДавальцу КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Услуга
	|			ПО ДанныеДокумента.Номенклатура = Услуга.Ссылка
	|ГДЕ
	|	&УсловиеДляДокумента
	|
	//-- НЕ УТКА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДвиженияРегистраВыручки.Организация,
	|	ВЫБОР КОГДА ДвиженияРегистраВыручки.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга) ТОГДА
	|		ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|	ИНАЧЕ
	|		ЕСТЬNULL(ДвиженияРегистраВыручки.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	|	КОНЕЦ КАК Склад,
	|	ВЫБОР КОГДА НЕ &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета
	|		ИЛИ ДвиженияРегистраВыручки.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга)
	|	ТОГДА
	|		НоменклатураСпр.ГруппаФинансовогоУчета
	|	ИНАЧЕ
	|		ЕСТЬNULL(ВидыЗапасовСпр.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка))
	|	КОНЕЦ КАК ГруппаФинансовогоУчета,
	|	ДвиженияРегистраВыручки.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.АгентскаяУслуга) КАК ЕстьСобственныеТовары,
	|	ДвиженияРегистраВыручки.ЕстьРеализацияБезПереходаПраваСобственности,
	|	ДвиженияРегистраВыручки.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|		ИЛИ ДвиженияРегистраВыручки.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.АгентскаяУслуга)
	|		ИЛИ НоменклатураСпр.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа) КАК ЕстьПродажаСобственныхТоваров, 
	|	ДвиженияРегистраВыручки.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга) КАК ЕстьПродажаУслуг,	
	|	ДвиженияРегистраВыручки.ЕстьПродажаСНДС
	|ИЗ ДвиженияРегистраВыручки КАК ДвиженияРегистраВыручки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСпр
	|			ПО НоменклатураСпр.Ссылка = ДвиженияРегистраВыручки.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасовСпр
	|			ПО ВидыЗапасовСпр.Ссылка = ДвиженияРегистраВыручки.ВидЗапасов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.Склад КАК Склад,
	|	ВложенныйЗапрос.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьСобственныеТовары) КАК ЕстьСобственныеТовары,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьРеализацияБезПереходаПраваСобственности) КАК ЕстьРеализацияБезПереходаПраваСобственности,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьПродажаСобственныхТоваров) КАК ЕстьПродажаСобственныхТоваров,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьПродажаУслуг) КАК ЕстьПродажаУслуг,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьПродажаСНДС) КАК ЕстьПродажаСНДС
	|ПОМЕСТИТЬ ТаблицаДвижений
	|ИЗ ТаблицаДвиженийБезГруппировки КАК ВложенныйЗапрос
	|	
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.Склад,
	|	ВложенныйЗапрос.ГруппаФинансовогоУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|	УНИЧТОЖИТЬ ДвиженияРегистраСебестоимости
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|	УНИЧТОЖИТЬ ДвиженияРегистраВыручки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|	УНИЧТОЖИТЬ ТаблицаДвиженийБезГруппировки";
	
	НалогообложениеСНДС = Новый Массив;
	НалогообложениеСНДС.Добавить(Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС);
	НалогообложениеСНДС.Добавить(Перечисления.ТипыНалогообложенияНДС.ПродажаНаЭкспорт);
	Запрос.УстановитьПараметр("НалогообложениеСНДС", НалогообложениеСНДС);
	Запрос.УстановитьПараметр("ФормироватьВидыЗапасовПоГруппамФинансовогоУчета",
		ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоГруппамФинансовогоУчета"));
		
	УсловиеВхожденияВРегистр = "ГДЕ" + Символы.ПС + Символы.Таб + "&УсловиеДляРегистра";
	УсловиеВхожденияВРегистрССоединениемПоОрганизации = "ГДЕ" + Символы.ПС + Символы.Таб + "&ОрганизацияИзРегистраСведений_УсловиеДляРегистра";
	УсловиеВхожденияВДокумент = "ГДЕ" + Символы.ПС + Символы.Таб + "&УсловиеДляДокумента";
	ТекстСоединенияСТаблицейДокументов = "		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДокументов КАК ТаблицаДокументов
	|		ПО ДанныеРегистра.Организация = ТаблицаДокументов.Организация
	|			И ДанныеРегистра.Регистратор = ТаблицаДокументов.Документ";
	ТекстУсловийПоОрганизацииИПериоду = "ГДЕ
	|	ДанныеРегистра.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ДанныеРегистра.Организация В (&Организации)";
	
	Если РежимАнализа = "ПоНеотраженным" Тогда
		
		Статусы = Новый Массив;
		Статусы.Добавить(Перечисления.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета);
		Статусы.Добавить(Перечисления.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете);
		РегистрыСведений.ОтражениеДокументовВРеглУчете.ДобавитьВременнуюТаблицуДокументовСОтборомПоСтатусуОтражения(
			Запрос.МенеджерВременныхТаблиц,
			ПараметрыАнализатора.МассивОрганизаций,
			Статусы);
	
		ТекстУсловий = ТекстСоединенияСТаблицейДокументов;
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВРегистр, ТекстУсловий);
		ТекстУсловий = СтрЗаменить(ТекстУсловий, "Регистратор", "Ссылка");
		ТекстУсловий = СтрЗаменить(ТекстУсловий, "ДанныеРегистра", "ДанныеДокумента");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВДокумент, ТекстУсловий);
		ТекстУсловий = ТекстСоединенияСТаблицейДокументов;
		ТекстУсловий = СтрЗаменить(ТекстУсловий, "ДанныеРегистра.Организация", "КлючиПартнеров.Организация");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВРегистрССоединениемПоОрганизации, ТекстУсловий);
	Иначе
		ТекстУсловий = ТекстУсловийПоОрганизацииИПериоду;
		Запрос.УстановитьПараметр("Организации", ПараметрыАнализатора.МассивОрганизаций);
		Запрос.УстановитьПараметр("ДатаНачала", ПараметрыАнализатора.НачалоПериода);
		Запрос.УстановитьПараметр("ДатаОкончания", ПараметрыАнализатора.КонецПериода);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВРегистр, ТекстУсловий);
		ТекстУсловий = СтрЗаменить(ТекстУсловий, "Период", "Дата");
		ТекстУсловий = СтрЗаменить(ТекстУсловий, "ДанныеРегистра", "ДанныеДокумента");
		ТекстУсловий = ТекстУсловий + "И ДанныеДокумента.Проведен" + Символы.ПС + Символы.Таб;
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВДокумент, ТекстУсловий);
		ТекстУсловий = ТекстУсловийПоОрганизацииИПериоду;		
		ТекстУсловий = СтрЗаменить(ТекстУсловий, "ДанныеРегистра.Организация", "КлючиПартнеров.Организация");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, УсловиеВхожденияВРегистрССоединениемПоОрганизации, ТекстУсловий);
	КонецЕсли;
		
	Запрос.Выполнить();
	
	Возврат Запрос.МенеджерВременныхТаблиц;
	
КонецФункции

Функция ВременнаяТаблицаДвиженийСоСчетами(ВременнаяТаблицаДвижений)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ВременнаяТаблицаДвижений;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДвиженияНоменклатуры.Организация КАК Организация,
	|	ДвиженияНоменклатуры.Склад КАК МестоУчета,
	|	ДвиженияНоменклатуры.ГруппаФинансовогоУчета КАК АналитикаУчета,
	|	ДвиженияНоменклатуры.ЕстьСобственныеТовары КАК ЕстьСобственныеТовары,
	|	ДвиженияНоменклатуры.ЕстьРеализацияБезПереходаПраваСобственности КАК ЕстьРеализацияБезПереходаПраваСобственности,
	|	ДвиженияНоменклатуры.ЕстьПродажаСобственныхТоваров КАК ЕстьПродажаСобственныхТоваров,
	|	ДвиженияНоменклатуры.ЕстьПродажаУслуг КАК ЕстьПродажаУслуг,
	|	ДвиженияНоменклатуры.ЕстьПродажаСНДС КАК ЕстьПродажаСНДС,
	|
	|	ВЫБОР КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаНаСкладе, &ПустойСчет) = &ПустойСчет
	|		ТОГДА ЕСТЬNULL(ОбщаяНастройка.СчетУчетаНаСкладе, ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаНаСкладе, &ПустойСчет))
	|		ИНАЧЕ ПорядокОтражения.СчетУчетаНаСкладе КОНЕЦ КАК СчетУчетаНаСкладе,
	|	ВЫБОР КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаВПути, &ПустойСчет) = &ПустойСчет
	|		ТОГДА ЕСТЬNULL(ОбщаяНастройка.СчетУчетаВПути, ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаВПути, &ПустойСчет))
	|		ИНАЧЕ ПорядокОтражения.СчетУчетаВПути КОНЕЦ КАК СчетУчетаВПути,
	|	ВЫБОР КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаВыручкиОтПродаж, &ПустойСчет) = &ПустойСчет
	|		ТОГДА ЕСТЬNULL(ОбщаяНастройка.СчетУчетаВыручкиОтПродаж, ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаВыручкиОтПродаж, &ПустойСчет))
	|		ИНАЧЕ ПорядокОтражения.СчетУчетаВыручкиОтПродаж КОНЕЦ КАК СчетУчетаВыручкиОтПродаж,
	|	ВЫБОР КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаСебестоимостиПродаж, &ПустойСчет) = &ПустойСчет
	|		ТОГДА ЕСТЬNULL(ОбщаяНастройка.СчетУчетаСебестоимостиПродаж, ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаСебестоимостиПродаж, &ПустойСчет))
	|		ИНАЧЕ ПорядокОтражения.СчетУчетаСебестоимостиПродаж КОНЕЦ КАК СчетУчетаСебестоимостиПродаж,
	|	ВЫБОР КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаНДСПриПродаже, &ПустойСчет) = &ПустойСчет
	|		ТОГДА ЕСТЬNULL(ОбщаяНастройка.СчетУчетаНДСПриПродаже, ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаНДСПриПродаже, &ПустойСчет))
	|		ИНАЧЕ ПорядокОтражения.СчетУчетаНДСПриПродаже КОНЕЦ КАК СчетУчетаНДСПриПродаже,
	|	ВЫБОР КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаНДСВПути, &ПустойСчет) = &ПустойСчет
	|		ТОГДА ЕСТЬNULL(ОбщаяНастройка.СчетУчетаНДСВПути, ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаНДСВПути, &ПустойСчет))
	|		ИНАЧЕ ПорядокОтражения.СчетУчетаНДСВПути КОНЕЦ КАК СчетУчетаНДСВПути,
	|	ВЫБОР КОГДА ЕСТЬNULL(ПорядокОтражения.СтатьяДоходовРегл, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка)) = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка)
	|		ТОГДА ЕСТЬNULL(ОбщаяНастройка.СтатьяДоходовРегл, ЕСТЬNULL(ГруппыФинансовогоУчета.СтатьяДоходовРегл, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка)))
	|		ИНАЧЕ ПорядокОтражения.СтатьяДоходовРегл КОНЕЦ КАК СтатьяДоходовРегл,
	|
	|	ЕСТЬNULL(ОбщаяНастройка.СчетУчетаНаСкладе, ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаНаСкладе, &ПустойСчет)) <> &ПустойСчет
	|		И ЕСТЬNULL(ПорядокОтражения.СчетУчетаНаСкладе, &ПустойСчет) = &ПустойСчет
	|		ИЛИ ЕСТЬNULL(ОбщаяНастройка.СчетУчетаНаСкладе, ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаНаСкладе, &ПустойСчет))
	|		= ЕСТЬNULL(ПорядокОтражения.СчетУчетаНаСкладе, &ПустойСчет) КАК СчетУчетаНаСкладеПоУмолчанию,
	|	ЕСТЬNULL(ОбщаяНастройка.СчетУчетаВПути, ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаВПути, &ПустойСчет)) <> &ПустойСчет
	|		И ЕСТЬNULL(ПорядокОтражения.СчетУчетаВПути, &ПустойСчет) = &ПустойСчет
	|		ИЛИ ЕСТЬNULL(ОбщаяНастройка.СчетУчетаВПути, ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаВПути, &ПустойСчет))
	|		= ЕСТЬNULL(ПорядокОтражения.СчетУчетаВПути, &ПустойСчет) КАК СчетУчетаВПутиПоУмолчанию,
	|	ЕСТЬNULL(ОбщаяНастройка.СчетУчетаВыручкиОтПродаж, ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаВыручкиОтПродаж, &ПустойСчет)) <> &ПустойСчет
	|		И ЕСТЬNULL(ПорядокОтражения.СчетУчетаВыручкиОтПродаж, &ПустойСчет) = &ПустойСчет
	|		ИЛИ ЕСТЬNULL(ОбщаяНастройка.СчетУчетаВыручкиОтПродаж, ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаВыручкиОтПродаж, &ПустойСчет))
	|		= ЕСТЬNULL(ПорядокОтражения.СчетУчетаВыручкиОтПродаж, &ПустойСчет) КАК СчетУчетаВыручкиОтПродажПоУмолчанию,
	|	ЕСТЬNULL(ОбщаяНастройка.СчетУчетаСебестоимостиПродаж, ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаСебестоимостиПродаж, &ПустойСчет)) <> &ПустойСчет
	|		И ЕСТЬNULL(ПорядокОтражения.СчетУчетаСебестоимостиПродаж, &ПустойСчет) = &ПустойСчет
	|		ИЛИ ЕСТЬNULL(ОбщаяНастройка.СчетУчетаСебестоимостиПродаж, ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаСебестоимостиПродаж, &ПустойСчет))
	|		= ЕСТЬNULL(ПорядокОтражения.СчетУчетаСебестоимостиПродаж, &ПустойСчет) КАК СчетУчетаСебестоимостиПродажПоУмолчанию,
	|	ЕСТЬNULL(ОбщаяНастройка.СчетУчетаНДСПриПродаже, ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаНДСПриПродаже, &ПустойСчет)) <> &ПустойСчет
	|		И ЕСТЬNULL(ПорядокОтражения.СчетУчетаНДСПриПродаже, &ПустойСчет) = &ПустойСчет
	|		ИЛИ ЕСТЬNULL(ОбщаяНастройка.СчетУчетаНДСПриПродаже, ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаНДСПриПродаже, &ПустойСчет))
	|		= ЕСТЬNULL(ПорядокОтражения.СчетУчетаНДСПриПродаже, &ПустойСчет) КАК СчетУчетаНДСПриПродажеПоУмолчанию,
	|	ЕСТЬNULL(ОбщаяНастройка.СчетУчетаНДСВПути, ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаНДСВПути, &ПустойСчет)) <> &ПустойСчет
	|		И ЕСТЬNULL(ПорядокОтражения.СчетУчетаНДСВПути, &ПустойСчет) = &ПустойСчет
	|		ИЛИ ЕСТЬNULL(ОбщаяНастройка.СчетУчетаНДСВПути, ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаНДСВПути, &ПустойСчет))
	|		= ЕСТЬNULL(ПорядокОтражения.СчетУчетаНДСВПути, &ПустойСчет) КАК СчетУчетаНДСВПутиПоУмолчанию,
	|	ЕСТЬNULL(ОбщаяНастройка.СтатьяДоходовРегл, ЕСТЬNULL(ГруппыФинансовогоУчета.СтатьяДоходовРегл, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка))) <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка)
	|		И ЕСТЬNULL(ПорядокОтражения.СтатьяДоходовРегл, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка)) = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка)
	|		ИЛИ ЕСТЬNULL(ОбщаяНастройка.СтатьяДоходовРегл, ЕСТЬNULL(ГруппыФинансовогоУчета.СтатьяДоходовРегл, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка)))
	|		= ЕСТЬNULL(ПорядокОтражения.СтатьяДоходовРегл, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка)) КАК СтатьяДоходовРеглПоУмолчанию
	|
	|ПОМЕСТИТЬ ТаблицаДвиженийСоСчетами
	|ИЗ
	|	ТаблицаДвижений КАК ДвиженияНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияНоменклатуры КАК ПорядокОтражения
	|		ПО ДвиженияНоменклатуры.Организация = ПорядокОтражения.Организация
	|			И ДвиженияНоменклатуры.Склад = ПорядокОтражения.Склад
	|			И ДвиженияНоменклатуры.ГруппаФинансовогоУчета = ПорядокОтражения.ГруппаФинансовогоУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГруппыФинансовогоУчетаНоменклатуры КАК ГруппыФинансовогоУчета
	|		ПО ДвиженияНоменклатуры.ГруппаФинансовогоУчета = ГруппыФинансовогоУчета.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияНоменклатуры КАК ОбщаяНастройка
	|		ПО ОбщаяНастройка.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			И ОбщаяНастройка.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|			И ДвиженияНоменклатуры.ГруппаФинансовогоУчета = ОбщаяНастройка.ГруппаФинансовогоУчета
	|			И ДвиженияНоменклатуры.ГруппаФинансовогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("ПустойСчет", ПланыСчетов.Хозрасчетный.ПустаяСсылка());
	Запрос.Выполнить();
	
	Возврат Запрос.МенеджерВременныхТаблиц;
	
КонецФункции

// Выполняет анализ всех неотраженных операций отражения номенклатуры,
//	ищет для них ненастроенные счета учета и заносит эти данные в регистр ненастроенных счетов учета.
//	
//	ПараметрыАнализатора - Структура, содержащая следующие значения:
//			* МассивОрганизаций - Массив - СправочникСсылка.Организации - отбор по организациям, операции которых будут анализироваться;
//			* РежимАнализа - Строка - строка вида "ПоНеотраженным" -
//					необходимо для того, чтобы анализатор работал только по документам, неотраженным в регл. учете.
//
Процедура ЗаполнитьРегистрТребующихНастройкиНаОснованииАнализаНеотраженныхОпераций(ПараметрыАнализатора) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВременнаяТаблицаДвижений = ВременнаяТаблицаДанныхПоСуществующимОперациям(ПараметрыАнализатора);
	ВременнаяТаблицаДвиженийСоСчетами = ВременнаяТаблицаДвиженийСоСчетами(ВременнаяТаблицаДвижений);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ВременнаяТаблицаДвиженийСоСчетами;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДвиженияНоменклатурыСоСчетами.Организация,
	|	ДвиженияНоменклатурыСоСчетами.МестоУчета,
	|	ДвиженияНоменклатурыСоСчетами.АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НаСкладе) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ДвиженияНоменклатурыСоСчетами
	|ГДЕ
	|	ДвиженияНоменклатурыСоСчетами.ЕстьСобственныеТовары
	|	И ДвиженияНоменклатурыСоСчетами.СчетУчетаНаСкладе = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДвиженияНоменклатурыСоСчетами.Организация,
	|	ДвиженияНоменклатурыСоСчетами.МестоУчета,
	|	ДвиженияНоменклатурыСоСчетами.АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.РеализацияБезПереходаПраваСобственности) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ДвиженияНоменклатурыСоСчетами
	|ГДЕ
	|	ДвиженияНоменклатурыСоСчетами.ЕстьРеализацияБезПереходаПраваСобственности
	|	И ДвиженияНоменклатурыСоСчетами.СчетУчетаВПути = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДвиженияНоменклатурыСоСчетами.Организация,
	|	ДвиженияНоменклатурыСоСчетами.МестоУчета,
	|	ДвиженияНоменклатурыСоСчетами.АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НДСПриОтгрузкеБезПереходаПраваСобственности) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ДвиженияНоменклатурыСоСчетами
	|ГДЕ
	|	ДвиженияНоменклатурыСоСчетами.ЕстьРеализацияБезПереходаПраваСобственности
	|	И ДвиженияНоменклатурыСоСчетами.СчетУчетаНДСВПути = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДвиженияНоменклатурыСоСчетами.Организация,
	|	ДвиженияНоменклатурыСоСчетами.МестоУчета,
	|	ДвиженияНоменклатурыСоСчетами.АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ВыручкаОтПродаж) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ДвиженияНоменклатурыСоСчетами
	|ГДЕ
	|	(ДвиженияНоменклатурыСоСчетами.ЕстьПродажаСобственныхТоваров ИЛИ ДвиженияНоменклатурыСоСчетами.ЕстьПродажаУслуг)
	|	И ДвиженияНоменклатурыСоСчетами.СчетУчетаВыручкиОтПродаж = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДвиженияНоменклатурыСоСчетами.Организация,
	|	ДвиженияНоменклатурыСоСчетами.МестоУчета,
	|	ДвиженияНоменклатурыСоСчетами.АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НДСПриПродаже) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ДвиженияНоменклатурыСоСчетами
	|ГДЕ
	|	ДвиженияНоменклатурыСоСчетами.ЕстьПродажаСНДС
	|	И ДвиженияНоменклатурыСоСчетами.СчетУчетаНДСПриПродаже = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДвиженияНоменклатурыСоСчетами.Организация,
	|	ДвиженияНоменклатурыСоСчетами.МестоУчета,
	|	ДвиженияНоменклатурыСоСчетами.АналитикаУчета,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СебестоимостьПродаж) КАК ВидСчета
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ДвиженияНоменклатурыСоСчетами
	|ГДЕ
	|	ДвиженияНоменклатурыСоСчетами.ЕстьПродажаСобственныхТоваров
	|	И ДвиженияНоменклатурыСоСчетами.СчетУчетаСебестоимостиПродаж = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураВидовСчетовИРезультатаЗапроса = Новый Соответствие;
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.НаСкладе, МассивРезультатов.Получить(0));
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.РеализацияБезПереходаПраваСобственности, МассивРезультатов.Получить(1));
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.НДСПриОтгрузкеБезПереходаПраваСобственности, МассивРезультатов.Получить(2));
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.ВыручкаОтПродаж, МассивРезультатов.Получить(3));
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.НДСПриПродаже, МассивРезультатов.Получить(4));
	СтруктураВидовСчетовИРезультатаЗапроса.Вставить(Перечисления.ВидыСчетовРеглУчета.СебестоимостьПродаж, МассивРезультатов.Получить(5));
	
	ИмяСобытия = НСтр("ru = 'Анализ порядка отражения номенклатуры, требующего настройки'");
	РегистрыСведений.СчетаРеглУчетаТребующиеНастройки.ЗаполнитьЗаписиРегистраПоВидамСчетов(СтруктураВидовСчетовИРезультатаЗапроса, ИмяСобытия);

КонецПроцедуры

// Выполняет анализ операций отражения номенклатуры, в зависимости от параметров и дополняет исходную таблицу операций новыми строками.
//	
//	Параметры:
//		ПараметрыАнализа - Структура - содержит настройки анализатора со следующими свойствами:
//			* РежимАнализа - Строка - принимает значения вида "ПоНеотраженным" и "ЗаПериод". Причем если не задана, значение по умолчанию "ЗаПериод".
//			* КонецПериода - Дата - по эту дату анализируются операции, причем если режим анализа "ПоНеотраженным" не актуальна;
//			* НачалоПериода - Дата - начиная с этой даты анализируются операции, причем если режим анализа "ПоНеотраженным" не актуальна;
//			* МассивОрганизаций - Массив - СправочникСсылка.Организации - отбор по организациям, операции которых будут анализироваться;
//		АдресХранилища - Строка - адрес сохранения результата работы метода (ТаблицаЗначений).
//
Процедура ДополнитьТаблицуНаОснованииАнализаОпераций(ПараметрыАнализа, АдресХранилища) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВременнаяТаблицаДвижений = ВременнаяТаблицаДанныхПоСуществующимОперациям(ПараметрыАнализа);
	 
	ВременнаяТаблицаДвиженийСоСчетами = ВременнаяТаблицаДвиженийСоСчетами(ВременнаяТаблицаДвижений);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Таблица", ПараметрыАнализа.ЗаполняемаяТаблица);
	Запрос.МенеджерВременныхТаблиц = ВременнаяТаблицаДвиженийСоСчетами;
	 
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТекущаяТаблицаСчетовУчета.Организация,
	|	ТекущаяТаблицаСчетовУчета.Склад,
	|	ТекущаяТаблицаСчетовУчета.ГруппаФинансовогоУчета,
	|
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаНаСкладе,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаВыручкиОтПродаж,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаСебестоимостиПродаж,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаНДСПриПродаже,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаВПути,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаНДСВПути,
	|	ТекущаяТаблицаСчетовУчета.СтатьяДоходовРегл,
	|
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаНаСкладеПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаВыручкиОтПродажПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаСебестоимостиПродажПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаНДСПриПродажеПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаВПутиПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаНДСВПутиПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СтатьяДоходовРеглПоУмолчанию,
	|
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаНаСкладе,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаВыручкиОтПродаж,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаСебестоимостиПродаж,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаНДСПриПродаже,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаВПути,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаНДСВПути,
	|
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройка,
	|	ТекущаяТаблицаСчетовУчета.ИзмененныеДанные
	|
	|ПОМЕСТИТЬ ТекущаяТаблицаСчетовУчета
	|ИЗ
	|	&Таблица КАК ТекущаяТаблицаСчетовУчета;
	|
	|///////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДвиженияНоменклатурыСоСчетами.Организация,
	|	ДвиженияНоменклатурыСоСчетами.МестоУчета КАК Склад,
	|	ДвиженияНоменклатурыСоСчетами.АналитикаУчета КАК ГруппаФинансовогоУчета,
	|
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаНаСкладе, ДвиженияНоменклатурыСоСчетами.СчетУчетаНаСкладе) КАК СчетУчетаНаСкладе,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаВыручкиОтПродаж, ДвиженияНоменклатурыСоСчетами.СчетУчетаВыручкиОтПродаж) КАК СчетУчетаВыручкиОтПродаж,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаСебестоимостиПродаж, ДвиженияНоменклатурыСоСчетами.СчетУчетаСебестоимостиПродаж) КАК СчетУчетаСебестоимостиПродаж,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаНДСПриПродаже, ДвиженияНоменклатурыСоСчетами.СчетУчетаНДСПриПродаже) КАК СчетУчетаНДСПриПродаже,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаВПути, ДвиженияНоменклатурыСоСчетами.СчетУчетаВПути) КАК СчетУчетаВПути,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаНДСВПути, ДвиженияНоменклатурыСоСчетами.СчетУчетаНДСВПути) КАК СчетУчетаНДСВПути,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СтатьяДоходовРегл, ДвиженияНоменклатурыСоСчетами.СтатьяДоходовРегл) КАК СтатьяДоходовРегл,
	|
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаНаСкладеПоУмолчанию, ДвиженияНоменклатурыСоСчетами.СчетУчетаНаСкладеПоУмолчанию) КАК СчетУчетаНаСкладеПоУмолчанию,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаВыручкиОтПродажПоУмолчанию, ДвиженияНоменклатурыСоСчетами.СчетУчетаВыручкиОтПродажПоУмолчанию) КАК СчетУчетаВыручкиОтПродажПоУмолчанию,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаСебестоимостиПродажПоУмолчанию, ДвиженияНоменклатурыСоСчетами.СчетУчетаСебестоимостиПродажПоУмолчанию) КАК СчетУчетаСебестоимостиПродажПоУмолчанию,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаНДСПриПродажеПоУмолчанию, ДвиженияНоменклатурыСоСчетами.СчетУчетаНДСПриПродажеПоУмолчанию) КАК СчетУчетаНДСПриПродажеПоУмолчанию,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаВПутиПоУмолчанию, ДвиженияНоменклатурыСоСчетами.СчетУчетаВПутиПоУмолчанию) КАК СчетУчетаВПутиПоУмолчанию,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаНДСВПутиПоУмолчанию, ДвиженияНоменклатурыСоСчетами.СчетУчетаНДСВПутиПоУмолчанию) КАК СчетУчетаНДСВПутиПоУмолчанию,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СтатьяДоходовРеглПоУмолчанию, ДвиженияНоменклатурыСоСчетами.СтатьяДоходовРеглПоУмолчанию) КАК СтатьяДоходовРеглПоУмолчанию,
	|
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаНаСкладе, ДвиженияНоменклатурыСоСчетами.СчетУчетаНаСкладе) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ДвиженияНоменклатурыСоСчетами.ЕстьСобственныеТовары КАК ТребуетсяНастройкаСчетУчетаНаСкладе,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаВыручкиОтПродаж, ДвиженияНоменклатурыСоСчетами.СчетУчетаВыручкиОтПродаж) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И (ДвиженияНоменклатурыСоСчетами.ЕстьПродажаСобственныхТоваров ИЛИ ДвиженияНоменклатурыСоСчетами.ЕстьПродажаУслуг) КАК ТребуетсяНастройкаСчетУчетаВыручкиОтПродаж,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаСебестоимостиПродаж, ДвиженияНоменклатурыСоСчетами.СчетУчетаСебестоимостиПродаж) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ДвиженияНоменклатурыСоСчетами.ЕстьПродажаСобственныхТоваров КАК ТребуетсяНастройкаСчетУчетаСебестоимостиПродаж,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаНДСПриПродаже, ДвиженияНоменклатурыСоСчетами.СчетУчетаНДСПриПродаже) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ДвиженияНоменклатурыСоСчетами.ЕстьПродажаСНДС КАК ТребуетсяНастройкаСчетУчетаНДСПриПродаже,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаВПути, ДвиженияНоменклатурыСоСчетами.СчетУчетаВПути) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ДвиженияНоменклатурыСоСчетами.ЕстьРеализацияБезПереходаПраваСобственности КАК ТребуетсяНастройкаСчетУчетаВПути,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаНДСВПути, ДвиженияНоменклатурыСоСчетами.СчетУчетаНДСВПути) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ДвиженияНоменклатурыСоСчетами.ЕстьРеализацияБезПереходаПраваСобственности КАК ТребуетсяНастройкаСчетУчетаНДСВПути,
	|
	|	(ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаНаСкладе, ДвиженияНоменклатурыСоСчетами.СчетУчетаНаСкладе) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ДвиженияНоменклатурыСоСчетами.ЕстьСобственныеТовары) ИЛИ
	|	(ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаВПути, ДвиженияНоменклатурыСоСчетами.СчетУчетаВПути) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ДвиженияНоменклатурыСоСчетами.ЕстьРеализацияБезПереходаПраваСобственности) ИЛИ
	|	(ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаНДСВПути, ДвиженияНоменклатурыСоСчетами.СчетУчетаНДСВПути) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ДвиженияНоменклатурыСоСчетами.ЕстьРеализацияБезПереходаПраваСобственности) ИЛИ
	|	(ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаВыручкиОтПродаж, ДвиженияНоменклатурыСоСчетами.СчетУчетаВыручкиОтПродаж) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И (ДвиженияНоменклатурыСоСчетами.ЕстьПродажаСобственныхТоваров ИЛИ ДвиженияНоменклатурыСоСчетами.ЕстьПродажаУслуг)) ИЛИ
	|	(ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаНДСПриПродаже, ДвиженияНоменклатурыСоСчетами.СчетУчетаНДСПриПродаже) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ДвиженияНоменклатурыСоСчетами.ЕстьПродажаСНДС) ИЛИ
	|	(ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.СчетУчетаСебестоимостиПродаж, ДвиженияНоменклатурыСоСчетами.СчетУчетаСебестоимостиПродаж) = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И ДвиженияНоменклатурыСоСчетами.ЕстьПродажаСобственныхТоваров) КАК ТребуетсяНастройка,
	|	ЕСТЬNULL(ТекущаяТаблицаСчетовУчета.ИзмененныеДанные, ""СчетУчетаНаСкладе, СчетУчетаВыручкиОтПродаж, СчетУчетаСебестоимостиПродаж, СчетУчетаНДСПриПродаже, СчетУчетаВПути, СчетУчетаНДСВПути"") КАК ИзмененныеДанные
	|
	|ИЗ
	|	ТаблицаДвиженийСоСчетами КАК ДвиженияНоменклатурыСоСчетами
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ТекущаяТаблицаСчетовУчета КАК ТекущаяТаблицаСчетовУчета
	|	ПО
	|		ДвиженияНоменклатурыСоСчетами.Организация = ТекущаяТаблицаСчетовУчета.Организация
	|		И ДвиженияНоменклатурыСоСчетами.МестоУчета = ТекущаяТаблицаСчетовУчета.Склад
	|		И ДвиженияНоменклатурыСоСчетами.АналитикаУчета = ТекущаяТаблицаСчетовУчета.ГруппаФинансовогоУчета
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТекущаяТаблицаСчетовУчета.Организация,
	|	ТекущаяТаблицаСчетовУчета.Склад,
	|	ТекущаяТаблицаСчетовУчета.ГруппаФинансовогоУчета,
	|
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаНаСкладе,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаВыручкиОтПродаж,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаСебестоимостиПродаж,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаНДСПриПродаже,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаВПути,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаНДСВПути,
	|	ТекущаяТаблицаСчетовУчета.СтатьяДоходовРегл,
	|
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаНаСкладеПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаВыручкиОтПродажПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаСебестоимостиПродажПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаНДСПриПродажеПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаВПутиПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СчетУчетаНДСВПутиПоУмолчанию,
	|	ТекущаяТаблицаСчетовУчета.СтатьяДоходовРеглПоУмолчанию,
	|
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаНаСкладе,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаВыручкиОтПродаж,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаСебестоимостиПродаж,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаНДСПриПродаже,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаВПути,
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройкаСчетУчетаНДСВПути,
	|
	|	ТекущаяТаблицаСчетовУчета.ТребуетсяНастройка,
	|	ТекущаяТаблицаСчетовУчета.ИзмененныеДанные
	|ИЗ
	|	ТекущаяТаблицаСчетовУчета КАК ТекущаяТаблицаСчетовУчета
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ТаблицаДвиженийСоСчетами КАК ДвиженияНоменклатурыСоСчетами
	|	ПО
	|		ТекущаяТаблицаСчетовУчета.Организация = ДвиженияНоменклатурыСоСчетами.Организация
	|		И ТекущаяТаблицаСчетовУчета.Склад = ДвиженияНоменклатурыСоСчетами.МестоУчета
	|		И ТекущаяТаблицаСчетовУчета.ГруппаФинансовогоУчета = ДвиженияНоменклатурыСоСчетами.АналитикаУчета
	|ГДЕ
	|	ДвиженияНоменклатурыСоСчетами.Организация ЕСТЬ NULL";
	
	Таблица = Запрос.Выполнить().Выгрузить();
	
	ПоместитьВоВременноеХранилище(Новый Структура("СчетаУчетаНоменклатуры", Таблица), АдресХранилища);
	
КонецПроцедуры

Процедура НайтиДокументыСоответствующиеНастройкам(ТаблицаНастроек, Дата, ТаблицаДокументов) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ИсходнаяТаблица.Организация КАК Организация,
	|	ИсходнаяТаблица.Склад КАК Склад,
	|	ИсходнаяТаблица.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета
	|
	|ПОМЕСТИТЬ ТаблицаНастроек
	|ИЗ
	|	&ИсходнаяТаблица КАК ИсходнаяТаблица
	|ГДЕ
	|	ИсходнаяТаблица.ИзмененныеДанные <> """"
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаНастроек.Организация КАК Организация,
	|	Аналитика.КлючАналитики КАК АналитикаУчетаНоменклатуры
	|
	|ПОМЕСТИТЬ ВтАналитика
	|ИЗ
	|	РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаНастроек КАК ТаблицаНастроек
	|	ПО
	|		Аналитика.Склад = ТаблицаНастроек.Склад
	|		И Аналитика.Номенклатура.ГруппаФинансовогоУчета = ТаблицаНастроек.ГруппаФинансовогоУчета
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Документ,
	|	ОтражениеДокументовВРеглУчете.ДатаОтражения КАК ДатаОтражения,
	|	ОтражениеДокументовВРеглУчете.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.Обороты(, &ДатаОкончания, Регистратор,
	|		(Организация, АналитикаУчетаНоменклатуры) В (
	|			ВЫБРАТЬ
	|				Аналитика.Организация,
	|				Аналитика.АналитикаУчетаНоменклатуры
	|			ИЗ
	|				ВтАналитика КАК Аналитика
	|		)
	|	) КАК ДанныеРегистра
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
	|	ПО
	|		ДанныеРегистра.Регистратор = ОтражениеДокументовВРеглУчете.Регистратор
	|		И ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Документ,
	|	ОтражениеДокументовВРеглУчете.ДатаОтражения КАК ДатаОтражения,
	|	ОтражениеДокументовВРеглУчете.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(, &ДатаОкончания, Регистратор,
	|		ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга)
	|		И (АналитикаУчетаПоПартнерам.Организация, АналитикаУчетаНоменклатуры) В (
	|			ВЫБРАТЬ
	|				Аналитика.Организация,
	|				Аналитика.АналитикаУчетаНоменклатуры
	|			ИЗ
	|				ВтАналитика КАК Аналитика
	|		)
	|	) КАК ДанныеРегистра
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
	|	ПО
	|		ДанныеРегистра.Регистратор = ОтражениеДокументовВРеглУчете.Регистратор
	|		И ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Документ
	|;");
	ИсходнаяТаблица = ТаблицаНастроек.Выгрузить(, "Организация, Склад, ГруппаФинансовогоУчета, ИзмененныеДанные");
	Запрос.УстановитьПараметр("ИсходнаяТаблица", ИсходнаяТаблица);
	Запрос.УстановитьПараметр("ДатаОкончания", Дата);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТаблицаДокументов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли