
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОтборНабораЗаписей = Параметры.Отбор;
	
	Если Не ЗначениеЗаполнено(ОтборНабораЗаписей.Организация) Или Не ЗначениеЗаполнено(ОтборНабораЗаписей.ТорговаяТочка) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Не ПравоДоступа("Изменение", Метаданные.РегистрыСведений.ПараметрыТорговыхТочек) Тогда
		ТолькоПросмотр = Истина;
		Элементы.КнопкаИзменить.Доступность = Ложь;
	КонецЕсли;
	
	Для Каждого ЗаписьИстории Из НаборЗаписей Цикл
		Если ЗаписьИстории.ВидОперации = Перечисления.ВидыОперацийТорговыеТочки.Регистрация Тогда
			ПредставлениеОперации = НСтр("ru='Поставлена на учет'");
		ИначеЕсли ЗаписьИстории.ВидОперации = Перечисления.ВидыОперацийТорговыеТочки.ИзменениеПараметров Тогда
			ПредставлениеОперации = НСтр("ru='Изменены параметры'");
		Иначе
			ПредставлениеОперации = НСтр("ru='Снята с учета'");
		КонецЕсли;
		
		ЗаписьИстории.ПредставлениеОперации = ПредставлениеОперации;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНаборЗаписей

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	ПараметрыТорговойТочки = Элементы.НаборЗаписей.ТекущиеДанные;
	
	Если ПараметрыТорговойТочки.Период < '20150701' Тогда
		ПараметрыТорговойТочки.Период = '20150701';
		ТекстСообщения = НСтр("ru='Дата начала использования торговой точки не может быть ранее 01 июля 2015 г.'");
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = ТекстСообщения;
		СообщениеПользователю.Сообщить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаборЗаписейПередУдалением(Элемент, Отказ)
	
	Отказ = НаборЗаписей.Количество() = 1;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Отказ = ТекущиеДанные.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийТорговыеТочки.Регистрация");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаИзменить(Команда)
	
	Элементы.НаборЗаписей.ТекущийЭлемент = Элементы.НаборЗаписей.ПодчиненныеЭлементы.Период;
	Элементы.НаборЗаписей.ИзменитьСтроку();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаписатьИЗакрыть(Команда)
	
	ДанныеИзменены = Модифицированность;
	Записать();
	Закрыть(ДанныеИзменены);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Закрыть(Модифицированность);
	
КонецПроцедуры

#КонецОбласти


