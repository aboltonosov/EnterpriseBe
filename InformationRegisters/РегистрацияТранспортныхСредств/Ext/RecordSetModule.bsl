#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	Если Не ДополнительныеСвойства.Свойство("ДляПроведения")
		ИЛИ ДополнительныеСвойства.Свойство("РассчитыватьИзменения")
			И НЕ ДополнительныеСвойства.РассчитыватьИзменения Тогда
		Возврат;
	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период,
	|	ТаблицаПередЗаписью.Организация,
	|	ТаблицаПередЗаписью.ОсновноеСредство,
	|	ТаблицаПередЗаписью.ВключатьВНалоговуюБазу
	|ПОМЕСТИТЬ РегистрацияТранспортныхСредствПередЗаписью
	|ИЗ
	|	РегистрСведений.РегистрацияТранспортныхСредств КАК ТаблицаПередЗаписью
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка 
		Или Не ДополнительныеСвойства.Свойство("ДляПроведения")
		ИЛИ ДополнительныеСвойства.Свойство("РассчитыватьИзменения")
			И НЕ ДополнительныеСвойства.РассчитыватьИзменения Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Период            КАК Период,
	|	Таблица.Организация       КАК Организация,
	|	Таблица.ОсновноеСредство  КАК ОсновноеСредство,
	|	&Регистратор              КАК Документ
	|ПОМЕСТИТЬ РегистрацияТранспортныхСредствИзменение
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ) КАК Период,
	|		ТаблицаПередЗаписью.Организация КАК Организация,
	|		ТаблицаПередЗаписью.ОсновноеСредство КАК ОсновноеСредство
	|	ИЗ
	|		РегистрацияТранспортныхСредствПередЗаписью КАК ТаблицаПередЗаписью
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияТранспортныхСредств КАК ТаблицаПриЗаписи
	|			ПО (ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация)
	|				И (ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство)
	|				И (ТаблицаПриЗаписи.Регистратор = &Регистратор)
	|	ГДЕ
	|		(ТаблицаПередЗаписью.ВключатьВНалоговуюБазу <> ЕСТЬNULL(ТаблицаПриЗаписи.ВключатьВНалоговуюБазу, НЕОПРЕДЕЛЕНО)
	|			ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ)
	|			ИЛИ ТаблицаПриЗаписи.Организация ЕСТЬ NULL)
	|		И ТаблицаПередЗаписью.Организация.ЮридическоеФизическоеЛицо <> ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ),
	|		ТаблицаПриЗаписи.Организация,
	|		ТаблицаПриЗаписи.ОсновноеСредство
	|	ИЗ
	|		РегистрСведений.РегистрацияТранспортныхСредств КАК ТаблицаПриЗаписи
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрацияТранспортныхСредствПередЗаписью КАК ТаблицаПередЗаписью
	|			ПО ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|				И ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство
	|	ГДЕ
	|		(ТаблицаПриЗаписи.ВключатьВНалоговуюБазу <> ЕСТЬNULL(ТаблицаПередЗаписью.ВключатьВНалоговуюБазу, НЕОПРЕДЕЛЕНО)
	|			ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ)
	|			ИЛИ ТаблицаПередЗаписью.Организация ЕСТЬ NULL)
	|		И ТаблицаПриЗаписи.Организация.ЮридическоеФизическоеЛицо <> ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|		И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|
	|	) КАК Таблица
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ РегистрацияТранспортныхСредствПередЗаписью";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	Выборка.Следующий();
	
	СтруктураВременныеТаблицы.Вставить("РегистрацияТранспортныхСредствИзменение", Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли