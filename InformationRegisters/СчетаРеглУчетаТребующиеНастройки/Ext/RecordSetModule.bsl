#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытийФормы

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Для каждого СтрокаРегистра из ЭтотОбъект Цикл
		
		ОбязательныеИзмеренияРегистра = РегистрыСведений.СчетаРеглУчетаТребующиеНастройки.СписокИзмеренийРегистраПоВидуСчета(СтрокаРегистра.ВидСчета, Истина);
		ОбязательныеИзмеренияРегистра.Добавить("ВидСчета");
		
		Для каждого ИзмерениеРегистра из ОбязательныеИзмеренияРегистра Цикл
			Если Не ЗначениеЗаполнено(СтрокаРегистра[ИзмерениеРегистра]) Тогда
				Отказ = Истина;
				ТекстСообщения = НСтр("ru = 'При записи счетов регл. учета, требующих настроек, не заполнено измерение регистра ""%1"".'",
					ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
				ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ИзмерениеРегистра);
			КонецЕсли;
		КонецЦикла;
		
		ОграничениеТипаМестоУчета = РегистрыСведений.СчетаРеглУчетаТребующиеНастройки.ОграничениеТипаМестаУчетаПоВидуСчета(СтрокаРегистра.ВидСчета);
		Если ОграничениеТипаМестоУчета <> Неопределено И Не ОграничениеТипаМестоУчета.СодержитТип(ТипЗнч(СтрокаРегистра.МестоУчета)) Тогда
			Отказ = Истина;
			ТекстСообщения = НСтр("ru = 'При записи счетов регл. учета, требующих настроек, некорректно заполнено измерение ""Место учета"".'",
				ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
			ВызватьИсключение ТекстСообщения;
		КонецЕсли;
		
		ОграничениеТипаАналитикаУчета = РегистрыСведений.СчетаРеглУчетаТребующиеНастройки.ОграничениеТипаАналитикаУчетаПоВидуСчета(СтрокаРегистра.ВидСчета);
		Если ОграничениеТипаАналитикаУчета <> Неопределено И Не ОграничениеТипаАналитикаУчета.СодержитТип(ТипЗнч(СтрокаРегистра.АналитикаУчета)) Тогда
			Отказ = Истина;
			ТекстСообщения = НСтр("ru = 'При записи счетов регл. учета, требующих настроек, некорректно заполнено измерение ""Аналитика учета"".'",
				ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
			ВызватьИсключение ТекстСообщения;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли