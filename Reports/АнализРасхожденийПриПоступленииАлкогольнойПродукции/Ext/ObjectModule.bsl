#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ТекстЗапроса = СхемаКомпоновкиДанных.НаборыДанных.НаборДанных.Запрос;
	
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса, 
		"&ТекстЗапросаКоэффициентУпаковки", 
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки("КлассификаторАлкогольнойПродукцииЕГАИСУпаковки.Упаковка", "КлассификаторАлкогольнойПродукцииЕГАИС.Номенклатура"));
		
	СхемаКомпоновкиДанных.НаборыДанных.НаборДанных.Запрос = ТекстЗапроса;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - УправляемаяФорма - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - Структура - см. возвращаемое значение ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию().
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	Настройки.События.ПриСозданииНаСервере = Истина;
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   Форма - УправляемаяФорма - Форма отчета.
//   Отказ - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Передается из параметров обработчика "как есть".
//
// См. также:
//   "УправляемаяФорма.ПриСозданииНаСервере" в синтакс-помощнике.
//
Процедура ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	КомпоновщикНастроекФормы = ЭтаФорма.Отчет.КомпоновщикНастроек;
	Параметры = ЭтаФорма.Параметры;
	
	Если Параметры.Свойство("ПараметрКоманды")
		И Параметры.ПараметрыОтчет.Свойство("ДополнительныеПараметры") Тогда
		
		Если Параметры.ПараметрыОтчет.ДополнительныеПараметры.ИмяКоманды = "АнализРасхожденийПриПоступленииАлкогольнойПродукцииНакладная" Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
				|	ПоступлениеТоваровУслуг.ТоварноТранспортнаяНакладнаяЕГАИС КАК ТТН
				|ИЗ
				|	Документ.ПоступлениеТоваровУслуг КАК ПоступлениеТоваровУслуг
				|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ТТНВходящаяЕГАИС КАК ТТН
				|		ПО ПоступлениеТоваровУслуг.ТоварноТранспортнаяНакладнаяЕГАИС = ТТН.Ссылка
				|ГДЕ
				|	ПоступлениеТоваровУслуг.Ссылка В (&МассивСсылок)
				|	И НЕ ТТН.Ссылка ЕСТЬ NULL";
			
			Запрос.УстановитьПараметр("МассивСсылок", Параметры.ПараметрКоманды);
			МассивСсылок = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ТТН");
			
			ЭтаФорма.ФормаПараметры.Отбор.Вставить("ТТНСсылка", МассивСсылок);
		ИначеЕсли Параметры.ПараметрыОтчет.ДополнительныеПараметры.ИмяКоманды = "АнализРасхожденийПриПоступленииАлкогольнойПродукцииТТН_ЕГАИС" Тогда
			ЭтаФорма.ФормаПараметры.Отбор.Вставить("ТТНСсылка", Параметры.ПараметрКоманды);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли