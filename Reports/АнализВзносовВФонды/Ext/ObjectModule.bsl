#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Перем ДатаНачалаНП, ДатаКонцаНП;

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ИнициализироватьОтчет();
	
	СтандартнаяОбработка = ложь;
	ДокументРезультат.Очистить();
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	
	ЗначениеПараметра = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период"));
	Если ЗначениеПараметра <> Неопределено Тогда
		ДатаНачалаНП = Дата(ЗначениеПараметра.Значение.ДатаНачала);
		ДатаКонцаНП  = Дата(ЗначениеПараметра.Значение.ДатаОкончания);
	КонецЕсли;
	
	Если ДатаНачалаНП = '00010101' Тогда
		ДатаНачалаНП = НачалоМесяца(ТекущаяДатаСеанса());
	КонецЕсли;
	Если ДатаКонцаНП = '00010101' Тогда
		ДатаКонцаНП = КонецМесяца(ТекущаяДатаСеанса());
	КонецЕсли;
	
	НастройкиОтчета.Структура.Очистить();
	
	Группировка = НастройкиОтчета.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	Группировка.ПоляГруппировки.Элементы.Очистить();
	Группировка.Выбор.Элементы.Очистить();
	Группировка.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	
	НастройкиОтчета.Выбор.Элементы.Очистить();
	ОтчетыКлиентСервер.ДобавитьВыбранноеПоле(НастройкиОтчета.Выбор, "Организация");
	ОтчетыКлиентСервер.ДобавитьВыбранноеПоле(НастройкиОтчета.Выбор, "ГоловнаяОрганизация");
	ОтчетыКлиентСервер.ДобавитьВыбранноеПоле(НастройкиОтчета.Выбор, "ФизическоеЛицо");
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, , , Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
		
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , , Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	
	ТаблицаЗначений =  Новый ТаблицаЗначений;
	ПроцессорВывода.УстановитьОбъект(ТаблицаЗначений);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	Если ТаблицаЗначений.Количество() > 0 Тогда
		СформироватьМакет(ДокументРезультат, ТаблицаЗначений, ДанныеРасшифровки);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьОтчет() Экспорт
	
	ЗарплатаКадрыОбщиеНаборыДанных.ЗаполнитьОбщиеИсточникиДанныхОтчета(ЭтотОбъект);
	
КонецПроцедуры

// Для общей формы "Форма отчета" подсистемы "Варианты отчетов".
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПриСозданииНаСервере = Истина;
	
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   Форма - УправляемаяФорма - Форма отчета.
//   Отказ - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Передается из параметров обработчика "как есть".
//
// См. также:
//   "УправляемаяФорма.ПриСозданииНаСервере" в синтакс-помощнике.
//
Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	ИнициализироватьОтчет();
	ЗначениеВДанныеФормы(ЭтотОбъект, Форма.Отчет);
	
КонецПроцедуры

// 4D:ERP для Беларуси, Яна, 08.09.2017 15:09:16 
// Локализация документов зарплатного модуля, №15480
// {
Процедура СформироватьМакет(ДокументРезультат, ФизЛицаОрганизаций, ДанныеРасшифровки)
	
	// Создание запроса и установка всех необходимых параметров.
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц; 
	Запрос.УстановитьПараметр("ТаблицаФизЛицОрганизаций", ФизЛицаОрганизаций);
	Запрос.УстановитьПараметр("НачалоПериода", ДатаНачалаНП);
	Запрос.УстановитьПараметр("КонецПериода",  ДатаКонцаНП);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаФизЛиц.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ТаблицаФизЛиц.Организация КАК Организация,
	|	ТаблицаФизЛиц.ГоловнаяОрганизация КАК ГоловнаяОрганизация
	|ПОМЕСТИТЬ ВТФизЛицаОрганизаций
	|ИЗ
	|	&ТаблицаФизЛицОрганизаций КАК ТаблицаФизЛиц
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизическоеЛицо,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ФизЛицаОрганизаций.Организация КАК Организация
	|ИЗ
	|	ВТФизЛицаОрганизаций КАК ФизЛицаОрганизаций";
	СписокОрганизаций = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Организация");
	
	Организация = Неопределено;
	Если СписокОрганизаций.Количество() = 1 Тогда
		Организация = СписокОрганизаций[0];
	КонецЕсли;	
		
	УчетСтраховыхВзносов.СформироватьВТРасширенныеСведенияОДоходахИВзносах(ДатаНачалаНП, ДатаКонцаНП, Организация, Запрос.МенеджерВременныхТаблиц, Истина, Истина, Истина, ,  , Макс(ТекущаяДатаСеанса(), ДатаКонцаНП));
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ФизЛицаОрганизаций.ФизическоеЛицо
	|ИЗ
	|	ВТФизЛицаОрганизаций КАК ФизЛицаОрганизаций
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ИсчисленныеСтраховыеВзносы КАК ИсчисленныеСтраховыеВзносы
	|		ПО ФизЛицаОрганизаций.ФизическоеЛицо = ИсчисленныеСтраховыеВзносы.ФизическоеЛицо
	|			И ФизЛицаОрганизаций.Организация = ИсчисленныеСтраховыеВзносы.Организация
	|			И ФизЛицаОрганизаций.ГоловнаяОрганизация = ИсчисленныеСтраховыеВзносы.ГоловнаяОрганизация
	|ГДЕ
	|	НАЧАЛОПЕРИОДА(ИсчисленныеСтраховыеВзносы.ДатаПолученияДохода, КВАРТАЛ) < НАЧАЛОПЕРИОДА(ИсчисленныеСтраховыеВзносы.Период, КВАРТАЛ)
	|	И ИсчисленныеСтраховыеВзносы.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ФизЛицаОрганизаций.ФизическоеЛицо
	|ИЗ
	|	ВТФизЛицаОрганизаций КАК ФизЛицаОрганизаций
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СведенияОДоходахСтраховыеВзносы КАК СведенияОДоходахСтраховыеВзносы
	|		ПО ФизЛицаОрганизаций.ФизическоеЛицо = СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо
	|			И ФизЛицаОрганизаций.Организация = СведенияОДоходахСтраховыеВзносы.Организация
	|			И ФизЛицаОрганизаций.ГоловнаяОрганизация = СведенияОДоходахСтраховыеВзносы.ГоловнаяОрганизация
	|ГДЕ
	|	НАЧАЛОПЕРИОДА(СведенияОДоходахСтраховыеВзносы.ДатаПолученияДохода, КВАРТАЛ) < НАЧАЛОПЕРИОДА(СведенияОДоходахСтраховыеВзносы.Период, КВАРТАЛ)
	|	И СведенияОДоходахСтраховыеВзносы.Период МЕЖДУ &НачалоПериода И &КонецПериода";
	
	Если Не Запрос.Выполнить().Пустой() Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СведенияОДоходах.Организация КАК Организация,
		|	СведенияОДоходах.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СведенияОДоходах.Период КАК МесяцПолученияДохода,
		|	СведенияОДоходах.ПериодРегистрации КАК МесяцРегистрацииДохода,
		|	ВЫБОР
		|		КОГДА СведенияОДоходах.Начисление ЕСТЬ NULL 
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		КОГДА СведенияОДоходах.Начисление = НЕОПРЕДЕЛЕНО
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		ИНАЧЕ СведенияОДоходах.Начисление
		|	КОНЕЦ КАК ВидРасчета,
		|	СведенияОДоходах.ВидТарифаСтраховыхВзносов,
		|	СведенияОДоходах.Инвалид КАК Инвалид, 
		|	СведенияОДоходах.ЗастрахованФСЗНкакЕАЭС,
		|	СУММА(СведенияОДоходах.Сумма) КАК НачисленоВсего,
		|	СУММА(СведенияОДоходах.БольничныйИзФСЗН) КАК БольничныйИзФСЗН,
		|	СУММА(СведенияОДоходах.ПособияИзФСЗН) КАК ПособияИзФСЗН,
		|	СУММА(СведенияОДоходах.НеОблагаетсяПолностью) КАК НеОблагаетсяПолностью,
		|	СУММА(СведенияОДоходах.БазаФСЗН) КАК БазаФСЗНПолная,
		|	СУММА(СведенияОДоходах.БазаФСЗН - СведенияОДоходах.СуммаПревысившаяПределФСЗН) КАК БазаФСЗН,
		|	СУММА(СведенияОДоходах.БазаБелгосстрах) КАК БазаБелгосстрах,
		|	СУММА(СведенияОДоходах.СуммаПревысившаяПределФСЗН) КАК СуммаПревысившаяПределФСЗН,
		|	СУММА(СведенияОДоходах.ФСЗН) КАК ФСЗН,
		|	СУММА(СведенияОДоходах.Белгосстрах) КАК Белгосстрах,
		|	СУММА(СведенияОДоходах.ПенсионныйФонд) КАК ПенсионныйФонд,
		|	СУММА(СведенияОДоходах.ПС) КАК ПС,
		|	СУММА(СведенияОДоходах.СС) КАК СС 		
		|ПОМЕСТИТЬ ВТДанныеБезОпоздавшихИсправлений
		|ИЗ
		|	ВТРасширенныеСведенияОДоходахИВзносах КАК СведенияОДоходах
		|ГДЕ
		|	СведенияОДоходах.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
		|
		|СГРУППИРОВАТЬ ПО
		|	СведенияОДоходах.ФизическоеЛицо,
		|	СведенияОДоходах.Организация,
		|	СведенияОДоходах.ВидТарифаСтраховыхВзносов,
		|	СведенияОДоходах.Инвалид,
		|	ВЫБОР
		|		КОГДА СведенияОДоходах.Начисление ЕСТЬ NULL 
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		КОГДА СведенияОДоходах.Начисление = НЕОПРЕДЕЛЕНО
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		ИНАЧЕ СведенияОДоходах.Начисление
		|	КОНЕЦ,
		|	СведенияОДоходах.Период,
		|	СведенияОДоходах.ПериодРегистрации,
		|	СведенияОДоходах.ЗастрахованФСЗНкакЕАЭС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТРасширенныеСведенияОДоходахИВзносах
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТРазмерыТарифныхСтавок
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТДанныеОбИнвалидности
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТИспользуемыеТарифы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТОписаниеТарифов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТВидыДеятельности
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(ДатыПолученияДохода.ДатаПолученияДохода) КАК ПоздняяДатаПолученияДохода,
		|	МИНИМУМ(ДатыПолученияДохода.ДатаПолученияДохода) КАК РанняяДатаПолученияДохода
		|ИЗ
		|	(ВЫБРАТЬ
		|		ИсчисленныеСтраховыеВзносы.ДатаПолученияДохода КАК ДатаПолученияДохода
		|	ИЗ
		|		ВТФизЛицаОрганизаций КАК ФизЛицаОрганизаций
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ИсчисленныеСтраховыеВзносы КАК ИсчисленныеСтраховыеВзносы
		|			ПО ФизЛицаОрганизаций.ФизическоеЛицо = ИсчисленныеСтраховыеВзносы.ФизическоеЛицо
		|				И ФизЛицаОрганизаций.Организация = ИсчисленныеСтраховыеВзносы.Организация
		|				И ФизЛицаОрганизаций.ГоловнаяОрганизация = ИсчисленныеСтраховыеВзносы.ГоловнаяОрганизация
		|	ГДЕ
		|		ИсчисленныеСтраховыеВзносы.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		СведенияОДоходахСтраховыеВзносы.ДатаПолученияДохода
		|	ИЗ
		|		ВТФизЛицаОрганизаций КАК ФизЛицаОрганизаций
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СведенияОДоходахСтраховыеВзносы КАК СведенияОДоходахСтраховыеВзносы
		|			ПО ФизЛицаОрганизаций.ФизическоеЛицо = СведенияОДоходахСтраховыеВзносы.ФизическоеЛицо
		|				И ФизЛицаОрганизаций.Организация = СведенияОДоходахСтраховыеВзносы.Организация
		|				И ФизЛицаОрганизаций.ГоловнаяОрганизация = СведенияОДоходахСтраховыеВзносы.ГоловнаяОрганизация
		|	ГДЕ
		|		СведенияОДоходахСтраховыеВзносы.Период МЕЖДУ &НачалоПериода И &КонецПериода) КАК ДатыПолученияДохода";
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.РанняяДатаПолученияДохода) Тогда
			УточненнаяДатаНачалаНП = Мин(ДатаНачалаНП, Выборка.РанняяДатаПолученияДохода);
			УточненнаяДатаКонцаНП = Макс(ДатаКонцаНП, Выборка.ПоздняяДатаПолученияДохода);
		Иначе 	
			УточненнаяДатаНачалаНП = ДатаНачалаНП;
			УточненнаяДатаКонцаНП = ДатаКонцаНП;
		КонецЕсли;
		
		УчетСтраховыхВзносов.СформироватьВТРасширенныеСведенияОДоходахИВзносах(УточненнаяДатаНачалаНП, УточненнаяДатаКонцаНП, Организация, Запрос.МенеджерВременныхТаблиц, Истина, Истина, Истина,,, Макс(ТекущаяДатаСеанса(), УточненнаяДатаКонцаНП));
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СведенияОДоходах.Организация КАК Организация,
		|	СведенияОДоходах.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СведенияОДоходах.Период КАК МесяцПолученияДохода,
		|	СведенияОДоходах.ПериодРегистрации КАК МесяцРегистрацииДохода,
		|	ВЫБОР
		|		КОГДА СведенияОДоходах.Начисление ЕСТЬ NULL 
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		КОГДА СведенияОДоходах.Начисление = НЕОПРЕДЕЛЕНО
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		ИНАЧЕ СведенияОДоходах.Начисление
		|	КОНЕЦ КАК ВидРасчета,
		|	СведенияОДоходах.ВидТарифаСтраховыхВзносов КАК ВидТарифаСтраховыхВзносов,
		|	СведенияОДоходах.Инвалид КАК Инвалид,
		|	СведенияОДоходах.ЗастрахованФСЗНкакЕАЭС,
		|	СУММА(СведенияОДоходах.Сумма) КАК НачисленоВсего,
		|	СУММА(СведенияОДоходах.БольничныйИзФСЗН) КАК БольничныйИзФСЗН,
		|	СУММА(СведенияОДоходах.ПособияИзФСЗН) КАК ПособияИзФСЗН,
		|	СУММА(СведенияОДоходах.НеОблагаетсяПолностью) КАК НеОблагаетсяПолностью,
		|	СУММА(СведенияОДоходах.БазаФСЗН) КАК БазаФСЗНПолная,
		|	СУММА(СведенияОДоходах.БазаФСЗН - СведенияОДоходах.СуммаПревысившаяПределФСЗН) КАК БазаФСЗН,
		|	СУММА(СведенияОДоходах.БазаБелгосстрах) КАК БазаБелгосстрах,
		|	СУММА(СведенияОДоходах.СуммаПревысившаяПределФСЗН) КАК СуммаПревысившаяПределФСЗН,
		|	СУММА(СведенияОДоходах.ФСЗН) КАК ФСЗН,
		|	СУММА(СведенияОДоходах.Белгосстрах) КАК Белгосстрах,
		|	СУММА(СведенияОДоходах.ПенсионныйФонд) КАК ПенсионныйФонд,
		|	СУММА(СведенияОДоходах.ПС) КАК ПС,
		|	СУММА(СведенияОДоходах.СС) КАК СС 		
		|ПОМЕСТИТЬ ВТДанныеДляОтчета
		|ИЗ
		|	ВТРасширенныеСведенияОДоходахИВзносах КАК СведенияОДоходах
		|ГДЕ
		|	СведенияОДоходах.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
		|
		|СГРУППИРОВАТЬ ПО
		|	СведенияОДоходах.ФизическоеЛицо,
		|	СведенияОДоходах.Организация,
		|	СведенияОДоходах.ВидТарифаСтраховыхВзносов,
		|	СведенияОДоходах.Инвалид,
		|	ВЫБОР
		|		КОГДА СведенияОДоходах.Начисление ЕСТЬ NULL 
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		КОГДА СведенияОДоходах.Начисление = НЕОПРЕДЕЛЕНО
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		ИНАЧЕ СведенияОДоходах.Начисление
		|	КОНЕЦ,
		|	СведенияОДоходах.Период,
		|	СведенияОДоходах.ПериодРегистрации,
		|	СведенияОДоходах.ЗастрахованФСЗНкакЕАЭС
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеБезОпоздавшихИсправлений.Организация,
		|	ДанныеБезОпоздавшихИсправлений.ФизическоеЛицо,
		|	ДанныеБезОпоздавшихИсправлений.МесяцПолученияДохода,
		|	ДанныеБезОпоздавшихИсправлений.МесяцРегистрацииДохода,
		|	ДанныеБезОпоздавшихИсправлений.ВидРасчета,
		|	ДанныеБезОпоздавшихИсправлений.ВидТарифаСтраховыхВзносов,
		|	ДанныеБезОпоздавшихИсправлений.Инвалид,
		|	ДанныеБезОпоздавшихИсправлений.ЗастрахованФСЗНкакЕАЭС,
		|	ДанныеБезОпоздавшихИсправлений.НачисленоВсего КАК НачисленоВсего,
		|	ДанныеБезОпоздавшихИсправлений.БольничныйИзФСЗН КАК БольничныйИзФСЗН,
		|	ДанныеБезОпоздавшихИсправлений.ПособияИзФСЗН КАК ПособияИзФСЗН,
		|	ДанныеБезОпоздавшихИсправлений.НеОблагаетсяПолностью КАК НеОблагаетсяПолностью,
		|	ДанныеБезОпоздавшихИсправлений.БазаФСЗНПолная КАК БазаФСЗНПолная,
		|	ДанныеБезОпоздавшихИсправлений.БазаФСЗН КАК БазаФСЗН,
		|	ДанныеБезОпоздавшихИсправлений.БазаБелгосстрах КАК БазаБелгосстрах,
		|	ДанныеБезОпоздавшихИсправлений.СуммаПревысившаяПределФСЗН КАК СуммаПревысившаяПределФСЗН,
		|	ДанныеБезОпоздавшихИсправлений.ФСЗН КАК ФСЗН,
		|	ДанныеБезОпоздавшихИсправлений.Белгосстрах КАК Белгосстрах,
		|	ДанныеБезОпоздавшихИсправлений.ПенсионныйФонд КАК ПенсионныйФонд,
		|	ДанныеБезОпоздавшихИсправлений.ПС КАК ПС,
		|	ДанныеБезОпоздавшихИсправлений.СС КАК СС 
		|ИЗ
		|	ВТДанныеБезОпоздавшихИсправлений КАК ДанныеБезОпоздавшихИсправлений";

	Иначе
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СведенияОДоходах.Организация КАК Организация,
		|	СведенияОДоходах.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СведенияОДоходах.Период КАК МесяцПолученияДохода,
		|	СведенияОДоходах.ПериодРегистрации КАК МесяцРегистрацииДохода,
		|	ВЫБОР
		|		КОГДА СведенияОДоходах.Начисление ЕСТЬ NULL 
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		КОГДА СведенияОДоходах.Начисление = НЕОПРЕДЕЛЕНО
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		ИНАЧЕ СведенияОДоходах.Начисление
		|	КОНЕЦ КАК ВидРасчета,
		|	СведенияОДоходах.ВидТарифаСтраховыхВзносов КАК ВидТарифаСтраховыхВзносов,
		|	СведенияОДоходах.Инвалид КАК Инвалид,
		|	СведенияОДоходах.ЗастрахованФСЗНкакЕАЭС,
		|	СУММА(СведенияОДоходах.Сумма) КАК НачисленоВсего,
		|	СУММА(СведенияОДоходах.БольничныйИзФСЗН) КАК БольничныйИзФСЗН,
		|	СУММА(СведенияОДоходах.ПособияИзФСЗН) КАК ПособияИзФСЗН,
		|	СУММА(СведенияОДоходах.НеОблагаетсяПолностью) КАК НеОблагаетсяПолностью,
		|	СУММА(СведенияОДоходах.БазаФСЗН) КАК БазаФСЗНПолная,
		|	СУММА(СведенияОДоходах.БазаФСЗН - СведенияОДоходах.СуммаПревысившаяПределФСЗН) КАК БазаФСЗН,
		|	СУММА(СведенияОДоходах.БазаБелгосстрах) КАК БазаБелгосстрах,
		|	СУММА(СведенияОДоходах.СуммаПревысившаяПределФСЗН) КАК СуммаПревысившаяПределФСЗН,
		|	СУММА(СведенияОДоходах.ФСЗН) КАК ФСЗН,
		|	СУММА(СведенияОДоходах.Белгосстрах) КАК Белгосстрах,
		|	СУММА(СведенияОДоходах.ПенсионныйФонд) КАК ПенсионныйФонд,
		|	СУММА(СведенияОДоходах.ПС) КАК ПС,
		|	СУММА(СведенияОДоходах.СС) КАК СС 		
		|ПОМЕСТИТЬ ВТДанныеДляОтчета
		|ИЗ
		|	ВТРасширенныеСведенияОДоходахИВзносах КАК СведенияОДоходах
		|ГДЕ
		|	СведенияОДоходах.ПериодРегистрации МЕЖДУ &НачалоПериода И &КонецПериода
		|
		|СГРУППИРОВАТЬ ПО
		|	СведенияОДоходах.ФизическоеЛицо,
		|	СведенияОДоходах.Организация,
		|	СведенияОДоходах.ВидТарифаСтраховыхВзносов,
		|	СведенияОДоходах.Инвалид,
		|	ВЫБОР
		|		КОГДА СведенияОДоходах.Начисление ЕСТЬ NULL 
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		КОГДА СведенияОДоходах.Начисление = НЕОПРЕДЕЛЕНО
		|			ТОГДА СведенияОДоходах.ВидДохода
		|		ИНАЧЕ СведенияОДоходах.Начисление
		|	КОНЕЦ,
		|	СведенияОДоходах.Период,
		|	СведенияОДоходах.ПериодРегистрации,
		|	СведенияОДоходах.ЗастрахованФСЗНкакЕАЭС";
	КонецЕсли;
	Запрос.Выполнить();
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеДляОтчета.ФизическоеЛицо,
		|	ДанныеДляОтчета.МесяцПолученияДохода КАК Период
		|ПОМЕСТИТЬ ВТФизическиеЛицаПериоды
		|ИЗ
		|	ВТДанныеДляОтчета КАК ДанныеДляОтчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеДляОтчета.Организация,
		|	ДанныеДляОтчета.МесяцПолученияДохода КАК Период
		|ПОМЕСТИТЬ ВТОрганизациПериоды
		|ИЗ
		|	ВТДанныеДляОтчета КАК ДанныеДляОтчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДАТАВРЕМЯ(1, 1, 1) КАК Период,
		|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК ФизическоеЛицо,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ПустаяСсылка) КАК ВидЗастрахованногоЛица
		|ПОМЕСТИТЬ Представления_КадровыеДанныеФизическихЛиц
		|ИЗ
		|	ВТФизическиеЛицаПериоды КАК ФизическиеЛицаПериоды
		|ГДЕ
		|	""ТолькоРазрешенные"" = ИСТИНА";
		
	ЗарплатаКадрыОбщиеНаборыДанных.ЗаменитьЗапросыКПредставлениямВиртуальныхТаблиц(Запрос.Текст);
	Запрос.Выполнить();
	
	// Таблица со взносами на страхование ФСС_НС
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних("СтавкаВзносаНаСтрахованиеОтНесчастныхСлучаев",	
		Запрос.МенеджерВременныхТаблиц,
		Истина,
		ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра("ВТОрганизациПериоды", "Организация"),,"ВТСтавкиВзносовБГС");

	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеДляОтчета.Организация КАК Организация,
	|	ДанныеДляОтчета.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДанныеДляОтчета.МесяцПолученияДохода КАК МесяцПолученияДохода,
	|	ДанныеДляОтчета.МесяцРегистрацииДохода КАК МесяцРегистрацииДохода,
	|	ДанныеДляОтчета.ВидРасчета КАК ВидРасчета,
	|	ДанныеДляОтчета.ВидТарифаСтраховыхВзносов КАК ВидТарифаСтраховыхВзносов,
	|	ДанныеДляОтчета.Инвалид КАК Инвалид,
	|	ДанныеДляОтчета.ЗастрахованФСЗНкакЕАЭС КАК ЗастрахованФСЗНкакЕАЭС,
	|	СУММА(ДанныеДляОтчета.НачисленоВсего) КАК НачисленоВсего,
	|	СУММА(ДанныеДляОтчета.БольничныйИзФСЗН) КАК БольничныйИзФСЗН,
	|	СУММА(ДанныеДляОтчета.ПособияИзФСЗН) КАК ПособияИзФСЗН,
	|	СУММА(ДанныеДляОтчета.НеОблагаетсяПолностью) КАК НеОблагаетсяПолностью,
	|	СУММА(ДанныеДляОтчета.БазаФСЗНПолная) КАК БазаФСЗНПолная,
	|	СУММА(ДанныеДляОтчета.БазаФСЗН) КАК БазаФСЗН,
	//|	СУММА(ДанныеДляОтчета.БазаБелгосстрах) КАК БазаБелгосстрах,
	|	СУММА(ДанныеДляОтчета.СуммаПревысившаяПределФСЗН) КАК СуммаПревысившаяПределФСЗН,
	|	СУММА(ДанныеДляОтчета.ФСЗН) КАК ФСЗН,
	//|	СУММА(ДанныеДляОтчета.Белгосстрах) КАК Белгосстрах,
	|	СУММА(ДанныеДляОтчета.ПенсионныйФонд) КАК ПенсионныйФонд,
	|	СУММА(ДанныеДляОтчета.ПС) КАК ПС,
	|	СУММА(ДанныеДляОтчета.СС) КАК СС,
	|	КадровыеДанныеФизическихЛиц.ВидЗастрахованногоЛица КАК ВидЗастрахованногоЛица,
	|	РазмерыТарифныхСтавок.ФСЗН КАК СтавкаФСЗН,
	|	РазмерыТарифныхСтавок.ПенсионныйФонд КАК СтавкаПенсионныйФонд,
	|	РазмерыТарифныхСтавок.ФСЗНИнГрЕАЭС КАК СтавкаФСЗНИнГрЕАЭС,
	|	РазмерыТарифныхСтавок.ПС КАК СтавкаПС,
	|	РазмерыТарифныхСтавок.СС КАК СтавкаСС,
	|	РазмерыТарифныхСтавок.ФСЗН + РазмерыТарифныхСтавок.ПенсионныйФонд КАК СтавкаСуммарныйТариф
	|ИЗ
	|	ВТДанныеДляОтчета КАК ДанныеДляОтчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРазмерыТарифныхСтавок КАК РазмерыТарифныхСтавок
	|		ПО ДанныеДляОтчета.ВидТарифаСтраховыхВзносов = РазмерыТарифныхСтавок.ВидТарифа
	|			И (НАЧАЛОПЕРИОДА(ДанныеДляОтчета.МесяцПолученияДохода, ГОД) = РазмерыТарифныхСтавок.Период)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Представления_КадровыеДанныеФизическихЛиц КАК КадровыеДанныеФизическихЛиц
	|		ПО ДанныеДляОтчета.ФизическоеЛицо = КадровыеДанныеФизическихЛиц.ФизическоеЛицо
	|			И ДанныеДляОтчета.МесяцПолученияДохода = КадровыеДанныеФизическихЛиц.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДляОтчета.Организация,
	|	ДанныеДляОтчета.ФизическоеЛицо,
	|	ДанныеДляОтчета.ВидРасчета,
	|	ДанныеДляОтчета.ВидТарифаСтраховыхВзносов,
	|	ДанныеДляОтчета.МесяцПолученияДохода,
	|	ДанныеДляОтчета.МесяцРегистрацииДохода,
	|	ДанныеДляОтчета.Инвалид,
	|	ДанныеДляОтчета.ЗастрахованФСЗНкакЕАЭС,
	|	КадровыеДанныеФизическихЛиц.ВидЗастрахованногоЛица,
	|	РазмерыТарифныхСтавок.ФСЗН,
	|	РазмерыТарифныхСтавок.ПенсионныйФонд,
	|	РазмерыТарифныхСтавок.ФСЗНИнГрЕАЭС,
	|	РазмерыТарифныхСтавок.ПС,
	|	РазмерыТарифныхСтавок.СС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДляОтчета.Организация,
	|	ДанныеДляОтчета.ФизическоеЛицо,
	|	ДанныеДляОтчета.МесяцПолученияДохода,
	|	ДанныеДляОтчета.МесяцРегистрацииДохода,
	|	ДанныеДляОтчета.ВидРасчета,
	|	ДанныеДляОтчета.Инвалид,
	|	КадровыеДанныеФизическихЛиц.ВидЗастрахованногоЛица КАК СтатусЗастрахованногоЛица,
	|	ДанныеДляОтчета.ВидТарифаСтраховыхВзносов КАК ВидТарифаСтраховыхВзносов,
	|	СУММА(ДанныеДляОтчета.НачисленоВсего) КАК НачисленоВсего,
	|	СУММА(ДанныеДляОтчета.БазаБелгосстрах) КАК БазаБелгосстрах,
	|	СУММА(ДанныеДляОтчета.Белгосстрах) КАК Белгосстрах,
	|	ВТСтавкиВзносовБГС.Ставка КАК СтавкаБГС
	|ИЗ
	|	ВТДанныеДляОтчета КАК ДанныеДляОтчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ Представления_КадровыеДанныеФизическихЛиц КАК КадровыеДанныеФизическихЛиц
	|		ПО ДанныеДляОтчета.ФизическоеЛицо = КадровыеДанныеФизическихЛиц.ФизическоеЛицо
	|			И ДанныеДляОтчета.МесяцПолученияДохода = КадровыеДанныеФизическихЛиц.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтавкиВзносовБГС КАК ВТСтавкиВзносовБГС
	|		ПО (ВТСтавкиВзносовБГС.Организация = ДанныеДляОтчета.Организация)
	|			И (ВТСтавкиВзносовБГС.Период = ДанныеДляОтчета.МесяцПолученияДохода)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДляОтчета.Организация,
	|	ДанныеДляОтчета.ФизическоеЛицо,
	|	ДанныеДляОтчета.ВидРасчета,
	|	ДанныеДляОтчета.Инвалид,
	|	ДанныеДляОтчета.МесяцПолученияДохода,
	|	ДанныеДляОтчета.МесяцРегистрацииДохода,
	|	КадровыеДанныеФизическихЛиц.ВидЗастрахованногоЛица,
	|	ДанныеДляОтчета.ВидТарифаСтраховыхВзносов,
	|	ВТСтавкиВзносовБГС.Ставка";
	
	МассивРезультатовЗапроса = Запрос.ВыполнитьПакет();
	
	КоличествоРезультатов = МассивРезультатовЗапроса.Количество();
	ТаблицаВзносыБГС = МассивРезультатовЗапроса[КоличествоРезультатов-1].Выгрузить();
	ТаблицаВзносы = МассивРезультатовЗапроса[КоличествоРезультатов-2].Выгрузить();
	
	ВнешниеНаборыДанных = Новый Структура;
	//ВнешниеНаборыДанных.Вставить("ДанныеПФР", ТаблицаВзносы);
	//ВнешниеНаборыДанных.Вставить("ДанныеФОМС", ТаблицаВзносы);
	//ВнешниеНаборыДанных.Вставить("ДанныеФСС", ТаблицаВзносы);
	//ВнешниеНаборыДанных.Вставить("ДанныеФСС_НС", ТаблицаВзносыФСС_НС);
	
	ВнешниеНаборыДанных.Вставить("ДанныеФСЗН", ТаблицаВзносы);
	ВнешниеНаборыДанных.Вставить("ДанныеБГС", ТаблицаВзносыБГС);

	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	НастройкиКомпоновки = КомпоновщикНастроек.ПолучитьНастройки();
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(ЭтотОбъект.СхемаКомпоновкиДанных, НастройкиКомпоновки);
		
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки, истина);
	
	УстановитьПараметрыВыводаТабличногоДокумента(ДокументРезультат);
	
КонецПроцедуры
// }
// 4D

Процедура УстановитьПараметрыВыводаТабличногоДокумента(ДокументРезультат)
	ДокументРезультат.Автомасштаб 			= 	Истина;
	ДокументРезультат.ОриентацияСтраницы 	= 	ОриентацияСтраницы.Портрет;
	ДокументРезультат.ТолькоПросмотр		= 	Истина;
	ДокументРезультат.ПолеСверху			= 	5;
	ДокументРезультат.ПолеСнизу				= 	0;
	ДокументРезультат.ПолеСлева				= 	15;
КонецПроцедуры

#КонецОбласти

#КонецЕсли