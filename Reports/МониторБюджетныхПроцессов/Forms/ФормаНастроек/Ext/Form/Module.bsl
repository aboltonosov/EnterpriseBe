
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ВосстановитьНастройки();
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьТекстПримечения();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ГруппировкиПриИзменении(Элемент)
	
	УстановитьТекстПримечения();
	УправлениеФормой();

КонецПроцедуры

&НаКлиенте
Процедура МодельБюджетированияПриИзменении(Элемент)
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидПериодаПриИзменении(Элемент)
	
	ПривестиЗначениеПериода(ВидПериода);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПериодНачалоВыбораЗавершение", ЭтотОбъект);
	
	ОбщегоНазначенияУТКлиент.ВыбратьПериодИзСписка(
				ЭтаФорма,
				Элемент,
				ВидПериода,
				НачалоПериода,
				ОписаниеОповещения);

КонецПроцедуры

&НаКлиенте
Процедура ТипОтчетаПриИзменении(Элемент)
	
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьНастройкиИСформировать(Команда)

    СписокПараметров = ПолучитьТекущиеНастройки();
	СписокПараметров.Вставить("Сформировать", Истина);

    Закрыть(СписокПараметров);

КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройки(Команда)
	
	СписокПараметров = ПолучитьТекущиеНастройки();
	
    Закрыть(СписокПараметров);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьТекстПримечения()
	
	Если Группировки = "Вариант1" Тогда
		ТекстПримечания = НСтр("ru = 'В отчете отображаются все задачи, период планирования которых 
			|входит в период отчета. На диаграмму будут выведены задачи, 
			|которые начинаются до периода планирования или заканчиваются после.
			|Вариант отчета предназначен для обзора всех задач бюджетного процесса'");
	Иначе
		ТекстПримечания = НСтр("ru = 'В отчете отображаются все задачи, период выполнения которых 
			|входит в период отчета.
			|Вариант отчета предназначен для контроля исполнения задач бюджетного процесса'");
	КонецЕсли;
	
	Элементы.ПримечаниеКВариантуОтчета.Заголовок = ТекстПримечания;
	
КонецПроцедуры

&НаСервере
Процедура ВосстановитьНастройки() 
	
	Реквизиты = ЭтаФорма.ПолучитьРеквизиты();
	Для Каждого Реквизит из Реквизиты Цикл
		
		Если Не Параметры.Свойство(Реквизит.Имя) Тогда
			Продолжить;
		КонецЕсли;
		ЭтаФорма[Реквизит.Имя] = Параметры[Реквизит.Имя];

	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТекущиеНастройки()
	
	ТекущиеНастройки = Новый Структура;
	Реквизиты = ЭтаФорма.ПолучитьРеквизиты();
	
	Для Каждого Реквизит из Реквизиты Цикл
		
		ТекущиеНастройки.Вставить(Реквизит.Имя,ЭтаФорма[Реквизит.Имя]);

	КонецЦикла;
	
	Возврат ТекущиеНастройки;

КонецФункции

&НаСервере
Процедура УправлениеФормой()
		
	Если Группировки = "Вариант1" 
		И ЗначениеЗаполнено(МодельБюджетирования) Тогда
		Структура = Новый Структура("ПериодичностьПредставление, Периодичность", "Представление(Периодичность)", "Периодичность");
		Реквизиты = ОбщегоНазначенияУТВызовСервера.ЗначенияРеквизитовОбъекта(МодельБюджетирования, Структура);
		НовыйВидПериода = Перечисления.ДоступныеПериодыОтчета[Реквизиты.ПериодичностьПредставление];
		Если ВидПериода <> НовыйВидПериода Тогда
			ВидПериода = НовыйВидПериода;
			ПривестиЗначениеПериода(ВидПериода);
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ВидПериода.Доступность = Группировки <> "Вариант1";
	Элементы.ПоказатьПланВыполненияПроцесса.Видимость = ТипОтчета > 0;
	
КонецПроцедуры

&НаСервере
Процедура ПривестиЗначениеПериода(ВидПериода)
	
	НачалоПериода = ОбщегоНазначенияУТКлиентСервер.НачалоПериодаОтчета(ВидПериода, НачалоПериода);
	КонецПериода =  ОбщегоНазначенияУТКлиентСервер.КонецПериодаОтчета(ВидПериода, НачалоПериода);
	
	СписокПериодов = ОбщегоНазначенияУТКлиентСервер.ПолучитьСписокПериодов(НачалоПериода, ВидПериода);
	ЭлементСписка = СписокПериодов.НайтиПоЗначению(НачалоПериода);
	
	Если ЭлементСписка = Неопределено Тогда
		Период = "";
	Иначе
		Период = ЭлементСписка.Представление;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодНачалоВыбораЗавершение(ВыбранныйПериод, ДополнительныеПараметры) Экспорт

	Если ВыбранныйПериод <> Неопределено Тогда
		
		Период = ВыбранныйПериод.Представление;
		
		НачалоПериода = ВыбранныйПериод.Значение;
		КонецПериода = ОбщегоНазначенияУТКлиентСервер.КонецПериодаОтчета(ВидПериода, ВыбранныйПериод.Значение);
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти



