#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Перем МассивОбъектов, Период, Валюта, ВидЦены, КэшированныеЗначения;

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - УправляемаяФорма - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - Структура - см. возвращаемое значение ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию().
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	Настройки.События.ПриСозданииНаСервере = Истина;
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   Форма - УправляемаяФорма - Форма отчета.
//   Отказ - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Передается из параметров обработчика "как есть".
//
// См. также:
//   "УправляемаяФорма.ПриСозданииНаСервере" в синтакс-помощнике.
//
Процедура ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	КомпоновщикНастроекФормы = ЭтаФорма.Отчет.КомпоновщикНастроек;
	Параметры = ЭтаФорма.Параметры;
	
	Если Параметры.Свойство("ПараметрКоманды") Тогда
		ЭтаФорма.Параметры.Отбор.Вставить("МассивОбъектов", Параметры.ПараметрКоманды);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий
	
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПользовательскиеНастройкиМодифицированы = Ложь;
	
	УстановитьОбязательныеНастройки(ПользовательскиеНастройкиМодифицированы);
	
	// Подготовим и выведем отчет.
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	НастройкиКомпоновкиДанных = КомпоновщикНастроек.ПолучитьНастройки();
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(
		СхемаКомпоновкиДанных,
		НастройкиКомпоновкиДанных,
		ДанныеРасшифровки);
	
	КомпоновкаДанныхСервер.УстановитьЗаголовкиМакетаКомпоновки(СтруктураЗаголовковПолей(), МакетКомпоновки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	
	ТаблицаМатериалов = СтоимостьПереработкиДавальческогоСырья();

	ВнешниеНаборыДанных = Новый Структура("ТаблицаМатериалов", ТаблицаМатериалов);
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.НачатьВывод();
	ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
	
	ПроцессорВывода.ЗакончитьВывод();
	
	// Сообщим форме отчета, что настройки модифицированы
	Если ПользовательскиеНастройкиМодифицированы Тогда
		КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПользовательскиеНастройкиМодифицированы", Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции
	
Процедура УстановитьОбязательныеНастройки(ПользовательскиеНастройкиМодифицированы)
	
	МассивОбъектов = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек.ФиксированныеНастройки, "МассивОбъектов").Значение;
	
	Период = КонецДня(ТекущаяДатаСеанса());
	ПараметрДатаОтчета = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ДатаОтчета");
	
	Если ЗначениеЗаполнено(ПараметрДатаОтчета.Значение) Тогда
		Период = КонецДня(ПараметрДатаОтчета.Значение);
	Иначе
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ДатаОтчета", Период);
		ПользовательскиеНастройкиМодифицированы = Истина;
	КонецЕсли;
	
	Валюта = Константы.ВалютаПлановойСебестоимостиПродукции.Получить();
	
	ПараметрДатаОтчета = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ВидЦены");
	ВидЦены = ПараметрДатаОтчета.Значение;
	
КонецПроцедуры

Функция СтоимостьПереработкиДавальческогоСырья()
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаказДавальцаПродукция.Ссылка КАК ЗаказДавальца,
	|	ЗаказДавальцаПродукция.Номенклатура КАК Номенклатура,
	|	ЗаказДавальцаПродукция.Характеристика КАК Характеристика,
	|	ЗаказДавальцаПродукция.Спецификация,
	|	ЗаказДавальцаПродукция.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА &ВидЦены = НЕОПРЕДЕЛЕНО
	|			ТОГДА ЗаказДавальцаПродукция.Ссылка.ВидЦены
	|		ИНАЧЕ &ВидЦены
	|	КОНЕЦ КАК ВидЦены
	|ИЗ
	|	Документ.ЗаказДавальца.Продукция КАК ЗаказДавальцаПродукция
	|ГДЕ
	|	ЗаказДавальцаПродукция.Ссылка В(&МассивОбъектов)
	|	И ЗаказДавальцаПродукция.Спецификация <> ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("ВидЦены", ВидЦены);
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	ТаблицаЗатрат = Новый ТаблицаЗначений;
	ТаблицаЗатрат.Колонки.Добавить("ЗаказДавальца",Новый ОписаниеТипов("ДокументСсылка.ЗаказДавальца"));
	ТаблицаЗатрат.Колонки.Добавить("Номенклатура",Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаЗатрат.Колонки.Добавить("Характеристика",Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаЗатрат.Колонки.Добавить("Продукция",Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаЗатрат.Колонки.Добавить("ХарактеристикаПродукции",Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаЗатрат.Колонки.Добавить("Количество",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,3,ДопустимыйЗнак.Неотрицательный)));
	ТаблицаЗатрат.Колонки.Добавить("ВидЦены",Новый ОписаниеТипов("СправочникСсылка.ВидыЦен"));
	ТаблицаЗатрат.Колонки.Добавить("Спецификация",Новый ОписаниеТипов("СправочникСсылка.РесурсныеСпецификации"));
	
	ПереченьДанных = Новый Массив();
	ПереченьДанных.Добавить("МатериалыИУслуги");
	ПереченьДанных.Добавить("Трудозатраты");
	
	Пока Выборка.Следующий() Цикл
		
		ДанныеПоНоменклатуре = Новый Структура("Номенклатура, Характеристика, Спецификация, Количество");
		ЗаполнитьЗначенияСвойств(ДанныеПоНоменклатуре, Выборка);
		ТабличныеЧасти = Справочники.РесурсныеСпецификации.ДанныеСпецификацииСПолуфабрикатами(ДанныеПоНоменклатуре, Истина, , ПереченьДанных);
		
		// материалы
		ТабличныеЧасти.МатериалыИУслуги.Колонки.Добавить("ЗаказДавальца",Новый ОписаниеТипов("ДокументСсылка.ЗаказДавальца"));
		ТабличныеЧасти.МатериалыИУслуги.ЗаполнитьЗначения(Выборка.ЗаказДавальца, "ЗаказДавальца");
		
		ТабличныеЧасти.МатериалыИУслуги.Колонки.Добавить("Продукция",Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
		ТабличныеЧасти.МатериалыИУслуги.ЗаполнитьЗначения(Выборка.Номенклатура, "Продукция");
		
		ТабличныеЧасти.МатериалыИУслуги.Колонки.Добавить("ХарактеристикаПродукции",Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
		ТабличныеЧасти.МатериалыИУслуги.ЗаполнитьЗначения(Выборка.Характеристика, "ХарактеристикаПродукции");
		
		ТабличныеЧасти.МатериалыИУслуги.Колонки.Добавить("ВидЦены",Новый ОписаниеТипов("СправочникСсылка.ВидыЦен"));
		ТабличныеЧасти.МатериалыИУслуги.ЗаполнитьЗначения(Выборка.ВидЦены, "ВидЦены");
		
		ТабличныеЧасти.МатериалыИУслуги.ЗаполнитьЗначения(Выборка.Спецификация, "Спецификация");
		
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТабличныеЧасти.МатериалыИУслуги, ТаблицаЗатрат);
	
	КонецЦикла;
	
	ТаблицаЗатрат.Свернуть("ЗаказДавальца, Спецификация, Продукция, ХарактеристикаПродукции, Номенклатура, Характеристика, ВидЦены", "Количество");
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаЗатрат.ЗаказДавальца,
	|	ТаблицаЗатрат.Спецификация,
	|	ТаблицаЗатрат.Продукция,
	|	ТаблицаЗатрат.ХарактеристикаПродукции,
	|	ТаблицаЗатрат.Номенклатура,
	|	ТаблицаЗатрат.Характеристика,
	|	ТаблицаЗатрат.Количество,
	|	ТаблицаЗатрат.ВидЦены
	|ПОМЕСТИТЬ ВТТаблицаМатериалов
	|ИЗ
	|	&ТаблицаЗатрат КАК ТаблицаЗатрат
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПродукция.Ссылка КАК ЗаказДавальца,
	|	ТаблицаПродукция.Номенклатура КАК Продукция,
	|	ТаблицаПродукция.Характеристика КАК ХарактеристикаПродукции,
	|	ТаблицаПродукция.Спецификация КАК Спецификация,
	|	СУММА(ТаблицаПродукция.Количество) КАК Количество,
	|	СУММА(ТаблицаПродукция.СуммаСобственныхМатериалов) КАК СуммаСобственныхМатериалов,
	|	СУММА(ТаблицаПродукция.СуммаУслуги) КАК СуммаУслуги,
	|	СУММА(ТаблицаПродукция.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ВТПродукцияДавальца
	|ИЗ
	|	Документ.ЗаказДавальца.Продукция КАК ТаблицаПродукция
	|ГДЕ
	|	ТаблицаПродукция.Ссылка В(&МассивОбъектов)
	|	И ТаблицаПродукция.Спецификация <> ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаПродукция.Ссылка,
	|	ТаблицаПродукция.Спецификация,
	|	ТаблицаПродукция.Номенклатура,
	|	ТаблицаПродукция.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказДавальцаМатериалы.Номенклатура,
	|	ЗаказДавальцаМатериалы.Характеристика,
	|	ЗаказДавальцаМатериалы.Спецификация,
	|	СУММА(ЗаказДавальцаМатериалы.Количество) КАК Количество,
	|	ЗаказДавальцаМатериалы.Ссылка КАК ЗаказДавальца
	|ПОМЕСТИТЬ СписокМатериаловДавальца
	|ИЗ
	|	Документ.ЗаказДавальца.Материалы КАК ЗаказДавальцаМатериалы
	|ГДЕ
	|	ЗаказДавальцаМатериалы.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказДавальцаМатериалы.Ссылка,
	|	ЗаказДавальцаМатериалы.Спецификация,
	|	ЗаказДавальцаМатериалы.Номенклатура,
	|	ЗаказДавальцаМатериалы.Характеристика
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаПродукция.Продукция,
	|	ТаблицаПродукция.Спецификация,
	|	ТаблицаПродукция.ХарактеристикаПродукции,
	|	СписокМатериаловДавальца.Номенклатура,
	|	СписокМатериаловДавальца.Характеристика,
	|	СУММА(СписокМатериаловДавальца.Количество) КАК Количество,
	|	СписокМатериаловДавальца.ЗаказДавальца КАК ЗаказДавальца
	|ПОМЕСТИТЬ ВТМатериалыДавальца
	|ИЗ
	|	СписокМатериаловДавальца КАК СписокМатериаловДавальца
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПродукцияДавальца КАК ТаблицаПродукция
	|		ПО СписокМатериаловДавальца.ЗаказДавальца = ТаблицаПродукция.ЗаказДавальца
	|		И СписокМатериаловДавальца.Спецификация = ТаблицаПродукция.Спецификация
	|
	|СГРУППИРОВАТЬ ПО
	|	СписокМатериаловДавальца.ЗаказДавальца,
	|	СписокМатериаловДавальца.Номенклатура,
	|	ТаблицаПродукция.Спецификация,
	|	СписокМатериаловДавальца.Характеристика,
	|	ТаблицаПродукция.Продукция,
	|	ТаблицаПродукция.ХарактеристикаПродукции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТТаблицаМатериалов.ЗаказДавальца,
	|	ВТТаблицаМатериалов.Номенклатура,
	|	ВТТаблицаМатериалов.Характеристика,
	|	ВТТаблицаМатериалов.ВидЦены,
	|	ВТТаблицаМатериалов.Спецификация,
	|	СУММА(ВТТаблицаМатериалов.Количество) КАК КоличествоПоСпецификации,
	|	СУММА(ЕСТЬNULL(ВТМатериалыДавальца.Количество, 0)) КАК КоличествоПредоставляетсяДавальцем,
	|	ЦеныНоменклатурыСрезПоследних.Цена,
	|	ЗаказДавальцаПродукция.Продукция КАК Продукция,
	|	ЗаказДавальцаПродукция.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
	|	ЗаказДавальцаПродукция.СуммаСобственныхМатериалов КАК СуммаМатериаловВыставлено,
	|	ЗаказДавальцаПродукция.СуммаУслуги КАК СуммаУслугиВыставлено,
	|	ЗаказДавальцаПродукция.Сумма КАК СуммаВсегоВыставлено
	|ИЗ
	|	ВТТаблицаМатериалов КАК ВТТаблицаМатериалов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТМатериалыДавальца КАК ВТМатериалыДавальца
	|		ПО ВТТаблицаМатериалов.ЗаказДавальца = ВТМатериалыДавальца.ЗаказДавальца
	|			И ВТТаблицаМатериалов.Продукция = ВТМатериалыДавальца.Продукция
	|			И ВТТаблицаМатериалов.ХарактеристикаПродукции = ВТМатериалыДавальца.ХарактеристикаПродукции
	|			И ВТТаблицаМатериалов.Номенклатура = ВТМатериалыДавальца.Номенклатура
	|			И ВТТаблицаМатериалов.Характеристика = ВТМатериалыДавальца.Характеристика
	|			И ВТТаблицаМатериалов.Спецификация = ВТМатериалыДавальца.Спецификация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&Период, ) КАК ЦеныНоменклатурыСрезПоследних
	|		ПО ВТТаблицаМатериалов.ВидЦены = ЦеныНоменклатурыСрезПоследних.ВидЦены
	|			И ВТТаблицаМатериалов.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|			И ВТТаблицаМатериалов.Характеристика = ЦеныНоменклатурыСрезПоследних.Характеристика
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПродукцияДавальца КАК ЗаказДавальцаПродукция
	|		ПО ВТТаблицаМатериалов.ЗаказДавальца = ЗаказДавальцаПродукция.ЗаказДавальца
	|			И ВТТаблицаМатериалов.Продукция = ЗаказДавальцаПродукция.Продукция
	|			И ВТТаблицаМатериалов.ХарактеристикаПродукции = ЗаказДавальцаПродукция.ХарактеристикаПродукции
	|			И ВТТаблицаМатериалов.Спецификация = ЗаказДавальцаПродукция.Спецификация
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТТаблицаМатериалов.ЗаказДавальца,
	|	ВТТаблицаМатериалов.Номенклатура,
	|	ВТТаблицаМатериалов.Характеристика,
	|	ВТТаблицаМатериалов.Спецификация,
	|	ВТТаблицаМатериалов.ВидЦены,
	|	ЗаказДавальцаПродукция.Продукция,
	|	ЗаказДавальцаПродукция.ХарактеристикаПродукции,
	|	ЦеныНоменклатурыСрезПоследних.Цена,
	|	ЗаказДавальцаПродукция.СуммаСобственныхМатериалов,
	|	ЗаказДавальцаПродукция.СуммаУслуги,
	|	ЗаказДавальцаПродукция.Сумма";
	
	Запрос.УстановитьПараметр("ТаблицаЗатрат", ТаблицаЗатрат);
	Запрос.УстановитьПараметр("Период", ?(ЗначениеЗаполнено(Период), Период, КонецДня(ТекущаяДатаСеанса())));
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция СтруктураЗаголовковПолей()
	
	СтруктураЗаголовковПолей = Новый Структура;
	СтруктураЗаголовковПолей.Вставить("ВалютаОтчета", "(" + Валюта + ")");
	
	Возврат СтруктураЗаголовковПолей;
КонецФункции

#КонецОбласти 

#КонецЕсли

