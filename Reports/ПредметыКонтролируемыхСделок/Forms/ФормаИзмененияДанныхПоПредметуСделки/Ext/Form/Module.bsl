#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
  	Если Параметры.Свойство("АвтоТест") Тогда
   		Возврат;
  	КонецЕсли;
	
	Если Не Параметры.Свойство("ПредметСделки", ПредметСделки) Тогда
		Возврат;
	КонецЕсли;                                    
	
	ЭтаФорма.Элементы.ПредметСделки.ТолькоПросмотр = Истина;
	
	ОбновитьДанныеФормыПоПредметуСделки(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредметСделкиПриИзменении(Элемент)
	ОбновитьДанныеФормыПоПредметуСделки(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	Если ЭтаФорма.Модифицированность Тогда 
		ОбработатьИзмененияВПредметеСделки(ПредметСделки, КодОКВЭД, КодТНВЭД, КодОКП, ОблагаетсяНДПИПоПроцентнойСтавке);
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ПрочиеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьДанныеФормыПоПредметуСделки(Форма)
	
	СтруктураДанных = ВернутьДанныеПредмета(Форма.ПредметСделки);
	ЗаполнитьЗначенияСвойств(Форма, СтруктураДанных);
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Форма.Элементы, "ГруппаТНВЭД, ГруппаОКП, ОблагаетсяНДПИПоПроцентнойСтавке", "Доступность", СтруктураДанных.ЭтоТовар);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВернутьДанныеПредмета(ПредметСделки)
	
	РеквизитыДляЗаполнения = "КодОКВЭД, КодТНВЭД, КодОКП, ОблагаетсяНДПИПоПроцентнойСтавке, ТипНоменклатуры";
	СтруктураВозврата = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПредметСделки, РеквизитыДляЗаполнения);
	СтруктураВозврата.Вставить("ЭтоТовар", Не СтруктураВозврата.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Работа
								И Не СтруктураВозврата.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга);
		
	Возврат СтруктураВозврата;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ОбработатьИзмененияВПредметеСделки(СсылкаНаОбъект, КодОКВЭД, КодТНВЭД, КодОКП, ОблагаетсяНДПИПоПроцентнойСтавке)
		
	ИзменяемыйОбъект = СсылкаНаОбъект.ПолучитьОбъект();
	
	Попытка
		ЗаблокироватьДанныеДляРедактирования(СсылкаНаОбъект);
	Исключение
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	
	Попытка
		ИзменяемыйОбъект.КодОКВЭД = КодОКВЭД;
		ИзменяемыйОбъект.КодТНВЭД = КодТНВЭД;
		ИзменяемыйОбъект.КодОКП   = КодОКП;
		ИзменяемыйОбъект.ОблагаетсяНДПИПоПроцентнойСтавке = ОблагаетсяНДПИПоПроцентнойСтавке;
	Исключение
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	
	Попытка
		ИзменяемыйОбъект.Записать();
	Исключение
		Отказ = Истина;
	КонецПопытки;
	
	РазблокироватьДанныеДляРедактирования(СсылкаНаОбъект);	
	
КонецПроцедуры

#КонецОбласти