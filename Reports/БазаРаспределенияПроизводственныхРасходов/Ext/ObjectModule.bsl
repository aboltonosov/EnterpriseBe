#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - УправляемаяФорма - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - Структура - см. возвращаемое значение ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию().
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПриСозданииНаСервере = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   Форма - УправляемаяФорма - Форма отчета.
//   Отказ - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Передается из параметров обработчика "как есть".
//
// См. также:
//   "УправляемаяФорма.ПриСозданииНаСервере" в синтакс-помощнике.
//
Процедура ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	КомпоновщикНастроекФормы = ЭтаФорма.Отчет.КомпоновщикНастроек;
	Параметры = ЭтаФорма.Параметры;
	
	Если Параметры.Свойство("ПараметрКоманды") Тогда
		
		Если Не ЗначениеЗаполнено(Параметры.ПараметрКоманды) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Распределение прочих расходов не выполнено или не требуется.'"),,,,Отказ);
		КонецЕсли;
		
		ЭтаФорма.ФормаПараметры.Отбор.Вставить("Регистратор", Параметры.ПараметрКоманды);
		
	Иначе
		
		Отказ = Истина;
		ВызватьИсключение НСтр("ru= 'Отчет предназначен только для открытия в документе ""Распределение расходов на себестоимость продукции"".'") ;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	
	ПараметрРегистратор = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкиОтчета, "Регистратор");
	Регистратор         = ПараметрРегистратор.Значение;
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Регистратор, "Дата, Организация, Подразделение, БазаРаспределенияПоПодразделениям,
		|БазаРаспределенияПоЭтапам, СтатьяРасходов, АналитикаРасходов, НаправлениеДеятельности, ПравилоРаспределенияПоПодразделениям, ПравилоРаспределенияПоЭтапам,
		|БазаРаспределенияПоПодразделениям, ДоляСтоимостиНаСтатьи, ВсегоДолейСтоимости, ДоляСтоимостиПоПравилам,
		|ВариантРаспределения");
	
	НачалоПериода     = НачалоМесяца(Реквизиты.Дата);
	ОкончаниеПериода  = КонецМесяца(Реквизиты.Дата);
	МассивОрганизаций = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Реквизиты.Организация);
	
	// Расчет сумм поступивших расходов
	Запрос = Новый Запрос;
	Запрос.Текст = Документы.РаспределениеПрочихЗатрат.ТекстЗапросаПоступилоПрочихРасходов() + "
	|ВЫБРАТЬ
	|	Т.ПоступилоУпр,
	|	Т.ПоступилоРегл
	|ИЗ
	|	ВТПоступило КАК Т
	|ГДЕ
	|	Т.СтатьяРасходов = &Статья
	|	И Т.АналитикаРасходов = &Аналитика
	|	И Т.НаправлениеДеятельности = &НаправлениеДеятельности";
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", ОкончаниеПериода);
	Запрос.УстановитьПараметр("МассивОрганизаций", Реквизиты.Организация);
	Запрос.УстановитьПараметр("СписокПодразделений", Реквизиты.Подразделение);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", Ложь);
	Запрос.УстановитьПараметр("ПоВсемПодразделениям", Ложь);
	Запрос.УстановитьПараметр("Статья", Реквизиты.СтатьяРасходов);
	Запрос.УстановитьПараметр("Аналитика", Реквизиты.АналитикаРасходов);
	Запрос.УстановитьПараметр("НаправлениеДеятельности", Реквизиты.НаправлениеДеятельности);
	
	УстановитьПривилегированныйРежим(Истина);
	
	ШаблонПредставления = Нстр("ru = 'Сумма расходов: упр. учет: %1 (%2), регл. учет: %3 (%4)'");
	ПредставлениеСумм = "";
	
	ВалютаУпр = Константы.ВалютаУправленческогоУчета.Получить();
	ВалютаРегл = Константы.ВалютаРегламентированногоУчета.Получить();
	
	Выборка = Запрос.Выполнить().Выбрать();
	ВыводитьПредставлениеСумм = Истина;
	Если Выборка.Следующий() Тогда
		ПредставлениеСумм = СтрШаблон(ШаблонПредставления, Выборка.ПоступилоУпр, ВалютаУпр, Выборка.ПоступилоРегл, ВалютаРегл);
	Иначе
		ВыводитьПредставлениеСумм = Ложь;
	КонецЕсли;
	
	// Стоимость
	МассивТиповРаспределенияБазыПоСтоимости = Новый Массив();
	МассивТиповРаспределенияБазыПоСтоимости.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.СуммаМатериальныхЗатрат);
	МассивТиповРаспределенияБазыПоСтоимости.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.СуммаРасходовНаОплатуТруда);
	МассивТиповРаспределенияБазыПоСтоимости.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.НормативыОплатыТруда);
	МассивТиповРаспределенияБазыПоСтоимости.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукции);
	МассивТиповРаспределенияБазыПоСтоимости.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат);
	
	// Количество
	МассивТиповРаспределенияБазыПоКоличеству = Новый Массив();
	МассивТиповРаспределенияБазыПоКоличеству.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.КоличествоУказанныхМатериалов);
	МассивТиповРаспределенияБазыПоКоличеству.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.КоличествоРаботУказанныхВидов);
	МассивТиповРаспределенияБазыПоКоличеству.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.КоличествоПродукции);
	
	// Объем
	МассивТиповРаспределенияБазыПоОбъему = Новый Массив();
	МассивТиповРаспределенияБазыПоОбъему.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.ОбъемУказанныхМатериалов);
	МассивТиповРаспределенияБазыПоОбъему.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.ОбъемПродукции);
	
	// Вес
	МассивТиповРаспределенияБазыПоВесу = Новый Массив();
	МассивТиповРаспределенияБазыПоВесу.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.ВесУказанныхМатериалов);
	МассивТиповРаспределенияБазыПоВесу.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.ВесПродукции);
	
	РасчетСебестоимостиВыполнен = Ложь;
	МВТ = ПартионныйУчет22.ДанныеДляРасшифровкиДолейПроизводственныхРасходов(НачалоПериода, МассивОрганизаций, Регистратор, РасчетСебестоимостиВыполнен);
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТ;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДД.ПартияПроизводства            КАК ПартияПроизводства,
	|	ДД.ЗаказНаПроизводство           КАК ЗаказНаПроизводство,
	|	ДД.КодСтрокиПродукция            КАК КодСтрокиПродукция,
	|	ДД.СтатьяРасходов                КАК СтатьяРасходов,
	|	ДД.АналитикаРасходов             КАК АналитикаРасходов,
	|	ДД.Подразделение                 КАК Подразделение,
	|	ДД.ГруппаПродукции               КАК ГруппаПродукции,
	|	СУММА(ДД.Стоимость)              КАК Сумма,
	|	СУММА(ДД.СтоимостьРегл)          КАК СуммаРегл
	|ПОМЕСТИТЬ НаПартииФакт
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходыНезавершенногоПроизводства КАК ДД
	|ГДЕ
	|	ДД.Регистратор = &Регистратор
	|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И ДД.Стоимость <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.ПартияПроизводства,
	|	ДД.ЗаказНаПроизводство,
	|	ДД.КодСтрокиПродукция,
	|	ДД.СтатьяРасходов,
	|	ДД.АналитикаРасходов,
	|	ДД.Подразделение,
	|	ДД.ДоляСтоимости,
	|	ДД.ГруппаПродукции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДД.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА Строки.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов
	|			ТОГДА Строки.СтатьяРасходов
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|	КОНЕЦ                           КАК СтатьяРасходов,
	|	Строки.АналитикаРасходов        КАК АналитикаРасходов,
	|	ВЫБОР
	|		КОГДА Строки.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|			ТОГДА Строки.СтатьяРасходов
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПустаяСсылка)
	|	КОНЕЦ                           КАК СтатьяАктивовПассивов,
	|	Строки.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	СУММА(ДД.Сумма)                 КАК Сумма,
	|	СУММА(ДД.СуммаРегл)             КАК СуммаРегл
	|ПОМЕСТИТЬ НаСтатьиФакт
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы КАК ДД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат.Списание КАК Строки
	|		ПО ДД.Регистратор = Строки.Ссылка
	|		И ДД.ИдентификаторСтроки = Строки.ИдентификаторСтроки
	|ГДЕ
	|	ДД.Регистратор = &Регистратор
	|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И ДД.Сумма <> 0
	|	
	|СГРУППИРОВАТЬ ПО
	|	ДД.НаправлениеДеятельности,
	|	Строки.СтатьяРасходов,
	|	Строки.АналитикаРасходов,
	|	Строки.АналитикаАктивовПассивов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДД.Подразделение     КАК Подразделение,
	|	СУММА(ДД.Сумма)      КАК Сумма,
	|	СУММА(ДД.СуммаРегл)  КАК СуммаРегл
	|ПОМЕСТИТЬ НаПодразделенияФакт
	|ИЗ
	|	НаПартииФакт КАК ДД
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДД.КорПодразделение                   КАК Подразделение,
	|	ДД.ПартияПроизводства                 КАК ПартияПроизводства,
	|	ДД.ЗаказНаПроизводство                КАК ЗаказНаПроизводство,
	|	ДД.КодСтрокиПродукция                 КАК КодСтрокиПродукция,
	|	ДД.ГруппаПродукции                    КАК ГруппаПродукции,
	|	ДД.Регистратор                        КАК Регистратор,
	|	&ПравилоРаспределенияПоПодразделениям КАК ПравилоРаспределенияПоПодразделениям,
	|	ДД.МатериалВидРабот                   КАК МатериалПродукцияУслуга,
	|	ДД.Знаменатель                        КАК ЗначениеПоказателя,
	|	ВЫБОР
	|		КОГДА ИтогиПоЭтапам.Регистратор ЕСТЬ NULL
	|			ТОГДА 0
	|		ИНАЧЕ ДД.Знаменатель
	|	КОНЕЦ                                 КАК ЗначениеПоказателяПроцент
	|ПОМЕСТИТЬ НаПодразделенияДетально
	|ИЗ
	|	БазаПоПодразделениямДетально КАК ДД
	|		ЛЕВОЕ СОЕДИНЕНИЕ ИтогиБазыПоЭтапамПодразделений КАК ИтогиПоЭтапам
	|		ПО (ИтогиПоЭтапам.Регистратор = ДД.Регистратор)
	|			И (ИтогиПоЭтапам.Подразделение = ДД.КорПодразделение)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДД.Подразделение                          КАК Подразделение,
	|	СУММА(ДД.ЗначениеПоказателяПроцент)       КАК ЗначениеПоказателяПроцент
	|ПОМЕСТИТЬ НаПодразделенияСводно
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ДД.Подразделение                   КАК Подразделение,
	|		ДД.ЗначениеПоказателяПроцент       КАК ЗначениеПоказателяПроцент
	|	ИЗ
	|		НаПодразделенияДетально КАК ДД
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		&Подразделение                   КАК Подразделение,
	|		1                                КАК ЗначениеПоказателяПроцент
	|	ГДЕ
	|		&ВариантРаспределения = ЗНАЧЕНИЕ(Перечисление.СпособыРаспределенияСтатейРасходов.ПоЭтапамПоПравилуВДанномПодразделении)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ДД.Подразделение                 КАК Подразделение,
	|		1                                КАК ЗначениеПоказателяПроцент
	|	ИЗ
	|		БазаПоЭтапам КАК ДД
	|	ГДЕ
	|		&ВариантРаспределения = ЗНАЧЕНИЕ(Перечисление.СпособыРаспределенияСтатейРасходов.ПоЭтапамПоПравилуПоВсемПодразделениям)
	|	) КАК ДД
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДД.Подразделение                      КАК Подразделение,
	|	ДД.ПартияПроизводства                 КАК ПартияПроизводства,
	|	ДД.ЗаказНаПроизводство                КАК ЗаказНаПроизводство,
	|	ДД.КодСтрокиПродукция                 КАК КодСтрокиПродукция,
	|	&ПравилоРаспределенияПоЭтапам         КАК ПравилоРаспределенияПоЭтапам,
	|	&БазаРаспределенияПоЭтапам            КАК ТипБазыРаспределенияПоЭтапам,
	|	ДД.ГруппаПродукции                    КАК ГруппаПродукции,
	|	ВЫБОР
	|		КОГДА ДД.ВидПоказателя = ""Материал""
	|			ТОГДА ДД.Материал
	|		КОГДА ДД.ВидПоказателя = ""ВидРабот""
	|			ТОГДА ДД.ВидРабот
	|		КОГДА ДД.ВидПоказателя = ""Продукция""
	|			ТОГДА ДД.Продукция
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	КОНЕЦ                                 КАК МатериалПродукцияУслуга,
	|	ДД.Знаменатель                        КАК Знаменатель
	|ПОМЕСТИТЬ НаЭтапыДетально
	|ИЗ
	|	БазаПоЭтапамДетально КАК ДД
	|	
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДД.Подразделение          КАК Подразделение,
	|	СУММА(ДД.Знаменатель)     КАК ЗначениеПоказателя
	|ПОМЕСТИТЬ ПоЭтапамИтог
	|ИЗ
	|	БазаПоЭтапам КАК ДД
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДД.ПартияПроизводства          КАК ПартияПроизводства,
	|	ДД.ЗаказНаПроизводство         КАК ЗаказНаПроизводство,
	|	ДД.КодСтрокиПродукция          КАК КодСтрокиПродукция,
	|	ДД.ГруппаПродукции             КАК ГруппаПродукции,
	|	ДД.Материал                    КАК Материал,
	|	2                              КАК ПорядокВывода,
	|	СУММА(ДД.Количество)           КАК Количество,
	|	СУММА(ДД.Объем)                КАК Объем,
	|	СУММА(ДД.Вес)                  КАК Вес,
	|	СУММА(ДД.Стоимость)            КАК Стоимость,
	|	0                              КАК НормативнаяСтоимость,
	|	ДД.Подразделение               КАК Подразделение
	|ПОМЕСТИТЬ ДанныеНеВошедшие
	|ИЗ
	|	ДанныеПоПредварительнымМатериальнымЗатратам КАК ДД
	|		ЛЕВОЕ СОЕДИНЕНИЕ БазаПоЭтапамДетально КАК ОтборПоПартиям
	|		ПО ДД.ПартияПроизводства = ОтборПоПартиям.ПартияПроизводства
	|			И ДД.ЗаказНаПроизводство = ОтборПоПартиям.ЗаказНаПроизводство
	|			И ДД.КодСтрокиПродукция = ОтборПоПартиям.КодСтрокиПродукция
	|			И ДД.Подразделение = ОтборПоПартиям.Подразделение
	|			И ДД.Материал = ОтборПоПартиям.Материал
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НаПодразделенияСводно КАК НаПодразделенияСводно
	|		ПО ДД.Подразделение = НаПодразделенияСводно.Подразделение
	|ГДЕ
	|	ОтборПоПартиям.Материал ЕСТЬ NULL
	|	И &БазаРаспределенияПоЭтапам В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхЗатрат),
	|							ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат),
	|							ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.НормативРасходаМатериала),
	|							ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоУказанныхМатериалов),
	|							ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемУказанныхМатериалов),
	|							ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесУказанныхМатериалов))
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.ПартияПроизводства,
	|	ДД.ЗаказНаПроизводство,
	|	ДД.КодСтрокиПродукция,
	|	ДД.ГруппаПродукции,
	|	ДД.Подразделение,
	|	ДД.Материал
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДД.ПартияПроизводства,
	|	ДД.ЗаказНаПроизводство,
	|	ДД.КодСтрокиПродукция,
	|	ДД.ГруппаПродукции,
	|	ДД.Продукция,
	|	2,
	|	СУММА(ДД.Количество),
	|	СУММА(ДД.Объем),
	|	СУММА(ДД.Вес),
	|	СУММА(ДД.Стоимость),
	|	0,
	|	ДД.Подразделение
	|ИЗ
	|	ДанныеПоПродукции КАК ДД
	|		ЛЕВОЕ СОЕДИНЕНИЕ БазаПоЭтапамДетально КАК ОтборПоПартиям
	|		ПО ДД.ПартияПроизводства = ОтборПоПартиям.ПартияПроизводства
	|			И ДД.ЗаказНаПроизводство = ОтборПоПартиям.ЗаказНаПроизводство
	|			И ДД.КодСтрокиПродукция = ОтборПоПартиям.КодСтрокиПродукция
	|			И ДД.Подразделение = ОтборПоПартиям.Подразделение
	|			И ДД.Продукция = ОтборПоПартиям.Продукция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НаПодразделенияСводно КАК НаПодразделенияСводно
	|		ПО ДД.Подразделение = НаПодразделенияСводно.Подразделение
	|
	|ГДЕ
	|	ОтборПоПартиям.Продукция ЕСТЬ NULL
	|	И &БазаРаспределенияПоЭтапам В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоПродукции),
	|							ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесПродукции),
	|							ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемПродукции),
	|							ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукции))
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.ПартияПроизводства,
	|	ДД.ЗаказНаПроизводство,
	|	ДД.КодСтрокиПродукция,
	|	ДД.ГруппаПродукции,
	|	ДД.Подразделение,
	|	ДД.Продукция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДД.ПартияПроизводства,
	|	ДД.ЗаказНаПроизводство,
	|	ДД.КодСтрокиПродукция,
	|	ДД.ГруппаПродукции,
	|	ДД.ВидРабот,
	|	2,
	|	СУММА(ДД.Количество),
	|	0,
	|	0,
	|	СУММА(ДД.Стоимость),
	|	СУММА(ДД.НормативнаяСтоимость),
	|	ДД.Подразделение
	|ИЗ
	|	ДанныеПоТрудоЗатратам КАК ДД
	|		ЛЕВОЕ СОЕДИНЕНИЕ БазаПоЭтапамДетально КАК ОтборПоПартиям
	|		ПО ДД.ПартияПроизводства = ОтборПоПартиям.ПартияПроизводства
	|			И ДД.ЗаказНаПроизводство = ОтборПоПартиям.ЗаказНаПроизводство
	|			И ДД.КодСтрокиПродукция = ОтборПоПартиям.КодСтрокиПродукция
	|			И ДД.Подразделение = ОтборПоПартиям.Подразделение
	|			И ДД.ВидРабот = ОтборПоПартиям.ВидРабот
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НаПодразделенияСводно КАК НаПодразделенияСводно
	|		ПО ДД.Подразделение = НаПодразделенияСводно.Подразделение
	|ГДЕ
	|	ОтборПоПартиям.ВидРабот ЕСТЬ NULL
	|	И &БазаРаспределенияПоЭтапам В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаРасходовНаОплатуТруда),
	|							ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат),
	|							ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоРаботУказанныхВидов),
	|							ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.НормативыОплатыТруда))
	|
	|СГРУППИРОВАТЬ ПО
	|	ДД.ПартияПроизводства,
	|	ДД.ЗаказНаПроизводство,
	|	ДД.КодСтрокиПродукция,
	|	ДД.ГруппаПродукции,
	|	ДД.Подразделение,
	|	ДД.ВидРабот
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.Подразделение                КАК Подразделение,
	|	ДД.ПартияПроизводства           КАК ПартияПроизводства,
	|	ДД.ЗаказНаПроизводство          КАК ЗаказНаПроизводство,
	|	ДД.ГруппаПродукции              КАК ГруппаПродукции
	|ПОМЕСТИТЬ ПартииНеВошедшие
	|ИЗ
	|	ДанныеПоПредварительнымМатериальнымЗатратам КАК ДД
	|		ЛЕВОЕ СОЕДИНЕНИЕ БазаПоЭтапам КАК БазаПоЭтапам
	|		ПО ДД.Подразделение = БазаПоЭтапам.Подразделение
	|			И ДД.ПартияПроизводства = БазаПоЭтапам.ПартияПроизводства
	|			И ДД.ЗаказНаПроизводство = БазаПоЭтапам.ЗаказНаПроизводство
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НаПодразделенияСводно КАК НаПодразделенияСводно
	|		ПО ДД.Подразделение = НаПодразделенияСводно.Подразделение
	|ГДЕ
	|	БазаПоЭтапам.ПартияПроизводства ЕСТЬ NULL
	|	И &БазаРаспределенияПоЭтапам В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхЗатрат),
	|							ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат),
	|							ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.НормативРасходаМатериала),
	|							ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоУказанныхМатериалов),
	|							ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемУказанныхМатериалов),
	|							ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесУказанныхМатериалов))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДД.Подразделение,
	|	ДД.ПартияПроизводства,
	|	ДД.ЗаказНаПроизводство,
	|	ДД.ГруппаПродукции
	|ИЗ
	|	ДанныеПоПродукции КАК ДД
	|		ЛЕВОЕ СОЕДИНЕНИЕ БазаПоЭтапамДетально КАК ОтборПоПартиям
	|		ПО ДД.ПартияПроизводства = ОтборПоПартиям.ПартияПроизводства
	|			И ДД.ЗаказНаПроизводство = ОтборПоПартиям.ЗаказНаПроизводство
	|			И ДД.Подразделение = ОтборПоПартиям.Подразделение
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НаПодразделенияСводно КАК НаПодразделенияСводно
	|		ПО ДД.Подразделение = НаПодразделенияСводно.Подразделение
	|ГДЕ
	|	ОтборПоПартиям.Продукция ЕСТЬ NULL
	|	И &БазаРаспределенияПоЭтапам В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоПродукции),
	|							ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесПродукции),
	|							ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемПродукции),
	|							ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукции))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДД.Подразделение,
	|	ДД.ПартияПроизводства,
	|	ДД.ЗаказНаПроизводство,
	|	ДД.ГруппаПродукции
	|ИЗ
	|	ДанныеПоТрудоЗатратам КАК ДД
	|		ЛЕВОЕ СОЕДИНЕНИЕ БазаПоЭтапамДетально КАК ОтборПоПартиям
	|		ПО ДД.ПартияПроизводства = ОтборПоПартиям.ПартияПроизводства
	|			И ДД.ЗаказНаПроизводство = ОтборПоПартиям.ЗаказНаПроизводство
	|			И ДД.Подразделение = ОтборПоПартиям.Подразделение
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НаПодразделенияСводно КАК НаПодразделенияСводно
	|		ПО ДД.Подразделение = НаПодразделенияСводно.Подразделение
	|ГДЕ
	|	ОтборПоПартиям.ВидРабот ЕСТЬ NULL
	|	И &БазаРаспределенияПоЭтапам В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаРасходовНаОплатуТруда),
	|								ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат),
	|								ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоРаботУказанныхВидов),
	|								ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.НормативыОплатыТруда))
	|	
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// партии и затраты по подразделениям
	|ВЫБРАТЬ
	|	""ПоПравилуНаПодразделения""            КАК ТипЗаписи,
	|	ДД.Подразделение                        КАК Подразделение,
	|	ВЫБОР
	|		КОГДА ДД.ПартияПроизводства <> НЕОПРЕДЕЛЕНО
	|			ТОГДА ДД.ПартияПроизводства
	|		ИНАЧЕ
	|			ДД.ЗаказНаПроизводство
	|	КОНЕЦ                                   КАК ПартияПроизводства,
	|	ДД.ЗначениеПоказателя                   КАК ЗначениеПоказателя,
	|	ДД.ЗначениеПоказателя                   КАК ЗначениеПоказателяПроцент,
	|	ЕСТЬNULL(Факт.Сумма, 0)                 КАК Сумма,
	|	ЕСТЬNULL(Факт.СуммаРегл, 0)             КАК СуммаРегл,
	|	НЕОПРЕДЕЛЕНО                            КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                            КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                            КАК АналитикаАктивовПассивов,
	|	&Регистратор                            КАК Регистратор,
	|	ДД.МатериалПродукцияУслуга              КАК МатериалПродукцияУслуга,
	|	ДД.ГруппаПродукции                      КАК ГруппаПродукции,
	|	1                                       КАК ПорядокВывода,
	|	ИСТИНА                                  КАК СуммироватьКоличествоПоЭтапу
	|ИЗ
	|	НаПодразделенияДетально КАК ДД
	|		ЛЕВОЕ СОЕДИНЕНИЕ НаПодразделенияФакт КАК Факт
	|		ПО ДД.Подразделение = Факт.Подразделение
	|ГДЕ
	|	&БазаРаспределенияПоПодразделениям В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхЗатрат),
	|										ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаРасходовНаОплатуТруда),
	|										ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// партии не вошедшие в распределение по подразделениям
	|ВЫБРАТЬ
	|	""ПоПравилуНаПодразделения""            КАК ТипЗаписи,
	|	ДД.Подразделение                        КАК Подразделение,
	|	ВЫБОР
	|		КОГДА ДД.ПартияПроизводства <> НЕОПРЕДЕЛЕНО
	|			ТОГДА ДД.ПартияПроизводства
	|		ИНАЧЕ
	|			ДД.ЗаказНаПроизводство
	|	КОНЕЦ                                   КАК ПартияПроизводства,
	|	0                                       КАК ЗначениеПоказателя,
	|	0                                       КАК ЗначениеПоказателяПроцент,
	|	0                                       КАК Сумма,
	|	0                                       КАК СуммаРегл,
	|	НЕОПРЕДЕЛЕНО                            КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                            КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                            КАК АналитикаАктивовПассивов,
	|	&Регистратор                            КАК Регистратор,
	|	НЕОПРЕДЕЛЕНО                            КАК МатериалПродукцияУслуга,
	|	ДД.ГруппаПродукции                      КАК ГруппаПродукции,
	|	3                                       КАК ПорядокВывода,
	|	ЛОЖЬ                                    КАК СуммироватьКоличествоПоЭтапу
	|ИЗ
	|	ПартииНеВошедшие КАК ДД
	|ГДЕ
	|	&БазаРаспределенияПоПодразделениям В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхЗатрат),
	|										ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаРасходовНаОплатуТруда),
	|										ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// значения показателей и распределение по подразделениям вручную
	|ВЫБРАТЬ
	|	""ПоПоказателямНаПодразделения""        КАК ТипЗаписи,
	|	ДД.Подразделение                        КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО                            КАК ПартияПроизводства,
	|	ДД.ЗначениеПоказателя                   КАК ЗначениеПоказателя,
	|	ДД.ЗначениеПоказателяПроцент            КАК ЗначениеПоказателяПроцент,
	|	ЕСТЬNULL(Факт.Сумма, 0)                 КАК Сумма,
	|	ЕСТЬNULL(Факт.СуммаРегл, 0)             КАК СуммаРегл,
	|	НЕОПРЕДЕЛЕНО                            КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                            КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                            КАК АналитикаАктивовПассивов,
	|	&Регистратор                            КАК Регистратор,
	|	НЕОПРЕДЕЛЕНО                            КАК МатериалПродукцияУслуга,
	|	НЕОПРЕДЕЛЕНО                            КАК ГруппаПродукции,
	|	1                                       КАК ПорядокВывода,
	|	ИСТИНА                                  КАК СуммироватьКоличествоПоЭтапу
	|ИЗ
	|	НаПодразделенияДетально КАК ДД
	|		ЛЕВОЕ СОЕДИНЕНИЕ НаПодразделенияФакт КАК Факт
	|		ПО ДД.Подразделение = Факт.Подразделение
	|ГДЕ
	|	&БазаРаспределенияПоПодразделениям В (ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВводитсяЕжемесячно),
	|										ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВводитсяПриИзменении))
	|	ИЛИ &ВариантРаспределения = ЗНАЧЕНИЕ(Перечисление.СпособыРаспределенияСтатейРасходов.ПоПодразделениямВручнуюПоЭтапамПоПравилу)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""ПоПравилуНаЭтапы""                    КАК ТипЗаписи,
	|	ДД.Подразделение                        КАК Подразделение,
	|	ВЫБОР
	|		КОГДА ДД.ПартияПроизводства <> НЕОПРЕДЕЛЕНО
	|			ТОГДА ДД.ПартияПроизводства
	|		ИНАЧЕ
	|			ДД.ЗаказНаПроизводство
	|	КОНЕЦ                                   КАК ПартияПроизводства,
	|	ДД.Знаменатель                          КАК ЗначениеПоказателя,
	|	ВЫБОР
	|		КОГДА НаПодразделенияСводно.Подразделение ЕСТЬ NULL ИЛИ НаПодразделенияСводно.ЗначениеПоказателяПроцент = 0
	|			ТОГДА 0
	|		ИНАЧЕ
	|			ДД.Знаменатель
	|	КОНЕЦ                                   КАК ЗначениеПоказателяПроцент,
	|	ЕСТЬNULL(Факт.Сумма, 0)                 КАК Сумма,
	|	ЕСТЬNULL(Факт.СуммаРегл, 0)             КАК СуммаРегл,
	|	НЕОПРЕДЕЛЕНО                            КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                            КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                            КАК АналитикаАктивовПассивов,
	|	&Регистратор                            КАК Регистратор,
	|	ДД.МатериалПродукцияУслуга              КАК МатериалПродукцияУслуга,
	|	ДД.ГруппаПродукции                      КАК ГруппаПродукции,
	|	1                                       КАК ПорядокВывода,
	|	ВЫБОР
	|		КОГДА ДД.ТипБазыРаспределенияПоЭтапам В (&МассивКоличества)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ                                   КАК СуммироватьКоличествоПоЭтапу
	|ИЗ
	|	НаЭтапыДетально КАК ДД
	|		ЛЕВОЕ СОЕДИНЕНИЕ НаПартииФакт КАК Факт
	|			ПО ДД.Подразделение = Факт.Подразделение
	|			И ДД.ПартияПроизводства = Факт.ПартияПроизводства
	|			И ДД.ЗаказНаПроизводство = Факт.ЗаказНаПроизводство
	|			И ДД.КодСтрокиПродукция = Факт.КодСтрокиПродукция
	|		ЛЕВОЕ СОЕДИНЕНИЕ НаПодразделенияСводно КАК НаПодразделенияСводно
	|			ПО ДД.Подразделение = НаПодразделенияСводно.Подразделение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""ПоПравилуНаЭтапы""                    КАК ТипЗаписи,
	|	ДД.Подразделение                        КАК Подразделение,
	|	ВЫБОР
	|		КОГДА ДД.ПартияПроизводства <> НЕОПРЕДЕЛЕНО
	|			ТОГДА ДД.ПартияПроизводства
	|		ИНАЧЕ
	|			ДД.ЗаказНаПроизводство
	|	КОНЕЦ                                   КАК ПартияПроизводства,
	|	ВЫБОР
	|		КОГДА &БазаРаспределенияПоЭтапам В (&МассивОбъема)
	|			ТОГДА ДД.Объем
	|		КОГДА &БазаРаспределенияПоЭтапам В (&МассивВеса)
	|			ТОГДА ДД.Вес
	|		КОГДА &БазаРаспределенияПоЭтапам В (&МассивСтоимости)
	|			ТОГДА ДД.Стоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ                                   КАК ЗначениеПоказателя,
	|	0                                       КАК ЗначениеПоказателяПроцент,
	|	0                                       КАК Сумма,
	|	0                                       КАК СуммаРегл,
	|	НЕОПРЕДЕЛЕНО                            КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                            КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                            КАК АналитикаАктивовПассивов,
	|	&Регистратор                            КАК Регистратор,
	|	НЕОПРЕДЕЛЕНО                            КАК МатериалПродукцияУслуга,
	|	ДД.ГруппаПродукции                      КАК ГруппаПродукции,
	|	ДД.ПорядокВывода                        КАК ПорядокВывода,
	|	ВЫБОР
	|		КОГДА &БазаРаспределенияПоЭтапам В (&МассивКоличества)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ                                   КАК СуммироватьКоличествоПоЭтапу
	|ИЗ
	|	ДанныеНеВошедшие КАК ДД
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПартииНеВошедшие КАК НеВошедшие
	|			ПО ДД.Подразделение = НеВошедшие.Подразделение
	|			И ДД.ПартияПроизводства = НеВошедшие.ПартияПроизводства
	|			И ДД.ЗаказНаПроизводство = НеВошедшие.ЗаказНаПроизводство
	|	
	|ГДЕ
	|	НеВошедшие.ПартияПроизводства ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""ПоПравилуНаЭтапы""                    КАК ТипЗаписи,
	|	ДД.Подразделение                        КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО                            КАК ПартияПроизводства,
	|	0                                       КАК ЗначениеПоказателя,
	|	0                                       КАК ЗначениеПоказателяПроцент,
	|	0                                       КАК Сумма,
	|	0                                       КАК СуммаРегл,
	|	НЕОПРЕДЕЛЕНО                            КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                            КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                            КАК АналитикаАктивовПассивов,
	|	&Регистратор                            КАК Регистратор,
	|	ВЫБОР
	|		КОГДА ДД.ПартияПроизводства <> НЕОПРЕДЕЛЕНО
	|			ТОГДА ДД.ПартияПроизводства
	|		ИНАЧЕ
	|			ДД.ЗаказНаПроизводство
	|	КОНЕЦ                                   КАК МатериалПродукцияУслуга,
	|	НЕОПРЕДЕЛЕНО                            КАК ГруппаПродукции,
	|	3                                       КАК ПорядокВывода,
	|	ЛОЖЬ                                    КАК СуммироватьКоличествоПоЭтапу
	|ИЗ
	|	ПартииНеВошедшие КАК ДД
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""НаДругиеСтатьи""                    КАК ТипЗаписи,
	|	НЕОПРЕДЕЛЕНО                          КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО                          КАК ПартияПроизводства,
	|	ДД.Знаменатель                        КАК ЗначениеПоказателя,
	|	ДД.Знаменатель                        КАК ЗначениеПоказателяПроцент,
	|	ЕСТЬNULL(Факт.Сумма, 0)               КАК Сумма,
	|	ЕСТЬNULL(Факт.СуммаРегл, 0)           КАК СуммаРегл,
	|	ВЫБОР
	|		КОГДА ДД.КорСтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			ТОГДА ДД.КорСтатьяРасходов
	|		ИНАЧЕ ДД.КорСтатьяАктивовПассивов
	|	КОНЕЦ                                 КАК СтатьяРасходов,
	|	ДД.КорАналитикаРасходов               КАК АналитикаРасходов,
	|	ДД.КорАналитикаАктивовПассивов        КАК АналитикаАктивовПассивов,
	|	&Регистратор                          КАК Регистратор,
	|	НЕОПРЕДЕЛЕНО                          КАК МатериалПродукцияУслуга,
	|	НЕОПРЕДЕЛЕНО                          КАК ГруппаПродукции,
	|	1                                     КАК ПорядокВывода,
	|	ИСТИНА                                КАК СуммироватьКоличествоПоЭтапу
	|ИЗ
	|	ДолиПроизводственныхРасходов КАК ДД
	|		ЛЕВОЕ СОЕДИНЕНИЕ НаСтатьиФакт КАК Факт
	|		ПО ДД.КорСтатьяРасходов = Факт.СтатьяРасходов
	|			И ДД.КорАналитикаРасходов = Факт.АналитикаРасходов
	|			И ДД.КорСтатьяАктивовПассивов = Факт.СтатьяАктивовПассивов
	|			И ДД.КорАналитикаАктивовПассивов = Факт.АналитикаАктивовПассивов
	|ГДЕ
	|	ДД.ТипЗаписи В (ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.СписаниеНаРасходы))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""НаЭтапыВручную""                      КАК ТипЗаписи,
	|	ДД.КорПодразделение                     КАК Подразделение,
	|	ВЫБОР
	|		КОГДА ДД.ПартияПроизводства <> НЕОПРЕДЕЛЕНО
	|			ТОГДА ДД.ПартияПроизводства
	|		ИНАЧЕ
	|			ДД.ЗаказНаПроизводство
	|	КОНЕЦ                                   КАК ПартияПроизводства,
	|	ДД.ДоляСтоимости                        КАК ЗначениеПоказателя,
	|	ДД.ДоляСтоимости                        КАК ЗначениеПоказателяПроцент,
	|	ЕСТЬNULL(Факт.Сумма, 0)                 КАК Сумма,
	|	ЕСТЬNULL(Факт.СуммаРегл, 0)             КАК СуммаРегл,
	|	НЕОПРЕДЕЛЕНО                            КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО                            КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                            КАК АналитикаАктивовПассивов,
	|	&Регистратор                            КАК Регистратор,
	|	НЕОПРЕДЕЛЕНО                            КАК МатериалПродукцияУслуга,
	|	НЕОПРЕДЕЛЕНО                            КАК ГруппаПродукции,
	|	1                                       КАК ПорядокВывода,
	|	ИСТИНА                                  КАК СуммироватьКоличествоПоЭтапу
	|ИЗ
	|	ДолиПроизводственныхРасходов КАК ДД
	|		ЛЕВОЕ СОЕДИНЕНИЕ НаПартииФакт КАК Факт
	|			ПО ДД.КорПодразделение = Факт.Подразделение
	|			И ДД.ПартияПроизводства = Факт.ПартияПроизводства
	|			И ДД.ЗаказНаПроизводство = Факт.ЗаказНаПроизводство
	|			И ДД.КодСтрокиПродукция = Факт.КодСтрокиПродукция
	|ГДЕ
	|	ДД.ТипЗаписи В (ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Дополнение))
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДД.ПартияПроизводства
	|ИЗ
	|	НаЭтапыДетально КАК ДД
	|";
	
	Если РасчетСебестоимостиВыполнен Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ДанныеПоПредварительнымМатериальнымЗатратам", "ДанныеПоФактическимМатериальнымЗатратам");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Регистратор",       Регистратор);
	Запрос.УстановитьПараметр("МассивКоличества",  МассивТиповРаспределенияБазыПоКоличеству);
	Запрос.УстановитьПараметр("МассивСтоимости",   МассивТиповРаспределенияБазыПоСтоимости);
	Запрос.УстановитьПараметр("МассивОбъема",      МассивТиповРаспределенияБазыПоОбъему);
	Запрос.УстановитьПараметр("МассивВеса",        МассивТиповРаспределенияБазыПоВесу);
	Запрос.УстановитьПараметр("ПравилоРаспределенияПоПодразделениям", Реквизиты.ПравилоРаспределенияПоПодразделениям);
	Запрос.УстановитьПараметр("ПравилоРаспределенияПоЭтапам",         Реквизиты.ПравилоРаспределенияПоЭтапам);
	Запрос.УстановитьПараметр("БазаРаспределенияПоЭтапам",            Реквизиты.БазаРаспределенияПоЭтапам);
	Запрос.УстановитьПараметр("БазаРаспределенияПоПодразделениям",    Реквизиты.БазаРаспределенияПоПодразделениям);
	Запрос.УстановитьПараметр("ВариантРаспределения",                 Реквизиты.ВариантРаспределения);
	Запрос.УстановитьПараметр("НачалоПериода",                        НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода",                     ОкончаниеПериода);
	Запрос.УстановитьПараметр("Организация",                          Реквизиты.Организация);
	Запрос.УстановитьПараметр("Подразделение",                        Реквизиты.Подразделение);
	
	Результат = Запрос.ВыполнитьПакет();
	ИндексыРезультатов = Результат.ВГраница();
	
	Распределение = Результат[ИндексыРезультатов - 1].Выгрузить();
	
	ВнешниеНаборыДанных = Новый Структура("Распределение", Распределение);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;

	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ВыводитьПредставлениеСумм", ВыводитьПредставлениеСумм, Истина);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ВыводитьПредставлениеРасчетаСебестоимости", Не РасчетСебестоимостиВыполнен, Истина);
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	//Пост обработка отчета.
	//Устанавливаем отражение правила распределения, меняем заголовки колонок и т.д.
	
	ТабличныйДокумент = ПроцессорВывода.ЗакончитьВывод();
	
	Область = ТабличныйДокумент.НайтиТекст("%ЗаголовокОтчета%");
	Если Область <> Неопределено Тогда
		
		Если ЗначениеЗаполнено(Реквизиты.АналитикаРасходов) Тогда
			ЗаголовокОтчета = НСтр("ru = 'По статье ""%1 (%2)"" в подразделении ""%3"" за %4'");
			ЗаголовокОтчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ЗаголовокОтчета,
				Реквизиты.СтатьяРасходов,
				Реквизиты.АналитикаРасходов,
				Реквизиты.Подразделение,
				Формат(Реквизиты.Дата, "ДФ='ММММ гггг'"));
		Иначе
			ЗаголовокОтчета = НСтр("ru = 'По статье ""%1"" в подразделении ""%2"" за %3'");
			ЗаголовокОтчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ЗаголовокОтчета,
				Реквизиты.СтатьяРасходов,
				Реквизиты.Подразделение,
				Формат(Реквизиты.Дата, "ДФ='ММММ гггг'"));
		КонецЕсли;
		
		Область.Текст = СтрЗаменить(Область.Текст, "%ЗаголовокОтчета%", ЗаголовокОтчета);
		
	КонецЕсли;
	
	//Вывод представления сумм
	Область = ТабличныйДокумент.НайтиТекст("%ПредставлениеСумм%");
	Если Область <> Неопределено Тогда
		Область.Текст = СтрЗаменить(Область.Текст, "%ПредставлениеСумм%", ПредставлениеСумм);;
	КонецЕсли;
	
	//Вывод информации о состоянии расчета себестоимости
	Область = ТабличныйДокумент.НайтиТекст("%ПредставлениеРасчетаСебестоимости%");
	Если Область <> Неопределено Тогда
		ПредставлениеРасчетаСебестоимости = НСтр("ru = 'Не выполнен расчет себестоимости, суммы расходов будут актуализироваты после расчета.'");
		Область.Текст = СтрЗаменить(Область.Текст, "%ПредставлениеРасчетаСебестоимости%", ПредставлениеРасчетаСебестоимости);
	КонецЕсли;
	
	//Инициализация переменных для оформления правила по подразделению
	ПредставлениеПоказателяСтоимость = СтрШаблон(НСтр("ru = 'Стоимость, %1'"), ВалютаУпр);
	ПредставлениеПозицииПоПодразделению  = НСтр("ru = 'Материалы/Работы/Продукция'");
	ПредставлениеПоказателяПодразделений = НСтр("ru = 'Значение показателя'");
	ПредставлениеПравилаПоПодразделениям  = НСтр("ru = 'Пропорционально стоимости материальных затрат'");
	
	//Инициализация переменных для оформления правила по этапу
	ПредставлениеПозицииПоЭтапу    = НСтр("ru = 'Материалы/Работы/Продукция'");
	ПредставлениеПоказателяПоЭтапу = НСтр("ru = 'Значение показателя'");
	ПредставлениеПравилаПоЭтапу    = НСтр("ru = 'Прочее правило распределения %1'");
	
	ОтборПоГруппамПродукции = Регистратор.ОтборПоГруппамПродукции.Количество() > 0;
	
	БазаРаспределенияПоПодразделениям = Реквизиты.БазаРаспределенияПоПодразделениям;
	БазаРаспределенияПоЭтапам         = Реквизиты.БазаРаспределенияПоЭтапам;
	ВариантРаспределения              = Реквизиты.ВариантРаспределения;
	
	Если БазаРаспределенияПоПодразделениям = Перечисления.ТипыБазыРаспределенияРасходов.СуммаМатериальныхЗатрат Тогда 
		ПредставлениеПозицииПоПодразделению  = НСтр("ru = 'Материал'");
		ПредставлениеПоказателяПодразделений = ПредставлениеПоказателяСтоимость;
		ПредставлениеПравилаПоПодразделениям  = НСтр("ru = '%1 пропорционально стоимости материалов %2'");
	ИначеЕсли БазаРаспределенияПоПодразделениям = Перечисления.ТипыБазыРаспределенияРасходов.СуммаРасходовНаОплатуТруда Тогда
		ПредставлениеПозицииПоПодразделению  = НСтр("ru = 'Вид работ'");
		ПредставлениеПоказателяПодразделений = ПредставлениеПоказателяСтоимость;
		ПредставлениеПравилаПоПодразделениям  = НСтр("ru = '%1 пропорционально расходам на оплату труда %2'");
	ИначеЕсли БазаРаспределенияПоПодразделениям = Перечисления.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат Тогда
		ПредставлениеПозицииПоПодразделению  = НСтр("ru = 'Материал \ Вид работ'");
		ПредставлениеПоказателяПодразделений = ПредставлениеПоказателяСтоимость;
		ПредставлениеПравилаПоПодразделениям  = НСтр("ru = '%1 пропорционально прямым затратам %2'");
	ИначеЕсли БазаРаспределенияПоПодразделениям = Перечисления.ТипыБазыРаспределенияРасходов.ВводитсяЕжемесячно Тогда
		ПредставлениеПравилаПоПодразделениям  = НСтр("ru = '%1 по переменным показателям %2'");
	ИначеЕсли БазаРаспределенияПоПодразделениям = Перечисления.ТипыБазыРаспределенияРасходов.ВводитсяПриИзменении Тогда
		ПредставлениеПравилаПоПодразделениям  = НСтр("ru = '%1 по постоянным показателям %2'");
	ИначеЕсли ВариантРаспределения = Перечисления.СпособыРаспределенияСтатейРасходов.ПоПодразделениямВручнуюПоЭтапамПоПравилу Тогда
		ПредставлениеПоказателяПодразделений = НСтр("ru = 'Доля стоимости'");
		ПредставлениеПравилаПоПодразделениям  = НСтр("ru = '%1 вручную %2'");
	КонецЕсли;
	
	Если БазаРаспределенияПоЭтапам = Перечисления.ТипыБазыРаспределенияРасходов.СуммаМатериальныхЗатрат Тогда 
		
		ПредставлениеПозицииПоЭтапу    = НСтр("ru = 'Материал'");
		ПредставлениеПоказателяПоЭтапу = ПредставлениеПоказателяСтоимость;
		ПредставлениеПравилаПоЭтапу    = НСтр("ru = '%1 пропорционально стоимости материалов %2'");
			
	ИначеЕсли БазаРаспределенияПоЭтапам = Перечисления.ТипыБазыРаспределенияРасходов.КоличествоУказанныхМатериалов Тогда
		
		ПредставлениеПозицииПоЭтапу    = НСтр("ru = 'Материал'");
		ПредставлениеПоказателяПоЭтапу = НСтр("ru = 'Количество'");
		ПредставлениеПравилаПоЭтапу    = НСтр("ru = '%1 пропорционально количеству материалов %2'");
			
	ИначеЕсли БазаРаспределенияПоЭтапам = Перечисления.ТипыБазыРаспределенияРасходов.ОбъемУказанныхМатериалов Тогда
		
		ПредставлениеПозицииПоЭтапу    = НСтр("ru = 'Материал'");
		ПредставлениеПоказателяПоЭтапу = НСтр("ru = 'Объем'");
		ПредставлениеПравилаПоЭтапу    = НСтр("ru = '%1 пропорционально объему материалов %2'");
		
	ИначеЕсли БазаРаспределенияПоЭтапам = Перечисления.ТипыБазыРаспределенияРасходов.ВесУказанныхМатериалов Тогда
		
		ПредставлениеПозицииПоЭтапу    = НСтр("ru = 'Материал'");
		ПредставлениеПоказателяПоЭтапу = НСтр("ru = 'Вес'");
		ПредставлениеПравилаПоЭтапу    = НСтр("ru = '%1 пропорционально весу материалов %2'");
		
	ИначеЕсли БазаРаспределенияПоЭтапам = Перечисления.ТипыБазыРаспределенияРасходов.СуммаРасходовНаОплатуТруда Тогда
		
		ПредставлениеПозицииПоЭтапу    = НСтр("ru = 'Вид работ'");;
		ПредставлениеПоказателяПоЭтапу = ПредставлениеПоказателяСтоимость;
		ПредставлениеПравилаПоЭтапу    = НСтр("ru = '%1 пропорционально расходам на оплату труда %2'");
		
	ИначеЕсли БазаРаспределенияПоЭтапам = Перечисления.ТипыБазыРаспределенияРасходов.НормативыОплатыТруда Тогда
		
		ПредставлениеПозицииПоЭтапу    = НСтр("ru = 'Вид работ'");;
		ПредставлениеПоказателяПоЭтапу = ПредставлениеПоказателяСтоимость;
		ПредставлениеПравилаПоЭтапу    = НСтр("ru = '%1 пропорционально нормативным расходам на оплату труда %2'");
		
	ИначеЕсли БазаРаспределенияПоЭтапам = Перечисления.ТипыБазыРаспределенияРасходов.КоличествоРаботУказанныхВидов Тогда
		
		ПредставлениеПозицииПоЭтапу    = НСтр("ru = 'Вид работ'");
		ПредставлениеПоказателяПоЭтапу = НСтр("ru = 'Количество'");
		ПредставлениеПравилаПоЭтапу    = НСтр("ru = '%1 пропорционально количеству видов работ %2'");
		
	ИначеЕсли БазаРаспределенияПоЭтапам = Перечисления.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукции Тогда
		
		ПредставлениеПозицииПоЭтапу    = НСтр("ru = 'Продукция'");
		ПредставлениеПоказателяПоЭтапу = ПредставлениеПоказателяСтоимость;
		ПредставлениеПравилаПоЭтапу    = НСтр("ru = '%1 пропорционально плановой стоимости продукции %2'");
		
	ИначеЕсли БазаРаспределенияПоЭтапам = Перечисления.ТипыБазыРаспределенияРасходов.КоличествоПродукции Тогда
		
		ПредставлениеПозицииПоЭтапу    = НСтр("ru = 'Продукция'");
		ПредставлениеПоказателяПоЭтапу = НСтр("ru = 'Количество'");
		ПредставлениеПравилаПоЭтапу    = НСтр("ru = '%1 пропорционально количеству продукции %2'");
		
	ИначеЕсли БазаРаспределенияПоЭтапам = Перечисления.ТипыБазыРаспределенияРасходов.ОбъемПродукции Тогда
		
		ПредставлениеПозицииПоЭтапу    = НСтр("ru = 'Продукция'");
		ПредставлениеПоказателяПоЭтапу = НСтр("ru = 'Объем'");
		ПредставлениеПравилаПоЭтапу    = НСтр("ru = '%1 пропорционально объему продукции %2'");
		
	ИначеЕсли БазаРаспределенияПоЭтапам = Перечисления.ТипыБазыРаспределенияРасходов.ВесПродукции Тогда
		
		ПредставлениеПозицииПоЭтапу    = НСтр("ru = 'Продукция'");
		ПредставлениеПоказателяПоЭтапу = НСтр("ru = 'Вес'");
		ПредставлениеПравилаПоЭтапу    = НСтр("ru = '%1 пропорционально весу продукции %2'");
		
	ИначеЕсли БазаРаспределенияПоЭтапам = Перечисления.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат Тогда
		
		ПредставлениеПозицииПоЭтапу    = НСтр("ru = 'Материал \ Вид работ'");
		ПредставлениеПоказателяПоЭтапу = ПредставлениеПоказателяСтоимость;
		ПредставлениеПравилаПоЭтапу    = НСтр("ru = '%1 пропорционально прямым затратам %2'");
		
	КонецЕсли;
	
	ПредставлениеНаСтатьи = "";
	ПрефиксПредставленияПравила = "";
	ПрефиксПредставленияПравилаНаЭтапыВручную = "";
	ПостфиксПредставленияПравила = "";
	ПредставлениеПравила = "";
	ПредставлениеПравилаПоЭтапам = "";
	ПредставлениеНаЭтапыВручную = "";
	Если Реквизиты.ДоляСтоимостиНаСтатьи > 0 И Реквизиты.ВсегоДолейСтоимости <> Реквизиты.ДоляСтоимостиНаСтатьи Тогда
		ПроцентПоПравилам = Окр((Реквизиты.ВсегоДолейСтоимости - Реквизиты.ДоляСтоимостиНаСтатьи) * 100 / Реквизиты.ВсегоДолейСтоимости);
		ПрефиксПредставленияПравила = СтрШаблон(НСтр("ru = '%1%% распределяется'"), ПроцентПоПравилам);
		ПрефиксПредставленияПравилаНаЭтапыВручную = СтрШаблон(НСтр("ru = '%1%% распределяется'"), ПроцентПоПравилам);
		ПредставлениеНаСтатьи = СтрШаблон(НСтр("ru = '%1%% распределяется на другие статьи'"), 100 - ПроцентПоПравилам);
	Иначе
		ПредставлениеНаСтатьи = НСтр("ru = 'Расходы полностью распределяются на другие статьи'");
	КонецЕсли;
	
	Если ОтборПоГруппамПродукции Тогда
		ПостфиксПредставленияПравила = НСтр("ru = 'на выбранные группы продукции'");
	КонецЕсли;
	
	Если Реквизиты.ВариантРаспределения = Перечисления.СпособыРаспределенияСтатейРасходов.ПоЭтапамПоПравилуПоВсемПодразделениям Тогда
		
		Если ЗначениеЗаполнено(ПрефиксПредставленияПравила) Тогда
			ПрефиксПредставленияПравила = СтрШаблон(НСтр("ru = '%1 по партиям всех подразделений'"), ПрефиксПредставленияПравила);
		Иначе
			ПрефиксПредставленияПравила = НСтр("ru = 'По партиям всех подразделений'");
		КонецЕсли;
		ПредставлениеПравилаПоЭтапам = СтрШаблон(ПредставлениеПравилаПоЭтапу, ПрефиксПредставленияПравила, ПостфиксПредставленияПравила);
		
	ИначеЕсли Реквизиты.ВариантРаспределения = Перечисления.СпособыРаспределенияСтатейРасходов.ПоЭтапамПоПравилуВДанномПодразделении Тогда
		
		Если ЗначениеЗаполнено(ПрефиксПредставленияПравила) Тогда
			ПрефиксПредставленияПравила = СтрШаблон(НСтр("ru = '%1 по партиям подразделения'"), ПрефиксПредставленияПравила);
		Иначе
			ПрефиксПредставленияПравила = НСтр("ru = 'По партиям подразделения'");
		КонецЕсли;
		ПредставлениеПравилаПоЭтапам = СтрШаблон(ПредставлениеПравилаПоЭтапу, ПрефиксПредставленияПравила, ПостфиксПредставленияПравила);
	
	ИначеЕсли Реквизиты.ВариантРаспределения = Перечисления.СпособыРаспределенияСтатейРасходов.ПоЭтапамВручнуюПоВсемПодразделениям Тогда
		
		Если ЗначениеЗаполнено(ПрефиксПредставленияПравилаНаЭтапыВручную) Тогда
			ПредставлениеНаЭтапыВручную = СтрШаблон(НСтр("ru = '%1 по этапам вручную'"), ПрефиксПредставленияПравила);
		Иначе
			ПредставлениеНаЭтапыВручную = НСтр("ru = 'Расходы распределяются по этапам вручную'");
		КонецЕсли;
		
	ИначеЕсли Реквизиты.ВариантРаспределения = Перечисления.СпособыРаспределенияСтатейРасходов.ПоПодразделениямИЭтапамПоПравилам
		Или Реквизиты.ВариантРаспределения = Перечисления.СпособыРаспределенияСтатейРасходов.ПоПодразделениямВручнуюПоЭтапамПоПравилу Тогда
		
		Если ЗначениеЗаполнено(ПрефиксПредставленияПравила) Тогда
			ПрефиксПредставленияПравила = СтрШаблон(НСтр("ru = '%1 по подразделениям'"), ПрефиксПредставленияПравила);
		Иначе
			ПрефиксПредставленияПравила = НСтр("ru = 'Расходы распределеяются по подразделениям'");
		КонецЕсли;
		
		ПредставлениеПравилаПоПодразделениям = СтрШаблон(ПредставлениеПравилаПоПодразделениям, ПрефиксПредставленияПравила, ПостфиксПредставленияПравила);
		ПредставлениеПравилаПоЭтапам = СтрШаблон(ПредставлениеПравилаПоЭтапу, НСтр("ru = 'По партиям'"));
		
	КонецЕсли;
	
	//Заменим для подразделений
	
	Область = ТабличныйДокумент.НайтиТекст("%ПредставлениеПравилаПоПодразделениям%");
	Если Область <> Неопределено Тогда
		Область.Текст = СтрЗаменить(Область.Текст, "%ПредставлениеПравилаПоПодразделениям%", ПредставлениеПравилаПоПодразделениям);
	КонецЕсли;
	
	Область = ТабличныйДокумент.НайтиТекст("%ПредставлениеНаЭтапыВручную%");
	Если Область <> Неопределено Тогда
		Область.Текст = СтрЗаменить(Область.Текст, "%ПредставлениеНаЭтапыВручную%", ПредставлениеНаЭтапыВручную);
	КонецЕсли;
	
	Область = ТабличныйДокумент.НайтиТекст("%ЗначПП%");
	Пока Область <> Неопределено Цикл
		Область.Текст = ПредставлениеПоказателяПодразделений;
		Область = ТабличныйДокумент.НайтиТекст("%ЗначПП%");
	КонецЦикла;
	
	Область = ТабличныйДокумент.НайтиТекст("%ПредставлениеПозицииПоПодразделениям%");
	Пока Область <> Неопределено Цикл
		Область.Текст = ПредставлениеПозицииПоПодразделению;
		Область = ТабличныйДокумент.НайтиТекст("%ПредставлениеПозицииПоПодразделениям%");
	КонецЦикла;
	
	//Заменим для этапов
	
	Область = ТабличныйДокумент.НайтиТекст("%ПредставлениеПравилаПоЭтапам%");
	Если Область <> Неопределено Тогда
		Область.Текст = СтрЗаменить(Область.Текст, "%ПредставлениеПравилаПоЭтапам%", ПредставлениеПравилаПоЭтапам);
	КонецЕсли;
	
	Область = ТабличныйДокумент.НайтиТекст("%ПредставлениеПозицииПоЭтапам%");
	Пока Область <> Неопределено Цикл
		Область.Текст = ПредставлениеПозицииПоЭтапу;
		Область = ТабличныйДокумент.НайтиТекст("%ПредставлениеПозицииПоЭтапам%");
	КонецЦикла;
	
	Область = ТабличныйДокумент.НайтиТекст("%ЗначПЭ%");
	Пока ТипЗнч(Область) <> Тип("Неопределено") Цикл
		Область.Текст = ПредставлениеПоказателяПоЭтапу;
		Область = ТабличныйДокумент.НайтиТекст("%ЗначПЭ%");
	КонецЦикла;
	
	Область = ТабличныйДокумент.НайтиТекст("%ПредставлениеПозицииВСтроке%");
	Пока Область <> Неопределено Цикл
		
		Если ПредставлениеПозицииПоЭтапу = НСтр("ru = 'Продукция'") Тогда
			ПредставлениеПозицииВСтроке = НСтр("ru = '<другая продукция>'");
		Иначе
			ПредставлениеПозицииВСтроке = СтрШаблон(НСтр("ru = '<другие %1>'"), НРег(ПредставлениеПозицииПоЭтапу));
		КонецЕсли;
		
		Область.Текст = СтрЗаменить(Область.Текст, "%ПредставлениеПозицииВСтроке%", ПредставлениеПозицииВСтроке);;
		Область = ТабличныйДокумент.НайтиТекст("%ПредставлениеПозицииВСтроке%");
		
	КонецЦикла;
	
	//Заменим для распределения на статьи
	
	Область = ТабличныйДокумент.НайтиТекст("%ПредставлениеНаСтатьи%");
	Если Область <> Неопределено Тогда
		Область.Текст = СтрЗаменить(Область.Текст, "%ПредставлениеНаСтатьи%", ПредставлениеНаСтатьи);;
	КонецЕсли;
	
	//Установка заголовков валют
	
	Область = ТабличныйДокумент.НайтиТекст("%ВалютаУпр%");
	Пока Область <> Неопределено Цикл
		Область.Текст = СтрЗаменить(Область.Текст, "%ВалютаУпр%", ВалютаУпр);
		Область = ТабличныйДокумент.НайтиТекст("%ВалютаУпр%");
	КонецЦикла;
	
	Область = ТабличныйДокумент.НайтиТекст("%ВалютаРегл%");
	Пока Область <> Неопределено Цикл
		Область.Текст = СтрЗаменить(Область.Текст, "%ВалютаРегл%", ВалютаРегл);
		Область = ТабличныйДокумент.НайтиТекст("%ВалютаРегл%");
	КонецЦикла;
	
	ТабличныйДокумент.ПоказатьУровеньГруппировокСтрок(2);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли
