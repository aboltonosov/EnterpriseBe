&НаКлиенте
Перем ПараметрыОбработчикаОжидания;

&НаКлиенте
Перем ФормаДлительнойОперации;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЗагрузитьНастройкиФормы();
	
	Если Параметры.Свойство("Заказы") Тогда
		
		Заказы.ЗагрузитьЗначения(Параметры.Заказы);
		УстановитьЗаголовок();
		ОбновитьДанныеНаСервере(Ложь);
		
	КонецЕсли;
	
	НастроитьФормуПриСоздании();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НачатьОжиданиеДлительнойОперации Тогда
		
		ПодключитьОбработчикОжидания("НачатьОжиданиеДлительнойОперации", 0.1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если НЕ ДлительнаяОперация = Неопределено Тогда
		ОтменитьДлительнуюОперацию(ДлительнаяОперация.ИдентификаторЗадания);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СкрыватьВыполненноеПриИзменении(Элемент)
	
	ОбновитьДанныеНаКлиенте(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьЭтапыПриИзменении(Элемент)
	
	ОбновитьДанныеНаКлиенте(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоЭтапов

&НаКлиенте
Процедура СтруктураЗаказовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СтрокаДерева = Элемент.ДанныеСтроки(ВыбраннаяСтрока);
	
	Если СтрокаДерева.ТипСтроки = ТипСтрокиЭтап() Тогда
		
		ПоказатьЗначение(, СтрокаДерева.Этап);
		
	ИначеЕсли СтрокаДерева.ТипСтроки = ТипСтрокиПолуфабрикат() Тогда
		
		ПоказатьЗначение(, СтрокаДерева.Номенклатура);
		
	ИначеЕсли СтрокаДерева.ТипСтроки = ТипСтрокиПродукция() Тогда
		
		ОткрытьСтрокуЗаказаНаПроизводства(СтрокаДерева);
		
	ИначеЕсли СтрокаДерева.ТипСтроки = ТипСтрокиЗаказ() Тогда
		
		ПоказатьЗначение(, СтрокаДерева.Заказ);
		
	ИначеЕсли СтрокаДерева.ТипСтроки = ТипСтрокиПолуфабрикатДругогоЗаказа() Тогда
		
		ОткрытьФормуСтруктурыПолуфабрикатаДругогоЗаказа(СтрокаДерева);
		
	ИначеЕсли СтрокаДерева.ТипСтроки = ТипСтрокиСоздатьЭтапы() Тогда
		
		ОткрытьФормуСозданияЭтапов(СтрокаДерева);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтруктураЗаказовПередРазворачиванием(Элемент, Строка, Отказ)
	
	СтрокаДерева = Элемент.ДанныеСтроки(Строка);
	Если НЕ СтрокаДерева.ПотомкиВыведены Тогда
		
		ДостроитьДеревоЭтапов(СтрокаДерева);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьДанныеНаКлиенте(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьПроцентВыполнения(Команда)
	
	ПоказыватьПроцентВыполнения = НЕ ПоказыватьПроцентВыполнения;
	
	УправлениеВидимостью(ЭтотОбъект);
	
	СохранитьНастройкиФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДиаграммаГанта(Команда)
	
	ОчиститьСообщения();
	
	ТекущиеДанные = Элементы.СтруктураЗаказов.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не выбрана строка структуры заказа'"),
			,
			"СтруктураЗаказов");
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.ТипСтроки = ТипСтрокиЗаказ() Тогда
		
		ОткрытьДиаграммуГантаЗаказа(ТекущиеДанные);
		
	ИначеЕсли ТекущиеДанные.ТипСтроки = ТипСтрокиПолуфабрикат()
		ИЛИ ТекущиеДанные.ТипСтроки = ТипСтрокиПродукция()
		И ЗначениеЗаполнено(ТекущиеДанные.ВыпускающийЭтап) Тогда
		
		ОткрытьДиаграммуГантаПродукции(ТекущиеДанные);
		
	ИначеЕсли ТекущиеДанные.ТипСтроки = ТипСтрокиЭтап() Тогда
		
		ОткрытьДиаграммуГантаЭтапа(ТекущиеДанные);
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Формирование отчета для выбранной строки недоступно'"));
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДиагностикаЭтапа(Команда)
	
	ОчиститьСообщения();
	
	ТекущиеДанные = Элементы.СтруктураЗаказов.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не выбрана строка структуры заказа'"),
			,
			"СтруктураЗаказов");
		
	ИначеЕсли ТекущиеДанные.ТипСтроки = ТипСтрокиЭтап() Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ПараметрКоманды", ТекущиеДанные.Этап);
		
		ОткрытьФорму(
			"Отчет.ДиагностикаЭтапаПроизводства.Форма",
			ПараметрыФормы,
			ЭтотОбъект,
			ТекущиеДанные.Этап);
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Для формирования отчета необходимо выбрать этап производства'"));
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЦепочкуЭтапов(Команда)
	
	ОчиститьСообщения();
	
	ТекущиеДанные = Элементы.СтруктураЗаказов.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не выбрана строка структуры заказа'"),
			,
			"СтруктураЗаказов");
		
	ИначеЕсли ( ТекущиеДанные.ТипСтроки = ТипСтрокиПродукция()
			ИЛИ ТекущиеДанные.ТипСтроки = ТипСтрокиПолуфабрикат()
			ИЛИ ТекущиеДанные.ТипСтроки = ТипСтрокиЭтап()
			) И Не ТекущиеДанные.ВыпускающийЭтап.Пустая() Тогда
		
		УправлениеПроизводствомКлиент.ОткрытьПоследовательностьЭтапов(
			ТекущиеДанные.Заказ,
			ТекущиеДанные.ВыпускающийЭтап,
			,
			ЭтаФорма);
		
	Иначе
		
		ТекстСообщения = НСтр("ru = 'Для текущей строки невозможно применить выбранное действие'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьНаУдалениеЦепочкуЭтапов(Команда)
	
	ОчиститьСообщения();
	
	ТекущиеДанные = Элементы.СтруктураЗаказов.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не выбрана строка структуры заказа'"),
			,
			"СтруктураЗаказов");
		
	ИначеЕсли ТекущиеДанные.ТипСтроки = ТипСтрокиЗаказ() Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПометитьНаУдалениеЦепочкуЭтаповЗавершение", ЭтаФорма, ТекущиеДанные.Заказ);
	
		Представление = УправлениеПроизводствомВызовСервера.ПредставлениеЗаказа(ТекущиеДанные.Заказ,"");
		ТекстВопроса = СтрШаблон(НСтр("ru = 'Пометить на удаление все этапы заказа %1?'"), Представление);
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	ИначеЕсли ( ТекущиеДанные.ТипСтроки = ТипСтрокиПродукция()
			ИЛИ ТекущиеДанные.ТипСтроки = ТипСтрокиПолуфабрикат()
			ИЛИ ТекущиеДанные.ТипСтроки = ТипСтрокиЭтап()
			) И Не ТекущиеДанные.ВыпускающийЭтап.Пустая() Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПометитьНаУдалениеЦепочкуЭтаповЗавершение", ЭтаФорма, ТекущиеДанные.ВыпускающийЭтап);
	
		Представление = УправлениеПроизводствомВызовСервера.ПредставлениеЭтапа(ТекущиеДанные.ВыпускающийЭтап);
		ТекстВопроса = СтрШаблон(НСтр("ru = 'Пометить на удаление цепочку этапов %1?'"), Представление);
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		
		ТекстСообщения = НСтр("ru = 'Для текущей строки невозможно применить выбранное действие'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьНаУдалениеЦепочкуЭтаповЗавершение(РезультатВопроса, ПараметрыЗадания) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ЗапуститьФоновоеЗадание("ПометитьНаУдалениеЭтапыПроизводства", ПараметрыЗадания);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ФормированиеОтчета

&НаКлиенте
Процедура ОбновитьДанныеНаКлиенте(СохранитьНастройкиФормы)
	
	ОбновитьДанныеНаСервере(СохранитьНастройкиФормы);
	
	Если НачатьОжиданиеДлительнойОперации Тогда
		
		НачатьОжиданиеДлительнойОперации();
		
	Иначе
		
		РаскрытьВерхнийУровеньСтруктурыЗаказов();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеНаСервере(СохранитьНастройкиФормы)
	
	Если СохранитьНастройкиФормы Тогда
		СохранитьНастройкиФормы(ЭтотОбъект);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Заказы) Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьИнформационнуюПанель();
	
	Если НЕ ДлительнаяОперация = Неопределено Тогда
		ОтменитьДлительнуюОперацию(ДлительнаяОперация.ИдентификаторЗадания);
	КонецЕсли;
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("Заказы", Заказы.ВыгрузитьЗначения());
	ПараметрыПроцедуры.Вставить("Номенклатура", РеквизитФормыВЗначение("Номенклатура"));
	ПараметрыПроцедуры.Вставить("Этапы", РеквизитФормыВЗначение("Этапы"));
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания =
		НСтр("ru = 'Формирование отчета ""Структура заказа на производство""'");
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"Отчеты.СтруктураЗаказаНаПроизводство2_2.ПрочитатьСтруктуруЗаказов",
		ПараметрыПроцедуры,
		ПараметрыВыполнения);
	
	Если ДлительнаяОперация.Статус = "Выполняется" Тогда
		
		НачатьОжиданиеДлительнойОперации = Истина;
		
	Иначе
		
		ОбработатьРезультатЧтенияСтруктурыЗаказовВФоне(ДлительнаяОперация);
		
		НачатьОжиданиеДлительнойОперации = Ложь;
		ДлительнаяОперация = Неопределено;
		
	КонецЕсли;
		
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтменитьДлительнуюОперацию(ИдентификаторЗадания)
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьОжиданиеДлительнойОперации()
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПрочитатьСтруктуруЗаказовВФонеЗавершение", ЭтотОбъект);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		ДлительнаяОперация,
		ОповещениеОЗавершении,
		ПараметрыОжидания);
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаДлительнаяОперация;
	
	НачатьОжиданиеДлительнойОперации = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьСтруктуруЗаказовВФонеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ДлительнаяОперация = Неопределено;
	
	Если Результат = Неопределено Тогда
		
		ОчиститьИПоказатьСтруктуруЗаказов();
		
	Иначе
		
		ОбработатьРезультатЧтенияСтруктурыЗаказовВФоне(Результат);
		РаскрытьВерхнийУровеньСтруктурыЗаказов();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатЧтенияСтруктурыЗаказовВФоне(Результат)
	
	Если Результат.Статус = "Выполнено" Тогда
		
		ПрочитанныеДанные = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		
		ЗначениеВРеквизитФормы(ПрочитанныеДанные.Номенклатура, "Номенклатура");
		ЗначениеВРеквизитФормы(ПрочитанныеДанные.Этапы, "Этапы");
		
		ДобавитьВДеревоКорневыеСтроки(ПрочитанныеДанные.ИндексыСтрокПродукции);
		
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаДанные;
		
	Иначе
		
		ОчиститьИПоказатьСтруктуруЗаказов();
		
		Если Результат.Статус = "Ошибка" Тогда
			
			ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьИПоказатьСтруктуруЗаказов()
	
	СтруктураЗаказов.ПолучитьЭлементы().Очистить();
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаДанные;
	
КонецПроцедуры

#КонецОбласти

#Область ФормированиеЭтапов

&НаКлиенте
Процедура СформироватьЭтапыПроизводства(Команда)
	
	МассивСсылок = Заказы.ВыгрузитьЗначения();
	Если МассивСсылок.ВГраница() = -1 Тогда
		Возврат;
	КонецЕсли;
	
	ОпределитьПараметрыИСформироватьЭтапы(МассивСсылок);
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьГруппуТребуетсяСформироватьЭтапыНажатие(Элемент)
	
	НеПоказыватьКомандуТребуетсяСформироватьЭтапы = Истина;
	ОбновитьИнформационнуюПанель();
	
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьПараметрыИСформироватьЭтапы(Распоряжения)
	
	ПараметрыФормы = УправлениеПроизводствомКлиентСервер.ПараметрыФормыИсполнениеЗаказа();
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
			"ОпределитьПараметрыИСформироватьЭтапыЗавершение", 
			ЭтотОбъект, Распоряжения);
	
	ОткрытьФорму("Перечисление.ВариантыОбеспечения.Форма.ИсполнениеЗаказа", 
		ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор,,,ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьПараметрыИСформироватьЭтапыЗавершение(Результат, Распоряжения) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("Распоряжения", Распоряжения);
	ПараметрыЗадания.Вставить("ВариантыОбеспечения", Результат.ПереченьВариантов);
	
	ЗапуститьФоновоеЗадание("СформироватьЭтапыПроизводства", ПараметрыЗадания);
	
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеДерева

&НаСервере
Функция ДобавитьВДеревоКорневыеСтроки(ИндексыСтрокПродукции)
	
	Дерево = РеквизитФормыВЗначение("СтруктураЗаказов");
	Дерево.Строки.Очистить();
	
	Для каждого Заказ Из Заказы.ВыгрузитьЗначения() Цикл
		
		СтрокаЗаказ = Дерево.Строки.Добавить();
		СтрокаЗаказ.Заказ = Заказ;
		СтрокаЗаказ.ПредставлениеСтроки = ПредставлениеЗаказа(Заказ);
		СтрокаЗаказ.ТипСтроки = ТипСтрокиЗаказ();
		СтрокаЗаказ.ПотомкиВыведены = Истина;
		
		Для каждого Индекс Из ИндексыСтрокПродукции Цикл
			
			ДанныеПродукции = Номенклатура[Индекс];
			
			Если ДанныеПродукции.Заказ = Заказ Тогда
				
				НоваяСтрока = СтрокаЗаказ.Строки.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеПродукции);
				НоваяСтрока.ИндексНоменклатура = Индекс;
				НоваяСтрока.ТипСтроки          = ТипСтрокиПродукция();
				
				НоваяСтрока.ПредставлениеСтроки = ДанныеПродукции.ПредставлениеНоменклатураЭтап;
				
				ЗаполнитьРассчитываемыеРеквизитыДерева(НоваяСтрока);
				НастроитьВыводПотомковНоменклатуры(ДанныеПродукции, ЭтотОбъект, НоваяСтрока, НоваяСтрока.Строки);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(Дерево, "СтруктураЗаказов");
	
КонецФункции

&НаКлиенте
Процедура ДостроитьДеревоЭтапов(СтрокаДерева)
	
	Потомки = СтрокаДерева.ПолучитьЭлементы();
	
	ПустойПотомок = Потомки[0];
	Потомки.Удалить(ПустойПотомок);
	
	Если СтрокаДерева.ТипСтроки = ТипСтрокиПродукция()
		ИЛИ СтрокаДерева.ТипСтроки = ТипСтрокиПолуфабрикат() Тогда
		
		ДобавитьВДеревоПотомковНоменклатуры(СтрокаДерева.ИндексНоменклатура, Потомки);
		
	ИначеЕсли СтрокаДерева.ТипСтроки = ТипСтрокиЭтап() Тогда
		
		ДобавитьВДеревоПотомковЭтапа(СтрокаДерева.ИндексЭтапы, Потомки);
		
	КонецЕсли;
	
	СтрокаДерева.ПотомкиВыведены = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВДеревоПотомковНоменклатуры(ИндексНоменклатура, Потомки)
	
	СтрокаНоменклатура = Номенклатура[ИндексНоменклатура];
	
	Для каждого ИндексЭтапы Из СтрокаНоменклатура.ИндексыЭтапов Цикл
		
		ДанныеЭтапа = Этапы[ИндексЭтапы];
		
		Если ПоказыватьЭтапы Тогда
			
			Если ПоказыватьЭтап(ДанныеЭтапа, ЭтотОбъект) Тогда
				
				НоваяСтрока = Потомки.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеЭтапа);
				НоваяСтрока.ПредставлениеСтроки = ДанныеЭтапа.ПредставлениеНоменклатураЭтап;
				НоваяСтрока.ИндексЭтапы = ИндексЭтапы;
				НоваяСтрока.ТипСтроки = ТипСтрокиЭтап();
				
				ЗаполнитьРассчитываемыеРеквизитыДерева(НоваяСтрока);
				
				ИмеютсяПотомки = ИмеютсяПотомкиЭтапа(ДанныеЭтапа, ЭтотОбъект);
				Если ИмеютсяПотомки Тогда
					НоваяСтрока.ПотомкиВыведены = Ложь;
					НоваяСтрока.ПолучитьЭлементы().Добавить();
				Иначе
					НоваяСтрока.ПотомкиВыведены = Истина;
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе
			
			Если ИмеютсяПотомкиЭтапа(ДанныеЭтапа, ЭтотОбъект) Тогда
				
				ДобавитьВДеревоПотомковЭтапа(ИндексЭтапы, Потомки);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВДеревоПотомковЭтапа(ИндексЭтапы, Потомки)
	
	СтрокаЭтапа = Этапы[ИндексЭтапы];
	
	Для каждого ИндексНоменклатура Из СтрокаЭтапа.ИндексыНоменклатуры Цикл
		
		ДанныеНоменклатуры = Номенклатура[ИндексНоменклатура];
		
		Если ПоказыватьНоменклатуру(ДанныеНоменклатуры, СтрокаЭтапа, ЭтотОбъект) Тогда
			
			НоваяСтрока = Потомки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеНоменклатуры);
			
			НоваяСтрока.ПредставлениеСтроки = ДанныеНоменклатуры.ПредставлениеНоменклатураЭтап;
			
			НоваяСтрока.ИндексНоменклатура = ИндексНоменклатура;
			
			Если СтрокаЭтапа.Заказ = ДанныеНоменклатуры.Заказ Тогда
				НоваяСтрока.ТипСтроки = ТипСтрокиПолуфабрикат();
			Иначе
				НоваяСтрока.ТипСтроки = ТипСтрокиПолуфабрикатДругогоЗаказа();
				НоваяСтрока.ИнформационнаяСтрока = ПредставлениеЗаказа(ДанныеНоменклатуры.Заказ);
			КонецЕсли;
			
			ЗаполнитьРассчитываемыеРеквизитыДерева(НоваяСтрока);
			НастроитьВыводПотомковНоменклатуры(ДанныеНоменклатуры, ЭтотОбъект, НоваяСтрока, НоваяСтрока.ПолучитьЭлементы());
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьВыводПотомковНоменклатуры(ДанныеНоменклатуры, Форма, СтрокаНоменклатура, Потомки)
	
	ИмеютсяПотомки = ИмеютсяПотомкиНоменклатуры(ДанныеНоменклатуры, Форма);
	
	Если ИмеютсяПотомки Тогда
		
		СтрокаНоменклатура.ПотомкиВыведены = Ложь;
		Потомки.Добавить();
		
	ИначеЕсли ТребуетсяСоздатьЭтапы(ДанныеНоменклатуры, СтрокаНоменклатура) Тогда
		
		СтрокаНоменклатура.ПотомкиВыведены = Истина;
		
		НоваяСтрока = Потомки.Добавить();
		НоваяСтрока.ТипСтроки = ТипСтрокиСоздатьЭтапы();
		НоваяСтрока.Заказ = СтрокаНоменклатура.Заказ;
		НоваяСтрока.ПотомкиВыведены = Истина;
		
	Иначе
		
		СтрокаНоменклатура.ПотомкиВыведены = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИмеютсяПотомкиЭтапа(ДанныеЭтапа, Форма)
	
	Результат = Ложь;
	
	Если ЗначениеЗаполнено(ДанныеЭтапа.ИндексыНоменклатуры) Тогда
		
		Для каждого ИндексНоменклатура Из ДанныеЭтапа.ИндексыНоменклатуры Цикл
			
			ДанныеНоменклатуры = Форма.Номенклатура[ИндексНоменклатура];
			Если ПоказыватьНоменклатуру(ДанныеНоменклатуры, ДанныеЭтапа, Форма) Тогда
				
				Результат = Истина;
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПоказыватьНоменклатуру(ДанныеНоменклатуры, ДанныеЭтапаРодителя, Форма)
	
	Если Форма.СкрыватьВыполненное Тогда
		
		РодительВыполнен = ЭтапВыполненПолуфабрикатПроизведен(ДанныеЭтапаРодителя, Истина);
		ПолуфабрикатПроизведен = ЭтапВыполненПолуфабрикатПроизведен(ДанныеНоменклатуры, Ложь);
		
		Результат = НЕ РодительВыполнен ИЛИ НЕ ПолуфабрикатПроизведен;
		
	Иначе
		
		Результат = Истина;
		
	КонецЕсли;
	
	Возврат Результат;
	
Конецфункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмеютсяПотомкиНоменклатуры(ДанныеНоменклатуры, Форма)
	
	Результат = Ложь;
	
	Если ЗначениеЗаполнено(ДанныеНоменклатуры.ИндексыЭтапов) Тогда
		
		Для каждого ИндексЭтапы Из ДанныеНоменклатуры.ИндексыЭтапов Цикл
			
			ДанныеЭтапа = Форма.Этапы[ИндексЭтапы];
			
			Если Форма.ПоказыватьЭтапы Тогда
				
				Если ПоказыватьЭтап(ДанныеЭтапа, Форма) Тогда
					
					Результат = Истина;
					Прервать;
					
				КонецЕсли;
				
			Иначе
				
				Если ИмеютсяПотомкиЭтапа(ДанныеЭтапа, Форма) Тогда
					
					Результат = Истина;
					Прервать;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПоказыватьЭтап(ДанныеЭтапа, Форма)
	
	Если Форма.ПоказыватьЭтапы Тогда
		
		Если Форма.СкрыватьВыполненное Тогда
			
			Результат = НЕ ЭтапВыполненПолуфабрикатПроизведен(ДанныеЭтапа, Истина);
			
		Иначе
			
			Результат = Истина;
			
		КонецЕсли;
		
	Иначе
		
		Результат = Ложь;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура РаскрытьВерхнийУровеньСтруктурыЗаказов()
	
	СтрокиДерева = СтруктураЗаказов.ПолучитьЭлементы();
	
	Для каждого Строка Из СтрокиДерева Цикл
		
		Идентификатор = Строка.ПолучитьИдентификатор();
		
		Если НЕ Элементы.СтруктураЗаказов.Развернут(Идентификатор) Тогда
			
			Элементы.СтруктураЗаказов.Развернуть(Идентификатор, Ложь);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПредставлениеЗаказа(Заказ)
	
	Возврат Документы.ЗаказНаПроизводство2_2.ПолноеПредставлениеЗаказа(Заказ);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТребуетсяСоздатьЭтапы(ДанныеНоменклатуры, СтрокаНоменклатура)
	
	Возврат СтрокаНоменклатура.ТипСтроки = ТипСтрокиПродукция()
			И ДанныеНоменклатуры.Спецификация.Пустая()
			И Не ЗначениеЗаполнено(ДанныеНоменклатуры.ИндексыЭтапов);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТипСтрокиПродукция()
	
	Возврат 1;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТипСтрокиПолуфабрикат()
	
	Возврат 2;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТипСтрокиЗаказ()
	
	Возврат 3;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТипСтрокиЭтап()
	
	Возврат 4;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТипСтрокиПолуфабрикатДругогоЗаказа()
	
	Возврат 5;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТипСтрокиСоздатьЭтапы()
	
	Возврат 6;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НомерКартинки(ТипСтроки)
	
	Если ТипСтроки = ТипСтрокиПолуфабрикат()
		ИЛИ ТипСтроки = ТипСтрокиПолуфабрикатДругогоЗаказа() Тогда
		
		Результат = 2;
		
	ИначеЕсли ТипСтроки = ТипСтрокиЭтап() Тогда
		
		Результат = 1;
		
	Иначе
		
		Результат = 0;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьРассчитываемыеРеквизитыДерева(СтрокаДерева)
	
	СтрокаДерева.НомерКартинки = НомерКартинки(СтрокаДерева.ТипСтроки);
	
	Если СтрокаДерева.ТипСтроки = ТипСтрокиЭтап()
		ИЛИ СтрокаДерева.ТипСтроки = ТипСтрокиПолуфабрикат()
		ИЛИ СтрокаДерева.ТипСтроки = ТипСтрокиПолуфабрикатДругогоЗаказа() Тогда
		
		СтрокаДерева.ВыполненПроизведен = ЭтапВыполненПолуфабрикатПроизведен(
			СтрокаДерева, СтрокаДерева.ТипСтроки = ТипСтрокиЭтап());
		СтрокаДерева.ВыполненПроизведенРодитель = СтрокаДерева.ПолучитьРодителя().ВыполненПроизведен;
		
	Иначе
		
		СтрокаДерева.ВыполненПроизведен = Ложь;
		
	КонецЕсли;
	
	СтрокаДерева.КоличествоДефицит = СтрокаДерева.КоличествоПлан - СтрокаДерева.КоличествоФакт;
	СтрокаДерева.КоличествоДефицитИтого = СтрокаДерева.КоличествоПланИтого - СтрокаДерева.КоличествоФактИтого;
	
	ПроцентВыполнено = ПроцентВыполнено(СтрокаДерева.КоличествоПлан, СтрокаДерева.КоличествоФакт);
	СтрокаДерева.ПроцентВыполнено = ПредставлениеПроцентВыполнено(ПроцентВыполнено);
	СтрокаДерева.НомерКартинкиПроцентВыполнено = НомерКартинкиПроцентВыполнено(ПроцентВыполнено);
	
	ПроцентВыполнено = ПроцентВыполнено(СтрокаДерева.КоличествоПланИтого, СтрокаДерева.КоличествоФактИтого);
	СтрокаДерева.ПроцентВыполненоИтого = ПредставлениеПроцентВыполнено(ПроцентВыполнено);
	СтрокаДерева.НомерКартинкиПроцентВыполненоИтого = НомерКартинкиПроцентВыполнено(ПроцентВыполнено);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПроцентВыполнено(КоличествоПлан, КоличествоФакт)
	
	Если КоличествоПлан > 0 Тогда
		Результат = Окр(Мин(КоличествоФакт/КоличествоПлан, 1) * 100);
	Иначе
		Результат = 0;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеПроцентВыполнено(ПроцентВыполнено)
	
	Если ПроцентВыполнено > 0 Тогда
		Результат = Формат(ПроцентВыполнено, "ЧЦ=3; ЧН=") + "%";
	Иначе
		Результат = "0%";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НомерКартинкиПроцентВыполнено(ПроцентВыполнено)
	
	Результат =
		?(ПроцентВыполнено =	100,	4,
		?(ПроцентВыполнено >=	70,		3,
		?(ПроцентВыполнено >=	40,		2,
		?(ПроцентВыполнено >	0,		1, 0))));
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтапВыполненПолуфабрикатПроизведен(ДанныеЭтапаПолуфабриката, ЭтоЭтап)
	
	Если ЭтоЭтап Тогда
		
		Результат = ДанныеЭтапаПолуфабриката.Выполнен;
		
	Иначе
		
		Результат = ДанныеЭтапаПолуфабриката.КоличествоФакт = ДанныеЭтапаПолуфабриката.КоличествоПлан;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ФоновыеЗадания

&НаКлиенте
Процедура ЗапуститьФоновоеЗадание(ИмяЗадания, ПараметрыЗадания)
	
	ДобавитьФоновоеЗадание(ИмяЗадания, Истина);
	
	ПараметрыЗаданий = Новый Соответствие;
	ПараметрыЗаданий.Вставить(ИмяЗадания, ПараметрыЗадания);
	
	ЗапуститьФоновыеЗадания(ПараметрыЗаданий);

КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьФоновыеЗадания(ПараметрыЗаданий = Неопределено)

	ВсеЗаданияВыполнены = ЗапуститьФоновыеЗаданияНаСервере(ПараметрыЗаданий);
	
	Если ВсеЗаданияВыполнены Тогда
		ОбработатьЗавершениеЗаданийНаКлиенте();
	Иначе
		ОткрытьФормуДлительнойОперации();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьФоновоеЗадание(ИмяЗадания, ОчиститьОчередь = Ложь)

	Если ОчиститьОчередь Тогда
		ТекущиеФоновыеЗадания.Очистить();
	КонецЕсли;
	
	ДанныеЗадания = ТекущиеФоновыеЗадания.Добавить();
	ДанныеЗадания.ИмяЗадания = ИмяЗадания;
	
КонецПроцедуры

&НаСервере
Функция ЗапуститьФоновыеЗаданияНаСервере(Знач ПараметрыЗаданий)
	
	ИдентификаторЗадания = Новый УникальныйИдентификатор;
	
	ВсеЗаданияВыполнены = Истина;
	
	Для Каждого ДанныеЗадания Из ТекущиеФоновыеЗадания Цикл
		
		ПараметрыЗадания = Неопределено;
		
		Если ПараметрыЗаданий <> Неопределено Тогда
			ПараметрыЗадания = ПараметрыЗаданий.Получить(ДанныеЗадания.ИмяЗадания);
		КонецЕсли; 
		
		Если ДанныеЗадания.ИмяЗадания = "СформироватьЭтапыПроизводства" Тогда
			
			РезультатЗапускаЗадания = ЗапуститьЗаданиеСформироватьЭтапыПроизводства(ПараметрыЗадания);
			
		ИначеЕсли ДанныеЗадания.ИмяЗадания = "ПометитьНаУдалениеЭтапыПроизводства" Тогда
			
			РезультатЗапускаЗадания = ЗапуститьЗаданиеПометитьНаУдалениеЭтапыПроизводства(ПараметрыЗадания);
			
		Иначе
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Неизвестное задание ""%1""'"),
								ДанныеЗадания.ИмяЗадания);
		КонецЕсли;
		
		ДанныеЗадания.ИдентификаторЗадания = РезультатЗапускаЗадания.ИдентификаторЗадания;
		ДанныеЗадания.АдресХранилища = РезультатЗапускаЗадания.АдресХранилища;
		
		Если НЕ РезультатЗапускаЗадания.ЗаданиеВыполнено Тогда
			ВсеЗаданияВыполнены = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ВсеЗаданияВыполнены Тогда
		ОбновитьДанныеНаСервере(Ложь);
	КонецЕсли;
	
	Возврат ВсеЗаданияВыполнены;

КонецФункции

&НаКлиенте
Процедура ОбработатьЗавершениеЗаданийНаКлиенте()

	Для Каждого ДанныеЗадания Из ТекущиеФоновыеЗадания Цикл
		
		Если ДанныеЗадания.ИмяЗадания = "СформироватьЭтапыПроизводства" Тогда
			ЗавершениеЗаданияСформироватьЭтапыПроизводства(ДанныеЗадания.АдресХранилища);
		ИначеЕсли ДанныеЗадания.ИмяЗадания = "ПометитьНаУдалениеЭтапыПроизводства" Тогда
			ЗавершениеЗаданияПометитьНаУдалениеЭтапыПроизводства(ДанныеЗадания.АдресХранилища);
		КонецЕсли;
		
	КонецЦикла;
	ОбновитьДанныеНаКлиенте(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуДлительнойОперации()

	ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
	
	// Уменьшим шаг увеличения времени опроса выполнения задания
	ПараметрыОбработчикаОжидания.КоэффициентУвеличенияИнтервала = 1.2;
	ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
	ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтаФорма, ИдентификаторЗадания);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗадания()
 
	Попытка
		
		Если ФормаДлительнойОперации.Открыта() 
			И ФормаДлительнойОперации.ИдентификаторЗадания = ИдентификаторЗадания Тогда
			
			ВсеЗаданияВыполнены = Истина;
			Для каждого ДанныеЗадания Из ТекущиеФоновыеЗадания Цикл
				Если НЕ ЗаданиеВыполнено(ДанныеЗадания.ИдентификаторЗадания) Тогда
					ВсеЗаданияВыполнены = Ложь;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если ВсеЗаданияВыполнены Тогда
				ОбработатьЗавершениеЗаданийНаКлиенте();
				ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
			Иначе
				ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
				ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания",
					ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
			КонецЕсли;
				
		КонецЕсли;
		
	Исключение
		
		ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаданиеВыполнено(ИдентификаторЗадания)
	
	Возврат ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания);
	
КонецФункции

#Область ВыполнениеЗаданий

&НаСервере
Функция ЗапуститьЗаданиеСформироватьЭтапыПроизводства(ПараметрыЗадания)
	
	НаименованиеЗадания = НСтр("ru = 'Формирование этапов производства'");
	
	РезультатЗапускаЗадания = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
									УникальныйИдентификатор,
									"Документы.ЭтапПроизводства2_2.ОбеспечитьПотребностиПроизводстваВПродукцииИПолуфабрикатах",
									ПараметрыЗадания,
									НаименованиеЗадания);
			
	Возврат РезультатЗапускаЗадания;
		
КонецФункции

&НаСервере
Функция ЗапуститьЗаданиеПометитьНаУдалениеЭтапыПроизводства(ПараметрыЗадания)
	
	НаименованиеЗадания = НСтр("ru = 'Пометка на удаление этапов производства'");
	
	РезультатЗапускаЗадания = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
									УникальныйИдентификатор,
									"Документы.ЭтапПроизводства2_2.ПометитьНаУдалениеЭтапыОтложенно",
									ПараметрыЗадания,
									НаименованиеЗадания);
			
	Возврат РезультатЗапускаЗадания;
		
КонецФункции

&НаКлиенте
Процедура ЗавершениеЗаданияСформироватьЭтапыПроизводства(АдресХранилища)
	
	Результат = ЗавершениеЗаданияНаСервере(АдресХранилища);
	УправлениеПроизводствомКлиент.ОповеститьПользователяОФормированииЭтаповПроизводства(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеЗаданияПометитьНаУдалениеЭтапыПроизводства(АдресХранилища)
	
	Результат = ЗавершениеЗаданияНаСервере(АдресХранилища);
	УправлениеПроизводствомКлиент.ОповеститьПользователяОПометкеНаУдалениеЭтаповПроизводства(Результат);
	
КонецПроцедуры

&НаСервере
Функция ЗавершениеЗаданияНаСервере(АдресХранилища)
	
	Результат = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	ОбновитьДанныеНаСервере(Ложь);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область Прочее

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	
	#Область СкрытьИнформационнуюСтроку
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтруктураЗаказовИнформационнаяСтрокаГиперссылка.Имя);
	
	ЗначенияОтбора = Новый СписокЗначений;
	ЗначенияОтбора.Добавить(ТипСтрокиПолуфабрикатДругогоЗаказа());
	ЗначенияОтбора.Добавить(ТипСтрокиСоздатьЭтапы());
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.ТипСтроки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке;
	ОтборЭлемента.ПравоеЗначение = ЗначенияОтбора;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	#КонецОбласти
	
	#Область СкрытьНазначениеПолуфабриката
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтруктураЗаказовНазначение.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.ТипСтроки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = ТипСтрокиПродукция();
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	#КонецОбласти

	#Область ПустоеКоличествоВСтрокаБезДанных
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтруктураЗаказовКоличествоПлан.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтруктураЗаказовКоличествоФакт.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтруктураЗаказовКоличествоДефицит.Имя);
	
	ЗначенияОтбора = Новый СписокЗначений;
	ЗначенияОтбора.Добавить(ТипСтрокиЗаказ());
	ЗначенияОтбора.Добавить(ТипСтрокиСоздатьЭтапы());
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.ТипСтроки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	ОтборЭлемента.ПравоеЗначение = ЗначенияОтбора;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", "");
	
	#КонецОбласти
	
	#Область СкрытьКартинкиВСтроках
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтруктураЗаказовНомерКартинки.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтруктураЗаказовНомерКартинкиПроцентВыполнено.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтруктураЗаказовНомерКартинкиПроцентВыполненоИтого.Имя);
	
	ЗначенияОтбора = Новый СписокЗначений;
	ЗначенияОтбора.Добавить(ТипСтрокиЗаказ());
	ЗначенияОтбора.Добавить(ТипСтрокиПродукция());
	ЗначенияОтбора.Добавить(ТипСтрокиСоздатьЭтапы());
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.ТипСтроки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	ОтборЭлемента.ПравоеЗначение = ЗначенияОтбора;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	
	#КонецОбласти
	
	#Область ИтогиПоКоличеству
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтруктураЗаказовКоличествоПланИтого.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтруктураЗаказовКоличествоФактИтого.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтруктураЗаказовКоличествоДефицитИтого.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтруктураЗаказовНомерКартинкиПроцентВыполненоИтого.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтруктураЗаказовПроцентВыполненоИтого.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтруктураЗаказовТекстОбщееКоличество.Имя);
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.КоличествоПланИтого");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.КоличествоПлан");
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.КоличествоФактИтого");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.КоличествоФакт");
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтруктураЗаказовКоличествоПланИтого.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтруктураЗаказовКоличествоФактИтого.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтруктураЗаказовКоличествоДефицитИтого.Имя);
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.КоличествоПланИтого");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.КоличествоПлан");
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.КоличествоФактИтого");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.КоличествоФакт");

	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(,, Истина));
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтруктураЗаказовТекстОбщееКоличество.Имя);

	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.КоличествоПланИтого");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.КоличествоПлан");
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.КоличествоФактИтого");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.КоличествоФакт");
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Партия выпуска'"));
	
	#КонецОбласти
	
	#Область СоздатьЭтапы
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтруктураЗаказовНомерКартинки.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтруктураЗаказовПредставлениеСтроки.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.ТипСтроки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = ТипСтрокиСоздатьЭтапы();
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтруктураЗаказовИнформационнаяСтрокаГиперссылка.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.ТипСтроки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = ТипСтрокиСоздатьЭтапы();
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Создать этапы'"));
	
	#КонецОбласти
	
	#Область ТекстЕдиницИзмеренияЭтапов
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтруктураЗаказовЕдиницаИзмерения.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.ТипСтроки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = ТипСтрокиЭтап();
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'единиц/партий'"));
	
	#КонецОбласти
	
	#Область ЦветФонаЗаказ
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтруктураЗаказов.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.ТипСтроки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = ТипСтрокиЗаказ();
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветаСтиля.ЦветФонаГруппировкиОтчета1);
	#КонецОбласти
	
	#Область ШрифтПродукция
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтруктураЗаказов.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.ТипСтроки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = ТипСтрокиПродукция();
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(,, Истина));
	
	#КонецОбласти
	
	#Область ВыделитьСветлосерым
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтруктураЗаказов.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.ТипСтроки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = ТипСтрокиЭтап();
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.ВыполненПроизведен");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.СерыйЦветТекста1);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтруктураЗаказов.Имя);
	
	ЗначенияОтбора = Новый СписокЗначений;
	ЗначенияОтбора.Добавить(ТипСтрокиПолуфабрикат());
	ЗначенияОтбора.Добавить(ТипСтрокиПолуфабрикатДругогоЗаказа());
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.ТипСтроки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	ОтборЭлемента.ПравоеЗначение = ЗначенияОтбора;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.ВыполненПроизведен");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.ВыполненПроизведенРодитель");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.СерыйЦветТекста1);
	
	#КонецОбласти
	
	#Область ВыделитьТемносерым
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтруктураЗаказов.Имя);
	
	ЗначенияОтбора = Новый СписокЗначений;
	ЗначенияОтбора.Добавить(ТипСтрокиПолуфабрикат());
	ЗначенияОтбора.Добавить(ТипСтрокиПолуфабрикатДругогоЗаказа());
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.ТипСтроки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	ОтборЭлемента.ПравоеЗначение = ЗначенияОтбора;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.ВыполненПроизведен");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.ВыполненПроизведенРодитель");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.СерыйЦветТекста2);
	
	#КонецОбласти
	
	#Область ВыделитьКрасным
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СтруктураЗаказовДатаОкончанияПлан.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.ВыполненПроизведен");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.ДатаОкончанияПлан");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СтруктураЗаказов.ДатаОкончанияПлан");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше;
	ОтборЭлемента.ПравоеЗначение = НачалоДня(ТекущаяДатаСеанса());
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветОсобогоТекста);
	
	#КонецОбласти
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовок()
	
	Если ЗначениеЗаполнено(Заказы) Тогда
		
		Если Заказы.Количество() = 1 Тогда
			
			ДанныеЗаказа = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Заказы[0].Значение, "Номер, Дата");
		
			НомерЗаказа = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(
				ДанныеЗаказа.Номер, Ложь, Истина);
			ДатаЗаказа = Формат(ДанныеЗаказа.Дата, "ДЛФ=D");
			
			Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Структура заказа на производство № %1 от %2'"),
				НомерЗаказа,
				ДатаЗаказа);
			
		Иначе
			
			Заголовок = НСтр("ru = 'Структура заказов на производство'");
			
		КонецЕсли;
		
	Иначе
		
		Заголовок = НСтр("ru = 'Структура заказа на производство (не выбран заказ)'");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СохранитьНастройкиФормы(Форма)
	
	Настройки = Новый Структура;
	Настройки.Вставить("СкрыватьВыполненное", Форма.СкрыватьВыполненное);
	Настройки.Вставить("ПоказыватьЭтапы", Форма.ПоказыватьЭтапы);
	Настройки.Вставить("ПоказыватьПроцентВыполнения", Форма.ПоказыватьПроцентВыполнения);
	
	СохранитьНастройкиФормыСервер(Настройки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьНастройкиФормыСервер(Настройки)
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		КлючФормы(),
		"",
		Настройки);
	
	КонецПроцедуры
	
&НаСервере
Процедура ЗагрузитьНастройкиФормы()
	
	Настройки = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(КлючФормы(), "");
	
	Если ТипЗнч(Настройки) = Тип("Структура") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Настройки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция КлючФормы()
	
	Возврат "Отчет.СтруктураЗаказаНаПроизводство2_2.СтруктураЗаказа";
	
КонецФункции

&НаСервере
Процедура НастроитьФормуПриСоздании()
	
	ГрафикИспользуется = УправлениеПроизводствомПовтИсп.ИспользуетсяГрафикПроизводства();
	
	Элементы.ДиаграммаГанта.Видимость = ГрафикИспользуется;
	Элементы.ДиагностикаЭтапа.Видимость = ГрафикИспользуется;
	
	Элементы.КонтекстноеМенюДиаграммаГанта.Видимость = ГрафикИспользуется;
	Элементы.КонтекстноеМенюДиагностикаЭтапа.Видимость = ГрафикИспользуется;
	
	УправлениеВидимостью(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеВидимостью(Форма)

	Форма.Элементы.ПоказыватьПроцентВыполнения.Пометка = Форма.ПоказыватьПроцентВыполнения;
	Форма.Элементы.СтруктураЗаказовКоличествоПроцентВыполнено.Видимость = Форма.ПоказыватьПроцентВыполнения;
	Форма.Элементы.СтруктураЗаказовКоличествоПроцентВыполненоИтого.Видимость = Форма.ПоказыватьПроцентВыполнения;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИнформационнуюПанель()
	
	Если НеПоказыватьКомандуТребуетсяСформироватьЭтапы Тогда
		
		ВидимостьКоманды = Ложь;
		
	Иначе
		
		МассивСсылок = Заказы.ВыгрузитьЗначения();
		ВидимостьКоманды = Документы.ЗаказНаПроизводство2_2.ТребуетсяСформироватьЭтапы(МассивСсылок);
		
	КонецЕсли;
	
	Элементы.ГруппаТребуетсяСформироватьЭтапы.Видимость = ВидимостьКоманды;

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДиаграммуГантаЗаказа(ТекущиеДанные)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПараметрКоманды", ТекущиеДанные.Заказ);
	
	ОткрытьФорму(
		"Отчет.ДиаграммаПроизводстваЗаказа.ФормаОбъекта",
		ПараметрыФормы,
		ЭтотОбъект,
		ТекущиеДанные.Заказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДиаграммуГантаПродукции(ТекущиеДанные)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Распоряжение", ТекущиеДанные.Заказ);
	ПараметрыФормы.Вставить("ВыпускающийЭтап", ТекущиеДанные.ВыпускающийЭтап);
	
	ОткрытьФорму(
		"Отчет.ДиаграммаПроизводстваПартииЗапуска.ФормаОбъекта",
		ПараметрыФормы,
		ЭтотОбъект,
		ТекущиеДанные.ВыпускающийЭтап);
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДиаграммуГантаЭтапа(ТекущиеДанные)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПараметрКоманды", ТекущиеДанные.Этап);
	
	ОткрытьФорму(
		"Отчет.ДиаграммаСмежныхЭтаповПроизводства.ФормаОбъекта",
		ПараметрыФормы,
		ЭтотОбъект,
		ТекущиеДанные.Этап);
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСтрокуЗаказаНаПроизводства(ТекущиеДанные)
	
	ДанныеСтроки = УправлениеПроизводствомКлиентСервер.СтруктураПродукцииЗаказа();
	ЗаполнитьЗначенияСвойств(ДанныеСтроки, ТекущиеДанные);
	ДанныеСтроки.Склад         = ТекущиеДанные.Получатель;
	ДанныеСтроки.Подразделение = ТекущиеДанные.Получатель;
	
	УправлениеПроизводствомКлиент.ОткрытьСтрокуЗаказаНаПроизводства(ТекущиеДанные.Заказ, ДанныеСтроки, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуСтруктурыПолуфабрикатаДругогоЗаказа(ТекущиеДанные)
	
	ПараметрЗаказы = Новый Массив;
	ПараметрЗаказы.Добавить(ТекущиеДанные.Заказ);
	
	ПараметрыФормы = Новый Структура("Заказы", ПараметрЗаказы);
	
	ОткрытьФорму("Отчет.СтруктураЗаказаНаПроизводство2_2.Форма.СтруктураЗаказа",
		ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор);

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуСозданияЭтапов(ТекущиеДанные)
	
	СтрокаПотребности = ТекущиеДанные.ПолучитьРодителя();
	
	Потребность = УправлениеПроизводствомКлиентСервер.СтруктураПотребностиПроизводства();
	ЗаполнитьЗначенияСвойств(Потребность, СтрокаПотребности,, "Этап, Спецификация");
	
	Потребность.Распоряжение = СтрокаПотребности.Заказ;
	Потребность.Количество   = СтрокаПотребности.КоличествоПлан;
	
	УправлениеПроизводствомКлиент.ОткрытьПоследовательностьЭтапов(
		СтрокаПотребности.Заказ,
		,
		Потребность,
		ЭтотОбъект);

КонецПроцедуры

#КонецОбласти

#КонецОбласти
