#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - УправляемаяФорма - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - Структура - см. возвращаемое значение ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию().
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПриСозданииНаСервере = Истина;
	Настройки.События.ПриЗагрузкеПользовательскихНастроекНаСервере = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   Форма - УправляемаяФорма - Форма отчета.
//   Отказ - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Передается из параметров обработчика "как есть".
//
// См. также:
//   "УправляемаяФорма.ПриСозданииНаСервере" в синтакс-помощнике.
//
Процедура ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	КомпоновщикНастроекФормы = ЭтаФорма.Отчет.КомпоновщикНастроек;
	Параметры = ЭтаФорма.Параметры;
	
	Если Параметры.Свойство("ПараметрКоманды") Тогда
		
		Если Не ЗначениеЗаполнено(Параметры.ПараметрКоманды) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Распределение материала не выполнено или не требуется.'"),,,,Отказ);
		КонецЕсли;
		
		ЭтаФорма.ФормаПараметры.Отбор.Вставить("Регистратор", Параметры.ПараметрКоманды);
		
	Иначе
		
		Отказ = Истина;
		ВызватьИсключение НСтр("ru= 'Отчет предназначен только для открытия в документе ""Распределение материалов и работ"".'") ;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	
	ПараметрРегистратор = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкиОтчета, "Регистратор");
	Регистратор         = ПараметрРегистратор.Значение;
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Регистратор, "Дата, Организация, БазаРаспределения, Номенклатура, Характеристика, Склад, Подразделение");
	
	НачалоПериода     = НачалоМесяца(Реквизиты.Дата);
	ОкончаниеПериода  = КонецМесяца(Реквизиты.Дата);
	МассивОрганизаций = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Реквизиты.Организация);
	
	// Стоимость
	МассивТиповРаспределенияБазыПоСтоимости = Новый Массив();
	МассивТиповРаспределенияБазыПоСтоимости.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.СуммаМатериальныхЗатрат);
	МассивТиповРаспределенияБазыПоСтоимости.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.СуммаРасходовНаОплатуТруда);
	МассивТиповРаспределенияБазыПоСтоимости.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.НормативыОплатыТруда);
	МассивТиповРаспределенияБазыПоСтоимости.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукции);
	
	// Количество
	МассивТиповРаспределенияБазыПоКоличеству = Новый Массив();
	МассивТиповРаспределенияБазыПоКоличеству.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.КоличествоУказанныхМатериалов);
	МассивТиповРаспределенияБазыПоКоличеству.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.КоличествоРаботУказанныхВидов);
	МассивТиповРаспределенияБазыПоКоличеству.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.КоличествоПродукции);
	
	// Объем
	МассивТиповРаспределенияБазыПоОбъему = Новый Массив();
	МассивТиповРаспределенияБазыПоОбъему.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.ОбъемУказанныхМатериалов);
	МассивТиповРаспределенияБазыПоОбъему.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.ОбъемПродукции);
	
	// Вес
	МассивТиповРаспределенияБазыПоВесу = Новый Массив();
	МассивТиповРаспределенияБазыПоВесу.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.ВесУказанныхМатериалов);
	МассивТиповРаспределенияБазыПоВесу.Добавить(Перечисления.ТипыБазыРаспределенияРасходов.ВесПродукции);
	
	МВТ = ПартионныйУчет22.ДанныеДляРасшифровкиРаспределенияНоменклатурыНаПроизводство(НачалоПериода, МассивОрганизаций, Регистратор);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТ;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПартииПроизводства.ПартияПроизводства
	|ПОМЕСТИТЬ ЭтапыНеВошедшиеВРаспределение
	|ИЗ
	|	ПартииПроизводства КАК ПартииПроизводства,
	|		ЛЕВОЕ СОЕДИНЕНИЕ БазаРаспределенияДетально КАК БазаРаспределенияДетально
	|			ПО ПартииПроизводства.ПартияПроизводства = БазаРаспределенияДетально.ПартияПроизводства
	|ГДЕ
	|	БазаРаспределенияДетально.ПартияПроизводства ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""ПоПравилу""                КАК ТипЗаписи,
	|	ДД.Регистратор               КАК Регистратор,
	|	ДД.ПартияПроизводства        КАК ПартияПроизводства,
	|	ДД.Знаменатель               КАК ЗначениеПоказателя,
	|	0 КАК                        Количество,
	|	ЕСТЬNULL(РасчетныеПартии.Количество,0) КАК КоличествоРаспределено,
	|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
	|	ВЫБОР
	|		КОГДА ДД.ВидПоказателя = ""Материал""
	|			ТОГДА ДД.Материал
	|		КОГДА ДД.ВидПоказателя = ""Продукция""
	|			ТОГДА ДД.Продукция
	|		КОГДА ДД.ВидПоказателя = ""ВидРабот""
	|			ТОГДА ДД.ВидРабот
	|	КОНЕЦ КАК МатериалПродукцияВидРабот,
	|	ДД.ГруппаПродукции КАК ГруппаПродукции,
	|	1 КАК ПорядокВывода,
	|	ГруппыПродукции.Ссылка ЕСТЬ NULL КАК ОтборПоГруппамПродукции,
	|	ДокументРаспределения.ПравилоРаспределения КАК ПравилоРаспределения,
	|	ДокументРаспределения.БазаРаспределения КАК БазаРаспределения
	|ПОМЕСТИТЬ ДанныеДляРасшифровки
	|ИЗ
	|	БазаРаспределенияДетально КАК ДД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат КАК ДокументРаспределения
	|			ПО ДД.Регистратор = ДокументРаспределения.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат.ОтборПоГруппамПродукции КАК ГруппыПродукции
	|			ПО ДД.Регистратор = ГруппыПродукции.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПроизводственныеЗатратыСебестоимостьТоваров КАК РасчетныеПартии
	|			ПО ДД.ПартияПроизводства = РасчетныеПартии.КорПартия
	|			И РасчетныеПартии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|ГДЕ
	|	ДД.Регистратор = &Регистратор
	|	И ДокументРаспределения.КоличествоКРаспределениюПоПравилу > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""ПоЭтапамВручную"",
	|	ДД.Ссылка,
	|	ДД.ПартияПроизводства,
	|	ДД.Количество,
	|	ДД.Количество,
	|	ДД.Количество,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	1,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО
	|ИЗ
	|	Документ.РаспределениеПроизводственныхЗатрат.ПартииПроизводства КАК ДД
	|ГДЕ
	|	ДД.Ссылка = &Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""НаСтатьи"",
	|	ДД.Ссылка,
	|	НЕОПРЕДЕЛЕНО,
	|	ДД.Количество,
	|	ДД.Количество,
	|	ДД.Количество,
	|	ДД.СтатьяРасходов,
	|	ДД.АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	1,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО
	|ИЗ
	|	Документ.РаспределениеПроизводственныхЗатрат.ПрочиеРасходы КАК ДД
	|ГДЕ
	|	ДД.Ссылка = &Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""ПоПравилу"",
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Документ.ЭтапПроизводства2_2.ПустаяСсылка),
	|	0,
	|	0,
	|	0,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ДД.ПартияПроизводства,
	|	НЕОПРЕДЕЛЕНО,
	|	3,
	|	ЛОЖЬ,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО
	|ИЗ
	|	ЭтапыНеВошедшиеВРаспределение КАК ДД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДругиеМатериалыЭтапа.ПартияПроизводства КАК ПартияПроизводства,
	|	ДругиеМатериалыЭтапа.ГруппаПродукции КАК ГруппаПродукции,
	|	2 КАК ПорядокВывода,
	|	СУММА(ДругиеМатериалыЭтапа.Количество) КАК Количество,
	|	СУММА(ДругиеМатериалыЭтапа.Объем) КАК Объем,
	|	СУММА(ДругиеМатериалыЭтапа.Вес) КАК Вес,
	|	СУММА(ДругиеМатериалыЭтапа.Стоимость) КАК Стоимость,
	|	0 КАК НормативнаяСтоимость
	|ПОМЕСТИТЬ ДанныеНеВошедшиеВБазуРаспределения
	|ИЗ
	|	ДанныеПоМатериальнымЗатратам КАК ДругиеМатериалыЭтапа
	|		ЛЕВОЕ СОЕДИНЕНИЕ БазаРаспределенияДетально КАК БазаРаспределенияЭтапа
	|		ПО ДругиеМатериалыЭтапа.ПартияПроизводства = БазаРаспределенияЭтапа.ПартияПроизводства
	|		И ДругиеМатериалыЭтапа.Материал = БазаРаспределенияЭтапа.Материал
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ БазаРаспределенияДетально КАК ОтборПоПартиям
	|		ПО ДругиеМатериалыЭтапа.ПартияПроизводства = ОтборПоПартиям.ПартияПроизводства
	|
	|ГДЕ
	|	БазаРаспределенияЭтапа.Материал ЕСТЬ NULL
	|	И ОтборПоПартиям.ВидПоказателя = ""Материал""
	|
	|СГРУППИРОВАТЬ ПО
	|	ДругиеМатериалыЭтапа.ПартияПроизводства,
	|	ДругиеМатериалыЭтапа.ГруппаПродукции
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДругаяПродукцияЭтапа.ПартияПроизводства,
	|	ДругаяПродукцияЭтапа.ГруппаПродукции,
	|	2,
	|	СУММА(ДругаяПродукцияЭтапа.Количество),
	|	СУММА(ДругаяПродукцияЭтапа.Объем),
	|	СУММА(ДругаяПродукцияЭтапа.Вес),
	|	СУММА(ДругаяПродукцияЭтапа.Стоимость),
	|	0
	|ИЗ
	|	ДанныеПоПродукции КАК ДругаяПродукцияЭтапа
	|		ЛЕВОЕ СОЕДИНЕНИЕ БазаРаспределенияДетально КАК БазаРаспределенияЭтапа
	|		ПО ДругаяПродукцияЭтапа.ПартияПроизводства = БазаРаспределенияЭтапа.ПартияПроизводства
	|		И ДругаяПродукцияЭтапа.Продукция = БазаРаспределенияЭтапа.Продукция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ БазаРаспределенияДетально КАК ОтборПоПартиям
	|		ПО ДругаяПродукцияЭтапа.ПартияПроизводства = ОтборПоПартиям.ПартияПроизводства
	|
	|ГДЕ
	|	БазаРаспределенияЭтапа.Продукция ЕСТЬ NULL
	|	И ОтборПоПартиям.ВидПоказателя = ""Продукция""
	|
	|СГРУППИРОВАТЬ ПО
	|	ДругаяПродукцияЭтапа.ПартияПроизводства,
	|	ДругаяПродукцияЭтапа.ГруппаПродукции
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДругиеВидыРаботЭтапа.ПартияПроизводства,
	|	ДругиеВидыРаботЭтапа.ГруппаПродукции,
	|	2,
	|	СУММА(ДругиеВидыРаботЭтапа.Количество),
	|	0,
	|	0,
	|	СУММА(ДругиеВидыРаботЭтапа.Стоимость),
	|	СУММА(ДругиеВидыРаботЭтапа.НормативнаяСтоимость)
	|ИЗ
	|	ДанныеПоТрудоЗатратам КАК ДругиеВидыРаботЭтапа
	|		ЛЕВОЕ СОЕДИНЕНИЕ БазаРаспределенияДетально КАК БазаРаспределенияЭтапа
	|		ПО ДругиеВидыРаботЭтапа.ПартияПроизводства = БазаРаспределенияЭтапа.ПартияПроизводства
	|		И ДругиеВидыРаботЭтапа.ВидРабот = БазаРаспределенияЭтапа.ВидРабот
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ БазаРаспределенияДетально КАК ОтборПоПартиям
	|		ПО ДругиеВидыРаботЭтапа.ПартияПроизводства = ОтборПоПартиям.ПартияПроизводства
	|
	|ГДЕ
	|	БазаРаспределенияЭтапа.ВидРабот ЕСТЬ NULL
	|	И ОтборПоПартиям.ВидПоказателя = ""ВидРабот""
	|
	|СГРУППИРОВАТЬ ПО
	|	ДругиеВидыРаботЭтапа.ПартияПроизводства,
	|	ДругиеВидыРаботЭтапа.ГруппаПродукции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(РасчетУдельногоВеса.ЗначениеПоказателя) КАК ЗначениеПоказателя,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ РасчетУдельногоВеса.МатериалПродукцияВидРабот) КАК КоличествоМатериалов
	|ПОМЕСТИТЬ ДанныеДляРасчетаУдельногоВеса
	|ИЗ
	|	ДанныеДляРасшифровки КАК РасчетУдельногоВеса
	|ГДЕ
	|	РасчетУдельногоВеса.ТипЗаписи = ""ПоПравилу""
	|	И РасчетУдельногоВеса.ПорядокВывода = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДД.ПартияПроизводства КАК ПартияПроизводства,
	|	ДД.ГруппаПродукции КАК ГруппаПродукции,
	|	ДД.ТипЗаписи КАК ТипЗаписи,
	|	ДД.ЗначениеПоказателя КАК ЗначениеПоказателя,
	|	ДД.ЗначениеПоказателя * 100 / ВЫБОР
	|		КОГДА ДанныеУдельногоВеса.ЗначениеПоказателя = 0
	|			ТОГДА 1
	|		ИНАЧЕ ДанныеУдельногоВеса.ЗначениеПоказателя
	|	КОНЕЦ КАК УдельныйВес,
	|	ДД.Количество КАК Количество,
	|	ДД.ПорядокВывода КАК ПорядокВывода,
	|	ДД.Регистратор КАК Регистратор,
	|	ВЫБОР
	|		КОГДА ДД.БазаРаспределения В (&МассивКоличества)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК СуммироватьКоличествоПоЭтапу,
	|	ДД.ПравилоРаспределения КАК ПравилоРаспределения,
	|	ДД.МатериалПродукцияВидРабот КАК МатериалПродукцияВидРабот,
	|	ДД.КоличествоРаспределено КАК КоличествоРаспределено,
	|	ДД.СтатьяРасходов КАК СтатьяРасходов,
	|	ДД.АналитикаРасходов КАК АналитикаРасходов,
	|	ДД.БазаРаспределения КАК БазаРаспределения
	|ИЗ
	|	ДанныеДляРасшифровки КАК ДД,
	|	ДанныеДляРасчетаУдельногоВеса КАК ДанныеУдельногоВеса
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеНеВошедшиеВБазуРаспределения.ПартияПроизводства,
	|	ДанныеНеВошедшиеВБазуРаспределения.ГруппаПродукции,
	|	""ПоПравилу"",
	//значение показателя не отображается
	|	0,
	|	0,
	|	ДанныеНеВошедшиеВБазуРаспределения.Количество,
	|	ДанныеНеВошедшиеВБазуРаспределения.ПорядокВывода,
	|	РаспределениеПроизводственныхЗатрат.Ссылка,
	|	ВЫБОР
	|		КОГДА РаспределениеПроизводственныхЗатрат.БазаРаспределения В (&МассивКоличества)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ЕСТЬNULL(РаспределениеПроизводственныхЗатрат.БазаРаспределения, ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПустаяСсылка))
	|ИЗ
	|	ДанныеНеВошедшиеВБазуРаспределения КАК ДанныеНеВошедшиеВБазуРаспределения
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат КАК РаспределениеПроизводственныхЗатрат
	|		ПО РаспределениеПроизводственныхЗатрат.Ссылка = &Регистратор
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЭтапыНеВошедшиеВРаспределение КАК ЭтапыНеВошедшиеВРаспределение
	|		ПО ДанныеНеВошедшиеВБазуРаспределения.ПартияПроизводства = ЭтапыНеВошедшиеВРаспределение.ПартияПроизводства
	|ГДЕ
	|	ЭтапыНеВошедшиеВРаспределение.ПартияПроизводства ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	БазаРаспределения УБЫВ";
	
	Запрос.УстановитьПараметр("Регистратор",       Регистратор);
	Запрос.УстановитьПараметр("МассивКоличества",  МассивТиповРаспределенияБазыПоКоличеству);
	Запрос.УстановитьПараметр("МассивСтоимости",   МассивТиповРаспределенияБазыПоСтоимости);
	Запрос.УстановитьПараметр("МассивОбъема",      МассивТиповРаспределенияБазыПоОбъему);
	Запрос.УстановитьПараметр("МассивВеса",        МассивТиповРаспределенияБазыПоВесу);
	
	РезультатЗапроса = Запрос.Выполнить();
	Распределение    = РезультатЗапроса.Выгрузить();
	
	ВнешниеНаборыДанных = Новый Структура("Распределение", Распределение);
	
	КомпоновщикМакета   = Новый КомпоновщикМакетаКомпоновкиДанных;
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	МакетКомпоновки     = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	// Пост обработка отчета.
	// Устанавливаем отражение правила распределения, меняем заголовки колонок и т.д.
	
	ТабличныйДокумент = ПроцессорВывода.ЗакончитьВывод();
	
	Область = ТабличныйДокумент.НайтиТекст("%ЗаголовокОтчета%");
	Если Область <> Неопределено Тогда
		Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Реквизиты.Номенклатура, "ТипНоменклатуры") = Перечисления.ТипыНоменклатуры.Работа Тогда
			ЗаголовокОтчета = НСтр("ru = 'Распределение работы ""%1"" в подразделении ""%2"" за %3'");
			ЗаголовокОтчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ЗаголовокОтчета,
				НоменклатураКлиентСервер.ПредставлениеНоменклатуры(Реквизиты.Номенклатура, Реквизиты.Характеристика),
				Реквизиты.Подразделение,
				Формат(Реквизиты.Дата, "ДФ='ММММ гггг'"));
		Иначе
			ЗаголовокОтчета = НСтр("ru = 'Распределение материала ""%1"" в кладовой ""%2"" за %3'");
			ЗаголовокОтчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ЗаголовокОтчета,
				НоменклатураКлиентСервер.ПредставлениеНоменклатуры(Реквизиты.Номенклатура, Реквизиты.Характеристика),
				Реквизиты.Склад,
				Формат(Реквизиты.Дата, "ДФ='ММММ гггг'"));
		КонецЕсли;
		Область.Текст = СтрЗаменить(Область.Текст, "%ЗаголовокОтчета%", ЗаголовокОтчета);
	КонецЕсли;
	
	ПредставлениеПравила    = НСтр("ru = 'Прочее правило распределения %1'");
	ПредставлениеПоказателя = НСтр("ru = 'Значение показателя'");
	ОтборПоГруппамПродукции = Регистратор.ОтборПоГруппамПродукции.Количество() > 0;
	
	Если Реквизиты.БазаРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.СуммаМатериальныхЗатрат Тогда 
		
		ПредставлениеПозиции    = "";
		ПредставлениеПоказателя = НСтр("ru = 'Стоимость'");
		ПредставлениеПравила    = НСтр("ru = 'Пропорционально стоимости материальных затрат %1'");
		
	ИначеЕсли Реквизиты.БазаРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.КоличествоУказанныхМатериалов Тогда
		
		ПредставлениеПозиции    = НСтр("ru = 'Материалы'");
		ПредставлениеПоказателя = НСтр("ru = 'Количество'");
		ПредставлениеПравила    = НСтр("ru = 'Пропорционально количеству указанных материалов %1'");
		
	ИначеЕсли Реквизиты.БазаРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.ОбъемУказанныхМатериалов Тогда
		
		ПредставлениеПозиции    = НСтр("ru = 'Материалы'");
		ПредставлениеПоказателя = НСтр("ru = 'Объем'");
		ПредставлениеПравила    = НСтр("ru = 'Пропорционально объему указанных материалов %1'");
		
	ИначеЕсли Реквизиты.БазаРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.ВесУказанныхМатериалов Тогда
		
		ПредставлениеПозиции    = НСтр("ru = 'Материалы'");
		ПредставлениеПоказателя = НСтр("ru = 'Вес'");
		ПредставлениеПравила    = НСтр("ru = 'Пропорционально весу указанных материалов %1'");
		
	ИначеЕсли Реквизиты.БазаРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.СуммаРасходовНаОплатуТруда Тогда
		
		ПредставлениеПозиции    = "";
		ПредставлениеПоказателя = НСтр("ru = 'Стоимость'");
		ПредставлениеПравила    = НСтр("ru = 'Пропорционально сумме расходов на оплату труда %1'");
		
	ИначеЕсли Реквизиты.БазаРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.НормативыОплатыТруда Тогда
		
		ПредставлениеПозиции    = "";
		ПредставлениеПоказателя = НСтр("ru = 'Стоимость'");
		ПредставлениеПравила    = НСтр("ru = 'Пропорционально нормативной сумме расходов на оплату труда %1'");
		
	ИначеЕсли Реквизиты.БазаРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.КоличествоРаботУказанныхВидов Тогда
		
		ПредставлениеПозиции    = НСтр("ru = 'Виды работ'");
		ПредставлениеПоказателя = НСтр("ru = 'Количество'");
		ПредставлениеПравила    = НСтр("ru = 'Пропорционально количеству указанных видов работ %1'");
		
	ИначеЕсли Реквизиты.БазаРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукции Тогда
		
		ПредставлениеПозиции    = НСтр("ru = 'Продукция'");
		ПредставлениеПоказателя = НСтр("ru = 'Стоимость'");
		ПредставлениеПравила    = НСтр("ru = 'Пропорционально плановой стоимости продукции %1'");
		
	ИначеЕсли Реквизиты.БазаРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.КоличествоПродукции Тогда
		
		ПредставлениеПозиции    = НСтр("ru = 'Продукция'");
		ПредставлениеПоказателя = НСтр("ru = 'Количество'");
		ПредставлениеПравила    = НСтр("ru = 'Пропорционально количеству продукции %1'");
		
	ИначеЕсли Реквизиты.БазаРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.ОбъемПродукции Тогда
		
		ПредставлениеПозиции    = НСтр("ru = 'Продукция'");
		ПредставлениеПоказателя = НСтр("ru = 'Объем'");
		ПредставлениеПравила    = НСтр("ru = 'Пропорционально объему продукции %1'");
		
	ИначеЕсли Реквизиты.БазаРаспределения = Перечисления.ТипыБазыРаспределенияРасходов.ВесПродукции Тогда
		
		ПредставлениеПозиции    = НСтр("ru = 'Продукция'");
		ПредставлениеПоказателя = НСтр("ru = 'Вес'");
		ПредставлениеПравила    = НСтр("ru = 'Пропорционально весу продукции %1'");
		
	КонецЕсли;
	
	Если ОтборПоГруппамПродукции Тогда
		ПредставлениеПравила = СтрШаблон(ПредставлениеПравила, НСтр("ru = 'на выбранные группы продукции'"));
	Иначе
		ПредставлениеПравила = СтрШаблон(ПредставлениеПравила, "");
	КонецЕсли;
	
	Область = ТабличныйДокумент.НайтиТекст("%ПредставлениеПравилаРаспределения%");
	Если Область <> Неопределено Тогда
		Область.Текст = СтрЗаменить(Область.Текст, "%ПредставлениеПравилаРаспределения%", ПредставлениеПравила);
	КонецЕсли;
	
	Область = ТабличныйДокумент.НайтиТекст("%ПредставлениеПозиции%");
	Если Область <> Неопределено Тогда
		Область.Текст = ПредставлениеПозиции;
	КонецЕсли;
	
	Область = ТабличныйДокумент.НайтиТекст("%ИмяПоказателя%");
	Если ТипЗнч(Область) <> Тип("Неопределено") Тогда
		Область.Текст = ПредставлениеПоказателя;
	КонецЕсли;
	
	Область = ТабличныйДокумент.НайтиТекст("%ПредставлениеПозицииВСтроке%");
	Пока Область <> Неопределено Цикл
		
		Если ПредставлениеПозиции = НСтр("ru = 'Продукция'") Тогда
			ПредставлениеПозицииВСтроке = НСтр("ru = '<другая продукция>'");
		Иначе
			ПредставлениеПозицииВСтроке = СтрШаблон(НСтр("ru = '<другие %1>'"), НРег(ПредставлениеПозиции));
		КонецЕсли;
		
		Область.Текст = СтрЗаменить(Область.Текст, "%ПредставлениеПозицииВСтроке%", ПредставлениеПозицииВСтроке);;
		Область = ТабличныйДокумент.НайтиТекст("%ПредставлениеПозицииВСтроке%");
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти

#КонецЕсли

