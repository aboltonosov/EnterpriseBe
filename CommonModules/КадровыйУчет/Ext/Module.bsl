////////////////////////////////////////////////////////////////////////////////
// КадровыйУчет: методы кадрового учета, работающие на стороне сервера.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ПолучениеКадровыхДанных

// Возвращает таблицу значений заполненную данными физических лиц, перечисленными в 
// параметре СписокНеобходимыхДанных.
//
// Параметры:
//	ТолькоРазрешенные - Булево
//  СписокФизическихЛиц - Массив ссылок на элементы справочника физические лица или
//						  СправочникСсылка.ФизическиеЛица.
//  КадровыеДанные 	- 	Строка - список полей данных, перечисленных через запятую.
//					-	Массив строк с полями данных,
//						  которые необходимо получить. Ниже приведены идентификаторы 
//						  получаемых данных и описание значения, которое будет возвращено в 
//						  таблице значений.
//
//	ДатаПолученияДанных	- дата на которую необходимо получить данные физических лиц, 
//						  применимо к данным, носящим периодический характер (например - ФИО).
//						  Если дату не указывать, будут получены самые последние данные.
//
//  ПоляОтбораПериодическихДанных - Структура, в качестве ключа указывается имя таблицы, содержащей
//						  периодические данные (например ФИОФизическихЛиц, ГражданствоФизическихЛиц).
//						  В качестве значений коллекция условий отбора, 
//						  соединяемых по "И" и применяемых к регистру сведений.
//						  Коллекция строки которой имеют поля.
//							 * ЛевоеЗначение - строка, имя поля регистра сведений.
//							 * ВидСравнения - строка, вид сравнения, допустимый в языке запросов.
//							 * ПравоеЗначение - значение для сравнения с полем ЛевоеЗначение.
//
//	Список полей, допустимых в параметре КадровыеДанные см. КадровыйУчетВнутренний.ЗапросВТКадровыеДанныеФизическихЛиц.
//
// Возвращаемое значение:
//   ТаблицаЗначений   - Таблица значений, содержащая обязательные поля ФизическоеЛицо, Период поля запрошенных
//						 кадровых данных.
//
Функция КадровыеДанныеФизическихЛиц(ТолькоРазрешенные, СписокФизическихЛиц, КадровыеДанные, ДатаПолученияДанных = '00010101', ПоляОтбораПериодическихДанных = Неопределено) Экспорт
	
	// Подготовим Запрос и Менеджер временных таблиц.
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	// Сформируем временную таблицу ВТКадровыеДанныеФизическихЛиц.
	СоздатьНаДатуВТКадровыеДанныеФизическихЛиц(Запрос.МенеджерВременныхТаблиц, ТолькоРазрешенные, СписокФизическихЛиц, КадровыеДанные, ДатаПолученияДанных, ПоляОтбораПериодическихДанных);
	
	// Выберем кадровые данные физлиц из временной таблицы.
	Запрос.Текст = "ВЫБРАТЬ КадровыеДанныеФизическихЛиц.* ИЗ ВТКадровыеДанныеФизическихЛиц КАК КадровыеДанныеФизическихЛиц";
	КадровыеДанныеФизическихЛиц = Запрос.Выполнить().Выгрузить();
	
	Возврат КадровыеДанныеФизическихЛиц;
	
КонецФункции

// Возвращает таблицу значений заполненную данными сотрудников, перечисленными в 
// параметре СписокНеобходимыхДанных.
//
// Параметры:
//	ТолькоРазрешенные - Булево
//  СписокСотрудников - Массив ссылок на элементы справочника сотрудники или
//						  СправочникСсылка.Сотрудники.
//  КадровыеДанные - 	Строка - список полей данных, перечисленных через запятую 
//						или Массив строк с полями данных,
//						которые необходимо получить. Ниже приведены идентификаторы 
//						получаемых данных и описание значения, которое будет возвращено в 
//						таблице значений
//						которые необходимо получить. Ниже приведены идентификаторы 
//						получаемых данных и описание значения, которое будет возвращено в 
//						таблице значений.
//
//	ДатаПолученияДанных	- дата на которую необходимо получить данные сотрудников, 
//						применимо к данным, носящим периодический характер.
//						Если дату не указывать, будут получены самые последние данные.
//
//
//  ПоляОтбораПериодическихДанных - Структура, в качестве ключа указывается имя таблицы, содержащей
//									периодические данные (например ФИОФизическихЛиц, ГражданствоФизическихЛиц).
//									В качестве значений коллекция условий отбора, 
//									соединяемых по "И" и применяемых к регистру сведений.
//									Коллекция строки которой имеют поля.
//										* ЛевоеЗначение - строка, имя поля регистра сведений.
//										* ВидСравнения - строка, вид сравнения, допустимый в языке запросов.
//										* ПравоеЗначение - значение для сравнения с полем ЛевоеЗначение.
//
//	Список полей, допустимых в параметре КадровыеДанные см. КадровыйУчетВнутренний.ЗапросВТКадровыеДанныеСотрудников.
//
// Возвращаемое значение:
//   ТаблицаЗначений   - Таблица значений, содержащая запрошенные данные.
//
Функция КадровыеДанныеСотрудников(ТолькоРазрешенные, СписокСотрудников, КадровыеДанные, ДатаПолученияДанных = '00010101', ПоляОтбораПериодическихДанных = Неопределено, ФормироватьСПериодичностьДень = Истина) Экспорт
	
	// Подготовим Запрос и Менеджер временных таблиц.
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	// Сформируем временную таблицу ВТКадровыеДанныеФизическихЛиц.
	СоздатьНаДатуВТКадровыеДанныеСотрудников(Запрос.МенеджерВременныхТаблиц, ТолькоРазрешенные, СписокСотрудников, КадровыеДанные, ДатаПолученияДанных, ПоляОтбораПериодическихДанных, ФормироватьСПериодичностьДень);
	
	// Выберем кадровые данные физлиц из временной таблицы.
	Запрос.Текст = "ВЫБРАТЬ КадровыеДанныеСотрудников.* ИЗ ВТКадровыеДанныеСотрудников КАК КадровыеДанныеСотрудников";
	КадровыеДанныеСотрудников = Запрос.Выполнить().Выгрузить();
	
	Возврат КадровыеДанныеСотрудников;
	
КонецФункции

// Возвращает таблицу значений заполненную физическими лицами, работавшими в организации в
// заданном периоде.
//
// Параметры:
//	ТолькоРазрешенные - Булево
//  Организация - СправочникСсылка.Организации - организация по которой необходимо отобрать работавших
//				  в ней физических лиц.
//	НачалоПериода 		- Дата
//	ОкончаниеПериода 	- Дата
//  Подразделение - СправочникСсылка.ПодразделенияОрганизаций - подразделение, по которому необходимо отобрать
//					работавших физических лиц. Если подразделение не указывать - будут отобраны физические
//					лица по всей организации.
//
// Возвращаемое значение:
//   ТаблицаЗначений   - Таблица значений, содержащая поля Период (Дата, соответствует указанному параметру
//                       ОкончаниеПериода) и ФизическоеЛицо.
//
Функция ФизическиеЛицаРаботавшиеВОрганизации(ТолькоРазрешенные, Организация, НачалоПериода, ОкончаниеПериода, Подразделение = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	// Сформируем временную таблицу ВТФизическиеЛицаРаботавшиеВОрганизации.
	СоздатьВТФизическиеЛицаРаботавшиеВОрганизации(Запрос.МенеджерВременныхТаблиц, ТолькоРазрешенные, Организация, НачалоПериода, ОкончаниеПериода, Подразделение);
	
	Запрос.Текст = 
	"ВЫБРАТЬ ФизическиеЛицаРаботавшиеВОрганизации.* ИЗ ВТФизическиеЛицаРаботавшиеВОрганизации КАК ФизическиеЛицаРаботавшиеВОрганизации";
	
	ФизическиеЛицаРаботавшиеВОрганизации = Запрос.Выполнить().Выгрузить();
	
	Возврат ФизическиеЛицаРаботавшиеВОрганизации;
	
КонецФункции

// Возвращает таблицу значений заполненную данными документов физических лиц.
//
// Параметры:
//  СписокФизическихЛиц - Массив ссылок на элементы справочника физические лица или
//						  СправочникСсылка.ФизическиеЛица.
//	ТолькоРазрешенные - Булево
//  ВидДокумента		- СправочникСсылка.ВидыДокументовФизическихЛиц, вид необходимого 
//						  документа.
//	ДатаПолученияДанных	- дата на которую необходимо получить данные документов физических лиц, 
//						если дату не указывать, данные будут получены на текущую дату.
//
// Возвращаемое значение:
//   ТаблицаЗначений   - Таблица значений, содержащая поля.
//						Период - Дата
//						ФизическоеЛицо - СправочникСсылка.ФизическиеЛица
//						ВидДокумента - СправочникСсылка.ВидыДокументовФизическихЛиц
//						Серия - Строка
//						Номер - Строка 
//						ДатаВыдачи - Дата
//						КемВыдан Строка
//						КодПодразделения - Строка
//						ЯвляетсяДокументомУдостоверяющимЛичность - Булево
//						Представление - Строка
//
Функция ДокументыФизическихЛиц(СписокФизическихЛиц, ТолькоРазрешенные, ВидДокумента, ДатаПолученияДанных = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ДатаПолученияДанных", ?(ДатаПолученияДанных = Неопределено, ТекущаяДатаСеанса(),ДатаПолученияДанных));
	Запрос.УстановитьПараметр("СписокФизическихЛиц", СписокФизическихЛиц);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	&ДатаПолученияДанных КАК Период,
	|	ФизическиеЛица.Ссылка КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТФизическихЛица
	|ИЗ
	|	Справочник.ФизическиеЛица КАК ФизическиеЛица
	|ГДЕ
	|	ФизическиеЛица.Ссылка В(&СписокФизическихЛиц)";
	
	Если НЕ ТолькоРазрешенные Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВЫБРАТЬ РАЗРЕШЕННЫЕ", "ВЫБРАТЬ");
	КонецЕсли;
	
	Запрос.Выполнить();
	
	ИзмеренияДаты = Новый ТаблицаЗначений;
	ИзмеренияДаты.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	ИзмеренияДаты.Добавить().Период = ?(ДатаПолученияДанных = Неопределено, ТекущаяДатаСеанса(), ДатаПолученияДанных);
	
	ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
	ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыПостроения.Отборы, "Физлицо", " В ", СписокФизическихЛиц);
	ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыПостроения.Отборы, "ВидДокумента", " = ", ВидДокумента);
	
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних(
		"ДокументыФизическихЛиц",
		Запрос.МенеджерВременныхТаблиц,
		ТолькоРазрешенные,
		ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(ИзмеренияДаты),
		ПараметрыПостроения,
		"ВТУдостоверенияЛичности");
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ФизическихЛица.Период,
	|	ФизическихЛица.ФизическоеЛицо,
	|	ЕСТЬNULL(ДокументыФизическихЛиц.ВидДокумента, ЗНАЧЕНИЕ(Справочник.ВидыДокументовФизическихЛиц.ПустаяСсылка)) КАК ВидДокумента,
	|	ЕСТЬNULL(ДокументыФизическихЛиц.Серия, """") КАК Серия,
	|	ЕСТЬNULL(ДокументыФизическихЛиц.Номер, """") КАК Номер,
	|	ЕСТЬNULL(ДокументыФизическихЛиц.ДатаВыдачи, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)) КАК ДатаВыдачи,
	|	ЕСТЬNULL(ДокументыФизическихЛиц.СрокДействия, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)) КАК СрокДействия,
	|	ЕСТЬNULL(ДокументыФизическихЛиц.КемВыдан, """") КАК КемВыдан,
	|	ЕСТЬNULL(ДокументыФизическихЛиц.КодПодразделения, """") КАК КодПодразделения,
	|	ЕСТЬNULL(ДокументыФизическихЛиц.ЯвляетсяДокументомУдостоверяющимЛичность, НЕОПРЕДЕЛЕНО) КАК ЯвляетсяДокументомУдостоверяющимЛичность,
	|	ЕСТЬNULL(ДокументыФизическихЛиц.Представление, """") КАК Представление
	|ИЗ
	|	ВТФизическихЛица КАК ФизическихЛица
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУдостоверенияЛичности КАК ДокументыФизическихЛиц
	|		ПО ФизическихЛица.ФизическоеЛицо = ДокументыФизическихЛиц.Физлицо";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Возвращает структуру параметров для СоздатьВТСотрудникиОрганизаций.
//
// Возвращаемое значение:
//   Структура - Содержит ключи:
//				Организация 		- СправочникСсылка.Организации, если параметр опустить в таблицу войдут все сотрудник
//										независимо от организации.
//				ОтбиратьПоГоловнойОрганизации - если Истина, то отбор по головной организации, иначе по текущей организации.
//												По умолчанию - ЛОЖЬ
//				Подразделение 		- СправочникСсылка.ПодразделенияОрганизаций, в таблицу войдут сотрудники указанного подразделения
//										и всех подразделений, подчиненных указанному, если параметр опустить в таблицу войдут все сотрудники.
//				СписокФизическихЛиц - Массив ссылок на элементы справочника ФизическиеЛица, если параметр опустить,
//										в таблицу войдут сотрудники всех физических лиц когда либо работавших в Организации.
//				НачалоПериода 		- Дата, значение по умолчанию '00010101'. Если параметр устанавливается, параметр ОкончаниеПериода
//										обязателен к заполнению.
//				ОкончаниеПериода	- Дата, значение по умолчанию '00010101'. При установке должно выполняться условие
//										ОкончаниеПериода >= НачалоПериода. Используется для выборки кадровых данных сотрудников, если
//										параметр не указан, выборка будет производиться на текущую дату сеанса.
//				КадровыеДанные		- Строка, содержит список полей кадровых данных, см. описание к функции КадровыеДанныеСотрудников.
//				Отборы				- Массив, структур описывающих отборы сотрудников
//										см. ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор,
//										допустимые отборы описаны см. КадровыйУчет.КритерииПоискаСотрудниковПоКоллекцииОтборов
//
Функция ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц() Экспорт
	
	Возврат КадровыйУчетВнутренний.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	
КонецФункции

// Возвращает структуру параметров для СоздатьВТСотрудникиОрганизаций.
//
// Возвращаемое значение:
//   Структура - Содержит ключи:
//				Организация 		- СправочникСсылка.Организации, если параметр опустить в таблицу войдут все сотрудник
//										независимо от организации.
//				ОтбиратьПоГоловнойОрганизации - если Истина, то отбор по головной организации, иначе по текущей организации.
//												По умолчанию - ЛОЖЬ
//				Подразделение 		- СправочникСсылка.ПодразделенияОрганизаций, в таблицу войдут сотрудники указанного подразделения
//										и всех подразделений, подчиненных указанному, если параметр опустить в таблицу войдут все сотрудники.
//				СписокФизическихЛиц - Массив ссылок на элементы справочника ФизическиеЛица, если параметр опустить,
//										в таблицу войдут сотрудники всех физических лиц когда либо работавших в Организации.
//				НачалоПериода 		- Дата, значение по умолчанию '00010101'. Если параметр устанавливается, параметр ОкончаниеПериода
//										обязателен к заполнению.
//				ОкончаниеПериода	- Дата, значение по умолчанию '00010101'. При установке должно выполняться условие
//										ОкончаниеПериода >= НачалоПериода. Используется для выборки кадровых данных сотрудников, если
//										параметр не указан, выборка будет производиться на текущую дату сеанса.
//				КадровыеДанные		- Строка, содержит список полей кадровых данных, см. описание к функции КадровыеДанныеСотрудников.
//				Отборы				- Массив, структур описывающих отборы сотрудников
//										см. ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор,
//										допустимые отборы описаны см. КадровыйУчет.КритерииПоискаСотрудниковПоКоллекцииОтборов
//
Функция ПараметрыПолученияВсехСотрудниковОрганизацийПоСпискуФизическихЛиц() Экспорт
	
	Возврат КадровыйУчетВнутренний.ПараметрыПолученияВсехСотрудниковОрганизацийПоСпискуФизическихЛиц();
	
КонецФункции

// Возвращает структуру параметров для СоздатьВТСотрудникиОрганизаций.
//
// Возвращаемое значение:
//   Структура - Содержит ключи:
//				Организация 		- СправочникСсылка.Организации, если параметр опустить в таблицу войдут все сотрудник
//										независимо от организации.
//				ОтбиратьПоГоловнойОрганизации - если Истина, то отбор по головной организации, иначе по текущей организации.
//													По умолчанию - ЛОЖЬ
//				Подразделение 		- СправочникСсылка.ПодразделенияОрганизаций, в таблицу войдут сотрудники указанного подразделения
//										и всех подразделений, подчиненных указанному, если параметр опустить в таблицу войдут все сотрудники.
//				ИмяВТФизическиеЛица - Строка, имя временной таблицы, содержащей физические лица (по умолчанию "ВТФизическиеЛица").
//				ИмяПоляФизическоеЛицо - Строка, имя поля - Физическое лицо (по умолчанию "ФизическоеЛицо").
//				НачалоПериода 		- Дата, значение по умолчанию '00010101'. Если параметр устанавливается, параметр ОкончаниеПериода
//										обязателен к заполнению.
//				ОкончаниеПериода 	- Дата, значение по умолчанию '00010101'. При установке должно выполняться условие
//										ОкончаниеПериода >= НачалоПериода. Используется для выборки кадровых данных сотрудников, если
//										параметр не указан, выборка будет производиться на текущую дату сеанса.
//				КадровыеДанные 		- Строка, содержит список полей кадровых данных, см. описание к функции КадровыеДанныеСотрудников.
//				Отборы				- Массив, структур описывающих отборы сотрудников
//										см. ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор,
//										допустимые отборы описаны см. КадровыйУчет.КритерииПоискаСотрудниковПоКоллекцииОтборов
//
Функция ПараметрыПолученияСотрудниковОрганизацийПоВременнойТаблице() Экспорт
		
	Возврат КадровыйУчетВнутренний.ПараметрыПолученияСотрудниковОрганизацийПоВременнойТаблице();
	
КонецФункции

// Возвращает таблицу значений, заполненную сотрудниками.
//
// Параметры:
//	ТолькоРазрешенные - Булево
//	Параметры - Структура, подготовленная с помощью методов
//	            ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц,
//	            ПараметрыПолученияСотрудниковОрганизацийПоВременнойТаблице
//
// Возвращаемое значение:
//   ТаблицаЗначений   - Таблица значений, содержащая поля
//							* Период 			- Дата, равен значению ключа ОкончаниеПериода параметра Параметры
//						 	* Сотрудник			- СправочникСсылка.Сотрудники
//							* ФизическоеЛицо	- СправочникСсылка.ФизическиеЛица
//							<кадровые данные>	- указанные в ключе КадровыеДанные параметра Параметры
//
Функция СотрудникиОрганизации(ТолькоРазрешенные, Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	КадровыеДанные = Параметры.КадровыеДанные;
	Если ЗначениеЗаполнено(КадровыеДанные) Тогда
		
		Если ТипЗнч(КадровыеДанные) = Тип("Строка") Тогда
			КадровыеДанные = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(КадровыеДанные, ",", , Истина);
		КонецЕсли;
		
		ТекстЗапросаКадровыхДанных = "СотрудникиОрганизации.ФизическоеЛицо";
		Для каждого ИмяКадровыхДанных Из КадровыеДанные Цикл
			ТекстЗапросаКадровыхДанных = ТекстЗапросаКадровыхДанных + ",
				|	СотрудникиОрганизации." + ИмяКадровыхДанных;
		КонецЦикла;
		
	Иначе
		ТекстЗапросаКадровыхДанных = "";
	КонецЕсли; 
	
	ЗарплатаКадры.ДополнитьКадровымиДаннымиНастройкиПорядкаСписка(Параметры.КадровыеДанные);
	
	СоздатьВТСотрудникиОрганизации(Запрос.МенеджерВременныхТаблиц, ТолькоРазрешенные, Параметры);
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СотрудникиОрганизации.Период,
		|	СотрудникиОрганизации.Сотрудник,
		|	СотрудникиОрганизации.ФизическоеЛицо
		|ИЗ
		|	ВТСотрудникиОрганизации КАК СотрудникиОрганизации";
		
	Если Не ПустаяСтрока(ТекстЗапросаКадровыхДанных) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "СотрудникиОрганизации.ФизическоеЛицо", ТекстЗапросаКадровыхДанных);
	КонецЕсли; 
	
	ЗарплатаКадры.ДополнитьТекстЗапросаУпорядочиваниемСотрудниковПоВТСДаннымиПорядка(Запрос, "СотрудникиОрганизации");
	
	СотрудникиОрганизации = Запрос.Выполнить().Выгрузить();
	
	Возврат СотрудникиОрганизации;
	
КонецФункции

// Возвращает таблицу значений содержащую поля ФизическоеЛицо и Сотрудник. Сотрудник отбирается по принципам:
//		- Работает на указанный период.
//		- Выбирается сотрудник, работающий по основному месту работы, если такового не существует, выбирается
//			сотрудник, работающий по совместительству, принятый на наиболее раннюю дату.
//
// Параметры:
//			СписокФизическихЛиц - Массив ссылок на элементы справочника ФизическиеЛица или 
//								- СправочникСсылка.ФизическиеЛица
//								- Неопределено - в таблицу войдут "основные" сотрудники всех
//									физических лиц когда либо работавших в организации.
//			ТолькоРазрешенные	- Булево
//			Организация			- СправочникСсылка.Организации
//			Период				- Дата
//
//
// Возвращаемое значение:
//   ТаблицаЗначений   - Таблица значений, содержащая поля Сотрудник и соответствующее сотруднику ФизическоеЛицо.
//
Функция ОсновныеСотрудникиФизическихЛиц(СписокФизическихЛиц, ТолькоРазрешенные, Организация, Период) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	СоздатьВТОсновныеСотрудникиФизическихЛиц(Запрос.МенеджерВременныхТаблиц, ТолькоРазрешенные, СписокФизическихЛиц, Организация, Период);
	
	Запрос.Текст = 
	"ВЫБРАТЬ ОсновныеСотрудникиФизическихЛиц.* ИЗ ВТОсновныеСотрудникиФизическихЛиц КАК ОсновныеСотрудникиФизическихЛиц";
	
	ОсновныеСотрудникиФизическихЛиц = Запрос.Выполнить().Выгрузить();
	
	Возврат ОсновныеСотрудникиФизическихЛиц;
	
КонецФункции

// Заполняет кадровые движения документа
//		КадроваяИсторияСотрудников
//		ЗанятыеПозицииШтатногоРасписания.
//
// Параметры:
//  РегистраторОбъект
//	Движения - коллекция движений, в которой необходимо заполнить кадровые движения.
//	КадровыеДвижения - таблица значений с полями:
//		ДатаСобытия
//		ВидСобытия - Перечисление.ВидыКадровыхСобытий
//		Сотрудник
//		ГоловнаяОрганизация
//		Организация
//		Подразделение
//		Должность
//		ВидЗанятости
//	ТолькоБронированиеПозиции	- Булево
//	Записывать					- Булево
//
Процедура СформироватьКадровыеДвижения(РегистраторОбъект, Движения, КадровыеДвижения, ТолькоБронированиеПозиции = Ложь, Записывать = Истина) Экспорт
	
	КадровыйУчетВнутренний.СформироватьКадровыеДвижения(РегистраторОбъект, Движения, КадровыеДвижения, ТолькоБронированиеПозиции, Записывать);
	
КонецПроцедуры

// Функция возвращает ссылку на физическое лицо, найденное по переданным фамилии, имени, отчеству СНИЛС и организации 
// (см. функцию ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервераПереопределяемый.ФизЛицоПоФИО).
//
// Параметры:
//  Фамилия		- Строка - Фамилия физического лица.
//  Имя			- Строка - Имя физического лица.
//  Отчество	- Строка - Отчество физического лица.
//  СНИЛС		- Строка - СНИЛС физического лица.
//  Организация - СправочникиСсылка.Организации - организация, в которой работает физическое лицо.
//
// Возвращаемое значение:
//   СправочникиСсылка.ФизическиеЛица
//
Функция ФизическоеЛицоПоФИОСНИЛСИОрганизации(Фамилия, Имя, Отчество, СНИЛС, Организация) Экспорт
	
	ВозвращаемоеЗначение = Справочники.ФизическиеЛица.ПустаяСсылка();
	
	СписокФизическихЛиц = ФизическиеЛицаЗарплатаКадры.ФизическиеЛицаПоФИОИСНИЛС(Фамилия, Имя, Отчество, СНИЛС);
	
	Если СписокФизическихЛиц.Количество() > 0 Тогда
		
		Параметры = ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
		Параметры.Организация = Организация;
		Параметры.ОкончаниеПериода = ТекущаяДатаСеанса();
		Параметры.СписокФизическихЛиц = СписокФизическихЛиц;
		
		СотрудникиФизическихЛиц = СотрудникиОрганизации(Истина, Параметры);
		Если СотрудникиФизическихЛиц.Количество() > 0 Тогда
			ВозвращаемоеЗначение = СотрудникиФизическихЛиц[0].ФизическоеЛицо;
		КонецЕсли; 
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение; 
	
КонецФункции

// Возвращает таблицу значений в строках которой содержится информация о периодах
// изменения (регистрации) фамилии, имени и отчества физических лиц.
//
// Параметры:
//		СписокФизическихЛиц - Массив, ссылок справочника ФизическиеЛица
//							- СправочникСсылка.ФизическиеЛица
//
// Возвращаемое значение:
//		ТаблицаЗначений - содержит колонки:
//			* Период			- Дата
//			* ФизическоеЛицо	- СправочникСсылка.ФизическиеЛица
//			* Фамилия			- Строка
//			* Имя				- Строка
//			* Отчество			- Строка
//
Функция ПериодыИзмененияФИОФизическихЛиц(СписокФизическихЛиц) Экспорт
	
	Если ТипЗнч(СписокФизическихЛиц) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		ФизическиеЛица = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СписокФизическихЛиц);
	Иначе
		ФизическиеЛица = СписокФизическихЛиц;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("СписокФизическихЛиц", ФизическиеЛица);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаНачала,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаОкончания,
		|	ФизическиеЛица.Ссылка
		|ПОМЕСТИТЬ ВТОтборФизическихЛиц
		|ИЗ
		|	Справочник.ФизическиеЛица КАК ФизическиеЛица
		|ГДЕ
		|	ФизическиеЛица.Ссылка В(&СписокФизическихЛиц)";
		
	Запрос.Выполнить();
	
	ОписательВТ = ОписательВременныхТаблицДляСоздатьВТИсторияИзмененияФИОФизическихЛиц(
		Запрос.МенеджерВременныхТаблиц,
		"ВТОтборФизическихЛиц",
		"Ссылка");
	
	РезультатЗапроса = СоздатьВТИсторияИзмененияФИОФизическихЛиц(Истина, ОписательВТ, "");
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

// Возвращает объект справочника ФизическиеЛица, созданный по переданным фамилии,
// имени, отчеству и другим данным (см. параметр ДанныеФизическогоЛица)
//
// Параметры:
//		ФИО						- Строка, содержит фамилию, имя и отчество
//								- Структура с ключами:
//									* Фамилия	- Строка
//									* Имя		- Строка
//									* Отчество	- Строка
//		ДанныеФизическогоЛица	- Структура, содержащая ключи одноименные реквизитам (включая стандартные)
//									справочника ФизическиеЛица, за исключением реквизитов:
//									Наименование, ФИО, УточнениеНаименования
//									Доступен ключ КонтактнаяИнформация - массив структур, в котором можно передать
//									контактную информацию в ключах (ЗначенияПолей, ВидИнформации, ТипИнформации)
//		ПериодДанных			- Дата, дата на которую будет зарегистрирована запись о ФИО
//
// Возвращаемое значение:
//		СправочникОбъект.ФизическиеЛица
//
Функция НовоеФизическоеЛицо(Знач ФИО, Знач ДанныеФизическогоЛица = Неопределено, Знач ПериодДанных = Неопределено) Экспорт
	
	Если ТипЗнч(ФИО) = Тип("Строка") Тогда
		ФамилияИмяОтчество = ФизическиеЛицаКлиентСервер.ЧастиИмени(ФИО);
	Иначе
		ФамилияИмяОтчество = Новый Структура("Фамилия,Имя,Отчество");
		ЗаполнитьЗначенияСвойств(ФамилияИмяОтчество, ФИО);
	КонецЕсли;
	
	УточнениеНаименования = "";
	Если ДанныеФизическогоЛица <> Неопределено
		И ДанныеФизическогоЛица.Свойство("УточнениеНаименования")
		И ЗначениеЗаполнено(ДанныеФизическогоЛица.УточнениеНаименования) Тогда
		
		УточнениеНаименования = ДанныеФизическогоЛица.УточнениеНаименования;
		
	КонецЕсли;
	
	Наименование = КадровыйУчетКлиентСервер.ПолноеНаименованиеСотрудника(
		ФамилияИмяОтчество.Фамилия, ФамилияИмяОтчество.Имя, ФамилияИмяОтчество.Отчество, УточнениеНаименования);
	
	ФизическоеЛицоОбъект = Справочники.ФизическиеЛица.СоздатьЭлемент();
	
	ФизическоеЛицоОбъект.Наименование = Наименование;
	ФизическоеЛицоОбъект.УточнениеНаименования = УточнениеНаименования;
	
	ФизическоеЛицоОбъект.ФИО = ФамилияИмяОтчество.Фамилия + " " + ФамилияИмяОтчество.Имя + " " + ФамилияИмяОтчество.Отчество;
	ЗаполнитьЗначенияСвойств(ФизическоеЛицоОбъект, ФамилияИмяОтчество, "Фамилия,Имя,Отчество");
	ФизическоеЛицоОбъект.ДополнительныеСвойства.Вставить("ФИОУстановлены", Истина);
	
	Если ДанныеФизическогоЛица <> Неопределено Тогда
		
		ИсключаемыеСвойства = "";
		Если ДанныеФизическогоЛица.Свойство("Наименование") Тогда
			ИсключаемыеСвойства = "Наименование";
		КонецЕсли;
		
		Если ДанныеФизическогоЛица.Свойство("ФИО") Тогда
			ИсключаемыеСвойства = ?(ПустаяСтрока(ИсключаемыеСвойства), "", ИсключаемыеСвойства + ",") + "ФИО";
		КонецЕсли;
		
		Если ДанныеФизическогоЛица.Свойство("УточнениеНаименования")Тогда
			ИсключаемыеСвойства = ?(ПустаяСтрока(ИсключаемыеСвойства), "", ИсключаемыеСвойства + ",") + "УточнениеНаименования";
		КонецЕсли;
		
		Если ДанныеФизическогоЛица.Свойство("Ссылка") Тогда
			ИсключаемыеСвойства = ?(ПустаяСтрока(ИсключаемыеСвойства), "", ИсключаемыеСвойства + ",") + "Ссылка";
		КонецЕсли;
		
		Если ДанныеФизическогоЛица.Свойство("КонтактнаяИнформация") Тогда
			
			ИсключаемыеСвойства = ?(ПустаяСтрока(ИсключаемыеСвойства), "", ИсключаемыеСвойства + ",") + "КонтактнаяИнформация";
			Для каждого ОписаниеКонтактнойИнформации Из ДанныеФизическогоЛица.КонтактнаяИнформация Цикл
				
				УправлениеКонтактнойИнформацией.ЗаписатьКонтактнуюИнформацию(ФизическоеЛицоОбъект,
					ОписаниеКонтактнойИнформации.ЗначенияПолей, ОписаниеКонтактнойИнформации.ВидИнформации, ОписаниеКонтактнойИнформации.ТипИнформации);
				
			КонецЦикла;
			
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ФизическоеЛицоОбъект, ДанныеФизическогоЛица, , ИсключаемыеСвойства);
		
		Если ДанныеФизическогоЛица.Свойство("Ссылка") 
			И ЗначениеЗаполнено(ДанныеФизическогоЛица.Ссылка) Тогда
			
			ФизическоеЛицоОбъект.УстановитьСсылкуНового(ДанныеФизическогоЛица.Ссылка);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ФизическоеЛицоОбъект.Пол) Тогда
		ФизическоеЛицоОбъект.Пол = СотрудникиКлиентСервер.ОпределитьПолПоОтчеству(ФамилияИмяОтчество.Отчество);
	КонецЕсли;
	
	ФизическоеЛицоОбъект.Записать();
	
	Если Не ПустаяСтрока(Наименование) Тогда
		
		Набор = РегистрыСведений.ФИОФизическихЛиц.СоздатьНаборЗаписей();
		Набор.Отбор.ФизическоеЛицо.Установить(ФизическоеЛицоОбъект.Ссылка);
		
		Запись = Набор.Добавить();
		Запись.Фамилия 	= ФизическоеЛицоОбъект.Фамилия;
		Запись.Имя	 	= ФизическоеЛицоОбъект.Имя;
		Запись.Отчество = ФизическоеЛицоОбъект.Отчество;
		Запись.ФизическоеЛицо = ФизическоеЛицоОбъект.Ссылка;
		
		Если ПериодДанных = Неопределено Тогда
			Запись.Период = ЗарплатаКадрыКлиентСервер.ДатаОтсчетаПериодическихСведений();
		Иначе
			Запись.Период = ПериодДанных;
		КонецЕсли;
		
		Набор.Записать();
		
	КонецЕсли;
	
	Возврат ФизическоеЛицоОбъект;
	
КонецФункции

#КонецОбласти

// Функция рассчитывает численность всех работников, женщин и инвалидов.
// При этом за переданный период считается среднесписочная численность, 
// а на конец заданного периода - списочная численность.
//
// Параметры:
//		Организация		- СправочникСсылка.Организации
//		НачалоПериода	- Дата
//		КонецПериода	- Дата
//
// Возвращаемое значение - Структура см. СтатистикаПерсонала.ДанныеОЧисленности 
//	
Функция СреднесписочнаяЧисленностьРаботающих(Организация, НачалоПериода, КонецПериода) Экспорт 
	Возврат КадровыйУчетВнутренний.СреднесписочнаяЧисленностьРаботающих(Организация, НачалоПериода, КонецПериода);
КонецФункции

#КонецОбласти


#Область СлужебныйПрограммныйИнтерфейс

// Возвращает настроенную структуру параметров для СоздатьВТСотрудникиОрганизаций
// для получения всех возможных рабочих мест. см. функцию ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц.
//
Функция ПараметрыПолученияРабочихМестВОрганизацийПоСпискуФизическихЛиц() Экспорт
	
	Возврат КадровыйУчетВнутренний.ПараметрыПолученияРабочихМестВОрганизацийПоСпискуФизическихЛиц();
	
КонецФункции

// Возвращает настроенную структуру параметров для СоздатьВТСотрудникиОрганизаций
// для получения всех возможных рабочих мест. см. функцию ПараметрыПолученияСотрудниковОрганизацийПоВременнойТаблице.
//
Функция ПараметрыПолученияРабочихМестВОрганизацийПоВременнойТаблице() Экспорт
	
	Возврат КадровыйУчетВнутренний.ПараметрыПолученияРабочихМестВОрганизацийПоВременнойТаблице();
	
КонецФункции

// Выводит сообщения пользователю по физическим лицам, которые не работают в указанный период.
// Вызывается из документов в обработчике ОбработкаПроверкиЗаполнения.
//
// Параметры:
//		СписокФизическихЛиц							- Массив ссылок на элементы справочника ФизическиеЛица или СправочникСсылка.ФизическиеЛица.
//		ПараметрыПолученияСотрудниковОрганизаций 	- Структура, см. функцию ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц
//		Отказ										- Булево - Если установить данному параметру значение Истина, то будет выполнен отказ от
//														продолжения работы после выполнения проверки заполнения.
//		ПараметрыСообщения							- Структура
//														* ИмяПоляСотрудник - Строка - наименование реквизита формы.
//														* ИмяОбъекта - Строка - путь к данным (путь к реквизиту формы).
//
Процедура ПроверитьРаботающихФизическихЛиц(СписокФизическихЛиц, ПараметрыПолученияСотрудниковОрганизаций, Отказ, ПараметрыСообщения) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("СписокФизическихЛиц", СписокФизическихЛиц);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ЗарплатаКадрыПовтИсп.ГоловнаяОрганизация(ПараметрыПолученияСотрудниковОрганизаций.Организация));
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Сотрудники.Ссылка КАК Сотрудник,
		|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо
		|ПОМЕСТИТЬ ВТФизическиеЛица
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.ФизическоеЛицо В(&СписокФизическихЛиц)
		|	И Сотрудники.ГоловнаяОрганизация = &ГоловнаяОрганизация";
	
	Запрос.Выполнить();
	
	ПараметрыПолученияСотрудниковОрганизаций.СписокФизическихЛиц = СписокФизическихЛиц;
	
	УстановитьПривилегированныйРежим(Истина);
	СоздатьВТСотрудникиОрганизации(Запрос.МенеджерВременныхТаблиц, Истина, ПараметрыПолученияСотрудниковОрганизаций);
	УстановитьПривилегированныйРежим(Ложь);
	
	СообщитьОНеработающихСотрудниках(Запрос.МенеджерВременныхТаблиц, ПараметрыПолученияСотрудниковОрганизаций, Отказ, ПараметрыСообщения, Истина);
	
КонецПроцедуры

// Выводит сообщения пользователю по сотрудникам, которые не работают в указанный период.
// Вызывается из документов в обработчике ОбработкаПроверкиЗаполнения.
//
// Параметры:
//		СписокСотрудников							- Массив ссылок на элементы справочника Сотрудники или СправочникСсылка.Сотрудники.
//		ПараметрыПолученияСотрудниковОрганизаций 	- Структура, см. функцию ПараметрыПолученияСотрудниковОрганизацийПоВременнойТаблице
//		Отказ										- Булево - Если установить данному параметру значение Истина, то будет выполнен
//														отказ от продолжения работы после выполнения проверки заполнения.
//		ПараметрыСообщения 							- Структура
//														* ИмяПоляСотрудник - Строка - наименование реквизита формы.
//														* ИмяОбъекта - Строка - путь к данным (путь к реквизиту формы).
//
Процедура ПроверитьРаботающихСотрудников(СписокСотрудников, ПараметрыПолученияСотрудниковОрганизаций, Отказ, ПараметрыСообщения) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("СписокСотрудников", СписокСотрудников);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Сотрудники.Ссылка КАК Сотрудник,
		|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо
		|ПОМЕСТИТЬ ВТФизическиеЛица
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.Ссылка В(&СписокСотрудников)";
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос.Выполнить();
	СоздатьВТСотрудникиОрганизации(Запрос.МенеджерВременныхТаблиц, Истина, ПараметрыПолученияСотрудниковОрганизаций);
	УстановитьПривилегированныйРежим(Ложь);
	
	СообщитьОНеработающихСотрудниках(Запрос.МенеджерВременныхТаблиц, ПараметрыПолученияСотрудниковОрганизаций, Отказ, ПараметрыСообщения);
	
КонецПроцедуры

Процедура СообщитьОНеработающихСотрудниках(МенеджерВременныхТаблиц, ПараметрыПолученияСотрудниковОрганизаций, Отказ, ПараметрыСообщения, ПроверкаРаботающихФизическихЛиц = Ложь)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ФизическиеЛица.Сотрудник КАК Сотрудник,
		|	ФизическиеЛица.ФизическоеЛицо КАК ФизическоеЛицо
		|ИЗ
		|	ВТФизическиеЛица КАК ФизическиеЛица
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСотрудникиОрганизации КАК СотрудникиОрганизации
		|		ПО ФизическиеЛица.Сотрудник = СотрудникиОрганизации.Сотрудник
		|ГДЕ
		|	СотрудникиОрганизации.Сотрудник ЕСТЬ NULL ";
	
	Если ПроверкаРаботающихФизическихЛиц Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПО ФизическиеЛица.Сотрудник = СотрудникиОрганизации.Сотрудник", "ПО ФизическиеЛица.ФизическоеЛицо = СотрудникиОрганизации.ФизическоеЛицо");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		НачалоПериода = НачалоДня(ПараметрыПолученияСотрудниковОрганизаций.НачалоПериода);
		ОкончаниеПериода = НачалоДня(ПараметрыПолученияСотрудниковОрганизаций.ОкончаниеПериода);
		
		НеРаботающиеСотрудники = Новый Соответствие;
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если ПроверкаРаботающихФизическихЛиц Тогда
				ОбъектПроверки = Выборка.ФизическоеЛицо;
			Иначе
				ОбъектПроверки = Выборка.Сотрудник;
			КонецЕсли;
			
			НеРаботающиеСотрудники.Вставить(ОбъектПроверки, Истина);
			
		КонецЦикла;
		
		Для каждого НеРаботающийСотрудники Из НеРаботающиеСотрудники Цикл
			
			Сотрудник = НеРаботающийСотрудники.Ключ;
			
			Если НачалоПериода = ОкончаниеПериода Или (ЗначениеЗаполнено(НачалоПериода) И Не ЗначениеЗаполнено(ОкончаниеПериода)) Тогда
				
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Сотрудник %1 не работает в организации на дату %2г.'"), Сотрудник, Формат(НачалоПериода, "ДЛФ=Д"));
				
			Иначе
				
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Сотрудник %1 не работает в организации за период с %2г. по %3г.'"), Сотрудник, Формат(НачалоПериода, "ДЛФ=Д"), Формат(ОкончаниеПериода, "ДЛФ=Д"));
				
			КонецЕсли;
				
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, , ПараметрыСообщения.ИмяПоляСотрудник, ПараметрыСообщения.ИмяОбъекта, Отказ);
			
		КонецЦикла;
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПроверитьВозможностьПроведенияПоКадровомуУчету(ДатаСобытия, Сотрудники, ИсключаемыеСсылки, Отказ, ВидСобытия = Неопределено) Экспорт
	
	СотрудникиДаты = Новый ТаблицаЗначений;
	СотрудникиДаты.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	СотрудникиДаты.Колонки.Добавить("ДатаСобытия", Новый ОписаниеТипов("Дата"));
	СотрудникиДаты.Колонки.Добавить("ВидСобытия", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыКадровыхСобытий")); 
	
	Если ТипЗнч(Сотрудники) = Тип("СправочникСсылка.Сотрудники") Тогда
		СписокСотрудников = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудники);
	Иначе
		СписокСотрудников = Сотрудники;
	КонецЕсли;
	
	Для каждого Сотрудник Из СписокСотрудников Цикл
		
		СтрокаСотрудникиДаты = СотрудникиДаты.Добавить();
		СтрокаСотрудникиДаты.ДатаСобытия = ДатаСобытия;
		СтрокаСотрудникиДаты.Сотрудник = Сотрудник;
		СтрокаСотрудникиДаты.ВидСобытия = ВидСобытия;
		
	КонецЦикла;
	
	ПроверитьВозможностьПроведенияПоКадровомуУчетуТаблицыСотрудников(СотрудникиДаты, ИсключаемыеСсылки, Отказ);
	
КонецПроцедуры

Процедура ПроверитьВозможностьПроведенияПоКадровомуУчетуТаблицыСотрудников(СотрудникиДаты, ИсключаемыеСсылки, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("СотрудникиДаты", СотрудникиДаты);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СотрудникиДаты.ДатаСобытия,
		|	СотрудникиДаты.Сотрудник,
		|	СотрудникиДаты.ВидСобытия,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаНачала,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаОкончания
		|ПОМЕСТИТЬ ВТСотрудникиДаты
		|ИЗ
		|	&СотрудникиДаты КАК СотрудникиДаты";
	
	Запрос.Выполнить();
	
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра("ВТСотрудникиДаты", "Сотрудник");
	
	Если ТипЗнч(ИсключаемыеСсылки) <> Тип("Массив") Тогда
		ИсключаемыеСсылкиМассив = Новый Массив;
		ИсключаемыеСсылкиМассив.Добавить(ИсключаемыеСсылки);
	Иначе
		ИсключаемыеСсылкиМассив = ИсключаемыеСсылки;
	КонецЕсли;
	
	ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистра();
	ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыПостроения.Отборы, "Регистратор", "НЕ В", ИсключаемыеСсылкиМассив);
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистра(
		"КадроваяИсторияСотрудников",
		Запрос.МенеджерВременныхТаблиц,
		Ложь,
		ОписаниеФильтра,
		ПараметрыПостроения);
	
	УстановитьПривилегированныйРежим(Ложь);
	
	// Проверка на уникальность событий Прием и Увольнение.
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КадроваяИсторияСотрудников.Регистратор,
		|	КадроваяИсторияСотрудников.Сотрудник КАК Сотрудник,
		|	КадроваяИсторияСотрудников.Период КАК Период,
		|	КадроваяИсторияСотрудников.ВидСобытия
		|ИЗ
		|	ВТКадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиДаты КАК СотрудникиДаты
		|		ПО КадроваяИсторияСотрудников.Сотрудник = СотрудникиДаты.Сотрудник
		|			И (СотрудникиДаты.ВидСобытия В (ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Прием), ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.НачальныеДанные), ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)))
		|			И (ВЫБОР
		|				КОГДА СотрудникиДаты.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
		|					ТОГДА КадроваяИсторияСотрудников.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
		|				ИНАЧЕ КадроваяИсторияСотрудников.ВидСобытия В (ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Прием), ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.НачальныеДанные))
		|			КОНЕЦ)
		|ГДЕ
		|	НЕ КадроваяИсторияСотрудников.ЭтоВозвратноеСобытие
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудник,
		|	Период
		|АВТОУПОРЯДОЧИВАНИЕ";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ТекстСообщенияПользователю = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='По сотруднику %1, уже зарегистрировано событие ""%2"" (%3), документ противоречит кадровому приказу %4'"),
				Выборка.Сотрудник,
				Выборка.ВидСобытия,
				Формат(Выборка.Период, "ДЛФ=D"),
				Выборка.Регистратор);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстСообщенияПользователю,
					Выборка.Регистратор,
					,
					,
					Отказ);
				
		КонецЦикла;
		
	КонецЕсли;
	
	// Проверка корректности заданного периода Период, должен быть более даты приема,
	// а для событий кадрового перевода менее даты увольнения.
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СотрудникиДаты.Сотрудник
		|ИЗ
		|	ВТСотрудникиДаты КАК СотрудникиДаты
		|ГДЕ
		|	СотрудникиДаты.ВидСобытия В (ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Перемещение), ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение))";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	КадроваяИсторияСотрудников.Сотрудник КАК Сотрудник,
			|	КадроваяИсторияСотрудников.Период КАК Период,
			|	КадроваяИсторияСотрудников.Регистратор КАК Регистратор
			|ПОМЕСТИТЬ ВТДатыПриема
			|ИЗ
			|	ВТКадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников
			|ГДЕ
			|	КадроваяИсторияСотрудников.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Прием)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	КадроваяИсторияСотрудников.Сотрудник КАК Сотрудник,
			|	КадроваяИсторияСотрудников.Период КАК Период,
			|	КадроваяИсторияСотрудников.Регистратор КАК Регистратор
			|ПОМЕСТИТЬ ВТДатыУвольнения
			|ИЗ
			|	ВТКадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников
			|ГДЕ
			|	КадроваяИсторияСотрудников.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ЕСТЬNULL(ДатыПриема.Сотрудник, ДатыУвольнения.Сотрудник) КАК Сотрудник,
			|	ЕСТЬNULL(ДатыПриема.Период, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаПриема,
			|	ЕСТЬNULL(ДатыПриема.Регистратор, ЗНАЧЕНИЕ(Документ.ПриемНаРаботу.ПустаяСсылка)) КАК ПриказОПриеме,
			|	ЕСТЬNULL(ДатыУвольнения.Период, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаУвольнения,
			|	ЕСТЬNULL(ДатыУвольнения.Регистратор, ЗНАЧЕНИЕ(Документ.Увольнение.ПустаяСсылка)) КАК ПриказОбУвольнении,
			|	Сотрудники.ДатаСобытия,
			|	Сотрудники.ВидСобытия
			|ИЗ
			|	ВТСотрудникиДаты КАК Сотрудники
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДатыПриема КАК ДатыПриема
			|		ПО Сотрудники.Сотрудник = ДатыПриема.Сотрудник
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДатыУвольнения КАК ДатыУвольнения
			|		ПО Сотрудники.Сотрудник = ДатыУвольнения.Сотрудник
			|ГДЕ
			|	Сотрудники.ВидСобытия В (ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Перемещение), ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение))
			|
			|УПОРЯДОЧИТЬ ПО
			|	Сотрудник,
			|	ДатаПриема,
			|	ДатаУвольнения";
		
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				
				ТекстСообщенияПользователю = "";
				ДокументПриказ = Неопределено;
				
				Если Выборка.ДатаПриема > Выборка.ДатаСобытия Тогда
					
					ТекстСообщенияПользователю = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru='По сотруднику %1, невозможно зарегистрировать событие ""%2"" ранее, чем дата приема на работу (%3 - %4)'"),
						Выборка.Сотрудник,
						Выборка.ВидСобытия,
						Формат(Выборка.ДатаПриема, "ДЛФ=D"),
						Выборка.ПриказОПриеме);
					
					ДокументПриказ = Выборка.ПриказОПриеме;
					
				ИначеЕсли Выборка.ДатаУвольнения < Выборка.ДатаСобытия И ЗначениеЗаполнено(Выборка.ДатаУвольнения) Тогда
					
					ТекстСообщенияПользователю = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru='По сотруднику %1, невозможно зарегистрировать событие ""%2"" позднее, чем дата увольнения (%3 - %4)'"),
						Выборка.Сотрудник,
						Выборка.ВидСобытия,
						Формат(Выборка.ДатаУвольнения, "ДЛФ=D"),
						Выборка.ПриказОбУвольнении);
						
					ДокументПриказ = Выборка.ПриказОбУвольнении;
					
				КонецЕсли;
				
				Если Не ПустаяСтрока(ТекстСообщенияПользователю) Тогда
					
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						ТекстСообщенияПользователю,
						ДокументПриказ,
						,
						,
						Отказ);
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Проверка того, что прием на работу самое первое событие.
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СотрудникиДаты.Сотрудник
		|ИЗ
		|	ВТСотрудникиДаты КАК СотрудникиДаты
		|ГДЕ
		|	СотрудникиДаты.ВидСобытия В (ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Прием), ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.НачальныеДанные))";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	КадроваяИсторияСотрудников.Сотрудник КАК Сотрудник,
			|	МИНИМУМ(КадроваяИсторияСотрудников.Период) КАК Период
			|ПОМЕСТИТЬ ВТРанниеПериоды
			|ИЗ
			|	ВТКадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников
			|
			|СГРУППИРОВАТЬ ПО
			|	КадроваяИсторияСотрудников.Сотрудник
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	КадроваяИсторияСотрудников.Сотрудник КАК Сотрудник,
			|	КадроваяИсторияСотрудников.Период КАК Период,
			|	КадроваяИсторияСотрудников.ВидСобытия КАК ВидСобытия,
			|	КадроваяИсторияСотрудников.Регистратор КАК Регистратор
			|ИЗ
			|	ВТРанниеПериоды КАК РанниеПериоды
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников
			|		ПО РанниеПериоды.Сотрудник = КадроваяИсторияСотрудников.Сотрудник
			|			И РанниеПериоды.Период = КадроваяИсторияСотрудников.Период
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиДаты КАК СотрудникиДаты
			|		ПО РанниеПериоды.Сотрудник = СотрудникиДаты.Сотрудник
			|			И (СотрудникиДаты.ВидСобытия В (ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Прием), ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.НачальныеДанные)))
			|			И РанниеПериоды.Период < СотрудникиДаты.ДатаСобытия
			|ГДЕ
			|	НЕ КадроваяИсторияСотрудников.ВидСобытия В (ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Прием), ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.НачальныеДанные))
			|
			|УПОРЯДОЧИТЬ ПО
			|	Сотрудник,
			|	Период";
		
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				
				ТекстСообщенияПользователю = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Сотрудника %1 необходимо принять на работу ранее кадрового события ""%2"" (%3 - %4)'"),
					Выборка.Сотрудник,
					Выборка.ВидСобытия,
					Формат(Выборка.Период, "ДЛФ=D"),
					Выборка.Регистратор);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстСообщенияПользователю,
					Выборка.Регистратор,
					,
					,
					Отказ);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Проверка того, что увольнение самое последнее событие.
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СотрудникиДаты.Сотрудник
		|ИЗ
		|	ВТСотрудникиДаты КАК СотрудникиДаты
		|ГДЕ
		|	СотрудникиДаты.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	КадроваяИсторияСотрудников.Сотрудник КАК Сотрудник,
			|	МАКСИМУМ(КадроваяИсторияСотрудников.Период) КАК Период
			|ПОМЕСТИТЬ ВТПоздниеПериоды
			|ИЗ
			|	ВТКадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников
			|ГДЕ
			|	НЕ КадроваяИсторияСотрудников.ЭтоВозвратноеСобытие
			|
			|СГРУППИРОВАТЬ ПО
			|	КадроваяИсторияСотрудников.Сотрудник
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	КадроваяИсторияСотрудников.Сотрудник КАК Сотрудник,
			|	КадроваяИсторияСотрудников.Период КАК Период,
			|	КадроваяИсторияСотрудников.ВидСобытия КАК ВидСобытия,
			|	КадроваяИсторияСотрудников.Регистратор КАК Регистратор
			|ИЗ
			|	ВТПоздниеПериоды КАК ПоздниеПериоды
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников
			|		ПО ПоздниеПериоды.Сотрудник = КадроваяИсторияСотрудников.Сотрудник
			|			И ПоздниеПериоды.Период = КадроваяИсторияСотрудников.Период
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиДаты КАК СотрудникиДаты
			|		ПО ПоздниеПериоды.Сотрудник = СотрудникиДаты.Сотрудник
			|			И (СотрудникиДаты.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение))
			|			И ПоздниеПериоды.Период > СотрудникиДаты.ДатаСобытия
			|
			|УПОРЯДОЧИТЬ ПО
			|	Сотрудник,
			|	Период";
		
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				
				ТекстСообщенияПользователю = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Сотрудника %1 необходимо уволить позднее кадрового события ""%2"" (%3 - %4)'"),
					Выборка.Сотрудник,
					Выборка.ВидСобытия,
					Формат(Выборка.Период, "ДЛФ=D"),
					Выборка.Регистратор);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстСообщенияПользователю,
					Выборка.Регистратор,
					,
					,
					Отказ);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Проверка того, что на период, равный ДатаСобытия, еще нет зарегистрированных событий.
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КадроваяИсторияСотрудников.Регистратор,
		|	КадроваяИсторияСотрудников.Сотрудник КАК Сотрудник,
		|	КадроваяИсторияСотрудников.Период КАК Период,
		|	КадроваяИсторияСотрудников.ВидСобытия
		|ИЗ
		|	ВТКадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиДаты КАК СотрудникиДаты
		|		ПО КадроваяИсторияСотрудников.Сотрудник = СотрудникиДаты.Сотрудник
		|			И КадроваяИсторияСотрудников.Период = СотрудникиДаты.ДатаСобытия
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудник,
		|	Период";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ТекстСообщенияПользователю = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='По сотруднику %1, дата кадрового события (%2) противоречит кадровому приказу %3'"),
				Выборка.Сотрудник,
				Формат(Выборка.Период, "ДЛФ=D"),
				Выборка.Регистратор);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстСообщенияПользователю,
				Выборка.Регистратор,
				,
				,
				Отказ);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьСоответствиеСотрудниковОрганизации(Организация, Сотрудник, Отказ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	ГоловнаяОрганизация = ЗарплатаКадрыПовтИсп.ГоловнаяОрганизация(Организация);
	ГоловнаяОрганизацияСотрудника = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сотрудник, "ГоловнаяОрганизация");
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ГоловнаяОрганизацияСотрудника <> ГоловнаяОрганизация Тогда
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Сотрудник %1 не соответствует организации %2.'"),
			Сотрудник,
			Организация);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстСообщения,
			,
			"Объект.Сотрудник",
			,
			Отказ);
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеДатыРожденияПоВременнойТаблице(МенеджерВременныхТаблиц, ИмяВТОтбораСотрудников, ДатаРожденияВТаблице = Истина) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Если Не ДатаРожденияВТаблице Тогда
		
		// Если даты рождения в таблице нет, нужно ее дополнительно получить.
		ИмяВТСпискаСотрудников = "ВТКадровыеДанныеСотрудниковДляПроверкиЗаполненностиДатыРождения";
		
		ОписательВременныхТаблиц = ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
			Запрос.МенеджерВременныхТаблиц,
			ИмяВТОтбораСотрудников);
		
		ОписательВременныхТаблиц.ИмяВТКадровыеДанныеСотрудников = ИмяВТСпискаСотрудников;
		СоздатьВТКадровыеДанныеСотрудников(ОписательВременныхТаблиц, Истина, "ДатаРождения");
		
	Иначе
		ИмяВТСпискаСотрудников = ИмяВТОтбораСотрудников;
	КонецЕсли; 
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Сотрудники.Сотрудник КАК Сотрудник
		|ИЗ
		|	ВТОтбораСотрудников КАК Сотрудники
		|ГДЕ
		|	Сотрудники.ДатаРождения = ДАТАВРЕМЯ(1, 1, 1)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудник";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТОтбораСотрудников", ИмяВТСпискаСотрудников);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		// Все хорошо, нет сотрудников с незаполненной датой рождения.
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	// Если записей больше 9, выводим только 5 из них.
	ВсегоОшибок = Выборка.Количество();
	КоличествоСообщений = ВсегоОшибок;
	Если КоличествоСообщений > 9 Тогда
		КоличествоСообщений = 5;
	КонецЕсли;
	
	Счетчик = 1;
	Пока Выборка.Следующий() Цикл
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='У сотрудника %1 не заполнена дата рождения'"), Выборка.Сотрудник);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Выборка.Сотрудник, "ДатаРождения", "ФизическоеЛицо");
		Счетчик = Счетчик + 1;
		
		Если Счетчик > КоличествоСообщений Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ВсегоОшибок > КоличествоСообщений Тогда
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='..и еще у %1 сотрудников'"), ВсегоОшибок - КоличествоСообщений);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
	КонецЕсли;
	
	ВызватьИсключение НСтр("ru = 'У одного или нескольких сотрудников не заполнена дата рождения.'");
	
КонецПроцедуры

// Возвращает соответствие сотрудников, переданным физическим лицам. Поиск сотрудников ведется
// в рамках головной организации, которая задается параметром Организация. Если, переданная 
// организация, является обособленным подразделением, то поиск ведется среди сотрудников головной
// этого обособленного подразделения.
// Если сотрудника не удается найти - создается новый.
// В ходе поиска сотрудников не производится проверка оформленности на работу. Предполагается, что
// функция вызывается для физических лиц, по которым не был определен сотрудник методом СотрудникиОрганизации.
//
// Параметры:
//			ФизическиеЛица 						- Массив ссылок на Справочник.ФизическиеЛица или ссылка на элемент.
//			Организация 						- СправочникСсылка.Организации
//			ВключатьВПоискУволенныхСотрудников	- Булево
//
// Возвращаемое значение:
//		Соответствие - ключи соответствия ссылки на переданные в параметрах физические лица.
//				Значения - соответствующие им сотрудники.
//
Функция НеоформленныеСотрудникиФизическихЛиц(ФизическиеЛица, Организация, ВключатьВПоискУволенныхСотрудников = Истина) Экспорт
	
	СоответствиеСотрудниковФизическимЛицам = Новый Соответствие;
	
	ГоловнаяОрганизация = ЗарплатаКадрыПовтИсп.ГоловнаяОрганизация(Организация);
	
	Если ТипЗнч(ФизическиеЛица) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		СписокФизическихЛиц = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическиеЛица);
	Иначе
		СписокФизическихЛиц = ФизическиеЛица;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("СписокФизическихЛиц", СписокФизическихЛиц);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ГоловнаяОрганизация);
	
	Если ВключатьВПоискУволенныхСотрудников Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Сотрудники.ФизическоеЛицо,
			|	Сотрудники.Ссылка КАК Сотрудник
			|ПОМЕСТИТЬ ВТСотрудники
			|ИЗ
			|	Справочник.Сотрудники КАК Сотрудники
			|ГДЕ
			|	Сотрудники.ФизическоеЛицо В(&СписокФизическихЛиц)
			|	И Сотрудники.ГоловнаяОрганизация = &ГоловнаяОрганизация";
			
	Иначе
		
		Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Сотрудники.ФизическоеЛицо,
			|	Сотрудники.Ссылка КАК Сотрудник
			|ПОМЕСТИТЬ ВТСотрудники
			|ИЗ
			|	Справочник.Сотрудники КАК Сотрудники
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
			|		ПО Сотрудники.Ссылка = ТекущиеКадровыеДанныеСотрудников.Сотрудник
			|ГДЕ
			|	Сотрудники.ФизическоеЛицо В(&СписокФизическихЛиц)
			|	И Сотрудники.ГоловнаяОрганизация = &ГоловнаяОрганизация
			|	И ЕСТЬNULL(ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)";
			
	КонецЕсли;
	
	Запрос.Выполнить();
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ФизическиеЛица.Ссылка КАК ФизическоеЛицо,
		|	МИНИМУМ(ЕСТЬNULL(Сотрудники.Сотрудник, ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка))) КАК Сотрудник
		|ИЗ
		|	Справочник.ФизическиеЛица КАК ФизическиеЛица
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСотрудники КАК Сотрудники
		|		ПО (Сотрудники.ФизическоеЛицо = ФизическиеЛица.Ссылка)
		|ГДЕ
		|	ФизическиеЛица.Ссылка В(&СписокФизическихЛиц)
		|
		|СГРУППИРОВАТЬ ПО
		|	ФизическиеЛица.Ссылка";

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ЗначениеЗаполнено(Выборка.Сотрудник) Тогда
			СотрудникФизическогоЛица = Выборка.Сотрудник;
		Иначе
			СотрудникФизическогоЛица = НовыйСотрудникФизическогоЛица(Выборка.ФизическоеЛицо, ГоловнаяОрганизация);
		КонецЕсли;
			
		СоответствиеСотрудниковФизическимЛицам.Вставить(Выборка.ФизическоеЛицо, СотрудникФизическогоЛица);
		
	КонецЦикла;
	
	Возврат СоответствиеСотрудниковФизическимЛицам;
	
КонецФункции

// Возвращает вновь созданного сотрудника по переданным физическому лицу и организации.
// Сотрудник создается для головной организации, переданной в параметре.
// 
// Параметры:
//		ФизическоеЛицо	- СправочникСсылка.ФизическиеЛица
//		Организация		- СправочникСсылка.Организации
//
// ВозвращаемоеЗначение:
//		СправочникСсылка.Сотрудники
//
Функция НовыйСотрудникФизическогоЛица(ФизическоеЛицо, Организация) Экспорт
	
	СотрудникОбъект = Справочники.Сотрудники.СоздатьЭлемент();
	СотрудникОбъект.ФизическоеЛицо = ФизическоеЛицо;
	СотрудникОбъект.ГоловнаяОрганизация = ЗарплатаКадрыПовтИсп.ГоловнаяОрганизация(Организация);
	СотрудникОбъект.Наименование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ФизическоеЛицо, "ФИО");
			
	СотрудникОбъект.Записать();
		
	Возврат СотрудникОбъект.Ссылка;
	
КонецФункции

// Формирует временную таблицу, содержащую поле Сотрудник, с сотрудниками не оформленными по
// трудовым договорам. В выборку включаются все неоформленные сотрудники, создававшиеся для головной организации.
//
// Параметры:
//		МенеджерВременныхТаблиц
//		ТолькоРазрешенные
//		Организация - если в качестве параметра передано обособленное подразделения, то в выборку войдут
//						не оформлявшиеся сотрудники головной организации обособленного подразделения
//		ИмяВТСотрудникиНеОформленныеПоТрудовомуДоговору.
//
Процедура СоздатьВТСотрудникиНеОформленныеПоТрудовомуДоговору(МенеджерВременныхТаблиц, ТолькоРазрешенные, Организация, ИмяВТСотрудникиНеОформленныеПоТрудовомуДоговору = "ВТСотрудникиНеОформленныеПоТрудовомуДоговору") Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ЗарплатаКадрыПовтИсп.ГоловнаяОрганизация(Организация));
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Сотрудники.Ссылка КАК Сотрудник
		|ПОМЕСТИТЬ ВТСотрудникиНеОформленныеПоТрудовомуДоговору
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
		|		ПО Сотрудники.Ссылка = ТекущиеКадровыеДанныеСотрудников.Сотрудник
		|			И (ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
		|ГДЕ
		|	Сотрудники.ГоловнаяОрганизация = &ГоловнаяОрганизация
		|	И ТекущиеКадровыеДанныеСотрудников.Сотрудник ЕСТЬ NULL ";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТСотрудникиНеОформленныеПоТрудовомуДоговору", ИмяВТСотрудникиНеОформленныеПоТрудовомуДоговору);
	
	Если НЕ ТолькоРазрешенные Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВЫБРАТЬ РАЗРЕШЕННЫЕ", "ВЫБРАТЬ");
	КонецЕсли;
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Возвращает структуру описывающую временную таблицу отборов для получения истории изменения
// ФИО физических лиц.
//
// Параметры:
//		МенеджерВременныхТаблиц	- МенеджерВременныхТаблиц
//		ИмяВТОтборов			- Строка
//		ИмяПоляФизическоеЛицо	- Строка
//
// Возвращаемое значение:
//		Структура - содержит ключи:
//			* МенеджерВременныхТаблиц	- МенеджерВременныхТаблиц.
//			* ИмяВТОтборов				- Строка.
//			* ИмяПоляФизическоеЛицо		- Строка.
//
Функция ОписательВременныхТаблицДляСоздатьВТИсторияИзмененияФИОФизическихЛиц(МенеджерВременныхТаблиц, ИмяВТОтборов, ИмяПоляФизическоеЛицо) Экспорт
	
	ОписательВТ = Новый Структура;
	
	ОписательВТ.Вставить("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц);
	ОписательВТ.Вставить("ИмяВТОтборов", ИмяВТОтборов);
	ОписательВТ.Вставить("ИмяПоляФизическоеЛицо", ИмяПоляФизическоеЛицо);
	
	Возврат ОписательВТ;
	
КонецФункции

// Создает временную таблицу, содержащую записи регистра сведений ФИОФизическихЛиц,
// по переданному отбору во временной таблице, содержащейся в МенеджереВременныхТаблиц.
//
// Параметры:
//		ТолькоРазрешенные						- Булево
//		ОписательВременныхТаблиц				- Структура (см. ОписательВременныхТаблицДляСоздатьВТИсторияИзмененияФИОФизическихЛиц).
//		ИмяВТИсторияИзмененияФИОФизическихЛиц	- Строка
//
// Возвращаемое значение:
//		РезультатЗапроса
//
Функция СоздатьВТИсторияИзмененияФИОФизическихЛиц(ТолькоРазрешенные, ОписательВременныхТаблиц, ИмяВТИсторияИзмененияФИОФизическихЛиц = "ВТИсторияИзмененияФИОФизическихЛиц") Экспорт
	
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(ОписательВременныхТаблиц.ИмяВТОтборов, "ФизическоеЛицо");
	ОписаниеФильтра.СоответствиеИзмеренийРегистраИзмерениямФильтра.Вставить("ФизическоеЛицо", ОписательВременныхТаблиц.ИмяПоляФизическоеЛицо);
	
	Запрос = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросВТИмяРегистра(
		"ФИОФизическихЛиц",
		ТолькоРазрешенные,
		ОписаниеФильтра,
		,
		ИмяВТИсторияИзмененияФИОФизическихЛиц);
	
	Запрос.МенеджерВременныхТаблиц = ОписательВременныхТаблиц.МенеджерВременныхТаблиц;
	Возврат Запрос.Выполнить();
	
КонецФункции

// Добавляет команду печати
Процедура ДобавитьКомандуПечатиПриказаОПредоставленииОтпуска(КомандыПечати) Экспорт
	
	// 4D:ERP для Беларуси, Юлия, 07.07.2017 13:35:29 
	// Печатные формы анализ, №15283
	// Редактирование списка печатных форм для печати из документа "Отпуск", №16192
	// {
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьКадровыхПриказов";
	КомандаПечати.Идентификатор = "ПФ_MXL_Т6_Локализация";
	КомандаПечати.Представление = НСтр("ru = 'Приказ о предоставлении отпуска'");
	КомандаПечати.Порядок = 10;		
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Обработка.ПечатьКадровыхПриказов";
	КомандаПечати.Идентификатор = "ПФ_MXL_Т6_1_Локализация";
	КомандаПечати.Представление = НСтр("ru = 'Приказ об отзыве работника из отпуска'");
	КомандаПечати.Порядок = 10;		
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	// }
	// 4D
	
КонецПроцедуры

// Возвращает таблицу договоров, сформированную по условиям переданным в параметре
// ТаблицаФизическихЛиц
//
// Параметры:
//		ТаблицаФизическихЛиц - ТаблицаЗначений с колонками
//									* ФизическоеЛицо		- СправочникСсылка.ФизическиеЛица
//									* ГоловнаяОрганизация	- СправочникСсылка.Организации
//									* НачалоПериода			- Дата
//									* ОкончаниеПериода		- Дата
//
// Возвращаемое значение:
//		ТаблицаЗначений с колонками
//			* ФизическоеЛицо	- СправочникСсылка.ФизическиеЛица
//			* ВидДоговора		- Булево, ложь - трудовой договор, истина - договор ГПХ
//			* ВидДоговораГПХ	- Булево, ложь - оказание услуг, истина - авторский
//									имеет смысл при значении ВидДоговора равное истина
//			* Номер				- Строка
//			* Дата				- Дата
//			* Организация		- СправочникСсылка.Организации
//			* Начало			- Дата
//			* Окончание			- Дата
//
Функция ДоговорыФизическихЛиц(ТаблицаФизическихЛиц) Экспорт
	
	Возврат КадровыйУчетВнутренний.ДоговорыФизическихЛиц(ТаблицаФизическихЛиц);
	
КонецФункции

Процедура ПроверитьУникальностьТабельногоНомера(ТабельныйНомер, Сотрудник) Экспорт
	
	КадровыйУчетВнутренний.ПроверитьУникальностьТабельногоНомера(ТабельныйНомер, Сотрудник);
	
КонецПроцедуры

// Проверяет наличие права доступа на раздел учета.
//
// Параметры:
//   Право - Строка - Проверяемое право. Допустимые значения:
//       "Чтение",
//       "Изменение".
//   ВидУчета - Строка - Проверяемый раздел учета. Допустимые значения:
//       "НачисленнаяЗарплата",
//       "ПлановыеНачисления",
//       "КадровыеДанные".
//
// Возвращаемое значение:
//   Булево - Наличие права на раздел учета.
//
Функция ПравоНаРазделУчета(Право, ВидУчета) Экспорт
	
	Возврат КадровыйУчетВнутренний.ПравоНаРазделУчета(Право, ВидУчета);
	
КонецФункции

#Область КадровыеДанныеФизическихЛиц

// Возвращает структуру, описывающую временную таблицу отборов физических лиц.
//
// Параметры:
//				ИмяВременнойТаблицыОтборовФизическихЛиц - Строка, имя временной таблицы
//								в менеджере запросов.
//				ИмяПоляФизическоеЛицо - Строка, указывает имя поля временной таблицы,
//								содержащего ссылку на физическое лицо.
//				ИмяПоляПериод - Строка, указывает имя поля временной таблицу, содержащее
//								период, на который предполагается получить кадровые данные.
//
Функция ОписаниеВременнойТаблицыОтборовФизическихЛиц(ИмяВременнойТаблицыОтборовФизическихЛиц, ИмяПоляФизическоеЛицо = "ФизическоеЛицо", ИмяПоляПериод = "Период") Экспорт
	
	ОписательВременнойТаблицыОтборов = Новый Структура;
	ОписательВременнойТаблицыОтборов.Вставить("ИмяВременнойТаблицыОтборовФизическихЛиц", ИмяВременнойТаблицыОтборовФизическихЛиц);
	ОписательВременнойТаблицыОтборов.Вставить("ИмяПоляФизическоеЛицо", ИмяПоляФизическоеЛицо);
	ОписательВременнойТаблицыОтборов.Вставить("ИмяПоляПериод", ИмяПоляПериод);
	ОписательВременнойТаблицыОтборов.Вставить("ИмяВременнойТаблицыОтборовРазличныхФизическихЛиц", "");
	
	Возврат ОписательВременнойТаблицыОтборов;
	
КонецФункции

Функция ЗапросВТКадровыеДанныеФизическихЛиц(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, КадровыеДанные, ПоляОтбораПериодическихДанных, ИсточникиДанных = Неопределено, ИмяВТКадровыеДанныеФизическихЛиц = "ВТКадровыеДанныеФизическихЛиц", ТаблицаОтборовСодержитПолеСотрудник = Ложь, УничтожитьВспомогательныеТаблицы = Истина) Экспорт
	
	Возврат КадровыйУчетВнутренний.ЗапросВТКадровыеДанныеФизическихЛиц(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, КадровыеДанные, ПоляОтбораПериодическихДанных, ИсточникиДанных, ИмяВТКадровыеДанныеФизическихЛиц, ТаблицаОтборовСодержитПолеСотрудник, УничтожитьВспомогательныеТаблицы);
	
КонецФункции

Функция ЗапросВТПостоянныеКадровыеДанныеФизическихЛиц(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, КадровыеДанные, ИсточникиДанных = Неопределено, ИмяВТПостоянныеКадровыеДанныеФизическихЛиц = "ВТПостоянныеКадровыеДанныеФизическихЛиц", ТаблицаОтборовСодержитПолеСотрудник = Ложь, ТаблицаОтборовСодержитПолеПериод = Ложь, УничтожитьВспомогательныеТаблицы = Истина) Экспорт
	
	Возврат КадровыйУчетВнутренний.ЗапросВТПостоянныеКадровыеДанныеФизическихЛиц(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, КадровыеДанные, ИсточникиДанных, ИмяВТПостоянныеКадровыеДанныеФизическихЛиц, ТаблицаОтборовСодержитПолеСотрудник, ТаблицаОтборовСодержитПолеПериод, УничтожитьВспомогательныеТаблицы);
	
КонецФункции

Функция ЗапросВТДокументыУдостоверяющиеЛичность(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИмяВТДокументыУдостоверяющиеЛичность = "ВТДокументыУдостоверяющиеЛичность") Экспорт
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТаблицаОтборов.Период КАК Период,
		|	ТаблицаОтборов.ФизическоеЛицо КАК ФизическоеЛицо,
		|	МАКСИМУМ(ДокументыФизическихЛиц.Период) КАК ПоследнийПериод
		|ПОМЕСТИТЬ ВТМаксимальныеПериодыДокументов
		|ИЗ
		|	ВТОтборовФизическихЛиц КАК ТаблицаОтборов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДокументыФизическихЛиц КАК ДокументыФизическихЛиц
		|		ПО ТаблицаОтборов.ФизическоеЛицо = ДокументыФизическихЛиц.Физлицо
		|			И (ДокументыФизическихЛиц.ЯвляетсяДокументомУдостоверяющимЛичность)
		|			И (ТаблицаОтборов.Период >= ДокументыФизическихЛиц.Период
		|				ИЛИ ТаблицаОтборов.Период = ДАТАВРЕМЯ(1, 1, 1))
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаОтборов.Период,
		|	ТаблицаОтборов.ФизическоеЛицо
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Период,
		|	ФизическоеЛицо,
		|	ПоследнийПериод
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	МаксимальныеПериодыДокументов.Период КАК Период,
		|	МаксимальныеПериодыДокументов.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ДокументыФизическихЛиц.Период КАК ПериодЗаписи,
		|	ДокументыФизическихЛиц.ВидДокумента КАК ДокументВид,
		|	ДокументыФизическихЛиц.ВидДокумента.КодМВД КАК ДокументКодМВД,
		|	ДокументыФизическихЛиц.Серия КАК ДокументСерия,
		|	ДокументыФизическихЛиц.Номер КАК ДокументНомер,
		|	ДокументыФизическихЛиц.ДатаВыдачи КАК ДокументДатаВыдачи,
		|	ДокументыФизическихЛиц.СрокДействия КАК ДокументСрокДействия,
		|	ДокументыФизическихЛиц.КемВыдан КАК ДокументКемВыдан,
		|	ДокументыФизическихЛиц.КодПодразделения КАК ДокументКодПодразделения,
		|	ДокументыФизическихЛиц.Представление КАК ДокументПредставление
		|ПОМЕСТИТЬ ВТДокументыУдостоверяющиеЛичность
		|ИЗ
		|	ВТМаксимальныеПериодыДокументов КАК МаксимальныеПериодыДокументов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДокументыФизическихЛиц КАК ДокументыФизическихЛиц
		|		ПО МаксимальныеПериодыДокументов.ПоследнийПериод = ДокументыФизическихЛиц.Период
		|			И МаксимальныеПериодыДокументов.ФизическоеЛицо = ДокументыФизическихЛиц.Физлицо
		|			И (ДокументыФизическихЛиц.ЯвляетсяДокументомУдостоверяющимЛичность)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ФизическоеЛицо,
		|	Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТМаксимальныеПериодыДокументов";
	
	ЗарплатаКадрыОбщиеНаборыДанных.УстановитьВыборкуТолькоРазрешенныхДанных(ТекстЗапроса, ТолькоРазрешенные);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТОтборовФизическихЛиц", ОписательВременнойТаблицыОтборов.ИмяВременнойТаблицыОтборовФизическихЛиц);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаОтборов.Период", "ТаблицаОтборов." + ОписательВременнойТаблицыОтборов.ИмяПоляПериод);
	УстановитьВТекстеЗапросаИмяПоляФизическоеЛицо(ТекстЗапроса, "ТаблицаОтборов.ФизическоеЛицо", ОписательВременнойТаблицыОтборов.ИмяПоляФизическоеЛицо);
	
	ЗарплатаКадрыОбщиеНаборыДанных.ЗаменитьИмяСоздаваемойВременнойТаблицы(ТекстЗапроса, "ВТДокументыУдостоверяющиеЛичность", ИмяВТДокументыУдостоверяющиеЛичность);
	
	Запрос.Текст = ТекстЗапроса;
	
	Возврат Запрос;
	
КонецФункции

Функция ЗапросВТСведенияОбИнвалидности(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИмяВТСведенияОбИнвалидности = "ВТСведенияОбИнвалидности") Экспорт
	
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
		ОписательВременнойТаблицыОтборов.ИмяВременнойТаблицыОтборовФизическихЛиц, "Период,ФизическоеЛицо");
		
	ОписаниеФильтра.СоответствиеИзмеренийРегистраИзмерениямФильтра.Вставить("Период", ОписательВременнойТаблицыОтборов.ИмяПоляПериод);
	ОписаниеФильтра.СоответствиеИзмеренийРегистраИзмерениямФильтра.Вставить("ФизическоеЛицо", ОписательВременнойТаблицыОтборов.ИмяПоляФизическоеЛицо);
	
	ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
	
	ПоляОтбора = Неопределено;
	Если ПоляОтбораПериодическихДанных <> Неопределено Тогда
		ПоляОтбораПериодическихДанных.Свойство("СведенияОбИнвалидностиФизическихЛиц", ПоляОтбора);
	КонецЕсли;
	
	ПараметрыПостроения.Отборы = ПоляОтбора;
	 
	ЗапросВТИмяРегистраСрез = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросВТИмяРегистраСрез(
		"СведенияОбИнвалидностиФизическихЛиц",
		ТолькоРазрешенные,
		ОписаниеФильтра,
		ПараметрыПостроения,
		Истина,
		ИмяВТСведенияОбИнвалидности);
			
	УстановитьПутьКПолюФизическоеЛицо(ЗапросВТИмяРегистраСрез.Текст, "ИзмеренияДаты", ОписательВременнойТаблицыОтборов.ИмяПоляФизическоеЛицо);
	
	Возврат ЗапросВТИмяРегистраСрез;
	
КонецФункции

Функция ЗапросВТСведенияОГражданстве(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИмяВТСведенияОГражданстве = "ВТСведенияОГражданстве") Экспорт
	
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
		ОписательВременнойТаблицыОтборов.ИмяВременнойТаблицыОтборовФизическихЛиц, "Период,ФизическоеЛицо");
	
	ОписаниеФильтра.СоответствиеИзмеренийРегистраИзмерениямФильтра.Вставить("Период", ОписательВременнойТаблицыОтборов.ИмяПоляПериод);
	ОписаниеФильтра.СоответствиеИзмеренийРегистраИзмерениямФильтра.Вставить("ФизическоеЛицо", ОписательВременнойТаблицыОтборов.ИмяПоляФизическоеЛицо);
	
	ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
	
	ПоляОтбора = Неопределено;
	Если ПоляОтбораПериодическихДанных <> Неопределено Тогда
		ПоляОтбораПериодическихДанных.Свойство("ГражданствоФизическихЛиц", ПоляОтбора);
	КонецЕсли;
	
	ПараметрыПостроения.Отборы = ПоляОтбора;
	
	ЗапросВТИмяРегистраСрез = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросВТИмяРегистраСрез(
		"ГражданствоФизическихЛиц",
		ТолькоРазрешенные,
		ОписаниеФильтра,
		ПараметрыПостроения,
		Истина,
		ИмяВТСведенияОГражданстве);
	
	УстановитьПутьКПолюФизическоеЛицо(ЗапросВТИмяРегистраСрез.Текст, "ИзмеренияДаты", ОписательВременнойТаблицыОтборов.ИмяПоляФизическоеЛицо);
	
	Возврат ЗапросВТИмяРегистраСрез;
	
КонецФункции

Функция ЗапросВТСведенияОФИОФизическихЛиц(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИмяВТСведенияОФИОФизическихЛиц = "ВТСведенияОФИОФизическихЛиц") Экспорт
	
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
		ОписательВременнойТаблицыОтборов.ИмяВременнойТаблицыОтборовФизическихЛиц, "Период,ФизическоеЛицо");
	
	ОписаниеФильтра.СоответствиеИзмеренийРегистраИзмерениямФильтра.Вставить("Период", ОписательВременнойТаблицыОтборов.ИмяПоляПериод);
	ОписаниеФильтра.СоответствиеИзмеренийРегистраИзмерениямФильтра.Вставить("ФизическоеЛицо", ОписательВременнойТаблицыОтборов.ИмяПоляФизическоеЛицо);
	
	ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
	ПараметрыПостроения.ИспользоватьРасширениеЯзыкаЗапросовДляСКД = Ложь;
	
	ПоляОтбора = Неопределено;
	Если ПоляОтбораПериодическихДанных <> Неопределено Тогда
		ПоляОтбораПериодическихДанных.Свойство("ФИОФизическихЛиц", ПоляОтбора);
	КонецЕсли;
	
	ПараметрыПостроения.Отборы = ПоляОтбора;
	
	ЗапросВТИмяРегистраСрез = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросВТИмяРегистраСрез(
		"ФИОФизическихЛиц",
		ТолькоРазрешенные,
		ОписаниеФильтра,
		ПараметрыПостроения,
		Истина,
		ИмяВТСведенияОФИОФизическихЛиц);
	
	УстановитьПутьКПолюФизическоеЛицо(ЗапросВТИмяРегистраСрез.Текст, "ИзмеренияДаты", ОписательВременнойТаблицыОтборов.ИмяПоляФизическоеЛицо);
	
	Возврат ЗапросВТИмяРегистраСрез;
	
КонецФункции

#КонецОбласти


#Область КадровыеДанныеСотрудников

// Возвращает структуру, описывающую временную таблицу отборов физических лиц.
//
// Параметры:
//				ИмяВременнойТаблицыОтборовСотрудников - Строка, имя временной таблицы
//								в менеджере запросов.
//				ИмяПоляСотрудник - Строка, указывает имя поля временной таблицы,
//								содержащего ссылку на сотрудника.
//				ИмяПоляПериод - Строка, указывает имя поля временной таблицу, содержащее
//								период, на который предполагается получить кадровые данные.
//
Функция ОписаниеВременнойТаблицыОтборовСотрудников(ИмяВременнойТаблицыОтборовСотрудников, ИмяПоляСотрудник = "Сотрудник", ИмяПоляПериод = "Период") Экспорт
	
	ОписательВременнойТаблицыОтборов = Новый Структура;
	ОписательВременнойТаблицыОтборов.Вставить("ИмяВременнойТаблицыОтборовСотрудников", ИмяВременнойТаблицыОтборовСотрудников);
	ОписательВременнойТаблицыОтборов.Вставить("ИмяПоляСотрудник", ИмяПоляСотрудник);
	ОписательВременнойТаблицыОтборов.Вставить("ИмяПоляПериод", ИмяПоляПериод);
	ОписательВременнойТаблицыОтборов.Вставить("ИмяВременнойТаблицыОтборовРазличныхСотрудников", "");
	ОписательВременнойТаблицыОтборов.Вставить("ПолучатьСведенияНаПериодНачалаУчета", Ложь);
	ОписательВременнойТаблицыОтборов.Вставить("ИменаСозданныхВременныхТаблиц", Новый Структура);
	
	ДополнитьСтруктуруПараметровИменамиТаблицСКадровойИсториейСотрудников(ОписательВременнойТаблицыОтборов);
	
	Возврат ОписательВременнойТаблицыОтборов;
	
КонецФункции

Функция ЗапросВТКадровыеДанныеСотрудников(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, КадровыеДанные, ПоляОтбораПериодическихДанных = Неопределено, ФормироватьСПериодичностьДень = Истина, ИсточникиДанных = Неопределено, ИмяВТКадровыеДанныеСотрудников = "ВТКадровыеДанныеСотрудников", ТаблицаОтборовСодержитПолеФизическоеЛицо = Ложь, УничтожитьВспомогательныеТаблицы = Истина) Экспорт
	
	Возврат КадровыйУчетВнутренний.ЗапросВТКадровыеДанныеСотрудников(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, КадровыеДанные, ПоляОтбораПериодическихДанных, ФормироватьСПериодичностьДень, ИсточникиДанных, ИмяВТКадровыеДанныеСотрудников, ТаблицаОтборовСодержитПолеФизическоеЛицо, УничтожитьВспомогательныеТаблицы);
	
КонецФункции

Функция ЗапросВТПостоянныеКадровыеДанныеСотрудников(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, КадровыеДанные, ИсточникиДанных = Неопределено, ИмяВТПостоянныеКадровыеДанныеСотрудников = "ВТПостоянныеКадровыеДанныеСотрудников", ТаблицаОтборовСодержитПолеПериод = Ложь, ТаблицаОтборовСодержитПолеФизическоеЛицо = Ложь, УничтожитьВспомогательныеТаблицы = Истина) Экспорт

	Возврат КадровыйУчетВнутренний.ЗапросВТПостоянныеКадровыеДанныеСотрудников(ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, КадровыеДанные, ИсточникиДанных, ИмяВТПостоянныеКадровыеДанныеСотрудников, ТаблицаОтборовСодержитПолеПериод, ТаблицаОтборовСодержитПолеФизическоеЛицо, УничтожитьВспомогательныеТаблицы);
	
КонецФункции

Процедура ДобавитьВЗапросСоединенияСИсточникамиПостоянныхКадровыхДанныхСотрудников(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных, ФормироватьТаблицуРазличных) Экспорт
	
	КадровыйУчетВнутренний.ДобавитьВЗапросСоединенияСИсточникамиПостоянныхКадровыхДанныхСотрудников(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных, ФормироватьТаблицуРазличных);
	
КонецПроцедуры

Функция ТекстыОписанияПолейЗапросПоляПостоянныхКадровыхДанныхСотрудников(КадровыеДанные, ИсточникиДанных) Экспорт
	
	Возврат КадровыйУчетВнутренний.ТекстыОписанияПолейЗапросПоляПостоянныхКадровыхДанныхСотрудников(КадровыеДанные, ИсточникиДанных);
	
КонецФункции

#КонецОбласти


#Область ОбслуживаниеОтображенияСведенийОГражданствеДляЛицБезГражданства

Процедура СоздатьВТСтраныМираДляГражданства(МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтраныМира.Ссылка КАК Страна,
	|	СтраныМира.Код,
	|	СтраныМира.Наименование
	|ПОМЕСТИТЬ ВТСтраныМираДляГражданства
	|ИЗ
	|	Справочник.СтраныМира КАК СтраныМира
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка),
	|	""999"",
	|	""Лицо без гражданства""
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СтраныМира.Ссылка";
	
	Запрос.Выполнить();

КонецПроцедуры

#КонецОбласти


#Область ФормированиеВременныхТаблицСКадровымиДанными

// Формирует временную таблицу ВТКадровыеДанныеФизическихЛиц, содержащую кадровые данные физических лиц,
// переданных ей в качестве параметра на указанную дату.
//
// Параметры:
//		МенеджерВременныхТаблиц - МенеджерВременныхТаблиц в котором формируется временная таблица.
//		ТолькоРазрешенные - Булево
//		СписокФизическихЛиц - Массив ссылок или ссылка на элемент справочника ФизическиеЛица.
//		КадровыеДанные - см. описание к функции КадровыеДанныеФизическихЛиц.
//		ДатаПолученияДанных - Дата, на которую необходимо получить "периодические" данные.
//		ПоляОтбораПериодическихДанных -  см. описание к функции КадровыеДанныеФизическихЛиц.
// 
Процедура СоздатьНаДатуВТКадровыеДанныеФизическихЛиц(МенеджерВременныхТаблиц, ТолькоРазрешенные, СписокФизическихЛиц, КадровыеДанные, Знач ДатаПолученияДанных, ПоляОтбораПериодическихДанных = Неопределено) Экспорт
	
	// Проверим Передана ли нам дата на которую необходимо получать периодические данные.
	Если ДатаПолученияДанных = '00010101000000' Тогда
		ДатаПолученияДанных = ТекущаяДатаСеанса();
	КонецЕсли;
	
	// Проверим, если в качестве параметра СписокФизическихЛиц, передана ссылка, создадим массив.
	Если ТипЗнч(СписокФизическихЛиц) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		МассивФизическихЛиц = Новый Массив;
		МассивФизическихЛиц.Добавить(СписокФизическихЛиц);
	Иначе
		МассивФизическихЛиц = СписокФизическихЛиц;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("МассивФизическихЛиц", МассивФизическихЛиц);
	Запрос.УстановитьПараметр("ДатаПолученияДанных", ДатаПолученияДанных);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	&ДатаПолученияДанных КАК Период,
		|	ФизическиеЛица.Ссылка КАК ФизическоеЛицо
		|ПОМЕСТИТЬ ВТСписокФизическихЛиц
		|ИЗ
		|	Справочник.ФизическиеЛица КАК ФизическиеЛица
		|ГДЕ
		|	ФизическиеЛица.Ссылка В(&МассивФизическихЛиц)";
	
	Если НЕ ТолькоРазрешенные Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВЫБРАТЬ РАЗРЕШЕННЫЕ", "ВЫБРАТЬ");
	КонецЕсли;
	
	Запрос.Выполнить();
	
	ОписательВременныхТаблиц = ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеФизическихЛиц(
		МенеджерВременныхТаблиц, "ВТСписокФизическихЛиц");
	
	СоздатьВТКадровыеДанныеФизическихЛиц(ОписательВременныхТаблиц, ТолькоРазрешенные, КадровыеДанные, ПоляОтбораПериодическихДанных);
	
	Запрос.Текст = "УНИЧТОЖИТЬ ВТСписокФизическихЛиц";
	Запрос.Выполнить();
	
КонецПроцедуры

// Возвращает структуру описания полей отбора для методов получения кадровых данных физических лиц.
//
// Параметры:
//		ИменаПолейВременнойТаблицыОтборовФизическихЛиц - строка с именами полей отбора
//							физических лиц ",". Строка должна содержать два имени поля, разделенные
//							запятой. Первое имя соответствует полю "ФизическоеЛицо", второе полю "Период".
//
// Возвращаемое значение:
//		Структура содержащая имена полей отбора сотрудников
//			 КлючиСтруктуры:
//			 	ФизическоеЛицо
//				Период
//
Функция ПоляОтбораФизическихЛиц(ИменаПолейВременнойТаблицыОтборовФизическихЛиц = "ФизическоеЛицо,Период") Экспорт
	
	МассивИменПолейОтбораФизическихЛиц = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИменаПолейВременнойТаблицыОтборовФизическихЛиц, ",", , Истина);
	Возврат Новый Структура("ФизическоеЛицо,Период", МассивИменПолейОтбораФизическихЛиц[0], МассивИменПолейОтбораФизическихЛиц[1]);
	
КонецФункции

// Возвращает структуру с параметрами для метода СоздатьВТКадровыеДанныеФизическихЛиц.
//
// Параметры:
//		МенеджерВременныхТаблиц
//		ИмяВременнойТаблицыОтборовФизическихЛиц - имя временной таблицы - "фильтра".
//		ИменаПолейВременнойТаблицыОтборовФизическихЛиц - строка с именами полей отбора
//							физических лиц ",". Строка должна содержать два имени поля, разделенные
//							запятой. Первое имя соответствует полю "ФизическоеЛицо", второе полю "Период".
//
// Возвращаемое значение:
//		Структура с полями:
//			МенеджерВременныхТаблиц
//			ИмяВременнойТаблицыОтборовФизическихЛиц - имя временной таблицы отборов физических лиц.
//			ИмяВТКадровыеДанныеФизическихЛиц - имя создаваемой временной таблицы.
//			ПоляОтбораФизическихЛиц - см. ПоляОтбораФизическихЛиц.
//
Функция ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеФизическихЛиц(МенеджерВременныхТаблиц, ИмяВременнойТаблицыОтборовФизическихЛиц, ИменаПолейВременнойТаблицыОтборовФизическихЛиц = "ФизическоеЛицо,Период") Экспорт
	
	ОписательВременныхТаблиц = Новый Структура;
	
	ОписательВременныхТаблиц.Вставить("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц);
	ОписательВременныхТаблиц.Вставить("ИмяВременнойТаблицыОтборовФизическихЛиц", ИмяВременнойТаблицыОтборовФизическихЛиц);
	ОписательВременныхТаблиц.Вставить("ИмяВТКадровыеДанныеФизическихЛиц", "ВТКадровыеДанныеФизическихЛиц");
	ОписательВременныхТаблиц.Вставить("ПоляОтбораФизическихЛиц", ПоляОтбораФизическихЛиц(ИменаПолейВременнойТаблицыОтборовФизическихЛиц));
	
	Возврат ОписательВременныхТаблиц;
	
КонецФункции

// Формирует временную таблицу ВТКадровыеДанныеФизическихЛиц, список физических лиц и периодов,
// по которым необходимо получить данные, берутся из временной таблицы в менеджере временных
// таблиц, переданном в качестве параметра. Временная таблица обязательно должна содержать
// колонки ФизическоеЛицо и Период.
//
// Параметры:
//		ОписательВременныхТаблиц - структура, сформированная 
//				методом ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеФизическихЛиц.
//		ТолькоРазрешенные - Булево
//		КадровыеДанные - см. описание к функции КадровыеДанныеФизическихЛиц.
//		ПоляОтбораПериодическихДанных -  см. описание к функции КадровыеДанныеФизическихЛиц.
//
Процедура СоздатьВТКадровыеДанныеФизическихЛиц(ОписательВременныхТаблиц, ТолькоРазрешенные, КадровыеДанные, ПоляОтбораПериодическихДанных = Неопределено) Экспорт
	
	Описатель = ОписаниеВременнойТаблицыОтборовФизическихЛиц(
		ОписательВременныхТаблиц.ИмяВременнойТаблицыОтборовФизическихЛиц,
		ОписательВременныхТаблиц.ПоляОтбораФизическихЛиц.ФизическоеЛицо,
		ОписательВременныхТаблиц.ПоляОтбораФизическихЛиц.Период);
	
	Запрос = ЗапросВТКадровыеДанныеФизическихЛиц(
		ТолькоРазрешенные,
		Описатель,
		КадровыеДанные,
		ПоляОтбораПериодическихДанных,
		,
		ОписательВременныхТаблиц.ИмяВТКадровыеДанныеФизическихЛиц);
	
	Запрос.МенеджерВременныхТаблиц = ОписательВременныхТаблиц.МенеджерВременныхТаблиц;
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Формирует временную таблицу ВТКадровыеДанныеСотрудников, содержащую кадровые данные сотрудников,
// переданных ей в качестве параметра на указанную дату.
//
// Параметры:
//		МенеджерВременныхТаблиц - МенеджерВременныхТаблиц в котором формируется временная таблица.
//		ТолькоРазрешенные - Булево
//		СписокСотрудников - Массив ссылок или ссылка на элемент справочника Сотрудники.
//		КадровыеДанные - см. описание к функции КадровыеДанныеСотрудников.
//		ДатаПолученияДанных - Дата, на которую необходимо получить "периодические" данные.
//		ПоляОтбораПериодическихДанных -  см. описание к функции КадровыеДанныеСотрудников.
// 
Процедура СоздатьНаДатуВТКадровыеДанныеСотрудников(МенеджерВременныхТаблиц, ТолькоРазрешенные, СписокСотрудников, КадровыеДанные, Знач ДатаПолученияДанных, ПоляОтбораПериодическихДанных = Неопределено, ФормироватьСПериодичностьДень = Истина) Экспорт
	
	// Проверим, если в качестве параметра СписокФизическихЛиц, передана ссылка, создадим массив.
	Если ТипЗнч(СписокСотрудников) = Тип("СправочникСсылка.Сотрудники") Тогда
		МассивСотрудников = Новый Массив;
		МассивСотрудников.Добавить(СписокСотрудников);
	Иначе
		МассивСотрудников = СписокСотрудников;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("МассивСотрудников", МассивСотрудников);
	Запрос.УстановитьПараметр("ДатаПолученияДанных", ДатаПолученияДанных);
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&ДатаПолученияДанных КАК Период,
		|	ДанныеСотрудников.Ссылка КАК Сотрудник
		|ПОМЕСТИТЬ ВТСписокСотрудников
		|ИЗ
		|	Справочник.Сотрудники КАК ДанныеСотрудников
		|ГДЕ
		|	ДанныеСотрудников.Ссылка В(&МассивСотрудников)";
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	ОписательВременныхТаблиц = ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
		МенеджерВременныхТаблиц, "ВТСписокСотрудников");
	
	СоздатьВТКадровыеДанныеСотрудников(ОписательВременныхТаблиц, ТолькоРазрешенные, КадровыеДанные, ПоляОтбораПериодическихДанных, ФормироватьСПериодичностьДень);
	
	Запрос.Текст = "УНИЧТОЖИТЬ ВТСписокСотрудников";
	Запрос.Выполнить();
	
КонецПроцедуры

// Возвращает структуру описания полей отбора для методов получения кадровых данных сотрудников.
//
// Параметры:
//		ИменаПолейВременнойТаблицыОтборовСотрудников - строка с именами полей отбора
//							сотрудников ",". Строка должна содержать два имени поля, разделенные
//							запятой. Первое имя соответствует полю "Сотрудник", второе полю "Период".
//
// Возвращаемое значение:
//		Структура содержащая имена полей отбора сотрудников
//			 КлючиСтруктуры:
//			 	Сотрудник
//				Период
//
Функция ПоляОтбораСотрудников(ИменаПолейВременнойТаблицыОтборовСотрудников = "Сотрудник,Период") Экспорт
	
	МассивИменПолейОтбораСотрудников = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИменаПолейВременнойТаблицыОтборовСотрудников, ",", , Истина);
	Возврат Новый Структура("Сотрудник,Период", МассивИменПолейОтбораСотрудников[0], МассивИменПолейОтбораСотрудников[1]);
	
КонецФункции

// Возвращает структуру с параметрами для метода СоздатьВТКадровыеДанныеСотрудников.
//
// Параметры:
//		МенеджерВременныхТаблиц
//		ИмяВременнойТаблицыОтборовСотрудников - имя временной таблицы - "фильтра".
//		ИменаПолейВременнойТаблицыОтборовСотрудников - строка с именами полей отбора
//							сотрудников ",". Строка должна содержать два имени поля, разделенные
//							запятой. Первое имя соответствует полю "Сотрудник", второе полю "Период".
//
// Возвращаемое значение:
//		Структура с полями:
//			МенеджерВременныхТаблиц
//			ИмяВременнойТаблицыОтборовСотрудников - имя временной таблицы отборов сотрудников.
//			ПоляОтбораСотрудников - см. ПоляОтбораСотрудников.
//
Функция ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(МенеджерВременныхТаблиц, ИмяВременнойТаблицыОтборовСотрудников, ИменаПолейВременнойТаблицыОтборовСотрудников = "Сотрудник,Период") Экспорт
	
	ОписательВременныхТаблиц = Новый Структура;
	
	ОписательВременныхТаблиц.Вставить("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц);
	ОписательВременныхТаблиц.Вставить("ИмяВременнойТаблицыОтборовСотрудников", ИмяВременнойТаблицыОтборовСотрудников);
	ОписательВременныхТаблиц.Вставить("ПоляОтбораСотрудников", ПоляОтбораСотрудников(ИменаПолейВременнойТаблицыОтборовСотрудников));
	ОписательВременныхТаблиц.Вставить("ИмяВТКадровыеДанныеСотрудников", "ВТКадровыеДанныеСотрудников");
	ОписательВременныхТаблиц.Вставить("ИмяВременнойТаблицыОтборовРазличныхСотрудников", "");
	
	ДополнитьСтруктуруПараметровИменамиТаблицСКадровойИсториейСотрудников(ОписательВременныхТаблиц);
	
	Возврат ОписательВременныхТаблиц;
	
КонецФункции

// Формирует временную таблицу ВТКадровыеДанныеСотрудников, список сотрудников и периодов,
// по которым необходимо получить данные, берутся из временной таблицы в менеджере временных
// таблиц, переданном в качестве параметра. Временная таблица обязательно должна содержать
// колонки Сотрудник и Период.
//
// Параметры:
//		ОписательВременныхТаблиц - структура, сформированная 
//				методом ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников.
//		ТолькоРазрешенные - Булево
//		КадровыеДанные - см. описание к функции КадровыеДанныеСотрудников.
//		ПоляОтбораПериодическихДанных -  см. описание к функции КадровыеДанныеСотрудников.
//
Процедура СоздатьВТКадровыеДанныеСотрудников(ОписательВременныхТаблиц, ТолькоРазрешенные, КадровыеДанные, ПоляОтбораПериодическихДанных = Неопределено, ФормироватьСПериодичностьДень = Истина) Экспорт
	
	ОписательВременнойТаблицыОтборов = ОписаниеВременнойТаблицыОтборовСотрудников(
		ОписательВременныхТаблиц.ИмяВременнойТаблицыОтборовСотрудников,
		ОписательВременныхТаблиц.ПоляОтбораСотрудников.Сотрудник,
		ОписательВременныхТаблиц.ПоляОтбораСотрудников.Период);
	
	ИсточникиДанных = Новый Соответствие;
	
	ПроверитьНеобходимостьПолученияСведенийНаПериодНачалаУчета(
		ОписательВременныхТаблиц.МенеджерВременныхТаблиц, ОписательВременнойТаблицыОтборов, ТолькоРазрешенные,
		КадровыеДанные, ПоляОтбораПериодическихДанных, ФормироватьСПериодичностьДень, ИсточникиДанных);
	
	Запрос = ЗапросВТКадровыеДанныеСотрудников(
		ТолькоРазрешенные,
		ОписательВременнойТаблицыОтборов,
		КадровыеДанные,
		ПоляОтбораПериодическихДанных,
		ФормироватьСПериодичностьДень,
		ИсточникиДанных,
		ОписательВременныхТаблиц.ИмяВТКадровыеДанныеСотрудников);
	
	Запрос.МенеджерВременныхТаблиц = ОписательВременныхТаблиц.МенеджерВременныхТаблиц;
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Формирует временную таблицу ВТФизическиеЛицаРаботавшиеВОрганизации со списком физических лиц, работавших в указанных
// организации, подразделении и в заданном периоде. Если подразделение не указывать в список будут отобраны физические
// лица работавшие в организации независимо от подразделения.
// Временная таблица содержит поля ФизическоеЛицо и Период. Значение поля Период всегда равно значению параметра
// ОкончаниеПериода.
//
// Параметры:
//		МенеджерВременныхТаблиц	- МенеджерВременныхТаблиц
//		ТолькоРазрешенные 		- Булево
//		Организация				- СправочникСсылка.Организации
//		НачалоПериода 			- Дата
//		ОкончаниеПериода 		- Дата
//		Подразделение			- СправочникСсылка.Подразделения
//		ОтбиратьПоГоловнойОрганизации - Булево
//		Отборы					- Массив, структур описывающих отборы сотрудников
//									описание структуры см. ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор,
//									допустимые отборы описаны см. КадровыйУчет.КритерииПоискаСотрудниковПоКоллекцииОтборов
//		ИмяВТФизическиеЛицаРаботавшиеВОрганизации - Строка
//
Процедура СоздатьВТФизическиеЛицаРаботавшиеВОрганизации(МенеджерВременныхТаблиц, ТолькоРазрешенные, Организация, Знач НачалоПериода, Знач ОкончаниеПериода, Подразделение = Неопределено, ОтбиратьПоГоловнойОрганизации = Ложь, Отборы = Неопределено, ИмяВТФизическиеЛицаРаботавшиеВОрганизации = "ВТФизическиеЛицаРаботавшиеВОрганизации") Экспорт
	
	ПараметрыПолученияСотрудниковОрганизаций = ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	
	Если ОтбиратьПоГоловнойОрганизации Тогда
		ПараметрыПолученияСотрудниковОрганизаций.Организация = ЗарплатаКадрыПовтИсп.ГоловнаяОрганизация(Организация);
	Иначе
		ПараметрыПолученияСотрудниковОрганизаций.Организация = Организация;
	КонецЕсли;
	
	ПараметрыПолученияСотрудниковОрганизаций.Подразделение 					= Подразделение;
	ПараметрыПолученияСотрудниковОрганизаций.ОтбиратьПоГоловнойОрганизации	= ОтбиратьПоГоловнойОрганизации;
	ПараметрыПолученияСотрудниковОрганизаций.НачалоПериода					= НачалоПериода;
	ПараметрыПолученияСотрудниковОрганизаций.ОкончаниеПериода				= ОкончаниеПериода;
	
	Если Отборы <> Неопределено Тогда
		ПараметрыПолученияСотрудниковОрганизаций.Отборы = Отборы;
	КонецЕсли;
	
	СоздатьВТСотрудникиОрганизации(МенеджерВременныхТаблиц, ТолькоРазрешенные, ПараметрыПолученияСотрудниковОрганизаций);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВЫРАЗИТЬ(СотрудникиОрганизации.Сотрудник КАК Справочник.Сотрудники).ФизическоеЛицо КАК ФизическоеЛицо
		|ПОМЕСТИТЬ ВТФизическиеЛицаРаботавшиеВОрганизации
		|ИЗ
		|	ВТСотрудникиОрганизации КАК СотрудникиОрганизации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТСотрудникиОрганизации";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТФизическиеЛицаРаботавшиеВОрганизации", ИмяВТФизическиеЛицаРаботавшиеВОрганизации);
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Формирует временную таблицу ВТСотрудникиОрганизации, содержащую поля Сотрудник и ФизическоеЛицо.
// Если заданы параметры НачалоПериода и ОкончаниеПериода метод вернет список сотрудников, работавших
// в заданном периоде. Если НачалоПериода и ОкончаниеПериода заданы и равны сформирует таблицу.
// Сотрудников работавших на заданную дату.
//
// Параметры:
//				МенеджерВременныхТаблиц
//				ТолькоРазрешенные - Булево
//				Параметры - Структура, подготовленная с помощью метода ПараметрыПолученияСотрудниковОрганизаций.
//				ИмяВТСотрудникиОрганизации - Строка
//
Процедура СоздатьВТСотрудникиОрганизации(МенеджерВременныхТаблиц, ТолькоРазрешенные, Параметры, ИмяВТСотрудникиОрганизации = "ВТСотрудникиОрганизации") Экспорт
	
	Если ПривилегированныйРежим() Тогда
		
		Запрос = ЗапросВТСотрудникиОрганизации(
			ТолькоРазрешенные,
			ИмяВТСотрудникиОрганизации,
			Параметры,
			МенеджерВременныхТаблиц);
		
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
	Иначе
		
		ПараметрыПолученияСотрудников = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(Параметры);
		
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
			Модуль = ОбщегоНазначения.ОбщийМодуль("УправленческаяЗарплата");
			Модуль.УточнитьПараметрыПолученияСотрудников(ПараметрыПолученияСотрудников);
		КонецЕсли;
		
		ИмяВТСотрудникиПериоды = "ВТСотрудникиПериодыДляПолученияСотрудниковОрганизации";
		Запрос = КадровыйУчетВнутренний.ЗапросВТОтборСотрудниковОрганизации(
			ТолькоРазрешенные, ПараметрыПолученияСотрудников, ИмяВТСотрудникиПериоды);
		
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
		Запрос = КадровыйУчетВнутренний.ЗапросВТСотрудникиОрганизацииПоРабочимМестам(
			ТолькоРазрешенные, ИмяВТСотрудникиОрганизации, ИмяВТСотрудникиПериоды, ПараметрыПолученияСотрудников, МенеджерВременныхТаблиц);
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
		УстановитьПривилегированныйРежим(Истина);
		Запрос.Выполнить();
		УстановитьПривилегированныйРежим(Ложь);
		
		Запрос.Текст = "";
		ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВЗапросСозданияВременнойТаблицыТекстУничтоженияВспомогательной(
			Запрос.Текст, ИмяВТСотрудникиПериоды);
		
		Запрос.Выполнить();
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает структуру параметров получения основных сотрудников организации.
//
// Параметры:
//				СписокФизическихЛиц - Массив ссылок на элементы справочника ФизическиеЛица или 
//					СправочникСсылка.ФизическиеЛица, если параметр опустить, в таблицу войдут
//					основные сотрудники всех физических лиц когда либо работавших в организации.
//				НачалоПериода - Дата
//				ОкончаниеПериода - Дата, если ОкончаниеПериода меньше НачалоПериода, данные будут 
//					получены на дату НачалоПериода.
//				Организация - СправочникСсылка.Организации
//
Функция ПараметрыПолученияОсновныхСотрудниковФизическихЛиц(СписокФизическихЛиц, Организация, НачалоПериода, ОкончаниеПериода)
	
	Возврат КадровыйУчетВнутренний.ПараметрыПолученияОсновныхСотрудниковФизическихЛиц(СписокФизическихЛиц, Организация, НачалоПериода, ОкончаниеПериода);
	
КонецФункции

// Формирует временную таблицу содержащую поля ФизическоеЛицо и Сотрудник. Сотрудник отбирается по принципам:
//		- Работает на указанный период.
//		- Выбирается сотрудник, работающий по основному месту работы, если такового не существует, выбирается
//			сотрудник, работающий по совместительству, принятый на наиболее раннюю дату.
//
// Параметры:
//		МенеджерВременныхТаблиц - МенеджерВременныхТаблиц
//		ТолькоРазрешенные		- Булево
//		СписокФизическихЛиц		- Массив ссылок на элементы справочника ФизическиеЛица или 
//								- СправочникСсылка.ФизическиеЛица
//								- Неопределено - в таблицу войдут "основные" сотрудники всех
//									физических лиц когда либо работавших в организации.
//		НачалоПериода			- Дата
//		ОкончаниеПериода		- Дата, если ОкончаниеПериода меньше НачалоПериода, данные будут 
//									получены на дату НачалоПериода.
//		Организация				- СправочникСсылка.Организации
//
Процедура СоздатьВТОсновныеСотрудникиФизическихЛиц(МенеджерВременныхТаблиц, ТолькоРазрешенные, СписокФизическихЛиц, Организация, НачалоПериода, ОкончаниеПериода = '00010101') Экспорт
	
	ПараметрыПолученияСотрудниковОрганизаций = ПараметрыПолученияОсновныхСотрудниковФизическихЛиц(СписокФизическихЛиц, Организация, НачалоПериода, ОкончаниеПериода);
	ПараметрыОтбораПоОрганизациям = ПараметрыОтбораПоОрганизациям(ПараметрыПолученияСотрудниковОрганизаций);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("СписокФизическихЛиц", СписокФизическихЛиц);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Сотрудники.Ссылка КАК Сотрудник,
		|	Сотрудники.ГоловнаяОрганизация,
		|	Сотрудники.ФизическоеЛицо
		|ПОМЕСТИТЬ ВТФизическиеЛицаДляПолученияСотрудниковОрганизаций
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.ФизическоеЛицо В(&СписокФизическихЛиц)
		|	И Сотрудники.ГоловнаяОрганизация В(&ГоловныеОрганизации)";
		
	Если ЗначениеЗаполнено(ПараметрыОтбораПоОрганизациям.ГоловныеОрганизации) Тогда
		Запрос.УстановитьПараметр("ГоловныеОрганизации", ПараметрыОтбораПоОрганизациям.ГоловныеОрганизации);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И Сотрудники.ГоловнаяОрганизация В(&ГоловныеОрганизации)", "");
	КонецЕсли;
	
	Запрос.Выполнить();
	
	УстановитьПривилегированныйРежим(Истина);
	СоздатьВТСотрудникиОрганизации(Запрос.МенеджерВременныхТаблиц, ТолькоРазрешенные, ПараметрыПолученияСотрудниковОрганизаций);
	УстановитьПривилегированныйРежим(Ложь);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДАТАВРЕМЯ(1, 1, 1) КАК Период,
		|	ФизическиеЛица.Сотрудник,
		|	ФизическиеЛица.ГоловнаяОрганизация,
		|	ФизическиеЛица.ФизическоеЛицо
		|ПОМЕСТИТЬ ВТДругиеСотрудники
		|ИЗ
		|	ВТФизическиеЛицаДляПолученияСотрудниковОрганизаций КАК ФизическиеЛица
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСотрудникиОрганизации КАК СотрудникиОрганизации
		|		ПО ФизическиеЛица.Сотрудник = СотрудникиОрганизации.Сотрудник
		|ГДЕ
		|	СотрудникиОрганизации.Сотрудник ЕСТЬ NULL 
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТФизическиеЛицаДляПолученияСотрудниковОрганизаций
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДругиеСотрудники.Сотрудник
		|ИЗ
		|	ВТДругиеСотрудники КАК ДругиеСотрудники";
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("ОкончаниеПериода", ОкончаниеПериода);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		ОписательТаблиц = ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
			Запрос.МенеджерВременныхТаблиц, "ВТДругиеСотрудники");
		
		ОписательТаблиц.ИмяВТКадровыеДанныеСотрудников = "ВТКадровыеДанныеОсновныхСотрудников";
		
		СоздатьВТКадровыеДанныеСотрудников(ОписательТаблиц, Истина, "ГоловнаяОрганизация,ДатаПриема,ДатаЗавершенияРаботы,ТекущийВидЗанятости");
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	КадровыеДанныеСотрудников.Сотрудник,
			|	КадровыеДанныеСотрудников.ГоловнаяОрганизация,
			|	КадровыеДанныеСотрудников.ФизическоеЛицо,
			|	КадровыеДанныеСотрудников.ДатаПриема,
			|	КадровыеДанныеСотрудников.ДатаЗавершенияРаботы,
			|	КадровыеДанныеСотрудников.ТекущийВидЗанятости КАК ВидЗанятости
			|ПОМЕСТИТЬ ВТВсеСотрудники
			|ИЗ
			|	ВТКадровыеДанныеОсновныхСотрудников КАК КадровыеДанныеСотрудников
			|ГДЕ
			|	ВЫБОР
			|			КОГДА &ОкончаниеПериода = ДАТАВРЕМЯ(1, 1, 1)
			|					И КадровыеДанныеСотрудников.ДатаПриема > ДАТАВРЕМЯ(1, 1, 1)
			|				ТОГДА ИСТИНА
			|			КОГДА КадровыеДанныеСотрудников.ДатаПриема > ДАТАВРЕМЯ(1, 1, 1)
			|					И КадровыеДанныеСотрудников.ДатаПриема <= &ОкончаниеПериода
			|					И КадровыеДанныеСотрудников.ДатаПриема >= &НачалоПериода
			|				ТОГДА ИСТИНА
			|			КОГДА КадровыеДанныеСотрудников.ДатаПриема = ДАТАВРЕМЯ(1, 1, 1)
			|					И КадровыеДанныеСотрудников.ТекущийВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ПустаяСсылка)
			|				ТОГДА ИСТИНА
			|			ИНАЧЕ ЛОЖЬ
			|		КОНЕЦ
			|	И (КадровыеДанныеСотрудников.ДатаЗавершенияРаботы = ДАТАВРЕМЯ(1, 1, 1)
			|			ИЛИ КадровыеДанныеСотрудников.ДатаЗавершенияРаботы >= &НачалоПериода)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	СотрудникиОрганизации.Сотрудник,
			|	СотрудникиОрганизации.ГоловнаяОрганизация,
			|	СотрудникиОрганизации.ФизическоеЛицо,
			|	СотрудникиОрганизации.ДатаПриема,
			|	СотрудникиОрганизации.ДатаЗавершенияРаботы,
			|	СотрудникиОрганизации.ВидЗанятости
			|ИЗ
			|	ВТСотрудникиОрганизации КАК СотрудникиОрганизации
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ВТКадровыеДанныеОсновныхСотрудников
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ВТДругиеСотрудники
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ВТСотрудникиОрганизации";
		
		Запрос.Выполнить();
		
		ИмяВТСотрудникиОрганизации = "ВТВсеСотрудники";
		
	Иначе
		ИмяВТСотрудникиОрганизации = "ВТСотрудникиОрганизации";
	КонецЕсли; 
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СотрудникиОрганизации.ГоловнаяОрганизация,
		|	СотрудникиОрганизации.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ВЫБОР
		|		КОГДА СотрудникиОрганизации.ДатаЗавершенияРаботы = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ЛОЖЬ
		|		КОГДА СотрудникиОрганизации.ДатаЗавершенияРаботы <= &ОкончаниеПериода
		|				И &ОкончаниеПериода <> ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ИСТИНА
		|		КОГДА СотрудникиОрганизации.ДатаЗавершенияРаботы > ДАТАВРЕМЯ(1, 1, 1)
		|				И &ОкончаниеПериода = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Уволен,
		|	МИНИМУМ(СотрудникиОрганизации.ДатаПриема) КАК ДатаПриема
		|ПОМЕСТИТЬ ВТРанниеДатыПриемаСотрудниковПоОсновномуМестуРаботы
		|ИЗ
		|	ВТСотрудникиОрганизации КАК СотрудникиОрганизации
		|ГДЕ
		|	СотрудникиОрганизации.ВидЗанятости В (ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы), ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.Совместительство))
		|	И (СотрудникиОрганизации.ДатаЗавершенияРаботы = ДАТАВРЕМЯ(1, 1, 1)
		|			ИЛИ СотрудникиОрганизации.ДатаЗавершенияРаботы >= &НачалоПериода)
		|
		|СГРУППИРОВАТЬ ПО
		|	СотрудникиОрганизации.ГоловнаяОрганизация,
		|	СотрудникиОрганизации.ФизическоеЛицо,
		|	ВЫБОР
		|		КОГДА СотрудникиОрганизации.ДатаЗавершенияРаботы = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ЛОЖЬ
		|		КОГДА СотрудникиОрганизации.ДатаЗавершенияРаботы <= &ОкончаниеПериода
		|				И &ОкончаниеПериода <> ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ИСТИНА
		|		КОГДА СотрудникиОрганизации.ДатаЗавершенияРаботы > ДАТАВРЕМЯ(1, 1, 1)
		|				И &ОкончаниеПериода = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СотрудникиОрганизации.ГоловнаяОрганизация,
		|	СотрудникиОрганизации.ФизическоеЛицо КАК ФизическоеЛицо,
		|	МАКСИМУМ(СотрудникиОрганизации.Сотрудник) КАК Сотрудник
		|ПОМЕСТИТЬ ВТСотрудникиПоОсновномуМестуРаботы
		|ИЗ
		|	ВТСотрудникиОрганизации КАК СотрудникиОрганизации
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРанниеДатыПриемаСотрудниковПоОсновномуМестуРаботы КАК РанниеДатыПриема
		|		ПО СотрудникиОрганизации.ГоловнаяОрганизация = РанниеДатыПриема.ГоловнаяОрганизация
		|			И СотрудникиОрганизации.ФизическоеЛицо = РанниеДатыПриема.ФизическоеЛицо
		|			И СотрудникиОрганизации.ДатаПриема = РанниеДатыПриема.ДатаПриема
		|			И (НЕ РанниеДатыПриема.Уволен)
		|ГДЕ
		|	СотрудникиОрганизации.ВидЗанятости В (ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы), ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.Совместительство))
		|
		|СГРУППИРОВАТЬ ПО
		|	СотрудникиОрганизации.ГоловнаяОрганизация,
		|	СотрудникиОрганизации.ФизическоеЛицо
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СотрудникиОрганизации.ГоловнаяОрганизация,
		|	СотрудникиОрганизации.ФизическоеЛицо,
		|	МАКСИМУМ(СотрудникиОрганизации.Сотрудник)
		|ИЗ
		|	ВТСотрудникиОрганизации КАК СотрудникиОрганизации
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРанниеДатыПриемаСотрудниковПоОсновномуМестуРаботы КАК РанниеДатыПриема
		|		ПО СотрудникиОрганизации.ГоловнаяОрганизация = РанниеДатыПриема.ГоловнаяОрганизация
		|			И СотрудникиОрганизации.ФизическоеЛицо = РанниеДатыПриема.ФизическоеЛицо
		|			И СотрудникиОрганизации.ДатаПриема = РанниеДатыПриема.ДатаПриема
		|			И (РанниеДатыПриема.Уволен)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРанниеДатыПриемаСотрудниковПоОсновномуМестуРаботы КАК РанниеДатыПриемаРаботающих
		|		ПО СотрудникиОрганизации.ГоловнаяОрганизация = РанниеДатыПриемаРаботающих.ГоловнаяОрганизация
		|			И СотрудникиОрганизации.ФизическоеЛицо = РанниеДатыПриемаРаботающих.ФизическоеЛицо
		|			И (НЕ РанниеДатыПриемаРаботающих.Уволен)
		|ГДЕ
		|	СотрудникиОрганизации.ВидЗанятости В (ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы), ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.Совместительство))
		|	И РанниеДатыПриемаРаботающих.ФизическоеЛицо ЕСТЬ NULL 
		|
		|СГРУППИРОВАТЬ ПО
		|	СотрудникиОрганизации.ГоловнаяОрганизация,
		|	СотрудникиОрганизации.ФизическоеЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СотрудникиОрганизации.ГоловнаяОрганизация,
		|	СотрудникиОрганизации.Сотрудник КАК Сотрудник,
		|	СотрудникиОрганизации.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СотрудникиОрганизации.ВидЗанятости КАК ВидЗанятости,
		|	СотрудникиОрганизации.ДатаПриема КАК ДатаПриема
		|ПОМЕСТИТЬ ВТВсеСотрудникиПоСовместительству
		|ИЗ
		|	ВТСотрудникиОрганизации КАК СотрудникиОрганизации
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСотрудникиПоОсновномуМестуРаботы КАК СотрудникиПоОсновномуМестуРаботы
		|		ПО СотрудникиОрганизации.ФизическоеЛицо = СотрудникиПоОсновномуМестуРаботы.ФизическоеЛицо
		|			И СотрудникиОрганизации.ГоловнаяОрганизация = СотрудникиПоОсновномуМестуРаботы.ГоловнаяОрганизация
		|ГДЕ
		|	НЕ ЕСТЬNULL(СотрудникиОрганизации.ВидЗанятости, ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ПустаяСсылка)) В (ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы), ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.Совместительство))
		|	И СотрудникиПоОсновномуМестуРаботы.ФизическоеЛицо ЕСТЬ NULL 
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВсеСотрудникиПоСовместительству.ГоловнаяОрганизация,
		|	ВсеСотрудникиПоСовместительству.ФизическоеЛицо,
		|	МИНИМУМ(ВсеСотрудникиПоСовместительству.ДатаПриема) КАК ДатаПриема
		|ПОМЕСТИТЬ ВТРанниеДатыПриемаСовместителей
		|ИЗ
		|	ВТВсеСотрудникиПоСовместительству КАК ВсеСотрудникиПоСовместительству
		|
		|СГРУППИРОВАТЬ ПО
		|	ВсеСотрудникиПоСовместительству.ГоловнаяОрганизация,
		|	ВсеСотрудникиПоСовместительству.ФизическоеЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВсеСотрудникиПоСовместительству.ГоловнаяОрганизация,
		|	ВсеСотрудникиПоСовместительству.ФизическоеЛицо,
		|	МАКСИМУМ(ВсеСотрудникиПоСовместительству.Сотрудник) КАК Сотрудник
		|ПОМЕСТИТЬ ВТСотрудникиПоСовместительству
		|ИЗ
		|	ВТВсеСотрудникиПоСовместительству КАК ВсеСотрудникиПоСовместительству
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРанниеДатыПриемаСовместителей КАК РанниеДатыПриемаСовместителей
		|		ПО ВсеСотрудникиПоСовместительству.ФизическоеЛицо = РанниеДатыПриемаСовместителей.ФизическоеЛицо
		|			И ВсеСотрудникиПоСовместительству.ГоловнаяОрганизация = РанниеДатыПриемаСовместителей.ГоловнаяОрганизация
		|			И ВсеСотрудникиПоСовместительству.ДатаПриема = РанниеДатыПриемаСовместителей.ДатаПриема
		|
		|СГРУППИРОВАТЬ ПО
		|	ВсеСотрудникиПоСовместительству.ГоловнаяОрганизация,
		|	ВсеСотрудникиПоСовместительству.ФизическоеЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СотрудникиПоОсновномуМестуРаботы.ГоловнаяОрганизация,
		|	СотрудникиПоОсновномуМестуРаботы.Сотрудник,
		|	СотрудникиПоОсновномуМестуРаботы.ФизическоеЛицо
		|ПОМЕСТИТЬ ВТОсновныеСотрудникиФизическихЛиц
		|ИЗ
		|	ВТСотрудникиПоОсновномуМестуРаботы КАК СотрудникиПоОсновномуМестуРаботы
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СотрудникиПоСовместительству.ГоловнаяОрганизация,
		|	СотрудникиПоСовместительству.Сотрудник,
		|	СотрудникиПоСовместительству.ФизическоеЛицо
		|ИЗ
		|	ВТСотрудникиПоСовместительству КАК СотрудникиПоСовместительству
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТРанниеДатыПриемаСотрудниковПоОсновномуМестуРаботы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТСотрудникиОрганизации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТСотрудникиПоОсновномуМестуРаботы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТВсеСотрудникиПоСовместительству
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТРанниеДатыПриемаСовместителей
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТСотрудникиПоСовместительству";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТСотрудникиОрганизации", ИмяВТСотрудникиОрганизации);
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Создает в переданном менеджере временных таблиц временную таблицу с именем переданным
//	в параметре ИмяВТОсновныеСотрудникиФизическихЛицВПериодах, содержащую поля:
//		* Период						- Дата
//		* Сотрудник						- СправочникСсылка.Сотрудники
//		* ФизическоеЛицо				- СправочникСсылка.ФизическиеЛица
//		* ГоловнаяОрганизация			- СправочникСсылка.Организации
//		* Организация					- СправочникСсылка.Организации
//		* Подразделение					- СправочникСсылка.ПодразделенияОрганизаций
//		* Должность						- СправочникСсылка.Должности
//		* КоличествоСтавок				- Число
//		* ВидЗанятости					- ПеречислениеСсылка.ВидыЗанятости
//
//		В зависимости от версии конфигурации, дополнительно может содержать поля
//		* ДолжностьПоШтатномуРасписанию	- СправочникСсылка.ШтатноеРасписание
//		* ВидДоговора					- ПеречислениеСсылка.ВидыДоговоровССотрудниками
//		* РольСотрудника				- ПеречислениеСсылка.РолиСотрудников
//
// Параметры:
//		МенеджерВременныхТаблиц 						- МенеджерВременныхТаблиц
//		ТолькоРазрешенные								- Булево
//		ГоловнаяОрганизация 							- СправочникСсылка.Организации - основные сотрудники определяются в рамках
//															значения реквизита ГоловнаяОрганизация
//		ИмяВТФизическиеЛицаПериоды						- Строка, имя таблицы в менеджере временных таблиц, задающий основной отбор
//															сотрудников, должна содержать поля:
//																* Период			- Дата
//																* ФизическоеЛицо	- СправочникСсылка.ФизическиеЛица
//		ИмяВТОсновныеСотрудникиФизическихЛицВПериодах	- Строка
//
Процедура СоздатьВТОсновныеСотрудникиФизическихЛицВПериодах(МенеджерВременныхТаблиц, ТолькоРазрешенные, Знач ГоловнаяОрганизация, Знач ИмяВТФизическиеЛицаПериоды, Знач ИмяВТОсновныеСотрудникиФизическихЛицВПериодах = "ВТОсновныеСотрудникиФизическихЛицВПериодах") Экспорт
	
	КадровыйУчетВнутренний.СоздатьВТОсновныеСотрудникиФизическихЛицВПериодах(МенеджерВременныхТаблиц, ТолькоРазрешенные, ГоловнаяОрганизация, ИмяВТФизическиеЛицаПериоды, ИмяВТОсновныеСотрудникиФизическихЛицВПериодах);
	
КонецПроцедуры

// Формирует массив физических лиц сотрудников.
//
// Параметры:
//	Сотрудники - массив
//
// Возвращаемое значение - массив уникальных физических лиц.
//	
Функция ФизическиеЛицаСотрудников(Сотрудники) Экспорт
	
	Соответствие = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Сотрудники, "ФизическоеЛицо");
	
	Возврат ОбщегоНазначения.ВыгрузитьКолонку(Соответствие, "Значение", Истина);
	
КонецФункции

Процедура ДобавитьЗапросыУничтоженияВспомогательныхВременныхТаблиц(Запрос, ИсточникиДанных) Экспорт
	
	ИменаТаблицКУничтожению = ИсточникиДанных.Получить("ВТКУничтожению");
	Если ИменаТаблицКУничтожению <> Неопределено Тогда
		
		Если ЗарплатаКадрыОбщиеНаборыДанных.ЗапросСозданияВременнойТаблицы(Запрос.Текст) Тогда
			ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьЗапросыУничтоженияВременныхТаблиц(Запрос.Текст, ИменаТаблицКУничтожению);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ФункцииДляРегламентированнойОтчетности

// РСВ-1

Функция СреднесписочнаяЧисленностьДляРСВ1(ПараметрыОтчета)

	Возврат СреднесписочнаяЧисленностьРаботающих(ПараметрыОтчета.Организация, НачалоГода(ПараметрыОтчета.ДатаНачалаПериодаОтчета), ПараметрыОтчета.ДатаКонцаПериодаОтчета).СреднесписочнаяЧисленностьРаботников;

КонецФункции


Процедура ОписаниеПоказателей_РСВ1_2015Кв1(ПоказателиОтчета) Экспорт
	
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "СрСписЧисл", Истина, Ложь);
	
КонецПроцедуры

Процедура ЗначенияПоказателей_РСВ1_2015Кв1(ПараметрыОтчета, Контейнер) Экспорт
	
	Контейнер.Титульный.СрСписЧисл = СреднесписочнаяЧисленностьДляРСВ1(ПараметрыОтчета)
	
КонецПроцедуры

Процедура ОписаниеПоказателей_РСВ1_2014Кв1(ПоказателиОтчета) Экспорт
	
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "СрСписЧисл", Истина, Ложь);
	
КонецПроцедуры

Процедура ЗначенияПоказателей_РСВ1_2014Кв1(ПараметрыОтчета, Контейнер) Экспорт
	
	Контейнер.Титульный.СрСписЧисл = СреднесписочнаяЧисленностьДляРСВ1(ПараметрыОтчета)
	
КонецПроцедуры

Процедура ОписаниеПоказателей_РСВ1_2013Кв1(ПоказателиОтчета) Экспорт
	
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "СрСписЧисл", Истина, Ложь);
	
КонецПроцедуры

Процедура ЗначенияПоказателей_РСВ1_2013Кв1(ПараметрыОтчета, Контейнер) Экспорт
	
	Контейнер.Титульный.СрСписЧисл = СреднесписочнаяЧисленностьДляРСВ1(ПараметрыОтчета)
	
КонецПроцедуры

Процедура ОписаниеПоказателей_РСВ1_2012Кв1(ПоказателиОтчета) Экспорт
	
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "СрСписЧисл", Истина, Ложь);
	
КонецПроцедуры

Процедура ЗначенияПоказателей_РСВ1_2012Кв1(ПараметрыОтчета, Контейнер) Экспорт
	
	Контейнер.Титульный.СрСписЧисл = СреднесписочнаяЧисленностьДляРСВ1(ПараметрыОтчета)
	
КонецПроцедуры

Процедура ОписаниеПоказателей_РСВ1_2011Кв1(ПоказателиОтчета) Экспорт
	
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "СрСписЧисл", Истина, Ложь);
	
КонецПроцедуры

Процедура ЗначенияПоказателей_РСВ1_2011Кв1(ПараметрыОтчета, Контейнер) Экспорт
	
	Контейнер.Титульный.СрСписЧисл = СреднесписочнаяЧисленностьДляРСВ1(ПараметрыОтчета)
	
КонецПроцедуры

// Сведения о среднесписочной численности работников за предшествующий календарный год (ФНС).

Процедура ОписаниеПоказателей_СреднесписочнаяЧисленностьРаботников_2007Кв1(ПоказателиОтчета) Экспорт
	
	ЗарплатаКадры.ДобавитьПоказательРегламентированнойОтчетности(ПоказателиОтчета, "Численность", Истина, Ложь);
	
КонецПроцедуры

Процедура ЗначенияПоказателей_СреднесписочнаяЧисленностьРаботников_2007Кв1(ПараметрыОтчета, Контейнер) Экспорт
	
	Контейнер.Титульный.Численность = СреднесписочнаяЧисленностьРаботающих(ПараметрыОтчета.Организация, ПараметрыОтчета.ДатаНачалаПериодаОтчета, ПараметрыОтчета.ДатаКонцаПериодаОтчета).СреднесписочнаяЧисленностьРаботников;
	
КонецПроцедуры

// Заменяет в исходном тексте запроса кадровых данных поле данных ФизическоеЛицо
// на имя поля, заданное во временной таблице, при этом если имя поле задано через точку
// производится приведение к типу Справочник.Сотрудники
//
// Параметры:
//		ТекстЗапроса	- Строка
//		ПутьКПолю		- Строка
//		ИмяПоля			- Строка
//
// Например:
//
//		ПутьКПолю = "ТаблицаОтборов.ФизическоеЛицо"
//		ИмяПоля	  = "ФизическоеЛицоСотрудника"
//		текст ПутьКПолю будет заменен на "ТаблицаОтборов.ФизическоеЛицоСотрудника"	
//
//		ПутьКПолю = "ТаблицаОтборов.ФизическоеЛицо"
//		ИмяПоля	  = "Сотрудник.ФизическоеЛицо"
//		текст ПутьКПолю будет заменен на "ВЫРАЗИТЬ(ТаблицаОтборов.Сотрудник КАК Справочник.Сотрудники).ФизическоеЛицо"	
//
Процедура УстановитьВТекстеЗапросаИмяПоляФизическоеЛицо(ТекстЗапроса, ПутьКПолю, ИмяПоля) Экспорт
	
	ИменаВПутиКПолю = СтрРазделить(ПутьКПолю, ".", Ложь);
	НовыйПутьКПолю = ПутьКПолюФизическоеЛицо(ИменаВПутиКПолю[0], ИмяПоля);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ПутьКПолю, НовыйПутьКПолю);
	
КонецПроцедуры

Процедура УстановитьПутьКПолюФизическоеЛицо(ТекстЗапроса, ПсевдонимТаблицы, ИмяПоляФизическоеЛицо) Экспорт
	
	Если СтрЧислоВхождений(ИмяПоляФизическоеЛицо, ".") > 0 Тогда
		
		ЗаменяемаяСтрока = ПсевдонимТаблицы + "." + ИмяПоляФизическоеЛицо;
		СтрокаЗамены = ПутьКПолюФизическоеЛицо(ПсевдонимТаблицы, ИмяПоляФизическоеЛицо);
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ЗаменяемаяСтрока, СтрокаЗамены);
		
	КонецЕсли; 
	
КонецПроцедуры

Функция ПутьКПолюФизическоеЛицо(ПсевдонимТаблицы, ИмяПоляФизическоеЛицо) Экспорт
	
	Если СтрЧислоВхождений(ИмяПоляФизическоеЛицо, ".") > 0 Тогда
		
		ИменаПути = СтрРазделить(ИмяПоляФизическоеЛицо, ".", Ложь);
		ПутьКПолю = "ВЫРАЗИТЬ(" + ПсевдонимТаблицы + "." + ИменаПути[0] + " КАК Справочник.Сотрудники)." + ИменаПути[1];
		
	Иначе
		ПутьКПолю = ПсевдонимТаблицы + "." + ИмяПоляФизическоеЛицо;
	КонецЕсли; 
	
	Возврат ПутьКПолю;
	
КонецФункции

// Формирует соответствие - критерии поиска сотрудников. В качестве ключа выступает полное имя объекта
// метаданных по которому будет производиться поиск в качестве значение массив структур описывающих отборы.
// см. ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор
//
// Параметры:
//		Отборы	- Массив, структур описывающих отборы по кадровым данным сотрудников или физических лиц
//				Описание доступных полей отборов сотрудников см. КадровыйУчетВнутренний.КритерииПоискаСотрудниковПоКоллекцииОтборов 
//
// Возвращаемое значение:
//		Соответствие
//
Функция КритерииПоискаСотрудниковПоКоллекцииОтборов(Отборы) Экспорт
	
	Возврат КадровыйУчетВнутренний.КритерииПоискаСотрудниковПоКоллекцииОтборов(Отборы);
	
КонецФункции

Процедура ДобавитьКритерийПоискаСотрудников(КритерииПоиска, ИмяКритерия, Условие) Экспорт
	
	КоллекцияКритерия = КритерииПоиска.Получить(ИмяКритерия);
	Если КоллекцияКритерия = Неопределено Тогда
		КоллекцияКритерия = Новый Массив;
	КонецЕсли; 
	
	КоллекцияКритерия.Добавить(Условие);
	КритерииПоиска.Вставить(ИмяКритерия, КоллекцияКритерия);
	
КонецПроцедуры

Функция ДобавитьКритерийПоискаПоКонтактнойИнформацииФизическогоЛица(КритерииПоиска, УсловиеПоиска) Экспорт
	
	Возврат КадровыйУчетВнутренний.ДобавитьКритерийПоискаПоКонтактнойИнформацииФизическогоЛица(КритерииПоиска, УсловиеПоиска);
	
КонецФункции

Процедура УдалитьПустыеКритерииПоиска(КритерииПоиска) Экспорт
	
	УдаляемыеКритерии = Новый Массив;
	Для каждого КритерийПоиска Из КритерииПоиска Цикл
		
		Если ЭтоПустойКритерийПоиска(КритерийПоиска) Тогда
			УдаляемыеКритерии.Добавить(КритерийПоиска.Ключ);
		КонецЕсли;
		
	КонецЦикла;
	
	Для каждого УдаляемыйКритерий Из УдаляемыеКритерии Цикл
		КритерииПоиска.Удалить(УдаляемыйКритерий);
	КонецЦикла;
	
КонецПроцедуры

Функция ЭтоПустойКритерийПоиска(КритерийПоиска) Экспорт
	
	Возврат ЭтоПустыеОтборыКритерияПоиска(КритерийПоиска.Значение);
	
КонецФункции

Функция ЭтоПустыеОтборыКритерияПоиска(Отборы) Экспорт
	
	ЭтоПустойКритерий = Истина;
	Для каждого ЭлементОтбора Из Отборы Цикл
		
		Если ВРег(ЭлементОтбора.ЛевоеЗначение) <> ВРег("Период")
			И ВРег(ЭлементОтбора.ЛевоеЗначение) <> ВРег("ДатаНачала")
			И ВРег(ЭлементОтбора.ЛевоеЗначение) <> ВРег("ДатаОкончания") Тогда
			
			ЭтоПустойКритерий = Ложь;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ЭтоПустойКритерий;
	
КонецФункции

// Возвращает запрос, создающий временную таблицу, содержащую колонку ФизическоеЛицо
// с физическими лицами (Сотрудник - для области поиска "Справочник.Сотрудники"), отвечающими критериям поиска
//
// Параметры:
//		КритерииПоискаСотрудников			- Соответствие
//												* Ключ		- Строка, полное имя объекта метаданных (справочник,
//													владельцем которого является справочник ФизическиеЛица 
//													или регистр сведений с именем измерения ФизическоеЛицо)
//												* Значение	- Массив структур, описывающих отборы (см. ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор)
//															- Структура описывающая таблицу - отбор
//																* ТаблицаОтбора	- Строка, имя временной таблицы - отбора
//																				- ТаблицаЗначений
//																* Колонки		- Строка, имена колонок таблицы отбора по которым осуществляется отбор
//																					обязателен в случае если тип ТаблицаОтбора - Строка
//		ТолькоРазрешенные					- Булево
//		ОбластьПоиска						- Строка, полное имя объекта метаданных - справочника ("Справочник.ФизическиеЛица" или "Справочник.Сотрудники")
//		ИмяВТСотрудникиПоКритериямПоиска	- Строка
//
Функция ЗапросВТСотрудникиПоКритериямПоиска(КритерииПоискаСотрудников, ТолькоРазрешенные, Знач ОбластьПоиска = "Справочник.ФизическиеЛица", ИмяВТСотрудникиПоКритериямПоиска = "ВТСотрудникиПоКритериямПоиска") Экспорт
	
	Если ТипЗнч(КритерииПоискаСотрудников) <> Тип("Соответствие") ИЛИ КритерииПоискаСотрудников.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru='Не заданы критерии поиска'");
	КонецЕсли; 
	
	ИменаВременныхТаблицКУничтожению = Новый Массив;
	
	ОбластьПоиска = ВРег(ОбластьПоиска);
	ОбластьПоискаПоСотрудникам = "";
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса = "";
	ТекстЗапросаСправочника = "";
	ТекстЗапросаСправочникаДляПоискаПоПериоду = "";
	ТекстСоединений = "";
	
	КритерийОбластьПоиска = Неопределено;
	КритерийПоискаФизическиеЛица = Неопределено;
	КритерийПоискаПоКадровойИсторииСотрудников = Неопределено;
	Для каждого КритерийПоиска Из КритерииПоискаСотрудников Цикл
		
		Если ВРег(КритерийПоиска.Ключ) = ОбластьПоиска Тогда
			КритерийОбластьПоиска = КритерийПоиска;
		ИначеЕсли ВРег(КритерийПоиска.Ключ) = ВРег("Справочник.ФизическиеЛица") Тогда
			КритерийПоискаФизическиеЛица = КритерийПоиска;
		ИначеЕсли ВРег(КритерийПоиска.Ключ) = ВРег("РегистрСведений.КадроваяИсторияСотрудников") Тогда
			КритерийПоискаПоКадровойИсторииСотрудников = КритерийПоиска;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ОбластьПоиска = ВРег("Справочник.Сотрудники")
		И Не ЗначениеЗаполнено(КритерийОбластьПоиска)
		И ЗначениеЗаполнено(КритерийПоискаПоКадровойИсторииСотрудников) Тогда
		
		КритерийОбластьПоиска = КритерийПоискаПоКадровойИсторииСотрудников;
		ОбластьПоискаПоСотрудникам = ВРег("РегистрСведений.КадроваяИсторияСотрудников");
		
	ИначеЕсли ОбластьПоиска = ВРег("Справочник.Сотрудники") Тогда
		ОбластьПоискаПоСотрудникам = ОбластьПоиска;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КритерийОбластьПоиска)
		И ЗначениеЗаполнено(КритерийПоискаФизическиеЛица) Тогда
		
		Для каждого ОписаниеУсловия Из КритерийПоискаФизическиеЛица.Значение Цикл
			
			Если ОписаниеУсловия.ОтносительныйПуть Тогда
				
				ОписаниеУсловия.ОтносительныйПуть = Ложь;
				ОписаниеУсловия.ЛевоеЗначение = "ВЫРАЗИТЬ(Т.ФизическоеЛицо КАК Справочник.ФизическиеЛица)." + ОписаниеУсловия.ЛевоеЗначение;
				
			КонецЕсли;
			
			КритерийОбластьПоиска.Значение.Добавить(ОписаниеУсловия);
			
		КонецЦикла;
		
		КритерииПоискаСотрудников.Удалить(КритерийПоискаФизическиеЛица.Ключ);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КритерийОбластьПоиска) Тогда
		
		ИмяВТСотрудники = "ВТСотрудникиПоУсловиямПоиска";
		
		ЗапросПоУсловиям = ЗапросВТСотрудникиПоУсловиямПоиска(КритерийОбластьПоиска.Ключ, ТолькоРазрешенные, КритерийОбластьПоиска.Значение, ОбластьПоиска, , ИмяВТСотрудники);
		
		ТекстЗапросаСправочника = ЗапросПоУсловиям.Текст;
		ЗарплатаКадрыОбщиеНаборыДанных.ЗаменитьИмяСоздаваемойВременнойТаблицы(ТекстЗапросаСправочника, "ПОМЕСТИТЬ " + ИмяВТСотрудники, "ПОМЕСТИТЬ " + ИмяВТСотрудникиПоКритериямПоиска);
		ЗарплатаКадрыОбщиеНаборыДанных.СкопироватьПараметрыЗапроса(Запрос, ЗапросПоУсловиям);
		
		ТекстЗапросаСправочникаДляПоискаПоПериоду = ЗапросПоУсловиям.Текст;
		ЗарплатаКадрыОбщиеНаборыДанных.ЗаменитьИмяСоздаваемойВременнойТаблицы(ТекстЗапросаСправочникаДляПоискаПоПериоду, ИмяВТСотрудники, "ВТСотрудникиОтборы");
		
	КонецЕсли;
	
	ТекстыЗапросов = Новый Массив;
	ТекстыСоединений = Новый Массив;
	
	Для каждого КритерийПоиска Из КритерииПоискаСотрудников Цикл
		
		Если ВРег(КритерийПоиска.Ключ) = ОбластьПоиска
			Или ВРег(КритерийПоиска.Ключ) = ОбластьПоискаПоСотрудникам Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяТаблицы = СтрЗаменить(КритерийПоиска.Ключ, ".", "");
		ИмяВТСотрудникиПоУсловиямПоиска = "ВТУсловияПоиска" + ИмяТаблицы;
		ИменаВременныхТаблицКУничтожению.Добавить(ИмяВТСотрудникиПоУсловиямПоиска);
		
		ЗапросПоУсловиям = ЗапросВТСотрудникиПоУсловиямПоиска(КритерийПоиска.Ключ, ТолькоРазрешенные, КритерийПоиска.Значение, ОбластьПоиска, ТекстЗапросаСправочникаДляПоискаПоПериоду, ИмяВТСотрудникиПоУсловиямПоиска);
		
		ТекстыЗапросов.Вставить(0, ЗапросПоУсловиям.Текст);
		
		ЭтоКритерийПоискаФизическогоЛица = КритерийПоискаФизическогоЛица(КритерийПоиска.Ключ);
		
		Если ОбластьПоиска = ОбластьПоискаПоСотрудникам
			И ЭтоКритерийПоискаФизическогоЛица Тогда
			
			Если ВРег(КритерийПоиска.Ключ) = ВРег("РегистрСведений.ДокументыФизическихЛиц") Тогда
				ИмяПоляФизическоеЛицоТаблицы = "Физлицо";
			Иначе
				ИмяПоляФизическоеЛицоТаблицы = "ФизическоеЛицо";
			КонецЕсли;
			
			ТекстыСоединений.Добавить("
				|		{ВНУТРЕННЕЕ СОЕДИНЕНИЕ " + ИмяВТСотрудникиПоУсловиямПоиска + " КАК " + ИмяТаблицы + "
				|		ПО Т.ФизическоеЛицо = " + ИмяТаблицы + "." + ИмяПоляФизическоеЛицоТаблицы + "}");
			
		ИначеЕсли ОбластьПоиска = ОбластьПоискаПоСотрудникам Тогда
			
			Если ОбластьПоиска = ВРег("Справочник.Сотрудники") Тогда
				
				ТекстыСоединений.Добавить("
					|		{ВНУТРЕННЕЕ СОЕДИНЕНИЕ " + ИмяВТСотрудникиПоУсловиямПоиска + " КАК " + ИмяТаблицы + "
					|		ПО Т.Ссылка = " + ИмяТаблицы + ".Сотрудник}");
				
			Иначе
				
				ТекстыСоединений.Добавить("
					|		{ВНУТРЕННЕЕ СОЕДИНЕНИЕ " + ИмяВТСотрудникиПоУсловиямПоиска + " КАК " + ИмяТаблицы + "
					|		ПО Т.Сотрудник = " + ИмяТаблицы + ".Сотрудник}");
				
			КонецЕсли;
			
		ИначеЕсли ОбластьПоиска = ВРег("Справочник.ФизическиеЛица")
			И Не ЭтоКритерийПоискаФизическогоЛица Тогда
			
			ТекстыСоединений.Добавить("
				|		{ВНУТРЕННЕЕ СОЕДИНЕНИЕ " + ИмяВТСотрудникиПоУсловиямПоиска + " КАК " + ИмяТаблицы + "
				|		ПО Т.Ссылка = ВЫРАЗИТЬ(" + ИмяТаблицы + ".Сотрудник КАК Справочник.Сотрудники).ФизическоеЛицо}");
			
		Иначе
			
			Если ВРег(КритерийПоиска.Ключ) = ВРег("РегистрСведений.ДокументыФизическихЛиц") Тогда
				ИмяПоляФизическоеЛицоТаблицы = "Физлицо";
			Иначе
				ИмяПоляФизическоеЛицоТаблицы = "ФизическоеЛицо";
			КонецЕсли;
			
			ТекстыСоединений.Добавить("
				|		{ВНУТРЕННЕЕ СОЕДИНЕНИЕ " + ИмяВТСотрудникиПоУсловиямПоиска + " КАК " + ИмяТаблицы + "
				|		ПО Т.Ссылка = " + ИмяТаблицы + "." + ИмяПоляФизическоеЛицоТаблицы + "}");
			
		КонецЕсли;
		
		ЗарплатаКадрыОбщиеНаборыДанных.СкопироватьПараметрыЗапроса(Запрос, ЗапросПоУсловиям);
		
	КонецЦикла;
	
	Если ПустаяСтрока(ТекстЗапросаСправочника) Тогда
		
		Если ОбластьПоиска = ВРег("Справочник.ФизическиеЛица") Тогда
			
			ТекстЗапросаСправочника =
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
				|	Т.Ссылка КАК ФизическоеЛицо
				|ПОМЕСТИТЬ ВТСотрудникиПоКритериямПоиска
				|ИЗ
				|	Справочник.ФизическиеЛица КАК Т";
			
		Иначе
			
			ТекстЗапросаСправочника =
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
				|	Т.Ссылка КАК Сотрудник,
				|	Т.ФизическоеЛицо КАК ФизическоеЛицо
				|ПОМЕСТИТЬ ВТСотрудникиПоКритериямПоиска
				|ИЗ
				|	Справочник.Сотрудники КАК Т";
			
		КонецЕсли;
		
		ЗарплатаКадрыОбщиеНаборыДанных.ЗаменитьИмяСоздаваемойВременнойТаблицы(
			ТекстЗапросаСправочника, "ВТСотрудникиПоКритериямПоиска", ИмяВТСотрудникиПоКритериямПоиска);
		
		ЗарплатаКадрыОбщиеНаборыДанных.УстановитьВыборкуТолькоРазрешенныхДанных(
			ТекстЗапросаСправочника, ТолькоРазрешенные);
		
		Если ТекстыСоединений.Количество() > 0 Тогда
			
			ТекстыСоединений.Вставить(0, ТекстЗапросаСправочника);
			ТекстЗапросаСправочника = СтрСоединить(ТекстыСоединений, Символы.ПС);
			
		КонецЕсли;
		
	Иначе
		
		Если ТекстыСоединений.Количество() > 0 Тогда
			
			Если ОбластьПоиска = ВРег("Справочник.ФизическиеЛица") Тогда
				СтрокаСИменемТаблицы = ВРег("Справочник.ФизическиеЛица КАК ");
			ИначеЕсли ОбластьПоиска = ВРег("Справочник.Сотрудники") И ОбластьПоискаПоСотрудникам = ВРег("Справочник.Сотрудники") Тогда
				СтрокаСИменемТаблицы = ВРег("Справочник.Сотрудники КАК ");
			Иначе
				СтрокаСИменемТаблицы = ВРег("РегистрСведений.КадроваяИсторияСотрудников КАК ");
			КонецЕсли;
			
			ПозицияИмениТаблицы = СтрНайти(ВРег(ТекстЗапросаСправочника), СтрокаСИменемТаблицы);
			Для НомерСимвола = ПозицияИмениТаблицы + СтрДлина(СтрокаСИменемТаблицы) По СтрДлина(ТекстЗапросаСправочника) Цикл
				Если СтроковыеФункцииКлиентСервер.ЭтоРазделительСлов(КодСимвола(ТекстЗапросаСправочника, НомерСимвола), Символы.ПС + Символы.Таб + " ;") Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			ТекстыСоединений.Вставить(0, Лев(ТекстЗапросаСправочника, НомерСимвола - 1));
			ТекстыСоединений.Добавить(Сред(ТекстЗапросаСправочника, НомерСимвола));
			ТекстЗапросаСправочника = СтрСоединить(ТекстыСоединений, Символы.ПС);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ТекстыЗапросов.Количество() = 0 Тогда
		Запрос.Текст = ТекстЗапросаСправочника;
	Иначе
		
		ТекстыЗапросов.Добавить(ТекстЗапросаСправочника);
		Запрос.Текст = СтрСоединить(ТекстыЗапросов, ЗарплатаКадрыОбщиеНаборыДанных.РазделительЗапросов());
		
	КонецЕсли;
	
	ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьЗапросыУничтоженияВременныхТаблиц(Запрос.Текст, ИменаВременныхТаблицКУничтожению);
	
	Возврат Запрос;
	
КонецФункции

Функция КритерийПоискаФизическогоЛица(Знач ПолноеИмяОбъектаМетаданных)
	
	Если СтрЧислоВхождений(ПолноеИмяОбъектаМетаданных, ".") > 1 Тогда
		
		ИменаОбъектаМетаданных = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПолноеИмяОбъектаМетаданных, ".");
		ПолноеИмяОбъектаМетаданныхВерхнемРегистре = ВРег(ИменаОбъектаМетаданных[0] + "." + ИменаОбъектаМетаданных[1]);
		
	Иначе
		ПолноеИмяОбъектаМетаданныхВерхнемРегистре = ВРег(ПолноеИмяОбъектаМетаданных);
	КонецЕсли;
	
	ПоискФизическогоЛица = Ложь;
	
	Если ПолноеИмяОбъектаМетаданныхВерхнемРегистре = ВРег("Справочник.ФизическиеЛица")
		Или ПолноеИмяОбъектаМетаданныхВерхнемРегистре = ВРег("РегистрСведений.ДокументыФизическихЛиц") Тогда
		ПоискФизическогоЛица = Истина;
	Иначе
		
		МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(ПолноеИмяОбъектаМетаданныхВерхнемРегистре);
		Если СтрНайти(ПолноеИмяОбъектаМетаданныхВерхнемРегистре, ВРег("Справочник.")) = 1 Тогда
			ПоискФизическогоЛица = МетаданныеОбъекта.Владельцы.Содержит(МетаДанные.Справочники.ФизическиеЛица);
		Иначе
			
			ПоискСотрудника = Ложь;
			Для каждого Измерение Из МетаданныеОбъекта.Измерения Цикл
				
				Если ВРег(Измерение.Имя) <> ВРег("ФизическоеЛицо")
					И ВРег(Измерение.Имя) <> ВРег("Сотрудник") Тогда
					Продолжить;
				КонецЕсли; 
				
				Если Измерение.Тип.СодержитТип(Тип("СправочникСсылка.Сотрудники")) Тогда
					ПоискСотрудника = Истина;
					Прервать;
				КонецЕсли; 
				
				Если Измерение.Тип.СодержитТип(Тип("СправочникСсылка.ФизическиеЛица")) Тогда
					ПоискФизическогоЛица = Истина;
				КонецЕсли; 
				
			КонецЦикла;
			
			Если ПоискСотрудника Тогда
				ПоискФизическогоЛица = Ложь;
			КонецЕсли; 
		
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ПоискФизическогоЛица;
	
КонецФункции

Функция ЗапросВТСотрудникиПоУсловиямПоиска(Знач ПолноеИмяОбъектаМетаданных, ТолькоРазрешенные, УсловияПоиска, Знач ОбластьПоиска, ТекстЗапросаСправочника = "", ИмяВТСотрудникиПоУсловиямПоиска = "ВТСотрудникиПоУсловиямПоиска")
	
	ОбластьПоиска = ВРег(ОбластьПоиска);
	
	Запрос = Новый Запрос;
	ИмяВТСотрудникиОтборы = "";
	
	СловаИмени = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПолноеИмяОбъектаМетаданных, ".");
	Если СтрНайти(ВРег(ПолноеИмяОбъектаМетаданных), ВРег("Справочник.ФизическиеЛица.КонтактнаяИнформация")) <> 1 Тогда
		ПолноеИмяОбъектаМетаданных = СловаИмени[0] + "." + СловаИмени[1];
	КонецЕсли;
	
	ПолноеИмяОбъектаМетаданныхВерхнемРегистре = ВРег(СловаИмени[0]) + "." + ВРег(СловаИмени[1]);
	ПредставлениеТаблицы = СловаИмени[1];
	
	ПредставлениеСреза = Неопределено;
	ПредставлениеТаблицыРегистра = Неопределено;
	
	ПредставлениеСрезаОпределено = Ложь;
	
	Если ВРег(СловаИмени[0]) = ВРег("РегистрСведений") Тогда
		
		МетаданныеРегистра = Метаданные.НайтиПоПолномуИмени(ПолноеИмяОбъектаМетаданныхВерхнемРегистре);
		Если МетаданныеРегистра.ПериодичностьРегистраСведений <> Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический Тогда
			
			Если СловаИмени.Количество() > 2
				И СловаИмени[2] = ВРег("ТаблицаРегистра") Тогда
				
				ПредставлениеТаблицыРегистра = Истина;
				
			Иначе
				
				ПредставлениеСреза = Истина;
				Если СловаИмени.Количество() > 2
					И СловаИмени[2] = ВРег("СрезПоследних") Тогда
					
					ПредставлениеСрезаОпределено = Истина;
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если ПредставлениеТаблицыРегистра <> Истина Тогда
				
				Для каждого ОписаниеУсловия Из УсловияПоиска Цикл
					
					Если ВРег(ОписаниеУсловия.ЛевоеЗначение) = ВРег("ДатаНачала")
						Или ВРег(ОписаниеУсловия.ЛевоеЗначение) = ВРег("ДатаОкончания") Тогда
						
						ПредставлениеТаблицыРегистра = Истина;
						Прервать;
						
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
		
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПредставлениеСреза = Истина Или ПредставлениеТаблицыРегистра = Истина Тогда
		
		МассивУсловий = Новый Массив;
		
		Период = "ДАТАВРЕМЯ(1, 1, 1)";
		ДатаНачала = Период;
		ДатаОкончания = Период;
		
		Если ТипЗнч(УсловияПоиска) = Тип("Массив") Тогда
			
			Для каждого ОписаниеУсловия Из УсловияПоиска Цикл
				
				Если ВРег(ОписаниеУсловия.ЛевоеЗначение) = ВРег("Период")
					Или ВРег(ОписаниеУсловия.ЛевоеЗначение) = ВРег("ДатаНачала")
					Или ВРег(ОписаниеУсловия.ЛевоеЗначение) = ВРег("ДатаОкончания") Тогда
					
					ПравоеЗначение = ОписаниеУсловия.ПравоеЗначение;
					Если ТипЗнч(ПравоеЗначение) = Тип("Дата") Тогда
						ПравоеЗначение = "ДАТАВРЕМЯ(" + Формат(ПравоеЗначение, "ДФ='гггг, ММ, дд, ЧЧ, мм, сс'; ДП=") + ")";
					ИначеЕсли ТипЗнч(ПравоеЗначение) <> Тип("Строка") Тогда
						
						ИмяПараметра = "Параметр" + ПредставлениеТаблицы + Формат(Запрос.Параметры.Количество(), "ЧГ=");
						Запрос.УстановитьПараметр(ИмяПараметра, ПравоеЗначение); 
						ПравоеЗначение = "&" + ИмяПараметра;
						
					КонецЕсли;
					
					Если ВРег(ОписаниеУсловия.ЛевоеЗначение) = ВРег("Период") Тогда
						Период = ПравоеЗначение;
					ИначеЕсли ВРег(ОписаниеУсловия.ЛевоеЗначение) = ВРег("ДатаНачала") Тогда
						ДатаНачала = ПравоеЗначение;
					Иначе
						ДатаОкончания = ПравоеЗначение;
					КонецЕсли;
					
				Иначе
					МассивУсловий.Добавить(ОписаниеУсловия);
				КонецЕсли; 
					
			КонецЦикла;
			
			Если ПредставлениеСреза = Истина И ПредставлениеСрезаОпределено = Ложь
				И (ДатаНачала <> "ДАТАВРЕМЯ(1, 1, 1)" Или ДатаОкончания <> "ДАТАВРЕМЯ(1, 1, 1)") Тогда
				
				ПредставлениеСреза = Ложь;
				ПредставлениеТаблицыРегистра = Истина;
				
			КонецЕсли; 
			
			ИмяВТСотрудникиОтборы = "ВТОтборыСотрудникиДля" + СтрЗаменить(ПолноеИмяОбъектаМетаданных, ".", "");
			
			ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
				ИмяВТСотрудникиОтборы);
			
			Запрос.Текст =
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|ПОМЕСТИТЬ ВТСотрудникиОтборы";
			
			Если ПредставлениеСреза = Истина Тогда
				
				ТекстПериодов = "
					|	&Период КАК Период" + ПредставлениеТаблицы + "
					|";
				
				ТекстПериодов = СтрЗаменить(ТекстПериодов, "&Период", Период);
				
				ОписаниеФильтра.СоответствиеИзмеренийРегистраИзмерениямФильтра.Вставить("Период", "Период" + ПредставлениеТаблицы);
				
			Иначе
				
				ТекстПериодов = "
					|	&ДатаНачала КАК ДатаНачала" + ПредставлениеТаблицы + ",
					|	&ДатаОкончания КАК ДатаОкончания" + ПредставлениеТаблицы + "
					|";
				
				ТекстПериодов = СтрЗаменить(ТекстПериодов, "&ДатаНачала", ДатаНачала);
				ТекстПериодов = СтрЗаменить(ТекстПериодов, "&ДатаОкончания", ДатаОкончания);
				
				ОписаниеФильтра.СоответствиеИзмеренийРегистраИзмерениямФильтра.Вставить("ДатаНачала", "ДатаНачала" + ПредставлениеТаблицы);
				ОписаниеФильтра.СоответствиеИзмеренийРегистраИзмерениямФильтра.Вставить("ДатаОкончания", "ДатаОкончания" + ПредставлениеТаблицы);
				
			КонецЕсли;
			
			ПозицияПоместить = СтрНайти(Запрос.Текст, "ПОМЕСТИТЬ");
			Запрос.Текст = Лев(Запрос.Текст, ПозицияПоместить - 2) + ТекстПериодов + Сред(Запрос.Текст, ПозицияПоместить);
			
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТСотрудникиОтборы", ИмяВТСотрудникиОтборы);
			ЗарплатаКадрыОбщиеНаборыДанных.УстановитьВыборкуТолькоРазрешенныхДанных(Запрос.Текст, ТолькоРазрешенные);
			
		Иначе
			ОписаниеФильтра = УсловияПоиска;
		КонецЕсли;
		
		ИмяРегистра = Сред(ПолноеИмяОбъектаМетаданных, СтрНайти(ПолноеИмяОбъектаМетаданных, ".") + 1);
		Если ПредставлениеСреза = Истина Тогда
			
			ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистраСрез();
			ОписаниеРегистра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеРегистра(ИмяРегистра);
			
			Для каждого ОписаниеОтбора Из МассивУсловий Цикл
				
				Если ОписаниеОтбора.ОтносительныйПуть
					И (ОписаниеРегистра.ИзмеренияДляПоиска.Получить(ВРег(ОписаниеОтбора.ЛевоеЗначение)) <> Неопределено
					Или ВРег(ОписаниеОтбора.ЛевоеЗначение) = ВРег("Регистратор")) Тогда
					
					ПараметрыПостроения.Отборы.Добавить(ОписаниеОтбора);
					
				Иначе
					ПараметрыПостроения.ОтборыПрименяемыеКСрезу.Добавить(ОписаниеОтбора);
				КонецЕсли;
				
			КонецЦикла;
			
			ЗапросКРегистру = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросВТИмяРегистраСрез(
				ИмяРегистра,
				ТолькоРазрешенные,
				ОписаниеФильтра,
				ПараметрыПостроения,
				Истина,
				ИмяВТСотрудникиПоУсловиямПоиска);
			
		Иначе
			
			ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистра();
			ПараметрыПостроения.ВключатьЗаписиНаНачалоПериода = Истина;
			ПараметрыПостроения.Отборы = МассивУсловий;
			
			ЗапросКРегистру = ЗарплатаКадрыОбщиеНаборыДанных.ЗапросВТИмяРегистра(
				ИмяРегистра,
				ТолькоРазрешенные,
				ОписаниеФильтра,
				ПараметрыПостроения,
				ИмяВТСотрудникиПоУсловиямПоиска);
			
		КонецЕсли;
		
		Если ПолноеИмяОбъектаМетаданныхВерхнемРегистре = ВРег("РегистрСведений.ДокументыФизическихЛиц") Тогда
			ЗапросКРегистру.Текст = СтрЗаменить(ЗапросКРегистру.Текст, "РегистрСведений.ФизическоеЛицо", "РегистрСведений.Физлицо");
		КонецЕсли;
		
		ЗарплатаКадрыОбщиеНаборыДанных.СкопироватьПараметрыЗапроса(Запрос, ЗапросКРегистру);
		
		ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьТекстыЗапросов(
			Запрос.Текст, ЗапросКРегистру.Текст);
		
	Иначе
		
		Если ТипЗнч(УсловияПоиска) = Тип("Массив") Тогда
			
			ТекстыУсловий = Новый Массив;
			Для каждого ОписаниеУсловия Из УсловияПоиска Цикл
				
				ПравоеЗначение = ОписаниеУсловия.ПравоеЗначение;
				Если ТипЗнч(ПравоеЗначение) = Тип("Число") Тогда
					ПравоеЗначение = Формат(ПравоеЗначение, "ЧН=; ЧГ=");
				ИначеЕсли ТипЗнч(ПравоеЗначение) = Тип("Дата") Тогда
					ПравоеЗначение = "ДАТАВРЕМЯ(" + Формат(ПравоеЗначение, "ДФ='гггг, ММ, дд, ЧЧ, мм, сс'; ДП=") + ")";
				ИначеЕсли ТипЗнч(ПравоеЗначение) <> Тип("Строка") Тогда
					
					ИмяПараметра = "Параметр" + ПредставлениеТаблицы + Формат(Запрос.Параметры.Количество(), "ЧГ=");
					Запрос.УстановитьПараметр(ИмяПараметра, ПравоеЗначение); 
					ПравоеЗначение = "&" + ИмяПараметра;
					
				КонецЕсли;
				
				Если ОписаниеУсловия.ОтносительныйПуть Тогда
					ЛевоеЗначение = "Т." + ОписаниеУсловия.ЛевоеЗначение;
				Иначе
					ЛевоеЗначение = ОписаниеУсловия.ЛевоеЗначение;
				КонецЕсли;
				
				ТекстыУсловий.Добавить("(" + ЛевоеЗначение + " " + ОписаниеУсловия.ВидСравнения + "(" + ПравоеЗначение + ")" + ")");
				
			КонецЦикла;
			
			УсловиеПоиска = СтрСоединить(ТекстыУсловий, Символы.ПС + Символы.Таб + "И ");
			
		Иначе
			УсловиеПоиска = "";
		КонецЕсли;
		
		Если ПолноеИмяОбъектаМетаданныхВерхнемРегистре = ВРег("Справочник.Сотрудники") Тогда
			
			Запрос.Текст =
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
				|	Т.Ссылка КАК Сотрудник,
				|	Т.ФизическоеЛицо КАК ФизическоеЛицо
				|ПОМЕСТИТЬ ВТСотрудникиПоУсловиямПоиска
				|ИЗ
				|	Справочник.Сотрудники КАК Т
				|ГДЕ
				|	&УсловиеПоиска";
			
		ИначеЕсли ПолноеИмяОбъектаМетаданныхВерхнемРегистре = ВРег("Справочник.ФизическиеЛица") Тогда
			
			Запрос.Текст =
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
				|	Т.Ссылка КАК ФизическоеЛицо
				|ПОМЕСТИТЬ ВТСотрудникиПоУсловиямПоиска
				|ИЗ
				|	Справочник.ФизическиеЛица КАК Т
				|ГДЕ
				|	&УсловиеПоиска";
			
			Если СтрНайти(ВРег(ПолноеИмяОбъектаМетаданных), ВРег("Справочник.ФизическиеЛица.КонтактнаяИнформация")) = 1 Тогда
				Запрос.Текст = СтрЗаменить(Запрос.Текст, "Справочник.ФизическиеЛица", "Справочник.ФизическиеЛица.КонтактнаяИнформация");
			КонецЕсли;
			
		Иначе
			
			Если ПолноеИмяОбъектаМетаданныхВерхнемРегистре = ВРег("РегистрСведений.КадроваяИсторияСотрудников") Тогда
				
				Запрос.Текст =
					"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
					|	Т.Сотрудник КАК Сотрудник,
					|	Т.ФизическоеЛицо КАК ФизическоеЛицо
					|ПОМЕСТИТЬ ВТСотрудникиПоУсловиямПоиска
					|ИЗ
					|	&ТаблицаССотрудником КАК Т
					|ГДЕ
					|	&УсловиеПоиска";
				
			Иначе
				
				Запрос.Текст =
					"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
					|	Т.Сотрудник КАК Сотрудник
					|ПОМЕСТИТЬ ВТСотрудникиПоУсловиямПоиска
					|ИЗ
					|	&ТаблицаССотрудником КАК Т
					|ГДЕ
					|	&УсловиеПоиска";
				
			КонецЕсли;
			
			ЗарплатаКадрыОбщиеНаборыДанных.УстановитьВыборкуТолькоРазрешенныхДанных(Запрос.Текст, ТолькоРазрешенные);
			
			ОбъектаМетаданныхСправочник = СтрНайти(ПолноеИмяОбъектаМетаданныхВерхнемРегистре, ВРег("Справочник.")) = 1;
			
			Если КритерийПоискаФизическогоЛица(ПолноеИмяОбъектаМетаданныхВерхнемРегистре) Тогда
				
				Если ОбъектаМетаданныхСправочник Тогда
					Запрос.Текст = СтрЗаменить(Запрос.Текст, "Т.Сотрудник КАК Сотрудник", "Т.Владелец КАК ФизическоеЛицо");
				Иначе
					Запрос.Текст = СтрЗаменить(Запрос.Текст, "Т.Сотрудник КАК Сотрудник", "Т.ФизическоеЛицо КАК ФизическоеЛицо");
				КонецЕсли;
				
			ИначеЕсли ОбъектаМетаданныхСправочник Тогда
				Запрос.Текст = СтрЗаменить(Запрос.Текст, "Т.Сотрудник КАК Сотрудник", "Т.Владелец КАК Сотрудник");
			КонецЕсли;
			
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТаблицаССотрудником", ПолноеИмяОбъектаМетаданных);
			
		КонецЕсли;
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоиска", ?(ПустаяСтрока(УсловиеПоиска), "(ИСТИНА)" , УсловиеПоиска));
		
		ЗарплатаКадрыОбщиеНаборыДанных.ЗаменитьИмяСоздаваемойВременнойТаблицы(Запрос.Текст, "ВТСотрудникиПоУсловиямПоиска", ИмяВТСотрудникиПоУсловиямПоиска);
		
	КонецЕсли;
	
	ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьЗапросУничтоженияВременнойТаблицы(Запрос.Текст, ИмяВТСотрудникиОтборы);
	
	Возврат Запрос;
	
КонецФункции

Процедура ДобавитьВКоллекциюИмяКадровыхДанных(ИсточникиДанных, ИмяКоллекции, ИмяКадровыхДанных = "") Экспорт
	
	ИменаКадровыхДанных = ИсточникиДанных.Получить(ИмяКоллекции);
	Если Не ПустаяСтрока(ИмяКадровыхДанных) Тогда
		
		Если ТипЗнч(ИменаКадровыхДанных) <> Тип("Массив") Тогда
			ИменаКадровыхДанных = Новый Массив;
		КонецЕсли; 
		
		Если ИменаКадровыхДанных.Найти(ИмяКадровыхДанных) = Неопределено Тогда
			ИменаКадровыхДанных.Добавить(ИмяКадровыхДанных);
		КонецЕсли;
		
	КонецЕсли; 
	
	ИсточникиДанных.Вставить(ИмяКоллекции, ?(Не ЗначениеЗаполнено(ИменаКадровыхДанных), Истина, ИменаКадровыхДанных));
			
КонецПроцедуры

Функция ТаблицаНачисленийСотрудниковПоВременнойТаблице(МенеджерВременныхТаблиц, ИмяВТСотрудникиПериоды, ИмяПоляПериод = "Период", ИмяПоляСотрудник = "Сотрудник", ИмяПоляДатаОкончания = "", ИспользоватьКраткиеПредставления = Истина, ВыводитьДенежныеПоказателиСВалютой = Ложь) Экспорт
	
	Возврат КадровыйУчетВнутренний.ТаблицаНачисленийСотрудниковПоВременнойТаблице(МенеджерВременныхТаблиц, ИмяВТСотрудникиПериоды, ИмяПоляПериод, ИмяПоляСотрудник, ИмяПоляДатаОкончания, ИспользоватьКраткиеПредставления, ВыводитьДенежныеПоказателиСВалютой);
	
КонецФункции

Процедура ОбновитьТекущиеКадровыеДанныеСпискаСотрудников(Сотрудники, СохранитьДатыПриема = Ложь) Экспорт
	
		Если Сотрудники.Количество() = 0 Тогда
			Возврат;
		КонецЕсли; 
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		Запрос.УстановитьПараметр("МассивСотрудниковДляОбновления", Сотрудники);
		
		// Подготовим таблицу ИзмеренияДаты для получения среза последних.
		ИзмеренияДаты = Новый ТаблицаЗначений;
		ИзмеренияДаты.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
		ИзмеренияДаты.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
		
		Для Каждого Сотрудник Из Сотрудники Цикл
			СтрокаИзмеренияДаты = ИзмеренияДаты.Добавить();
			СтрокаИзмеренияДаты.Сотрудник = Сотрудник;
		КонецЦикла;
		
		// Получим последние значения занятости.
		ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних(
			"КадроваяИсторияСотрудников",
			Запрос.МенеджерВременныхТаблиц,
			Истина,
			ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(ИзмеренияДаты));
		
		// Подготовим таблицу ИзмеренияДаты Для получения информации о датах приема и увольнения.
		ИзмеренияДаты.Колонки.Добавить("ДатаНачала", Новый ОписаниеТипов("Дата"));
		ИзмеренияДаты.Колонки.Период.Имя = "ДатаОкончания";
		
		// Сформируем отбор по события прием и увольнение.
		СобытияПриемУвольнение = Новый Массив;
		СобытияПриемУвольнение.Добавить(Перечисления.ВидыКадровыхСобытий.Прием);
		СобытияПриемУвольнение.Добавить(Перечисления.ВидыКадровыхСобытий.Увольнение);
		
		// Получим таблицу событий прием и увольнения.
		ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(ИзмеренияДаты);
		
		ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистра();
		ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыПостроения.Отборы, "ВидСобытия","В", СобытияПриемУвольнение);
	
		ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистра(
			"КадроваяИсторияСотрудников",
			Запрос.МенеджерВременныхТаблиц,
			Истина,
			ОписаниеФильтра,
			ПараметрыПостроения);
			
		Запрос.УстановитьПараметр("СохранитьДатыПриема", СохранитьДатыПриема);
			
		КадровыйУчетВнутренний.ОбновитьТекущиеКадровыеДанныеСотрудников(Запрос);
		
КонецПроцедуры

Процедура ИсключитьПовторениеЗаписейТекущихДанныхСотрудников(ИмяРегистраТекущихСведений, ОтключитьБизнесЛогику = Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	РегистрСведений.Сотрудник,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ РегистрСведений.ТекущаяОрганизация) КАК КоличествоЗаписей
		|ПОМЕСТИТЬ ВТСотрудники
		|ИЗ
		|	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК РегистрСведений
		|
		|СГРУППИРОВАТЬ ПО
		|	РегистрСведений.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Сотрудники.Сотрудник
		|ПОМЕСТИТЬ ВТСотрудникиСПовторением
		|ИЗ
		|	ВТСотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.КоличествоЗаписей > 1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	СотрудникиСПовторением.Сотрудник
		|ИЗ
		|	ВТСотрудникиСПовторением КАК СотрудникиСПовторением";
		
	Если ВРег(ИмяРегистраТекущихСведений) <> ВРег("ТекущиеКадровыеДанныеСотрудников") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ТекущиеКадровыеДанныеСотрудников", ИмяРегистраТекущихСведений);
	КонецЕсли; 
		
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ОписаниеРегистра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеРегистра(ИмяРегистраТекущихСведений);
		
		ВыборкаПолей = "МАКСИМУМ(РегистрСведений.ТекущаяОрганизация) КАК ТекущаяОрганизация";
			
		Для каждого ИмяПоля Из ОписаниеРегистра.Ресурсы Цикл
			ВыборкаПолей = ВыборкаПолей + ",
			|	МАКСИМУМ(РегистрСведений." + ИмяПоля + ") КАК " + ИмяПоля;
		КонецЦикла;
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	РегистрСведений.Сотрудник,
			|	ВЫРАЗИТЬ(РегистрСведений.Сотрудник КАК Справочник.Сотрудники).ФизическоеЛицо КАК ФизическоеЛицо,
			|	ВЫРАЗИТЬ(РегистрСведений.Сотрудник КАК Справочник.Сотрудники).ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	МАКСИМУМ(РегистрСведений.ТекущаяОрганизация) КАК ТекущаяОрганизация
			|ИЗ
			|	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК РегистрСведений
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиСПовторением КАК СотрудникиСПовторением
			|		ПО РегистрСведений.Сотрудник = СотрудникиСПовторением.Сотрудник
			|
			|СГРУППИРОВАТЬ ПО
			|	РегистрСведений.Сотрудник,
			|	ВЫРАЗИТЬ(РегистрСведений.Сотрудник КАК Справочник.Сотрудники).ФизическоеЛицо,
			|	ВЫРАЗИТЬ(РегистрСведений.Сотрудник КАК Справочник.Сотрудники).ГоловнаяОрганизация";
			
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "МАКСИМУМ(РегистрСведений.ТекущаяОрганизация) КАК ТекущаяОрганизация", ВыборкаПолей);
				
		Если ВРег(ИмяРегистраТекущихСведений) <> ВРег("ТекущиеКадровыеДанныеСотрудников") Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "ТекущиеКадровыеДанныеСотрудников", ИмяРегистраТекущихСведений);
		КонецЕсли; 
			
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Набор = РегистрыСведений[ИмяРегистраТекущихСведений].СоздатьНаборЗаписей();
			Набор.Отбор.Сотрудник.Установить(Выборка.Сотрудник);
			
			ЗаполнитьЗначенияСвойств(Набор.Добавить(), Выборка);
			
			Если ОтключитьБизнесЛогику Тогда
				
				Набор.ОбменДанными.Загрузка = Истина;
				Если ВРег(ИмяРегистраТекущихСведений) = ВРег("ТекущиеКадровыеДанныеСотрудников") Тогда
					РегистрыСведений.ТекущиеКадровыеДанныеСотрудников.УстановитьОсновноеРабочееМестоВОрганизации(Набор);
				КонецЕсли; 
				
			КонецЕсли; 
			
			Набор.Записать();
			
		КонецЦикла; 
		
	КонецЕсли; 
	
КонецПроцедуры

Функция ПараметрыОтбораПоОрганизациям(ПараметрыПостроения) Экспорт
	
	ПараметрыОтбора = Новый Структура("ГоловныеОрганизации,Организации");
	
	Если ЗначениеЗаполнено(ПараметрыПостроения.Организация) Тогда
		
		Если ТипЗнч(ПараметрыПостроения.Организация) = Тип("СправочникСсылка.Организации") Тогда
			ПараметрыОтбора.Организации = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПараметрыПостроения.Организация);
		Иначе
			ПараметрыОтбора.Организации = ПараметрыПостроения.Организация;
		КонецЕсли;
				
		Для каждого Организация Из ПараметрыОтбора.Организации Цикл
			
			ГоловнаяОрганизация = ЗарплатаКадрыПовтИсп.ГоловнаяОрганизация(Организация);
			Если ПараметрыОтбора.ГоловныеОрганизации = Неопределено Тогда
				ПараметрыОтбора.ГоловныеОрганизации = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ГоловнаяОрганизация);
			Иначе
				ПараметрыОтбора.ГоловныеОрганизации.Добавить(ГоловнаяОрганизация);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ПараметрыОтбора;
	
КонецФункции

// Формирует движения по прекращению вычетов личных и вычетов на детей
//
// Параметры:
//		Движения			- КоллекцияДвижений
//		ФизическиеЛица		- СправочникСсылка.ФизическиеЛица
//							- Массив (СправочникСсылка.ФизическиеЛица)
//		Организация			- СправочникСсылка.Организации
//		МесяцРегистрации	- Дата
//		Отказ				- Булево
//
Процедура СформироватьПрекращениеВычетовСтандартныхИНаДетей(Движения, ФизическиеЛица, Организация, МесяцРегистрации, Отказ) Экспорт
	
	Если ТипЗнч(ФизическиеЛица) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		СписокФизическихЛиц = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическиеЛица);
	Иначе
		СписокФизическихЛиц = ФизическиеЛица;
	КонецЕсли;
	
	ГоловнаяОрганизация = ЗарплатаКадрыПовтИсп.ГоловнаяОрганизация(Организация);
	
	ТаблицаВычетыНаДетей = Новый ТаблицаЗначений;
	ТаблицаВычетыНаДетей.Колонки.Добавить("ФизическоеЛицо");
	ТаблицаВычетыНаДетей.Колонки.Добавить("МесяцРегистрации");
	ТаблицаВычетыНаДетей.Колонки.Добавить("КодВычета");
	ТаблицаВычетыНаДетей.Колонки.Добавить("ДатаДействия");
	ТаблицаВычетыНаДетей.Колонки.Добавить("КоличествоДетей");
	ТаблицаВычетыНаДетей.Колонки.Добавить("ДействуетДо");
	ТаблицаВычетыНаДетей.Колонки.Добавить("КоличествоДетейПоОкончании");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Месяц",					МесяцРегистрации);
	Запрос.УстановитьПараметр("ФизическиеЛица",			СписокФизическихЛиц);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",	ГоловнаяОрганизация);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СтандартныеВычетыНаДетейНДФЛ.ФизическоеЛицо
		|ИЗ
		|	РегистрСведений.СтандартныеВычетыФизическихЛицНДФЛ КАК СтандартныеВычетыНаДетейНДФЛ
		|ГДЕ
		|	СтандартныеВычетыНаДетейНДФЛ.Период < &Месяц
		|	И СтандартныеВычетыНаДетейНДФЛ.ФизическоеЛицо В(&ФизическиеЛица)
		|	И СтандартныеВычетыНаДетейНДФЛ.КодВычетаЛичный <> ЗНАЧЕНИЕ(Справочник.ВидыВычетовНДФЛ.ПустаяСсылка)
		|	И СтандартныеВычетыНаДетейНДФЛ.Регистратор.Организация.ГоловнаяОрганизация = &ГоловнаяОрганизация";
	
	ВычетыЛичныеВыборка = Запрос.Выполнить().Выбрать();
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СтандартныеВычетыНаДетейНДФЛ.КодВычета,
		|	СтандартныеВычетыНаДетейНДФЛ.ФизическоеЛицо
		|ИЗ
		|	РегистрСведений.СтандартныеВычетыНаДетейНДФЛ КАК СтандартныеВычетыНаДетейНДФЛ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			МАКСИМУМ(СтандартныеВычетыНаДетейНДФЛ.МесяцРегистрации) КАК МесяцРегистрации
		|		ИЗ
		|			РегистрСведений.СтандартныеВычетыНаДетейНДФЛ КАК СтандартныеВычетыНаДетейНДФЛ
		|		ГДЕ
		|			СтандартныеВычетыНаДетейНДФЛ.МесяцРегистрации < &Месяц
		|			И СтандартныеВычетыНаДетейНДФЛ.ФизическоеЛицо В(&ФизическиеЛица)) КАК ВложенныйЗапрос
		|		ПО СтандартныеВычетыНаДетейНДФЛ.МесяцРегистрации = ВложенныйЗапрос.МесяцРегистрации
		|			И (СтандартныеВычетыНаДетейНДФЛ.ФизическоеЛицо В (&ФизическиеЛица))
		|ГДЕ
		|	СтандартныеВычетыНаДетейНДФЛ.КоличествоДетей > 0
		|	И СтандартныеВычетыНаДетейНДФЛ.ДействуетДо >= &Месяц
		|	И СтандартныеВычетыНаДетейНДФЛ.Регистратор.Организация.ГоловнаяОрганизация = &ГоловнаяОрганизация";
	
	ВычетыНаДетейВыборка = Запрос.Выполнить().Выбрать();
	
	Для каждого ФизическоеЛицо Из СписокФизическихЛиц Цикл
		
		ДанныеОВычетах = Новый Структура(
			"МесяцРегистрации,ФизическоеЛицо,ГоловнаяОрганизация,
			|ИзменитьВычетыНаДетей,ВычетыНаДетей,
			|ИзменитьЛичныйВычет,КодВычетаЛичный");
		
		// Общие сведения для прекращения вычетов
		ДанныеОВычетах.МесяцРегистрации = МесяцРегистрации;
		ДанныеОВычетах.ФизическоеЛицо = ФизическоеЛицо;
		ДанныеОВычетах.ГоловнаяОрганизация = ГоловнаяОрганизация;
		
		СтруктураПоиска = Новый Структура("ФизическоеЛицо", ФизическоеЛицо);
		
		// Личные вычеты
		ВычетыЛичныеВыборка.Сбросить();
		Если ВычетыЛичныеВыборка.НайтиСледующий(СтруктураПоиска) Тогда
			ДанныеОВычетах.ИзменитьЛичныйВычет = Истина;
		Иначе
			ДанныеОВычетах.ИзменитьЛичныйВычет = Ложь;
		КонецЕсли;
		
		// Вычеты на детей
		ТаблицаВычетыНаДетей.Очистить();
		
		ВычетыНаДетейВыборка.Сбросить();
		Пока ВычетыНаДетейВыборка.НайтиСледующий(СтруктураПоиска) Цикл
			ЗаписьОВычетеНаДетей					= ТаблицаВычетыНаДетей.Добавить();
			ЗаписьОВычетеНаДетей.ФизическоеЛицо		= ДанныеОВычетах.ФизическоеЛицо;
			ЗаписьОВычетеНаДетей.МесяцРегистрации	= ДанныеОВычетах.МесяцРегистрации;
			ЗаписьОВычетеНаДетей.КодВычета			= ВычетыНаДетейВыборка.КодВычета;
			ЗаписьОВычетеНаДетей.ДатаДействия		= ДанныеОВычетах.МесяцРегистрации;
		КонецЦикла;
		
		ДанныеОВычетах.Вставить("ВычетыНаДетей", ТаблицаВычетыНаДетей);
		ДанныеОВычетах.ИзменитьВычетыНаДетей = ДанныеОВычетах.ВычетыНаДетей.Количество() > 0;
		
		// Формирование движений
		Если ДанныеОВычетах.ИзменитьЛичныйВычет
			Или ДанныеОВычетах.ИзменитьВычетыНаДетей Тогда
			
			УчетНДФЛ.СформироватьПрименениеСтандартныхВычетов(Движения, Отказ, ДанныеОВычетах, , Ложь);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция НеобходимыПостоянныеКадровыеДанныеФизическихЛиц(Знач ИмяПоля) Экспорт
	
	Возврат КадровыйУчетВнутренний.НеобходимыПостоянныеКадровыеДанныеФизическихЛиц(ИмяПоля)
	
КонецФункции

Функция ЭтоОбязательноеПолеКадровыхДанныхФизическогоЛица(Знач ИмяПоля) Экспорт
	
	ПропуститьОбработку = Ложь;
	
	ИмяПоля = ВРег(ИмяПоля);
	
	Если ПустаяСтрока(ИмяПоля) 
		ИЛИ ИмяПоля = ВРег("Период")
		ИЛИ ИмяПоля = ВРег("ФизическоеЛицо") Тогда
		
		ПропуститьОбработку = Истина;
		
	КонецЕсли;
	
	Возврат ПропуститьОбработку;
	
КонецФункции

Процедура ДобавитьВТекстЗапросаОписаниеПолей(ТекстЗапроса, ТекстыОписанияПолей, СтрокаПоместить) Экспорт
	
	Если ТекстыОписанияПолей.Количество() > 0 Тогда
		
		ПозицияДляВстраиванияПолей = СтрНайти(ТекстЗапроса, СтрокаПоместить);
		
		ЧастиТекстаЗапроса = Новый Массив;
		ЧастиТекстаЗапроса.Добавить(СокрЛП(Лев(ТекстЗапроса, ПозицияДляВстраиванияПолей - 1)));
		
		ЧастиТекстаЗапроса.Добавить("," + Символы.ПС + Символы.Таб);
		
		ЧастиТекстаЗапроса.Добавить(ТекстОписанияПолейКадровыхДанных(ТекстыОписанияПолей));
		ЧастиТекстаЗапроса.Добавить(Сред(ТекстЗапроса, ПозицияДляВстраиванияПолей));
		
		ТекстЗапроса = СтрСоединить(ЧастиТекстаЗапроса, Символы.ПС);
		
	КонецЕсли;
	
КонецПроцедуры

Функция НеобходимыЗначенияРеквизитовСправочникаФизическиеЛица(Знач ИмяПоля) Экспорт
	
	Возврат КадровыйУчетВнутренний.НеобходимыЗначенияРеквизитовСправочникаФизическиеЛица(ИмяПоля);
	
КонецФункции

// Контактная информация

Функция НеобходимаКонтактнаяИнформацияФизическогоЛица(Знач ИмяПоля) Экспорт
	
	Возврат КадровыйУчетВнутренний.НеобходимаКонтактнаяИнформацияФизическогоЛица(ИмяПоля);
		
КонецФункции

#КонецОбласти


#Область КадровыеДанныеСотрудников

Функция ЭтоОбязательноеПолеКадровыхДанныхСотрудника(Знач ИмяПоля) Экспорт
	
	ПропуститьОбработку = Ложь;
	
	ИмяПоля = ВРег(ИмяПоля);
	
	Если ПустаяСтрока(ИмяПоля) 
		Или ИмяПоля = ВРег("Период")
		Или ИмяПоля = ВРег("Сотрудник")
		Или ИмяПоля = ВРег("ФизическоеЛицо") Тогда
		
		ПропуститьОбработку = Истина;
		
	КонецЕсли;
	
	Возврат ПропуститьОбработку;
	
КонецФункции

Функция ЭтоОбработанноеПолеКадровыхДанных(Знач ИмяПоля, ИсточникиДанных, Знач ИмяКоллекцииОбработанныеПоля) Экспорт
	
	ОбработанныеПоля = ИсточникиДанных.Получить(ИмяКоллекцииОбработанныеПоля);
	Если ОбработанныеПоля = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли; 
	
	Возврат ОбработанныеПоля.Получить(ВРег(ИмяПоля)) <> Неопределено;
	
КонецФункции

Процедура ДобавитьВКоллекциюИмяОбработанныхКадровыхДанных(Знач ИмяПоля, ИсточникиДанных, Знач ИмяКоллекцииОбработанныеПоля) Экспорт
	
	ОбработанныеПоля = ИсточникиДанных.Получить(ИмяКоллекцииОбработанныеПоля);
	Если ОбработанныеПоля = Неопределено Тогда
		ОбработанныеПоля = Новый Соответствие;
	КонецЕсли; 
	
	ОбработанныеПоля.Вставить(ВРег(ИмяПоля), Истина);
	
	ИсточникиДанных.Вставить(ИмяКоллекцииОбработанныеПоля, ОбработанныеПоля);
	
КонецПроцедуры

Функция НеобходимыПостоянныеКадровыеДанныеСотрудников(Знач ИмяПоля) Экспорт
	
	Возврат КадровыйУчетВнутренний.НеобходимыПостоянныеКадровыеДанныеСотрудников(ИмяПоля)
	
КонецФункции

Функция ПутьКДаннымСведенийЗависящихОтФункциональныхОпцийОрганизаций(Знач ИмяПоля) Экспорт
	
	Возврат КадровыйУчетВнутренний.ПутьКДаннымСведенийЗависящихОтФункциональныхОпцийОрганизаций(ИмяПоля);
	
КонецФункции

Функция НеобходимыСведенияЗависящиеОтФункциональныхОпцийОрганизации(Знач ИмяПоля) Экспорт
	
	ИмяПоля = ВРег(ИмяПоля);
	
	Возврат ИмяПоля = ВРег("ЯвляетсяЧленомЛетногоЭкипажа")
		Или ИмяПоля = ВРег("ЯвляетсяШахтером")
		Или ИмяПоля = ВРег("ЯвляетсяФармацевтом")
		Или ИмяПоля = ВРег("ЯвляетсяПрокурором")
		Или ИмяПоля = ВРег("ЯвляетсяСудьей")
		Или ИмяПоля = ВРег("ЯвляетсяВоеннослужащим")
		Или ИмяПоля = ВРег("ЯвляетсяВоеннослужащимПоПризыву")
		Или ИмяПоля = ВРег("РаботаетВСтуденческомОтряде")
		Или ИмяПоля = ВРег("ЯвляетсяРаботникомСДосрочнойПенсией")
		Или ИмяПоля = ВРег("ЯвляетсяЧленомЭкипажаСуднаПодФлагомРФ");
	
КонецФункции

Функция ИмяФункциональнойОпцииПоИмениДанныхСотрудника(Знач ИмяПоля) Экспорт
	
	Возврат КадровыйУчетВнутренний.ИмяФункциональнойОпцииПоИмениДанныхСотрудника(ИмяПоля);
	
КонецФункции

Функция ИмяФункциональнойОпцииОрганизацииПоИмениДанныхСотрудника(Знач ИмяПоля, ИсточникиДанных) Экспорт
	
	Возврат КадровыйУчетВнутренний.ИмяФункциональнойОпцииОрганизацииПоИмениДанныхСотрудника(ИмяПоля, ИсточникиДанных);
	
КонецФункции

Процедура ДобавитьТекстыЗапросовВТСведенийЗависящихОтФункциональныхОпцийОрганизаций(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных) Экспорт
	
	КадровыйУчетВнутренний.ДобавитьТекстыЗапросовВТСведенийЗависящихОтФункциональныхОпцийОрганизаций(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных);
	
КонецПроцедуры

Функция ИсточникИнформацииОКлассахУсловийТруда() Экспорт
	
	Возврат КадровыйУчетВнутренний.ИсточникИнформацииОКлассахУсловийТруда();
	
КонецФункции

// Текущие кадровые данные

Функция НеобходимыСведенияТекущихКадровыхДанныхСотрудников(Знач ИмяПоля) Экспорт
	
	Возврат КадровыйУчетВнутренний.НеобходимыСведенияТекущихКадровыхДанныхСотрудников(ИмяПоля);
	
КонецФункции

Функция ПутьКДаннымСведенийТекущихКадровыхДанныхСотрудников(Знач ИмяПоля) Экспорт
	
	Возврат КадровыйУчетВнутренний.ПутьКДаннымСведенийТекущихКадровыхДанныхСотрудников(ИмяПоля);
	
КонецФункции

Функция НеобходимыСведенияТекущихТарифныхСтавкахСотрудников(Знач ИмяПоля) Экспорт
	
	Возврат КадровыйУчетВнутренний.НеобходимыСведенияТекущихТарифныхСтавкахСотрудников(ИмяПоля);
	
КонецФункции

Функция ПутьКДаннымСведенийТекущихТарифныхСтавокСотрудников(Знач ИмяПоля) Экспорт
	
	Возврат КадровыйУчетВнутренний.ПутьКДаннымСведенийТекущихТарифныхСтавокСотрудников(ИмяПоля);
	
КонецФункции

// Сведения о приказах на прием и увольнение.

Функция ЗапросВТСведенийПриказовОПриеме(ИмяВТКадроваяИсторияСотрудников, ТолькоРазрешенные, ИмяВТСведенийПриказовОПриеме = "ВТСведенийПриказовОПриеме") Экспорт
	
	Возврат КадровыйУчетВнутренний.ЗапросВТСведенийПриказовОПриеме(ИмяВТКадроваяИсторияСотрудников, ТолькоРазрешенные, ИмяВТСведенийПриказовОПриеме);
	
КонецФункции

Функция ЗапросВТСведенийПриказовОбУвольнении(ИмяВТКадроваяИсторияСотрудников, ТолькоРазрешенные, ИмяВТСведенийПриказовОбУвольнении = "ВТСведенийПриказовОбУвольнении") Экспорт
	
	Возврат КадровыйУчетВнутренний.ЗапросВТСведенийПриказовОбУвольнении(ИмяВТКадроваяИсторияСотрудников, ТолькоРазрешенные, ИмяВТСведенийПриказовОбУвольнении);
	
КонецФункции

Функция НеобходимыСведенияПриказаОПриеме(Знач ИмяПоля) Экспорт
	
	Возврат КадровыйУчетВнутренний.НеобходимыСведенияПриказаОПриеме(ИмяПоля);
	
КонецФункции

Функция ПутьКДаннымСведенийПриказаОПриемеОбУвольнении(Знач ИмяПоля) Экспорт
	
	Возврат КадровыйУчетВнутренний.ПутьКДаннымСведенийПриказаОПриемеОбУвольнении(ИмяПоля);
	
КонецФункции

// Сведения кадровой истории сотрудников.

Функция НеобходимыСведенияКадровойИсторииСотрудников(Знач ИмяПоля) Экспорт
	
	Возврат КадровыйУчетВнутренний.НеобходимыСведенияКадровойИсторииСотрудников(ИмяПоля);
	
КонецФункции

Функция ПутьКДаннымСведенийКадровойИсторииСотрудников(Знач ИмяПоля) Экспорт
	
	Возврат КадровыйУчетВнутренний.ПутьКДаннымСведенийКадровойИсторииСотрудников(ИмяПоля);
	
КонецФункции

Процедура ДобавитьТекстЗапросаВТСведенияКадроваяИсторияСотрудников(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИсточникиДанных) Экспорт
	
	КадровыйУчетВнутренний.ДобавитьТекстЗапросаВТСведенияКадроваяИсторияСотрудников(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИсточникиДанных);
	
КонецПроцедуры

// Сведения об оплате труда

Функция НеобходимыСведенияОбОплатеТруда(Знач ИмяПоля) Экспорт
	
	Возврат КадровыйУчетВнутренний.НеобходимыСведенияОбОплатеТруда(ИмяПоля);
	
КонецФункции

Функция ПутьКДаннымСведенийОбОплатеТруда(Знач ИмяПоля) Экспорт
	
	Возврат КадровыйУчетВнутренний.ПутьКДаннымСведенийОбОплатеТруда(ИмяПоля);
	
КонецФункции

#КонецОбласти


#Область ПроцедурыИФункцииФормированияОтчетов

Функция ПараметрыКадровогоПриказа() Экспорт
	
	СтруктураПараметровКадровогоПриказа = Новый Структура;
	
	СтруктураПараметровКадровогоПриказа.Вставить("Ссылка");
	СтруктураПараметровКадровогоПриказа.Вставить("НазваниеОрганизации", "");
	СтруктураПараметровКадровогоПриказа.Вставить("КодПоОКПО", "");
	СтруктураПараметровКадровогоПриказа.Вставить("НомерДок", "");
	СтруктураПараметровКадровогоПриказа.Вставить("ДатаДок", '00010101');
	СтруктураПараметровКадровогоПриказа.Вставить("Работник", "");
	СтруктураПараметровКадровогоПриказа.Вставить("Пол");
	СтруктураПараметровКадровогоПриказа.Вставить("ТрудовойДоговорЧисло", "    ");
	СтруктураПараметровКадровогоПриказа.Вставить("ТрудовойДоговорМесяцГод", "______________ 20__");
	СтруктураПараметровКадровогоПриказа.Вставить("ТрудовойДоговорДата", '00010101');
	СтруктураПараметровКадровогоПриказа.Вставить("ТрудовойДоговорНомер", "____");
	СтруктураПараметровКадровогоПриказа.Вставить("ТабельныйНомер", "");
	СтруктураПараметровКадровогоПриказа.Вставить("Подразделение");
	СтруктураПараметровКадровогоПриказа.Вставить("Должность");
	СтруктураПараметровКадровогоПриказа.Вставить("РазрядКатегория");
	СтруктураПараметровКадровогоПриказа.Вставить("ДолжностьРуководителя", "");
	СтруктураПараметровКадровогоПриказа.Вставить("РуководительРасшифровкаПодписи", "");
	
	Возврат СтруктураПараметровКадровогоПриказа;
		
КонецФункции

Процедура ЗаполнитьПараметрыКадровогоПриказа(Параметры, КадровыеДанные, УчитыватьНастройкиФормированияПечатныхФорм = Истина) Экспорт
	
	ЗаполнитьЗначенияСвойств(Параметры, КадровыеДанные);
	
	НастройкиПечатныхФорм = ЗарплатаКадрыПовтИсп.НастройкиПечатныхФорм();
	
	Если НЕ УчитыватьНастройкиФормированияПечатныхФорм
		ИЛИ НастройкиПечатныхФорм.УдалятьПрефиксыОрганизацииИИБИзНомеровКадровыхПриказов Тогда
		
		Параметры.НомерДок = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Параметры.НомерДок, Истина, Истина);
		
	КонецЕсли;
	
	Если УчитыватьНастройкиФормированияПечатныхФорм
		И НастройкиПечатныхФорм.ВыводитьПолнуюИерархиюПодразделений
		И ЗначениеЗаполнено(Параметры.Подразделение) Тогда
		
		Параметры.Подразделение = Параметры.Подразделение.ПолноеНаименование();
		
	КонецЕсли; 
	
	Параметры.ДатаДок = ?(ЗначениеЗаполнено(Параметры.ДатаДок), Параметры.ДатаДок, ТекущаяДатаСеанса());
	Параметры.ТабельныйНомер = ПрефиксацияОбъектовКлиентСервер.УдалитьПрефиксыИзНомераОбъекта(Параметры.ТабельныйНомер, Истина, Истина);
	
	Если ЗначениеЗаполнено(Параметры.ТрудовойДоговорДата) Тогда
		
		ПредставлениеДатыТрудовогоДоговора  = Формат(Параметры.ТрудовойДоговорДата, "ДЛФ=DD");
		
		Параметры.ТрудовойДоговорЧисло = СокрЛП(Лев(ПредставлениеДатыТрудовогоДоговора,2));
		Параметры.ТрудовойДоговорМесяцГод = СокрЛП(Прав(ПредставлениеДатыТрудовогоДоговора, СтрДлина(ПредставлениеДатыТрудовогоДоговора)-2));
		
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(Параметры.РазрядКатегория) Тогда
		Параметры.РазрядКатегория = ", " + Параметры.РазрядКатегория;
	КонецЕсли; 
	
КонецПроцедуры	

Функция ДополнительныеСведенияУнифицированнойФормыТ2(СтрокиДанных, ДатаОтчета, КлючВарианта) Экспорт
	
	Возврат КадровыйУчетВнутренний.ДополнительныеСведенияУнифицированнойФормыТ2(СтрокиДанных, ДатаОтчета, КлючВарианта)
	
КонецФункции

Функция СформироватьЗапросДляТ1(МассивОбъектов) Экспорт
	
	Возврат КадровыйУчетВнутренний.СформироватьЗапросДляТ1(МассивОбъектов);
	
КонецФункции

Функция СформироватьЗапросДляТ5(МассивОбъектов) Экспорт
	
	Возврат КадровыйУчетВнутренний.СформироватьЗапросДляТ5(МассивОбъектов);
	
КонецФункции

Функция СформироватьЗапросДляТ6(МассивОбъектов) Экспорт
	
	Возврат КадровыйУчетВнутренний.СформироватьЗапросДляТ6(МассивОбъектов);
	
КонецФункции

Функция СформироватьЗапросДляТ8(МассивОбъектов) Экспорт
	
	Возврат КадровыйУчетВнутренний.СформироватьЗапросДляТ8(МассивОбъектов);		
	
КонецФункции	

Процедура СоздатьВТДанныеДокументовКадровыхПеремещений(МенеджерВременныхТаблиц, МассивОбъектов) Экспорт
	
	КадровыйУчетВнутренний.СоздатьВТДанныеДокументовКадровыхПеремещений(МенеджерВременныхТаблиц, МассивОбъектов);
	
КонецПроцедуры

Процедура СоздатьВТДанныеДокументовПриемНаРаботу(МенеджерВременныхТаблиц, МассивОбъектов) Экспорт
	
	КадровыйУчетВнутренний.СоздатьВТДанныеДокументовПриемНаРаботу(МенеджерВременныхТаблиц, МассивОбъектов);
	
КонецПроцедуры

Процедура СоздатьВТДанныеДокументовУвольнение(МенеджерВременныхТаблиц, МассивОбъектов) Экспорт
	
	КадровыйУчетВнутренний.СоздатьВТДанныеДокументовУвольнение(МенеджерВременныхТаблиц, МассивОбъектов);
	
КонецПроцедуры

Функция КадровыеДанныеДляПечатиКадровыхПриказов() Экспорт
	
	Возврат КадровыйУчетВнутренний.КадровыеДанныеДляПечатиКадровыхПриказов();
	
КонецФункции

Функция ОписанияСоставаНачисленийПоВременнойТаблице(МенеджерВременныхТаблиц, ИмяВТСотрудникиПериоды, ИмяПоляПериод = "Период", ИмяПоляСотрудник = "Сотрудник", ИмяПоляДатаОкончания = "", ИспользоватьКраткиеПредставления = Истина, ВыводитьДенежныеПоказателиСВалютой = Ложь) Экспорт
	
	Возврат КадровыйУчетВнутренний.ОписанияСоставаНачисленийПоВременнойТаблице(МенеджерВременныхТаблиц, ИмяВТСотрудникиПериоды, ИмяПоляПериод, ИмяПоляСотрудник, ИмяПоляДатаОкончания, ИспользоватьКраткиеПредставления, ВыводитьДенежныеПоказателиСВалютой);
	
КонецФункции

Функция ЗапросВТТарифныеСтавкиСотрудников(ТолькоРазрешенные, ИмяВТТарифныеСтавкиСотрудников, ИмяВременнойТаблицыОтборовСотрудников, ПоляОтбораСотрудников, ПоляОтбораПериодическихДанных) Экспорт
	
	Возврат КадровыйУчетВнутренний.ЗапросВТТарифныеСтавкиСотрудников(ТолькоРазрешенные, ИмяВТТарифныеСтавкиСотрудников, ИмяВременнойТаблицыОтборовСотрудников, ПоляОтбораСотрудников, ПоляОтбораПериодическихДанных);
	
КонецФункции

Функция ЗапросВТПлановыеНачисленияСотрудников(ТолькоРазрешенные, ИмяВТПлановыеНачисленияСотрудников, ИмяВременнойТаблицыОтборовСотрудников, Знач ПоляОтбораСотрудников = "Сотрудник,Период", ПоляОтбораПериодическихДанных = Неопределено, ФормироватьСПериодичностьДень = Истина) Экспорт
	
	Возврат КадровыйУчетВнутренний.ЗапросВТПлановыеНачисленияСотрудников(ТолькоРазрешенные, ИмяВТПлановыеНачисленияСотрудников, ИмяВременнойТаблицыОтборовСотрудников, ПоляОтбораСотрудников, ПоляОтбораПериодическихДанных, ФормироватьСПериодичностьДень);
	
КонецФункции

Функция НеобходимыКадровыеДанныеФизическогоЛица(Знач ИмяПоля) Экспорт
	
	Возврат КадровыйУчетВнутренний.НеобходимыКадровыеДанныеФизическогоЛица(ИмяПоля);
	
КонецФункции

Функция ЗапросВТСотрудникиОрганизации(ТолькоРазрешенные, ИмяВТСотрудникиОрганизации, Параметры, МенеджерВременныхТаблиц = Неопределено) Экспорт
	
	ПараметрыПолученияСотрудников = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(Параметры);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.УправленческаяЗарплата") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("УправленческаяЗарплата");
		Модуль.УточнитьПараметрыПолученияСотрудников(ПараметрыПолученияСотрудников);
	КонецЕсли;
	
	ИмяВТСотрудникиПериоды = "ВТСотрудникиПериодыДляПолученияСотрудниковОрганизации";
	Запрос = КадровыйУчетВнутренний.ЗапросВТОтборСотрудниковОрганизации(ТолькоРазрешенные, ПараметрыПолученияСотрудников, ИмяВТСотрудникиПериоды);
	
	Если МенеджерВременныхТаблиц <> Неопределено Тогда
		
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
		Запрос.Текст = "";
		
	КонецЕсли;
	
	ЗапросПоРабочимМестам = КадровыйУчетВнутренний.ЗапросВТСотрудникиОрганизацииПоРабочимМестам(
		ТолькоРазрешенные, ИмяВТСотрудникиОрганизации, ИмяВТСотрудникиПериоды, ПараметрыПолученияСотрудников, МенеджерВременныхТаблиц);
	
	ЗарплатаКадрыОбщиеНаборыДанных.ОбъединитьЗапросы(Запрос, ЗапросПоРабочимМестам);
	
	ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВЗапросСозданияВременнойТаблицыТекстУничтоженияВспомогательной(
		Запрос.Текст, ИмяВТСотрудникиПериоды);
	
	Возврат Запрос;
	
КонецФункции

Функция ЗапросВТСотрудникиОрганизацииПоРабочимМестам(ТолькоРазрешенные, ИмяВТСотрудникиОрганизации, ИмяВТСотрудникиПериоды, Параметры, МенеджерВременныхТаблиц = Неопределено) Экспорт
	
	Возврат КадровыйУчетВнутренний.ЗапросВТСотрудникиОрганизацииПоРабочимМестам(ТолькоРазрешенные, ИмяВТСотрудникиОрганизации, ИмяВТСотрудникиПериоды, Параметры, МенеджерВременныхТаблиц);
	
КонецФункции

// Создает временную таблицу содержащую сострудников физических лиц, окончивших работу
// ранее на начало периода, переданного в параметре ПараметрыПолученияСотрудников.
//
// Параметры:
//		МенеджерВременныхТаблиц			- МенеджерВременныхТаблиц
//		ТолькоРазрешенные				- Булево
//		ФизическиеЛица					- Массив, СправочникСсылка.ФизическоеЛицо
//		ПараметрыПолученияСотрудников	- Структура, см Функцию ПараметрыПолученияВсехСотрудниковОрганизацийПоСпискуФизическихЛиц
//		ИмяВТПоследниеСотрудники		- Строка
//
Процедура СоздатьВТПоследниеРаботавшиеСотрудникиФизическихЛиц(МенеджерВременныхТаблиц, ТолькоРазрешенные, ФизическиеЛица, ПараметрыПолученияСотрудников, ИмяВТПоследниеСотрудники = "ВТПоследниеСотрудники") Экспорт
	
	ПараметрыПолучения = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ПараметрыПолученияСотрудников);
	ПараметрыПолучения.ОтбиратьПоГоловнойОрганизации = Истина;
	
	ДатаПолучения = ПараметрыПолучения.НачалоПериода;
	ГоловнаяОрганизация = ЗарплатаКадрыПовтИсп.ГоловнаяОрганизация(ПараметрыПолучения.Организация);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ДатаПолучения", ДатаПолучения);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("ФизическиеЛица", ФизическиеЛица);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо КАК ФизическоеЛицо,
		|	МАКСИМУМ(ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения) КАК ДатаУвольнения
		|ПОМЕСТИТЬ ВТФизическиеЛицаСПоследнимиДатамиУвольнения
		|ИЗ
		|	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
		|ГДЕ
		|	ТекущиеКадровыеДанныеСотрудников.ГоловнаяОрганизация = &ГоловнаяОрганизация
		|	И ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо В(&ФизическиеЛица)
		|	И ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения > ДАТАВРЕМЯ(1, 1, 1)
		|	И ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения < &ДатаПолучения
		|
		|СГРУППИРОВАТЬ ПО
		|	ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТекущиеКадровыеДанныеСотрудников.Сотрудник КАК Сотрудник,
		|	ДОБАВИТЬКДАТЕ(ФизическиеЛицаСПоследнимиДатамиУвольнения.ДатаУвольнения, МЕСЯЦ, -1) КАК ДатаНачала,
		|	ФизическиеЛицаСПоследнимиДатамиУвольнения.ДатаУвольнения КАК ДатаОкончания
		|ПОМЕСТИТЬ ВТСотрудникиПериодыДляПолученияПоследнихСотрудников
		|ИЗ
		|	ВТФизическиеЛицаСПоследнимиДатамиУвольнения КАК ФизическиеЛицаСПоследнимиДатамиУвольнения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
		|		ПО ФизическиеЛицаСПоследнимиДатамиУвольнения.ФизическоеЛицо = ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо
		|			И ФизическиеЛицаСПоследнимиДатамиУвольнения.ДатаУвольнения >= ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения
		|			И (ДОБАВИТЬКДАТЕ(ФизическиеЛицаСПоследнимиДатамиУвольнения.ДатаУвольнения, МЕСЯЦ, -1) <= ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТФизическиеЛицаСПоследнимиДатамиУвольнения";
	
	ЗарплатаКадрыОбщиеНаборыДанных.УстановитьВыборкуТолькоРазрешенныхДанных(Запрос.Текст, ТолькоРазрешенные);
	
	Запрос.Выполнить();
	
	ЗапросСотрудникиБезБазы = ЗапросВТСотрудникиОрганизацииПоРабочимМестам(ТолькоРазрешенные, ИмяВТПоследниеСотрудники,
		"ВТСотрудникиПериодыДляПолученияПоследнихСотрудников", ПараметрыПолучения, МенеджерВременныхТаблиц);
	
	ЗапросСотрудникиБезБазы.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ЗапросСотрудникиБезБазы.Выполнить();
	
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, "ВТСотрудникиПериодыДляПолученияПоследнихСотрудников");
	
КонецПроцедуры

// Кадровые данные

Процедура ЗапомнитьСотрудниковДляОбновленияКадровыхДанных(Источник) Экспорт
	
	СотрудникиДляОбновления = СотрудникиДляОбновления(Источник);
	ЗапомнитьСписокСотрудниковДляОбновленияКадровыхДанных(Источник, СотрудникиДляОбновления);
	
КонецПроцедуры

Процедура ЗапомнитьСписокСотрудниковДляОбновленияКадровыхДанных(Источник, СотрудникиДляОбновления)
	
	Если СотрудникиДляОбновления.Количество() > 0 Тогда
		Источник.ДополнительныеСвойства.Вставить("СотрудникиДляОбновленияКадровыхДанных", СотрудникиДляОбновления);
	КонецЕсли;
	
КонецПроцедуры

// Данные об оплате труда

Процедура ЗапомнитьСотрудниковДляОбновленияДанныхОбОплатеТруда(Источник) Экспорт
	
	СотрудникиДляОбновления = СотрудникиДляОбновления(Источник);
	ЗапомнитьСписокСотрудниковДляОбновленияДанныхОбОплатеТруда(Источник, СотрудникиДляОбновления);
	
КонецПроцедуры

Процедура ЗапомнитьСписокСотрудниковДляОбновленияДанныхОбОплатеТруда(Источник, СотрудникиДляОбновления) Экспорт
	
	Если СотрудникиДляОбновления.Количество() > 0 Тогда
		
		Если Источник.ДополнительныеСвойства.Свойство("СотрудникиДляОбновленияДанныхОбОплатеТруда") Тогда
			
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
				Источник.ДополнительныеСвойства.СотрудникиДляОбновленияДанныхОбОплатеТруда, СотрудникиДляОбновления, Истина);
			
		Иначе
			Источник.ДополнительныеСвойства.Вставить("СотрудникиДляОбновленияДанныхОбОплатеТруда", СотрудникиДляОбновления);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьВторичныеКадровыеДанныеСотрудников(НаборЗаписей) Экспорт
	
	КадровыйУчетВнутренний.ОбновитьВторичныеКадровыеДанныеСотрудников(НаборЗаписей);
	
КонецПроцедуры

Функция СотрудникиДляОбновления(НаборЗаписей)
	
	СписокСотрудников = Новый Массив;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") Тогда
		
		ИмяРегистраСведений = НаборЗаписей.Метаданные().Имя;
		
		// Обновлять записи необходимо, только при записи набора, содержащего первичные данные.
		Если Не НаборЗаписей.ДополнительныеСвойства.Свойство("ЭтоВторичныйНабор") Тогда
			
			// Соберем информацию о сотрудниках, у которых меняются данные кадровой истории.
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
			
			ИзмеренияДаты = Новый ТаблицаЗначений;
			ИзмеренияДаты.Колонки.Добавить("ДатаНачала", Новый ОписаниеТипов("Дата"));
			ИзмеренияДаты.Колонки.Добавить("ДатаОкончания", Новый ОписаниеТипов("Дата"));
			СтрокаИзмеренияДаты = ИзмеренияДаты.Добавить();
			
			ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(ИзмеренияДаты);
			
			ПараметрыПостроения = ЗарплатаКадрыОбщиеНаборыДанных.ПараметрыПостроенияДляСоздатьВТИмяРегистра();
			ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыПостроения.Отборы, "Регистратор", "=", НаборЗаписей.Отбор.Регистратор.Значение.Ссылка);
		
			ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистра(
				ИмяРегистраСведений,
				Запрос.МенеджерВременныхТаблиц,
				Истина,
				ОписаниеФильтра,
				ПараметрыПостроения,
				"ВТПредыдущиеЗначения");
			
			Запрос.УстановитьПараметр("Регистратор", НаборЗаписей.Отбор.Регистратор.Значение.Ссылка);
			Запрос.УстановитьПараметр("Источник", НаборЗаписей.Выгрузить());
			
			Запрос.Текст =
			"ВЫБРАТЬ
			|	НаборИсточник.Сотрудник
			|ПОМЕСТИТЬ ВТСохраняемыеЗначения
			|ИЗ
			|	&Источник КАК НаборИсточник
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	ВЫБОР
			|		КОГДА СохраняемыеЗначения.Сотрудник ЕСТЬ NULL 
			|			ТОГДА ПредыдущиеЗначения.Сотрудник
			|		ИНАЧЕ СохраняемыеЗначения.Сотрудник
			|	КОНЕЦ КАК Сотрудник
			|ИЗ
			|	ВТПредыдущиеЗначения КАК ПредыдущиеЗначения
			|		ПОЛНОЕ СОЕДИНЕНИЕ ВТСохраняемыеЗначения КАК СохраняемыеЗначения
			|		ПО ПредыдущиеЗначения.Сотрудник = СохраняемыеЗначения.Сотрудник";
			РезультатЗапроса = Запрос.Выполнить();
			
			Если НЕ РезультатЗапроса.Пустой() Тогда
				СписокСотрудников = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Сотрудник");
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СписокСотрудников;
	
КонецФункции

Процедура ОбновитьТекущиеТарифныеСтавки(МассивСотрудниковДляОбновления) Экспорт
	
	КадровыйУчетВнутренний.ОбновитьТекущиеТарифныеСтавки(МассивСотрудниковДляОбновления);
	
КонецПроцедуры

#КонецОбласти


#Область ПолучениеРабочихМест

// Возвращает структуру параметров для ЗапросВТРабочиеМестаСотрудниковПоСпискуСотрудников.
//
// Параметры:
//				Сотрудники - СправочникСсылка.Сотрудники, массив сотрудников, если параметр опущен
//							рабочие места будут подбираться по всем сотрудникам соответствующим отбору
//							по организации и подразделению.
//				НачалоПериода - Дата, Строка (при встраивание в тест другого запроса).
//				ОкончаниеПериода - Дата, если не указана - будут отбираться все рабочие места,
//							начиная с НачалоПериода.
//								- Строка (при встраивание в тест другого запроса).
//
// Возвращаемое значение:
//   Структура - Содержит ключи, возвращаемые методом ПараметрыДляЗапросВТРабочиеМестаСотрудников.
//				СписокСотрудников - Массив ссылок справочника сотрудники.
//				НачалоПериода - Дата, значение по умолчанию '00010101'. 
//				ОкончаниеПериода - Дата, значение по умолчанию '00010101'. При установке должно выполняться условие
//					ОкончаниеПериода >= НачалоПериода. Используется для выборки кадровых данных сотрудников, если
//					параметр не указан, выборка будет производиться на текущую дату сеанса.
//
Функция ПараметрыДляЗапросВТРабочиеМестаСотрудниковПоСпискуСотрудников(Сотрудники = Неопределено, НачалоПериода = '00010101', ОкончаниеПериода = '00010101') Экспорт
		
	ПараметрыПолученияРабочихМестСотрудников = ПараметрыДляЗапросВТРабочиеМестаСотрудников();
	
	ПараметрыПолученияРабочихМестСотрудников.Вставить("НачалоПериода", НачалоПериода);
	ПараметрыПолученияРабочихМестСотрудников.Вставить("ОкончаниеПериода", ОкончаниеПериода);
	
	Если ТипЗнч(Сотрудники) = Тип("СправочникСсылка.Сотрудники") Тогда
		СписокСотрудников = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудники);
	Иначе
		СписокСотрудников = Сотрудники;
	КонецЕсли;
	
	ПараметрыПолученияРабочихМестСотрудников.Вставить("СписокСотрудников", СписокСотрудников);

	Возврат ПараметрыПолученияРабочихМестСотрудников;
	
КонецФункции

// Возвращает структуру параметров для ЗапросВТРабочиеМестаСотрудниковПоВременнойТаблице.
//
// Параметры:
//			ИмяВТСотрудникиПериоды - Строка
//			ИмяПоляСотрудник - Строка, 
//			ИмяПоляНачалоПериода - Строка
//			ИмяПоляОкончаниеПериода - Строка
//
// Возвращаемое значение:
//   Структура - Содержит ключи, возвращаемые методом ПараметрыДляЗапросВТРабочиеМестаСотрудников
//				ИмяВТСотрудникиПериоды
//				ИмяПоляСотрудник
//				ИмяПоляНачалоПериода
//				ИмяПоляОкончаниеПериода.
//
Функция ПараметрыДляЗапросВТРабочиеМестаСотрудниковПоВременнойТаблице(ИмяВТСотрудникиПериоды = "ВТСотрудникиПериоды", ИмяПоляСотрудник = "Сотрудник", ИмяПоляНачалоПериода = "НачалоПериода", ИмяПоляОкончаниеПериода = "ОкончаниеПериода") Экспорт
		
	ПараметрыПолученияРабочихМестСотрудников = ПараметрыДляЗапросВТРабочиеМестаСотрудников();
	
	ПараметрыПолученияРабочихМестСотрудников.Вставить("ИмяВТСотрудникиПериоды", ИмяВТСотрудникиПериоды);
	ПараметрыПолученияРабочихМестСотрудников.Вставить("ИмяПоляСотрудник", ИмяПоляСотрудник);
	ПараметрыПолученияРабочихМестСотрудников.Вставить("ИмяПоляНачалоПериода", ИмяПоляНачалоПериода);
	ПараметрыПолученияРабочихМестСотрудников.Вставить("ИмяПоляОкончаниеПериода", ИмяПоляОкончаниеПериода);

	Возврат ПараметрыПолученияРабочихМестСотрудников;
	
КонецФункции

// Создает временную таблицу в переданном менеджере временных таблиц.
//
// Параметры:
//		МенеджерВременныхТаблиц 		- МенеджерВременныхТаблиц
//		ТолькоРазрешенные				- Булево
//		Параметры 						- Структура, см. функцию ПараметрыДляЗапросВТРабочиеМестаСотрудниковПоСпискуСотрудников.
//		ИмяВТРабочиеМестаСотрудников	- Строка
//
Процедура СоздатьВТРабочиеМестаСотрудников(МенеджерВременныхТаблиц, ТолькоРазрешенные, Параметры, ИмяВТРабочиеМестаСотрудников = "ВТРабочиеМестаСотрудников") Экспорт
	
	Если ПривилегированныйРежим() Тогда
		
		Запрос = ЗапросВТРабочиеМестаСотрудниковПоСпискуСотрудников(ТолькоРазрешенные, ИмяВТРабочиеМестаСотрудников, Параметры, МенеджерВременныхТаблиц);
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
	Иначе
		
		ИмяВТСотрудникиПериоды = "ВТСотрудникиПериодыДляПолученияРабочихМест";
		Запрос = ЗапросВТСотрудникиПериодыДляПолученияРабочихМестОрганизаций(ТолькоРазрешенные, ИмяВТСотрудникиПериоды, Параметры);
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
		ПараметрыПоВТ = ПараметрыДляЗапросВТРабочиеМестаСотрудниковПоВременнойТаблице();
		ЗаполнитьЗначенияСвойств(ПараметрыПоВТ, Параметры);
		ПараметрыПоВТ.ИмяВТСотрудникиПериоды = ИмяВТСотрудникиПериоды;
		
		УстановитьПривилегированныйРежим(Истина);
		СоздатьВТРабочиеМестаСотрудниковПоВременнойТаблице(МенеджерВременныхТаблиц, Ложь, ПараметрыПоВТ, ИмяВТРабочиеМестаСотрудников);
		УстановитьПривилегированныйРежим(Ложь);
		
		Запрос.Текст = "";
		
		ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВЗапросСозданияВременнойТаблицыТекстУничтоженияВспомогательной(
			Запрос.Текст, ИмяВТСотрудникиПериоды);
		
		Запрос.Выполнить();
		
	КонецЕсли;
	
КонецПроцедуры

// Создает временную таблицу в переданном менеджере временных таблиц.
//
// Параметры:
//		МенеджерВременныхТаблиц 		- МенеджерВременныхТаблиц
//		ТолькоРазрешенные				- Булево
//		Параметры 						- Структура, см. функцию ПараметрыДляЗапросВТРабочиеМестаСотрудниковПоВременнойТаблице.
//		ИмяВТРабочиеМестаСотрудников	- Строка
//
Процедура СоздатьВТРабочиеМестаСотрудниковПоВременнойТаблице(МенеджерВременныхТаблиц, ТолькоРазрешенные, Параметры, ИмяВТРабочиеМестаСотрудников = "ВТРабочиеМестаСотрудников") Экспорт
	
	Запрос = ЗапросВТРабочиеМестаСотрудниковПоВременнойТаблице(ТолькоРазрешенные, ИмяВТРабочиеМестаСотрудников, Параметры, МенеджерВременныхТаблиц);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция ЗапросВТСотрудникиПериодыДляПолученияРабочихМестОрганизаций(ТолькоРазрешенные, ИмяВТСотрудникиПериоды, Параметры)
	
	Запрос = Новый Запрос;
	
	ТекстДатаВремя = "ДАТАВРЕМЯ(1, 1, 1)";
	
	Если ТипЗнч(Параметры.НачалоПериода) = Тип("Строка") Тогда
		ТекстДатаНачала = Параметры.НачалоПериода;
	Иначе
		Если ТипЗнч(Параметры.НачалоПериода) <> Тип("Дата") Тогда
			Параметры.НачалоПериода = '00010101';
		КонецЕсли;
		ТекстДатаНачала = СтрЗаменить(ТекстДатаВремя, "1, 1, 1", Формат(Параметры.НачалоПериода, "ДФ='гггг, М, д, Ч, м, с'; ДП="));
	КонецЕсли;
	
	Если ТипЗнч(Параметры.ОкончаниеПериода) = Тип("Строка") Тогда
		ТекстДатаОкончания = Параметры.ОкончаниеПериода;
	Иначе
		Если ТипЗнч(Параметры.ОкончаниеПериода) <> Тип("Дата") Тогда
			Параметры.ОкончаниеПериода = '00010101';
		КонецЕсли;
		ТекстДатаОкончания = СтрЗаменить(ТекстДатаВремя, "1, 1, 1", Формат(Параметры.ОкончаниеПериода, "ДФ='гггг, М, д, Ч, м, с'; ДП="));
	КонецЕсли;
	
	ТекстЗапросаВТСотрудникиПериоды =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Сотрудники.Ссылка КАК Сотрудник,
		|	&НачалоПериода КАК НачалоПериода,
		|	&ОкончаниеПериода КАК ОкончаниеПериода
		|ПОМЕСТИТЬ ВТСотрудникиПериоды
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	&ТекстОтборовСотрудникиПериоды";
	
	ЗарплатаКадрыОбщиеНаборыДанных.УстановитьВыборкуТолькоРазрешенныхДанных(ТекстЗапросаВТСотрудникиПериоды, ТолькоРазрешенные);
	
	ТекстЗапросаВТСотрудникиПериоды = СтрЗаменить(ТекстЗапросаВТСотрудникиПериоды, "&НачалоПериода", ТекстДатаНачала);
	ТекстЗапросаВТСотрудникиПериоды = СтрЗаменить(ТекстЗапросаВТСотрудникиПериоды, "&ОкончаниеПериода", ТекстДатаОкончания);
	ТекстЗапросаВТСотрудникиПериоды = СтрЗаменить(ТекстЗапросаВТСотрудникиПериоды, "ВТСотрудникиПериоды", ИмяВТСотрудникиПериоды);
	
	ТекстОтборовСотрудникиПериоды = "";
	
	Если Параметры.СписокСотрудников <> Неопределено Тогда
		
		ТекстОтборовСотрудникиПериоды = "Сотрудники.Ссылка В(&СписокСотрудников)";
		Запрос.УстановитьПараметр("СписокСотрудников", Параметры.СписокСотрудников);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Организация) Тогда
		
		ТекстОтборовГоловнаяОрганизация =  "Сотрудники.ГоловнаяОрганизация = &ГоловнаяОрганизация";
		Если ПустаяСтрока(ТекстОтборовСотрудникиПериоды) Тогда
			ТекстОтборовСотрудникиПериоды = ТекстОтборовГоловнаяОрганизация;
		Иначе
			
			МассивПодстрок = Новый Массив;
			МассивПодстрок.Добавить(ТекстОтборовСотрудникиПериоды);
			МассивПодстрок.Добавить(ТекстОтборовГоловнаяОрганизация);
			
			ТекстОтборовСотрудникиПериоды = СтрСоединить(МассивПодстрок, Символы.ПС + Символы.Таб + "И ");
			
		КонецЕсли;
		
		Запрос.УстановитьПараметр("ГоловнаяОрганизация", ЗарплатаКадрыПовтИсп.ГоловнаяОрганизация(Параметры.Организация));
		
	КонецЕсли;
	
	ТекстЗапросаВТСотрудникиПериоды = СтрЗаменить(ТекстЗапросаВТСотрудникиПериоды, "&ТекстОтборовСотрудникиПериоды", ?(ПустаяСтрока(ТекстОтборовСотрудникиПериоды), "(ИСТИНА)", ТекстОтборовСотрудникиПериоды));
	
	Запрос.Текст = ТекстЗапросаВТСотрудникиПериоды;
	
	Возврат Запрос;
	
КонецФункции

Функция ЗапросВТРабочиеМестаСотрудниковПоСпискуСотрудников(ТолькоРазрешенные, ИмяВТРабочиеМестаСотрудников, Параметры, МенеджерВременныхТаблиц) Экспорт
	
	ИмяВТСотрудникиПериоды = "ВТСотрудникиПериодыДляПолученияРабочихМест";
	
	Запрос = ЗапросВТСотрудникиПериодыДляПолученияРабочихМестОрганизаций(ТолькоРазрешенные, ИмяВТСотрудникиПериоды, Параметры);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
	ПараметрыПоВТ = ПараметрыДляЗапросВТРабочиеМестаСотрудниковПоВременнойТаблице();
	ЗаполнитьЗначенияСвойств(ПараметрыПоВТ, Параметры);
	
	ПараметрыПоВТ.ИмяВТСотрудникиПериоды = ИмяВТСотрудникиПериоды;
	
	Запрос = ЗапросВТРабочиеМестаСотрудниковПоВременнойТаблице(ТолькоРазрешенные, ИмяВТРабочиеМестаСотрудников, ПараметрыПоВТ, МенеджерВременныхТаблиц);
	
	ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьЗапросУничтоженияВременнойТаблицы(
		Запрос.Текст, ИмяВТСотрудникиПериоды);
	
	Возврат Запрос;
	
КонецФункции

Функция ЗапросВТРабочиеМестаСотрудниковПоВременнойТаблице(ТолькоРазрешенные, ИмяВТРабочиеМестаСотрудников, Параметры, МенеджерВременныхТаблиц = Неопределено) Экспорт
	
	Возврат КадровыйУчетВнутренний.ЗапросВТРабочиеМестаСотрудниковПоВременнойТаблице(ТолькоРазрешенные, ИмяВТРабочиеМестаСотрудников, Параметры, МенеджерВременныхТаблиц);
	
КонецФункции

Функция ПоляВТРабочиеМестаСотрудников() Экспорт
	
	Возврат КадровыйУчетВнутренний.ПоляВТРабочиеМестаСотрудников();
	
КонецФункции

// Возвращает структуру параметров для, формирующих ВТРабочиеМестаСотрудников.
//
// Возвращаемое значение:
//   Структура - Содержит ключи:
//				Организация - СправочникСсылка.Организации, если параметр опустить в таблицу войдут все сотрудник
//					независимо от организации.
//				ОтбиратьПоГоловнойОрганизации - если Истина, то отбор по головной организации, иначе по текущей организации.
//					По умолчанию - ЛОЖЬ
//				Подразделение - СправочникСсылка.ПодразделенияОрганизаций, в таблицу войдут сотрудники указанного подразделения
//					и всех подразделений, подчиненных указанному, если параметр опустить в таблицу войдут все сотрудники.
//
Функция ПараметрыДляЗапросВТРабочиеМестаСотрудников()
	
	Возврат КадровыйУчетВнутренний.ПараметрыДляЗапросВТРабочиеМестаСотрудников();
	
КонецФункции

Функция РольСотрудникаРаботник() Экспорт 
	Возврат КадровыйУчетВнутренний.РольСотрудникаРаботник();
КонецФункции	

#КонецОбласти


#Область СобытияРегистровСведений

Процедура КадроваяИсторияСотрудниковПередЗаписью(НаборЗаписей, Отказ) Экспорт
	
	КадровыйУчетВнутренний.КадроваяИсторияСотрудниковПередЗаписью(НаборЗаписей, Отказ);
	
КонецПроцедуры

Процедура КадроваяИсторияСотрудниковПриЗаписи(НаборЗаписей, Отказ) Экспорт
	
	КадровыйУчетВнутренний.КадроваяИсторияСотрудниковПриЗаписи(НаборЗаписей, Отказ);
	
КонецПроцедуры

Процедура ПлановыеНачисленияПередЗаписью(НаборЗаписей, Отказ) Экспорт
	
	КадровыйУчетВнутренний.ПлановыеНачисленияПередЗаписью(НаборЗаписей, Отказ);
	
КонецПроцедуры

Процедура ПлановыеНачисленияПриЗаписи(НаборЗаписей, Отказ) Экспорт
	
	КадровыйУчетВнутренний.ПлановыеНачисленияПриЗаписи(НаборЗаписей, Отказ);
	
КонецПроцедуры

#КонецОбласти


#Область ОбновлениеИнформационнойБазы

Процедура ОписатьДокумент(СсылкаНаЭлемент = Неопределено, Наименование = "", КодМВД = "", КодПФР = "")

	Если Не ЗначениеЗаполнено(СсылкаНаЭлемент) Тогда
		Если Наименование = "" Тогда
			Возврат
		КонецЕсли;
		СсылкаНаЭлемент = Справочники.ВидыДокументовФизическихЛиц.НайтиПоНаименованию(Наименование);
	КонецЕсли;

	Если ЗначениеЗаполнено(СсылкаНаЭлемент) Тогда
		ДанныеИБ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаЭлемент, "КодМВД, КодПФР");
		Если ДанныеИБ.КодМВД = КодМВД И ДанныеИБ.КодПФР = КодПФР Тогда
			Возврат
		КонецЕсли;
		Элемент = СсылкаНаЭлемент.ПолучитьОбъект();
	Иначе
		Элемент = Справочники.ВидыДокументовФизическихЛиц.СоздатьЭлемент();
	КонецЕсли;

	Если ЗначениеЗаполнено(Наименование) Тогда
		Элемент.Наименование = Наименование;
	КонецЕсли;
	Элемент.КодМВД = КодМВД;
	Элемент.КодПФР = КодПФР;
	
	Элемент.ОбменДанными.Загрузка = Истина;
	Элемент.Записать();

КонецПроцедуры

// Добавляет в список Обработчики процедуры-обработчики обновления,
// необходимые данной подсистеме.
//
// Параметры:
//   Обработчики - ТаблицаЗначений - см. описание функции НоваяТаблицаОбработчиковОбновления
//                                   общего модуля ОбновлениеИнформационнойБазы.
// 
Процедура ЗарегистрироватьОбработчикиОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "КадровыйУчет.ОбновитьВидыКонтактнойИнформацииФизическогоЛица";
	Обработчик.НачальноеЗаполнение = Истина;
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "КадровыйУчет.ЗаполнитьДокументыФизическихЛицПоКлассификаторуМВД";
	Обработчик.НачальноеЗаполнение = Истина;
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.3.11";
	Обработчик.Процедура = "КадровыйУчет.ПеренестиТекущиеКадровыеДанныеСотрудниковИзСправочникаВРегистры";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.3.15";
	Обработчик.Процедура = "КадровыйУчет.ЗаполнитьФИОФизическихЛицПоНаименованию";
	
	Если ЗарплатаКадры.АвтономнаяРаботаРазрешеноИзменениеДанных(Метаданные.Справочники.ОснованияУвольнения) Тогда
		Обработчик = Обработчики.Добавить();
		Обработчик.Версия = "3.0.5.3";
		Обработчик.Процедура = "Справочники.ОснованияУвольнения.НачальноеЗаполнение";
		Обработчик.НачальноеЗаполнение = Истина;
		Обработчик.ОбщиеДанные = Истина;
	КонецЕсли;
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.5.3";
	Обработчик.Процедура = "КадровыйУчет.ПеренестиТекущиеВидыЗанятостиСправочниковВРегистр";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.5.16";
	Обработчик.Процедура = "КадровыйУчет.ЗаполнитьФизическоеЛицоВКадроваяИсторияСотрудников";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.5.16";
	Обработчик.Процедура = "КадровыйУчет.ЗаполнитьФизическоеЛицоВПлановыеНачисления";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.5.19";
	Обработчик.Процедура = "КадровыйУчет.ЗаполнитьОрганизациюТекущихДанныхСотрудников";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.6.11";
	Обработчик.Процедура = "КадровыйУчет.ПерезаполнитьСлужебныеИдентификаторыФизическихЛиц";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.6.10";
	Обработчик.Процедура = "СотрудникиФормы.ОбновитьРежимыРаботыФормы";
	Обработчик.НачальноеЗаполнение = Истина;
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.6.19";
	Обработчик.Процедура = "КадровыйУчет.УдалитьУправляющиеКонструкцииСлужебныхИдентификаторовФизическихЛиц";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.6.22";
	Обработчик.Процедура = "КадровыйУчет.ЗаполнитьЗаписиПоУмолчаниюРегистровСведений";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.6.31";
	Обработчик.Процедура = "СотрудникиФормы.ОбновитьРежимыРаботыФормы";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.7.22";
	Обработчик.Процедура = "КадровыйУчет.ИсключитьПовторениеТекущихДанныхСотрудников";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.12.8";
	Обработчик.Процедура = "КадровыйУчет.ЗаполнитьНаименованияКраткиеДолжностей";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.13.2";
	Обработчик.Процедура = "КадровыйУчет.ЗаполнитьНаборыЗначенийДоступаСотрудников";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.15.3";
	Обработчик.Процедура = "КадровыйУчет.ЗаполнитьОбособленныеПодразделенияКадровыхПереводов";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.17.2";
	Обработчик.Процедура = "КадровыйУчет.УстановитьПризнакТолькоРоссийскийАдресовФизическогоЛица";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.18.14";
	Обработчик.Процедура = "КадровыйУчет.ЗаполнитьГоловногоСотрудника";
	
	Если ЗарплатаКадры.АвтономнаяРаботаРазрешеноИзменениеДанных(Метаданные.Справочники.ОснованияУвольнения) Тогда
		Обработчик = Обработчики.Добавить();
		Обработчик.Версия = "3.0.20.24";
		Обработчик.Процедура = "Справочники.ОснованияУвольнения.ОбновитьСправочникИзКлассификатора";
		Обработчик.ОбщиеДанные = Истина;
	КонецЕсли;
	
	Если ЗарплатаКадры.АвтономнаяРаботаРазрешеноИзменениеДанных(Метаданные.Справочники.ОснованияУвольнения) Тогда
		Обработчик = Обработчики.Добавить();
		Обработчик.Версия = "3.0.23.2";
		Обработчик.Процедура = "Справочники.ОснованияУвольнения.ЗагрузитьКлассификатор";
		Обработчик.ОбщиеДанные = Истина;
	КонецЕсли;
	
	Если ЗарплатаКадры.АвтономнаяРаботаРазрешеноИзменениеДанных(Метаданные.Справочники.ОснованияУвольнения) Тогда
		Обработчик = Обработчики.Добавить();
		Обработчик.Версия = "3.0.23.13";
		Обработчик.Процедура = "Справочники.ОснованияУвольнения.ОбновитьСправочникИзКлассификатора";
		Обработчик.ОбщиеДанные = Истина;
	КонецЕсли;
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.24.9";
	Обработчик.Процедура = "КадровыйУчет.ОбновитьВидыКонтактнойИнформацииФизическогоЛица";
	
	Если ЗарплатаКадры.АвтономнаяРаботаРазрешеноИзменениеДанных(Метаданные.Справочники.ОснованияУвольнения) Тогда
		Обработчик = Обработчики.Добавить();
		Обработчик.Версия = "3.1.2.6";
		Обработчик.Процедура = "Справочники.ОснованияУвольнения.ОбновитьОснованияУвольненийСтатьи278";
		Обработчик.ОбщиеДанные = Истина;
	КонецЕсли;

	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.2.32";
	Обработчик.Процедура = "КадровыйУчет.ЗаполнитьФамилиюИмяИОтчествоПоДаннымРегистра";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("1dbd5789-c8c3-450d-afed-3d2d900233ff");
	Обработчик.Комментарий = НСтр("ru='Заполняет реквизиты ""Фамилия"", ""Имя"" и ""Отчество"" справочника ""Физические лица""'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.2.32";
	Обработчик.Процедура = "РегистрыСведений.ФИОФизическихЛиц.ЗаполнитьИнициалыИмени";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("469c0060-8c77-47fb-b36d-a87507898b20");
	Обработчик.Комментарий = НСтр("ru='Заполняет реквизит ""Инициалы имени"" регистра сведений ""ФИО физических лиц""'");
		
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.2.33";
	Обработчик.Процедура = "КадровыйУчет.ЗаполнитьГоловногоСотрудникаИИнтервальныйРегистрСведений";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("719c25f4-211d-4236-8b9a-0ee9abd9387f");
	Обработчик.Комментарий = НСтр("ru='Заполняет реквизит ""Головной сотрудник"" и интервальный регистр регистра сведений ""Кадровая история сотрудников""'");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.2.211";
	Обработчик.Процедура = "КадровыйУчет.ЗаполнитьОрганизацииСотрудников";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("0b8d2250-9448-4224-b3f8-a8c87e77be93");
	Обработчик.Комментарий = НСтр("ru='Заполняет ограничения доступа к сотрудникам по организациям'");
	
КонецПроцедуры

// Процедура заполняет справочник ДокументыФизическихЛиц по классификатору МВД.
//
Процедура ЗаполнитьДокументыФизическихЛицПоКлассификаторуМВД() Экспорт
	
	ОписатьДокумент(Справочники.ВидыДокументовФизическихЛиц.ПаспортСССР,			"", 						"01", НСтр("ru='ПАСПОРТ'"));		// R-ББ 999999
	ОписатьДокумент(Справочники.ВидыДокументовФизическихЛиц.ЗагранпаспортСССР,		"", 						"02", НСтр("ru='ЗГПАСПОРТ'"));		// 99 0999999
	ОписатьДокумент(Справочники.ВидыДокументовФизическихЛиц.СвидетельствоОРождении,	"", 						"03", НСтр("ru='СВИД О РОЖД'"));	// R-ББ 999999
	ОписатьДокумент(Справочники.ВидыДокументовФизическихЛиц.УдостоверениеОфицера,	"", 						"04", НСтр("ru='УДОСТ ОФИЦЕРА'"));	// ББ 9999990
	ОписатьДокумент(, НСтр("ru='Справка об освобождении'"),														"05", НСтр("ru='СПРАВКА ОБ ОСВ'"));	// -
	ОписатьДокумент(Справочники.ВидыДокументовФизическихЛиц.ПаспортМинморфлота, 	"",							"06", НСтр("ru='ПАСПОРТ МОРФЛТ'"));	// ББ 999999
	ОписатьДокумент(Справочники.ВидыДокументовФизическихЛиц.ВоенныйБилет,
		НСтр("ru='Военный билет солдата (матроса, сержанта, старшины)'"), 										"07", НСтр("ru='ВОЕННЫЙ БИЛЕТ'"));	// ББ 0999999
	// -
	ОписатьДокумент(, НСтр("ru='Временное удостоверение, выданное взамен военного билета'"),					"08", НСтр("ru='ПРОЧЕЕ'"));
	// 99 9999999
	ОписатьДокумент(Справочники.ВидыДокументовФизическихЛиц.ДипломатическийПаспорт, "",							"09", НСтр("ru='ДИППАСПОРТ РФ'"));
	ОписатьДокумент(, НСтр("ru='Иностранный паспорт'"),															"10", НСтр("ru='ИНПАСПОРТ'"));		// -
	// -
	ОписатьДокумент(, НСтр("ru='Свидетельство о регистрации ходатайства иммигранта о признании его беженцем'"), "11", НСтр("ru='СВИД БЕЖЕНЦА'"));
	ОписатьДокумент(, НСтр("ru='Вид на жительство'"),															"12", НСтр("ru='ВИД НА ЖИТЕЛЬ'"));	// -
	ОписатьДокумент(, НСтр("ru='Удостоверение беженца'"),														"13", НСтр("ru='УДОСТ БЕЖЕНЦА'"));	// -
	// -
	ОписатьДокумент(, НСтр("ru='Временное удостоверение личности гражданина РФ'"),								"14", НСтр("ru='ВРЕМ УДОСТ'"));
	// -
	ОписатьДокумент(, НСтр("ru='Разрешение на временное проживание в Российской Федерации'"),					"15", НСтр("ru='ПРОЧЕЕ'"));
	ОписатьДокумент(,
		// ББ-999 9999999
		НСтр("ru='Свидетельство о предоставлении временного убежища на территории Российской Федерации'"), 		"18", НСтр("ru='ПРОЧЕЕ'"));
	ОписатьДокумент(Справочники.ВидыДокументовФизическихЛиц.ПаспортРФ,				"",							"21", НСтр("ru='ПАСПОРТ РОССИИ'"));	// 99 99 9999990
	// 99 9999999
	ОписатьДокумент(Справочники.ВидыДокументовФизическихЛиц.ЗагранпаспортРФ,		"",							"22", НСтр("ru='ЗГПАСПОРТ РФ'"));
	ОписатьДокумент(, 
		// -
		НСтр("ru='Свидетельство о рождении, выданное уполномоченным органом иностранного государства'"), 		"23", НСтр("ru='ПРОЧЕЕ'"));
	ОписатьДокумент(, НСтр("ru='Удостоверение личности военнослужащего Российской Федерации'"),					"24", НСтр("ru='ПРОЧЕЕ'"));			// ББ 9999999
	ОписатьДокумент(Справочники.ВидыДокументовФизическихЛиц.ПаспортМоряка,			"",							"26", НСтр("ru='ПАСПОРТ МОРЯКА'"));	// ББ 0999999
	ОписатьДокумент(Справочники.ВидыДокументовФизическихЛиц.ВоенныйБилетОфицераЗапаса, "",						"27", НСтр("ru='БИЛЕТ ОЗ'"));		// ББ 0999999
	ОписатьДокумент(, НСтр("ru='Иные документы, выдаваемые органами МВД'"),										"91", НСтр("ru='ПРОЧЕЕ'"));			// -
	
КонецПроцедуры

// Процедура переносит текущие данные об организации, подразделении, должности, дате приема, дате увольнения и тарифной
// ставке сотрудника  из справочника "Сотрудники" в регистры сведений "ТекущиеКадровыеДанныеСотрудников" и
// "ТекущаяТарифнаяСтавкаСотрудников".
//
Процедура ПеренестиТекущиеКадровыеДанныеСотрудниковИзСправочникаВРегистры() Экспорт
	КадровыйУчетВнутренний.ПеренестиТекущиеКадровыеДанныеСотрудниковИзСправочникаВРегистры();
КонецПроцедуры

// Процедура производит первоначальное заполнение ФИО физических лиц
// ФИО берется из поля наименование.
Процедура ЗаполнитьФИОФизическихЛицПоНаименованию() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ФизическиеЛица.Ссылка,
	               |	ФизическиеЛица.Наименование
	               |ИЗ
	               |	Справочник.ФизическиеЛица КАК ФизическиеЛица
	               |ГДЕ
	               |	ФизическиеЛица.ЭтоГруппа = ЛОЖЬ";
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			 ФизическоеЛицоОбъект = Выборка.Ссылка.ПолучитьОбъект();
			 ФизическоеЛицоОбъект.ФИО = Выборка.Наименование;
			 ФизическоеЛицоОбъект.Записать();
		КонецЦикла;	
	КонецЕсли;
КонецПроцедуры

Процедура ПеренестиТекущиеВидыЗанятостиСправочниковВРегистр() Экспорт
	
	КадровыйУчетВнутренний.ПеренестиТекущиеВидыЗанятостиСправочниковВРегистр();
	
КонецПроцедуры

Процедура ЗаполнитьФизическоеЛицоВКадроваяИсторияСотрудников() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КадроваяИсторияСотрудников.Регистратор
	|ПОМЕСТИТЬ ВТРегистраторыБезФизическогоЛица
	|ИЗ
	|	РегистрСведений.КадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников
	|ГДЕ
	|	КадроваяИсторияСотрудников.ФизическоеЛицо = ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(КадроваяИсторияСотрудников.Сотрудник КАК Справочник.Сотрудники).ФизическоеЛицо КАК ФизическоеЛицо,
	|	КадроваяИсторияСотрудников.*
	|ИЗ
	|	РегистрСведений.КадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников
	|ГДЕ
	|	КадроваяИсторияСотрудников.Регистратор В
	|			(ВЫБРАТЬ
	|				РегистраторыБезФизическогоЛица.Регистратор
	|			ИЗ
	|				ВТРегистраторыБезФизическогоЛица КАК РегистраторыБезФизическогоЛица)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор,
	|	НомерСтроки";
	Выборка = Запрос.Выполнить().Выбрать();
	
	НаборЗаписей = РегистрыСведений.КадроваяИсторияСотрудников.СоздатьНаборЗаписей();
	НаборЗаписей.ОбменДанными.Загрузка = Истина;
	
	Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
		КонецЦикла;
		
		НаборЗаписей.Записать();
		НаборЗаписей.Очистить();
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьФизическоеЛицоВПлановыеНачисления() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПлановыеНачисления.Регистратор
	|ПОМЕСТИТЬ ВТРегистраторыБезФизическогоЛица
	|ИЗ
	|	РегистрСведений.ПлановыеНачисления КАК ПлановыеНачисления
	|ГДЕ
	|	ПлановыеНачисления.ФизическоеЛицо = ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ПлановыеНачисления.Сотрудник КАК Справочник.Сотрудники).ФизическоеЛицо КАК ФизическоеЛицо,
	|	ПлановыеНачисления.*
	|ИЗ
	|	РегистрСведений.ПлановыеНачисления КАК ПлановыеНачисления
	|ГДЕ
	|	ПлановыеНачисления.Регистратор В
	|			(ВЫБРАТЬ
	|				РегистраторыБезФизическогоЛица.Регистратор
	|			ИЗ
	|				ВТРегистраторыБезФизическогоЛица КАК РегистраторыБезФизическогоЛица)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор,
	|	НомерСтроки";
	Выборка = Запрос.Выполнить().Выбрать();
	
	НаборЗаписей = РегистрыСведений.ПлановыеНачисления.СоздатьНаборЗаписей();
	НаборЗаписей.ОбменДанными.Загрузка = Истина;
	
	Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
		КонецЦикла;
		
		НаборЗаписей.Записать();
		НаборЗаписей.Очистить();
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьОрганизациюТекущихДанныхСотрудников() Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация
		|ИЗ
		|	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
		|ГДЕ
		|	ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ТекущиеКадровыеДанныеСотрудников.Сотрудник,
			|	ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо,
			|	ТекущиеКадровыеДанныеСотрудников.ГоловнаяОрганизация,
			|	ТекущиеКадровыеДанныеСотрудников.УдалитьТекущаяОрганизация КАК ТекущаяОрганизация,
			|	ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение,
			|	ТекущиеКадровыеДанныеСотрудников.ТекущаяДолжность,
			|	ТекущиеКадровыеДанныеСотрудников.ДатаПриема,
			|	ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения,
			|	ТекущиеКадровыеДанныеСотрудников.ОсновноеРабочееМестоВОрганизации,
			|	ТекущиеКадровыеДанныеСотрудников.ТекущийВидЗанятости,
			|	ТекущиеКадровыеДанныеСотрудников.УдалитьТекущаяОрганизация
			|ИЗ
			|	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников";
			
		НаборЗаписей = РегистрыСведений.ТекущиеКадровыеДанныеСотрудников.СоздатьНаборЗаписей();
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		
		НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
		
		НаборЗаписей.Записать();
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ТекущаяТарифнаяСтавкаСотрудников.Сотрудник,
			|	ТекущаяТарифнаяСтавкаСотрудников.ФизическоеЛицо,
			|	ТекущаяТарифнаяСтавкаСотрудников.ГоловнаяОрганизация,
			|	МАКСИМУМ(ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация) КАК ТекущаяОрганизация,
			|	ТекущаяТарифнаяСтавкаСотрудников.ТекущаяТарифнаяСтавка
			|ИЗ
			|	РегистрСведений.ТекущаяТарифнаяСтавкаСотрудников КАК ТекущаяТарифнаяСтавкаСотрудников
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
			|		ПО ТекущаяТарифнаяСтавкаСотрудников.Сотрудник = ТекущиеКадровыеДанныеСотрудников.Сотрудник
			|			И ТекущаяТарифнаяСтавкаСотрудников.ФизическоеЛицо = ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо
			|
			|СГРУППИРОВАТЬ ПО
			|	ТекущаяТарифнаяСтавкаСотрудников.Сотрудник,
			|	ТекущаяТарифнаяСтавкаСотрудников.ФизическоеЛицо,
			|	ТекущаяТарифнаяСтавкаСотрудников.ГоловнаяОрганизация,
			|	ТекущаяТарифнаяСтавкаСотрудников.ТекущаяТарифнаяСтавка";
			
		НаборЗаписей = РегистрыСведений.ТекущаяТарифнаяСтавкаСотрудников.СоздатьНаборЗаписей();
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		
		НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
		
		НаборЗаписей.Записать();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПерезаполнитьСлужебныеИдентификаторыФизическихЛиц() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ФизическиеЛица.Ссылка,
		|	ФизическиеЛица.Наименование,
		|	ФизическиеЛица.НаименованиеСлужебное
		|ИЗ
		|	Справочник.ФизическиеЛица КАК ФизическиеЛица
		|ГДЕ
		|	НЕ ФизическиеЛица.ЭтоГруппа";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка= РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НаименованиеСлужебное = ФизическиеЛицаЗарплатаКадры.НаименованиеСлужебное(Выборка.Наименование);
			Если Выборка.НаименованиеСлужебное <> НаименованиеСлужебное Тогда
					
				ФизическоеЛицоОбъект = Выборка.Ссылка.ПолучитьОбъект();
				ФизическоеЛицоОбъект.НаименованиеСлужебное = НаименованиеСлужебное;
				ФизическоеЛицоОбъект.ОбменДанными.Загрузка = Истина;
				ФизическоеЛицоОбъект.Записать();
				
			КонецЕсли; 
			
		КонецЦикла;
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура УдалитьУправляющиеКонструкцииСлужебныхИдентификаторовФизическихЛиц() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ФизическиеЛица.Ссылка,
		|	ФизическиеЛица.Наименование
		|ИЗ
		|	Справочник.ФизическиеЛица КАК ФизическиеЛица
		|ГДЕ
		|	(ФизическиеЛица.НаименованиеСлужебное ПОДОБНО ""%[%""
		|			ИЛИ ФизическиеЛица.НаименованиеСлужебное ПОДОБНО ""%]%"")
		|	И НЕ ФизическиеЛица.ЭтоГруппа";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ФизическоеЛицоОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ФизическоеЛицоОбъект.НаименованиеСлужебное =
				ФизическиеЛицаЗарплатаКадры.НаименованиеСлужебное(Выборка.Наименование);
			ФизическоеЛицоОбъект.ОбменДанными.Загрузка = Истина;
			ФизическоеЛицоОбъект.Записать();
			
		КонецЦикла;
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ЗаполнитьЗаписиПоУмолчаниюРегистровСведений() Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ФизическиеЛица.Ссылка
		|ИЗ
		|	Справочник.ФизическиеЛица КАК ФизическиеЛица
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГражданствоФизическихЛиц КАК ГражданствоФизическихЛиц
		|		ПО ГражданствоФизическихЛиц.ФизическоеЛицо = ФизическиеЛица.Ссылка
		|ГДЕ
		|	НЕ ФизическиеЛица.ЭтоГруппа
		|	И ГражданствоФизическихЛиц.Период ЕСТЬ NULL ";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ЗаписьПоУмолчанию = РегистрыСведений.ГражданствоФизическихЛиц.СоздатьМенеджерЗаписи();
		
		Набор = РегистрыСведений.ГражданствоФизическихЛиц.СоздатьНаборЗаписей();
		Набор.Прочитать();
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Запись = Набор.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, ЗаписьПоУмолчанию);
			Запись.ФизическоеЛицо = Выборка.Ссылка;
			
		КонецЦикла;
		
		Набор.ОбменДанными.Загрузка = Истина;
		Набор.Записать();
		
	КонецЕсли; 
		
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ФизическиеЛица.Ссылка
		|ИЗ
		|	Справочник.ФизическиеЛица КАК ФизическиеЛица
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусФизическихЛицКакНалогоплательщиковНДФЛ КАК СтатусФизическихЛицКакНалогоплательщиковНДФЛ
		|		ПО (СтатусФизическихЛицКакНалогоплательщиковНДФЛ.ФизическоеЛицо = ФизическиеЛица.Ссылка)
		|ГДЕ
		|	НЕ ФизическиеЛица.ЭтоГруппа
		|	И СтатусФизическихЛицКакНалогоплательщиковНДФЛ.Период ЕСТЬ NULL ";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ЗаписьПоУмолчанию = РегистрыСведений.СтатусФизическихЛицКакНалогоплательщиковНДФЛ.СоздатьМенеджерЗаписи();
		
		Набор = РегистрыСведений.СтатусФизическихЛицКакНалогоплательщиковНДФЛ.СоздатьНаборЗаписей();
		Набор.Прочитать();
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Запись = Набор.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, ЗаписьПоУмолчанию);
			Запись.ФизическоеЛицо = Выборка.Ссылка;
			
		КонецЦикла;
		
		Набор.ОбменДанными.Загрузка = Истина;
		Набор.Записать();
		
	КонецЕсли; 
		
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ФизическиеЛица.Ссылка
		|ИЗ
		|	Справочник.ФизическиеЛица КАК ФизическиеЛица
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыЗастрахованныхФизическихЛиц КАК СтатусыЗастрахованныхФизическихЛиц
		|		ПО (СтатусыЗастрахованныхФизическихЛиц.ФизическоеЛицо = ФизическиеЛица.Ссылка)
		|ГДЕ
		|	НЕ ФизическиеЛица.ЭтоГруппа
		|	И СтатусыЗастрахованныхФизическихЛиц.Период ЕСТЬ NULL ";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ЗаписьПоУмолчанию = РегистрыСведений.СтатусыЗастрахованныхФизическихЛиц.СоздатьМенеджерЗаписи();
		
		Набор = РегистрыСведений.СтатусыЗастрахованныхФизическихЛиц.СоздатьНаборЗаписей();
		Набор.Прочитать();
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Запись = Набор.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, ЗаписьПоУмолчанию);
			Запись.ФизическоеЛицо = Выборка.Ссылка;
			
		КонецЦикла;
		
		Набор.ОбменДанными.Загрузка = Истина;
		Набор.Записать();
		
	КонецЕсли; 
		
КонецПроцедуры

Процедура ИсключитьПовторениеТекущихДанныхСотрудников() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТекущиеКадровыеДанныеСотрудников.Сотрудник,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация) КАК КоличествоЗаписей
		|ПОМЕСТИТЬ ВТСотрудники
		|ИЗ
		|	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
		|
		|СГРУППИРОВАТЬ ПО
		|	ТекущиеКадровыеДанныеСотрудников.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Сотрудники.Сотрудник
		|ПОМЕСТИТЬ ВТСотрудникиСПовторением
		|ИЗ
		|	ВТСотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.КоличествоЗаписей > 1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	СотрудникиСПовторением.Сотрудник
		|ИЗ
		|	ВТСотрудникиСПовторением КАК СотрудникиСПовторением";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ТекущиеКадровыеДанныеСотрудников.Сотрудник,
			|	ВЫРАЗИТЬ(ТекущиеКадровыеДанныеСотрудников.Сотрудник КАК Справочник.Сотрудники).ФизическоеЛицо КАК ФизическоеЛицо,
			|	ВЫРАЗИТЬ(ТекущиеКадровыеДанныеСотрудников.Сотрудник КАК Справочник.Сотрудники).ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	МАКСИМУМ(ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация) КАК ТекущаяОрганизация,
			|	МАКСИМУМ(ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение) КАК ТекущееПодразделение,
			|	МАКСИМУМ(ТекущиеКадровыеДанныеСотрудников.ТекущаяДолжность) КАК ТекущаяДолжность,
			|	МАКСИМУМ(ТекущиеКадровыеДанныеСотрудников.ДатаПриема) КАК ДатаПриема,
			|	МАКСИМУМ(ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения) КАК ДатаУвольнения,
			|	МАКСИМУМ(ТекущиеКадровыеДанныеСотрудников.ТекущийВидЗанятости) КАК ТекущийВидЗанятости,
			|	МАКСИМУМ(ТекущиеКадровыеДанныеСотрудников.УдалитьТекущаяОрганизация) КАК УдалитьТекущаяОрганизация
			|ИЗ
			|	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиСПовторением КАК СотрудникиСПовторением
			|		ПО ТекущиеКадровыеДанныеСотрудников.Сотрудник = СотрудникиСПовторением.Сотрудник
			|
			|СГРУППИРОВАТЬ ПО
			|	ТекущиеКадровыеДанныеСотрудников.Сотрудник,
			|	ВЫРАЗИТЬ(ТекущиеКадровыеДанныеСотрудников.Сотрудник КАК Справочник.Сотрудники).ФизическоеЛицо,
			|	ВЫРАЗИТЬ(ТекущиеКадровыеДанныеСотрудников.Сотрудник КАК Справочник.Сотрудники).ГоловнаяОрганизация";
			
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Набор = РегистрыСведений.ТекущиеКадровыеДанныеСотрудников.СоздатьНаборЗаписей();
			Набор.Отбор.Сотрудник.Установить(Выборка.Сотрудник);
			
			ЗаполнитьЗначенияСвойств(Набор.Добавить(), Выборка);
			РегистрыСведений.ТекущиеКадровыеДанныеСотрудников.УстановитьОсновноеРабочееМестоВОрганизации(Набор);
			
			Набор.ОбменДанными.Загрузка = Истина;
			Набор.Записать();
			
		КонецЦикла; 
		
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТекущаяТарифнаяСтавкаСотрудников.Сотрудник,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ТекущаяТарифнаяСтавкаСотрудников.ТекущаяОрганизация) КАК КоличествоЗаписей
		|ПОМЕСТИТЬ ВТСотрудники
		|ИЗ
		|	РегистрСведений.ТекущаяТарифнаяСтавкаСотрудников КАК ТекущаяТарифнаяСтавкаСотрудников
		|
		|СГРУППИРОВАТЬ ПО
		|	ТекущаяТарифнаяСтавкаСотрудников.Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Сотрудники.Сотрудник
		|ПОМЕСТИТЬ ВТСотрудникиСПовторением
		|ИЗ
		|	ВТСотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.КоличествоЗаписей > 1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	СотрудникиСПовторением.Сотрудник
		|ИЗ
		|	ВТСотрудникиСПовторением КАК СотрудникиСПовторением";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ТекущаяТарифнаяСтавкаСотрудников.Сотрудник,
			|	ВЫРАЗИТЬ(ТекущаяТарифнаяСтавкаСотрудников.Сотрудник КАК Справочник.Сотрудники).ФизическоеЛицо КАК ФизическоеЛицо,
			|	ВЫРАЗИТЬ(ТекущаяТарифнаяСтавкаСотрудников.Сотрудник КАК Справочник.Сотрудники).ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	МАКСИМУМ(ТекущаяТарифнаяСтавкаСотрудников.ТекущаяОрганизация) КАК ТекущаяОрганизация,
			|	МАКСИМУМ(ТекущаяТарифнаяСтавкаСотрудников.ТекущаяТарифнаяСтавка) КАК ТекущаяТарифнаяСтавка
			|ИЗ
			|	РегистрСведений.ТекущаяТарифнаяСтавкаСотрудников КАК ТекущаяТарифнаяСтавкаСотрудников
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиСПовторением КАК СотрудникиСПовторением
			|		ПО ТекущаяТарифнаяСтавкаСотрудников.Сотрудник = СотрудникиСПовторением.Сотрудник
			|
			|СГРУППИРОВАТЬ ПО
			|	ТекущаяТарифнаяСтавкаСотрудников.Сотрудник,
			|	ВЫРАЗИТЬ(ТекущаяТарифнаяСтавкаСотрудников.Сотрудник КАК Справочник.Сотрудники).ФизическоеЛицо,
			|	ВЫРАЗИТЬ(ТекущаяТарифнаяСтавкаСотрудников.Сотрудник КАК Справочник.Сотрудники).ГоловнаяОрганизация";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Набор = РегистрыСведений.ТекущаяТарифнаяСтавкаСотрудников.СоздатьНаборЗаписей();
			Набор.Отбор.Сотрудник.Установить(Выборка.Сотрудник);
			
			ЗаполнитьЗначенияСвойств(Набор.Добавить(), Выборка);
			
			Набор.ОбменДанными.Загрузка = Истина;
			Набор.Записать();
			
		КонецЦикла; 
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбновитьВидыКонтактнойИнформацииФизическогоЛица() Экспорт
	
	КадровыйУчетВнутренний.ОбновитьВидыКонтактнойИнформацииФизическогоЛица();
	
КонецПроцедуры

Процедура ОбновитьСобытияУвольнений() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КадроваяИсторияСотрудников.Регистратор,
		|	ДОБАВИТЬКДАТЕ(Увольнение.ДатаУвольнения, ДЕНЬ, 1) КАК Период
		|ПОМЕСТИТЬ ВТРегистраторы
		|ИЗ
		|	РегистрСведений.КадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Увольнение КАК Увольнение
		|		ПО КадроваяИсторияСотрудников.Регистратор = Увольнение.Ссылка
		|			И (КадроваяИсторияСотрудников.Период = ЕСТЬNULL(Увольнение.ДатаУвольнения, ДАТАВРЕМЯ(1, 1, 1)))
		|			И (КадроваяИсторияСотрудников.ВидСобытия = ЗНАЧЕНИЕ(Перечисление.ВидыКадровыхСобытий.Увольнение))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВТРегистраторы.Регистратор
		|ИЗ
		|	ВТРегистраторы КАК ВТРегистраторы";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		КадровыйУчетВнутренний.ОбновитьСобытияУвольнений(Запрос.МенеджерВременныхТаблиц);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьНаименованияКраткиеДолжностей() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Должности.Ссылка
		|ИЗ
		|	Справочник.Должности КАК Должности
		|ГДЕ
		|	Должности.НаименованиеКраткое = """"";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ДолжностьОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ДолжностьОбъект.НаименованиеКраткое = ДолжностьОбъект.Наименование;
			ДолжностьОбъект.ОбменДанными.Загрузка = Истина;
			ДолжностьОбъект.Записать();
		КонецЦикла; 
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ЗаполнитьНаборыЗначенийДоступаСотрудников() Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СотрудникиНаборыЗначенийДоступа.Ссылка,
		|	СотрудникиНаборыЗначенийДоступа.НомерНабора
		|ПОМЕСТИТЬ ВТТекущиеЗначенияДоступаСотрудниковПоФизическимЛицам
		|ИЗ
		|	Справочник.Сотрудники.УдалитьНаборыЗначенийДоступа КАК СотрудникиНаборыЗначенийДоступа
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(СотрудникиНаборыЗначенийДоступа.ЗначениеДоступа) = ТИП(Справочник.ФизическиеЛица)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СотрудникиНаборыЗначенийДоступа.Ссылка,
		|	СотрудникиНаборыЗначенийДоступа.НомерНабора
		|ПОМЕСТИТЬ ВТТекущиеЗначенияДоступаСотрудниковПоОрганизациям
		|ИЗ
		|	Справочник.Сотрудники.УдалитьНаборыЗначенийДоступа КАК СотрудникиНаборыЗначенийДоступа
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(СотрудникиНаборыЗначенийДоступа.ЗначениеДоступа) <> ТИП(Справочник.ФизическиеЛица)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ЗначенияДоступаСотрудниковПоФизическимЛицам.Ссылка, ЗначенияДоступаСотрудниковПоОрганизациям.Ссылка) КАК Сотрудник,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ЗначенияДоступаСотрудниковПоФизическимЛицам.Ссылка ЕСТЬ NULL
		|					ИЛИ ЗначенияДоступаСотрудниковПоОрганизациям.Ссылка ЕСТЬ NULL
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ) КАК НеобходимостьОбновления
		|ПОМЕСТИТЬ ВТСотрудникиСПризнакомОбновления
		|ИЗ
		|	ВТТекущиеЗначенияДоступаСотрудниковПоФизическимЛицам КАК ЗначенияДоступаСотрудниковПоФизическимЛицам
		|		ПОЛНОЕ СОЕДИНЕНИЕ ВТТекущиеЗначенияДоступаСотрудниковПоОрганизациям КАК ЗначенияДоступаСотрудниковПоОрганизациям
		|		ПО ЗначенияДоступаСотрудниковПоФизическимЛицам.Ссылка = ЗначенияДоступаСотрудниковПоОрганизациям.Ссылка
		|			И ЗначенияДоступаСотрудниковПоФизическимЛицам.НомерНабора = ЗначенияДоступаСотрудниковПоОрганизациям.НомерНабора
		|
		|СГРУППИРОВАТЬ ПО
		|	ЕСТЬNULL(ЗначенияДоступаСотрудниковПоФизическимЛицам.Ссылка, ЗначенияДоступаСотрудниковПоОрганизациям.Ссылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СотрудникиСПризнакомОбновления.Сотрудник
		|ИЗ
		|	ВТСотрудникиСПризнакомОбновления КАК СотрудникиСПризнакомОбновления
		|ГДЕ
		|	СотрудникиСПризнакомОбновления.НеобходимостьОбновления";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			СотрудникОбъект = Выборка.Сотрудник.ПолучитьОбъект();
			
			Попытка
				СотрудникОбъект.Заблокировать();
			Исключение
				
				ТекстИсключенияЗаписи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не удалось обновить права доступа к %1.
						|Возможно, данные сотрудника редактируется другим пользователем'"),
					Выборка.Сотрудник);
				ВызватьИсключение ТекстИсключенияЗаписи;
				
			КонецПопытки;
			
			СотрудникОбъект.Записать();
			
		КонецЦикла; 
		
	КонецЕсли; 
		
КонецПроцедуры

Процедура ЗаполнитьОбособленныеПодразделенияКадровыхПереводов() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КадровыйПеревод.Ссылка,
		|	КадровыйПеревод.Подразделение.Владелец КАК ОбособленноеПодразделение
		|ИЗ
		|	Документ.КадровыйПеревод КАК КадровыйПеревод
		|ГДЕ
		|	КадровыйПеревод.ОбособленноеПодразделение = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|	И КадровыйПеревод.Подразделение <> ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
		|	И КадровыйПеревод.ИзменитьПодразделениеИДолжность";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ДокументОбъект.ОбособленноеПодразделение = Выборка.ОбособленноеПодразделение;
			
			ДокументОбъект.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
			ДокументОбъект.ОбменДанными.Загрузка = Истина;
			ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
			
		КонецЦикла; 
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	КадроваяИсторияСотрудников.Регистратор
		|ИЗ
		|	РегистрСведений.КадроваяИсторияСотрудников КАК КадроваяИсторияСотрудников
		|ГДЕ
		|	КадроваяИсторияСотрудников.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|	И КадроваяИсторияСотрудников.Подразделение <> ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НаборЗаписей = РегистрыСведений.КадроваяИсторияСотрудников.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
			
			НаборЗаписей.Прочитать();
			
			Для каждого ЗаписьНабора Из НаборЗаписей Цикл
				Если НЕ ЗначениеЗаполнено(ЗаписьНабора.Организация) Тогда
					ЗаписьНабора.Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗаписьНабора.Подразделение, "Владелец");
				КонецЕсли; 
			КонецЦикла;
			
			НаборЗаписей.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
			НаборЗаписей.ОбменДанными.Загрузка = Истина;
			НаборЗаписей.Записать();
			
		КонецЦикла; 
		
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТекущиеКадровыеДанныеСотрудников.Сотрудник
		|ИЗ
		|	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
		|ГДЕ
		|	ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|	И ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение <> ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ВЫРАЗИТЬ(ТекущиеКадровыеДанныеСотрудников.Сотрудник КАК Справочник.Сотрудники).ФизическоеЛицо КАК ФизическоеЛицо,
			|	ТекущиеКадровыеДанныеСотрудников.Сотрудник,
			|	ВЫРАЗИТЬ(ТекущиеКадровыеДанныеСотрудников.Сотрудник КАК Справочник.Сотрудники).ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	ВЫБОР
			|		КОГДА ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
			|			ТОГДА ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение.Владелец
			|		ИНАЧЕ ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация
			|	КОНЕЦ КАК ТекущаяОрганизация,
			|	МАКСИМУМ(ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение) КАК ТекущееПодразделение,
			|	МАКСИМУМ(ТекущиеКадровыеДанныеСотрудников.ТекущаяДолжность) КАК ТекущаяДолжность,
			|	МАКСИМУМ(ТекущиеКадровыеДанныеСотрудников.ДатаПриема) КАК ДатаПриема,
			|	МАКСИМУМ(ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения) КАК ДатаУвольнения,
			|	МАКСИМУМ(ТекущиеКадровыеДанныеСотрудников.ОсновноеРабочееМестоВОрганизации) КАК ОсновноеРабочееМестоВОрганизации,
			|	МАКСИМУМ(ТекущиеКадровыеДанныеСотрудников.ТекущийВидЗанятости) КАК ТекущийВидЗанятости
			|ИЗ
			|	РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
			|
			|СГРУППИРОВАТЬ ПО
			|	ВЫРАЗИТЬ(ТекущиеКадровыеДанныеСотрудников.Сотрудник КАК Справочник.Сотрудники).ФизическоеЛицо,
			|	ТекущиеКадровыеДанныеСотрудников.Сотрудник,
			|	ВЫРАЗИТЬ(ТекущиеКадровыеДанныеСотрудников.Сотрудник КАК Справочник.Сотрудники).ГоловнаяОрганизация,
			|	ВЫБОР
			|		КОГДА ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
			|			ТОГДА ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение.Владелец
			|		ИНАЧЕ ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация
			|	КОНЕЦ";
			
		НаборЗаписей = РегистрыСведений.ТекущиеКадровыеДанныеСотрудников.СоздатьНаборЗаписей();
		НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
		
		НаборЗаписей.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		НаборЗаписей.Записать();
			
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ТекущаяТарифнаяСтавкаСотрудников.Сотрудник
		|ИЗ
		|	РегистрСведений.ТекущаяТарифнаяСтавкаСотрудников КАК ТекущаяТарифнаяСтавкаСотрудников
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
		|		ПО ТекущаяТарифнаяСтавкаСотрудников.Сотрудник = ТекущиеКадровыеДанныеСотрудников.Сотрудник
		|			И ТекущаяТарифнаяСтавкаСотрудников.ТекущаяОрганизация = ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация
		|ГДЕ
		|	ТекущиеКадровыеДанныеСотрудников.Сотрудник ЕСТЬ NULL ";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ВЫРАЗИТЬ(ТекущаяТарифнаяСтавкаСотрудников.Сотрудник КАК Справочник.Сотрудники).ФизическоеЛицо КАК ФизическоеЛицо,
			|	ТекущаяТарифнаяСтавкаСотрудников.Сотрудник,
			|	ВЫРАЗИТЬ(ТекущаяТарифнаяСтавкаСотрудников.Сотрудник КАК Справочник.Сотрудники).ГоловнаяОрганизация КАК ГоловнаяОрганизация,
			|	ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация КАК ТекущаяОрганизация,
			|	МАКСИМУМ(ТекущаяТарифнаяСтавкаСотрудников.ТекущаяТарифнаяСтавка) КАК ТекущаяТарифнаяСтавка
			|ИЗ
			|	РегистрСведений.ТекущаяТарифнаяСтавкаСотрудников КАК ТекущаяТарифнаяСтавкаСотрудников
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
			|		ПО ТекущаяТарифнаяСтавкаСотрудников.Сотрудник = ТекущиеКадровыеДанныеСотрудников.Сотрудник
			|
			|СГРУППИРОВАТЬ ПО
			|	ВЫРАЗИТЬ(ТекущаяТарифнаяСтавкаСотрудников.Сотрудник КАК Справочник.Сотрудники).ФизическоеЛицо,
			|	ТекущаяТарифнаяСтавкаСотрудников.Сотрудник,
			|	ВЫРАЗИТЬ(ТекущаяТарифнаяСтавкаСотрудников.Сотрудник КАК Справочник.Сотрудники).ГоловнаяОрганизация,
			|	ТекущиеКадровыеДанныеСотрудников.ТекущаяОрганизация";
		
		НаборЗаписей = РегистрыСведений.ТекущаяТарифнаяСтавкаСотрудников.СоздатьНаборЗаписей();
		НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
		
		НаборЗаписей.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		НаборЗаписей.Записать();
			
	КонецЕсли; 
	
КонецПроцедуры

Процедура УстановитьПризнакТолькоРоссийскийАдресовФизическогоЛица() Экспорт
	
	СвойстваВидаКИ = УправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации(Перечисления.ТипыКонтактнойИнформации.Адрес);
	СвойстваВидаКИ.МожноИзменятьСпособРедактирования = Ложь;
	СвойстваВидаКИ.РедактированиеТолькоВДиалоге = Истина;
	СвойстваВидаКИ.ОбязательноеЗаполнение = Ложь;
	СвойстваВидаКИ.НастройкиПроверки.АдресТолькоРоссийский = Истина;
	СвойстваВидаКИ.НастройкиПроверки.ПроверятьКорректность = Ложь;
	СвойстваВидаКИ.НастройкиПроверки.СкрыватьНеактуальныеАдреса = Ложь;
	СвойстваВидаКИ.НастройкиПроверки.ВключатьСтрануВПредставление = Истина;
	
	// АдресПоПропискеФизическиеЛица
	СвойстваВидаКИ.Вид = Справочники.ВидыКонтактнойИнформации.АдресПоПропискеФизическиеЛица;
	УправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(СвойстваВидаКИ);
	
	// АдресМестаПроживанияФизическиеЛица
	СвойстваВидаКИ.Вид = Справочники.ВидыКонтактнойИнформации.АдресМестаПроживанияФизическиеЛица;
	УправлениеКонтактнойИнформацией.УстановитьСвойстваВидаКонтактнойИнформации(СвойстваВидаКИ);
		
КонецПроцедуры

Процедура ЗаполнитьГоловногоСотрудника() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Сотрудники.Ссылка
		|ИЗ
		|	Справочник.Сотрудники КАК Сотрудники
		|ГДЕ
		|	Сотрудники.ГоловнойСотрудник = ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			СотрудникОбъект = Выборка.Ссылка.ПолучитьОбъект();
			СотрудникОбъект.ГоловнойСотрудник = СотрудникОбъект.Ссылка;
			СотрудникОбъект.ОбменДанными.Загрузка = Истина;
			СотрудникОбъект.Записать();
			
		КонецЦикла; 
		
	КонецЕсли; 
		
КонецПроцедуры

Процедура ЗаполнитьФамилиюИмяИОтчествоПоДаннымРегистра(Параметры) Экспорт
	
	Параметры.ОбработкаЗавершена = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Если Параметры.ПрогрессВыполнения.ВсегоОбъектов = 0 Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ФизическиеЛица.Ссылка) КАК КоличествоФизическихЛиц
			|ИЗ
			|	Справочник.ФизическиеЛица КАК ФизическиеЛица
			|ГДЕ
			|	ФизическиеЛица.Фамилия = """"";
		
		РезультатЗапроса = Запрос.Выполнить();
		Если РезультатЗапроса.Пустой() Тогда
			
			Параметры.ОбработкаЗавершена = Истина;
			Возврат;
			
		КонецЕсли;
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		Параметры.ПрогрессВыполнения.ВсегоОбъектов = Выборка.КоличествоФизическихЛиц;
		
		Параметры.Вставить("ОбработанныеФизическиеЛица", Новый Массив);
		Параметры.Вставить("ФизическиеЛицаСОшибкой", Новый Массив);
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ОбработанныеФизическиеЛица", Параметры.ОбработанныеФизическиеЛица);
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ДАТАВРЕМЯ(1, 1, 1) КАК Период,
		|	ФизическиеЛица.Ссылка КАК ФизическоеЛицо,
		|	ФизическиеЛица.ФИО
		|ПОМЕСТИТЬ ВТФизическиеЛица
		|ИЗ
		|	Справочник.ФизическиеЛица КАК ФизическиеЛица
		|ГДЕ
		|	ФизическиеЛица.Фамилия = """"
		|	И НЕ ФизическиеЛица.Ссылка В (&ОбработанныеФизическиеЛица)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ФизическиеЛица.ФизическоеЛицо
		|ИЗ
		|	ВТФизическиеЛица КАК ФизическиеЛица";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних(
			"ФИОФизическихЛиц",
			Запрос.МенеджерВременныхТаблиц,
			Ложь,
			ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
				"ВТФизическиеЛица", "ФизическоеЛицо"));
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ФизическиеЛица.ФизическоеЛицо,
			|	ФизическиеЛица.ФИО,
			|	ФИОФизическихЛиц.Фамилия,
			|	ФИОФизическихЛиц.Имя,
			|	ФИОФизическихЛиц.Отчество
			|ИЗ
			|	ВТФизическиеЛица КАК ФизическиеЛица
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФИОФизическихЛицСрезПоследних КАК ФИОФизическихЛиц
			|		ПО ФизическиеЛица.ФизическоеЛицо = ФИОФизическихЛиц.ФизическоеЛицо";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Параметры.ОбработанныеФизическиеЛица.Добавить(Выборка.ФизическоеЛицо);
			
			НачатьТранзакцию();
			Попытка
				
				Блокировка = Новый БлокировкаДанных;
				
				ЭлементБлокировки = Блокировка.Добавить("Справочник.ФизическиеЛица");
				ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.ФизическоеЛицо);
				
				Блокировка.Заблокировать();
				
				ФизическоеЛицоОбъект = Выборка.ФизическоеЛицо.ПолучитьОбъект();
				
				// Заполнение реквизитов Фамилия, Имя, Отчество
				Если ЗначениеЗаполнено(Выборка.Фамилия) Тогда
					ЧастиИмени = Выборка;
				Иначе
					ЧастиИмени = ФизическиеЛицаКлиентСервер.ЧастиИмени(Выборка.ФИО);
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(ФизическоеЛицоОбъект, ЧастиИмени, "Фамилия,Имя,Отчество");
				
				// Заполнение реквизита ИнициалыИмени
				Если Не ПустаяСтрока(ФизическоеЛицоОбъект.Имя) Тогда
					ИнициалыИмени = ФизическиеЛицаЗарплатаКадрыКлиентСервер.ИнициалыИмени(ФизическоеЛицоОбъект.Имя);
				Иначе
					ИнициалыИмени = "";
				КонецЕсли;
				
				ФизическоеЛицоОбъект.ИнициалыИмени = ИнициалыИмени;
				
				// Сохранение объекта
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(ФизическоеЛицоОбъект, Ложь, Ложь);
				
				ЗафиксироватьТранзакцию();
				
			Исключение
				
				ОтменитьТранзакцию();
				
				Параметры.ФизическиеЛицаСОшибкой.Добавить(Выборка.ФизическоеЛицо);
				
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось обработать %1 по причине: %2'"),
					Выборка.ФизическоеЛицо,
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
				ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
					УровеньЖурналаРегистрации.Предупреждение,
					Метаданные.Справочники.ФизическиеЛица,
					Выборка.ФизическоеЛицо,
					ТекстСообщения);
				
			КонецПопытки;
			
		КонецЦикла;
		
		Параметры.ПрогрессВыполнения.ОбработаноОбъектов = Параметры.ПрогрессВыполнения.ОбработаноОбъектов + 1000;
		
	Иначе
		
		Если Параметры.ФизическиеЛицаСОшибкой.Количество() > 0 Тогда
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось обработать %1 физических лиц'"), Параметры.ФизическиеЛицаСОшибкой.Количество());
			ВызватьИсключение ТекстСообщения;
			
		КонецЕсли;
		
		Параметры.ОбработкаЗавершена = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьНеобходимостьПолученияСведенийНаПериодНачалаУчета(МенеджерВременныхТаблиц, ОписательВременныхТаблиц, ТолькоРазрешенные, КадровыеДанные, ПоляОтбораПериодическихДанных, ФормироватьСПериодичностьДень, ИсточникиДанных)
	
	КадровыйУчетВнутренний.ПроверитьНеобходимостьПолученияСведенийНаПериодНачалаУчета(МенеджерВременныхТаблиц, ОписательВременныхТаблиц, ТолькоРазрешенные, КадровыеДанные, ПоляОтбораПериодическихДанных, ФормироватьСПериодичностьДень, ИсточникиДанных);
	
КонецПроцедуры

#КонецОбласти


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Функция ТаблицаАнализаИзменений() Экспорт
	
	ТаблицаАнализаИзменений = Новый ТаблицаЗначений;
	ТаблицаАнализаИзменений.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаАнализаИзменений.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ТаблицаАнализаИзменений.Колонки.Добавить("ФлагИзменений", Новый ОписаниеТипов("Число"));
	
	Возврат ТаблицаАнализаИзменений;
	
КонецФункции

Функция ОбработатьИзменениеОрганизацийВНаборе(ИзмененияВНаборе) Экспорт
	
	ТаблицаАнализаИзменений = КадровыйУчет.ТаблицаАнализаИзменений();
	
	Для Каждого ОписаниеИзменившихсяДанных Из ИзмененияВНаборе Цикл
		
		Если ОписаниеИзменившихсяДанных.ИзменилсяРесурсОрганизация Или ОписаниеИзменившихсяДанных.Добавление Или ОписаниеИзменившихсяДанных.Удаление Тогда
			
			Если ЗначениеЗаполнено(ОписаниеИзменившихсяДанных.СтароеЗначениеОрганизация) Тогда
				СтрокаАнализаИзменений = ТаблицаАнализаИзменений.Добавить();
				СтрокаАнализаИзменений.Организация = ОписаниеИзменившихсяДанных.СтароеЗначениеОрганизация;
				СтрокаАнализаИзменений.Сотрудник = ОписаниеИзменившихсяДанных.Сотрудник;
				СтрокаАнализаИзменений.ФлагИзменений = -1;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ОписаниеИзменившихсяДанных.НовоеЗначениеОрганизация) Тогда
				СтрокаАнализаИзменений = ТаблицаАнализаИзменений.Добавить();
				СтрокаАнализаИзменений.Организация = ОписаниеИзменившихсяДанных.НовоеЗначениеОрганизация;
				СтрокаАнализаИзменений.Сотрудник = ОписаниеИзменившихсяДанных.Сотрудник;
				СтрокаАнализаИзменений.ФлагИзменений = 1;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОбработатьИзменениеОрганизацийВНабореПоТаблицеИзменений(ТаблицаАнализаИзменений)
	
КонецФункции

Процедура ОбработатьИзменениеОрганизацийВНабореПоТаблицеИзменений(ТаблицаАнализаИзменений) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	КадровыйУчетВнутренний.ОбработатьИзменениеОрганизацийВНабореПоТаблицеИзменений(ТаблицаАнализаИзменений);
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ДополнитьСтруктуруПараметровИменамиТаблицСКадровойИсториейСотрудников(СтруктураПараметров) Экспорт
	
	ИменаТаблицСКадровойИсториейСотрудников = Новый Структура;
	ИменаТаблицСКадровойИсториейСотрудников.Вставить("ИмяВТКадроваяИсторияСотрудниковТаблицаРегистра", "");
	ИменаТаблицСКадровойИсториейСотрудников.Вставить("ИмяВТКадроваяИсторияСотрудниковСрезПоследних", "");
	
	СтруктураПараметров.Вставить("ИменаТаблицСКадровойИсториейСотрудников", ИменаТаблицСКадровойИсториейСотрудников);
	
КонецПроцедуры

Функция ТекстОписанияПолейКадровыхДанных(ТекстыОписанияПолей) Экспорт
	
	Если ТекстыОписанияПолей.Количество() = 0 Тогда
		ТекстОписанияПолей = "";
	ИначеЕсли ТекстыОписанияПолей.Количество() = 1 Тогда
		ТекстОписанияПолей = ТекстыОписанияПолей[0];
	Иначе
		ТекстОписанияПолей = СтрСоединить(ТекстыОписанияПолей, "," + Символы.ПС + Символы.Таб);
	КонецЕсли;
	
	Возврат ТекстОписанияПолей;
	
КонецФункции

Функция КадровыеДанныеДляТрудовогоДоговора() Экспорт
	
	Возврат КадровыйУчетВнутренний.КадровыеДанныеДляТрудовогоДоговора();
	
КонецФункции

Процедура СоздатьВТКадровыеДанныеТрудовогоДоговора(МенеджерВременныхТаблиц, ИмяВременнойТаблицыОтборовСотрудников, ИменаПолейВременнойТаблицыОтборовСотрудников)
	
	КадровыйУчетВнутренний.СоздатьВТКадровыеДанныеТрудовогоДоговора(МенеджерВременныхТаблиц, ИмяВременнойТаблицыОтборовСотрудников, ИменаПолейВременнойТаблицыОтборовСотрудников);
	
КонецПроцедуры

Функция ДанныеТрудовыхДоговоров(МассивОбъектов, ДистанционныеРаботы, ДоговорСНадомником) Экспорт
	
	ДанныеТрудовыхДоговоров = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	МассивПриказовОПриеме = Новый Массив;
	
	ИспользоватьКадровыйУчет = ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет");
	ВыводПоСсылкеНаСотрудника = Ложь;
	
	Если ТипЗнч(МассивОбъектов[0]) = Тип("СправочникСсылка.Сотрудники") Тогда
		
		ВыводПоСсылкеНаСотрудника = Истина;
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ДАТАВРЕМЯ(1, 1, 1) КАК Период,
			|	Сотрудники.Ссылка КАК Сотрудник
			|ПОМЕСТИТЬ ВТСотрудникиДляПечатиТрудовыхДоговоров
			|ИЗ
			|	Справочник.Сотрудники КАК Сотрудники
			|ГДЕ
			|	Сотрудники.Ссылка В(&МассивОбъектов)";
		
		Запрос.Выполнить();
		
		Описатель = ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
			Запрос.МенеджерВременныхТаблиц, "ВТСотрудникиДляПечатиТрудовыхДоговоров");
		
		Описатель.ИмяВТКадровыеДанныеСотрудников = "ВТСотрудникиСДаннымиОПриеме";
		
		Если ИспользоватьКадровыйУчет Тогда
			КадровыеДанныеПриема = "ДатаПриема,ПриказОПриеме";
		Иначе
			КадровыеДанныеПриема = "ДатаПриема";
		КонецЕсли;
		
		СоздатьВТКадровыеДанныеСотрудников(Описатель, Истина, КадровыеДанныеПриема);
		
		Если ИспользоватьКадровыйУчет Тогда
			
			Запрос.Текст =
				"ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	СотрудникиСДаннымиОПриеме.ПриказОПриеме
				|ИЗ
				|	ВТСотрудникиСДаннымиОПриеме КАК СотрудникиСДаннымиОПриеме
				|ГДЕ
				|	ЕСТЬNULL(СотрудникиСДаннымиОПриеме.ПриказОПриеме, ЗНАЧЕНИЕ(Документ.ПриемНаРаботу.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Документ.ПриемНаРаботу.ПустаяСсылка)";
			
			МассивПриказовОПриеме = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ПриказОПриеме");
			
		КонецЕсли;
		
	Иначе
		МассивПриказовОПриеме = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(МассивОбъектов);
	КонецЕсли;
	
	Если ИспользоватьКадровыйУчет Тогда
		
		СоздатьВТДанныеДокументовПриемНаРаботу(Запрос.МенеджерВременныхТаблиц, МассивПриказовОПриеме);
		Если ВыводПоСсылкеНаСотрудника Тогда
			
			Запрос.Текст =
				"ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	ПриказыОПриеме.Ссылка,
				|	СотрудникиСДаннымиОПриеме.Сотрудник,
				|	СотрудникиСДаннымиОПриеме.ДатаПриема КАК Период,
				|	СотрудникиСДаннымиОПриеме.ДатаПриема КАК Дата,
				|	ПриказыОПриеме.ДлительностьИспытательногоСрока,
				|	ПриказыОПриеме.РазрешениеНаРаботу,
				|	ПриказыОПриеме.РазрешениеНаПроживание,
				|	ПриказыОПриеме.УсловияОказанияМедпомощи,
				|	ПриказыОПриеме.Руководитель,
				|	ПриказыОПриеме.ДолжностьРуководителя
				|ПОМЕСТИТЬ ВТДанныеПриказовОПриеме
				|ИЗ
				|	ВТДанныеДокументов КАК ПриказыОПриеме
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиСДаннымиОПриеме КАК СотрудникиСДаннымиОПриеме
				|		ПО ПриказыОПриеме.Ссылка = СотрудникиСДаннымиОПриеме.ПриказОПриеме";
			
			Запрос.Выполнить();
			
		Иначе
			
			Запрос.Текст =
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
				|	ПриказыОПриеме.Сотрудник,
				|	ПриказыОПриеме.Дата КАК ДатаПриема,
				|	ПриказыОПриеме.Ссылка КАК ПриказОПриеме
				|ПОМЕСТИТЬ ВТСотрудникиСДаннымиОПриеме
				|ИЗ
				|	ВТДанныеДокументов КАК ПриказыОПриеме
				|ГДЕ
				|	ПриказыОПриеме.Ссылка В(&МассивОбъектов)
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
				|	ПриказыОПриеме.Ссылка,
				|	ПриказыОПриеме.Сотрудник,
				|	ПриказыОПриеме.Дата КАК Период,
				|	ПриказыОПриеме.Дата,
				|	ПриказыОПриеме.ДлительностьИспытательногоСрока,
				|	ПриказыОПриеме.РазрешениеНаРаботу,
				|	ПриказыОПриеме.РазрешениеНаПроживание,
				|	ПриказыОПриеме.УсловияОказанияМедпомощи,
				|	ПриказыОПриеме.Руководитель,
				|	ПриказыОПриеме.ДолжностьРуководителя
				|ПОМЕСТИТЬ ВТДанныеПриказовОПриеме
				|ИЗ
				|	ВТДанныеДокументов КАК ПриказыОПриеме
				|ГДЕ
				|	ПриказыОПриеме.Ссылка В(&МассивОбъектов)";
			
			Запрос.Выполнить();
		
		КонецЕсли;
		
	Иначе
		
		Запрос.Текст =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	СотрудникиСДаннымиОПриеме.Сотрудник КАК Ссылка,
			|	СотрудникиСДаннымиОПриеме.Сотрудник,
			|	СотрудникиСДаннымиОПриеме.ДатаПриема КАК Период,
			|	СотрудникиСДаннымиОПриеме.ДатаПриема КАК Дата,
			|	0 КАК ДлительностьИспытательногоСрока,
			|	NULL КАК РазрешениеНаРаботу,
			|	NULL КАК РазрешениеНаПроживание,
			|	NULL КАК УсловияОказанияМедпомощи,
			|	NULL КАК Руководитель,
			|	NULL КАК ДолжностьРуководителя
			|ПОМЕСТИТЬ ВТДанныеПриказовОПриеме
			|ИЗ
			|	ВТСотрудникиСДаннымиОПриеме КАК СотрудникиСДаннымиОПриеме";
		
		Запрос.Выполнить();
		
	КонецЕсли;
	
	ЗарплатаКадры.СоздатьВТФИООтветственныхЛиц(Запрос.МенеджерВременныхТаблиц, Истина, "Руководитель", "ВТДанныеПриказовОПриеме");
	
	СоздатьВТКадровыеДанныеТрудовогоДоговора(Запрос.МенеджерВременныхТаблиц, "ВТСотрудникиСДаннымиОПриеме", "Сотрудник,ДатаПриема");
	
	НачисленияСотрудников = Новый Соответствие;
	Если ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ПлановыеНачисления) Тогда
		
		ЗапросНачислений = ЗапросВТПлановыеНачисленияСотрудников(Истина, "ВТПлановыеНачисления", "ВТКадровыеДанныеСотрудников");
		ЗапросНачислений.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
		ЗапросНачислений.Выполнить();
		
		КатегорииСдельнойОплатыТруда = РасчетЗарплаты.КатегорииСдельнойОплатыТруда();
		ЗапросНачислений.УстановитьПараметр("КатегорииСдельнойОплатыТруда", КатегорииСдельнойОплатыТруда);
		
		КатегорииКомпенсаций = РасчетЗарплаты.КатегорииНачисленийКомпенсационныхВыплат();
		ЗапросНачислений.УстановитьПараметр("КатегорииКомпенсаций", КатегорииКомпенсаций);
		
		ЗапросНачислений.Текст =
			"ВЫБРАТЬ
			|	ПлановыеНачисления.Период КАК Период,
			|	ПлановыеНачисления.Сотрудник КАК Сотрудник,
			|	ПлановыеНачисления.Начисление,
			|	ПлановыеНачисления.ОсновнойПоказатель,
			|	ПлановыеНачисления.ЗначениеОсновногоПоказателя,
			|	ПлановыеНачисления.Размер,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(ПлановыеНачисления.НачислениеТарифнойСтавки, ЛОЖЬ) = ИСТИНА
			|				И НЕ Начисления.КатегорияНачисленияИлиНеоплаченногоВремени В (&КатегорииСдельнойОплатыТруда)
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ЭтоТарифнаяСтавка,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(ПлановыеНачисления.НачислениеТарифнойСтавки, ЛОЖЬ) = ИСТИНА
			|				И НЕ Начисления.КатегорияНачисленияИлиНеоплаченногоВремени В (&КатегорииСдельнойОплатыТруда)
			|			ТОГДА ЛОЖЬ
			|		КОГДА Начисления.КатегорияНачисленияИлиНеоплаченногоВремени В (&КатегорииСдельнойОплатыТруда)
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ЭтоСдельнаяОплатаТруда,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(ПлановыеНачисления.НачислениеТарифнойСтавки, ЛОЖЬ) = ИСТИНА
			|				И НЕ Начисления.КатегорияНачисленияИлиНеоплаченногоВремени В (&КатегорииСдельнойОплатыТруда)
			|			ТОГДА ЛОЖЬ
			|		КОГДА Начисления.КатегорияНачисленияИлиНеоплаченногоВремени В (&КатегорииСдельнойОплатыТруда)
			|			ТОГДА ЛОЖЬ
			|		КОГДА Начисления.КатегорияНачисленияИлиНеоплаченногоВремени В (&КатегорииКомпенсаций)
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ЭтоКомпенсация,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(ПлановыеНачисления.НачислениеТарифнойСтавки, ЛОЖЬ) = ИСТИНА
			|				И НЕ Начисления.КатегорияНачисленияИлиНеоплаченногоВремени В (&КатегорииСдельнойОплатыТруда)
			|			ТОГДА ЛОЖЬ
			|		КОГДА Начисления.КатегорияНачисленияИлиНеоплаченногоВремени В (&КатегорииСдельнойОплатыТруда)
			|			ТОГДА ЛОЖЬ
			|		КОГДА Начисления.КатегорияНачисленияИлиНеоплаченногоВремени В (&КатегорииКомпенсаций)
			|			ТОГДА ЛОЖЬ
			|		ИНАЧЕ ИСТИНА
			|	КОНЕЦ КАК ЭтоНадбавка
			|ИЗ
			|	ВТПлановыеНачисления КАК ПлановыеНачисления
			|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК Начисления
			|		ПО ПлановыеНачисления.Начисление = Начисления.Ссылка
			|
			|УПОРЯДОЧИТЬ ПО
			|	Период,
			|	Сотрудник,
			|	Начисления.РеквизитДопУпорядочивания";
		
		УстановитьДанныеТарифнойСтавки = Истина;
		
		ВыборкаНачислений = ЗапросНачислений.Выполнить().Выбрать();
		Пока ВыборкаНачислений.Следующий() Цикл
			
			Периоды = НачисленияСотрудников.Получить(ВыборкаНачислений.Период);
			Если Периоды = Неопределено Тогда
				Периоды = Новый Соответствие;
				НачисленияСотрудников.Вставить(ВыборкаНачислений.Период, Периоды);
			КонецЕсли;
			
			ОписаниеНачислений = Периоды.Получить(ВыборкаНачислений.Сотрудник);
			Если ОписаниеНачислений = Неопределено Тогда
				
				УстановитьДанныеТарифнойСтавки = Истина;
				
				ОписаниеНачислений = Новый Структура;
				ОписаниеНачислений.Вставить("ТарифнаяСтавка", Новый Структура("Начисление,ОсновнойПоказатель,ЗначениеОсновногоПоказателя,Размер"));
				ОписаниеНачислений.Вставить("СдельнаяОплатаТруда", Новый Структура("Начисление,Размер"));
				ОписаниеНачислений.Вставить("Компенсации", Новый Массив);
				ОписаниеНачислений.Вставить("Надбавки", Новый Массив);
				
				Периоды.Вставить(ВыборкаНачислений.Сотрудник, ОписаниеНачислений);
				
			КонецЕсли;
			
			Если УстановитьДанныеТарифнойСтавки И ВыборкаНачислений.ЭтоТарифнаяСтавка Тогда
				
				ЗаполнитьЗначенияСвойств(ОписаниеНачислений.ТарифнаяСтавка, ВыборкаНачислений);
				УстановитьДанныеТарифнойСтавки = Ложь;
				
			ИначеЕсли УстановитьДанныеТарифнойСтавки И ВыборкаНачислений.ЭтоСдельнаяОплатаТруда Тогда
				
				ЗаполнитьЗначенияСвойств(ОписаниеНачислений.СдельнаяОплатаТруда, ВыборкаНачислений);
				УстановитьДанныеТарифнойСтавки = Ложь;
				
			Иначе
				
				ОписаниеНачисления = Новый Структура("Начисление,Размер");
				ЗаполнитьЗначенияСвойств(ОписаниеНачисления, ВыборкаНачислений);
				
				Если ВыборкаНачислений.ЭтоКомпенсация Тогда
					ОписаниеНачислений.Компенсации.Добавить(ОписаниеНачисления);
				Иначе
					ОписаниеНачислений.Надбавки.Добавить(ОписаниеНачисления);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	КадровыеДанные.Сотрудник
		|ИЗ
		|	ВТКадровыеДанныеСотрудников КАК КадровыеДанные
		|ГДЕ
		|	ЕСТЬNULL(КадровыеДанные.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Сотрудник %1 не принят на работу'"),
			Выборка.Сотрудник);
		
		ВызватьИсключение ТекстИсключения;
		
	КонецЕсли;
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	КадровыеДанные.Организация,
		|	КадровыеДанные.Период
		|ИЗ
		|	ВТКадровыеДанныеСотрудников КАК КадровыеДанные";
	
	СведенияОбОрганизациях = Новый ТаблицаЗначений;
	СведенияОбОрганизациях.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	СведенияОбОрганизациях.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	СведенияОбОрганизациях.Колонки.Добавить("НаименованиеПолное", Новый ОписаниеТипов("Строка"));
	СведенияОбОрганизациях.Колонки.Добавить("ИНН", Новый ОписаниеТипов("Строка"));
	СведенияОбОрганизациях.Колонки.Добавить("КПП", Новый ОписаниеТипов("Строка"));
	СведенияОбОрганизациях.Колонки.Добавить("ТелефонОрганизации", Новый ОписаниеТипов("Строка"));
	СведенияОбОрганизациях.Колонки.Добавить("ФаксОрганизации", Новый ОписаниеТипов("Строка"));
	СведенияОбОрганизациях.Колонки.Добавить("АдресЮридический", Новый ОписаниеТипов("Строка"));
	СведенияОбОрганизациях.Колонки.Добавить("АдресФактический", Новый ОписаниеТипов("Строка"));
	СведенияОбОрганизациях.Колонки.Добавить("ОрганизацияГородФактическогоАдреса", Новый ОписаниеТипов("Строка"));
	
	РезультатЗапросаПоШапке = Запрос.Выполнить();
	
	АдресаОрганизаций = УправлениеКонтактнойИнформациейЗарплатаКадры.АдресаОрганизаций(РезультатЗапросаПоШапке.Выгрузить().ВыгрузитьКолонку("Организация"));
	
	Выборка = РезультатЗапросаПоШапке.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрокаСведенияОбОрганизациях = СведенияОбОрганизациях.Добавить();
		
		Сведения = Новый СписокЗначений;
		Сведения.Добавить("", "НаимЮЛПол");
		Сведения.Добавить("", "ИННЮЛ");
		Сведения.Добавить("", "КППЮЛ");
		Сведения.Добавить("", "ТелОрганизации");
		Сведения.Добавить("", "ФаксОрганизации");
		
		УстановитьПривилегированныйРежим(Истина);
		ОргСведения = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Выборка.Организация, Выборка.Период, Сведения);
		УстановитьПривилегированныйРежим(Ложь);
		
		НоваяСтрокаСведенияОбОрганизациях.Организация = Выборка.Организация;
		НоваяСтрокаСведенияОбОрганизациях.Период = Выборка.Период;
		
		Если ОргСведения.Свойство("НаимЮЛПол") Тогда
			НоваяСтрокаСведенияОбОрганизациях.НаименованиеПолное = ОргСведения.НаимЮЛПол;
		КонецЕсли;
		
		Если ОргСведения.Свойство("ИННЮЛ") Тогда
			НоваяСтрокаСведенияОбОрганизациях.ИНН = ОргСведения.ИННЮЛ;
		КонецЕсли;
		
		Если ОргСведения.Свойство("КППЮЛ") Тогда
			НоваяСтрокаСведенияОбОрганизациях.КПП = ОргСведения.КППЮЛ;
		КонецЕсли;
		
		Если ОргСведения.Свойство("ТелОрганизации") Тогда
			НоваяСтрокаСведенияОбОрганизациях.ТелефонОрганизации = ОргСведения.ТелОрганизации;
		КонецЕсли;
		
		Если ОргСведения.Свойство("ФаксОрганизации") Тогда
			НоваяСтрокаСведенияОбОрганизациях.ФаксОрганизации = ОргСведения.ФаксОрганизации;
		КонецЕсли;
		
		ОписаниеЮридическогоАдреса = УправлениеКонтактнойИнформациейЗарплатаКадры.АдресОрганизации(
			АдресаОрганизаций,
			Выборка.Организация,
			Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации);
		НоваяСтрокаСведенияОбОрганизациях.АдресЮридический = ОписаниеЮридическогоАдреса.Представление;
		
		ОписаниеФактическогоАдреса = УправлениеКонтактнойИнформациейЗарплатаКадры.АдресОрганизации(
			АдресаОрганизаций,
			Выборка.Организация,
			Справочники.ВидыКонтактнойИнформации.ФактАдресОрганизации);
		НоваяСтрокаСведенияОбОрганизациях.АдресФактический = ОписаниеФактическогоАдреса.Представление;
		НоваяСтрокаСведенияОбОрганизациях.ОрганизацияГородФактическогоАдреса = ОписаниеФактическогоАдреса.Город;
		
	КонецЦикла;
	
	Запрос.УстановитьПараметр("СведенияОбОрганизациях", СведенияОбОрганизациях);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СведенияОбОрганизациях.Период,
		|	СведенияОбОрганизациях.Организация,
		|	СведенияОбОрганизациях.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
		|	СведенияОбОрганизациях.ИНН КАК ИНН,
		|	СведенияОбОрганизациях.КПП КАК КПП,
		|	СведенияОбОрганизациях.ТелефонОрганизации КАК ТелефонОрганизации,
		|	СведенияОбОрганизациях.ФаксОрганизации КАК ФаксОрганизации,
		|	СведенияОбОрганизациях.АдресЮридический КАК ОрганизацияАдресЮридический,
		|	СведенияОбОрганизациях.АдресФактический КАК ОрганизацияАдресФактический,
		|	СведенияОбОрганизациях.ОрганизацияГородФактическогоАдреса КАК ОрганизацияГородФактическогоАдреса
		|ПОМЕСТИТЬ ВТДанныеОрганизаций
		|ИЗ
		|	&СведенияОбОрганизациях КАК СведенияОбОрганизациях
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СотрудникиСДаннымиОПриеме.Сотрудник КАК Ссылка,
		|	СотрудникиСДаннымиОПриеме.Сотрудник,
		|	СотрудникиСДаннымиОПриеме.ДатаПриема,
		|	КадровыеДанныеСотрудников.Подразделение,
		|	КадровыеДанныеСотрудников.Должность,
		|	КадровыеДанныеСотрудников.ВидЗанятости,
		|	ДанныеОрганизаций.Организация,
		|	ДанныеОрганизаций.ОрганизацияНаименованиеПолное,
		|	ДанныеОрганизаций.ИНН,
		|	ДанныеОрганизаций.КПП,
		|	ДанныеОрганизаций.ТелефонОрганизации,
		|	ДанныеОрганизаций.ФаксОрганизации,
		|	ДанныеОрганизаций.ОрганизацияАдресЮридический,
		|	ДанныеОрганизаций.ОрганизацияАдресФактический,
		|	ДанныеОрганизаций.ОрганизацияГородФактическогоАдреса,
		|	Организации.НаименованиеСокращенное КАК ОрганизацияНаименованиеСокращенное,
		|	КадровыеДанныеСотрудников.Период,
		|	КадровыеДанныеСотрудников.Страна,
		|	КадровыеДанныеСотрудников.ФИОПолные КАК СотрудникФИОПолные,
		|	КадровыеДанныеСотрудников.АдресМестаПроживанияПредставление КАК СотрудникАдресМестаПроживания,
		|	КадровыеДанныеСотрудников.ДокументПредставление КАК СотрудникУдостоверениеЛичности,
		|	КадровыеДанныеСотрудников.СтраховойНомерПФР КАК СотрудникСНИЛС,
		|	КадровыеДанныеСотрудников.КлассУсловийТруда КАК УсловияТрудаНаРабочемМесте,
		|	КадровыеДанныеСотрудников.ДатаДоговораКонтракта КАК ТрудовойДоговорДата,
		|	КадровыеДанныеСотрудников.НомерДоговораКонтракта КАК ТрудовойДоговорНомер,
		|	КадровыеДанныеСотрудников.ДатаОкончания КАК ДатаЗавершенияТрудовогоДоговора,
		|	КадровыеДанныеСотрудников.ОснованиеСрочногоДоговора,
		|	КадровыеДанныеСотрудников.ДлительностьРабочейНедели КАК ДлительностьРабочейНедели,
		|	КадровыеДанныеСотрудников.ОписаниеДлительностиРабочейНеделиВДнях КАК ОписаниеДлительностиРабочейНеделиВДнях,
		|	КадровыеДанныеСотрудников.ПродолжительностьРабочегоДня КАК ПродолжительностьРабочегоДня,
		|	КадровыеДанныеСотрудников.СокращеннаяРабочаяНеделя КАК СокращеннаяРабочаяНеделя,
		|	КадровыеДанныеСотрудников.НеполноеРабочееВремя КАК НеполноеРабочееВремя,
		|	КадровыеДанныеСотрудников.КоличествоДнейОсновногоОтпуска КАК ДлительностьОсновногоОтпуска,
		|	КадровыеДанныеСотрудников.КоличествоДнейОтпускаЗаВредность КАК ДлительностьВредногоОтпуска,
		|	КадровыеДанныеСотрудников.КоличествоДнейСеверногоОтпуска КАК ДлительностьСеверногоОтпуска,
		|	КадровыеДанныеСотрудников.ОснованиеПредставителя КАК ОснованиеРуководителяВРодительномПадеже,
		|	ДанныеПриказовОПриеме.ДлительностьИспытательногоСрока,
		|	ДанныеПриказовОПриеме.РазрешениеНаРаботу,
		|	ДанныеПриказовОПриеме.РазрешениеНаПроживание,
		|	ДанныеПриказовОПриеме.УсловияОказанияМедпомощи,
		|	ДанныеПриказовОПриеме.ДолжностьРуководителя КАК РуководительДолжность,
		|	ФИОРуководителя.ФИОПолные КАК РуководительФИОПолные,
		|	ФИОРуководителя.Пол КАК РуководительПол
		|ИЗ
		|	ВТСотрудникиСДаннымиОПриеме КАК СотрудникиСДаннымиОПриеме
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеПриказовОПриеме КАК ДанныеПриказовОПриеме
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТФИООтветственныхЛиц КАК ФИОРуководителя
		|			ПО ДанныеПриказовОПриеме.Дата = ФИОРуководителя.Дата
		|				И ДанныеПриказовОПриеме.Руководитель = ФИОРуководителя.ФизическоеЛицо
		|		ПО СотрудникиСДаннымиОПриеме.ДатаПриема = ДанныеПриказовОПриеме.Дата
		|			И СотрудникиСДаннымиОПриеме.Сотрудник = ДанныеПриказовОПриеме.Сотрудник
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеТрудовогоДоговора КАК КадровыеДанныеСотрудников
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеОрганизаций КАК ДанныеОрганизаций
		|				ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
		|				ПО ДанныеОрганизаций.Организация = Организации.Ссылка
		|			ПО КадровыеДанныеСотрудников.Организация = ДанныеОрганизаций.Организация
		|				И КадровыеДанныеСотрудников.Период = ДанныеОрганизаций.Период
		|		ПО СотрудникиСДаннымиОПриеме.Сотрудник = КадровыеДанныеСотрудников.Сотрудник
		|			И СотрудникиСДаннымиОПриеме.ДатаПриема = КадровыеДанныеСотрудников.Период";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДанныеДоговора = ПараметрыТрудовогоДоговора(ДистанционныеРаботы, ДоговорСНадомником);
		
		ИсключаемыеИЗЗаполненияКолонки =
			"ДатаПриема,
			|ТрудовойДоговорДата,
			|ВидЗанятости,
			|ОрганизацияНаименованиеСокращенное,
			|ДатаЗавершенияТрудовогоДоговора,
			|ОснованиеСрочногоДоговора,
			|ДлительностьРабочейНедели,
			|ОписаниеДлительностиРабочейНеделиВДнях,
			|ПродолжительностьРабочегоДня";
		
		ЗаполнитьЗначенияСвойств(ДанныеДоговора, Выборка, , ИсключаемыеИЗЗаполненияКолонки);
		
		Если ЗначениеЗаполнено(Выборка.ТрудовойДоговорДата) Тогда
			ДанныеДоговора.ТрудовойДоговорДата = Формат(Выборка.ТрудовойДоговорДата, "ДЛФ=DD");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.ДатаЗавершенияТрудовогоДоговора) Тогда
			
			ДанныеДоговора.СрочностьТрудовогоДоговора = НСтр("ru='срочный трудовой договор'");
			ДанныеДоговора.ДатаЗавершенияТрудовогоДоговора = Формат(Выборка.ДатаЗавершенияТрудовогоДоговора, "ДЛФ=DD");
			
			ДанныеДоговора.ОснованиеСрочногоДоговора = Выборка.ОснованиеСрочногоДоговора;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.ДатаПриема) Тогда
			
			ДанныеДоговора.ДатаПриема = Формат(Выборка.ДатаПриема, "ДЛФ=DD");
			Если Выборка.ТрудовойДоговорДата = NULL Тогда
				ДанныеДоговора.ТрудовойДоговорДата = ДанныеДоговора.ДатаПриема;
			КонецЕсли;
			
		Иначе
			ДанныеДоговора.ДатаПриема = Выборка.ТрудовойДоговорДата;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.ВидЗанятости) Тогда
			
			Если Выборка.ВидЗанятости = Перечисления.ВидыЗанятости.ОсновноеМестоРаботы Тогда
				ДанныеДоговора.ВидЗанятости = НСтр("ru='по основной работе'");
			Иначе
				ДанныеДоговора.ВидЗанятости = НСтр("ru='по совместительству'");
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.УсловияТрудаНаРабочемМесте) Тогда
			ДанныеДоговора.УсловияТрудаНаРабочемМесте = Выборка.УсловияТрудаНаРабочемМесте;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.ДлительностьРабочейНедели) Тогда
			
			ДанныеДоговора.ДлительностьРабочейНедели =
				ЧислоПрописью(Выборка.ДлительностьРабочейНедели, "Л = ru_RU", НСтр("ru='час,часа,часов,м,,,,,0'"))
				+ " " + НСтр("ru='в неделю'");
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.ОписаниеДлительностиРабочейНеделиВДнях)
			Или Выборка.ОписаниеДлительностиРабочейНеделиВДнях = 0 Тогда
			
			Если Выборка.ОписаниеДлительностиРабочейНеделиВДнях = 5 Тогда
				ДанныеДоговора.ОписаниеДлительностиРабочейНеделиВДнях = НСтр("ru='пятидневная с двумя выходными днями'");
			ИначеЕсли Выборка.ОписаниеДлительностиРабочейНеделиВДнях = 6 Тогда
				ДанныеДоговора.ОписаниеДлительностиРабочейНеделиВДнях = НСтр("ru='шестидневная с одним выходными днем'");
			ИначеЕсли Выборка.СокращеннаяРабочаяНеделя = Истина Тогда
				ДанныеДоговора.ОписаниеДлительностиРабочейНеделиВДнях = НСтр("ru='неполная рабочая неделя'");
			ИначеЕсли Выборка.НеполноеРабочееВремя = Истина Тогда
				ДанныеДоговора.ОписаниеДлительностиРабочейНеделиВДнях = НСтр("ru='сокращенное рабочее время'");
			Иначе
				ДанныеДоговора.ОписаниеДлительностиРабочейНеделиВДнях = НСтр("ru='рабочая неделя с предоставлением выходных дней по скользящему графику'");
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.ПродолжительностьРабочегоДня) Тогда
			ДанныеДоговора.ПродолжительностьРабочегоДня = Выборка.ПродолжительностьРабочегоДня;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.ОрганизацияНаименованиеСокращенное) Тогда
			ДанныеДоговора.ОрганизацияНаименованиеСокращенное = ", " + Выборка.ОрганизацияНаименованиеСокращенное;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.ИНН) Тогда
			
			ДанныеДоговора.ОрганизацияИННКПП = Выборка.ИНН;
			
			Если ЗначениеЗаполнено(Выборка.КПП) Тогда
				ДанныеДоговора.ОрганизацияИННКПП = ДанныеДоговора.ОрганизацияИННКПП + " / " + Выборка.КПП;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.ОснованиеРуководителяВРодительномПадеже) Тогда
			ДанныеДоговора.ОснованиеРуководителяВРодительномПадеже = Выборка.ОснованиеРуководителяВРодительномПадеже;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.РуководительФИОПолные) Тогда
			
			РезультатСклонения = "";
			Если ФизическиеЛицаЗарплатаКадры.Просклонять(Выборка.РуководительФИОПолные, 2, РезультатСклонения, Выборка.РуководительПол) Тогда
				ДанныеДоговора.РуководительФИОПолныеВРодительномПадеже = РезультатСклонения;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.ДлительностьИспытательногоСрока) Тогда
			
			ДанныеДоговора.ИспытательныйСрок = НСтр("ru='устанавливается'");
			ДанныеДоговора.ИспытательныйСрокМесяцев = Выборка.ДлительностьИспытательногоСрока;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.РазрешениеНаПроживание) Тогда
			ДанныеДоговора.ИныеДокументыПредставляемыеИностраннымиГражданами = Выборка.РазрешениеНаПроживание
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.РазрешениеНаРаботу) Тогда
			
			Если ЗначениеЗаполнено(ДанныеДоговора.ИныеДокументыПредставляемыеИностраннымиГражданами) Тогда
				
				ДанныеДоговора.ИныеДокументыПредставляемыеИностраннымиГражданами =
					ДанныеДоговора.ИныеДокументыПредставляемыеИностраннымиГражданами + ";" + Символы.ПС;
				
			КонецЕсли;
			
			ДанныеДоговора.ИныеДокументыПредставляемыеИностраннымиГражданами =
				ДанныеДоговора.ИныеДокументыПредставляемыеИностраннымиГражданами
				+ Выборка.РазрешениеНаРаботу;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.РуководительДолжность) Тогда
			
			ДанныеДоговора.РуководительДолжностьВРодительномПадеже =
				СклонениеПредставленийОбъектов.ПросклонятьПредставление(Строка(Выборка.РуководительДолжность), 2, Выборка.РуководительДолжность);
			
		КонецЕсли;
		
		Периоды = НачисленияСотрудников.Получить(Выборка.Период);
		Если Периоды <> Неопределено Тогда
			
			ОписанияНачислений = Периоды.Получить(Выборка.Сотрудник);
			Если ОписанияНачислений <> Неопределено Тогда
				
				Если ЗначениеЗаполнено(ОписанияНачислений.ТарифнаяСтавка.Начисление) Тогда
					ДанныеДоговора.ТарифнаяСтавка = ПредставлениеТарифнойСтавкиДоговоровМикропредприятий(ОписанияНачислений.ТарифнаяСтавка);
				ИначеЕсли ЗначениеЗаполнено(ОписанияНачислений.СдельнаяОплатаТруда.Начисление) Тогда
					
					ДанныеДоговора.СдельнаяОплатаТруда = Строка(ОписанияНачислений.СдельнаяОплатаТруда.Начисление);
					Если ЗначениеЗаполнено(ОписанияНачислений.СдельнаяОплатаТруда.Размер) Тогда
						ДанныеДоговора.СдельнаяОплатаТруда = ДанныеДоговора.СдельнаяОплатаТруда + " " + Формат(ОписанияНачислений.СдельнаяОплатаТруда.Размер, "ЧДЦ=2");
					КонецЕсли;
					
				КонецЕсли;
				
				ДанныеДоговора.КомпенсационныеВыплаты = ОписанияНачислений.Компенсации;
				ДанныеДоговора.СтимулирующиеВыплаты = ОписанияНачислений.Надбавки;
				
			КонецЕсли;
			
		КонецЕсли;
		
		ДанныеТрудовыхДоговоров.Добавить(ДанныеДоговора);
		
	КонецЦикла;
	
	Возврат ДанныеТрудовыхДоговоров;
	
КонецФункции

Функция ПараметрыТрудовогоДоговора(ДистанционныеРаботы = Ложь, ДоговорСНадомником = Ложь)
	
	ПараметрыДоговора = Новый Структура;
	
	ПараметрыДоговора.Вставить("Ссылка");
	
	ПараметрыДоговора.Вставить("ТрудовойДоговорНомер", "__________");
	ПараметрыДоговора.Вставить("ТрудовойДоговорДата");
	
	ПараметрыДоговора.Вставить("ТрудовойДоговорНаДистанционныеРаботы", ДистанционныеРаботы);
	ПараметрыДоговора.Вставить("ТрудовойДоговорСНадомником", ДоговорСНадомником);
	ПараметрыДоговора.Вставить("ТрудовойДоговорСИностранцем", Ложь);
	ПараметрыДоговора.Вставить("ОснованиеСрочногоДоговора");
	
	ПараметрыДоговора.Вставить("СотрудникФИОПолные");
	ПараметрыДоговора.Вставить("СотрудникАдресМестаПроживания");
	ПараметрыДоговора.Вставить("СотрудникУдостоверениеЛичности");
	ПараметрыДоговора.Вставить("СотрудникСНИЛС");
	
	ПараметрыДоговора.Вставить("УсловияОказанияМедпомощи");
	ПараметрыДоговора.Вставить("ИныеДокументыПредставляемыеИностраннымиГражданами");
	
	ПараметрыДоговора.Вставить("ДатаПриема");
	ПараметрыДоговора.Вставить("Подразделение");
	ПараметрыДоговора.Вставить("Должность");
	ПараметрыДоговора.Вставить("УсловияТрудаНаРабочемМесте");
	ПараметрыДоговора.Вставить("СрочностьТрудовогоДоговора", НСтр("ru='трудовой договор на неопределенный срок'"));
	ПараметрыДоговора.Вставить("ДатаЗавершенияТрудовогоДоговора");
	ПараметрыДоговора.Вставить("ИспытательныйСрок", НСтр("ru='не устанавливается'"));
	ПараметрыДоговора.Вставить("ИспытательныйСрокМесяцев");
	ПараметрыДоговора.Вставить("ВидЗанятости");
	ПараметрыДоговора.Вставить("МестоОпределенияДолжностныхОбязанностей", НСтр("ru='в должностной инструкции'"));
	
	ПараметрыДоговора.Вставить("ТарифнаяСтавка");
	ПараметрыДоговора.Вставить("СдельнаяОплатаТруда");
	ПараметрыДоговора.Вставить("КомпенсационныеВыплаты", Новый Массив);
	ПараметрыДоговора.Вставить("СтимулирующиеВыплаты", Новый Массив);
	
	ПараметрыДоговора.Вставить("ДлительностьРабочейНедели", НСтр("ru='40 часов в неделю'"));
	ПараметрыДоговора.Вставить("ОписаниеДлительностиРабочейНеделиВДнях", НСтр("ru='пятидневная с двумя выходными днями'"));
	ПараметрыДоговора.Вставить("ПродолжительностьРабочегоДня", 8);
	
	ПараметрыДоговора.Вставить("ДлительностьОсновногоОтпуска", 28);
	ПараметрыДоговора.Вставить("ДлительностьВредногоОтпуска", 0);
	ПараметрыДоговора.Вставить("ДлительностьСеверногоОтпуска", 0);
	
	ПараметрыДоговора.Вставить("ОрганизацияНаименованиеПолное");
	ПараметрыДоговора.Вставить("ОрганизацияНаименованиеСокращенное");
	ПараметрыДоговора.Вставить("ОрганизацияГородФактическогоАдреса");
	ПараметрыДоговора.Вставить("ОрганизацияАдресЮридический");
	ПараметрыДоговора.Вставить("ОрганизацияАдресФактический");
	ПараметрыДоговора.Вставить("ОрганизацияИННКПП");
	
	ПараметрыДоговора.Вставить("РуководительДолжностьВРодительномПадеже");
	ПараметрыДоговора.Вставить("РуководительФИОПолныеВРодительномПадеже");
	ПараметрыДоговора.Вставить("ОснованиеРуководителяВРодительномПадеже", НСтр("ru='устава'"));
	
	Возврат ПараметрыДоговора;
	
КонецФункции

Функция ПредставлениеТарифнойСтавкиДоговоровМикропредприятий(ОписаниеТарифнойСтавки)
	
	Возврат КадровыйУчетВнутренний.ПредставлениеТарифнойСтавкиДоговоровМикропредприятий(ОписаниеТарифнойСтавки);
	
КонецФункции

Функция ДанныеСправокОСреднемЗаработкеДляПособияПоБезработице(МассивОбъектов) Экспорт
	
	ДанныеСправок = Новый Массив;
	
	ДатаСведений = ОбщегоНазначения.РабочаяДатаПользователя();
	Если Не ЗначениеЗаполнено(ДатаСведений) Тогда
		ДатаСведений = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ФОИспользоватьКадровыйУчет = ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет");
	
	Если Не ФОИспользоватьКадровыйУчет Тогда
		Сотрудники = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(МассивОбъектов);
	Иначе
		
		Если ТипЗнч(МассивОбъектов[0]) = Тип("СправочникСсылка.Сотрудники") Тогда
			
			Сотрудники = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(МассивОбъектов);
			
			ИменаКадровыхДанных = "ПриказОбУвольнении";
			ПриказыОбУвольнении = КадровыйУчет.КадровыеДанныеСотрудников(Истина, Сотрудники, ИменаКадровыхДанных, ДатаСведений).ВыгрузитьКолонку("ПриказОбУвольнении");
			
		Иначе
			ПриказыОбУвольнении = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(МассивОбъектов);
		КонецЕсли;
		
		КадровыйУчет.СоздатьВТДанныеДокументовУвольнение(Запрос.МенеджерВременныхТаблиц, ПриказыОбУвольнении);
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ДанныеДокументов.*
			|ИЗ
			|	ВТДанныеДокументов КАК ДанныеДокументов";
			
		ДанныеПриказов = Запрос.Выполнить().Выгрузить();
		
		Если ТипЗнч(МассивОбъектов[0]) <> Тип("СправочникСсылка.Сотрудники") Тогда
			Сотрудники = ДанныеПриказов.ВыгрузитьКолонку("Сотрудник");
		КонецЕсли;
		
	КонецЕсли;
	
	ИменаКадровыхДанных = "ФИОПолные,Пол,ГоловнаяОрганизация,Организация,ДатаПриема,ДатаУвольнения,ПриказОбУвольнении,ВидЗанятости";
	КадровыеДанныеСотрудников = КадровыйУчет.КадровыеДанныеСотрудников(Истина, Сотрудники, ИменаКадровыхДанных, ДатаСведений);
	
	Для Каждого ДанныеСотрудника Из КадровыеДанныеСотрудников Цикл
		
		Если Не ЗначениеЗаполнено(ДанныеСотрудника.ВидЗанятости) Тогда
			
			ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Сотрудник %1 еще не оформлен на работу'"),
				ДанныеСотрудника.Сотрудник);
			
			ВызватьИсключение ТекстИсключения;
			
		ИначеЕсли Не ЗначениеЗаполнено(ДанныеСотрудника.ДатаУвольнения) Тогда
			
			ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='По сотруднику %1 еще не оформлено увольнение'"),
				ДанныеСотрудника.Сотрудник);
			
			ВызватьИсключение ТекстИсключения;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Сбор сведений организаций
	Организации = КадровыеДанныеСотрудников.ВыгрузитьКолонку("Организация");
	Запрос.УстановитьПараметр("Организации", Организации);
	Запрос.УстановитьПараметр("ДатаСведений", ДатаСведений);
	
	СведенияОбОрганизациях = Новый ТаблицаЗначений;
	СведенияОбОрганизациях.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	СведенияОбОрганизациях.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	СведенияОбОрганизациях.Колонки.Добавить("НаименованиеПолное", Новый ОписаниеТипов("Строка"));
	СведенияОбОрганизациях.Колонки.Добавить("ИНН", Новый ОписаниеТипов("Строка"));
	СведенияОбОрганизациях.Колонки.Добавить("ОКВЭД", Новый ОписаниеТипов("Строка"));
	СведенияОбОрганизациях.Колонки.Добавить("ТелефонОрганизации", Новый ОписаниеТипов("Строка"));
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Организации.Ссылка КАК Организация,
		|	&ДатаСведений КАК Период
		|ИЗ
		|	Справочник.Организации КАК Организации Где Организации.Ссылка В (&Организации)";
	
	РезультатЗапросаПоШапке = Запрос.Выполнить();
	Выборка = РезультатЗапросаПоШапке.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрокаСведенияОбОрганизациях = СведенияОбОрганизациях.Добавить();
		
		Сведения = Новый СписокЗначений;
		Сведения.Добавить("", "НаимЮЛПол");
		Сведения.Добавить("", "ИННЮЛ");
		Сведения.Добавить("", "ОКВЭД");
		Сведения.Добавить("", "ТелОрганизации");
		
		УстановитьПривилегированныйРежим(Истина);
		ОргСведения = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Выборка.Организация, Выборка.Период, Сведения);
		УстановитьПривилегированныйРежим(Ложь);
		
		НоваяСтрокаСведенияОбОрганизациях.Организация = Выборка.Организация;
		НоваяСтрокаСведенияОбОрганизациях.Период = Выборка.Период;
		НоваяСтрокаСведенияОбОрганизациях.НаименованиеПолное = ОргСведения.НаимЮЛПол;
		НоваяСтрокаСведенияОбОрганизациях.ИНН = ОргСведения.ИННЮЛ;
		НоваяСтрокаСведенияОбОрганизациях.ОКВЭД = ОргСведения.ОКВЭД;
		НоваяСтрокаСведенияОбОрганизациях.ТелефонОрганизации = ОргСведения.ТелОрганизации;
		
	КонецЦикла;
	
	// СборСведений о среднем заработке
	СведенияОСреднемЗаработке = КадровыйУчетВнутренний.СведенияОСреднемЗаработкеДляСправкиПоБезработице(КадровыеДанныеСотрудников);
	
	// СборСведений о графиках работы
	СведенияОГрафикахРаботы = КадровыйУчетВнутренний.СведенияОГрафикахРаботыДляСправкиПоБезработице(КадровыеДанныеСотрудников);
	
	// СборСведений о периодах не работы
	СведенияОПериодахНеРаботы = КадровыйУчетВнутренний.СведенияОПериодахНеРаботыДляСправкиПоБезработице(КадровыеДанныеСотрудников);
	
	Для Каждого КадровыеДанныеСотрудника Из КадровыеДанныеСотрудников Цикл
		
		ДанныеСправки = ПараметрыСправокОСреднемЗаработкеДляПособияПоБезработице();
		
		ДанныеСправки.ДатаСправки = Макс(КадровыеДанныеСотрудника.Период, КадровыеДанныеСотрудника.ДатаУвольнения);
		
		ФизическиеЛицаЗарплатаКадры.Просклонять(КадровыеДанныеСотрудника.ФИОПолные, 3,
			ДанныеСправки.СотрудникФИОПолныеВДательномПадеже, КадровыеДанныеСотрудника.Пол);
		
		ДанныеСправки.НачалоПериодаРаботы = КадровыеДанныеСотрудника.ДатаПриема;
		ДанныеСправки.ОкончаниеПериодаРаботы = КадровыеДанныеСотрудника.ДатаУвольнения;
		
		ДанныеОрганизации = СведенияОбОрганизациях.НайтиСтроки(Новый Структура("Организация", КадровыеДанныеСотрудника.Организация));
		Если ДанныеОрганизации.Количество() > 0 Тогда
			
			СведенияОбОрганизации = ДанныеОрганизации[0];
			
			ДанныеСправки.ОрганизацияИНН = СведенияОбОрганизации.ИНН;
			ДанныеСправки.ОрганизацияОКВЭД = СведенияОбОрганизации.ОКВЭД;
			ДанныеСправки.ОрганизацияНаименованиеПолное = СведенияОбОрганизации.НаименованиеПолное;
			ДанныеСправки.ОрганизацияТелефон = СведенияОбОрганизации.ТелефонОрганизации;
			
		КонецЕсли;
		
		СреднийЗаработок = СведенияОСреднемЗаработке.Получить(КадровыеДанныеСотрудника.Сотрудник);
		Если СреднийЗаработок <> Неопределено И СреднийЗаработок > 0 Тогда
			
			ДанныеСправки.СреднийЗаработокРубли = Цел(СреднийЗаработок);
			ДанныеСправки.СреднийЗаработокКопейки = (СреднийЗаработок - ДанныеСправки.СреднийЗаработокРубли) * 100;
			
			ДанныеСправки.СреднийЗаработокПрописью = ЧислоПрописью(СреднийЗаработок, "Л = ru_RU", НСтр("ru='рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 2'"));
			
		КонецЕсли;
		
		ПериодыРаботы = СведенияОГрафикахРаботы.Получить(КадровыеДанныеСотрудника.Сотрудник);
		Если ПериодыРаботы <> Неопределено Тогда
			
			ПоследняяУчтеннаяНеделя = Неопределено;
			Для Каждого ПериодРаботы Из ПериодыРаботы Цикл
				
				ЗаполнитьЗначенияСвойств(ДанныеСправки.ПериодыРаботы.Добавить(), ПериодРаботы);
				
				// Проверка, что неделя начала периода еще не учтена
				Если ПоследняяУчтеннаяНеделя <> Неопределено Тогда
					
					НачалоНеделиПериода = НачалоНедели(ПериодРаботы.НачалоПериода);
					Если Год(ПоследняяУчтеннаяНеделя) = Год(НачалоНеделиПериода) Тогда
						
						Если ПоследняяУчтеннаяНеделя = НачалоНеделиПериода Тогда
							
							Если ПоследняяУчтеннаяНеделя = НачалоНедели(ПериодРаботы.ОкончаниеПериода) Тогда
								Продолжить;
							Иначе
								НачалоПериода = КонецНедели(ПериодРаботы.НачалоПериода) + 1;
							КонецЕсли;
							
						Иначе
							НачалоПериода = ПериодРаботы.НачалоПериода;
						КонецЕсли;
						
					Иначе
						НачалоПериода = ПериодРаботы.НачалоПериода;
					КонецЕсли;
					
				Иначе
					НачалоПериода = ПериодРаботы.НачалоПериода;
				КонецЕсли;
				
				Если Год(ПериодРаботы.НачалоПериода) = Год(ПериодРаботы.ОкончаниеПериода) Тогда
					НедельПериода = НеделяГода(ПериодРаботы.ОкончаниеПериода) - НеделяГода(ПериодРаботы.НачалоПериода) + 1;
				Иначе
					НедельПериода = (КонецДня(ПериодРаботы.ОкончаниеПериода) - ПериодРаботы.НачалоПериода + 1) / 60 / 60 / 24 / 7;
				КонецЕсли;
				
				ЦелыхНедельПериода = Цел(НедельПериода);
				Если ЦелыхНедельПериода < НедельПериода Тогда
					ЦелыхНедельПериода = ЦелыхНедельПериода + 1;
				КонецЕсли;
				
				ДанныеСправки.КалендарныхНедель = ДанныеСправки.КалендарныхНедель + ЦелыхНедельПериода;
				
				ПоследняяУчтеннаяНеделя = НачалоНедели(ПериодРаботы.ОкончаниеПериода);
				
			КонецЦикла;
			
		КонецЕсли;
		
		ПериодыНеРаботы = СведенияОПериодахНеРаботы.Получить(КадровыеДанныеСотрудника.Сотрудник);
		Если ПериодыНеРаботы <> Неопределено Тогда
			
			Для Каждого ПериодНеРаботы Из ПериодыНеРаботы Цикл
				
				ЗаполнитьЗначенияСвойств(ДанныеСправки.ПериодыНеРаботы.Добавить(), ПериодНеРаботы);
				
				Если Год(ПериодНеРаботы.НачалоПериода) = Год(ПериодНеРаботы.ОкончаниеПериода) Тогда
					НедельПериода = НеделяГода(ПериодНеРаботы.ОкончаниеПериода) - НеделяГода(ПериодНеРаботы.НачалоПериода) + 1;
				Иначе
					
					НедельПериодаПредварительно = (КонецДня(ПериодНеРаботы.ОкончаниеПериода) - ПериодНеРаботы.НачалоПериода + 1) / 60 / 60 / 24 / 7;
					НедельПериода = Цел(НедельПериодаПредварительно);
					Если НедельПериода < НедельПериодаПредварительно Тогда
						НедельПериода = НедельПериода + 1;
					КонецЕсли;
					
				КонецЕсли;
				
				Если НачалоНедели(ПериодНеРаботы.НачалоПериода) < ПериодНеРаботы.НачалоПериода Тогда
					НедельПериода = НедельПериода - 1;
				КонецЕсли;
				
				Если КонецНедели(ПериодНеРаботы.ОкончаниеПериода) > КонецДня(ПериодНеРаботы.ОкончаниеПериода)
					И НеделяГода(ПериодНеРаботы.ОкончаниеПериода) > НеделяГода(ПериодНеРаботы.НачалоПериода) Тогда
					
					НедельПериода = НедельПериода - 1;
					
				КонецЕсли;
				
				ДанныеСправки.КалендарныхНедель = ДанныеСправки.КалендарныхНедель - НедельПериода;
				
			КонецЦикла;
			
		КонецЕсли;
		
		Руководитель = Неопределено;
		ГлавныйБухгалтер = Неопределено;
		
		Если ФОИспользоватьКадровыйУчет Тогда
			
			СтрокиПриказа = ДанныеПриказов.НайтиСтроки(Новый Структура("Сотрудник", КадровыеДанныеСотрудника.Сотрудник));
			Если СтрокиПриказа.Количество() > 0 Тогда
				Руководитель = СтрокиПриказа[0].Руководитель;
				ГлавныйБухгалтер = СтрокиПриказа[0].ГлавныйБухгалтер;
			КонецЕсли;
			
		Иначе
			
			Подписанты = Новый Структура("Организация,Руководитель,ГлавныйБухгалтер", КадровыеДанныеСотрудника.ГоловнаяОрганизация);
			ЗарплатаКадры.ПолучитьЗначенияПоУмолчанию(Подписанты, КадровыеДанныеСотрудника.Период);
			
			Руководитель = Подписанты.Руководитель;
			ГлавныйБухгалтер = Подписанты.ГлавныйБухгалтер;
			
		КонецЕсли;
		
		СписокПодписантов = Новый Массив;
		Если ЗначениеЗаполнено(Руководитель) Тогда
			СписокПодписантов.Добавить(Руководитель);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ГлавныйБухгалтер) И Руководитель <> ГлавныйБухгалтер Тогда
			СписокПодписантов.Добавить(ГлавныйБухгалтер);
		КонецЕсли;
		
		Если СписокПодписантов.Количество() > 0 Тогда
			
			ДанныеПодписантов = КадровыйУчет.КадровыеДанныеФизическихЛиц(Истина, СписокПодписантов, "ИОФамилия", КадровыеДанныеСотрудника.Период);
			
			Если ЗначениеЗаполнено(Руководитель) Тогда
				
				ДанныеПодписанта = ДанныеПодписантов.Найти(Руководитель, "ФизическоеЛицо");
				Если ДанныеПодписанта <> Неопределено Тогда
					
					ДанныеСправки.ИОФамилияРуководителя = ДанныеПодписанта.ИОФамилия;
					Если Руководитель = ГлавныйБухгалтер Тогда
						ДанныеСправки.ИОФамилияГлавногоБухгалтера = ДанныеПодписанта.ИОФамилия;
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
				
			Если ЗначениеЗаполнено(ГлавныйБухгалтер) И Руководитель <> ГлавныйБухгалтер Тогда
				
				ДанныеПодписанта = ДанныеПодписантов.Найти(ГлавныйБухгалтер, "ФизическоеЛицо");
				Если ДанныеПодписанта <> Неопределено Тогда
					ДанныеСправки.ИОФамилияГлавногоБухгалтера = ДанныеПодписанта.ИОФамилия;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		ДанныеСправок.Добавить(ДанныеСправки);
		
	КонецЦикла;
	
	Возврат ДанныеСправок;
	
КонецФункции

Функция ПараметрыСправокОСреднемЗаработкеДляПособияПоБезработице()
	
	ПараметрыСправок = Новый Структура;
	
	ПараметрыСправок.Вставить("ДатаСправки", ОбщегоНазначения.РабочаяДатаПользователя());
	
	ПараметрыСправок.Вставить("ОрганизацияИНН");
	ПараметрыСправок.Вставить("ОрганизацияОКВЭД");
	ПараметрыСправок.Вставить("ОрганизацияНаименованиеПолное");
	ПараметрыСправок.Вставить("ОрганизацияТелефон");
	
	ПараметрыСправок.Вставить("СотрудникФИОПолныеВДательномПадеже");
	
	ПараметрыСправок.Вставить("НачалоПериодаРаботы");
	ПараметрыСправок.Вставить("ОкончаниеПериодаРаботы");
	
	ПараметрыСправок.Вставить("СреднийЗаработокРубли");
	ПараметрыСправок.Вставить("СреднийЗаработокКопейки");
	ПараметрыСправок.Вставить("СреднийЗаработокПрописью");
	
	ПараметрыСправок.Вставить("КалендарныхНедель", 0);
	
	ТаблицаПериодовРаботы = Новый ТаблицаЗначений;
	ТаблицаПериодовРаботы.Колонки.Добавить("Часов", Новый ОписаниеТипов("Число"));
	ТаблицаПериодовРаботы.Колонки.Добавить("Дней", Новый ОписаниеТипов("Число"));
	ТаблицаПериодовРаботы.Колонки.Добавить("НачалоПериода", Новый ОписаниеТипов("Дата"));
	ТаблицаПериодовРаботы.Колонки.Добавить("ОкончаниеПериода", Новый ОписаниеТипов("Дата"));
	ТаблицаПериодовРаботы.Колонки.Добавить("ПериодРаботыСПолнымРабочимДнем", Новый ОписаниеТипов("Булево"));
	
	ПараметрыСправок.Вставить("ПериодыРаботы", ТаблицаПериодовРаботы);
	
	ТаблицаПериодовНеРаботы = Новый ТаблицаЗначений;
	ТаблицаПериодовНеРаботы.Колонки.Добавить("НачалоПериода", Новый ОписаниеТипов("Дата"));
	ТаблицаПериодовНеРаботы.Колонки.Добавить("ОкончаниеПериода", Новый ОписаниеТипов("Дата"));
	ТаблицаПериодовНеРаботы.Колонки.Добавить("ПричинаОтсутствия", Новый ОписаниеТипов("Строка"));
	
	ПараметрыСправок.Вставить("ПериодыНеРаботы", ТаблицаПериодовНеРаботы);
	
	ПараметрыСправок.Вставить("ИОФамилияРуководителя");
	ПараметрыСправок.Вставить("ИОФамилияГлавногоБухгалтера");
	
	Возврат ПараметрыСправок;
	
КонецФункции

#Область КадровыеДанныеФизическихЛиц

Процедура ДобавитьВЗапросПоляКадровыхДанныхФизическихЛиц(Запрос, КадровыеДанные, ИсточникиДанных, ОписательВТОтборов, ИмяВТКадровыеДанныеФизическихЛиц) Экспорт
	
	КадровыйУчетВнутренний.ДобавитьВЗапросПоляКадровыхДанныхФизическихЛиц(Запрос, КадровыеДанные, ИсточникиДанных, ОписательВТОтборов, ИмяВТКадровыеДанныеФизическихЛиц);
	
КонецПроцедуры

Процедура ДобавитьВЗапросСоединенияСИсточникамиКадровыхДанныхФизическихЛиц(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИсточникиДанных, ИмяВТКадровыеДанныеФизическихЛиц) Экспорт
	
	КадровыйУчетВнутренний.ДобавитьВЗапросСоединенияСИсточникамиКадровыхДанныхФизическихЛиц(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ПоляОтбораПериодическихДанных, ИсточникиДанных, ИмяВТКадровыеДанныеФизическихЛиц);
	
КонецПроцедуры

Процедура ДобавитьВЗапросПоляПостоянныхКадровыхДанныхФизическихЛиц(Запрос, КадровыеДанные, ИсточникиДанных, ИмяВТПостоянныеКадровыеДанныеФизическихЛиц) Экспорт
	
	КадровыйУчетВнутренний.ДобавитьВЗапросПоляПостоянныхКадровыхДанныхФизическихЛиц(Запрос, КадровыеДанные, ИсточникиДанных, ИмяВТПостоянныеКадровыеДанныеФизическихЛиц);
	
КонецПроцедуры

Процедура ДобавитьВЗапросСоединенияСИсточникамиПостоянныхКадровыхДанныхФизическихЛиц(Запрос, ТолькоРазрешенные, ОписательВТОтборов, ИсточникиДанных) Экспорт
	
	КадровыйУчетВнутренний.ДобавитьВЗапросСоединенияСИсточникамиПостоянныхКадровыхДанныхФизическихЛиц(Запрос, ТолькоРазрешенные, ОписательВТОтборов, ИсточникиДанных);
	
КонецПроцедуры

Процедура ЗаполнитьГоловногоСотрудникаИИнтервальныйРегистрСведений(ПараметрыОбновления = Неопределено) Экспорт
	
	ЗаполнитьГоловногоСотрудникаРегистрСведений("КадроваяИсторияСотрудников", ПараметрыОбновления);
	
	//Пока полностью не выполнено заполнение головных сотрудников
	//  перенос в интервальный регистр не вызывается
	Если ПараметрыОбновления <> Неопределено
		И ПараметрыОбновления.Свойство("ОбработкаЗавершена")
		И ПараметрыОбновления.ОбработкаЗавершена = Ложь Тогда
		
		Возврат;
	КонецЕсли;
	
	ПараметрыРесурсов = ЗарплатаКадрыПериодическиеРегистры.ПараметрыНаследованияРесурсов("КадроваяИсторияСотрудников");
	Для каждого КлючИЗначение Из ПараметрыРесурсов Цикл
	
		Ресурс = КлючИЗначение.Ключ;
		Наследование = КлючИЗначение.Значение;
		Если Ресурс = "ВидСобытия" Тогда
			
			Наследование.ПравилоНаследования = "Фиксированное";
			Наследование.Значение = Перечисления.ВидыКадровыхСобытий.Увольнение;
			
		Иначе
			Наследование.ПравилоНаследования = "Наследование";
		КонецЕсли; 
		
	КонецЦикла;
	
	ПараметрыПостроения = ЗарплатаКадрыПериодическиеРегистры.ПараметрыПостроенияИнтервальногоРегистра();
	ПараметрыПостроения.ОсновноеИзмерение = "Сотрудник";
	ПараметрыПостроения.ПараметрыРесурсов = ПараметрыРесурсов;
	
	ЗарплатаКадрыПериодическиеРегистры.ПеренестиВозвратныйРегистрВИнтервальныйРегистрСведений("КадроваяИсторияСотрудников", ПараметрыПостроения, ПараметрыОбновления);
	
КонецПроцедуры

Процедура ЗаполнитьГоловногоСотрудникаРегистрСведений(ИмяРегистра, ПараметрыОбновления = Неопределено) Экспорт 
	
	МетаданныеРегистра = Метаданные.РегистрыСведений.Найти(ИмяРегистра);
	Если МетаданныеРегистра = Неопределено Тогда
		ВызватьИсключение НСтр("ru='Неизвестное имя регистра'") + " """ + ИмяРегистра + """";
	КонецЕсли;
	
	ПодчиненРегистратору = МетаданныеРегистра.РежимЗаписи = Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.ПодчинениеРегистратору;
	
	Запрос = Новый Запрос;
	ТекстЗапроса =  
	"ВЫБРАТЬ
	|	*,
	|	РегистрСведений.Сотрудник.ГоловнойСотрудник КАК ГоловнойСотрудникЗаполнение
	|ИЗ
	|	РегистрСведений.#ИмяРегистра КАК РегистрСведений
	|ГДЕ
	|	РегистрСведений.ГоловнойСотрудник = &ПустойГоловнойСотрудник";
	
	Если ПодчиненРегистратору Тогда
	
		ТекстЗапроса = ТекстЗапроса + "
			|УПОРЯДОЧИТЬ ПО
			|	РегистрСведений.Регистратор";
			
	Иначе
		
		ТекстЗапроса = ТекстЗапроса + "
			|УПОРЯДОЧИТЬ ПО
			|	РегистрСведений.Сотрудник";
			
	КонецЕсли; 
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяРегистра", ИмяРегистра);
	Запрос.УстановитьПараметр("ПустойГоловнойСотрудник", Справочники.Сотрудники.ПустаяСсылка());
	Запрос.Текст = ТекстЗапроса;
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	Иначе
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	КонецЕсли;
	Выборка = Результат.Выбрать();
	
	НаборЗаписей = РегистрыСведений[ИмяРегистра].СоздатьНаборЗаписей();
	НаборЗаписей.ОбменДанными.Загрузка = Истина;
	
	Если ПодчиненРегистратору Тогда
	
		Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл
			
			ИмяПространстваБлокировки = "РегистрСведений." + ИмяРегистра + ".НаборЗаписей";
			Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, ИмяПространстваБлокировки, "Регистратор", Выборка.Регистратор) Тогда
				Продолжить;
			КонецЕсли;
				
			НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
			Пока Выборка.Следующий() Цикл
				НоваяСтрока = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
				НоваяСтрока.ГоловнойСотрудник = Выборка.ГоловнойСотрудникЗаполнение;
				НоваяСтрока.ЭтоГоловнойСотрудник = НоваяСтрока.Сотрудник = НоваяСтрока.ГоловнойСотрудник;
			КонецЦикла;
			НаборЗаписей.Записать();
			НаборЗаписей.Очистить();
			ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
			
		КонецЦикла;
		
	Иначе
		
		Пока Выборка.СледующийПоЗначениюПоля("Сотрудник") Цикл
			
			ИмяПространстваБлокировки = "РегистрСведений." + ИмяРегистра;
			Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, ИмяПространстваБлокировки, "Сотрудник", Выборка.Сотрудник) Тогда
				Продолжить;
			КонецЕсли;
			
			НаборЗаписей.Отбор.Сотрудник.Установить(Выборка.Сотрудник);
			Пока Выборка.Следующий() Цикл
				НоваяСтрока = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
				НоваяСтрока.ГоловнойСотрудник = Выборка.ГоловнойСотрудникЗаполнение;
				НоваяСтрока.ЭтоГоловнойСотрудник = НоваяСтрока.Сотрудник = НоваяСтрока.ГоловнойСотрудник;
			КонецЦикла;
			НаборЗаписей.Записать();
			НаборЗаписей.Очистить();
			ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
			
		КонецЦикла;	
			
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьТекстЗапросаТекущихКадровыхДанныхСотрудников(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных) Экспорт 
	КадровыйУчетВнутренний.ДобавитьТекстЗапросаТекущихКадровыхДанныхСотрудников(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных);
КонецПроцедуры	

Процедура ЗаполнитьОрганизацииСотрудников(ПараметрыОбновления = Неопределено) Экспорт
	
	КадровыйУчетВнутренний.ЗаполнитьОрганизацииСотрудников(ПараметрыОбновления);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
