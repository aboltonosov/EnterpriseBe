////////////////////////////////////////////////////////////////////////////////
//  Процедуры и функции, используемые при отражения документов
//  в международном учете
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область РегистрацияДокументовКОтражениюВМеждународномУчете

// Возвращает документы к отражению в международном учете учете
//
// Параметры:
// 	ДокументыКОтражению -   ТаблицаЗачений, 
// 							МенеджерВременныхТаблиц - Таблица документов, которые надо вернуть к отражению, 
// 													  или менеджер временых таблиц имеющий таблицу ДокументыКОтражению
// 													  Таблица должна имееть колонки Документ, Организация, ДатаОтражения. 
//
Процедура ВернутьДокументыКОтражению(ДокументыКОтражению) Экспорт
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьМеждународныйФинансовыйУчет") Тогда
		Возврат;
	КонецЕсли;
	
	КлючеваяОперация = "МеждународныйУчет.ВозвратДокументовКОтражению";
	ОписаниеЗамера = Производительность.НачатьЗамерВремени(КлючеваяОперация);
	КоличествоДокументов = 0;
	
	МенеджерВременныхТаблиц = ИнициализироватьМенеджерДляВозвратаКОтражению(ДокументыКОтражению);
	
	Если МенеджерВременныхТаблиц <> Неопределено Тогда
		Отбор = Новый Структура("Период, Регистратор",'00010101',Неопределено);
		Пока Истина Цикл
			
			ДанныеКОтражению = ДанныеДляВозвратаКОтражениюВУчете(МенеджерВременныхТаблиц, Отбор);
			
			ВыборкаПоДокументам = ДанныеКОтражению.Регистраторы;
			Если ВыборкаПоДокументам.Количество() = 0 Тогда
				Прервать;
			КонецЕсли;
			
			ДанныеРегистра = ДанныеКОтражению.ДанныеРегистра;
			Отбор = Новый Структура("Период, Регистратор",'00010101',Неопределено);
			Пока ВыборкаПоДокументам.Следующий() Цикл
				НаборЗаписей = РегистрыСведений.ОтражениеДокументовВМеждународномУчете.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Регистратор.Установить(ВыборкаПоДокументам.Регистратор);
				ЗаполнитьЗначенияСвойств(Отбор, ВыборкаПоДокументам);
				НайденныеСтроки = ДанныеРегистра.Скопировать(Отбор);
				Если НайденныеСтроки <> Неопределено Тогда
					Для Каждого НовыеДанные Из НайденныеСтроки Цикл
						НоваяЗапись = НаборЗаписей.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяЗапись, НовыеДанные);
					КонецЦикла;
				КонецЕсли;
				НаборЗаписей.Записать();
			КонецЦикла;
			КоличествоДокументов = КоличествоДокументов + ВыборкаПоДокументам.Количество();
		КонецЦикла;
	КонецЕсли;
	
	Производительность.ЗакончитьЗамерВремени(ОписаниеЗамера, КоличествоДокументов);
	
КонецПроцедуры

// Формирует движения по регистру ОтражениеДокументовВМеждународномУчете, выполняет очистку неактуальных записей в Международный.
// Для документов отражаемых в международном учете по данным оборотных регистров и регл. учета.
//
// Параметры:
//  Объект - ДокументОбъект - документ региструруемый к отражению.
//  ДополнительныеСвойства - Структура - дополнительные свойства документа инициализированные при проведении документа.
//  Движения - КоллекцияДвижений - наборы записей регистров записываемые документом.
//                                 На момент вызова метода все наборы должны быть записаны.
//  Отказ - Булево - признак отказа в проведении.
//
Процедура ЗарегистрироватьКОтражению(Объект, ДополнительныеСвойства, Движения, Отказ) Экспорт
	
	Если Отказ ИЛИ НЕ ПолучитьФункциональнуюОпцию("ИспользоватьМеждународныйФинансовыйУчет") Тогда
	 	Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("НеРегистрироватьКОтражениюВМеждународномУчете") 
		 И ДополнительныеСвойства.НеРегистрироватьКОтражениюВМеждународномУчете Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Движения) = Тип("Структура") Тогда
		
		ДатаДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Ссылка, "Дата");
		Объект.Вставить("Дата", ДатаДокумента);
		
		Если НЕ Движения.Свойство("Международный") Тогда
			Международный = РегистрыБухгалтерии.Международный.СоздатьНаборЗаписей();
			Международный.Отбор.Регистратор.Установить(Объект.Ссылка);
			Движения.Вставить("Международный", Международный);
		КонецЕсли;
		
		Если НЕ Движения.Свойство("ОтражениеДокументовВМеждународномУчете") Тогда
			ОтражениеДокументовВМеждународномУчете = РегистрыСведений.ОтражениеДокументовВМеждународномУчете.СоздатьНаборЗаписей();
			ОтражениеДокументовВМеждународномУчете.Отбор.Регистратор.Установить(Объект.Ссылка);
			Движения.Вставить("ОтражениеДокументовВМеждународномУчете", ОтражениеДокументовВМеждународномУчете);
		КонецЕсли;
		
	КонецЕсли;
	
	ДвиженияМеждународный = Движения.Международный;
	ДвиженияМеждународный.ДополнительныеСвойства.Вставить("НеВыполнятьДопОбработкуПроводок", Истина);
	
	ДвиженияОтражениеДокументовВМеждународномУчете = Движения.ОтражениеДокументовВМеждународномУчете;
	
	ТаблицаДанные = Новый ТаблицаЗначений;
	ТаблицаДанные.Колонки.Добавить("Период",        Новый ОписаниеТипов("Дата"));
	ТаблицаДанные.Колонки.Добавить("Организация",   Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаДанные.Колонки.Добавить("ДатаОтражения", Новый ОписаниеТипов("Дата"));
	ТаблицаДанные.Колонки.Добавить("ХозяйственнаяОперация", Новый ОписаниеТипов("ПеречислениеСсылка.ХозяйственныеОперации"));
	
	Если ПолучитьФункциональнуюОпцию("ФормироватьПроводкиМеждународногоУчетаПоДаннымОперативного") Тогда
		ТаблицаОтражениеДокументовВМеждународномУчете = Неопределено;
		ПереданаТаблицаОтражениеДокументовВМеждународномУчете = 
				ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаОтражениеДокументовВМеждународномУчете", ТаблицаОтражениеДокументовВМеждународномУчете)
				ИЛИ ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ОтражениеДокументовВМеждународномУчете", ТаблицаОтражениеДокументовВМеждународномУчете);
		
		Если ПереданаТаблицаОтражениеДокументовВМеждународномУчете  Тогда 
			// В модуле менеджера была подготовлена таблица данных для регистрации.
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаОтражениеДокументовВМеждународномУчете, ТаблицаДанные);
		Иначе
			// Документ регистрируется общим порядком - по движениям оборотных регистров.
			ТаблицаДанные = ТаблицаОтражениеДокументовВМеждународномУчете(Объект);
		КонецЕсли;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ФормироватьПроводкиМеждународногоУчетаПоДаннымРегламентированного") Тогда
		ТаблицаОтражениеДокументовВРеглУчете = Неопределено;
		ПереданаТаблицаТаблицаОтражениеДокументовВРеглУчете = 
				ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаОтражениеДокументовВРеглУчете", ТаблицаОтражениеДокументовВРеглУчете)
				ИЛИ ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ОтражениеДокументовВРеглУчете", ТаблицаОтражениеДокументовВРеглУчете);
		
		Если ПереданаТаблицаТаблицаОтражениеДокументовВРеглУчете  Тогда 
			// В модуле менеджера была подготовлена таблица данных для регистрации.
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу( ТаблицаОтражениеДокументовВРеглУчете, ТаблицаДанные);
		Иначе
			// Документ регистрируется общим порядком:
			// - датой документа
			// - по организации документа.
			НоваяСтрока = ТаблицаДанные.Добавить();
			НоваяСтрока.Период         = Объект.Дата;
			НоваяСтрока.Организация    = Объект.Организация;
			НоваяСтрока.ДатаОтражения  = НачалоДня(Объект.Дата);
		КонецЕсли;
	КонецЕсли;
	
	ВыборочнаяРегистрацияКОтражениюВРеглУчете = 
		ДополнительныеСвойства.Свойство("ВыборочнаяРегистрацияКОтражениюВРеглУчете")
		И ДополнительныеСвойства.ВыборочнаяРегистрацияКОтражениюВРеглУчете;
		
	Если ВыборочнаяРегистрацияКОтражениюВРеглУчете Тогда
		ТаблицаВыборочнойРегистрации = ДополнительныеСвойства.ТаблицаВыборочнойРегистрацииКОтражениюВРеглУчете.Скопировать();
		ТаблицаВыборочнойРегистрации.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
		ТаблицаВыборочнойРегистрации.ЗаполнитьЗначения(Объект.Дата, "Период");
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаВыборочнойРегистрации, ТаблицаДанные);
	КонецЕсли;
		
	ТаблицаДанные.Свернуть("Период, Организация, ДатаОтражения, ХозяйственнаяОперация");
	ДобавитьУчетнуюПолитикуИСтатус(ТаблицаДанные, Объект.Ссылка);
	
	Если НЕ ВыборочнаяРегистрацияКОтражениюВРеглУчете Тогда
		Если ТаблицаДанные.Количество() > 0 Тогда
			ТаблицаДанные.Сортировать("Период, ДатаОтражения");
			ДвиженияОтражениеДокументовВМеждународномУчете.Загрузить(ТаблицаДанные);
			ДвиженияОтражениеДокументовВМеждународномУчете.Записывать = Истина;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	#Область ТекстЗапросаНовыхСтатусовРегистрации
	ТекстЗапросаНовыхСтатусов =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДанных.Период          КАК Период,
	|	ТаблицаДанных.Организация     КАК Организация,
	|	ТаблицаДанных.ДатаОтражения   КАК ДатаОтражения,
	|	ТаблицаДанных.УчетнаяПолитика КАК УчетнаяПолитика
	|ПОМЕСТИТЬ НоваяРегистрация
	|ИЗ
	|	&ТаблицаДанные КАК ТаблицаДанных
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтражениеДокументов.Период          КАК Период,
	|	ОтражениеДокументов.Организация     КАК Организация,
	|	ВЫБОР КОГДА ОтражениеДокументов.ДатаОтражения = ДАТАВРЕМЯ(1,1,1)
	|		ТОГДА НАЧАЛОПЕРИОДА(ОтражениеДокументов.Период, ДЕНЬ)
	|		ИНАЧЕ ОтражениеДокументов.ДатаОтражения
	|	КОНЕЦ КАК ДатаОтражения,
	|	ОтражениеДокументов.Статус          КАК Статус,
	|	ОтражениеДокументов.УчетнаяПолитика КАК УчетнаяПолитика,
	|	ОтражениеДокументов.Комментарий     КАК Комментарий
	|ПОМЕСТИТЬ ТекущаяРегистрация
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК ОтражениеДокументов
	|ГДЕ
	|	ОтражениеДокументов.Регистратор = &Ссылка
	|	И ОтражениеДокументов.УчетнаяПолитика <> ЗНАЧЕНИЕ(Справочник.УчетныеПолитики.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоваяРегистрация.Период КАК Период,
	|	НоваяРегистрация.Организация КАК Организация,
	|	НоваяРегистрация.ДатаОтражения КАК ДатаОтражения,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТекущаяРегистрация.Статус, НЕОПРЕДЕЛЕНО) В (&ОтраженоВУчетеВручную, &КОтражениюВУчетеВручную)
	|			ТОГДА &КОтражениюВУчетеВручную
	|		ИНАЧЕ &КОтражениюВУчете
	|	КОНЕЦ КАК Статус,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТекущаяРегистрация.Статус, НЕОПРЕДЕЛЕНО) В (&ОтраженоВУчетеВручную, &КОтражениюВУчетеВручную)
	|			ТОГДА ТекущаяРегистрация.Комментарий
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК Комментарий,
	|	НоваяРегистрация.УчетнаяПолитика КАК УчетнаяПолитика
	|ПОМЕСТИТЬ НовыеСтатусы
	|ИЗ
	|	НоваяРегистрация КАК НоваяРегистрация
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		ТекущаяРегистрация КАК ТекущаяРегистрация
	|	ПО 
	|		НоваяРегистрация.Организация = ТекущаяРегистрация.Организация
	|		И НоваяРегистрация.ДатаОтражения = ТекущаяРегистрация.ДатаОтражения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.Организация КАК Организация,
	|	Таблица.ДатаОтражения КАК ДатаОтражения
	|ПОМЕСТИТЬ ИзмененияСтатусов
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.ДатаОтражения КАК ДатаОтражения,
	|		Таблица.Организация КАК Организация,
	|		ВЫБОР
	|			КОГДА Таблица.Статус = &КОтражениюВУчете
	|				ТОГДА 1
	|			КОГДА Таблица.Статус = &ОтраженоВУчете
	|				ТОГДА 2
	|		КОНЕЦ КАК Статус
	|	ИЗ
	|		НовыеСтатусы КАК Таблица
	|	ГДЕ
	|		Таблица.Статус В (&КОтражениюВУчете, &ОтраженоВУчете)
	|		
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.ДатаОтражения,
	|		Таблица.Организация,
	|		ВЫБОР
	|			КОГДА Таблица.Статус = &КОтражениюВУчете
	|				ТОГДА -1
	|			КОГДА Таблица.Статус = &ОтраженоВУчете
	|				ТОГДА -2
	|		КОНЕЦ
	|	ИЗ
	|		ТекущаяРегистрация КАК Таблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НоваяРегистрация КАК НоваяРегистрация
	|	ПО 
	|		Таблица.Организация = НоваяРегистрация.Организация
	|		И Таблица.ДатаОтражения = НоваяРегистрация.ДатаОтражения
	|
	|	ГДЕ
	|		Таблица.Статус В (&КОтражениюВУчете, &ОтраженоВУчете)) КАК Таблица
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.ДатаОтражения,
	|	Таблица.Организация
	|
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.Статус) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НовыеСтатусы.Период          КАК Период,
	|	НовыеСтатусы.Организация     КАК Организация,
	|	НовыеСтатусы.ДатаОтражения   КАК ДатаОтражения,
	|	НовыеСтатусы.Статус          КАК Статус,
	|	НовыеСтатусы.УчетнаяПолитика КАК УчетнаяПолитика,
	|	НовыеСтатусы.Комментарий     КАК Комментарий
	|ИЗ
	|	НовыеСтатусы КАК НовыеСтатусы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТекущаяРегистрация.Период          КАК Период,
	|	ТекущаяРегистрация.Организация     КАК Организация,
	|	ТекущаяРегистрация.ДатаОтражения   КАК ДатаОтражения,
	|	ТекущаяРегистрация.Статус          КАК Статус,
	|	ТекущаяРегистрация.УчетнаяПолитика КАК УчетнаяПолитика,
	|	ТекущаяРегистрация.Комментарий     КАК Комментарий
	|ИЗ
	|	ТекущаяРегистрация КАК ТекущаяРегистрация
	|	ЛЕВОЕ СОЕДИНЕНИЕ НовыеСтатусы КАК НовыеСтатусы
	|	ПО ТекущаяРегистрация.Период = НовыеСтатусы.Период
	|		И ТекущаяРегистрация.Организация = НовыеСтатусы.Организация
	|		И ТекущаяРегистрация.ДатаОтражения = НовыеСтатусы.ДатаОтражения
	|		И ТекущаяРегистрация.УчетнаяПолитика = НовыеСтатусы.УчетнаяПолитика
	|ГДЕ
	|	НовыеСтатусы.Организация ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период,
	|	ДатаОтражения
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Международный.Период                                       КАК Период,
	|	Международный.Организация                                  КАК Организация,
	|	
	|	Международный.ПодразделениеДт                              КАК ПодразделениеДт,
	|	Международный.СчетДт                                       КАК СчетДт,
	|	ЕСТЬNULL(Международный.ВидСубконтоДт1, &ПустойВидСубконто) КАК ВидСубконтоДт1,
	|	ЕСТЬNULL(Международный.СубконтоДт1, НЕОПРЕДЕЛЕНО)          КАК СубконтоДт1,
	|	ЕСТЬNULL(Международный.ВидСубконтоДт2, &ПустойВидСубконто) КАК ВидСубконтоДт2,
	|	ЕСТЬNULL(Международный.СубконтоДт2, НЕОПРЕДЕЛЕНО)          КАК СубконтоДт2,
	|	ЕСТЬNULL(Международный.ВидСубконтоДт3, &ПустойВидСубконто) КАК ВидСубконтоДт3,
	|	ЕСТЬNULL(Международный.СубконтоДт3, НЕОПРЕДЕЛЕНО)          КАК СубконтоДт3,
	|	Международный.ВалютаДт                                     КАК ВалютаДт,
	|	Международный.ВалютнаяСуммаДт                              КАК ВалютнаяСуммаДт,
	|	
	|	Международный.ПодразделениеКт                              КАК ПодразделениеКт,
	|	Международный.СчетКт                                       КАК СчетКт,
	|	ЕСТЬNULL(Международный.ВидСубконтоКт1, &ПустойВидСубконто) КАК ВидСубконтоКт1,
	|	ЕСТЬNULL(Международный.СубконтоКт1, НЕОПРЕДЕЛЕНО)          КАК СубконтоКт1,
	|	ЕСТЬNULL(Международный.ВидСубконтоКт2, &ПустойВидСубконто) КАК ВидСубконтоКт2,
	|	ЕСТЬNULL(Международный.СубконтоКт2, НЕОПРЕДЕЛЕНО)          КАК СубконтоКт2,
	|	ЕСТЬNULL(Международный.ВидСубконтоКт3, &ПустойВидСубконто) КАК ВидСубконтоКт3,
	|	ЕСТЬNULL(Международный.СубконтоКт3, НЕОПРЕДЕЛЕНО)          КАК СубконтоКт3,
	|	Международный.ВалютаКт                                     КАК ВалютаКт,
	|	Международный.ВалютнаяСуммаКт                              КАК ВалютнаяСуммаКт,
	|	
	|	Международный.Сумма                                        КАК Сумма,
	|	Международный.СуммаПредставления                           КАК СуммаПредставления,
	|	
	|	Международный.Содержание                                   КАК Содержание,
	|	Международный.ШаблонПроводки                               КАК ШаблонПроводки,
	|	Международный.ТипПроводки                                  КАК ТипПроводки
	|ИЗ
	|	РегистрБухгалтерии.Международный.ДвиженияССубконто(,,Регистратор = &Ссылка,,) КАК Международный
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ИзмененияСтатусов КАК ИзмененияСтатусов
	|	ПО
	|		Международный.Организация = ИзмененияСтатусов.Организация
	|		И НАЧАЛОПЕРИОДА(Международный.Период, ДЕНЬ) = ИзмененияСтатусов.ДатаОтражения
	|ГДЕ
	|	ИзмененияСтатусов.Организация ЕСТЬ NULL";
	#КонецОбласти
	
	Запрос = Новый Запрос(ТекстЗапросаНовыхСтатусов);
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТаблицаДанные",     ТаблицаДанные);
	Запрос.УстановитьПараметр("Ссылка",            Объект.Ссылка);
	Запрос.УстановитьПараметр("ПустойВидСубконто", ПланыВидовХарактеристик.ВидыСубконтоМеждународные.ПустаяСсылка());
	
	Запрос.УстановитьПараметр("ОтраженоВУчете",      Перечисления.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчете);
	Запрос.УстановитьПараметр("КОтражениюВУчете",    Перечисления.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчете);
	Запрос.УстановитьПараметр("ОтраженоВУчетеВручную",   Перечисления.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчетеВручную);
	Запрос.УстановитьПараметр("КОтражениюВУчетеВручную", Перечисления.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчетеВручную);
	
	Результат = Запрос.ВыполнитьПакет();
	Количество = Результат.Количество();
	
	Международный = Результат[Количество - 1].Выгрузить();
	ДвиженияМеждународный.Загрузить(Международный);
	ДвиженияМеждународный.Записывать = Истина;
	
	ТаблицаДанные = Результат[Количество - 2].Выгрузить();
	ДвиженияОтражениеДокументовВМеждународномУчете.Загрузить(ТаблицаДанные);
	ДвиженияОтражениеДокументовВМеждународномУчете.Записывать = Истина;
	
	Если ТипЗнч(Движения) = Тип("Структура") Тогда
		Если ДвиженияМеждународный.Количество() > 0 Тогда
			ДвиженияМеждународный.Записать();
		КонецЕсли;
		Если ДвиженияОтражениеДокументовВМеждународномУчете.Количество() > 0 Тогда
			ДвиженияОтражениеДокументовВМеждународномУчете.Записать();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Регистрирует документы расчетов с партнерами к отражению в международном учете.
//
// Параметры:
//	МассивДокументов - Массив - массив документов к регистрации в международном учете.
//
Процедура ЗарегистрироватьДокументыРасчетовСПартнерамиКОтражениюВМеждународномУчете(ТаблицаРасчетов) Экспорт

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьМеждународныйФинансовыйУчет") Тогда
		Возврат;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаРасчетов.Период КАК Период,
	|	ТаблицаРасчетов.Регистратор КАК Регистратор,
	|	ТаблицаРасчетов.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|ПОМЕСТИТЬ ТаблицаДокументов
	|ИЗ
	|	&ТаблицаРасчетов КАК ТаблицаРасчетов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТаблицаРасчетов.АналитикаУчетаПоПартнерам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументов.Период КАК Период,
	|	ТаблицаДокументов.ДатаОтражения КАК ДатаОтражения,
	|	ТаблицаДокументов.Регистратор КАК Регистратор,
	|	ТаблицаДокументов.Организация КАК Организация
	|ПОМЕСТИТЬ ДокументыКОтражению
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ТаблицаДокументов.Период КАК Период,
	|		ТаблицаДокументов.Период КАК ДатаОтражения,
	|		ТаблицаДокументов.Регистратор КАК Регистратор,
	|		Аналитика.Организация КАК Организация
	|	ИЗ
	|		ТаблицаДокументов КАК ТаблицаДокументов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|			ПО ТаблицаДокументов.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ТаблицаДокументов.Период,
	|		ТаблицаДокументов.Период,
	|		ТаблицаДокументов.Регистратор,
	|		Аналитика.Контрагент
	|	ИЗ
	|		ТаблицаДокументов КАК ТаблицаДокументов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|			ПО ТаблицаДокументов.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|	ГДЕ
	|		Аналитика.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.НашеПредприятие)
	|		И ТИПЗНАЧЕНИЯ(ТаблицаДокументов.Регистратор) В (
	|			ТИП(Документ.ВозвратТоваровМеждуОрганизациями),
	|			ТИП(Документ.ОтчетПоКомиссииМеждуОрганизациями),
	|			ТИП(Документ.ОтчетПоКомиссииМеждуОрганизациямиОСписании),
	|			ТИП(Документ.ПередачаТоваровМеждуОрганизациями))
	|
	|	) КАК ТаблицаДокументов
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокументов.Период,
	|	ТаблицаДокументов.ДатаОтражения,
	|	ТаблицаДокументов.Регистратор,
	|	ТаблицаДокументов.Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Регистратор,
	|	Организация,
	|	ДатаОтражения";
	
	Запрос.УстановитьПараметр("ТаблицаРасчетов", ТаблицаРасчетов);
	Запрос.ВыполнитьПакет();
	
	ВернутьДокументыКОтражению(Запрос.МенеджерВременныхТаблиц);

КонецПроцедуры

#КонецОбласти

// Обработчик вызываемый регламентным заданием ОтражениеДокументовВМеждународномУчете.
//
Процедура ОтразитьВМеждународномУчетеРегламентнымЗаданием() Экспорт

	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания();
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьМеждународныйФинансовыйУчет") Тогда
		Возврат;
	КонецЕсли;
	
	ОтразитьВМеждународномУчете(,,,,Ложь);

КонецПроцедуры

// Выполняет отражение документов в международном учете.
//
// Параметры:
//	Организация				- СправочникСсылка.Организации - отбор документов по организации.
//	ДатаОкончания			- Дата - отбор документов до даты.
//	Документы				- ДокументСсылка, Массив - документ или массив документов к отражению.
//	АвтоматическоеОтражение - Булево - признак необходимости записи сформированных проводок.
//	ВыполнитьПересчеты - Булево - признак необходимости выполнения отложенного распределения взаиморасчетов с партнерами.
//
// Возвращаемое значение:
//	ТаблицаПроводок - ТаблицаЗначений - Таблица сформированных проводок, 
//										соответствует структуре регистра бухгалтерии Международный.
//
Функция ОтразитьВМеждународномУчете(Организация = Неопределено, ДатаОкончания = Неопределено, Документы = Неопределено, АвтоматическоеОтражение = Истина, ВыполнитьПересчеты = Истина) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Если ВыполнитьПересчеты Тогда
		ВыполнитьОффлайновыеРасчеты(Организация, ДатаОкончания, Документы);
	КонецЕсли;
	
	ОписаниеЗамераОтражениеВМеждународномУчете = Производительность.НачатьЗамерВремени("МеждународныйУчет.ОтражениеВМеждународномУчете");
	
	ВалютыУчета = Новый Структура;
	ВалютыМеждународногоУчета = МеждународнаяОтчетностьВызовСервера.УчетнаяВалюта();
	ВалютыУчета.Вставить("ФункциональнаяВалюта", ВалютыМеждународногоУчета.Функциональная);
	ВалютыУчета.Вставить("ВалютаПредставления", ВалютыМеждународногоУчета.Представления);
	ВалютыУчета.Вставить("ВалютаУпр", Константы.ВалютаУправленческогоУчета.Получить());
	ВалютыУчета.Вставить("ВалютаРегл", Константы.ВалютаРегламентированногоУчета.Получить());

	ПараметрыОтражения = Новый Структура;
	ПараметрыОтражения.Вставить("ВалютыУчета", ВалютыУчета);
	ПараметрыОтражения.Вставить("ТаблицаУточненияСчетов", ТаблицаУточненияСчетов());
	ПараметрыОтражения.Вставить("НастройкиОтражения", НастройкиОтраженияВМеждународномУчете());
	ПараметрыОтражения.Вставить("СтруктураПроводки", СтруктураПроводки());
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ПараметрыОтражения.Вставить("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц);
	
	ИнициализироватьВременныеТаблицы(МенеджерВременныхТаблиц, Организация, ДатаОкончания, Документы);
	
	ТаблицаПроводок = ТаблицаПроводок();
	
	ПараметрыОтражения.Вставить("ТекстыЗапросовПоРегистрам", ТекстыЗапросовПоРегистрам());
	ПараметрыОтражения.Вставить("ИменаДокументовКОтражению", ДокументыКОтражениюВМеждународномУчете());
	
	КоличествоДокументов = 0;
	МоментВремени = Новый МоментВремени('00010101');
	Пока МоментВремени <> Неопределено Цикл
		
		ДокументыКОтражениюВУчете = ДокументыКОтражениюВУчете(МенеджерВременныхТаблиц, Организация, ДатаОкончания, Документы, МоментВремени);
		Если МоментВремени = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		КоличествоДокументов = КоличествоДокументов + ДокументыКОтражениюВУчете.Количество();
		
		НачатьТранзакцию();
		
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ОтражениеДокументовВМеждународномУчете.НаборЗаписей");
		УстановитьДанныеБлокировки(ЭлементБлокировки, ПараметрыОтражения.МенеджерВременныхТаблиц);
		БлокировкаДанных.Заблокировать();
		
		ОтразитьПоДаннымОперативногоУчета(ПараметрыОтражения, ТаблицаПроводок);
		ОтразитьПоДаннымРеглУчета(ПараметрыОтражения, ТаблицаПроводок);
		
		ПересчитатьТаблицуПроводокПоКурсу(ПараметрыОтражения, ТаблицаПроводок);
		
		КоличествоПроводок = ТаблицаПроводок.Количество();
		Если АвтоматическоеОтражение Тогда
			ЗаписатьПроводки(ТаблицаПроводок, ДокументыКОтражениюВУчете);
			ТаблицаПроводок.Очистить();
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
		УничтожитьВременныеТаблицы(МенеджерВременныхТаблиц);
		
	КонецЦикла;
	
	Производительность.ЗакончитьЗамерВремени(ОписаниеЗамераОтражениеВМеждународномУчете, КоличествоДокументов);
	
	Возврат ТаблицаПроводок;
	
КонецФункции

// По передаваемой в качестве параметра таблице проводок, возвращает таблицу проводок, удовлетворяющих условиям записи.
//
// Параметры:
//	ПроводкиДокумента - ТаблицаЗначений - таблица проводок.
//
// Возвращаемое значение:
//	ТаблицаЗначений - таблица записываемых проводок.
//
Функция ЗаписываемыеПроводки(ПроводкиДокумента) Экспорт

	ПараметрыОтбора = Новый Структура("Статус", Перечисления.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчете);
	ЗаписываемыеПроводки = ПроводкиДокумента.Скопировать(ПараметрыОтбора);
	ИсключаемыеПроводки =  ИсключаемыеПроводки(ЗаписываемыеПроводки);
	Для каждого Проводка Из ИсключаемыеПроводки Цикл
		ЗаписываемыеПроводки.Удалить(Проводка);
	КонецЦикла;
	
	Возврат ЗаписываемыеПроводки;

КонецФункции

// Обновляет статус отражения документа в международном учете на основе статусов сформированных проводок.
//
// Параметры:
//	ПроводкиДокумента - ТаблицаЗначений - таблица проводок документа.
//	Документ - ДокументСсылка - ссылка на документ.
//	ОбновлятьЗаписиОтраженияДляОтстутствующихПроводок - Булево - если среди таблицы проводок нет записей по отраженной организации, по умолчанию будет вставать "Отражено".
//
Процедура ОбновитьСтатусОтраженияДокумента(ПроводкиДокумента, Документ, ОбновлятьЗаписиОтраженияДляОтстутствующихПроводок = Истина) Экспорт

	ЗаписиОтраженияДокумента = РегистрыСведений.ОтражениеДокументовВМеждународномУчете.СоздатьНаборЗаписей();
	ЗаписиОтраженияДокумента.Отбор.Регистратор.Установить(Документ);
	ЗаписиОтраженияДокумента.Прочитать();
	
	Для каждого Запись Из ЗаписиОтраженияДокумента Цикл
		ПараметрыОтбора = Новый Структура("Организация", Запись.Организация);
		ПроводкиОрганизации = ПроводкиДокумента.НайтиСтроки(ПараметрыОтбора);
		Если ПроводкиОрганизации.Количество() > 0 Тогда
			Статус = Неопределено;
			Комментарий = "";
			УникальныеЗначения = Новый Соответствие;
			Для каждого Проводка Из ПроводкиОрганизации Цикл
				Статус = МеждународныйУчетСерверПовтИсп.Статус(Статус, Проводка.Статус);
				ЗаполнитьКомментарий(Комментарий, Проводка, УникальныеЗначения);
			КонецЦикла;
			Запись.Статус = Статус;
			Запись.Комментарий = Комментарий;
		ИначеЕсли ОбновлятьЗаписиОтраженияДляОтстутствующихПроводок Тогда
			Запись.Статус = Перечисления.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчете;
			Запись.Комментарий = "";
		КонецЕсли;
	КонецЦикла;
	
	ЗаписиОтраженияДокумента.Записать();

КонецПроцедуры

// Возвращает массив регистров, для отражения в международном учете.
//
// Возвращаемое значение:
//	Массив - массив регистров для отражения в международном учете.
//
Функция РегистрыКОтражениюВМеждународномУчете() Экспорт

	МассивРегистров = Новый Массив;
	
	Если ПолучитьФункциональнуюОпцию("ФормироватьПроводкиМеждународногоУчетаПоДаннымОперативного") Тогда
		МассивРегистров.Добавить("ДвиженияДенежныхСредств");
		МассивРегистров.Добавить("ДвиженияДенежныеСредстваДоходыРасходы");
		МассивРегистров.Добавить("ДвиженияДенежныеСредстваКонтрагент");
		МассивРегистров.Добавить("НДСЗаписиКнигиПродаж");
		МассивРегистров.Добавить("НДСЗаписиКнигиПокупок");
		МассивРегистров.Добавить("ДвиженияКонтрагентДоходыРасходы");
		МассивРегистров.Добавить("ДвиженияКонтрагентКонтрагент");
		МассивРегистров.Добавить("ДвиженияДоходыРасходыПрочиеАктивыПассивы");
		МассивРегистров.Добавить("Закупки");
		МассивРегистров.Добавить("ВыручкаИСебестоимостьПродаж");
		МассивРегистров.Добавить("ДвиженияНоменклатураДоходыРасходы");
		МассивРегистров.Добавить("ДвиженияНоменклатураНоменклатура");
	КонецЕсли;
	
	Возврат МассивРегистров;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОтражениеВМеждународномУчетеПоДаннымОперативного

Функция ТаблицаУточненияСчетов()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПравилаОтражения.ШаблонПроводки КАК ШаблонПроводки,
	|	ПравилаУточненияСчетов.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ПравилаУточненияСчетов.ВидДвижения КАК ВидДвижения,
	|	ПравилаУточненияСчетов.СчетУчета КАК СчетУчета
	|ИЗ
	|	РегистрСведений.ПравилаОтраженияВМеждународномУчете КАК ПравилаОтражения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПравилаУточненияСчетовВМеждународномУчете КАК ПравилаУточненияСчетов
	|		ПО ПравилаОтражения.ШаблонПроводки = ПравилаУточненияСчетов.ШаблонПроводки
	|";
	
	ТаблицаУточненияСчетов = Запрос.Выполнить().Выгрузить();
	ТаблицаУточненияСчетов.Индексы.Добавить("ШаблонПроводки, ГруппаФинансовогоУчета, ВидДвижения");

	Возврат ТаблицаУточненияСчетов;

КонецФункции

Процедура ИнициализироватьВременныеТаблицы(МенеджерВременныхТаблиц, Организация, ДатаОкончания, Документы)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УчетнаяПолитикаОрганизаций.Период КАК Период,
	|	УчетнаяПолитикаОрганизаций.Организация КАК Организация,
	|	УчетнаяПолитикаОрганизаций.УчетнаяПолитика КАК УчетнаяПолитика
	|ПОМЕСТИТЬ УчетнаяПолитикаОрганизаций
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаОрганизацийДляМеждународногоУчета КАК УчетнаяПолитикаОрганизаций
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	УчетнаяПолитикаОрганизаций.Организация КАК Организация
	|ПОМЕСТИТЬ Организации
	|ИЗ
	|	УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|ГДЕ
	|	ВЫБОР
	|		КОГДА &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ УчетнаяПолитикаОрганизаций.Организация <> &Организация
	|	КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.Период,
	|	ДанныеРегистра.НомерСтроки,
	|	ДанныеРегистра.Активность,
	|	ДанныеРегистра.СчетДт,
	|	ДанныеРегистра.СчетКт,
	|	ДанныеРегистра.ПодразделениеДт,
	|	ДанныеРегистра.ПодразделениеКт,
	|	ДанныеРегистра.ВалютаДт,
	|	ДанныеРегистра.ВалютаКт,
	|	ДанныеРегистра.Сумма,
	|	ДанныеРегистра.СуммаПредставления,
	|	ДанныеРегистра.ВалютнаяСуммаДт,
	|	ДанныеРегистра.ВалютнаяСуммаКт,
	|	ДанныеРегистра.Содержание,
	|	ДанныеРегистра.ШаблонПроводки,
	|	ДанныеРегистра.ТипПроводки,
	|	ДанныеРегистра.СубконтоДт1,
	|	ДанныеРегистра.ВидСубконтоДт1,
	|	ДанныеРегистра.СубконтоДт2,
	|	ДанныеРегистра.ВидСубконтоДт2,
	|	ДанныеРегистра.СубконтоДт3,
	|	ДанныеРегистра.ВидСубконтоДт3,
	|	ДанныеРегистра.СубконтоКт1,
	|	ДанныеРегистра.ВидСубконтоКт1,
	|	ДанныеРегистра.СубконтоКт2,
	|	ДанныеРегистра.ВидСубконтоКт2,
	|	ДанныеРегистра.СубконтоКт3,
	|	ДанныеРегистра.ВидСубконтоКт3
	|ПОМЕСТИТЬ ПроводкиДругихОрганизаций
	|ИЗ
	|	РегистрБухгалтерии.Международный.ДвиженияССубконто(
	|			,
	|			&ДатаОкончания,
	|			Организация В (ВЫБРАТЬ Организация ИЗ Организации)
	|				И (Регистратор В (&Документы) ИЛИ &Документы = НЕОПРЕДЕЛЕНО),
	|			,
	|			) КАК ДанныеРегистра
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|";
	
	Запрос.УстановитьПараметр("Организация", ?(Организация = Неопределено, Справочники.Организации.ПустаяСсылка(),Организация));
	Запрос.УстановитьПараметр("ДатаОкончания", ?(НЕ ЗначениеЗаполнено(ДатаОкончания) ИЛИ ЗначениеЗаполнено(Документы), '39991231', ДатаОкончания));
	Запрос.УстановитьПараметр("Документы", Документы);
	Запрос.Выполнить();

КонецПроцедуры

Функция ДокументыКОтражениюВУчете(МенеджерВременныхТаблиц, Организация, ДатаОкончания, Документы, МоментВремени)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	ТаблицаДокументов.Период КАК Период,
	|	ТаблицаДокументов.Регистратор КАК Регистратор,
	|	ТаблицаДокументов.Организация КАК Организация,
	|	ТаблицаДокументов.МоментВремени КАК МоментВремени,
	|	МАКСИМУМ(УчетнаяПолитикаОрганизаций.Период) КАК ПериодУчетнойПолитики
	|ПОМЕСТИТЬ ТаблицаДокументов
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК ТаблицаДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|		ПО ТаблицаДокументов.Организация = УчетнаяПолитикаОрганизаций.Организация
	|			И ТаблицаДокументов.Период >= УчетнаяПолитикаОрганизаций.Период
	|ГДЕ
	|	(&НеПроверятьСтатусОтражения
	|			ИЛИ ТаблицаДокументов.Статус В (
	|					ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчете),
	|					ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.ОтсутствуютПравилаОтраженияВУчете)))
	|	И (ТаблицаДокументов.Организация = &Организация ИЛИ &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|	И (ТаблицаДокументов.Регистратор В (&Документы) ИЛИ &Документы = НЕОПРЕДЕЛЕНО)
	|	И ТаблицаДокументов.Период <= &ДатаОкончания
	|	И ТаблицаДокументов.МоментВремени > &МоментВремени
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокументов.Период,
	|	ТаблицаДокументов.Регистратор,
	|	ТаблицаДокументов.Организация,
	|	ТаблицаДокументов.МоментВремени
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаДокументов.МоментВремени
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ПериодУчетнойПолитики
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ТаблицаДокументов.Период) КАК Период,
	|	ТаблицаДокументов.Регистратор КАК Регистратор,
	|	ТаблицаДокументов.Организация КАК Организация,
	|	ЕСТЬNULL(УчетнаяПолитикаОрганизаций.УчетнаяПолитика, НЕОПРЕДЕЛЕНО) КАК УчетнаяПолитика
	|ПОМЕСТИТЬ ДокументыКОтражению
	|ИЗ
	|	ТаблицаДокументов КАК ТаблицаДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|		ПО ТаблицаДокументов.Организация = УчетнаяПолитикаОрганизаций.Организация
	|			И ТаблицаДокументов.ПериодУчетнойПолитики = УчетнаяПолитикаОрганизаций.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокументов.Регистратор,
	|	ТаблицаДокументов.Организация,
	|	ЕСТЬNULL(УчетнаяПолитикаОрганизаций.УчетнаяПолитика, НЕОПРЕДЕЛЕНО)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Регистратор,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументов.Регистратор КАК Регистратор,
	|	ТаблицаДокументов.МоментВремени КАК МоментВремени
	|ИЗ
	|	ТаблицаДокументов КАК ТаблицаДокументов
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаДокументов.МоментВремени
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокументов.Регистратор КАК Документ,
	|	ТаблицаДокументов.МоментВремени КАК МоментВремени,
	|	ДанныеРегистра.Период,
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ДанныеРегистра.НомерСтроки,
	|	ДанныеРегистра.Активность,
	|	ДанныеРегистра.СчетДт,
	|	ДанныеРегистра.СчетКт,
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.ПодразделениеДт,
	|	ДанныеРегистра.ПодразделениеКт,
	|	ДанныеРегистра.ВалютаДт,
	|	ДанныеРегистра.ВалютаКт,
	|	ДанныеРегистра.Сумма,
	|	ДанныеРегистра.СуммаПредставления,
	|	ДанныеРегистра.ВалютнаяСуммаДт,
	|	ДанныеРегистра.ВалютнаяСуммаКт,
	|	ДанныеРегистра.Содержание,
	|	ДанныеРегистра.ШаблонПроводки,
	|	ДанныеРегистра.ТипПроводки,
	|	ДанныеРегистра.СубконтоДт1,
	|	ДанныеРегистра.ВидСубконтоДт1,
	|	ДанныеРегистра.СубконтоДт2,
	|	ДанныеРегистра.ВидСубконтоДт2,
	|	ДанныеРегистра.СубконтоДт3,
	|	ДанныеРегистра.ВидСубконтоДт3,
	|	ДанныеРегистра.СубконтоКт1,
	|	ДанныеРегистра.ВидСубконтоКт1,
	|	ДанныеРегистра.СубконтоКт2,
	|	ДанныеРегистра.ВидСубконтоКт2,
	|	ДанныеРегистра.СубконтоКт3,
	|	ДанныеРегистра.ВидСубконтоКт3
	|ИЗ
	|	ТаблицаДокументов КАК ТаблицаДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПроводкиДругихОрганизаций КАК ДанныеРегистра
	|		ПО ТаблицаДокументов.Регистратор = ДанныеРегистра.Регистратор
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаДокументов.МоментВремени
	|ИТОГИ ПО
	|	Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаДокументов";
	
	Запрос.УстановитьПараметр("Организация", ?(Организация = Неопределено, Справочники.Организации.ПустаяСсылка(),Организация));
	Запрос.УстановитьПараметр("ДатаОкончания", ?(НЕ ЗначениеЗаполнено(ДатаОкончания) ИЛИ ЗначениеЗаполнено(Документы), '39991231', ДатаОкончания));
	Запрос.УстановитьПараметр("Документы", Документы);
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени);
	Запрос.УстановитьПараметр("НеПроверятьСтатусОтражения", Документы <> Неопределено);
	
	Результат = Запрос.ВыполнитьПакет();
	Если Не Результат[2].Пустой() Тогда
		ДокументыКОтражениюВУчете = Результат[3].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		ТаблицаДокументовКОтражениюВУчете = Результат[2].Выгрузить();
		МоментВремени = ТаблицаДокументовКОтражениюВУчете[ТаблицаДокументовКОтражениюВУчете.Количество() - 1].МоментВремени;
	Иначе
		ДокументыКОтражениюВУчете = Неопределено;
		МоментВремени = Неопределено;
	КонецЕсли;
	
	Возврат ДокументыКОтражениюВУчете;
	
КонецФункции

Функция НастройкиОтраженияВМеждународномУчете()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ШаблоныПроводок.Ссылка КАК ШаблонПроводки,
	|	ШаблоныПроводок.ЭтоГруппаШаблонов КАК ЭтоГруппаШаблонов,
	|	ШаблоныПроводок.Операция КАК Операция,
	|	ШаблоныПроводок.Операция.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ШаблоныПроводок.ВариантСовместногоПрименения КАК ВариантСовместногоПрименения,
	|	ШаблоныПроводок.СчетДебетаПоУмолчанию КАК СчетДебетаПоУмолчанию,
	|	ШаблоныПроводок.СчетКредитаПоУмолчанию КАК СчетКредитаПоУмолчанию,
	|	ШаблоныПроводок.ТипИсточникаУточненияСчетаДт КАК ИсточникУточненияСчетаДт,
	|	ШаблоныПроводок.ТипИсточникаУточненияСчетаКт КАК ИсточникУточненияСчетаКт,
	|	ШаблоныПроводок.ИсточникПодразделенияДт КАК ИсточникПодразделенияДт,
	|	ШаблоныПроводок.ИсточникПодразделенияКт КАК ИсточникПодразделенияКт,
	|	ШаблоныПроводок.ИсточникНаправленияДт КАК ИсточникНаправленияДт,
	|	ШаблоныПроводок.ИсточникНаправленияКт КАК ИсточникНаправленияКт,
	|	ШаблоныПроводок.ИсточникСуммыВВалютеДт КАК ИсточникСуммыВВалютеДт,
	|	ШаблоныПроводок.ИсточникСуммыВВалютеКт КАК ИсточникСуммыВВалютеКт,
	|	ШаблоныПроводок.ИсточникБалансовойСуммы КАК ИсточникБалансовойСуммы,
	|	ШаблоныПроводок.ИзменятьЗнакОперацииПриОтраженииВУчете КАК ИзменятьЗнакОперации,
	|	ШаблоныПроводок.РучноеУточнениеПроводки КАК РучноеУточнениеПроводки,
	|	ШаблоныПроводок.ДополнительныйОтбор КАК ДополнительныйОтбор,
	|	ШаблоныПроводок.УстановленДополнительныйОтбор КАК УстановленДополнительныйОтбор,
	|	ШаблоныПроводок.Игнорировать КАК Игнорировать,
	|	ШаблоныПроводок.ЗаполнениеСубконтоПоСоответствию КАК ЗаполнениеСубконтоПоСоответствию,
	|	ПравилаОтражения.УчетнаяПолитика КАК УчетнаяПолитика,
	|	НастройкиОпераций.ИсточникДанных КАК ИсточникДанных,
	|	НастройкиОпераций.Приход = ЗНАЧЕНИЕ(Перечисление.ТипыДанныхУчета.Номенклатура) КАК ДвижениеНоменклатурыДт,
	|	НастройкиОпераций.Расход = ЗНАЧЕНИЕ(Перечисление.ТипыДанныхУчета.Номенклатура) КАК ДвижениеНоменклатурыКт,
	|	ШаблоныПроводок.Родитель КАК РодительскийШаблонПроводки,
	|	ШаблоныПроводок.Родитель.Родитель КАК РодительскийШаблонПроводки2,
	|	ШаблоныПроводок.Родитель.Родитель.ВариантСовместногоПрименения КАК ВариантСовместногоПримененияУровня2
	|
	|ИЗ 
	|	Справочник.ШаблоныПроводокДляМеждународногоУчета КАК ШаблоныПроводок
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ПравилаОтраженияВМеждународномУчете КАК ПравилаОтражения
	|	ПО
	|		ШаблоныПроводок.Ссылка = ПравилаОтражения.ШаблонПроводки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.НастройкиХозяйственныхОпераций КАК НастройкиОпераций
	|	ПО
	|		ШаблоныПроводок.Операция = НастройкиОпераций.Ссылка
	|ГДЕ
	|	НЕ ШаблоныПроводок.ПометкаУдаления
	|	И (НЕ ПравилаОтражения.УчетнаяПолитика ЕСТЬ NULL ИЛИ ШаблоныПроводок.ЭтоГруппаШаблонов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ШаблоныПроводок.РеквизитДопУпорядочивания ИЕРАРХИЯ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкиЗаполнения.Ссылка КАК ШаблонПроводки,
	|	НастройкиЗаполнения.ПоложениеСубконто КАК ПоложениеСубконто,
	|	НастройкиЗаполнения.ВидСубконто КАК ВидСубконто,
	|	НастройкиЗаполнения.ЗаполнятьИзИсточника КАК ЗаполнятьИзИсточника,
	|	НастройкиЗаполнения.УказанноеЗначение КАК УказанноеЗначение,
	|	НастройкиЗаполнения.Выражение КАК Выражение,
	|	НастройкиОпераций.ИсточникДанных КАК ИсточникДанных
	|ИЗ
	|	Справочник.ШаблоныПроводокДляМеждународногоУчета.НастройкиЗаполненияСубконто КАК НастройкиЗаполнения
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НастройкиХозяйственныхОпераций КАК НастройкиОпераций
	|	ПО НастройкиЗаполнения.Ссылка.Операция = НастройкиОпераций.Ссылка
	|ГДЕ
	|	НЕ НастройкиЗаполнения.Ссылка.ПометкаУдаления
	|";
	
	Результаты = Запрос.ВыполнитьПакет();
	
	Настройки = Новый Структура;
	Настройки.Вставить("ШаблоныПроводок", Результаты[0].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией));
	Настройки.Вставить("СоответствиеСубконто", Результаты[1].Выгрузить());
	
	Возврат Настройки;
	
КонецФункции

Процедура УничтожитьВременныеТаблицы(МенеджерВременныхТаблиц)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"УНИЧТОЖИТЬ ДокументыКОтражению
	|";
	
	Запрос.Выполнить();

КонецПроцедуры

Процедура УстановитьДанныеБлокировки(ЭлементБлокировки, МенеджерВременныхТаблиц)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументыКОтражению.Регистратор КАК Регистратор
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|";
	Результат = Запрос.Выполнить();
	
	ЭлементБлокировки.ИсточникДанных  = Результат;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Регистратор", "Регистратор");

КонецПроцедуры

Процедура ОтразитьПоДаннымОперативногоУчета(ПараметрыОтражения, ТаблицаПроводок)
	
	Если Не ПолучитьФункциональнуюОпцию("ФормироватьПроводкиМеждународногоУчетаПоДаннымОперативного") Тогда
		Возврат;
	КонецЕсли;
	
	ТекстыЗапросовПоРегистрам = ПараметрыОтражения.ТекстыЗапросовПоРегистрам;
	Для каждого ЭлементКоллекции Из ТекстыЗапросовПоРегистрам Цикл
		ИмяРегистра = ЭлементКоллекции.Ключ;
		ОтразитьДанныеРегистра(ИмяРегистра, ТекстыЗапросовПоРегистрам[ИмяРегистра], ПараметрыОтражения, ТаблицаПроводок);
	КонецЦикла;
	
	Для каждого ИмяДокумента Из ПараметрыОтражения.ИменаДокументовКОтражению Цикл
		ОтразитьДанныеДокумента(ИмяДокумента, ПараметрыОтражения, ТаблицаПроводок);
	КонецЦикла;

КонецПроцедуры

Процедура ОтразитьДанныеДокумента(ИмяДокумента, ПараметрыОтражения, ТаблицаПроводок)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ПараметрыОтражения.МенеджерВременныхТаблиц;
	Запрос.Текст = Документы[ИмяДокумента].ТекстОтраженияВМеждународномУчете();
	Запрос.УстановитьПараметр("ВалютаФункциональная", ПараметрыОтражения.ВалютыУчета.ФункциональнаяВалюта);
	Запрос.УстановитьПараметр("ВалютаПредставления", ПараметрыОтражения.ВалютыУчета.ВалютаПредставления);
	Запрос.УстановитьПараметр("ВалютаРегл", ПараметрыОтражения.ВалютыУчета.ВалютаРегл);
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Проводка = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ПараметрыОтражения.СтруктураПроводки);
		ЗаполнитьЗначенияСвойств(Проводка, Выборка);
		Проводка.Активность = Истина;
		
		ЗаполнитьЗначенияСвойств(Проводка, ВидыСубконтоСчета(Проводка.СчетДт, "Дт"));
		ЗаполнитьЗначенияСвойств(Проводка, ВидыСубконтоСчета(Проводка.СчетКт, "Кт"));
		
		Документы[ИмяДокумента].ПроверитьЗаполнениеПроводкиМеждународногоУчета(Проводка, Выборка);
		
		ДобавитьПроводкуВТаблицуПроводок(ТаблицаПроводок, Проводка);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ВидыСубконтоСчета(Счет, Суффикс="Дт")
	
	СтруктураВидовСубконто = Новый Структура;
	СтруктураВидовСубконто.Вставить("ВидСубконто"+Суффикс+"1");
	СтруктураВидовСубконто.Вставить("ВидСубконто"+Суффикс+"2");
	СтруктураВидовСубконто.Вставить("ВидСубконто"+Суффикс+"3");
	
	Если ЗначениеЗаполнено(Счет) Тогда
		
		ВидыСубконто = Счет.ВидыСубконто;
		КоличествоСубконто = ВидыСубконто.Количество();
		Для К = 1 По КоличествоСубконто Цикл
			СтруктураВидовСубконто.Вставить("ВидСубконто"+Суффикс+Формат(К, "ЧГ=0"), ВидыСубконто[К - 1].ВидСубконто);
		КонецЦикла;
		
	КонецЕсли;
	Возврат СтруктураВидовСубконто;
	
КонецФункции

Процедура ОтразитьДанныеРегистра(ИмяРегистра, ТекстЗапроса, ПараметрыОтражения, ТаблицаПроводок);

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ПараметрыОтражения.МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапроса;
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеОперативногоУчета = Результат.Выгрузить();
	
	// Добавление ключевого поля
	ДанныеОперативногоУчета.Колонки.Добавить("КлючСтроки", Новый ОписаниеТипов("Число"));
	ДанныеОперативногоУчета.Индексы.Добавить("КлючСтроки");
	КлючСтроки = 1;
	Для каждого Строка Из ДанныеОперативногоУчета Цикл
		Строка.КлючСтроки = КлючСтроки;
		КлючСтроки = КлючСтроки + 1;
	КонецЦикла;
	
	ДанныеОперативногоУчета.Колонки.Добавить("Обработан", Новый ОписаниеТипов("Булево"));
	ДанныеОперативногоУчета.Колонки.Добавить("ГруппаВытеснения", Новый ОписаниеТипов("СправочникСсылка.ШаблоныПроводокДляМеждународногоУчета"));
	
	НеОтражаемыеХозяйственныеОперации = НеОтражаемыеХозяйственныеОперации(ДанныеОперативногоУчета, ТаблицаПроводок, ПараметрыОтражения.СтруктураПроводки);
	Для каждого НеОтражаемаяХозяйственнаяОперация Из НеОтражаемыеХозяйственныеОперации Цикл
		ДанныеОперативногоУчета.Удалить(НеОтражаемаяХозяйственнаяОперация);
	КонецЦикла;
	
	ПараметрыОтражения.Вставить("ИмяРегистра", ИмяРегистра);
	ПараметрыРегистра = МеждународныйУчетСерверПовтИсп.ПараметрыРегистра(ИмяРегистра);
	Если Не ПроверитьКорректностьПоказателей(Результат, ИмяРегистра, ПараметрыРегистра) Тогда
		Возврат;
	КонецЕсли;
	ПараметрыОтражения.Вставить("ПараметрыРегистра", ПараметрыРегистра);
	
	Отбор = Новый Структура("ИсточникДанных", ИмяРегистра);
	ШаблоныРегистра = ПараметрыОтражения.НастройкиОтражения.ШаблоныПроводок.Строки.НайтиСтроки(Отбор);
	ПараметрыОтражения.Вставить("ШаблоныПроводок", ШаблоныРегистра);
	ПараметрыОтражения.Вставить("СоответствиеСубконто", ПараметрыОтражения.НастройкиОтражения.СоответствиеСубконто);
	ПараметрыОтражения.Вставить("ВариантСовместногоПрименения", Перечисления.ВариантыСовместногоПримененияШаблоновПроводок.Все);
	ПараметрыОтражения.Вставить("ДополнительныйОтборГруппы", Неопределено);
	
	СхемаКомпоновкиДанных = СхемаКомпоновкиДанныхОперативногоУчета(ДанныеОперативногоУчета);
	
	ОтразитьДанныеОперативногоУчетаРекурсивно(СхемаКомпоновкиДанных,
		ДанныеОперативногоУчета,
		ПараметрыОтражения,
		ТаблицаПроводок);
		
	ДанныеОперативногоУчета.Свернуть("Регистратор, ХозяйственнаяОперация, Организация", "Обработан");
	Для каждого Данные Из ДанныеОперативногоУчета Цикл
		Если Не Данные.Обработан Тогда
			Проводка = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ПараметрыОтражения.СтруктураПроводки);
			ЗаполнитьЗначенияСвойств(Проводка, Данные);
			ЗаполнитьСтатусПроводки(Проводка, Перечисления.СтатусыОтраженияВМеждународномУчете.ОтсутствуютПравилаОтраженияВУчете);
			ТекстОшибки = НСтр("ru = 'Для хозяйственной операции ""%ХозяйственнаяОперация%"" отсутствуют шаблоны проводок.'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ХозяйственнаяОперация%", Данные.ХозяйственнаяОперация);
			ДобавитьОшибкуПроводки(Проводка, ТекстОшибки);
			ДобавитьПроводкуВТаблицуПроводок(ТаблицаПроводок, Проводка);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция НеОтражаемыеХозяйственныеОперации(ДанныеОперативногоУчета, ТаблицаПроводок, СтруктураПроводки)

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеОперативногоУчета.Период КАК Период,
	|	ДанныеОперативногоУчета.КлючСтроки КАК КлючСтроки,
	|	ДанныеОперативногоУчета.Организация КАК Организация,
	|	ДанныеОперативногоУчета.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеОперативногоУчета.УчетнаяПолитика КАК УчетнаяПолитика
	|ПОМЕСТИТЬ ДанныеОперативногоУчета
	|ИЗ
	|	&ДанныеОперативногоУчета КАК ДанныеОперативногоУчета
	|ИНДЕКСИРОВАТЬ ПО
	|	ХозяйственнаяОперация,
	|	УчетнаяПолитика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеОперативногоУчета.Период КАК Период,
	|	ДанныеОперативногоУчета.КлючСтроки КАК КлючСтроки,
	|	ДанныеОперативногоУчета.Организация КАК Организация,
	|	ДанныеОперативногоУчета.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ИСТИНА КАК ИспользоватьВМеждународномУчете
	|ИЗ
	|	ДанныеОперативногоУчета КАК ДанныеОперативногоУчета
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.НастройкиХозяйственныхОпераций КАК НастройкиХозопераций
	|	ПО
	|		ДанныеОперативногоУчета.ХозяйственнаяОперация = НастройкиХозопераций.ХозяйственнаяОперация
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ХозяйственныеОперацииНеОтражаемыеВМеждународномУчете КАК НеОтражаемыеХозоперации
	|	ПО
	|		ДанныеОперативногоУчета.УчетнаяПолитика = НеОтражаемыеХозоперации.УчетнаяПолитика
	|		И НастройкиХозопераций.Ссылка = НеОтражаемыеХозоперации.Операция
	|ГДЕ
	|	 НастройкиХозОпераций.ИспользоватьВМеждународномУчете
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеОперативногоУчета.Период КАК Период,
	|	ДанныеОперативногоУчета.КлючСтроки КАК КлючСтроки,
	|	ДанныеОперативногоУчета.Организация КАК Организация,
	|	ДанныеОперативногоУчета.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	НастройкиХозОпераций.ИспользоватьВМеждународномУчете КАК ИспользоватьВМеждународномУчете
	|ИЗ
	|	ДанныеОперативногоУчета КАК ДанныеОперативногоУчета
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.НастройкиХозяйственныхОпераций КАК НастройкиХозОпераций
	|	ПО
	|		ДанныеОперативногоУчета.ХозяйственнаяОперация = НастройкиХозопераций.ХозяйственнаяОперация
	|ГДЕ
	|	НЕ НастройкиХозОпераций.ИспользоватьВМеждународномУчете
	|";
	
	Запрос.УстановитьПараметр("ДанныеОперативногоУчета", ДанныеОперативногоУчета);
	ВыборкаНеОтражаемыеХозоперации = Запрос.Выполнить().Выбрать();
	НеОтражаемыеХозяйственныеОперации = Новый Массив;
	Пока ВыборкаНеОтражаемыеХозоперации.Следующий() Цикл
		ЗаписьРегистра = ДанныеОперативногоУчета.Найти(ВыборкаНеОтражаемыеХозоперации.КлючСтроки, "КлючСтроки");
		НеОтражаемыеХозяйственныеОперации.Добавить(ЗаписьРегистра);
		Проводка = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(СтруктураПроводки);
		Проводка.Регистратор = ЗаписьРегистра.Регистратор;
		ЗаполнитьЗначенияСвойств(Проводка, ВыборкаНеОтражаемыеХозоперации);
		ЗаполнитьСтатусПроводки(Проводка, Перечисления.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчете);
		ТекстОшибки = НСтр("ru = 'Хозяйственная операция ""%ХозяйственнаяОперация%"" игнорируется учетной политикой.'");
		Если НЕ ВыборкаНеОтражаемыеХозОперации.ИспользоватьВМеждународномУчете Тогда
			ТекстОшибки = НСтр("ru = 'Хозяйственная операция ""%ХозяйственнаяОперация%"" не используется в международном учете.'");
		КонецЕсли;
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ХозяйственнаяОперация%", ВыборкаНеОтражаемыеХозоперации.ХозяйственнаяОперация);
		ДобавитьОшибкуПроводки(Проводка, ТекстОшибки);
		ДобавитьПроводкуВТаблицуПроводок(ТаблицаПроводок, Проводка);
	КонецЦикла;
	
	Возврат НеОтражаемыеХозяйственныеОперации;

КонецФункции

Функция СхемаКомпоновкиДанныхОперативногоУчета(ДанныеОперативногоУчета)

	СхемаКомпоновкиДанных = ФинансоваяОтчетностьСервер.НоваяСхемаНаборОбъект("ДанныеОперативногоУчета");
	НаборДанных = СхемаКомпоновкиДанных.НаборыДанных.Найти("ДанныеОперативногоУчета");
	
	Настройки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	НоваяГруппировка = Настройки.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	НоваяГруппировка.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	НоваяГруппировка.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
	
	Для Каждого Колонка Из ДанныеОперативногоУчета.Колонки Цикл
		ИмяПоля = Колонка.Имя;
		НовоеПоле = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
		НовоеПоле.Поле = ИмяПоля;
		НовоеПоле.ПутьКДанным = ИмяПоля;
		НовоеПоле.ТипЗначения = Колонка.ТипЗначения;
	КонецЦикла;
	
	Возврат СхемаКомпоновкиДанных;

КонецФункции

Процедура ОтразитьДанныеОперативногоУчетаРекурсивно(СхемаКомпоновкиДанных, ДанныеОперативногоУчета, ПараметрыОтражения, ТаблицаПроводок)

	ШаблоныПроводок = ПараметрыОтражения.ШаблоныПроводок;
	СоответствиеСубконто = ПараметрыОтражения.СоответствиеСубконто;
	ВариантСовместногоПрименения = ПараметрыОтражения.ВариантСовместногоПрименения;
	ДополнительныйОтборГруппы = ПараметрыОтражения.ДополнительныйОтборГруппы;
	СтруктураПроводки = ПараметрыОтражения.СтруктураПроводки;
	
	Вытеснение = Перечисления.ВариантыСовместногоПримененияШаблоновПроводок.Вытеснение;
	МассивВытесненныхСтрок = Новый Массив;
	Для каждого ШаблонПроводки Из ШаблоныПроводок Цикл
		
		Если ШаблонПроводки.ЭтоГруппаШаблонов Тогда
			Если ШаблонПроводки.УстановленДополнительныйОтбор Тогда
				ДополнительныйОтбор = Новый НастройкиКомпоновкиДанных;
				НастройкиОтбора = ШаблонПроводки.ДополнительныйОтбор.Получить();
				ФинансоваяОтчетностьСервер.СкопироватьОтбор(НастройкиОтбора.Отбор, ДополнительныйОтбор.Отбор); 
				Если ДополнительныйОтборГруппы <> Неопределено Тогда
					ФинансоваяОтчетностьСервер.СкопироватьОтбор(ДополнительныйОтборГруппы.Отбор, ДополнительныйОтбор.Отбор); 
				КонецЕсли;
			КонецЕсли;
			ПараметрыОтраженияГруппы = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ПараметрыОтражения);
			ПараметрыОтраженияГруппы.Вставить("ШаблоныПроводок", ШаблонПроводки.Строки);
			ПараметрыОтраженияГруппы.Вставить("ВариантСовместногоПрименения", ШаблонПроводки.ВариантСовместногоПрименения);
			ПараметрыОтраженияГруппы.Вставить("ДополнительныйОтборГруппы", ДополнительныйОтбор);
			ПараметрыОтраженияГруппы.Вставить("СтруктураПроводки", СтруктураПроводки);
			
			ОтразитьДанныеОперативногоУчетаРекурсивно(
				СхемаКомпоновкиДанных, 
				ДанныеОперативногоУчета, 
				ПараметрыОтраженияГруппы, 
				ТаблицаПроводок);
		Иначе
			
			Настройки = Новый НастройкиКомпоновкиДанных;
			НоваяГруппировка = Настройки.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
			НоваяГруппировка.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
			НоваяГруппировка.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
			
			ФинансоваяОтчетностьСервер.НовоеПолеВыбора(Настройки, "КлючСтроки");
			
			ТаблицаУточненияСчетов  = ПараметрыОтражения.ТаблицаУточненияСчетов;
			ВалютыУчета             = ПараметрыОтражения.ВалютыУчета;
			
			Показатели              = ПараметрыОтражения.ПараметрыРегистра.Показатели;
			ПоказателиВВалюте       = ПараметрыОтражения.ПараметрыРегистра.ПоказателиВВалюте;
			ИсточникиУточненияСчета = ПараметрыОтражения.ПараметрыРегистра.ИсточникиУточненияСчета;
			ИсточникиПодразделений  = ПараметрыОтражения.ПараметрыРегистра.ИсточникиПодразделений;
			ИсточникиНаправлений    = ПараметрыОтражения.ПараметрыРегистра.ИсточникиНаправлений;
			ИсточникиСубконто       = ПараметрыОтражения.ПараметрыРегистра.ИсточникиСубконто;
			
			ЗаполнениеСубконтоПоСоответствию = ШаблонПроводки.ЗаполнениеСубконтоПоСоответствию;
			Если ЗаполнениеСубконтоПоСоответствию Тогда
				ИсточникиСубконто = УстановитьЗаполнениеСубконтоПоСоответствиюПользователя(
										СхемаКомпоновкиДанных, 
										Настройки, 
										ШаблонПроводки, 
										СоответствиеСубконто);
			КонецЕсли;
			
			УстановитьОтборДанныхПоШаблонуПроводки(
				СхемаКомпоновкиДанных, 
				Настройки, 
				ШаблонПроводки, 
				ДополнительныйОтборГруппы,
				ВариантСовместногоПрименения);
				
			ВнешниеНаборы = Новый Структура;
			ВнешниеНаборы.Вставить("ДанныеОперативногоУчета", ДанныеОперативногоУчета);
			
			Результат = ФинансоваяОтчетностьСервер.ВыгрузитьРезультатСКД(СхемаКомпоновкиДанных, Настройки, ВнешниеНаборы);
			
			Для каждого Строка Из Результат Цикл
				
				Отбор = Новый Структура;
				Отбор.Вставить("КлючСтроки", Строка.КлючСтроки);
				Данные = ДанныеОперативногоУчета.НайтиСтроки(Отбор)[0];
				
				Данные.Обработан = Истина;
				
				Если ШаблонПроводки.Игнорировать Тогда
					Продолжить;
				КонецЕсли;
				
				Если ВариантСовместногоПрименения = Вытеснение Тогда
					Данные.ГруппаВытеснения = ШаблонПроводки.РодительскийШаблонПроводки;
				КонецЕсли;
				
				Если ШаблонПроводки.Уровень() = 2
					И ШаблонПроводки.ВариантСовместногоПримененияУровня2 = Вытеснение Тогда
					МассивВытесненныхСтрок.Добавить(Данные);
				КонецЕсли;
				
				Проводка = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(СтруктураПроводки);
				ЗаполнитьЗначенияСвойств(Проводка, Данные, "Период, Регистратор, Организация");
				
				Если ШаблонПроводки.РучноеУточнениеПроводки Тогда
					ЗаполнитьСтатусПроводки(Проводка, Перечисления.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчетеВручную);
				КонецЕсли;
				
				Проводка.Активность = Истина;
				Проводка.ШаблонПроводки = ШаблонПроводки.ШаблонПроводки;
				
				ИсточникБалансовойСуммы = ШаблонПроводки.ИсточникБалансовойСуммы;
				Если Не ЗначениеЗаполнено(ИсточникБалансовойСуммы) Тогда
					ЗаполнитьСтатусПроводки(Проводка, Перечисления.СтатусыОтраженияВМеждународномУчете.ОтсутствуютПравилаОтраженияВУчете);
					ТекстОшибки = НСтр("ru = 'Не выбран источник балансовой суммы.'");
					ДобавитьОшибкуПроводки(Проводка, ТекстОшибки);
				Иначе
					Ресурсы = Показатели.Получить(ИсточникБалансовойСуммы).Ресурсы;
					
					БалансоваяСумма = БалансоваяСумма(Данные, ШаблонПроводки, Ресурсы, ВалютыУчета, ВалютыУчета.ФункциональнаяВалюта); 
					Проводка.Сумма = БалансоваяСумма.Сумма;
					Проводка.ВалютаХраненияСуммыФункциональной = БалансоваяСумма.Валюта;
					
					БалансоваяСумма = БалансоваяСумма(Данные, ШаблонПроводки, Ресурсы, ВалютыУчета, ВалютыУчета.ВалютаПредставления);
					Проводка.СуммаПредставления = БалансоваяСумма.Сумма;
					Проводка.ВалютаХраненияСуммыПредставления = БалансоваяСумма.Валюта;
				КонецЕсли;
					
				Проводка.СчетДт = СчетУчета(Данные, ШаблонПроводки, ТаблицаУточненияСчетов, ИсточникиУточненияСчета, "Дт");
				Проводка.СчетКт = СчетУчета(Данные, ШаблонПроводки, ТаблицаУточненияСчетов, ИсточникиУточненияСчета, "Кт");
				
				СвойстваСчетаДт = МеждународныйУчетСерверПовтИсп.СвойстваСчета(Проводка.СчетДт);
				Если СвойстваСчетаДт.Валютный Тогда
					ИсточникСуммыВВалютеДт = ШаблонПроводки.ИсточникСуммыВВалютеДт;
					Если Не ЗначениеЗаполнено(ИсточникСуммыВВалютеДт) Тогда
						ЗаполнитьСтатусПроводки(Проводка, Перечисления.СтатусыОтраженияВМеждународномУчете.ОтсутствуютПравилаОтраженияВУчете);
						ТекстОшибки = НСтр("ru = 'Не выбран источник валютной суммы Дт.'");
						ДобавитьОшибкуПроводки(Проводка, ТекстОшибки);
					Иначе
						ИсточникВалютыДт = ПоказателиВВалюте.Получить(ИсточникСуммыВВалютеДт).ИсточникВалюты;
						Проводка.ВалютаДт = Данные[ИсточникВалютыДт];
						Проводка.ВалютнаяСуммаДт = СуммаВВалюте(Данные, ШаблонПроводки, Ресурсы, ИсточникВалютыДт);
					КонецЕсли;
				Иначе
					Проводка.ВалютаДт = Справочники.Валюты.ПустаяСсылка();
					Проводка.ВалютнаяСуммаДт = 0;
				КонецЕсли;
				
				СвойстваСчетаКт = МеждународныйУчетСерверПовтИсп.СвойстваСчета(Проводка.СчетКт);
				Если СвойстваСчетаКт.Валютный Тогда
					ИсточникСуммыВВалютеКт = ШаблонПроводки.ИсточникСуммыВВалютеКт;
					Если Не ЗначениеЗаполнено(ИсточникСуммыВВалютеКт) Тогда
						ЗаполнитьСтатусПроводки(Проводка, Перечисления.СтатусыОтраженияВМеждународномУчете.ОтсутствуютПравилаОтраженияВУчете);
						ТекстОшибки = НСтр("ru = 'Не выбран источник валютной суммы Кт.'");
						ДобавитьОшибкуПроводки(Проводка, ТекстОшибки);
					Иначе
						ИсточникВалютыКт = ПоказателиВВалюте.Получить(ИсточникСуммыВВалютеКт).ИсточникВалюты;
						Проводка.ВалютаКт = Данные[ИсточникВалютыКт];
						Проводка.ВалютнаяСуммаКт = СуммаВВалюте(Данные, ШаблонПроводки, Ресурсы, ИсточникВалютыКт);
					КонецЕсли;
				Иначе
					Проводка.ВалютаКт = Справочники.Валюты.ПустаяСсылка();
					Проводка.ВалютнаяСуммаКт = 0;
				КонецЕсли;
				
				Если СвойстваСчетаДт.УчетПоПодразделениям Тогда
					Проводка.ПодразделениеДт = ЗначениеИзмерения(Данные, ИсточникиПодразделений, ШаблонПроводки.ИсточникПодразделенияДт);
				КонецЕсли;
				Если СвойстваСчетаКт.УчетПоПодразделениям Тогда
					Проводка.ПодразделениеКт = ЗначениеИзмерения(Данные, ИсточникиПодразделений, ШаблонПроводки.ИсточникПодразделенияКт);
				КонецЕсли;
				
				Если СвойстваСчетаДт.УчетПоНаправлениямДеятельности Тогда
					Проводка.НаправлениеДеятельностиДт = ЗначениеИзмерения(Данные, ИсточникиНаправлений, ШаблонПроводки.ИсточникНаправленияДт);
				КонецЕсли;
				Если СвойстваСчетаКт.УчетПоНаправлениямДеятельности Тогда
					Проводка.НаправлениеДеятельностиКт = ЗначениеИзмерения(Данные, ИсточникиНаправлений, ШаблонПроводки.ИсточникНаправленияКт);
				КонецЕсли;
				
				Проводка.Вставить("ДанныеШаблонаПроводки", ШаблонПроводки);
				
				ДанныеЗапоненияСубконто = Новый Структура;
				ДанныеЗапоненияСубконто.Вставить("Проводка", Проводка);
				ДанныеЗапоненияСубконто.Вставить("Данные", Данные);
				ДанныеЗапоненияСубконто.Вставить("ИсточникиСубконто", ИсточникиСубконто);
				ДанныеЗапоненияСубконто.Вставить("СвойстваСчетаДт", СвойстваСчетаДт);
				ДанныеЗапоненияСубконто.Вставить("СвойстваСчетаКт", СвойстваСчетаКт);
				ДанныеЗапоненияСубконто.Вставить("ИмяРегистра", ПараметрыОтражения.ИмяРегистра);
				Если ЗаполнениеСубконтоПоСоответствию Тогда
					ДанныеЗапоненияСубконто.Данные = Строка;
				КонецЕсли;
				ЗаполнитьСубконто("Дт", ДанныеЗапоненияСубконто);
				ЗаполнитьСубконто("Кт", ДанныеЗапоненияСубконто);
				
				ПроверитьЗаполнениеПроводки(Проводка, ШаблонПроводки, ПараметрыОтражения.ПараметрыРегистра);
				
				ДобавитьПроводкуВТаблицуПроводок(ТаблицаПроводок, Проводка);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ВытесненнаяСтрока Из МассивВытесненныхСтрок Цикл
		ВытесненнаяСтрока.ГруппаВытеснения = ШаблоныПроводок.Родитель.Родитель.ШаблонПроводки
	КонецЦикла;

КонецПроцедуры

Функция УстановитьЗаполнениеСубконтоПоСоответствиюПользователя(СхемаКомпоновкиДанных, Настройки, ШаблонПроводки, СоответствиеСубконто)
	
	СхемаКомпоновкиДанных.ВычисляемыеПоля.Очистить();
	
	Отбор = Новый Структура;
	Отбор.Вставить("ШаблонПроводки", ШаблонПроводки.ШаблонПроводки);
	Отбор.Вставить("ПоложениеСубконто");
	
	ПоложенияСубконто = Новый Массив;
	ПоложенияСубконто.Добавить("Дт");
	ПоложенияСубконто.Добавить("Кт");
	
	ИсточникиСубконто = Новый Структура;
	ИсточникиСубконто.Вставить("СубконтоДт", Новый Массив);
	ИсточникиСубконто.Вставить("СубконтоКт", Новый Массив);
	Для каждого ПоложениеСубконто Из ПоложенияСубконто Цикл
		Отбор.ПоложениеСубконто = ПоложениеСубконто;
		НастройкиЗаполнения = СоответствиеСубконто.НайтиСтроки(Отбор);
		
		НомерНастройки = 1;
		ПоляИсточника = ИсточникиСубконто["Субконто"+ПоложениеСубконто];
		Для Каждого Настройка Из НастройкиЗаполнения Цикл
			ПутьКДанным = "СубконтоМеждународный" + ПоложениеСубконто + "_Н" + НомерНастройки;
			Выражение = ВыражениеЗаполненияСубконтоПоНастройке(СхемаКомпоновкиДанных, Настройка);
			
			ФинансоваяОтчетностьСервер.НовоеВычисляемоеПоле(СхемаКомпоновкиДанных, ПутьКДанным, Выражение); 
			ФинансоваяОтчетностьСервер.НовоеПолеВыбора(Настройки, ПутьКДанным);
			
			ПоляИсточника.Добавить(ПутьКДанным);
			НомерНастройки = НомерНастройки + 1;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ИсточникиСубконто;
	
КонецФункции

Функция УстановитьОтборДанныхПоШаблонуПроводки(СхемаКомпоновкиДанных, Настройки, ШаблонПроводки, ДополнительныйОтборГруппы, ВариантСовместногоПрименения)
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Настройки, "ХозяйственнаяОперация", ШаблонПроводки.ХозяйственнаяОперация);
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Настройки, "УчетнаяПолитика", ШаблонПроводки.УчетнаяПолитика);
	
	Если ДополнительныйОтборГруппы <> Неопределено Тогда
		ФинансоваяОтчетностьСервер.СкопироватьОтбор(ДополнительныйОтборГруппы.Отбор, Настройки.Отбор);
	КонецЕсли;
	
	Если ШаблонПроводки.УстановленДополнительныйОтбор Тогда
		НастройкиОтбора = ШаблонПроводки.ДополнительныйОтбор.Получить();
		ФинансоваяОтчетностьСервер.СкопироватьОтбор(НастройкиОтбора.Отбор, Настройки.Отбор);
	КонецЕсли;
	
	Уровень = ШаблонПроводки.Уровень();
	
	Если Уровень = 1 И ВариантСовместногоПрименения = Перечисления.ВариантыСовместногоПримененияШаблоновПроводок.Вытеснение Тогда
		ГруппаЭлементовОтбораИли = Настройки.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
		ГруппаЭлементовОтбораИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
		КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(ГруппаЭлементовОтбораИли,
			"Обработан",
			Ложь,
			ВидСравненияКомпоновкиДанных.Равно);
		КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(ГруппаЭлементовОтбораИли,
			"ГруппаВытеснения",
			ШаблонПроводки.РодительскийШаблонПроводки,
			ВидСравненияКомпоновкиДанных.НеРавно);
	КонецЕсли;
	
	Если Уровень = 2 Тогда
		Если ВариантСовместногоПрименения = Перечисления.ВариантыСовместногоПримененияШаблоновПроводок.Все Тогда
			ГруппаЭлементовОтбораИли = Настройки.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
			ГруппаЭлементовОтбораИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
			КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(ГруппаЭлементовОтбораИли,
				"Обработан",
				Ложь,
				ВидСравненияКомпоновкиДанных.Равно);
			КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(ГруппаЭлементовОтбораИли,
				"ГруппаВытеснения",
				ШаблонПроводки.РодительскийШаблонПроводки2,
				ВидСравненияКомпоновкиДанных.НеРавно);
		Иначе
			ГруппаЭлементовОтбораИли = Настройки.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
			ГруппаЭлементовОтбораИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
			КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(ГруппаЭлементовОтбораИли,
				"Обработан",
				Ложь,
				ВидСравненияКомпоновкиДанных.Равно);
			ГруппаЭлементовОтбораИ = ГруппаЭлементовОтбораИли.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
			ГруппаЭлементовОтбораИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
			КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(ГруппаЭлементовОтбораИ,
				"ГруппаВытеснения",
				ШаблонПроводки.РодительскийШаблонПроводки,
				ВидСравненияКомпоновкиДанных.НеРавно);
			КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(ГруппаЭлементовОтбораИ,
				"ГруппаВытеснения",
				ШаблонПроводки.РодительскийШаблонПроводки2,
				ВидСравненияКомпоновкиДанных.НеРавно);
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Функция ТекстыЗапросовПоРегистрам()

	ТекстыЗапросов = Новый Соответствие;
	ТекстыЗапросов.Вставить("ВыручкаИСебестоимостьПродаж", ТекстЗапросаВыручкаИСебестоимостьПродаж());
	ТекстыЗапросов.Вставить("ДвиженияДенежныеСредстваДоходыРасходы", ТекстЗапросаДвиженияДенежныеСредстваДоходыРасходы());
	ТекстыЗапросов.Вставить("ДвиженияДенежныеСредстваКонтрагент", ТекстЗапросаДвиженияДенежныеСредстваКонтрагент());
	ТекстыЗапросов.Вставить("ДвиженияДенежныхСредств", ТекстЗапросаДвиженияДенежныхСредств());
	ТекстыЗапросов.Вставить("ДвиженияДоходыРасходыПрочиеАктивыПассивы", ТекстЗапросаДвиженияДоходыРасходыПрочиеАктивыПассивы());
	ТекстыЗапросов.Вставить("ДвиженияКонтрагентДоходыРасходы", ТекстЗапросаДвиженияКонтрагентДоходыРасходы());
	ТекстыЗапросов.Вставить("ДвиженияКонтрагентКонтрагент", ТекстЗапросаДвиженияКонтрагентКонтрагент());
	ТекстыЗапросов.Вставить("ДвиженияНоменклатураДоходыРасходы", ТекстЗапросаДвиженияНоменклатураДоходыРасходы());
	ТекстыЗапросов.Вставить("ДвиженияНоменклатураНоменклатура", ТекстЗапросаДвиженияНоменклатураНоменклатура());
	ТекстыЗапросов.Вставить("Закупки", ТекстЗапросаЗакупки());
	ТекстыЗапросов.Вставить("НДСЗаписиКнигиПокупок", ТекстЗапросаНДСЗаписиКнигиПокупок());
	ТекстыЗапросов.Вставить("НДСЗаписиКнигиПродаж", ТекстЗапросаНДСЗаписиКнигиПродаж());
	ТекстыЗапросов.Вставить("РасчетыСКлиентамиПоДокументам", ТекстЗапросаРасчетыСКлиентамиПоДокументам());
	ТекстыЗапросов.Вставить("РасчетыСПоставщикамиПоДокументам", ТекстЗапросаРасчетыСПоставщикамиПоДокументам());
	
	Возврат ТекстыЗапросов;

КонецФункции

Функция ТекстЗапросаВыручкаИСебестоимостьПродаж() Экспорт

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ДанныеРегистра.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.Подразделение КАК Подразделение,
	|	
	|	ДанныеРегистра.НаправлениеДеятельностиНоменклатуры КАК НаправлениеДеятельностиНоменклатуры,
	|	ДанныеРегистра.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ДанныеРегистра.АналитикаУчетаНаборов КАК АналитикаУчетаНаборов,
	|	ДанныеРегистра.Склад КАК Склад,
	|	ДанныеРегистра.ТипЗапасов КАК ТипЗапасов,
	|	ДанныеРегистра.ВидЗапасов КАК ВидЗапасов,
	|	
	|	ДанныеРегистра.НаправлениеДеятельностиКонтрагента КАК НаправлениеДеятельностиКонтрагента,
	|	ДанныеРегистра.Партнер КАК Партнер,
	|	ДанныеРегистра.Контрагент КАК Контрагент,
	|	ДанныеРегистра.ВидЗапасов.Контрагент КАК Поставщик,
	|	ДанныеРегистра.Соглашение КАК Соглашение,
	|	ДанныеРегистра.Договор КАК Договор,
	|	ДанныеРегистра.ЗаказКлиента КАК ЗаказКлиента,
	|	
	|	СУММА(ДанныеРегистра.СуммаВыручки) КАК СуммаВыручки,
	|	СУММА(ДанныеРегистра.СуммаВыручкиБезНДС) КАК СуммаВыручкиБезНДС,
	|	СУММА(ДанныеРегистра.СуммаВыручки - ДанныеРегистра.СуммаВыручкиБезНДС) КАК СуммаНДС,
	|	СУММА(ДанныеРегистра.СуммаВыручкиСНДСРегл) КАК СуммаВыручкиСНДСРегл,
	|	СУММА(ДанныеРегистра.СуммаВыручкиРегл) КАК СуммаВыручкиРегл,
	|	СУММА(ДанныеРегистра.СуммаВыручкиСНДСРегл - ДанныеРегистра.СуммаВыручкиРегл) КАК СуммаНДСРегл,
	|	
	|	СУММА(ДанныеРегистра.Себестоимость) КАК Себестоимость,
	|	СУММА(ДанныеРегистра.СебестоимостьБезНДС) КАК СебестоимостьБезНДС,
	|	СУММА(ДанныеРегистра.СебестоимостьРегл) КАК СебестоимостьРегл,
	|	СУММА(ДанныеРегистра.СуммаДополнительныхРасходов) КАК СуммаДополнительныхРасходов,
	|	СУММА(ДанныеРегистра.СуммаДополнительныхРасходовБезНДС) КАК СуммаДополнительныхРасходовБезНДС,
	|	СУММА(ДанныеРегистра.СуммаДополнительныхРасходов - ДанныеРегистра.СуммаДополнительныхРасходовБезНДС) КАК СуммаНДСДополнительныхРасходов,
	|	
	|	ДанныеРегистра.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	СУММА(ДанныеРегистра.СуммаВВалютеВзаиморасчетов) КАК СуммаВВалютеВзаиморасчетов,
	|	СУММА(ДанныеРегистра.СуммаБезНДСВВалютеВзаиморасчетов) КАК СуммаБезНДСВВалютеВзаиморасчетов,
	|	СУММА(ДанныеРегистра.СуммаВВалютеВзаиморасчетов - ДанныеРегистра.СуммаБезНДСВВалютеВзаиморасчетов) КАК СуммаНДСВВалютеВзаиморасчетов,
	|	
	|	ДанныеРегистра.ВалютаДокумента КАК ВалютаДокумента,
	|	СУММА(ДанныеРегистра.СуммаВВалютеДокумента) КАК СуммаВВалютеДокумента,
	|	СУММА(ДанныеРегистра.СуммаБезНДСВВалютеДокумента) КАК СуммаБезНДСВВалютеДокумента,
	|	СУММА(ДанныеРегистра.СуммаВВалютеДокумента - ДанныеРегистра.СуммаБезНДСВВалютеДокумента) КАК СуммаНДСВВалютеДокумента,
	|	
	|	ДанныеРегистра.ИсточникГФУНоменклатуры КАК ИсточникГФУНоменклатуры,
	|	ДанныеРегистра.ИсточникГФУРасчетов КАК ИсточникГФУРасчетов,
	|	ДанныеРегистра.УчетнаяПолитика КАК УчетнаяПолитика
	|ПОМЕСТИТЬ ДанныеРегистра
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДанныеРегистра.Период КАК Период,
	|		ДанныеРегистра.Регистратор КАК Регистратор,
	|		ДанныеРегистра.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|		АналитикаПартнеров.Организация КАК Организация,
	|		ДанныеРегистра.Подразделение КАК Подразделение,
	|	
	|		ДанныеРегистра.НаправлениеДеятельностиНоменклатуры КАК НаправлениеДеятельностиНоменклатуры,
	|		ДанныеРегистра.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ДанныеРегистра.АналитикаУчетаНаборов КАК АналитикаУчетаНаборов,
	|		ДанныеРегистра.Склад КАК Склад,
	|		ДанныеРегистра.ТипЗапасов КАК ТипЗапасов,
	|		ДанныеРегистра.ВидЗапасов КАК ВидЗапасов,
	|	
	|		ДанныеРегистра.НаправлениеДеятельностиКонтрагента КАК НаправлениеДеятельностиКонтрагента,
	|		АналитикаПартнеров.Партнер КАК Партнер,
	|		АналитикаПартнеров.Контрагент КАК Контрагент,
	|		ДанныеРегистра.Соглашение КАК Соглашение,
	|		ДанныеРегистра.Договор КАК Договор,
	|		ДанныеРегистра.ЗаказКлиента КАК ЗаказКлиента,
	|		
	|		ДанныеРегистра.СуммаВыручки КАК СуммаВыручки,
	|		ДанныеРегистра.СуммаВыручкиБезНДС КАК СуммаВыручкиБезНДС,
	|		ДанныеРегистра.СуммаВыручкиСНДСРегл КАК СуммаВыручкиСНДСРегл,
	|		ДанныеРегистра.СуммаВыручкиРегл КАК СуммаВыручкиРегл,
	|	
	|		ДанныеРегистра.Себестоимость КАК Себестоимость,
	|		ДанныеРегистра.СебестоимостьБезНДС КАК СебестоимостьБезНДС,
	|		ДанныеРегистра.СебестоимостьРегл КАК СебестоимостьРегл,
	|		ДанныеРегистра.СуммаДополнительныхРасходов КАК СуммаДополнительныхРасходов,
	|		ДанныеРегистра.СуммаДополнительныхРасходовБезНДС КАК СуммаДополнительныхРасходовБезНДС,
	|	
	|		ДанныеРегистра.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|		ДанныеРегистра.СуммаВВалютеВзаиморасчетов КАК СуммаВВалютеВзаиморасчетов,
	|		ДанныеРегистра.СуммаБезНДСВВалютеВзаиморасчетов КАК СуммаБезНДСВВалютеВзаиморасчетов,
	|	
	|		ДанныеРегистра.ВалютаДокумента КАК ВалютаДокумента,
	|		ДанныеРегистра.СуммаВВалютеДокумента КАК СуммаВВалютеДокумента,
	|		ДанныеРегистра.СуммаБезНДСВВалютеДокумента КАК СуммаБезНДСВВалютеДокумента,
	|	
	|		ДанныеРегистра.ИсточникГФУНоменклатуры КАК ИсточникГФУНоменклатуры,
	|		ДанныеРегистра.ИсточникГФУРасчетов КАК ИсточникГФУРасчетов,
	|		ДокументыКОтражению.УчетнаяПолитика КАК УчетнаяПолитика
	|	ИЗ
	|		РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПартнеров
	|	ПО
	|		ДанныеРегистра.АналитикаУчетаПоПартнерам = АналитикаПартнеров.КлючАналитики
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ДокументыКОтражению КАК ДокументыКОтражению
	|	ПО
	|		ДанныеРегистра.Регистратор = ДокументыКОтражению.Регистратор
	|		И АналитикаПартнеров.Организация = ДокументыКОтражению.Организация
	|	ГДЕ
	|		ДанныеРегистра.Активность
	// Отсечем движения сделанные расчетом себестоимости (они попадут в результат после выполнения объединения со следующим запросом):
	|		И ДанныеРегистра.ДокументДвижения = НЕОПРЕДЕЛЕНО
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ДанныеРегистра.Период КАК Период,
	|		ДанныеРегистра.ДокументДвижения КАК Регистратор,
	|		ДанныеРегистра.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|		АналитикаПартнеров.Организация КАК Организация,
	|		ДанныеРегистра.Подразделение КАК Подразделение,
	|	
	|		ДанныеРегистра.НаправлениеДеятельностиНоменклатуры КАК НаправлениеДеятельностиНоменклатуры,
	|		ДанныеРегистра.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ДанныеРегистра.АналитикаУчетаНаборов КАК АналитикаУчетаНаборов,
	|		ДанныеРегистра.Склад КАК Склад,
	|		ДанныеРегистра.ТипЗапасов КАК ТипЗапасов,
	|		ДанныеРегистра.ВидЗапасов КАК ВидЗапасов,
	|	
	|		ДанныеРегистра.НаправлениеДеятельностиКонтрагента КАК НаправлениеДеятельностиКонтрагента,
	|		АналитикаПартнеров.Партнер КАК Партнер,
	|		АналитикаПартнеров.Контрагент КАК Контрагент,
	|		ДанныеРегистра.Соглашение КАК Соглашение,
	|		ДанныеРегистра.Договор КАК Договор,
	|		ДанныеРегистра.ЗаказКлиента КАК ЗаказКлиента,
	|		
	|		ДанныеРегистра.СуммаВыручки КАК СуммаВыручки,
	|		ДанныеРегистра.СуммаВыручкиБезНДС КАК СуммаВыручкиБезНДС,
	|		ДанныеРегистра.СуммаВыручкиСНДСРегл КАК СуммаВыручкиСНДСРегл,
	|		ДанныеРегистра.СуммаВыручкиРегл КАК СуммаВыручкиРегл,
	|	
	|		ДанныеРегистра.Себестоимость КАК Себестоимость,
	|		ДанныеРегистра.СебестоимостьБезНДС КАК СебестоимостьБезНДС,
	|		ДанныеРегистра.СебестоимостьРегл КАК СебестоимостьРегл,
	|		ДанныеРегистра.СуммаДополнительныхРасходов КАК СуммаДополнительныхРасходов,
	|		ДанныеРегистра.СуммаДополнительныхРасходовБезНДС КАК СуммаДополнительныхРасходовБезНДС,
	|	
	|		ДанныеРегистра.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|		ДанныеРегистра.СуммаВВалютеВзаиморасчетов КАК СуммаВВалютеВзаиморасчетов,
	|		ДанныеРегистра.СуммаБезНДСВВалютеВзаиморасчетов КАК СуммаБезНДСВВалютеВзаиморасчетов,
	|	
	|		ДанныеРегистра.ВалютаДокумента КАК ВалютаДокумента,
	|		ДанныеРегистра.СуммаВВалютеДокумента КАК СуммаВВалютеДокумента,
	|		ДанныеРегистра.СуммаБезНДСВВалютеДокумента КАК СуммаБезНДСВВалютеДокумента,
	|	
	|		ДанныеРегистра.ИсточникГФУНоменклатуры КАК ИсточникГФУНоменклатуры,
	|		ДанныеРегистра.ИсточникГФУРасчетов КАК ИсточникГФУРасчетов,
	|		ДокументыКОтражению.УчетнаяПолитика КАК УчетнаяПолитика
	|	ИЗ
	|		РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПартнеров
	|	ПО
	|		ДанныеРегистра.АналитикаУчетаПоПартнерам = АналитикаПартнеров.КлючАналитики
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ДокументыКОтражению КАК ДокументыКОтражению
	|	ПО
	|		ДанныеРегистра.ДокументДвижения = ДокументыКОтражению.Регистратор
	|		И АналитикаПартнеров.Организация = ДокументыКОтражению.Организация
	|	ГДЕ
	|		ДанныеРегистра.Активность
	|	) КАК ДанныеРегистра
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистра.Период,
	|	ДанныеРегистра.Регистратор,
	|	ДанныеРегистра.ХозяйственнаяОперация,
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.Подразделение,
	|	ДанныеРегистра.НаправлениеДеятельностиНоменклатуры,
	|	ДанныеРегистра.АналитикаУчетаНоменклатуры,
	|	ДанныеРегистра.АналитикаУчетаНаборов,
	|	ДанныеРегистра.Склад,
	|	ДанныеРегистра.ТипЗапасов,
	|	ДанныеРегистра.ВидЗапасов,
	|	ДанныеРегистра.НаправлениеДеятельностиКонтрагента,
	|	ДанныеРегистра.Партнер,
	|	ДанныеРегистра.Контрагент,
	|	ДанныеРегистра.ВидЗапасов.Контрагент,
	|	ДанныеРегистра.Соглашение,
	|	ДанныеРегистра.Договор,
	|	ДанныеРегистра.ЗаказКлиента,
	|	ДанныеРегистра.ВалютаВзаиморасчетов,
	|	ДанныеРегистра.ВалютаДокумента,
	|	ДанныеРегистра.ИсточникГФУНоменклатуры,
	|	ДанныеРегистра.ИсточникГФУРасчетов,
	|	ДанныеРегистра.УчетнаяПолитика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) КАК ТипДокумента,
	|	ДанныеРегистра.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.Подразделение КАК Подразделение,
	|
	|	ДанныеРегистра.НаправлениеДеятельностиНоменклатуры КАК НаправлениеДеятельностиНоменклатуры,
	|	АналитикаНоменклатуры.Номенклатура КАК Номенклатура,
	|	АналитикаНоменклатуры.Характеристика КАК Характеристика,
	|	АналитикаНоменклатуры.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ДанныеРегистра.АналитикаУчетаНаборов КАК АналитикаУчетаНаборов,
	|	ДанныеРегистра.Склад КАК Склад,
	|	ВЫБОР
	|		КОГДА ДанныеРегистра.Склад ССЫЛКА Справочник.Склады
	|			ТОГДА ДанныеРегистра.Склад.Подразделение
	|		ИНАЧЕ ДанныеРегистра.Склад
	|	КОНЕЦ КАК СкладПодразделение,
	|	ДанныеРегистра.ТипЗапасов КАК ТипЗапасов,
	|	ДанныеРегистра.ВидЗапасов КАК ВидЗапасов,
	|
	|	ДанныеРегистра.НаправлениеДеятельностиКонтрагента КАК НаправлениеДеятельностиКонтрагента,
	|	ДанныеРегистра.Партнер КАК Партнер,
	|	ДанныеРегистра.Контрагент КАК Контрагент,
	|	ДанныеРегистра.Поставщик КАК Поставщик,
	|	ДанныеРегистра.Соглашение КАК Соглашение,
	|	ДанныеРегистра.Договор КАК Договор,
	|	ДанныеРегистра.ЗаказКлиента КАК ЗаказКлиента,
	|	ДанныеРегистра.ЗаказКлиента.Подразделение КАК ЗаказПодразделение,
	|	
	|	ДанныеРегистра.СуммаВыручки КАК СуммаВыручки,
	|	ДанныеРегистра.СуммаВыручкиБезНДС КАК СуммаВыручкиБезНДС,
	|	ДанныеРегистра.СуммаВыручки - ДанныеРегистра.СуммаВыручкиБезНДС КАК СуммаНДС,
	|	ДанныеРегистра.СуммаВыручкиСНДСРегл КАК СуммаВыручкиСНДСРегл,
	|	ДанныеРегистра.СуммаВыручкиРегл КАК СуммаВыручкиРегл,
	|	ДанныеРегистра.СуммаВыручкиСНДСРегл - ДанныеРегистра.СуммаВыручкиРегл КАК СуммаНДСРегл,
	|
	|	ДанныеРегистра.Себестоимость КАК Себестоимость,
	|	ДанныеРегистра.СебестоимостьБезНДС КАК СебестоимостьБезНДС,
	|	ДанныеРегистра.СебестоимостьРегл КАК СебестоимостьРегл,
	|	ДанныеРегистра.СуммаДополнительныхРасходов КАК СуммаДополнительныхРасходов,
	|	ДанныеРегистра.СуммаДополнительныхРасходовБезНДС КАК СуммаДополнительныхРасходовБезНДС,
	|	ДанныеРегистра.СуммаДополнительныхРасходов - ДанныеРегистра.СуммаДополнительныхРасходовБезНДС КАК СуммаНДСДополнительныхРасходов,
	|
	|	ДанныеРегистра.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ДанныеРегистра.СуммаВВалютеВзаиморасчетов КАК СуммаВВалютеВзаиморасчетов,
	|	ДанныеРегистра.СуммаБезНДСВВалютеВзаиморасчетов КАК СуммаБезНДСВВалютеВзаиморасчетов,
	|	ДанныеРегистра.СуммаВВалютеВзаиморасчетов - ДанныеРегистра.СуммаБезНДСВВалютеВзаиморасчетов КАК СуммаНДСВВалютеВзаиморасчетов,
	|
	|	ДанныеРегистра.ВалютаДокумента КАК ВалютаДокумента,
	|	ДанныеРегистра.СуммаВВалютеДокумента КАК СуммаВВалютеДокумента,
	|	ДанныеРегистра.СуммаБезНДСВВалютеДокумента КАК СуммаБезНДСВВалютеДокумента,
	|	ДанныеРегистра.СуммаВВалютеДокумента - ДанныеРегистра.СуммаБезНДСВВалютеДокумента КАК СуммаНДСВВалютеДокумента,
	|
	|	ДанныеРегистра.ИсточникГФУНоменклатуры.ГруппаФинансовогоУчета КАК ГФУНоменклатуры,
	|	ДанныеРегистра.ИсточникГФУРасчетов.ГруппаФинансовогоУчета КАК ГФУРасчетов,
	|	ДанныеРегистра.УчетнаяПолитика КАК УчетнаяПолитика
	|ИЗ
	|	ДанныеРегистра КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаНоменклатуры
	|	ПО
	|		ДанныеРегистра.АналитикаУчетаНоменклатуры = АналитикаНоменклатуры.КлючАналитики
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДанныеРегистра
	|";

	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаДвиженияДенежныеСредстваДоходыРасходы()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) КАК ТипДокумента,
	|	ДанныеРегистра.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.Подразделение КАК Подразделение,
	|	ДанныеРегистра.ПодразделениеДоходовРасходов КАК ПодразделениеДоходовРасходов,
	|
	|	ДанныеРегистра.НаправлениеДеятельностиДС КАК НаправлениеДеятельностиДС,
	|	ДанныеРегистра.ДенежныеСредства КАК ДенежныеСредства,
	|	ДанныеРегистра.ДенежныеСредства.Подразделение КАК ДенежныеСредстваПодразделение,
	|	ДанныеРегистра.ТипДенежныхСредств КАК ТипДенежныхСредств,
	|	ДанныеРегистра.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	ДанныеРегистра.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ДанныеРегистра.ТипПлатежаФЗ275 КАК ТипПлатежаФЗ275,
	|	ДанныеРегистра.Валюта КАК Валюта,
	|
	|	ДанныеРегистра.НаправлениеДеятельностиСтатьи КАК НаправлениеДеятельностиСтатьи,
	|	ДанныеРегистра.СтатьяДоходовРасходов КАК СтатьяДоходовРасходов,
	|	ДанныеРегистра.АналитикаДоходов КАК АналитикаДоходов,
	|	ДанныеРегистра.АналитикаРасходов КАК АналитикаРасходов,
	|	ДанныеРегистра.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	ВЫБОР
	|		КОГДА ДанныеРегистра.СтатьяДоходовРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиДоходов
	|			ТОГДА ДанныеРегистра.АналитикаДоходов
	|		ИНАЧЕ ДанныеРегистра.АналитикаРасходов
	|	КОНЕЦ КАК АналитикаДоходовРасходовПодразделение,
	|	ДанныеРегистра.СтавкаНДС КАК СтавкаНДС,
	|
	|	ДанныеРегистра.Сумма КАК Сумма,
	|	ДанныеРегистра.СуммаРегл КАК СуммаРегл,
	|	ДанныеРегистра.СуммаВВалюте КАК СуммаВВалюте,
	|
	|	ДанныеРегистра.Сумма - ДанныеРегистра.СуммаНДС КАК СуммаБезНДС,
	|	ДанныеРегистра.СуммаРегл - ДанныеРегистра.СуммаНДСРегл КАК СуммаРеглБезНДС,
	|	ДанныеРегистра.СуммаВВалюте - ДанныеРегистра.СуммаНДСВВалютеПлатежа КАК СуммаБезНДСВВалютеПлатежа,
	|
	|	ДанныеРегистра.СуммаНДС КАК СуммаНДС,
	|	ДанныеРегистра.СуммаНДСРегл КАК СуммаНДСРегл,
	|	ДанныеРегистра.СуммаНДСВВалютеПлатежа КАК СуммаНДСВВалютеПлатежа,
	|
	|	ДанныеРегистра.СуммаКВыплатеВРамкахЛимита + ДанныеРегистра.СуммаКВыплатеСверхЛимита КАК СуммаКВыплате,
	|	ДанныеРегистра.СуммаКВыплатеВРамкахЛимита КАК СуммаКВыплатеВРамкахЛимита,
	|	ДанныеРегистра.СуммаКВыплатеСверхЛимита КАК СуммаКВыплатеСверхЛимита,
	|	
	|	ВЫБОР
	|		КОГДА ДанныеРегистра.ИсточникГФУДенежныхСредств ССЫЛКА Справочник.ДоговорыКредитовИДепозитов
	|			ТОГДА ДанныеРегистра.ИсточникГФУДенежныхСредств.ГруппаФинансовогоУчетаДенежныхСредств
	|		ИНАЧЕ ДанныеРегистра.ИсточникГФУДенежныхСредств.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК ГФУДенежныхСредств,
	|	ДанныеРегистра.ИсточникГФУДоходовРасходов.ГруппаФинансовогоУчета КАК ГФУДоходовРасходов,
	|	ДокументыКОтражению.УчетнаяПолитика КАК УчетнаяПолитика
	|ИЗ
	|	РегистрНакопления.ДвиженияДенежныеСредстваДоходыРасходы КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ДокументыКОтражению КАК ДокументыКОтражению
	|	ПО
	|		ДанныеРегистра.Регистратор = ДокументыКОтражению.Регистратор
	|		И ДанныеРегистра.Организация = ДокументыКОтражению.Организация
	|ГДЕ
	|	ДанныеРегистра.Активность
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДвиженияДенежныеСредстваКонтрагент()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) КАК ТипДокумента,
	|	ДанныеРегистра.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.Подразделение КАК Подразделение,
	|
	|	ДанныеРегистра.ДенежныеСредства КАК ДенежныеСредства,
	|	ДанныеРегистра.НаправлениеДеятельностиДС КАК НаправлениеДеятельностиДС,
	|	ДанныеРегистра.ДенежныеСредства.Подразделение КАК ДенежныеСредстваПодразделение,
	|	ДанныеРегистра.ТипДенежныхСредств КАК ТипДенежныхСредств,
	|	ДанныеРегистра.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	ДанныеРегистра.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ДанныеРегистра.ТипПлатежаФЗ275 КАК ТипПлатежаФЗ275,
	|	ДанныеРегистра.ВалютаПлатежа КАК ВалютаПлатежа,
	|
	|	ДанныеРегистра.НаправлениеДеятельностиКонтрагента КАК НаправлениеДеятельностиКонтрагента,
	|	ДанныеРегистра.Партнер КАК Партнер,
	|	ДанныеРегистра.Контрагент КАК Контрагент,
	|	ДанныеРегистра.Договор КАК Договор,
	|	ДанныеРегистра.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ДанныеРегистра.ОбъектРасчетов.Подразделение КАК ОбъектРасчетовПодразделение,
	|	ДанныеРегистра.РасчетныйДокумент КАК РасчетныйДокумент,
	|	
	|	ДанныеРегистра.СуммаОплаты КАК СуммаОплаты,
	|	ДанныеРегистра.СуммаОплатыРегл КАК СуммаОплатыРегл,
	|	ДанныеРегистра.СуммаОплатыВВалютеПлатежа КАК СуммаОплатыВВалютеПлатежа,
	|	ДанныеРегистра.СуммаПостоплаты КАК СуммаПостоплаты,
	|	ДанныеРегистра.СуммаПостоплатыРегл КАК СуммаПостоплатыРегл,
	|	ДанныеРегистра.СуммаПостоплатыВВалютеПлатежа КАК СуммаПостоплатыВВалютеПлатежа,
	|	ДанныеРегистра.СуммаПредоплаты КАК СуммаПредоплаты,
	|	ДанныеРегистра.СуммаПредоплатыРегл КАК СуммаПредоплатыРегл,
	|	ДанныеРегистра.СуммаПредоплатыВВалютеПлатежа КАК СуммаПредоплатыВВалютеПлатежа,
	|	
	|	ДанныеРегистра.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ДанныеРегистра.СуммаОплатыВВалютеВзаиморасчетов КАК СуммаОплатыВВалютеВзаиморасчетов,
	|	ДанныеРегистра.СуммаПостоплатыВВалютеВзаиморасчетов КАК СуммаПостоплатыВВалютеВзаиморасчетов,
	|	ДанныеРегистра.СуммаПредоплатыВВалютеВзаиморасчетов КАК СуммаПредоплатыВВалютеВзаиморасчетов,
	|
	|	ДанныеРегистра.СтавкаНДС КАК СтавкаНДС,
	|	ДанныеРегистра.СуммаНДС КАК СуммаНДС,
	|	ДанныеРегистра.СуммаНДСРегл КАК СуммаНДСРегл,
	|	ДанныеРегистра.СуммаНДСВВалютеПлатежа КАК СуммаНДСВВалютеПлатежа,
	|
	|	ДанныеРегистра.СуммаКВыплатеВРамкахЛимита + ДанныеРегистра.СуммаКВыплатеСверхЛимита КАК СуммаКВыплате,
	|	ДанныеРегистра.СуммаКВыплатеВРамкахЛимита КАК СуммаКВыплатеВРамкахЛимита,
	|	ДанныеРегистра.СуммаКВыплатеСверхЛимита КАК СуммаКВыплатеСверхЛимита,
	|	
	|	ВЫБОР
	|		КОГДА ДанныеРегистра.ИсточникГФУДенежныхСредств ССЫЛКА Справочник.ДоговорыКредитовИДепозитов
	|			ТОГДА ДанныеРегистра.ИсточникГФУДенежныхСредств.ГруппаФинансовогоУчетаДенежныхСредств
	|		ИНАЧЕ ДанныеРегистра.ИсточникГФУДенежныхСредств.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК ГФУДенежныхСредств,
	|	ДанныеРегистра.ИсточникГФУРасчетов.ГруппаФинансовогоУчета КАК ГФУРасчетов,
	|	ДокументыКОтражению.УчетнаяПолитика КАК УчетнаяПолитика
	|ИЗ
	|	РегистрНакопления.ДвиженияДенежныеСредстваКонтрагент КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ДокументыКОтражению КАК ДокументыКОтражению
	|	ПО
	|		ДанныеРегистра.Регистратор = ДокументыКОтражению.Регистратор
	|		И ДанныеРегистра.Организация = ДокументыКОтражению.Организация
	|ГДЕ
	|	ДанныеРегистра.Активность
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДвиженияДенежныхСредств()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) КАК ТипДокумента,
	|	ДанныеРегистра.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.Подразделение КАК Подразделение,
	|
	|	ДанныеРегистра.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ДанныеРегистра.ДенежныеСредства КАК ДенежныеСредства,
	|	ДанныеРегистра.ДенежныеСредства.Подразделение КАК ДенежныеСредстваПодразделение,
	|	ДанныеРегистра.ТипДенежныхСредств КАК ТипДенежныхСредств,
	|	ДанныеРегистра.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|	ДанныеРегистра.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ДанныеРегистра.ТипПлатежаФЗ275 КАК ТипПлатежаФЗ275,
	|	ДанныеРегистра.Валюта КАК Валюта,
	|
	|	ДанныеРегистра.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	ДанныеРегистра.КорДенежныеСредства КАК КорДенежныеСредства,
	|	ДанныеРегистра.КорДенежныеСредства.Подразделение КАК КорДенежныеСредстваПодразделение,
	|	ДанныеРегистра.КорТипДенежныхСредств КАК КорТипДенежныхСредств,
	|	ДанныеРегистра.КорВалюта КАК КорВалюта,
	|
	|	ДанныеРегистра.Сумма КАК Сумма,
	|	ДанныеРегистра.СуммаРегл КАК СуммаРегл,
	|	ДанныеРегистра.СуммаВВалюте КАК СуммаВВалюте,
	|	ДанныеРегистра.СуммаВКорВалюте КАК СуммаВКорВалюте,
	|
	|	ДанныеРегистра.СуммаКВыплатеВРамкахЛимита + ДанныеРегистра.СуммаКВыплатеСверхЛимита КАК СуммаКВыплате,
	|	ДанныеРегистра.СуммаКВыплатеВРамкахЛимита КАК СуммаКВыплатеВРамкахЛимита,
	|	ДанныеРегистра.СуммаКВыплатеСверхЛимита КАК СуммаКВыплатеСверхЛимита,
	|	
	|	ВЫБОР
	|		КОГДА ДанныеРегистра.ИсточникГФУДенежныхСредств ССЫЛКА Справочник.ДоговорыКредитовИДепозитов
	|			ТОГДА ДанныеРегистра.ИсточникГФУДенежныхСредств.ГруппаФинансовогоУчетаДенежныхСредств
	|		ИНАЧЕ ДанныеРегистра.ИсточникГФУДенежныхСредств.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК ГФУДенежныхСредств,
	|	ВЫБОР
	|		КОГДА ДанныеРегистра.ИсточникКорГФУДенежныхСредств ССЫЛКА Справочник.ДоговорыКредитовИДепозитов
	|			ТОГДА ДанныеРегистра.ИсточникКорГФУДенежныхСредств.ГруппаФинансовогоУчетаДенежныхСредств
	|		ИНАЧЕ ДанныеРегистра.ИсточникКорГФУДенежныхСредств.ГруппаФинансовогоУчета
	|	КОНЕЦ КАК КорГФУДенежныхСредств,
	|	ДокументыКОтражению.УчетнаяПолитика КАК УчетнаяПолитика
	|ИЗ
	|	РегистрНакопления.ДвиженияДенежныхСредств КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ДокументыКОтражению КАК ДокументыКОтражению
	|	ПО
	|		ДанныеРегистра.Регистратор = ДокументыКОтражению.Регистратор
	|		И ДанныеРегистра.Организация = ДокументыКОтражению.Организация
	|ГДЕ
	|	ДанныеРегистра.Активность
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДвиженияДоходыРасходыПрочиеАктивыПассивы()

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) КАК ТипДокумента,
	|	ДанныеРегистра.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.Подразделение КАК Подразделение,
	|
	|	ДанныеРегистра.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ДанныеРегистра.Статья КАК Статья,
	|	ДанныеРегистра.АналитикаДоходов КАК АналитикаДоходов,
	|	ДанныеРегистра.АналитикаРасходов КАК АналитикаРасходов,
	|	ДанныеРегистра.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	ВЫБОР
	|		КОГДА ДанныеРегистра.Статья ССЫЛКА ПланВидовХарактеристик.СтатьиДоходов
	|			ТОГДА ДанныеРегистра.АналитикаДоходов
	|		ИНАЧЕ ДанныеРегистра.АналитикаРасходов
	|	КОНЕЦ КАК АналитикаДоходовРасходовПодразделение,
	|	ДанныеРегистра.Статья.ГруппаФинансовогоУчета КАК ГФУДоходовРасходов,
	|
	|	ДанныеРегистра.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	ДанныеРегистра.КорПодразделение КАК КорПодразделение,
	|	ДанныеРегистра.КорСтатья КАК КорСтатья,
	|	ДанныеРегистра.КорАналитикаДоходов КАК КорАналитикаДоходов,
	|	ДанныеРегистра.КорАналитикаРасходов КАК КорАналитикаРасходов,
	|	ДанныеРегистра.КорАналитикаАктивовПассивов КАК КорАналитикаАктивовПассивов,
	|	ДанныеРегистра.КорСтатья.ГруппаФинансовогоУчета КАК КорГФУДоходовРасходов,
	|
	|	ДанныеРегистра.Сумма КАК Сумма,
	|	ДанныеРегистра.СуммаРегл КАК СуммаРегл,
	|	
	|	ДанныеРегистра.Валюта КАК Валюта,
	|	ДанныеРегистра.СуммаВВалюте КАК СуммаВВалюте,
	|
	|	ДанныеРегистра.ВидОперацииПоЗарплате КАК ВидОперацииПоЗарплате,
	|	ДокументыКОтражению.УчетнаяПолитика КАК УчетнаяПолитика
	|ИЗ
	|	РегистрНакопления.ДвиженияДоходыРасходыПрочиеАктивыПассивы КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ДокументыКОтражению КАК ДокументыКОтражению
	|	ПО
	|		ДанныеРегистра.Регистратор = ДокументыКОтражению.Регистратор
	|		И ДанныеРегистра.Организация = ДокументыКОтражению.Организация
	|ГДЕ
	|	ДанныеРегистра.Активность
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДвиженияКонтрагентДоходыРасходы()

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) КАК ТипДокумента,
	|	ДанныеРегистра.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.Подразделение КАК Подразделение,
	|
	|	ДанныеРегистра.НаправлениеДеятельностиКонтрагента КАК НаправлениеДеятельностиКонтрагента,
	|	ДанныеРегистра.Партнер КАК Партнер,
	|	ДанныеРегистра.Контрагент КАК Контрагент,
	|	ДанныеРегистра.Договор КАК Договор,
	|	ДанныеРегистра.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ДанныеРегистра.ОбъектРасчетов.Подразделение КАК ОбъектРасчетовПодразделение,
	|	
	|	ДанныеРегистра.НаправлениеДеятельностиСтатьи КАК НаправлениеДеятельностиСтатьи,
	|	ДанныеРегистра.СтатьяДоходовРасходов КАК СтатьяДоходовРасходов,
	|	ДанныеРегистра.АналитикаДоходов КАК АналитикаДоходов,
	|	ДанныеРегистра.АналитикаРасходов КАК АналитикаРасходов,
	|	ДанныеРегистра.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	ВЫБОР
	|		КОГДА ДанныеРегистра.СтатьяДоходовРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиДоходов
	|			ТОГДА ДанныеРегистра.АналитикаДоходов
	|		ИНАЧЕ ДанныеРегистра.АналитикаРасходов
	|	КОНЕЦ КАК АналитикаДоходовРасходовПодразделение,
	|
	|	ДанныеРегистра.Сумма КАК Сумма,
	|	ДанныеРегистра.СуммаБезНДС КАК СуммаБезНДС,
	|	ДанныеРегистра.Сумма - ДанныеРегистра.СуммаБезНДС КАК СуммаНДС,
	|	ДанныеРегистра.СуммаРегл КАК СуммаРегл,
	|	ДанныеРегистра.СуммаРеглБезНДС КАК СуммаРеглБезНДС,
	|	ДанныеРегистра.СуммаРегл - ДанныеРегистра.СуммаРеглБезНДС КАК СуммаНДСРегл,
	|
	|	ДанныеРегистра.Валюта КАК Валюта,
	|	ДанныеРегистра.СуммаВВалюте КАК СуммаВВалюте,
	|	ДанныеРегистра.СуммаБезНДСВВалюте КАК СуммаБезНДСВВалюте,
	|	ДанныеРегистра.СуммаВВалюте - ДанныеРегистра.СуммаБезНДСВВалюте КАК СуммаНДСВВалюте,
	|
	|	ДанныеРегистра.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ДанныеРегистра.СуммаВВалютеВзаиморасчетов КАК СуммаВВалютеВзаиморасчетов,
	|	ДанныеРегистра.СуммаБезНДСВВалютеВзаиморасчетов КАК СуммаБезНДСВВалютеВзаиморасчетов,
	|	ДанныеРегистра.СуммаВВалютеВзаиморасчетов - ДанныеРегистра.СуммаБезНДСВВалютеВзаиморасчетов КАК СуммаНДСВВалютеВзаиморасчетов,
	|
	|	ДанныеРегистра.ИсточникГФУРасчетов.ГруппаФинансовогоУчета КАК ГФУРасчетов,
	|	ДанныеРегистра.СтатьяДоходовРасходов.ГруппаФинансовогоУчета КАК ГФУДоходовРасходов,
	|	ДокументыКОтражению.УчетнаяПолитика КАК УчетнаяПолитика
	|ИЗ
	|	РегистрНакопления.ДвиженияКонтрагентДоходыРасходы КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ДокументыКОтражению КАК ДокументыКОтражению
	|	ПО
	|		ДанныеРегистра.Регистратор = ДокументыКОтражению.Регистратор
	|		И ДанныеРегистра.Организация = ДокументыКОтражению.Организация
	|	ГДЕ
	|		ДанныеРегистра.Активность
	|";
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаДвиженияКонтрагентКонтрагент()

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) КАК ТипДокумента,
	|	ДанныеРегистра.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.Подразделение КАК Подразделение,
	|
	|	ДанныеРегистра.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ДанныеРегистра.Партнер КАК Партнер,
	|	ДанныеРегистра.Контрагент КАК Контрагент,
	|	ДанныеРегистра.Договор КАК Договор,
	|	ДанныеРегистра.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ДанныеРегистра.ОбъектРасчетов.Подразделение КАК ОбъектРасчетовПодразделение,
	|	ДанныеРегистра.ТипСуммы КАК ТипСуммы,
	|	
	|	ДанныеРегистра.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	ДанныеРегистра.КорПартнер КАК КорПартнер,
	|	ДанныеРегистра.КорКонтрагент КАК КорКонтрагент,
	|	ДанныеРегистра.КорДоговор КАК КорДоговор,
	|	ДанныеРегистра.КорОбъектРасчетов КАК КорОбъектРасчетов,
	|	ДанныеРегистра.КорОбъектРасчетов.Подразделение КАК КорОбъектРасчетовПодразделение,
	|	ДанныеРегистра.КорТипСуммы КАК КорТипСуммы,
	|
	|	ДанныеРегистра.Сумма КАК Сумма,
	|	ДанныеРегистра.СуммаБезНДС КАК СуммаБезНДС,
	|	ДанныеРегистра.Сумма - ДанныеРегистра.СуммаБезНДС КАК СуммаНДС,
	|	ДанныеРегистра.СуммаРегл КАК СуммаРегл,
	|	ДанныеРегистра.СуммаРеглБезНДС КАК СуммаРеглБезНДС,
	|	ДанныеРегистра.СуммаРегл - ДанныеРегистра.СуммаРеглБезНДС КАК СуммаНДСРегл,
	|
	|	ДанныеРегистра.Валюта КАК Валюта,
	|	ДанныеРегистра.СуммаВВалюте КАК СуммаВВалюте,
	|	ДанныеРегистра.СуммаБезНДСВВалюте КАК СуммаБезНДСВВалюте,
	|	ДанныеРегистра.СуммаВВалюте - ДанныеРегистра.СуммаБезНДСВВалюте КАК СуммаНДСВВалюте,
	|
	|	ДанныеРегистра.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ДанныеРегистра.СуммаВВалютеВзаиморасчетов КАК СуммаВВалютеВзаиморасчетов,
	|	ДанныеРегистра.СуммаБезНДСВВалютеВзаиморасчетов КАК СуммаБезНДСВВалютеВзаиморасчетов,
	|	ДанныеРегистра.СуммаВВалютеВзаиморасчетов - ДанныеРегистра.СуммаБезНДСВВалютеВзаиморасчетов КАК СуммаНДСВВалютеВзаиморасчетов,
	|	
	|	ДанныеРегистра.КорВалютаВзаиморасчетов КАК КорВалютаВзаиморасчетов,
	|	ДанныеРегистра.КорСуммаВВалютеВзаиморасчетов КАК КорСуммаВВалютеВзаиморасчетов,
	|	ДанныеРегистра.КорСуммаБезНДСВВалютеВзаиморасчетов КАК КорСуммаБезНДСВВалютеВзаиморасчетов,
	|	ДанныеРегистра.КорСуммаВВалютеВзаиморасчетов - ДанныеРегистра.КорСуммаБезНДСВВалютеВзаиморасчетов КАК КорСуммаНДСВВалютеВзаиморасчетов,
	|	
	|	ДанныеРегистра.ИсточникГФУРасчетов.ГруппаФинансовогоУчета КАК ГФУРасчетов,
	|	ДанныеРегистра.КорИсточникГФУРасчетов.ГруппаФинансовогоУчета КАК КорГФУРасчетов,
	|	ДокументыКОтражению.УчетнаяПолитика КАК УчетнаяПолитика
	|ИЗ
	|	РегистрНакопления.ДвиженияКонтрагентКонтрагент КАК ДанныеРегистра	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ДокументыКОтражению КАК ДокументыКОтражению
	|	ПО
	|		ДанныеРегистра.Регистратор = ДокументыКОтражению.Регистратор
	|		И ДанныеРегистра.Организация = ДокументыКОтражению.Организация
	|ГДЕ
	|	ДанныеРегистра.Активность
	|";
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаДвиженияНоменклатураДоходыРасходы()

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ДанныеРегистра.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.Подразделение КАК Подразделение,
	|
	|	ДанныеРегистра.НаправлениеДеятельностиСтатьи КАК НаправлениеДеятельностиСтатьи,
	|	ДанныеРегистра.СтатьяДоходовРасходов КАК СтатьяДоходовРасходов,
	|	ДанныеРегистра.АналитикаДоходов КАК АналитикаДоходов,
	|	ДанныеРегистра.АналитикаРасходов КАК АналитикаРасходов,
	|	ДанныеРегистра.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|
	|	ДанныеРегистра.НаправлениеДеятельностиНоменклатуры КАК НаправлениеДеятельностиНоменклатуры,
	|	ДанныеРегистра.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ДанныеРегистра.Склад КАК Склад,
	|	ДанныеРегистра.ТипЗапасов КАК ТипЗапасов,
	|	ДанныеРегистра.ВидЗапасов КАК ВидЗапасов,
	|	ДанныеРегистра.ВидЗапасов.Контрагент КАК Поставщик,
	|
	|	СУММА(ДанныеРегистра.Стоимость) КАК Стоимость,
	|	СУММА(ДанныеРегистра.СтоимостьБезНДС) КАК СтоимостьБезНДС,
	|	СУММА(ДанныеРегистра.Стоимость - ДанныеРегистра.СтоимостьБезНДС) КАК СуммаНДС,
	|	СУММА(ДанныеРегистра.СтоимостьРегл) КАК СтоимостьРегл,
	|	
	|	ДанныеРегистра.ИсточникГФУНоменклатуры КАК ИсточникГФУНоменклатуры,
	|	ДанныеРегистра.УчетнаяПолитика КАК УчетнаяПолитика
	|ПОМЕСТИТЬ ДанныеРегистра
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДанныеРегистра.Период КАК Период,
	|		ДанныеРегистра.Регистратор КАК Регистратор,
	|		ДанныеРегистра.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|		ДанныеРегистра.Организация КАК Организация,
	|		ДанныеРегистра.Подразделение КАК Подразделение,
	|	
	|		ДанныеРегистра.НаправлениеДеятельностиСтатьи КАК НаправлениеДеятельностиСтатьи,
	|		ДанныеРегистра.СтатьяДоходовРасходов КАК СтатьяДоходовРасходов,
	|		ДанныеРегистра.АналитикаДоходов КАК АналитикаДоходов,
	|		ДанныеРегистра.АналитикаРасходов КАК АналитикаРасходов,
	|		ДанныеРегистра.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	
	|		ДанныеРегистра.НаправлениеДеятельностиНоменклатуры КАК НаправлениеДеятельностиНоменклатуры,
	|		ДанныеРегистра.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ДанныеРегистра.Склад КАК Склад,
	|		ДанныеРегистра.ТипЗапасов КАК ТипЗапасов,
	|		ДанныеРегистра.ВидЗапасов КАК ВидЗапасов,
	|	
	|		ДанныеРегистра.Стоимость КАК Стоимость,
	|		ДанныеРегистра.СтоимостьБезНДС КАК СтоимостьБезНДС,
	|		ДанныеРегистра.СтоимостьРегл КАК СтоимостьРегл,
	|		
	|		ДанныеРегистра.ИсточникГФУНоменклатуры КАК ИсточникГФУНоменклатуры,
	|		ДокументыКОтражению.УчетнаяПолитика КАК УчетнаяПолитика
	|	ИЗ
	|		РегистрНакопления.ДвиженияНоменклатураДоходыРасходы КАК ДанныеРегистра
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			ДокументыКОтражению КАК ДокументыКОтражению
	|		ПО
	|			ДанныеРегистра.Регистратор = ДокументыКОтражению.Регистратор
	|			И ДанныеРегистра.Организация = ДокументыКОтражению.Организация
	|	ГДЕ
	|		ДанныеРегистра.Активность
	// Отсечем движения сделанные расчетом себестоимости (они попадут в результат после выполнения объединения со следующим запросом):
	|		И ДанныеРегистра.ДокументДвижения = НЕОПРЕДЕЛЕНО
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ДанныеРегистра.Период КАК Период,
	|		ДанныеРегистра.ДокументДвижения КАК Регистратор,
	|		ДанныеРегистра.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|		ДанныеРегистра.Организация КАК Организация,
	|		ДанныеРегистра.Подразделение КАК Подразделение,
	|	
	|		ДанныеРегистра.НаправлениеДеятельностиСтатьи КАК НаправлениеДеятельностиСтатьи,
	|		ДанныеРегистра.СтатьяДоходовРасходов КАК СтатьяДоходовРасходов,
	|		ДанныеРегистра.АналитикаДоходов КАК АналитикаДоходов,
	|		ДанныеРегистра.АналитикаРасходов КАК АналитикаРасходов,
	|		ДанныеРегистра.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	
	|		ДанныеРегистра.НаправлениеДеятельностиНоменклатуры КАК НаправлениеДеятельностиНоменклатуры,
	|		ДанныеРегистра.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ДанныеРегистра.Склад КАК Склад,
	|		ДанныеРегистра.ТипЗапасов КАК ТипЗапасов,
	|		ДанныеРегистра.ВидЗапасов КАК ВидЗапасов,
	|	
	|		ДанныеРегистра.Стоимость КАК Стоимость,
	|		ДанныеРегистра.СтоимостьБезНДС КАК СтоимостьБезНДС,
	|		ДанныеРегистра.СтоимостьРегл КАК СтоимостьРегл,
	|		
	|		ДанныеРегистра.ИсточникГФУНоменклатуры КАК ИсточникГФУНоменклатуры,
	|		ДокументыКОтражению.УчетнаяПолитика КАК УчетнаяПолитика
	|	ИЗ
	|		РегистрНакопления.ДвиженияНоменклатураДоходыРасходы КАК ДанныеРегистра
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			ДокументыКОтражению КАК ДокументыКОтражению
	|		ПО
	|			ДанныеРегистра.ДокументДвижения = ДокументыКОтражению.Регистратор
	|			И ДанныеРегистра.Организация = ДокументыКОтражению.Организация
	|	ГДЕ
	|		ДанныеРегистра.Активность
	|	) КАК ДанныеРегистра
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистра.Период,
	|	ДанныеРегистра.Регистратор,
	|	ДанныеРегистра.ХозяйственнаяОперация,
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.Подразделение,
	|	ДанныеРегистра.НаправлениеДеятельностиСтатьи,
	|	ДанныеРегистра.СтатьяДоходовРасходов,
	|	ДанныеРегистра.АналитикаДоходов,
	|	ДанныеРегистра.АналитикаРасходов,
	|	ДанныеРегистра.НаправлениеДеятельностиНоменклатуры,
	|	ДанныеРегистра.АналитикаАктивовПассивов,
	|	ДанныеРегистра.АналитикаУчетаНоменклатуры,
	|	ДанныеРегистра.Склад,
	|	ДанныеРегистра.ТипЗапасов,
	|	ДанныеРегистра.ВидЗапасов,
	|	ДанныеРегистра.ВидЗапасов.Контрагент,
	|	ДанныеРегистра.ИсточникГФУНоменклатуры,
	|	ДанныеРегистра.УчетнаяПолитика
	|ИМЕЮЩИЕ
	|	СУММА(ДанныеРегистра.Стоимость) <> 0
	|	ИЛИ СУММА(ДанныеРегистра.СтоимостьБезНДС) <> 0
	|	ИЛИ СУММА(ДанныеРегистра.Стоимость - ДанныеРегистра.СтоимостьБезНДС) <> 0
	|	ИЛИ СУММА(ДанныеРегистра.СтоимостьРегл) <> 0 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) КАК ТипДокумента,
	|	ДанныеРегистра.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.Подразделение КАК Подразделение,
	|
	|	ДанныеРегистра.НаправлениеДеятельностиСтатьи КАК НаправлениеДеятельностиСтатьи,
	|	ДанныеРегистра.СтатьяДоходовРасходов КАК СтатьяДоходовРасходов,
	|	ДанныеРегистра.АналитикаДоходов КАК АналитикаДоходов,
	|	ДанныеРегистра.АналитикаРасходов КАК АналитикаРасходов,
	|	ДанныеРегистра.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	ВЫБОР
	|		КОГДА ДанныеРегистра.СтатьяДоходовРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиДоходов
	|			ТОГДА ДанныеРегистра.АналитикаДоходов
	|		ИНАЧЕ ДанныеРегистра.АналитикаРасходов
	|	КОНЕЦ КАК АналитикаДоходовРасходовПодразделение,
	|
	|	ДанныеРегистра.НаправлениеДеятельностиНоменклатуры КАК НаправлениеДеятельностиНоменклатуры,
	|	Аналитика.Номенклатура КАК Номенклатура,
	|	Аналитика.Характеристика КАК Характеристика,
	|	Аналитика.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ДанныеРегистра.Склад КАК Склад,
	|	ТИПЗНАЧЕНИЯ(ДанныеРегистра.Склад) КАК ТипСклада,
	|	ВЫБОР
	|		КОГДА ДанныеРегистра.Склад ССЫЛКА Справочник.Склады
	|			ТОГДА ДанныеРегистра.Склад.Подразделение
	|		ИНАЧЕ ДанныеРегистра.Склад
	|	КОНЕЦ КАК СкладПодразделение,
	|	ДанныеРегистра.ТипЗапасов КАК ТипЗапасов,
	|	ДанныеРегистра.ВидЗапасов КАК ВидЗапасов,
	|	ДанныеРегистра.Поставщик КАК Поставщик,
	|
	|	ДанныеРегистра.Стоимость КАК Стоимость,
	|	ДанныеРегистра.СтоимостьБезНДС КАК СтоимостьБезНДС,
	|	ДанныеРегистра.СуммаНДС КАК СуммаНДС,
	|	ДанныеРегистра.СтоимостьРегл КАК СтоимостьРегл,
	|	
	|	ДанныеРегистра.ИсточникГФУНоменклатуры.ГруппаФинансовогоУчета КАК ГФУНоменклатуры,
	|	ДанныеРегистра.СтатьяДоходовРасходов.ГруппаФинансовогоУчета КАК ГФУДоходовРасходов,
	|	ДанныеРегистра.УчетнаяПолитика КАК УчетнаяПолитика
	|ИЗ
	|	ДанныеРегистра КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО
	|		ДанныеРегистра.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДанныеРегистра
	|";

	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаДвиженияНоменклатураНоменклатура()

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ДанныеРегистра.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.Подразделение КАК Подразделение,
	|
	|	ДанныеРегистра.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ДанныеРегистра.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ДанныеРегистра.Склад КАК Склад,
	|	ДанныеРегистра.ТипЗапасов КАК ТипЗапасов,
	|	ДанныеРегистра.ВидЗапасов КАК ВидЗапасов,
	|	ДанныеРегистра.ВидЗапасов.Контрагент КАК Поставщик,
	|
	|	ДанныеРегистра.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	ДанныеРегистра.КорАналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
	|	ДанныеРегистра.КорСклад КАК КорСклад,
	|	ДанныеРегистра.КорТипЗапасов КАК КорТипЗапасов,
	|	ДанныеРегистра.КорВидЗапасов КАК КорВидЗапасов,
	|	ДанныеРегистра.КорВидЗапасов.Контрагент КАК КорПоставщик,
	|
	|	СУММА(ДанныеРегистра.Стоимость) КАК Стоимость,
	|	СУММА(ДанныеРегистра.СтоимостьБезНДС) КАК СтоимостьБезНДС,
	|	СУММА(ДанныеРегистра.СтоимостьРегл) КАК СтоимостьРегл,
	|	
	|	ДанныеРегистра.ИсточникГФУНоменклатуры КАК ИсточникГФУНоменклатуры,
	|	ДанныеРегистра.КорИсточникГФУНоменклатуры КАК КорИсточникГФУНоменклатуры,
	|	ДанныеРегистра.КорОрганизация КАК КорОрганизация,
	|	ДанныеРегистра.УчетнаяПолитика КАК УчетнаяПолитика
	|ПОМЕСТИТЬ ДанныеРегистра
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДанныеРегистра.Период КАК Период,
	|		ДанныеРегистра.Регистратор КАК Регистратор,
	|		ДанныеРегистра.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|		ДанныеРегистра.Организация КАК Организация,
	|		ДанныеРегистра.Подразделение КАК Подразделение,
	|	
	|		ДанныеРегистра.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ДанныеРегистра.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ДанныеРегистра.Склад КАК Склад,
	|		ДанныеРегистра.ТипЗапасов КАК ТипЗапасов,
	|		ДанныеРегистра.ВидЗапасов КАК ВидЗапасов,
	|	
	|		ДанныеРегистра.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|		ДанныеРегистра.КорАналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
	|		ДанныеРегистра.КорСклад КАК КорСклад,
	|		ДанныеРегистра.КорТипЗапасов КАК КорТипЗапасов,
	|		ДанныеРегистра.КорВидЗапасов КАК КорВидЗапасов,
	|	
	|		ДанныеРегистра.Стоимость КАК Стоимость,
	|		ДанныеРегистра.СтоимостьБезНДС КАК СтоимостьБезНДС,
	|		ДанныеРегистра.СтоимостьРегл КАК СтоимостьРегл,
	|		
	|		ДанныеРегистра.ИсточникГФУНоменклатуры КАК ИсточникГФУНоменклатуры,
	|		ДанныеРегистра.КорИсточникГФУНоменклатуры КАК КорИсточникГФУНоменклатуры,
	|		ДанныеРегистра.КорОрганизация КАК КорОрганизация,
	|		ДокументыКОтражению.УчетнаяПолитика КАК УчетнаяПолитика
	|	ИЗ
	|		РегистрНакопления.ДвиженияНоменклатураНоменклатура КАК ДанныеРегистра
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			ДокументыКОтражению КАК ДокументыКОтражению
	|		ПО
	|			ДанныеРегистра.Регистратор = ДокументыКОтражению.Регистратор
	|			И ДанныеРегистра.Организация = ДокументыКОтражению.Организация
	|	ГДЕ
	|		ДанныеРегистра.Активность
	// Отсечем движения сделанные расчетом себестоимости (они попадут в результат после выполнения объединения со следующим запросом):
	|		И ДанныеРегистра.ДокументДвижения = НЕОПРЕДЕЛЕНО
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ДанныеРегистра.Период КАК Период,
	|		ДанныеРегистра.ДокументДвижения КАК Регистратор,
	|		ДанныеРегистра.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|		ДанныеРегистра.Организация КАК Организация,
	|		ДанныеРегистра.Подразделение КАК Подразделение,
	|	
	|		ДанныеРегистра.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ДанныеРегистра.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ДанныеРегистра.Склад КАК Склад,
	|		ДанныеРегистра.ТипЗапасов КАК ТипЗапасов,
	|		ДанныеРегистра.ВидЗапасов КАК ВидЗапасов,
	|	
	|		ДанныеРегистра.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|		ДанныеРегистра.КорАналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
	|		ДанныеРегистра.КорСклад КАК КорСклад,
	|		ДанныеРегистра.КорТипЗапасов КАК КорТипЗапасов,
	|		ДанныеРегистра.КорВидЗапасов КАК КорВидЗапасов,
	|	
	|		ДанныеРегистра.Стоимость КАК Стоимость,
	|		ДанныеРегистра.СтоимостьБезНДС КАК СтоимостьБезНДС,
	|		ДанныеРегистра.СтоимостьРегл КАК СтоимостьРегл,
	|		
	|		ДанныеРегистра.ИсточникГФУНоменклатуры КАК ИсточникГФУНоменклатуры,
	|		ДанныеРегистра.КорИсточникГФУНоменклатуры КАК КорИсточникГФУНоменклатуры,
	|		ДанныеРегистра.КорОрганизация КАК КорОрганизация,
	|		ДокументыКОтражению.УчетнаяПолитика КАК УчетнаяПолитика
	|	ИЗ
	|		РегистрНакопления.ДвиженияНоменклатураНоменклатура КАК ДанныеРегистра
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			ДокументыКОтражению КАК ДокументыКОтражению
	|		ПО
	|			ДанныеРегистра.ДокументДвижения = ДокументыКОтражению.Регистратор
	|			И ДанныеРегистра.Организация = ДокументыКОтражению.Организация
	|	ГДЕ
	|		ДанныеРегистра.Активность
	|	) ДанныеРегистра
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистра.Период,
	|	ДанныеРегистра.Регистратор,
	|	ДанныеРегистра.ХозяйственнаяОперация,
	|	ДанныеРегистра.Организация,
	|	ДанныеРегистра.Подразделение,
	|	ДанныеРегистра.НаправлениеДеятельности,
	|	ДанныеРегистра.АналитикаУчетаНоменклатуры,
	|	ДанныеРегистра.Склад,
	|	ДанныеРегистра.ТипЗапасов,
	|	ДанныеРегистра.ВидЗапасов,
	|	ДанныеРегистра.ВидЗапасов.Контрагент,
	|	ДанныеРегистра.КорНаправлениеДеятельности,
	|	ДанныеРегистра.КорАналитикаУчетаНоменклатуры,
	|	ДанныеРегистра.КорСклад,
	|	ДанныеРегистра.КорТипЗапасов,
	|	ДанныеРегистра.КорВидЗапасов,
	|	ДанныеРегистра.КорВидЗапасов.Контрагент,
	|	ДанныеРегистра.ИсточникГФУНоменклатуры,
	|	ДанныеРегистра.КорИсточникГФУНоменклатуры,
	|	ДанныеРегистра.КорОрганизация,
	|	ДанныеРегистра.УчетнаяПолитика
	|ИМЕЮЩИЕ
	|	СУММА(ДанныеРегистра.Стоимость) <> 0
	|	ИЛИ СУММА(ДанныеРегистра.СтоимостьБезНДС) <> 0
	|	ИЛИ СУММА(ДанныеРегистра.СтоимостьРегл) <> 0 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) КАК ТипДокумента,
	|	ДанныеРегистра.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.Подразделение КАК Подразделение,
	|
	|	ДанныеРегистра.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Аналитика.Номенклатура КАК Номенклатура,
	|	Аналитика.Характеристика КАК Характеристика,
	|	Аналитика.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ДанныеРегистра.Склад КАК Склад,
	|	ТИПЗНАЧЕНИЯ(ДанныеРегистра.Склад) КАК ТипСклада,
	|	ВЫБОР
	|		КОГДА ДанныеРегистра.Склад ССЫЛКА Справочник.Склады
	|			ТОГДА ДанныеРегистра.Склад.Подразделение
	|		ИНАЧЕ ДанныеРегистра.Склад
	|	КОНЕЦ КАК СкладПодразделение,
	|	ДанныеРегистра.ТипЗапасов КАК ТипЗапасов,
	|	ДанныеРегистра.ВидЗапасов КАК ВидЗапасов,
	|	ДанныеРегистра.Поставщик КАК Поставщик,
	|
	|	ДанныеРегистра.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	КорАналитика.Номенклатура КАК КорНоменклатура,
	|	КорАналитика.Характеристика КАК КорХарактеристика,
	|	КорАналитика.Номенклатура.ТипНоменклатуры КАК КорТипНоменклатуры,
	|	ДанныеРегистра.КорСклад КАК КорСклад,
	|	ТИПЗНАЧЕНИЯ(ДанныеРегистра.КорСклад) КАК ТипКорСклада,
	|	ВЫБОР
	|		КОГДА ДанныеРегистра.Склад ССЫЛКА Справочник.Склады
	|			ТОГДА ДанныеРегистра.КорСклад.Подразделение
	|		ИНАЧЕ ДанныеРегистра.КорСклад
	|	КОНЕЦ КАК КорСкладПодразделение,
	|	ДанныеРегистра.КорТипЗапасов КАК КорТипЗапасов,
	|	ДанныеРегистра.КорВидЗапасов КАК КорВидЗапасов,
	|	ДанныеРегистра.КорПоставщик КАК КорПоставщик,
	|
	|	ДанныеРегистра.Стоимость КАК Стоимость,
	|	ДанныеРегистра.СтоимостьБезНДС КАК СтоимостьБезНДС,
	|	ДанныеРегистра.СтоимостьРегл КАК СтоимостьРегл,
	|	
	|	ДанныеРегистра.ИсточникГФУНоменклатуры.ГруппаФинансовогоУчета КАК ГФУНоменклатуры,
	|	ДанныеРегистра.КорИсточникГФУНоменклатуры.ГруппаФинансовогоУчета КАК КорГФУНоменклатуры,
	|	ДанныеРегистра.КорОрганизация КАК КорОрганизация,
	|	ДанныеРегистра.УчетнаяПолитика КАК УчетнаяПолитика
	|ИЗ
	|	ДанныеРегистра КАК ДанныеРегистра
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО
	|		ДанныеРегистра.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК КорАналитика
	|	ПО
	|		ДанныеРегистра.КорАналитикаУчетаНоменклатуры = КорАналитика.КлючАналитики
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДанныеРегистра
	|";
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаЗакупки()

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) КАК ТипДокумента,
	|	ДанныеРегистра.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.Подразделение КАК Подразделение,
	|
	|	ДанныеРегистра.НаправлениеДеятельностиНоменклатуры КАК НаправлениеДеятельностиНоменклатуры,
	|	Аналитика.Номенклатура КАК Номенклатура,
	|	Аналитика.Характеристика КАК Характеристика,
	|	Аналитика.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ДанныеРегистра.Склад КАК Склад,
	|	ВЫБОР
	|		КОГДА ДанныеРегистра.Склад ССЫЛКА Справочник.Склады
	|			ТОГДА ДанныеРегистра.Склад.Подразделение
	|		ИНАЧЕ ДанныеРегистра.Склад
	|	КОНЕЦ КАК СкладПодразделение,
	|	ДанныеРегистра.ТипЗапасов КАК ТипЗапасов,
	|	ДанныеРегистра.ВидЗапасов КАК ВидЗапасов,
	|
	|	ДанныеРегистра.НаправлениеДеятельностиКонтрагента КАК НаправлениеДеятельностиКонтрагента,
	|	ДанныеРегистра.Партнер КАК Партнер,
	|	ДанныеРегистра.Контрагент КАК Контрагент,
	|	ДанныеРегистра.ВидЗапасов.Контрагент КАК Поставщик,
	|	ДанныеРегистра.Соглашение КАК Соглашение,
	|	ДанныеРегистра.Договор КАК Договор,
	|	ДанныеРегистра.Заказ КАК Заказ,
	|	ДанныеРегистра.Заказ.Подразделение КАК ЗаказПодразделение,
	|	ДанныеРегистра.ПодотчетноеЛицо КАК ПодотчетноеЛицо,
	|	
	|	ДанныеРегистра.Сумма КАК Сумма,
	|	ДанныеРегистра.СуммаБезНДС КАК СуммаБезНДС,
	|	ДанныеРегистра.Сумма - ДанныеРегистра.СуммаБезНДС КАК СуммаНДС,
	|	ДанныеРегистра.СуммаРегл КАК СуммаРегл,
	|	ДанныеРегистра.СуммаРеглБезНДС КАК СуммаРеглБезНДС,
	|	ДанныеРегистра.СуммаРегл - ДанныеРегистра.СуммаРеглБезНДС КАК СуммаНДСРегл,
	|
	|	ДанныеРегистра.Стоимость КАК Стоимость,
	|	ДанныеРегистра.СтоимостьБезНДС КАК СтоимостьБезНДС,
	|	ДанныеРегистра.СтоимостьРегл КАК СтоимостьРегл,
	|	ДанныеРегистра.СуммаДопРасходов КАК СуммаДопРасходов,
	|	ДанныеРегистра.СуммаДопРасходовБезНДС КАК СуммаДопРасходовБезНДС,
	|	ДанныеРегистра.СуммаДопРасходов - ДанныеРегистра.СуммаДопРасходовБезНДС КАК СуммаНДСДопРасходов,
	|
	|	ДанныеРегистра.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ДанныеРегистра.СуммаВВалютеВзаиморасчетов КАК СуммаВВалютеВзаиморасчетов,
	|	ДанныеРегистра.СуммаБезНДСВВалютеВзаиморасчетов КАК СуммаБезНДСВВалютеВзаиморасчетов,
	|	ДанныеРегистра.СуммаВВалютеВзаиморасчетов - ДанныеРегистра.СуммаБезНДСВВалютеВзаиморасчетов КАК СуммаНДСВВалютеВзаиморасчетов,
	|
	|	ДанныеРегистра.ВалютаДокумента КАК ВалютаДокумента,
	|	ДанныеРегистра.СуммаВВалютеДокумента КАК СуммаВВалютеДокумента,
	|	ДанныеРегистра.СуммаБезНДСВВалютеДокумента КАК СуммаБезНДСВВалютеДокумента,
	|	ДанныеРегистра.СуммаВВалютеДокумента - ДанныеРегистра.СуммаБезНДСВВалютеДокумента КАК СуммаНДСВВалютеДокумента,
	|
	|	ДанныеРегистра.ИсточникГФУНоменклатуры.ГруппаФинансовогоУчета КАК ГФУНоменклатуры,
	|	ДанныеРегистра.ИсточникГФУРасчетов.ГруппаФинансовогоУчета КАК ГФУРасчетов,
	|	ДокументыКОтражению.УчетнаяПолитика КАК УчетнаяПолитика
	|ИЗ
	|	РегистрНакопления.Закупки КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО
	|		ДанныеРегистра.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ДокументыКОтражению КАК ДокументыКОтражению
	|	ПО
	|		ДанныеРегистра.Регистратор = ДокументыКОтражению.Регистратор
	|		И ДанныеРегистра.Организация = ДокументыКОтражению.Организация
	|ГДЕ
	|	ДанныеРегистра.Активность
	|";
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаНДСЗаписиКнигиПокупок()

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) КАК ТипДокумента,
	|	ДанныеРегистра.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеРегистра.Организация КАК Организация,
	|
	|	ДанныеРегистра.Поставщик КАК Поставщик,
	|	ДанныеРегистра.СтавкаНДС КАК СтавкаНДС,
	|	ДанныеРегистра.ДокументОплаты,
	|	ДанныеРегистра.ДокументОплаты.Подразделение КАК ДокументОплатыПодразделение,
	|	ДанныеРегистра.ДоговорКонтрагента,
	|	ДанныеРегистра.ДоговорКонтрагента.Подразделение КАК ДоговорКонтрагентаПодразделение,
	|	
	|	ДанныеРегистра.НДС,
	|	ДокументыКОтражению.УчетнаяПолитика КАК УчетнаяПолитика
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ДокументыКОтражению КАК ДокументыКОтражению
	|	ПО
	|		ДанныеРегистра.Регистратор = ДокументыКОтражению.Регистратор
	|		И ДанныеРегистра.Организация = ДокументыКОтражению.Организация
	|ГДЕ
	|	ДанныеРегистра.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
	|	И ДанныеРегистра.Активность
	|";
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаНДСЗаписиКнигиПродаж()

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) КАК ТипДокумента,
	|	ДанныеРегистра.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.Покупатель КАК Покупатель,
	|	ДанныеРегистра.СтавкаНДС КАК СтавкаНДС,
	|	ДанныеРегистра.ДокументОплаты,
	|	ДанныеРегистра.ДокументОплаты.Подразделение КАК ДокументОплатыПодразделение,
	|	ДанныеРегистра.ДоговорКонтрагента,
	|	ДанныеРегистра.ДоговорКонтрагента.Подразделение КАК ДоговорКонтрагентаПодразделение,
	|	ДанныеРегистра.НДС,
	|	ДокументыКОтражению.УчетнаяПолитика КАК УчетнаяПолитика
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж КАК ДанныеРегистра
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтражению КАК ДокументыКОтражению
	|		ПО ДанныеРегистра.Регистратор = ДокументыКОтражению.Регистратор
	|			И ДанныеРегистра.Организация = ДокументыКОтражению.Организация
	|ГДЕ
	|	ДанныеРегистра.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
	|	И ДанныеРегистра.Активность";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРасчетыСКлиентамиПоДокументам()

	#Область ЗачетАвансаКлиента
	ТекстЗапросаЗачетАвансаКлиента = 
	"// ЗачетАвансаКлиента, ЗачетВознагражденияОплатойКомиссионера
	|ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) КАК ТипДокумента,
	|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) = ТИП(Документ.ОтчетКомиссионера)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗачетВознагражденияАвансомКомиссионера)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗачетАвансаКлиента)
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	Аналитика.Организация КАК Организация,
	|
	|	Аналитика.Партнер КАК Партнер,
	|	Аналитика.Контрагент КАК Контрагент,
	|	ДанныеРегистра.ЗаказКлиента КАК Заказ,
	|	ДанныеРегистра.ЗаказКлиента.Подразделение КАК Подразделение,
	|	ДанныеРегистра.ЗаказКлиента.НаправлениеДеятельности КАК НаправлениеДеятельностиКонтрагента,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДанныеРегистра.ЗаказКлиента) = ТИП(Справочник.ДоговорыКонтрагентов)
	|			ИЛИ ТИПЗНАЧЕНИЯ(ДанныеРегистра.ЗаказКлиента) = ТИП(Справочник.ДоговорыМеждуОрганизациями)
	|		ТОГДА
	|			ДанныеРегистра.ЗаказКлиента
	|		ИНАЧЕ
	|			ДанныеРегистра.ЗаказКлиента.Договор
	|	КОНЕЦ КАК Договор,
	|
	|	ДанныеРегистра.Валюта КАК Валюта,
	|
	|	ДанныеРегистра.Предоплата КАК Предоплата,
	|	ДанныеРегистра.ПредоплатаУпр КАК ПредоплатаУпр,
	|	ДанныеРегистра.ПредоплатаРегл КАК ПредоплатаРегл,
	|
	|	ДанныеРегистра.ЗаказКлиента.ГруппаФинансовогоУчета КАК ГФУРасчетов,
	|	ДокументыКОтражению.УчетнаяПолитика КАК УчетнаяПолитика
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|	ПО 
	|		ДанныеРегистра.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ДокументыКОтражению КАК ДокументыКОтражению
	|	ПО
	|		ДанныеРегистра.Регистратор = ДокументыКОтражению.Регистратор
	|		И Аналитика.Организация = ДокументыКОтражению.Организация
	|ГДЕ
	|	ДанныеРегистра.ПредоплатаРегл > 0 И ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И ДанныеРегистра.Активность";
	#КонецОбласти

	#Область ВосстановлениеАвансаКлиента
	ТекстЗапросаВосстановлениеАвансаКлиента = 
	"// ВосстановлениеАвансаКлиента
	|ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) КАК ТипДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВосстановлениеАвансаКлиента) КАК ХозяйственнаяОперация,
	|	Аналитика.Организация КАК Организация,
	|
	|	Аналитика.Партнер КАК Партнер,
	|	Аналитика.Контрагент КАК Контрагент,
	|	ДанныеРегистра.ЗаказКлиента КАК Заказ,
	|	ДанныеРегистра.ЗаказКлиента.Подразделение КАК Подразделение,
	|	ДанныеРегистра.ЗаказКлиента.НаправлениеДеятельности КАК НаправлениеДеятельностиКонтрагента,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДанныеРегистра.ЗаказКлиента) = ТИП(Справочник.ДоговорыКонтрагентов)
	|			ИЛИ ТИПЗНАЧЕНИЯ(ДанныеРегистра.ЗаказКлиента) = ТИП(Справочник.ДоговорыМеждуОрганизациями)
	|		ТОГДА
	|			ДанныеРегистра.ЗаказКлиента
	|		ИНАЧЕ
	|			ДанныеРегистра.ЗаказКлиента.Договор
	|	КОНЕЦ КАК Договор,
	|
	|	ДанныеРегистра.Валюта КАК Валюта,
	|
	|	ДанныеРегистра.Предоплата КАК Предоплата,
	|	ДанныеРегистра.ПредоплатаУпр КАК ПредоплатаУпр,
	|	ДанныеРегистра.ПредоплатаРегл КАК ПредоплатаРегл,
	|
	|	ДанныеРегистра.ЗаказКлиента.ГруппаФинансовогоУчета КАК ГФУРасчетов,
	|	ДокументыКОтражению.УчетнаяПолитика КАК УчетнаяПолитика
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|	ПО 
	|		ДанныеРегистра.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ДокументыКОтражению КАК ДокументыКОтражению
	|	ПО
	|		ДанныеРегистра.Регистратор = ДокументыКОтражению.Регистратор
	|		И Аналитика.Организация = ДокументыКОтражению.Организация
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) = ТИП(Документ.ВозвратТоваровОтКлиента)
	|	И ДанныеРегистра.ПредоплатаРегл > 0 И ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И ДанныеРегистра.Активность";
	#КонецОбласти

	#Область ЗачетВознагражденияОплатойКомиссионера
	ТекстЗапросаЗачетВознагражденияОплатойКомиссионера = 
	"// ЗачетВознагражденияОплатойКомиссионера
	|ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) КАК ТипДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗачетВознагражденияОплатойКомиссионера) КАК ХозяйственнаяОперация,
	|	Аналитика.Организация КАК Организация,
	|
	|	Аналитика.Партнер КАК Партнер,
	|	Аналитика.Контрагент КАК Контрагент,
	|	ДанныеРегистра.ЗаказКлиента КАК Заказ,
	|	ДанныеРегистра.ЗаказКлиента.Подразделение КАК Подразделение,
	|	ДанныеРегистра.ЗаказКлиента.НаправлениеДеятельности КАК НаправлениеДеятельностиКонтрагента,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДанныеРегистра.ЗаказКлиента) = ТИП(Справочник.ДоговорыКонтрагентов)
	|			ИЛИ ТИПЗНАЧЕНИЯ(ДанныеРегистра.ЗаказКлиента) = ТИП(Справочник.ДоговорыМеждуОрганизациями)
	|		ТОГДА
	|			ДанныеРегистра.ЗаказКлиента
	|		ИНАЧЕ
	|			ДанныеРегистра.ЗаказКлиента.Договор
	|	КОНЕЦ КАК Договор,
	|
	|	ДанныеРегистра.Валюта КАК Валюта,
	|
	|	ДанныеРегистра.Долг КАК Предоплата,
	|	ДанныеРегистра.ДолгУпр КАК ПредоплатаУпр,
	|	ДанныеРегистра.ДолгРегл КАК ПредоплатаРегл,
	|
	|	ДанныеРегистра.ЗаказКлиента.ГруппаФинансовогоУчета КАК ГФУРасчетов,
	|	ДокументыКОтражению.УчетнаяПолитика КАК УчетнаяПолитика
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|	ПО 
	|		ДанныеРегистра.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ДокументыКОтражению КАК ДокументыКОтражению
	|	ПО
	|		ДанныеРегистра.Регистратор = ДокументыКОтражению.Регистратор
	|		И Аналитика.Организация = ДокументыКОтражению.Организация
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор)  В 
	|		(ТИП(Документ.ОтчетПоКомиссииМеждуОрганизациями),
	|		ТИП(Документ.ОтчетКомиссионера))
	|	И ДанныеРегистра.ДолгРегл > 0 И ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И ДанныеРегистра.Активность";
	#КонецОбласти

	ТекстыТаблиц = Новый Массив;
	ТекстыТаблиц.Добавить(ТекстЗапросаЗачетАвансаКлиента);
	ТекстыТаблиц.Добавить(ТекстЗапросаВосстановлениеАвансаКлиента);
	ТекстыТаблиц.Добавить(ТекстЗапросаЗачетВознагражденияОплатойКомиссионера);
	
	РазделительЗапросов = "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|";
	
	ТекстЗапроса = СтрСоединить(ТекстыТаблиц, РазделительЗапросов);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаРасчетыСПоставщикамиПоДокументам()

	#Область ЗачетАвансаПоставщику
	
	ТекстЗапросаЗачетАвансаПоставщику = 
	"// ЗачетАвансаПоставщику
	|ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) КАК ТипДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗачетАвансаПоставщику) КАК ХозяйственнаяОперация,
	|	Аналитика.Организация КАК Организация,
	|
	|	Аналитика.Партнер КАК Партнер,
	|	Аналитика.Контрагент КАК Контрагент,
	|	ДанныеРегистра.ЗаказПоставщику КАК Заказ,
	|
	|	ДанныеРегистра.ЗаказПоставщику.Подразделение КАК Подразделение,
	|	ДанныеРегистра.ЗаказПоставщику.НаправлениеДеятельности КАК НаправлениеДеятельностиКонтрагента,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДанныеРегистра.ЗаказПоставщику) = ТИП(Справочник.ДоговорыКонтрагентов)
	|			ИЛИ ТИПЗНАЧЕНИЯ(ДанныеРегистра.ЗаказПоставщику) = ТИП(Справочник.ДоговорыМеждуОрганизациями)
	|		ТОГДА
	|			ДанныеРегистра.ЗаказПоставщику
	|		ИНАЧЕ
	|			ДанныеРегистра.ЗаказПоставщику.Договор
	|	КОНЕЦ КАК Договор,
	|
	|	ДанныеРегистра.Валюта КАК Валюта,
	|
	|	ДанныеРегистра.Предоплата КАК Предоплата,
	|	ДанныеРегистра.ПредоплатаУпр КАК ПредоплатаУпр,
	|	ДанныеРегистра.ПредоплатаРегл КАК ПредоплатаРегл,
	|
	|	ДанныеРегистра.ЗаказПоставщику.ГруппаФинансовогоУчета КАК ГФУРасчетов,
	|	ДокументыКОтражению.УчетнаяПолитика КАК УчетнаяПолитика
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|	ПО 
	|		ДанныеРегистра.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ДокументыКОтражению КАК ДокументыКОтражению
	|	ПО
	|		ДанныеРегистра.Регистратор = ДокументыКОтражению.Регистратор
	|		И Аналитика.Организация = ДокументыКОтражению.Организация
	|ГДЕ
	|	ДанныеРегистра.ПредоплатаРегл > 0 И ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И ДанныеРегистра.Активность
	|";
	#КонецОбласти

	#Область ПереносПретензииНаАвансы
	ТекстЗапросаПереносПретензииНаАвансы = 
	"// ПереносПретензииНаАвансы
	|ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) КАК ТипДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносПретензииНаАвансы) КАК ХозяйственнаяОперация,
	|	Аналитика.Организация КАК Организация,
	|
	|	Аналитика.Партнер КАК Партнер,
	|	Аналитика.Контрагент КАК Контрагент,
	|	ДанныеРегистра.ЗаказПоставщику КАК Заказ,
	|
	|	ДанныеРегистра.ЗаказПоставщику.Подразделение КАК Подразделение,
	|	ДанныеРегистра.ЗаказПоставщику.НаправлениеДеятельности КАК НаправлениеДеятельностиКонтрагента,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДанныеРегистра.ЗаказПоставщику) = ТИП(Справочник.ДоговорыКонтрагентов)
	|			ИЛИ ТИПЗНАЧЕНИЯ(ДанныеРегистра.ЗаказПоставщику) = ТИП(Справочник.ДоговорыМеждуОрганизациями)
	|		ТОГДА
	|			ДанныеРегистра.ЗаказПоставщику
	|		ИНАЧЕ
	|			ДанныеРегистра.ЗаказПоставщику.Договор
	|	КОНЕЦ КАК Договор,
	|
	|	ДанныеРегистра.Валюта КАК Валюта,
	|
	|	ДанныеРегистра.Предоплата КАК Предоплата,
	|	ДанныеРегистра.ПредоплатаУпр КАК ПредоплатаУпр,
	|	ДанныеРегистра.ПредоплатаРегл КАК ПредоплатаРегл,
	|
	|	ДанныеРегистра.ЗаказПоставщику.ГруппаФинансовогоУчета КАК ГФУРасчетов,
	|	ДокументыКОтражению.УчетнаяПолитика КАК УчетнаяПолитика
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|	ПО 
	|		ДанныеРегистра.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ДокументыКОтражению КАК ДокументыКОтражению
	|	ПО
	|		ДанныеРегистра.Регистратор = ДокументыКОтражению.Регистратор
	|		И Аналитика.Организация = ДокументыКОтражению.Организация
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) = ТИП(Документ.ВозвратТоваровПоставщику)
	|	И (ТИПЗНАЧЕНИЯ(ДанныеРегистра.ЗаказПоставщику) <> ТИП(Документ.ВозвратТоваровПоставщику)
	|		 ИЛИ ДанныеРегистра.ЗаказПоставщику = НЕОПРЕДЕЛЕНО)
	|	И ДанныеРегистра.ПредоплатаРегл > 0 И ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И ДанныеРегистра.Активность";
	#КонецОбласти

	#Область ПереносПретензииНаРасчеты
	ТекстЗапросаПереносПретензииНаРасчеты = 
	"// ПереносПретензииНаРасчеты
	|ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) КАК ТипДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносПретензииНаРасчеты) КАК ХозяйственнаяОперация,
	|	Аналитика.Организация КАК Организация,
	|
	|	Аналитика.Партнер КАК Партнер,
	|	Аналитика.Контрагент КАК Контрагент,
	|	ДанныеРегистра.ЗаказПоставщику КАК Заказ,
	|
	|	ДанныеРегистра.ЗаказПоставщику.Подразделение КАК Подразделение,
	|	ДанныеРегистра.ЗаказПоставщику.НаправлениеДеятельности КАК НаправлениеДеятельностиКонтрагента,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДанныеРегистра.ЗаказПоставщику) = ТИП(Справочник.ДоговорыКонтрагентов)
	|			ИЛИ ТИПЗНАЧЕНИЯ(ДанныеРегистра.ЗаказПоставщику) = ТИП(Справочник.ДоговорыМеждуОрганизациями)
	|		ТОГДА
	|			ДанныеРегистра.ЗаказПоставщику
	|		ИНАЧЕ
	|			ДанныеРегистра.ЗаказПоставщику.Договор
	|	КОНЕЦ КАК Договор,
	|
	|	ДанныеРегистра.Валюта КАК Валюта,
	|
	|	ДанныеРегистра.Предоплата КАК Предоплата,
	|	ДанныеРегистра.ПредоплатаУпр КАК ПредоплатаУпр,
	|	ДанныеРегистра.ПредоплатаРегл КАК ПредоплатаРегл,
	|
	|	ДанныеРегистра.ЗаказПоставщику.ГруппаФинансовогоУчета КАК ГФУРасчетов,
	|	ДокументыКОтражению.УчетнаяПолитика КАК УчетнаяПолитика
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|	ПО 
	|		ДанныеРегистра.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ДокументыКОтражению КАК ДокументыКОтражению
	|	ПО
	|		ДанныеРегистра.Регистратор = ДокументыКОтражению.Регистратор
	|		И Аналитика.Организация = ДокументыКОтражению.Организация
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) = ТИП(Документ.ВозвратТоваровПоставщику)
	|	И ДанныеРегистра.ДолгРегл > 0 И ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И ДанныеРегистра.Активность";
	#КонецОбласти

	#Область ЗачетВознагражденияОплатойКомитенту
	ТекстЗапросаЗачетВознагражденияОплатойКомитенту = 
	"// ЗачетВознагражденияОплатойКомитенту
	|ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) КАК ТипДокумента,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗачетВознагражденияОплатойКомитенту) КАК ХозяйственнаяОперация,
	|	Аналитика.Организация КАК Организация,
	|
	|	Аналитика.Партнер КАК Партнер,
	|	Аналитика.Контрагент КАК Контрагент,
	|	ДанныеРегистра.ЗаказПоставщику КАК Заказ,
	|
	|	ДанныеРегистра.ЗаказПоставщику.Подразделение КАК Подразделение,
	|	ДанныеРегистра.ЗаказПоставщику.НаправлениеДеятельности КАК НаправлениеДеятельностиКонтрагента,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ДанныеРегистра.ЗаказПоставщику) = ТИП(Справочник.ДоговорыКонтрагентов)
	|			ИЛИ ТИПЗНАЧЕНИЯ(ДанныеРегистра.ЗаказПоставщику) = ТИП(Справочник.ДоговорыМеждуОрганизациями)
	|		ТОГДА
	|			ДанныеРегистра.ЗаказПоставщику
	|		ИНАЧЕ
	|			ДанныеРегистра.ЗаказПоставщику.Договор
	|	КОНЕЦ КАК Договор,
	|
	|	ДанныеРегистра.Валюта КАК Валюта,
	|
	|	ДанныеРегистра.Долг КАК Предоплата,
	|	ДанныеРегистра.ДолгУпр КАК ПредоплатаУпр,
	|	ДанныеРегистра.ДолгРегл КАК ПредоплатаРегл,
	|
	|	ДанныеРегистра.ЗаказПоставщику.ГруппаФинансовогоУчета КАК ГФУРасчетов,
	|	ДокументыКОтражению.УчетнаяПолитика КАК УчетнаяПолитика
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоДокументам КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК Аналитика
	|	ПО 
	|		ДанныеРегистра.АналитикаУчетаПоПартнерам = Аналитика.КлючАналитики
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ДокументыКОтражению КАК ДокументыКОтражению
	|	ПО
	|		ДанныеРегистра.Регистратор = ДокументыКОтражению.Регистратор
	|		И Аналитика.Организация = ДокументыКОтражению.Организация
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДанныеРегистра.Регистратор) В 
	|		(ТИП(Документ.ОтчетПоКомиссииМеждуОрганизациями),
	|		ТИП(Документ.ОтчетКомитенту))
	|	И ДанныеРегистра.ДолгРегл > 0 И ДанныеРегистра.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И ДанныеРегистра.Активность";
	#КонецОбласти

	ТекстыТаблиц = Новый Массив;
	ТекстыТаблиц.Добавить(ТекстЗапросаЗачетАвансаПоставщику);
	ТекстыТаблиц.Добавить(ТекстЗапросаПереносПретензииНаАвансы);
	ТекстыТаблиц.Добавить(ТекстЗапросаПереносПретензииНаРасчеты);
	ТекстыТаблиц.Добавить(ТекстЗапросаЗачетВознагражденияОплатойКомитенту);

	РазделительЗапросов = "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|";
	
	ТекстЗапроса = СтрСоединить(ТекстыТаблиц, РазделительЗапросов);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ПроверитьКорректностьПоказателей(РезультатЗапроса, ИмяРегистра, ПараметрыРегистра)

	Показатели				= ПараметрыРегистра.Показатели;
	ПоказателиВВалюте		= ПараметрыРегистра.ПоказателиВВалюте;
	ИсточникиУточненияСчета = ПараметрыРегистра.ИсточникиУточненияСчета;
	ИсточникиПодразделений	= ПараметрыРегистра.ИсточникиПодразделений;
	ИсточникиСубконто		= ПараметрыРегистра.ИсточникиСубконто;
	
	КолонкиРезультата = РезультатЗапроса.Колонки;
	МассивОшибок = Новый Массив;
	Для каждого Показатель Из Показатели Цикл
		МассивРесурсов = Показатель.Значение.Ресурсы;
		Для Каждого Ресурс из МассивРесурсов Цикл
			ИмяРесурса = Ресурс.Имя;
			Если КолонкиРезультата.Найти(ИмяРесурса) = Неопределено Тогда
				ТекстОшибки = НСтр("ru = 'Не найден ресурс ""%ИмяРесурса%"" показателя ""%Показатель%"".'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ИмяРесурса%", ИмяРесурса);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Показатель%", Показатель.Ключ);
				МассивОшибок.Добавить(ТекстОшибки);
			КонецЕсли;
			ИсточникВалюты = Ресурс.ИсточникВалюты;
			Если ИсточникВалюты <> "ВалютаУпр" И ИсточникВалюты <> "ВалютаРегл"
				И КолонкиРезультата.Найти(ИсточникВалюты) = Неопределено Тогда
				ТекстОшибки = НСтр("ru = 'Не найден источник валюты ""%ИсточникВалюты%"" показателя ""%Показатель%"".'");
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ИсточникВалюты%", ИсточникВалюты);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Показатель%", Показатель.Ключ);
				МассивОшибок.Добавить(ТекстОшибки);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Для каждого Показатель Из ПоказателиВВалюте Цикл
		ИсточникВалюты = Показатель.Значение.ИсточникВалюты;
		Если ИсточникВалюты <> "ВалютаУпр" И ИсточникВалюты <> "ВалютаРегл"
			И КолонкиРезультата.Найти(ИсточникВалюты) = Неопределено Тогда
			ТекстОшибки = НСтр("ru = 'Не найден источник валюты ""%ИсточникВалюты%"" показателя в валюте ""%Показатель%"".'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ИсточникВалюты%", ИсточникВалюты);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Показатель%", Показатель.Ключ);
			МассивОшибок.Добавить(ТекстОшибки);
		КонецЕсли;
	КонецЦикла;
	
	Если МассивОшибок.Количество() > 0 Тогда
		ТекстОшибки = НСтр("ru = 'Обнаружены ошибки показателей регистра ""%ИмяРегистра%"":'");
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ИмяРегистра%", ИмяРегистра);
		Для каждого Ошибка Из МассивОшибок Цикл
			ТекстОшибки = ТекстОшибки + Символы.ПС + Ошибка;
		КонецЦикла;
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;

	Возврат Истина;

КонецФункции

Функция ТаблицаПроводок()

	ТаблицаПроводок = РегистрыБухгалтерии.Международный.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	ТаблицаПроводок.Колонки.Добавить("Статус", Новый ОписаниеТипов("ПеречислениеСсылка.СтатусыОтраженияВМеждународномУчете"));
	ТаблицаПроводок.Колонки.Добавить("Комментарий", Новый ОписаниеТипов("Строка"));
	ТаблицаПроводок.Колонки.Добавить("ВалютаХраненияСуммыПредставления", Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	ТаблицаПроводок.Колонки.Добавить("ВалютаХраненияСуммыФункциональной", Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	
	Возврат ТаблицаПроводок;
	
КонецФункции

Функция СтруктураПроводки()

	Проводка = Новый Структура;
	СтруктураНабораЗаписей = РегистрыБухгалтерии.Международный.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	Для каждого Колонка Из СтруктураНабораЗаписей.Колонки Цикл
		Проводка.Вставить(Колонка.Имя); 
	КонецЦикла;
	Проводка.Вставить("Статус");
	Проводка.Вставить("ШаблонПроводки");
	Проводка.Вставить("МассивОшибок", Новый Массив);
	Проводка.Вставить("ВалютаХраненияСуммыПредставления");
	Проводка.Вставить("ВалютаХраненияСуммыФункциональной");
	
	Возврат Проводка;

КонецФункции

Функция СчетУчета(Данные, ШаблонПроводки, ТаблицаУточненияСчетов, ИсточникиУточненияСчета, ВидДвижения);

	Если ВидДвижения = "Дт" Тогда
		ИсточникУточненияСчетаШаблонаПроводки = ШаблонПроводки.ИсточникУточненияСчетаДт;
		ВидДвижения = Перечисления.ВидыДвиженийБухгалтерии.Дебет;
		СчетПоУмолчанию = ШаблонПроводки.СчетДебетаПоУмолчанию;
	ИначеЕсли ВидДвижения = "Кт" Тогда
		ИсточникУточненияСчетаШаблонаПроводки = ШаблонПроводки.ИсточникУточненияСчетаКт;
		ВидДвижения = Перечисления.ВидыДвиженийБухгалтерии.Кредит;
		СчетПоУмолчанию = ШаблонПроводки.СчетКредитаПоУмолчанию;
	КонецЕсли;
	
	СчетУчета = СчетПоУмолчанию;
	Если ЗначениеЗаполнено(ИсточникУточненияСчетаШаблонаПроводки) Тогда
		ИсточникУточненияСчета = ИсточникиУточненияСчета.Получить(ИсточникУточненияСчетаШаблонаПроводки);
		ГруппаФинансовогоУчета = Данные[ИсточникУточненияСчета.ИмяПоля];
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ГруппаФинансовогоУчета", ГруппаФинансовогоУчета);
		ПараметрыОтбора.Вставить("ШаблонПроводки", ШаблонПроводки.ШаблонПроводки);
		ПараметрыОтбора.Вставить("ВидДвижения", ВидДвижения);
		МассивНайденныхСтрок = ТаблицаУточненияСчетов.НайтиСтроки(ПараметрыОтбора);
		Если МассивНайденныхСтрок.Количество() > 0 Тогда
			СчетУчета = МассивНайденныхСтрок[0].СчетУчета;
		КонецЕсли;
	КонецЕсли;

	Возврат СчетУчета;
	
КонецФункции

Функция БалансоваяСумма(Данные, ШаблонПроводки, РесурсыРегистра, ВалютыУчета, Валюта);

	Сумма = 0;
	ИмяРесурсаУпр = "";
	ИмяРесурсаРегл = "";
	РесурсВВалютеНайден = Ложь;
	
	Для каждого Ресурс Из РесурсыРегистра Цикл
		Если Ресурс.ИсточникВалюты = "ВалютаУпр" Тогда
			ИмяРесурсаУпр = Ресурс.Имя;
			ВалютаРесурса = ВалютыУчета.ВалютаУпр;
		ИначеЕсли Ресурс.ИсточникВалюты = "ВалютаРегл" Тогда
			ИмяРесурсаРегл = Ресурс.Имя;
			ВалютаРесурса = ВалютыУчета.ВалютаРегл;
		Иначе
			ВалютаРесурса = Данные[Ресурс.ИсточникВалюты];
		КонецЕсли;
		Если ВалютаРесурса = Валюта Тогда
			Сумма = Данные[Ресурс.Имя];
			РесурсВВалютеНайден = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Не РесурсВВалютеНайден Тогда
		Если ЗначениеЗаполнено(ИмяРесурсаУпр) Тогда
			Сумма = Данные[ИмяРесурсаУпр];
			ВалютаРесурса = ВалютыУчета.ВалютаУпр;
		ИначеЕсли ЗначениеЗаполнено(ИмяРесурсаРегл) Тогда
			Сумма = Данные[ИмяРесурсаРегл];
			ВалютаРесурса = ВалютыУчета.ВалютаРегл;
		КонецЕсли;
	КонецЕсли;

	Результат = Новый Структура;
	Результат.Вставить("Сумма", ?(ШаблонПроводки.ИзменятьЗнакОперации, -Сумма, Сумма));
	Результат.Вставить("Валюта", ВалютаРесурса);
	
	Возврат Результат;
	
КонецФункции

Функция СуммаВВалюте(Данные, ШаблонПроводки, РесурсыРегистра, ИсточникВалюты);

	Сумма = 0;
	Для каждого Ресурс Из РесурсыРегистра Цикл
		Если Ресурс.ИсточникВалюты = ИсточникВалюты Тогда
			Сумма = Данные[Ресурс.Имя];
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ?(ШаблонПроводки.ИзменятьЗнакОперации, -Сумма, Сумма);

КонецФункции

Функция ЗначениеИзмерения(Данные, ИсточникиИзмерения, ИсточникИзмеренияВШаблонеПроводки)

	ИсточникИзмерения = ИсточникиИзмерения.Получить(ИсточникИзмеренияВШаблонеПроводки);
	Если ИсточникИзмерения <> Неопределено Тогда
		Результат = Данные[ИсточникИзмерения];
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьСубконто(ВидДвижения, Параметры)
	
	ИмяРегистра = Параметры.ИмяРегистра;
	Данные = Параметры.Данные;
	Проводка = Параметры.Проводка;
	ИсточникиСубконто = Параметры.ИсточникиСубконто;
	СвойстваСчета = Параметры["СвойстваСчета" + ВидДвижения];
	ЗаполнениеСубконтоПоСоответствию = Проводка.ДанныеШаблонаПроводки.ЗаполнениеСубконтоПоСоответствию;
	
	Счет = Проводка["Счет" + ВидДвижения];
	ПоляИсточникаСубконто = ИсточникиСубконто["Субконто" + ВидДвижения];
	ДвижениеНоменклатуры = Проводка.ДанныеШаблонаПроводки["ДвижениеНоменклатуры"+ВидДвижения];
	
	ТипыЗаполненныхПолей = Новый Соответствие;
	ТипыДоступныхПолей = Новый Соответствие;
	Для каждого ПолеИсточникаСубконто из ПоляИсточникаСубконто Цикл
		Значение = Данные[ПолеИсточникаСубконто];
		Если Значение <> Неопределено Тогда
			ТипыЗаполненныхПолей.Вставить(ПолеИсточникаСубконто, ТипЗнч(Значение));
		Иначе
			МассивТипов = МеждународныйУчетСерверПовтИсп.ТипыАтрибутаРегистра(ИмяРегистра, ПолеИсточникаСубконто);
			ТипыДоступныхПолей.Вставить(ПолеИсточникаСубконто, МассивТипов);
		КонецЕсли;
	КонецЦикла;
	
	// Субконто с типом Контрагент на номенклатурных счетах заполняем контрагентом из вида запасов
	Если НЕ ЗаполнениеСубконтоПоСоответствию Тогда
		Если (ИмяРегистра = "ВыручкаИСебестоимостьПродаж" ИЛИ ИмяРегистра = "Закупки"
			ИЛИ ИмяРегистра = "ДвиженияНоменклатураДоходыРасходы" ИЛИ ИмяРегистра = "ДвиженияНоменклатураНоменклатура") Тогда
			Если ДвижениеНоменклатуры Тогда// подменить значение поля "Контрагент"
				Если ЗначениеЗаполнено(Данные.Поставщик) И ТипыЗаполненныхПолей["Контрагент"] <> Неопределено Тогда
					ТипыЗаполненныхПолей.Удалить("Контрагент");
				КонецЕсли;
			Иначе// удалить поле "Поставщик"
				Если ТипыЗаполненныхПолей["Поставщик"] <> Неопределено Тогда
					ТипыЗаполненныхПолей.Удалить("Поставщик");
				КонецЕсли;
			КонецЕсли;// ДвижениеНоменклатуры = Истина
		КонецЕсли;// ИмяРегистра = "ВыручкаИСебестоимостьПродаж" ИЛИ ИмяРегистра = "Закупки"
	КонецЕсли;
	
	Для К = 1 По СвойстваСчета.КоличествоСубконто Цикл
		ВидСубконто = СвойстваСчета["ВидСубконто" + К];
		Проводка["ВидСубконто" + ВидДвижения + К] = ВидСубконто;
		ТипСубконто = СвойстваСчета["ВидСубконто" + К + "ТипЗначения"];
		ТипНайден = Ложь;
		Для каждого ТипПоля Из ТипыЗаполненныхПолей Цикл
			Если ТипСубконто.СодержитТип(ТипПоля.Значение) Тогда
				Проводка["Субконто" + ВидДвижения + К] = Данные[ТипПоля.Ключ];
				ТипНайден = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если Не ТипНайден Тогда
			Для каждого ТипПоля Из ТипыДоступныхПолей Цикл
				Для каждого Тип Из ТипПоля.Значение Цикл
					Если ТипСубконто.СодержитТип(Тип) Тогда
						Проводка["Субконто" + ВидДвижения + К] = Данные[ТипПоля.Ключ];
						ТипНайден = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				Если ТипНайден Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		Если Не ТипНайден Тогда
			ТекстОшибки = НСтр("ru = 'Не удалось заполнить субконто %ВидСубконто% для счета %Счет%'");
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ВидСубконто%", ВидСубконто);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Счет%", Счет);
			ЗаполнитьСтатусПроводки(Проводка, Перечисления.СтатусыОтраженияВМеждународномУчете.ОтсутствуютПравилаОтраженияВУчете);
			ДобавитьОшибкуПроводки(Проводка, ТекстОшибки);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеПроводки(Проводка, ШаблонПроводки, ПараметрыРегистра)

	Если Не ЗначениеЗаполнено(Проводка.СчетДт) Тогда
		ЗаполнитьСтатусПроводки(Проводка, Перечисления.СтатусыОтраженияВМеждународномУчете.ОтсутствуютПравилаОтраженияВУчете);
		ТекстОшибки = НСтр("ru = 'Не удалось заполнить счет Дт.'");
		ДобавитьОшибкуПроводки(Проводка, ТекстОшибки);
	КонецЕсли;

	Если Не ЗначениеЗаполнено(Проводка.СчетКт) Тогда
		ЗаполнитьСтатусПроводки(Проводка, Перечисления.СтатусыОтраженияВМеждународномУчете.ОтсутствуютПравилаОтраженияВУчете);
		ТекстОшибки = НСтр("ru = 'Не удалось заполнить счет Кт.'");
		ДобавитьОшибкуПроводки(Проводка, ТекстОшибки);
	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьОшибкуПроводки(Проводка, ТекстОшибки)

	Проводка.МассивОшибок.Добавить(ТекстОшибки);

КонецПроцедуры

Процедура ДобавитьПроводкуВТаблицуПроводок(ТаблицаПроводок, Проводка)

	СтрокаТаблицы = ТаблицаПроводок.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Проводка);
	
	ЗаполнитьСтатусВТаблицеПроводок(СтрокаТаблицы, Проводка);
	
	ЗаполнитьКомментарийВТаблицеПроводок(СтрокаТаблицы, Проводка);

КонецПроцедуры

Процедура ЗаполнитьСтатусВТаблицеПроводок(СтрокаТаблицы, Проводка)

	Если ЗначениеЗаполнено(СтрокаТаблицы.Статус) Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоОшибок = Проводка.МассивОшибок.Количество();
	Если КоличествоОшибок > 0 Тогда
		СтрокаТаблицы.Статус = Проводка.Статус;
	Иначе
		СтрокаТаблицы.Статус = Перечисления.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчете;
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьСтатусПроводки(Проводка, Статус)

	Проводка.Статус = МеждународныйУчетСерверПовтИсп.Статус(Проводка.Статус, Статус);

КонецПроцедуры

Процедура ЗаполнитьКомментарийВТаблицеПроводок(СтрокаТаблицы, Проводка)

	МассивОшибок = Проводка.МассивОшибок;
	КоличествоОшибок = МассивОшибок.Количество();
	Если КоличествоОшибок > 0 Тогда
		
		Если ЗначениеЗаполнено(Проводка.ШаблонПроводки) Тогда
			ТекстШаблонПроводки = НСтр("ru = 'Шаблон проводки ""%ШаблонПроводки%""'") + Символы.ПС;
			ТекстШаблонПроводки = СтрЗаменить(ТекстШаблонПроводки, "%ШаблонПроводки%", Проводка.ШаблонПроводки);
			ТекстКоличествоОшибок = НСтр("ru = 'Обнаружены ошибки (%КоличествоОшибок%):'");
			ТекстКоличествоОшибок = СтрЗаменить(ТекстКоличествоОшибок, "%КоличествоОшибок%", КоличествоОшибок);
			ОписаниеОшибок = ТекстШаблонПроводки + ТекстКоличествоОшибок;
			Для К = 1 По КоличествоОшибок Цикл
				ОписаниеОшибки = НСтр("ru = '%НомерОшибки%. %ТекстОшибки%'");
				ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%НомерОшибки%", К);
				ОписаниеОшибки = СтрЗаменить(ОписаниеОшибки, "%ТекстОшибки%", МассивОшибок[К - 1]);
				ОписаниеОшибок = ОписаниеОшибок + Символы.ПС + ОписаниеОшибки;
			КонецЦикла;
		Иначе
			ОписаниеОшибок = НСтр("ru = '%ТекстОшибки%'");
			ОписаниеОшибок = СтрЗаменить(ОписаниеОшибок, "%ТекстОшибки%", Проводка.МассивОшибок[0]);
		КонецЕсли;
		
		СтрокаТаблицы.Комментарий = ОписаниеОшибок;
		
	КонецЕсли;

КонецПроцедуры

Процедура ЗаписатьПроводки(ТаблицаПроводок, ДокументыКОтражениюВУчете)

	Пока ДокументыКОтражениюВУчете.Следующий() Цикл
		
		Регистратор = ДокументыКОтражениюВУчете.Документ;
		
		ПроводкиДокумента = ТаблицаПроводок.Скопировать(Новый Структура("Регистратор", Регистратор));
		ОбновитьСтатусОтраженияДокумента(ПроводкиДокумента, Регистратор);
		ЗаписываемыеПроводки = ЗаписываемыеПроводки(ПроводкиДокумента);
		
		НаборЗаписей = РегистрыБухгалтерии.Международный.СоздатьНаборЗаписей();
		НаборЗаписей.ДополнительныеСвойства.Вставить("ЗаписыватьПустойНабор", Истина);
		НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
		Для Каждого Проводка Из ЗаписываемыеПроводки Цикл
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Проводка);
			НаборЗаписей.УстановитьСубконто(НоваяЗапись, Проводка, "Дт");
			НаборЗаписей.УстановитьСубконто(НоваяЗапись, Проводка, "Кт");
		КонецЦикла;
		
		// Дозапишем проводки документа, которые не относятся к организации по которым были сформированы новые проводки.
		ПроводокиПоДругимОрганизациям = ДокументыКОтражениюВУчете.Выбрать();
		Пока ПроводокиПоДругимОрганизациям.Следующий() Цикл
			Если ЗначениеЗаполнено(ПроводокиПоДругимОрганизациям.Организация) Тогда
				НоваяПроводка = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяПроводка, ПроводокиПоДругимОрганизациям);
				НаборЗаписей.УстановитьСубконто(НоваяПроводка, ПроводокиПоДругимОрганизациям, "Дт");
				НаборЗаписей.УстановитьСубконто(НоваяПроводка, ПроводокиПоДругимОрганизациям, "Кт");
			КонецЕсли;
		КонецЦикла;
		НаборЗаписей.Записать();
		
	КонецЦикла;

КонецПроцедуры

Функция ИсключаемыеПроводки(ПроводкиДокумента)

	ИсключаемыеПроводки = Новый Массив;
	Для каждого Проводка Из ПроводкиДокумента Цикл
		Если Проводка.Сумма = 0 И Проводка.СуммаПредставления = 0 Тогда
			ИсключаемыеПроводки.Добавить(Проводка);
		КонецЕсли;
	КонецЦикла; 

	Возврат ИсключаемыеПроводки;

КонецФункции

Процедура ЗаполнитьКомментарий(Комментарий, Проводка, УникальныеЗначения)

	Если УникальныеЗначения[Проводка.Комментарий] = Неопределено Тогда
		Комментарий = ?(Не ПустаяСтрока(Комментарий), Комментарий + Символы.ПС, Комментарий);
		Комментарий = Комментарий + Проводка.Комментарий;
		УникальныеЗначения.Вставить(Проводка.Комментарий, Истина);
	КонецЕсли;

КонецПроцедуры

Процедура ПересчитатьТаблицуПроводокПоКурсу(ПараметрыОтражения, ТаблицаПроводок)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаПроводок.Период КАК Период,
	|	ТаблицаПроводок.Организация КАК Организация,
	|	&ТекстВыборкиОстальныхКолонок,
	|	ВЫБОР КОГДА ТаблицаПроводок.Сумма = НЕОПРЕДЕЛЕНО
	|		ТОГДА 0
	|		ИНАЧЕ ТаблицаПроводок.Сумма
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР КОГДА ТаблицаПроводок.СуммаПредставления = НЕОПРЕДЕЛЕНО
	|		ТОГДА 0
	|		ИНАЧЕ ТаблицаПроводок.СуммаПредставления
	|	КОНЕЦ КАК СуммаПредставления,
	|	ТаблицаПроводок.ВалютаХраненияСуммыФункциональной КАК ВалютаХраненияСуммыФункциональной,
	|	ТаблицаПроводок.ВалютаХраненияСуммыПредставления КАК ВалютаХраненияСуммыПредставления
	|ПОМЕСТИТЬ ТаблицаПроводок
	|ИЗ
	|	&ТаблицаПроводок КАК ТаблицаПроводок
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Период,
	|	ВалютаХраненияСуммыФункциональной,
	|	ВалютаХраненияСуммыПредставления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаПроводок.Период,
	|	ТаблицаПроводок.ВалютаХраненияСуммыФункциональной,
	|	ТаблицаПроводок.ВалютаХраненияСуммыПредставления
	|ПОМЕСТИТЬ Валюты
	|ИЗ
	|	ТаблицаПроводок КАК ТаблицаПроводок
	|ГДЕ
	|	(ТаблицаПроводок.ВалютаХраненияСуммыФункциональной <> &ФункциональнаяВалюта
	|			ИЛИ ТаблицаПроводок.ВалютаХраненияСуммыПредставления <> &ВалютаПредставления)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТаблицаПроводок.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсыВалют.Период КАК Период,
	|	КурсыВалют.Валюта КАК Валюта,
	|	МАКСИМУМ(КурсыВалют.ПериодКурса) КАК ПериодКурса
	|ПОМЕСТИТЬ ПериодыКурсовВалют
	|ИЗ
	|	(ВЫБРАТЬ
	|		Валюты.Период КАК Период,
	|		Валюты.ВалютаХраненияСуммыФункциональной КАК Валюта,
	|		КурсыВалют.Период КАК ПериодКурса
	|	ИЗ
	|		Валюты КАК Валюты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
	|			ПО Валюты.Период >= КурсыВалют.Период
	|				И (КурсыВалют.Валюта = Валюты.ВалютаХраненияСуммыФункциональной)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Валюты.Период,
	|		Валюты.ВалютаХраненияСуммыПредставления,
	|		КурсыВалют.Период
	|	ИЗ
	|		Валюты КАК Валюты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
	|			ПО Валюты.Период >= КурсыВалют.Период
	|				И (КурсыВалют.Валюта = Валюты.ВалютаХраненияСуммыПредставления)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Валюты.Период,
	|		&ФункциональнаяВалюта,
	|		КурсыВалют.Период
	|	ИЗ
	|		Валюты КАК Валюты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
	|			ПО Валюты.Период >= КурсыВалют.Период
	|				И (КурсыВалют.Валюта = &ФункциональнаяВалюта)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Валюты.Период,
	|		&ВалютаПредставления,
	|		КурсыВалют.Период
	|	ИЗ
	|		Валюты КАК Валюты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
	|			ПО Валюты.Период >= КурсыВалют.Период
	|				И (КурсыВалют.Валюта = &ВалютаПредставления)) КАК КурсыВалют
	|
	|СГРУППИРОВАТЬ ПО
	|	КурсыВалют.Период,
	|	КурсыВалют.Валюта
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КурсыВалют.Период,
	|	КурсыВалют.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыКурсовВалют.Период КАК Период,
	|	ПериодыКурсовВалют.Валюта КАК Валюта,
	|	КурсыВалют.Курс / КурсыВалют.Кратность КАК Курс
	|ПОМЕСТИТЬ КурсыВалют
	|ИЗ
	|	ПериодыКурсовВалют КАК ПериодыКурсовВалют
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
	|		ПО ПериодыКурсовВалют.ПериодКурса = КурсыВалют.Период
	|			И ПериодыКурсовВалют.Валюта = КурсыВалют.Валюта
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПериодыКурсовВалют.Период,
	|	ПериодыКурсовВалют.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Валюты.Период,
	|	Валюты.ВалютаХраненияСуммыФункциональной,
	|	Валюты.ВалютаХраненияСуммыПредставления,
	|	ЕСТЬNULL(КурсыВалютХраненияСуммыФункциональной.Курс, 1) / ЕСТЬNULL(КурсыФункциональнойВалюты.Курс, 1) КАК КоэффицентПересчетаСуммы,
	|	ЕСТЬNULL(КурсыВалютХраненияСуммыПредставления.Курс, 1) / ЕСТЬNULL(КурсыВалютыПредставления.Курс, 1) КАК КоэффицентПересчетаСуммыПредставления
	|ПОМЕСТИТЬ КоэффициентыПересчета
	|ИЗ
	|	Валюты КАК Валюты
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютХраненияСуммыФункциональной
	|		ПО Валюты.Период = КурсыВалютХраненияСуммыФункциональной.Период
	|			И Валюты.ВалютаХраненияСуммыФункциональной = КурсыВалютХраненияСуммыФункциональной.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютХраненияСуммыПредставления
	|		ПО Валюты.Период = КурсыВалютХраненияСуммыПредставления.Период
	|			И Валюты.ВалютаХраненияСуммыПредставления = КурсыВалютХраненияСуммыПредставления.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыФункциональнойВалюты
	|		ПО Валюты.Период = КурсыФункциональнойВалюты.Период
	|			И (&ФункциональнаяВалюта = КурсыФункциональнойВалюты.Валюта)
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютыПредставления
	|		ПО Валюты.Период = КурсыВалютыПредставления.Период
	|			И (&ВалютаПредставления = КурсыВалютыПредставления.Валюта)
	|ГДЕ
	|	(ЕСТЬNULL(КурсыВалютХраненияСуммыФункциональной.Курс, 1) / ЕСТЬNULL(КурсыФункциональнойВалюты.Курс, 1) <> 1
	|			ИЛИ ЕСТЬNULL(КурсыВалютХраненияСуммыПредставления.Курс, 1) / ЕСТЬNULL(КурсыВалютыПредставления.Курс, 1) <> 1)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПроводок.Период КАК Период,
	|	ТаблицаПроводок.Организация,
	|	&ТекстВыборкиОстальныхКолонок,
	|	ТаблицаПроводок.Сумма * ЕСТЬNULL(КоэффициентыПересчета.КоэффицентПересчетаСуммы, 1) КАК Сумма,
	|	ТаблицаПроводок.СуммаПредставления * ЕСТЬNULL(КоэффициентыПересчета.КоэффицентПересчетаСуммыПредставления, 1) КАК СуммаПредставления,
	|	ТаблицаПроводок.ВалютаХраненияСуммыФункциональной,
	|	ТаблицаПроводок.ВалютаХраненияСуммыПредставления
	|ИЗ
	|	ТаблицаПроводок КАК ТаблицаПроводок
	|		ЛЕВОЕ СОЕДИНЕНИЕ КоэффициентыПересчета КАК КоэффициентыПересчета
	|		ПО ТаблицаПроводок.Период = КоэффициентыПересчета.Период
	|			И ТаблицаПроводок.ВалютаХраненияСуммыФункциональной = КоэффициентыПересчета.ВалютаХраненияСуммыФункциональной
	|			И ТаблицаПроводок.ВалютаХраненияСуммыПредставления = КоэффициентыПересчета.ВалютаХраненияСуммыПредставления
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаПроводок.Организация,
	|	ТаблицаПроводок.Период";
	
	Запрос.УстановитьПараметр("ТаблицаПроводок", ТаблицаПроводок);
	Запрос.УстановитьПараметр("ФункциональнаяВалюта", ПараметрыОтражения.ВалютыУчета.ФункциональнаяВалюта);
	Запрос.УстановитьПараметр("ВалютаПредставления", ПараметрыОтражения.ВалютыУчета.ВалютаПредставления);
	
	ШаблонТекстаВыборкиКолонки = "	ТаблицаПроводок.%1,"+Символы.ПС;
	ТекстВыборкиОстальныхКолонок = "";
	СтруктураКолонокИсключений = Новый Структура("Период, Сумма, СуммаПредставления, Организация, ВалютаХраненияСуммыФункциональной, ВалютаХраненияСуммыПредставления, МоментВремени");
	Для каждого Колонка из ТаблицаПроводок.Колонки Цикл
		Если СтруктураКолонокИсключений.Свойство(Колонка.Имя) Тогда
			Продолжить;
		КонецЕсли;
		ТекстВыборкиОстальныхКолонок = ТекстВыборкиОстальныхКолонок + СтрШаблон(ШаблонТекстаВыборкиКолонки, Колонка.Имя);
	КонецЦикла;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстВыборкиОстальныхКолонок,", ТекстВыборкиОстальныхКолонок);
	
	ТаблицаПроводок = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

Процедура ВыполнитьОффлайновыеРасчеты(Организация = Неопределено, ДатаОкончания = Неопределено, ДокументыКОтражению = Неопределено)

	Если ДокументыКОтражению = Неопределено Тогда
		Если ЗначениеЗаполнено(ДатаОкончания) Тогда
			КонецРасчета = КонецМесяца(ДатаОкончания)+1;
		Иначе
			КонецРасчета = КонецМесяца(ТекущаяДатаСеанса())+1;
		КонецЕсли;
		АналитикиРасчета = РаспределениеВзаиморасчетов.АналитикиРасчета();
		Если Организация <> Неопределено Тогда
			МассивОрганизаций = Новый Массив;
			МассивОрганизаций.Добавить(Организация);
			АналитикиРасчета.Организации = МассивОрганизаций;
		КонецЕсли;
		РаспределениеВзаиморасчетов.РассчитатьВсе(КонецРасчета, АналитикиРасчета);
		Возврат;
	КонецЕсли;
	
	НачалоМесяца = НачалоМесяца(ДатаОкончания);
	КонецМесяца = КонецМесяца(ДатаОкончания);
	ОкончаниеРасчета = КонецМесяца(ДатаОкончания)+1;
	
	Если ТипЗнч(ДокументыКОтражению) = Тип("Массив") Тогда
		УчетНДСУТ.ОтразитьДокументыВУчетеНДС(КонецМесяца, ДокументыКОтражению);
	Иначе
		МассивДокументыКОтражению = Новый Массив;
		МассивДокументыКОтражению.Добавить(ДокументыКОтражению);
		УчетНДСУТ.ОтразитьДокументыВУчетеНДС(КонецМесяца, МассивДокументыКОтражению);
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками КАК Расчеты
	|ГДЕ
	|	Расчеты.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|	И Расчеты.Регистратор В (&ДокументыКОтражению)
	|	И Расчеты.Активность
	|");
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца);
	Запрос.УстановитьПараметр("ОкончаниеПериода", КонецМесяца);
	Запрос.УстановитьПараметр("ДокументыКОтражению", ДокументыКОтражению);
	МассивАналитикПоставщиков = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("АналитикаУчетаПоПартнерам");
	Если МассивАналитикПоставщиков.Количество() > 0 Тогда
		АналитикиРасчета = РаспределениеВзаиморасчетов.АналитикиРасчета();
		АналитикиРасчета.АналитикиУчетаПоПартнерам = МассивАналитикПоставщиков;
		РаспределениеВзаиморасчетов.РаспределитьВсеРасчетыСПоставщиками(ОкончаниеРасчета, АналитикиРасчета);
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами КАК Расчеты
	|ГДЕ
	|	Расчеты.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|	И Расчеты.Регистратор В (&ДокументыКОтражению)
	|	И Расчеты.Активность
	|");
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца);
	Запрос.УстановитьПараметр("ОкончаниеПериода", КонецМесяца);
	Запрос.УстановитьПараметр("ДокументыКОтражению", ДокументыКОтражению);
	МассивАналитикКлиентов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("АналитикаУчетаПоПартнерам");
	Если МассивАналитикКлиентов.Количество() > 0 Тогда
		АналитикиРасчета = РаспределениеВзаиморасчетов.АналитикиРасчета();
		АналитикиРасчета.АналитикиУчетаПоПартнерам = МассивАналитикКлиентов;
		РаспределениеВзаиморасчетов.РаспределитьВсеРасчетыСКлиентами(ОкончаниеРасчета, АналитикиРасчета);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РегистрацияДокументовКОтражениюВМеждународномУчете

Функция ТаблицаОтражениеДокументовВМеждународномУчете(Документ)

	ИмяДокумента = Документ.Ссылка.Метаданные().Имя;
	
	НаборЗаписей = РегистрыСведений.ОтражениеДокументовВМеждународномУчете.СоздатьНаборЗаписей();
	ИсходнаяТаблица = НаборЗаписей.ВыгрузитьКолонки("Период, Организация, ДатаОтражения");
	ИсходнаяТаблица.Колонки.Добавить("ХозяйственнаяОперация", Новый ОписаниеТипов("ПеречислениеСсылка.ХозяйственныеОперации"));
	
	МассивРегистров = РегистрыКОтражениюВМеждународномУчете();
	Для каждого ИмяРегистра Из МассивРегистров Цикл
		// Обрабатываем не все документы, являющиеся регистраторами для регистра.
		ДокументыКОтражению = МеждународныйУчетСерверПовтИсп.ДокументыКОтражениюВМеждународномУчете(ИмяРегистра);
		Если ДокументыКОтражению.Найти(ИмяДокумента) <> Неопределено Тогда
			ДобавитьНаборЗаписейВТаблицу(ИсходнаяТаблица, Документ, ИмяРегистра);
		КонецЕсли;
	КонецЦикла;
	
	МассивДокументов = ДокументыКОтражениюВМеждународномУчете();
	Если МассивДокументов.Найти(ИмяДокумента) <> Неопределено Тогда
		ДобавитьДокументКОтражениюВМеждународномУчете(ИсходнаяТаблица, Документ, ИмяДокумента);
	КонецЕсли;
	
	ИсходнаяТаблица.ЗаполнитьЗначения(Документ.Дата, "Период");
	ИсходнаяТаблица.Свернуть("Период, Организация, ДатаОтражения, ХозяйственнаяОперация");
	
	Возврат ИсходнаяТаблица;
	
КонецФункции

Процедура ДобавитьНаборЗаписейВТаблицу(ТаблицаНаборовЗаписей, Документ, ИмяРегистра)

	Перем ИзменилисьДанныеРегистра;
	
	Если ТипЗнч(Документ.Движения) = Тип("Структура") И НЕ Документ.Движения.Свойство(ИмяРегистра) Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = Документ.Движения[ИмяРегистра];
	
	Если НаборЗаписей.ДополнительныеСвойства.Свойство("ИзменилисьДанныеРегистра", ИзменилисьДанныеРегистра) И НЕ ИзменилисьДанныеРегистра Тогда
		Возврат;
	КонецЕсли;
	
	Если ИмяРегистра = "ВыручкаИСебестоимостьПродаж" Тогда
		ДобавитьНаборЗаписейПоРегиструВыручкаИСебестоимостьПродаж(ТаблицаНаборовЗаписей, Документ);
	Иначе
		Для каждого Запись Из НаборЗаписей Цикл
			НоваяЗапись = ТаблицаНаборовЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Запись);
			НоваяЗапись.ДатаОтражения = Запись.Период;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьНаборЗаписейПоРегиструВыручкаИСебестоимостьПродаж(ТаблицаНаборовЗаписей, Документ)

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеДвижений.Период КАК Период,
	|	ДанныеДвижений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|ПОМЕСТИТЬ ДанныеДвижений
	|ИЗ
	|	&ТаблицаДвижений КАК ДанныеДвижений
	|;
	|ВЫБРАТЬ
	|	ДанныеДвижений.Период КАК Период,
	|	АналитикаПартнеров.Организация КАК Организация,
	|	ДанныеДвижений.Период КАК ДатаОтражения
	|ИЗ
	|	ДанныеДвижений КАК ДанныеДвижений
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаПартнеров
	|	ПО
	|		ДанныеДвижений.АналитикаУчетаПоПартнерам = АналитикаПартнеров.КлючАналитики
	|";
	
	Запрос.УстановитьПараметр("ТаблицаДвижений", Документ.Движения.ВыручкаИСебестоимостьПродаж);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяЗапись = ТаблицаНаборовЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
	КонецЦикла;

КонецПроцедуры

Функция ДокументыКОтражениюВМеждународномУчете()
	
	МассивДокументов = Новый Массив();
	МассивДокументов.Добавить("ПринятиеКУчетуОСМеждународныйУчет");
	МассивДокументов.Добавить("ПринятиеКУчетуНМАМеждународныйУчет");
	
	Возврат МассивДокументов;
	
КонецФункции

Процедура ДобавитьДокументКОтражениюВМеждународномУчете(ТаблицаКОтражениюВМеждународномУчете, Документ, ИмяДокумента)

	ТаблицаДокумента = ТаблицаКОтражениюВМеждународномУчете.СкопироватьКолонки();
	Документы[ИмяДокумента].ЗаполнитьТаблицуДокументаКОтражениюВМеждународномУчете(ТаблицаДокумента, Документ);
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаДокумента, ТаблицаКОтражениюВМеждународномУчете);

КонецПроцедуры

Процедура ДобавитьУчетнуюПолитикуИСтатус(ТаблицаОтражения, Регистратор)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаОтражения.Период          КАК Период,
	|	ТаблицаОтражения.Организация     КАК Организация,
	|	ТаблицаОтражения.ДатаОтражения   КАК ДатаОтражения,
	|	ТаблицаОтражения.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|ПОМЕСТИТЬ ТаблицаДанные
	|ИЗ
	|	&ТаблицаОтражения КАК ТаблицаОтражения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДанные.Период КАК Период,
	|	ТаблицаДанные.Организация,
	|	ТаблицаДанные.ДатаОтражения КАК ДатаОтражения,
	|	ТаблицаДанные.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	МАКСИМУМ(УчетнаяПолитикаМФУ.Период) КАК Месяц
	|ПОМЕСТИТЬ ПериодыУчетнойПолитики
	|ИЗ
	|	ТаблицаДанные КАК ТаблицаДанные
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизацийДляМеждународногоУчета КАК УчетнаяПолитикаМФУ
	|	ПО ТаблицаДанные.Организация = УчетнаяПолитикаМФУ.Организация
	|		И ТаблицаДанные.ДатаОтражения >= УчетнаяПолитикаМФУ.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДанные.Период,
	|	ТаблицаДанные.Организация,
	|	ТаблицаДанные.ДатаОтражения,
	|	ТаблицаДанные.ХозяйственнаяОперация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыУчетнойПолитики.Период КАК Период,
	|	ПериодыУчетнойПолитики.Организация КАК Организация,
	|	ПериодыУчетнойПолитики.ДатаОтражения КАК ДатаОтражения,
	|	ПериодыУчетнойПолитики.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	УчетнаяПолитикаМФУ.УчетнаяПолитика КАК УчетнаяПолитика
	|ПОМЕСТИТЬ ИсходнаяТаблица
	|ИЗ
	|	ПериодыУчетнойПолитики КАК ПериодыУчетнойПолитики
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизацийДляМеждународногоУчета КАК УчетнаяПолитикаМФУ
	|	ПО ПериодыУчетнойПолитики.Организация = УчетнаяПолитикаМФУ.Организация
	|		И ПериодыУчетнойПолитики.Месяц = УчетнаяПолитикаМФУ.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкиХозяйственныхОпераций.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ПравилаОтражения.УчетнаяПолитика КАК УчетнаяПолитика,
	|	МАКСИМУМ(ШаблоныПроводок.РучноеУточнениеПроводки) КАК РучноеУточнениеПроводки
	|ПОМЕСТИТЬ ПравилаОтражения
	|ИЗ
	|	Справочник.ШаблоныПроводокДляМеждународногоУчета КАК ШаблоныПроводок
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.НастройкиХозяйственныхОпераций КАК НастройкиХозяйственныхОпераций
	|	ПО
	|		ШаблоныПроводок.Операция = НастройкиХозяйственныхОпераций.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ПравилаОтраженияВМеждународномУчете КАК ПравилаОтражения
	|	ПО
	|		ШаблоныПроводок.Ссылка = ПравилаОтражения.ШаблонПроводки
	|СГРУППИРОВАТЬ ПО
	|	НастройкиХозяйственныхОпераций.ХозяйственнаяОперация,
	|	ПравилаОтражения.УчетнаяПолитика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаКОтражению.Период КАК Период,
	|	ТаблицаКОтражению.Организация КАК Организация,
	|	ТаблицаКОтражению.ДатаОтражения КАК ДатаОтражения,
	|	ТаблицаКОтражению.УчетнаяПолитика КАК УчетнаяПолитика,
	|	ВЫБОР
	|		КОГДА ТаблицаКОтражению.Статус = 1 ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчетеВручную)
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчете)
	|	КОНЕЦ КАК Статус
	|ИЗ
	|	(ВЫБРАТЬ
	|		ИсходнаяТаблица.Период КАК Период,
	|		ИсходнаяТаблица.Организация КАК Организация,
	|		ИсходнаяТаблица.ДатаОтражения КАК ДатаОтражения,
	|		ИсходнаяТаблица.УчетнаяПолитика КАК УчетнаяПолитика,
	|		МИНИМУМ (ВЫБОР
	|			КОГДА ОтражениеДокументов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчетеВручную)
	|				ИЛИ ОтражениеДокументов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчетеВручную)
	|				ИЛИ ПравилаОтражения.РучноеУточнениеПроводки
	|				ТОГДА 1
	|			ИНАЧЕ 2
	|		КОНЕЦ) КАК Статус
	|	ИЗ
	|		ИсходнаяТаблица КАК ИсходнаяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			ПравилаОтражения КАК ПравилаОтражения
	|		ПО
	|			ИсходнаяТаблица.УчетнаяПолитика = ПравилаОтражения.УчетнаяПолитика
	|			И ИсходнаяТаблица.ХозяйственнаяОперация = ПравилаОтражения.ХозяйственнаяОперация
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК ОтражениеДокументов
	|		ПО
	|			ИсходнаяТаблица.Организация = ОтражениеДокументов.Организация
	|			И ОтражениеДокументов.Регистратор = &Регистратор
	|
	|	СГРУППИРОВАТЬ ПО
	|		ИсходнаяТаблица.Период,
	|		ИсходнаяТаблица.Организация,
	|		ИсходнаяТаблица.ДатаОтражения,
	|		ИсходнаяТаблица.УчетнаяПолитика
	|	) КАК ТаблицаКОтражению
	|");
	
	Запрос.УстановитьПараметр("ТаблицаОтражения", ТаблицаОтражения);
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	ТаблицаОтражения = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

Функция ИнициализироватьМенеджерДляВозвратаКОтражению(ДокументыКОтражению)
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Если ТипЗнч(ДокументыКОтражению) = Тип("ТаблицаЗначений") Тогда
		Если ДокументыКОтражению.Количество() = 0 Тогда
			Возврат Неопределено;
		КонецЕсли;
		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДокументыКОтражению.Период КАК Период,
		|	ДокументыКОтражению.Регистратор КАК Регистратор,
		|	ДокументыКОтражению.Организация КАК Организация,
		|	ДокументыКОтражению.ДатаОтражения КАК ДатаОтражения
		|ПОМЕСТИТЬ ДокументыКОтражению
		|ИЗ
		|	&ДокументыКОтражению КАК ДокументыКОтражению
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Период,
		|	Регистратор,
		|	Организация,
		|	ДатаОтражения
		|;
		|/////////////////////////////////////////////////
		|");
		Запрос.УстановитьПараметр("ДокументыКОтражению", ДокументыКОтражению);
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.Выполнить();
	Иначе
		МенеджерВременныхТаблиц = ДокументыКОтражению;
	КонецЕсли;
	
	Возврат МенеджерВременныхТаблиц;
	
КонецФункции

Функция ДанныеДляВозвратаКОтражениюВУчете(ДокументыКОтражению, Отбор)

	#Область ТекстЗапросаТаблицаОтраженияДокументовВМеждународномУчете
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	ДокументыКОтражению.Период КАК Период,
	|	ДокументыКОтражению.Регистратор КАК Регистратор,
	|	ДокументыКОтражению.Организация КАК Организация,
	|	ДокументыКОтражению.ДатаОтражения КАК ДатаОтражения
	|ПОМЕСТИТЬ ТолькоДокументыКОтражению
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДокументыКОтражению.Регистратор) <> ТИП(Документ.КорректировкаРегистров)
	|	И ДокументыКОтражению.Период >= &Период
	|	И (ДокументыКОтражению.Регистратор > &Регистратор ИЛИ &Регистратор = Неопределено)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период,
	|	Регистратор
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Регистратор,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.ДатаОтражения КАК ДатаОтражения,
	|	ДанныеРегистра.Статус,
	|	ДанныеРегистра.УчетнаяПолитика,
	|	ДанныеРегистра.Комментарий
	|ПОМЕСТИТЬ ВсеЗаписиРегистратора
	|ИЗ
	|	ТолькоДокументыКОтражению КАК ДокументыКОтражению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК ДанныеРегистра
	|		ПО ДокументыКОтражению.Регистратор = ДанныеРегистра.Регистратор
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Регистратор,
	|	Организация,
	|	ДатаОтражения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистра.Период КАК Период,
	|	ДанныеРегистра.Регистратор КАК Регистратор,
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.ДатаОтражения КАК ДатаОтражения,
	|	ДанныеРегистра.Статус КАК Статус,
	|	ДанныеРегистра.УчетнаяПолитика,
	|	ДанныеРегистра.Комментарий
	|ПОМЕСТИТЬ ВременнаяТаблицаРегистра
	|ИЗ
	|	ТолькоДокументыКОтражению КАК ДокументыКОтражению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК ДанныеРегистра
	|		ПО ДокументыКОтражению.Регистратор = ДанныеРегистра.Регистратор
	|		И ДокументыКОтражению.Организация = ДанныеРегистра.Организация
	|		И НАЧАЛОПЕРИОДА(ДокументыКОтражению.ДатаОтражения, ДЕНЬ) = НАЧАЛОПЕРИОДА(ДанныеРегистра.ДатаОтражения, ДЕНЬ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|//если регистрации небыло, то надо зарегистрировать
	|ВЫБРАТЬ
	|	ДокументыКОтражению.Период КАК Период,
	|	ДокументыКОтражению.Регистратор КАК Регистратор,
	|	ДокументыКОтражению.Организация КАК Организация,
	|	ДокументыКОтражению.ДатаОтражения КАК ДатаОтражения,
	|	НЕОПРЕДЕЛЕНО КАК Статус,
	|	НЕОПРЕДЕЛЕНО КАК УчетнаяПолитика,
	|	"""" КАК Комментарий
	|ИЗ
	|	ТолькоДокументыКОтражению КАК ДокументыКОтражению
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК ДанныеРегистра
	|		ПО ДокументыКОтражению.Регистратор = ДанныеРегистра.Регистратор
	|		И ДокументыКОтражению.Организация = ДанныеРегистра.Организация
	|		И НАЧАЛОПЕРИОДА(ДокументыКОтражению.ДатаОтражения, ДЕНЬ) = НАЧАЛОПЕРИОДА(ДанныеРегистра.ДатаОтражения, ДЕНЬ)
	|ГДЕ
	|	ДанныеРегистра.Регистратор ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Регистратор,
	|	Организация,
	|	ДатаОтражения
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДокументыКОтражению.Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(ЕстьNULL(ДанныеРегистра.ДатаОтражения, ДокументыКОтражению.ДатаОтражения), МЕСЯЦ) КАК Период
	|ПОМЕСТИТЬ МесяцОтражения
	|ИЗ
	|	ТолькоДокументыКОтражению КАК ДокументыКОтражению
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаРегистра КАК ДанныеРегистра
	|	ПО ДокументыКОтражению.Организация = ДанныеРегистра.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МесяцОтражения.Организация КАК Организация,
	|	МАКСИМУМ(УчетнаяПолитикаОрганизаций.Период) КАК Период,
	|	МесяцОтражения.Период КАК ПериодРегистрации
	|ПОМЕСТИТЬ ПериодыУчетнойПолитики
	|ИЗ
	|	МесяцОтражения КАК МесяцОтражения
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизацийДляМеждународногоУчета КАК УчетнаяПолитикаОрганизаций
	|	ПО МесяцОтражения.Организация = УчетнаяПолитикаОрганизаций.Организация
	|		И МесяцОтражения.Период >= УчетнаяПолитикаОрганизаций.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	МесяцОтражения.Организация,
	|	МесяцОтражения.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УчетнаяПолитикаОрганизаций.Организация КАК Организация,
	|	ПериодыУчетнойПолитики.ПериодРегистрации КАК Период,
	|	УчетнаяПолитикаОрганизаций.УчетнаяПолитика КАК Политика
	|ПОМЕСТИТЬ УчетнаяПолитикаОрганизаций
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаОрганизацийДляМеждународногоУчета КАК УчетнаяПолитикаОрганизаций
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыУчетнойПолитики
	|	ПО УчетнаяПолитикаОрганизаций.Организация = ПериодыУчетнойПолитики.Организация
	|		И УчетнаяПолитикаОрганизаций.Период = ПериодыУчетнойПолитики.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтражениеДокументовВМеждународномУчете.Период КАК Период,
	|	ОтражениеДокументовВМеждународномУчете.Регистратор КАК Регистратор,
	|	ОтражениеДокументовВМеждународномУчете.Организация КАК Организация,
	|	ОтражениеДокументовВМеждународномУчете.ДатаОтражения КАК ДатаОтражения,
	|	ОтражениеДокументовВМеждународномУчете.Статус,
	|	ЕСТЬNULL(УчетнаяПолитикаОрганизаций.Политика, ОтражениеДокументовВМеждународномУчете.УчетнаяПолитика) КАК УчетнаяПолитика,
	|	ОтражениеДокументовВМеждународномУчете.Комментарий
	|ПОМЕСТИТЬ ТаблицаРегистра
	|ИЗ
	|	ВременнаяТаблицаРегистра КАК ОтражениеДокументовВМеждународномУчете
	|	ЛЕВОЕ СОЕДИНЕНИЕ УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|	ПО ОтражениеДокументовВМеждународномУчете.Организация = УчетнаяПолитикаОрганизаций.Организация
	|		И НАЧАЛОПЕРИОДА(ОтражениеДокументовВМеждународномУчете.ДатаОтражения, МЕСЯЦ) = УчетнаяПолитикаОрганизаций.Период
	|ГДЕ
	|	НЕ (УчетнаяПолитикаОрганизаций.Политика ЕСТЬ NULL
	|		ИЛИ ОтражениеДокументовВМеждународномУчете.УчетнаяПолитика = ЗНАЧЕНИЕ(Справочник.УчетныеПолитики.ПустаяСсылка))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Регистратор,
	|	Организация,
	|	ДатаОтражения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДокументыКОтражению.Регистратор КАК Регистратор,
	|	ДокументыКОтражению.Период КАК Период
	|ПОМЕСТИТЬ ПериодыРегистрации
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТолькоДокументыКОтражению КАК ТолькоДокументыКОтражению
	|	ПО ДокументыКОтражению.Период = ТолькоДокументыКОтражению.Период
	|		И ДокументыКОтражению.Регистратор = ТолькоДокументыКОтражению.Регистратор
	|		И ДокументыКОтражению.Организация = ТолькоДокументыКОтражению.Организация
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументыКОтражению.Период,
	|	ДокументыКОтражению.Регистратор
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ МесяцОтражения;
	|//////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПериодыУчетнойПолитики;
	|//////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВременнаяТаблицаРегистра;
	|
	|//////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтражениеДокументовВМеждународномУчете.Период КАК Период,
	|	ОтражениеДокументовВМеждународномУчете.Регистратор КАК Регистратор,
	|	ОтражениеДокументовВМеждународномУчете.Организация КАК Организация,
	|	ОтражениеДокументовВМеждународномУчете.ДатаОтражения КАК ДатаОтражения,
	|	ОтражениеДокументовВМеждународномУчете.Статус КАК Статус,
	|	ОтражениеДокументовВМеждународномУчете.Комментарий КАК Комментарий,
	|	ОтражениеДокументовВМеждународномУчете.УчетнаяПолитика КАК УчетнаяПолитика
	|ПОМЕСТИТЬ втОстальныеЗаписиРегистратора
	|ИЗ
	|	ВсеЗаписиРегистратора КАК ОтражениеДокументовВМеждународномУчете
	|	ЛЕВОЕ СОЕДИНЕНИЕ ТолькоДокументыКОтражению КАК ДокументыКОтражению
	|	ПО ДокументыКОтражению.Регистратор = ОтражениеДокументовВМеждународномУчете.Регистратор
	|		И ДокументыКОтражению.Организация = ОтражениеДокументовВМеждународномУчете.Организация
	|		И НАЧАЛОПЕРИОДА(ДокументыКОтражению.ДатаОтражения, ДЕНЬ) = ОтражениеДокументовВМеждународномУчете.ДатаОтражения
	|ГДЕ
	|	ДокументыКОтражению.Регистратор ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтражениеДокументовВМеждународномУчете.Период         КАК Период,
	|	ОтражениеДокументовВМеждународномУчете.Регистратор    КАК Регистратор,
	|	ОтражениеДокументовВМеждународномУчете.Организация    КАК Организация,
	|	ОтражениеДокументовВМеждународномУчете.ДатаОтражения  КАК ДатаОтражения,
	|	ВЫБОР 
	|		КОГДА НЕ ДокументыКОтражению.Организация ЕСТЬ NULL ТОГДА
	|			ВЫБОР КОГДА ОтражениеДокументовВМеждународномУчете.Статус В (
	|					ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчетеВручную),
	|					ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчетеВручную))
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчетеВручную)
	|				ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчете)
	|			КОНЕЦ
	|		ИНАЧЕ ОтражениеДокументовВМеждународномУчете.Статус
	|	КОНЕЦ КАК Статус,
	|	ВЫБОР
	|		КОГДА НЕ ДокументыКОтражению.Организация ЕСТЬ NULL ТОГДА
	|			ВЫБОР КОГДА ОтражениеДокументовВМеждународномУчете.Статус В (
	|					ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчетеВручную),
	|					ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчетеВручную))
	|				ТОГДА ОтражениеДокументовВМеждународномУчете.Комментарий
	|				ИНАЧЕ """"
	|			КОНЕЦ
	|		ИНАЧЕ ОтражениеДокументовВМеждународномУчете.Комментарий
	|	КОНЕЦ КАК Комментарий,
	|	ОтражениеДокументовВМеждународномУчете.УчетнаяПолитика КАК УчетнаяПолитика
	|ПОМЕСТИТЬ НовыеДанныеРегистра
	|ИЗ
	|	ТаблицаРегистра КАК ОтражениеДокументовВМеждународномУчете
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ДокументыКОтражению КАК ДокументыКОтражению
	|	ПО
	|		ОтражениеДокументовВМеждународномУчете.Период = ДокументыКОтражению.Период
	|		И ОтражениеДокументовВМеждународномУчете.ДатаОтражения = НАЧАЛОПЕРИОДА(ДокументыКОтражению.ДатаОтражения, ДЕНЬ)
	|		И ОтражениеДокументовВМеждународномУчете.Организация = ДокументыКОтражению.Организация
	|		И ОтражениеДокументовВМеждународномУчете.Регистратор = ДокументыКОтражению.Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтражениеДокументовВМеждународномУчете.Период КАК Период,
	|	ОтражениеДокументовВМеждународномУчете.Регистратор КАК Регистратор,
	|	ОтражениеДокументовВМеждународномУчете.Организация КАК Организация,
	|	ОтражениеДокументовВМеждународномУчете.ДатаОтражения КАК ДатаОтражения,
	|	ОтражениеДокументовВМеждународномУчете.Статус КАК Статус,
	|	ОтражениеДокументовВМеждународномУчете.Комментарий КАК Комментарий,
	|	ОтражениеДокументовВМеждународномУчете.УчетнаяПолитика КАК УчетнаяПолитика
	|ИЗ
	|	втОстальныеЗаписиРегистратора КАК ОтражениеДокументовВМеждународномУчете
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НовыеДанныеРегистра.Период КАК Период,
	|	НовыеДанныеРегистра.Регистратор КАК Регистратор
	|ИЗ
	|	НовыеДанныеРегистра КАК НовыеДанныеРегистра
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период,
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НовыеДанныеРегистра.Период КАК Период,
	|	НовыеДанныеРегистра.Регистратор КАК Регистратор,
	|	НовыеДанныеРегистра.Организация КАК Организация,
	|	НовыеДанныеРегистра.ДатаОтражения КАК ДатаОтражения,
	|	НовыеДанныеРегистра.Статус КАК Статус,
	|	НовыеДанныеРегистра.Комментарий КАК Комментарий,
	|	НовыеДанныеРегистра.УчетнаяПолитика КАК УчетнаяПолитика
	|ИЗ
	|	НовыеДанныеРегистра КАК НовыеДанныеРегистра
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период,
	|	Регистратор
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТолькоДокументыКОтражению;
	|//////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПериодыРегистрации;
	|//////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ УчетнаяПолитикаОрганизаций;
	|//////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НовыеДанныеРегистра;
	|//////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаРегистра;
	|//////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втОстальныеЗаписиРегистратора;
	|//////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВсеЗаписиРегистратора;
	|";
	#КонецОбласти
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДокументыКОтражению;
	Запрос.УстановитьПараметр("Период", Отбор.Период);
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ДанныеРегистра = РезультатЗапроса[14].Выгрузить();
	ДанныеРегистра.Индексы.Добавить("Период,Регистратор");
	
	Результат = Новый Структура("Регистраторы, ДанныеРегистра");
	Результат.Регистраторы = РезультатЗапроса[13].Выбрать();
	Результат.ДанныеРегистра = ДанныеРегистра;
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ОтражениеВМеждународномУчетеПоДаннымРегламентированного

Процедура ОтразитьПоДаннымРеглУчета(ПараметрыОтражения, ТаблицаПроводок)
	
	Если Не ПолучитьФункциональнуюОпцию("ФормироватьПроводкиМеждународногоУчетаПоДаннымРегламентированного") Тогда
		Возврат;
	КонецЕсли;
	
	#Область ТекстЗапросаПроводокРеглУчетаКОтражениуюВМеждународномУчете
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДокументыКОтражению.Период КАК Период,
	|	ДокументыКОтражению.Регистратор КАК Регистратор,
	|	ДокументыКОтражению.Организация КАК Организация,
	|	ДокументыКОтражению.УчетнаяПолитика КАК УчетнаяПолитика,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЕСТЬNULL(ОтражениеДокументовВРеглУчете.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете)) В (
	|				ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете),
	|				ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВУчетеВручную))
	|			ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ОтраженВРеглУчете
	|ПОМЕСТИТЬ ДокументыКОтражениюПоДаннымРегл
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
	|		ПО 
	|			ДокументыКОтражению.Организация = ОтражениеДокументовВРеглУчете.Организация
	|			И ДокументыКОтражению.Регистратор = ОтражениеДокументовВРеглУчете.Регистратор
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыКОтражению.Период,
	|	ДокументыКОтражению.Регистратор,
	|	ДокументыКОтражению.Организация,
	|	ДокументыКОтражению.УчетнаяПолитика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыКОтражению.Период КАК Период,
	|	ДокументыКОтражению.Регистратор КАК Регистратор,
	|	ДокументыКОтражению.Организация КАК Организация,
	|	ДокументыКОтражению.УчетнаяПолитика КАК УчетнаяПолитика
	|ИЗ
	|	ДокументыКОтражениюПоДаннымРегл КАК ДокументыКОтражению
	|ГДЕ
	|	НЕ ДокументыКОтражению.ОтраженВРеглУчете
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыКОтражению.Период КАК Период,
	|	ДокументыКОтражению.Регистратор КАК Регистратор,
	|	ДокументыКОтражению.Организация КАК Организация,
	|	ДокументыКОтражению.УчетнаяПолитика КАК УчетнаяПолитика
	|ИЗ
	|	ДокументыКОтражениюПоДаннымРегл КАК ДокументыКОтражению
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
	|	ПО
	|		ДокументыКОтражению.Регистратор = Хозрасчетный.Регистратор
	|ГДЕ
	|	ДокументыКОтражению.ОтраженВРеглУчете
	|	И Хозрасчетный.Регистратор ЕСТЬ NULL
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыКОтражению.Период КАК Период,
	|	ДокументыКОтражению.Регистратор КАК Регистратор,
	|	ДокументыКОтражению.Организация КАК Организация,
	|	ДокументыКОтражению.УчетнаяПолитика КАК УчетнаяПолитика
	|ПОМЕСТИТЬ ДокументыОтраженныеВРеглУчете
	|ИЗ
	|	ДокументыКОтражениюПоДаннымРегл КАК ДокументыКОтражению
	|ГДЕ
	|	ДокументыКОтражению.ОтраженВРеглУчете
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйДвиженияССубконто.Период,
	|	ХозрасчетныйДвиженияССубконто.Регистратор,
	|	ХозрасчетныйДвиженияССубконто.НомерСтроки,
	|	ХозрасчетныйДвиженияССубконто.Активность,
	|	ХозрасчетныйДвиженияССубконто.СчетДт,
	|	ХозрасчетныйДвиженияССубконто.СубконтоДт1,
	|	ХозрасчетныйДвиженияССубконто.ВидСубконтоДт1,
	|	ХозрасчетныйДвиженияССубконто.СубконтоДт2,
	|	ХозрасчетныйДвиженияССубконто.ВидСубконтоДт2,
	|	ХозрасчетныйДвиженияССубконто.СубконтоДт3,
	|	ХозрасчетныйДвиженияССубконто.ВидСубконтоДт3,
	|	ХозрасчетныйДвиженияССубконто.СчетКт,
	|	ХозрасчетныйДвиженияССубконто.СубконтоКт1,
	|	ХозрасчетныйДвиженияССубконто.ВидСубконтоКт1,
	|	ХозрасчетныйДвиженияССубконто.СубконтоКт2,
	|	ХозрасчетныйДвиженияССубконто.ВидСубконтоКт2,
	|	ХозрасчетныйДвиженияССубконто.СубконтоКт3,
	|	ХозрасчетныйДвиженияССубконто.ВидСубконтоКт3,
	|	ХозрасчетныйДвиженияССубконто.Организация,
	|	ХозрасчетныйДвиженияССубконто.ВалютаДт,
	|	ХозрасчетныйДвиженияССубконто.ВалютаКт,
	|	ХозрасчетныйДвиженияССубконто.ПодразделениеДт,
	|	ХозрасчетныйДвиженияССубконто.ПодразделениеКт,
	|	ХозрасчетныйДвиженияССубконто.НаправлениеДеятельностиДт,
	|	ХозрасчетныйДвиженияССубконто.НаправлениеДеятельностиКт,
	|	ХозрасчетныйДвиженияССубконто.Сумма,
	|	ХозрасчетныйДвиженияССубконто.ВалютнаяСуммаДт,
	|	ХозрасчетныйДвиженияССубконто.ВалютнаяСуммаКт,
	|	ХозрасчетныйДвиженияССубконто.КоличествоДт,
	|	ХозрасчетныйДвиженияССубконто.КоличествоКт
	|ПОМЕСТИТЬ ПроводкиРеглУчета
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(
	|			,
	|			,
	|			НЕ СчетДт = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|			И НЕ СчетКт = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) 
	|			И (Регистратор, Организация) В
	|				(ВЫБРАТЬ
	|					ДокументыОтраженныеВРеглУчете.Регистратор,
	|					ДокументыОтраженныеВРеглУчете.Организация
	|				ИЗ
	|					ДокументыОтраженныеВРеглУчете КАК ДокументыОтраженныеВРеглУчете),
	|			,
	|			) КАК ХозрасчетныйДвиженияССубконто
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	Период,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроводкиРеглУчета.Регистратор,
	|	ПроводкиРеглУчета.НомерСтроки,
	|	ПроводкиРеглУчета.Период,
	|	ПроводкиРеглУчета.Активность,
	|	ПроводкиРеглУчета.СчетДт,
	|	ПроводкиРеглУчета.СубконтоДт1,
	|	ПроводкиРеглУчета.ВидСубконтоДт1,
	|	ПроводкиРеглУчета.СубконтоДт2,
	|	ПроводкиРеглУчета.ВидСубконтоДт2,
	|	ПроводкиРеглУчета.СубконтоДт3,
	|	ПроводкиРеглУчета.ВидСубконтоДт3,
	|	ПроводкиРеглУчета.СчетКт,
	|	ПроводкиРеглУчета.СубконтоКт1,
	|	ПроводкиРеглУчета.ВидСубконтоКт1,
	|	ПроводкиРеглУчета.СубконтоКт2,
	|	ПроводкиРеглУчета.ВидСубконтоКт2,
	|	ПроводкиРеглУчета.СубконтоКт3,
	|	ПроводкиРеглУчета.ВидСубконтоКт3,
	|	ПроводкиРеглУчета.Организация,
	|	ПроводкиРеглУчета.ВалютаДт,
	|	ПроводкиРеглУчета.ВалютаКт,
	|	ПроводкиРеглУчета.ПодразделениеДт,
	|	ПроводкиРеглУчета.ПодразделениеКт,
	|	ПроводкиРеглУчета.НаправлениеДеятельностиДт,
	|	ПроводкиРеглУчета.НаправлениеДеятельностиКт,
	|	ПроводкиРеглУчета.Сумма,
	|	ПроводкиРеглУчета.Сумма КАК СуммаПредставления,
	|	&ВалютаРеглУчета КАК ВалютаХраненияСуммыФункциональной,
	|	&ВалютаРеглУчета КАК ВалютаХраненияСуммыПредставления,
	|	ПроводкиРеглУчета.ВалютнаяСуммаДт,
	|	ПроводкиРеглУчета.ВалютнаяСуммаКт,
	|	ПроводкиРеглУчета.КоличествоДт,
	|	ПроводкиРеглУчета.КоличествоКт,
	|	ДокументыОтраженныеВРеглУчете.УчетнаяПолитика
	|ИЗ
	|	ПроводкиРеглУчета КАК ПроводкиРеглУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДокументыОтраженныеВРеглУчете КАК ДокументыОтраженныеВРеглУчете
	|		ПО ПроводкиРеглУчета.Регистратор = ДокументыОтраженныеВРеглУчете.Регистратор
	|			И ПроводкиРеглУчета.Организация = ДокументыОтраженныеВРеглУчете.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПроводкиРеглУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДокументыКОтражениюПоДаннымРегл
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДокументыОтраженныеВРеглУчете
	|";
	#КонецОбласти
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ПараметрыОтражения.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ВалютаРеглУчета", ПараметрыОтражения.ВалютыУчета.ВалютаРегл);
	РезультатыЗапросов = Запрос.ВыполнитьПакет();
	
	ВыборкаДокументыОжидаютОтраженияВРеглУчете = РезультатыЗапросов[1].Выбрать();
	Пока ВыборкаДокументыОжидаютОтраженияВРеглУчете.Следующий() Цикл
		Проводка = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ПараметрыОтражения.СтруктураПроводки);
		ЗаполнитьЗначенияСвойств(Проводка, ВыборкаДокументыОжидаютОтраженияВРеглУчете);
		ЗаполнитьСтатусПроводки(Проводка, Перечисления.СтатусыОтраженияВМеждународномУчете.ОжидаетсяОтражениеВРеглУчете);
		ДобавитьОшибкуПроводки(Проводка, НСтр("ru = 'Для отражения в международном учете документ предварительно необходимо отразить в регл.'"));
		ДобавитьПроводкуВТаблицуПроводок(ТаблицаПроводок, Проводка);
	КонецЦикла;
	
	ВыборкаДокументыОтраженныеВРеглУчетеБезПроводок = РезультатыЗапросов[2].Выбрать();
	Пока ВыборкаДокументыОтраженныеВРеглУчетеБезПроводок.Следующий() Цикл
		Проводка = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ПараметрыОтражения.СтруктураПроводки);
		ЗаполнитьЗначенияСвойств(Проводка, ВыборкаДокументыОтраженныеВРеглУчетеБезПроводок);
		ЗаполнитьСтатусПроводки(Проводка, Перечисления.СтатусыОтраженияВМеждународномУчете.ОтраженоВУчете);
		ДобавитьПроводкуВТаблицуПроводок(ТаблицаПроводок, Проводка);
	КонецЦикла;

	ПроводкиРеглУчета = РезультатыЗапросов[5].Выгрузить();
	ПроводкиРеглУчета.Индексы.Добавить("Регистратор, НомерСтроки");

	Если ПроводкиРеглУчета.Количество() Тогда
		ТранслироватьПроводкиПоСоответствиямОборотов(ПроводкиРеглУчета, ТаблицаПроводок, ПараметрыОтражения.СтруктураПроводки);
	КонецЕсли;
	
	Если ПроводкиРеглУчета.Количество() Тогда
		ТранслироватьПроводкиПоСоответствиямСчетов(ПроводкиРеглУчета, ТаблицаПроводок, ПараметрыОтражения.СтруктураПроводки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ТранслироватьПроводкиПоСоответствиямОборотов(ПроводкиРеглУчета, ТаблицаПроводок, СтруктураПроводки)

	#Область ТекстЗапросаДанныеТрансляцииОборотовРеглУчета
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПроводкиРеглУчета.СчетДт,
	|	ПроводкиРеглУчета.СчетКт,
	|	ПроводкиРеглУчета.УчетнаяПолитика
	|ПОМЕСТИТЬ ПроводкиРеглУчета
	|ИЗ
	|	&ПроводкиРеглУчета КАК ПроводкиРеглУчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПроводкиРеглУчета.СчетДт,
	|	ПроводкиРеглУчета.СчетКт
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоответствиеОборотов.Ссылка,
	|	СоответствиеОборотов.ЭтоГруппа,
	|	СоответствиеОборотов.СчетРеглУчетаДт,
	|	СоответствиеОборотов.СчетРеглУчетаКт,
	|	СоответствиеОборотов.ДополнительныйОтбор,
	|	СоответствиеОборотов.СчетМеждународногоУчетаДт,
	|	ЕСТЬNULL(СоответствиеОборотов.СчетМеждународногоУчетаДт.Валютный, ЛОЖЬ) КАК СчетМеждународногоУчетаДтВалютный,
	|	ЕСТЬNULL(СоответствиеОборотов.СчетМеждународногоУчетаДт.УчетПоПодразделениям, ЛОЖЬ) КАК УчетПоПодразделениямДт,
	|	ЕСТЬNULL(СоответствиеОборотов.СчетМеждународногоУчетаДт.УчетПоНаправлениямДеятельности, ЛОЖЬ) КАК УчетПоНаправлениямДт,
	|	ВидыСубконтоДт1.ВидСубконто КАК ВидСубконтоДт1,
	|	ВидыСубконтоДт2.ВидСубконто КАК ВидСубконтоДт2,
	|	ВидыСубконтоДт3.ВидСубконто КАК ВидСубконтоДт3,
	|	СоответствиеОборотов.СчетМеждународногоУчетаКт,
	|	ЕСТЬNULL(СоответствиеОборотов.СчетМеждународногоУчетаКт.Валютный, ЛОЖЬ) КАК СчетМеждународногоУчетаКтВалютный,
	|	ЕСТЬNULL(СоответствиеОборотов.СчетМеждународногоУчетаКт.УчетПоПодразделениям, ЛОЖЬ) КАК УчетПоПодразделениямКт,
	|	ЕСТЬNULL(СоответствиеОборотов.СчетМеждународногоУчетаКт.УчетПоНаправлениямДеятельности, ЛОЖЬ) КАК УчетПоНаправлениямКт,
	|	ВидыСубконтоКт1.ВидСубконто КАК ВидСубконтоКт1,
	|	ВидыСубконтоКт2.ВидСубконто КАК ВидСубконтоКт2,
	|	ВидыСубконтоКт3.ВидСубконто КАК ВидСубконтоКт3,
	|	СоответствиеОборотов.УстановленДополнительныйОтбор,
	|	СоответствиеОборотов.ВариантСовместногоПрименения,
	|	СоответствиеОборотов.РучноеУточнениеПроводки,
	|	СоответствиеОборотов.НеОтражаетсяВМеждународномУчете,
	|	ПравилаОтраженияВМеждународномУчете.УчетнаяПолитика
	|ИЗ
	|	Справочник.СоответствияОборотовМеждународногоУчета КАК СоответствиеОборотов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПравилаОтраженияВМеждународномУчете КАК ПравилаОтраженияВМеждународномУчете
	|		ПО (ПравилаОтраженияВМеждународномУчете.ШаблонПроводки = СоответствиеОборотов.Ссылка)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК ВидыСубконтоДт1
	|		ПО СоответствиеОборотов.СчетМеждународногоУчетаДт = ВидыСубконтоДт1.Ссылка
	|			И (ВидыСубконтоДт1.НомерСтроки = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК ВидыСубконтоДт2
	|		ПО СоответствиеОборотов.СчетМеждународногоУчетаДт = ВидыСубконтоДт2.Ссылка
	|			И (ВидыСубконтоДт2.НомерСтроки = 2)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК ВидыСубконтоДт3
	|		ПО СоответствиеОборотов.СчетМеждународногоУчетаДт = ВидыСубконтоДт3.Ссылка
	|			И (ВидыСубконтоДт3.НомерСтроки = 3)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК ВидыСубконтоКт1
	|		ПО СоответствиеОборотов.СчетМеждународногоУчетаКт = ВидыСубконтоКт1.Ссылка
	|			И (ВидыСубконтоКт1.НомерСтроки = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК ВидыСубконтоКт2
	|		ПО СоответствиеОборотов.СчетМеждународногоУчетаКт = ВидыСубконтоКт2.Ссылка
	|			И (ВидыСубконтоКт2.НомерСтроки = 2)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК ВидыСубконтоКт3
	|		ПО СоответствиеОборотов.СчетМеждународногоУчетаКт = ВидыСубконтоКт3.Ссылка
	|			И (ВидыСубконтоКт3.НомерСтроки = 3)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПроводкиРеглУчета КАК ПроводкиРеглУчетаСчетПоДт
	|		ПО СоответствиеОборотов.СчетРеглУчетаДт = ПроводкиРеглУчетаСчетПоДт.СчетДт
	|			И (ПравилаОтраженияВМеждународномУчете.УчетнаяПолитика = ПроводкиРеглУчетаСчетПоДт.УчетнаяПолитика)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПроводкиРеглУчета КАК ПроводкиРеглУчетаСчетПоКт
	|		ПО СоответствиеОборотов.СчетРеглУчетаКт = ПроводкиРеглУчетаСчетПоКт.СчетКт
	|			И (ПравилаОтраженияВМеждународномУчете.УчетнаяПолитика = ПроводкиРеглУчетаСчетПоКт.УчетнаяПолитика)
	|ГДЕ
	|	НЕ СоответствиеОборотов.ПометкаУдаления
	|	И НЕ ПроводкиРеглУчетаСчетПоДт.СчетДт ЕСТЬ NULL
	|	И НЕ ПроводкиРеглУчетаСчетПоКт.СчетКт ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	СоответствиеОборотов.РеквизитДопУпорядочивания ИЕРАРХИЯ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкиЗаполненияСубконтоСоответствий.Ссылка КАК СоответствиеОборотов,
	|	НастройкиЗаполненияСубконтоСоответствий.ПоложениеСубконто,
	|	НастройкиЗаполненияСубконтоСоответствий.ВидСубконто,
	|	НастройкиЗаполненияСубконтоСоответствий.ЗаполнятьИзИсточника,
	|	НастройкиЗаполненияСубконтоСоответствий.УказанноеЗначение,
	|	НастройкиЗаполненияСубконтоСоответствий.Выражение
	|ИЗ
	|	Справочник.СоответствияОборотовМеждународногоУчета.НастройкиЗаполненияСубконто КАК НастройкиЗаполненияСубконтоСоответствий
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СоответствияОборотовМеждународногоУчета КАК СоответствияОборотов
	|		ПО НастройкиЗаполненияСубконтоСоответствий.Ссылка = СоответствияОборотов.Ссылка
	|			И (НЕ СоответствияОборотов.ПометкаУдаления)";
	#КонецОбласти
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ПроводкиРеглУчета", ПроводкиРеглУчета);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	СоответствияОборотов = РезультатЗапроса[1].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	НастройкиЗаполненияСубконто = РезультатЗапроса[2].Выгрузить();
	НастройкиЗаполненияСубконто.Индексы.Добавить("СоответствиеОборотов");
	
	ПараметрыТрансляции = Новый Структура;
	ПараметрыТрансляции.Вставить("СоответствияОборотов", СоответствияОборотов.Строки);
	ПараметрыТрансляции.Вставить("НастройкиЗаполненияСубконто", НастройкиЗаполненияСубконто);
	ПараметрыТрансляции.Вставить("ВариантСовместногоПрименения", Перечисления.ВариантыСовместногоПримененияШаблоновПроводок.Вытеснение);
	ПараметрыТрансляции.Вставить("ДополнительныйОтборГруппы", Неопределено);
	ПараметрыТрансляции.Вставить("СтруктураПроводки", СтруктураПроводки);
	
	СхемаКомпоновкиДанных = СхемаКомпоновкиДанныхРеглУчета(ПроводкиРеглУчета);
	
	ОбработанныеПроводки = Новый Соответствие;
	
	ТранслироватьПроводкиПоСоответствиямОборотовРекурсивно(
		СхемаКомпоновкиДанных,
		ПроводкиРеглУчета, 
		ПараметрыТрансляции,
		ТаблицаПроводок,
		ОбработанныеПроводки);

КонецПроцедуры

Процедура ТранслироватьПроводкиПоСоответствиямОборотовРекурсивно(СхемаКомпоновкиДанных, ПроводкиРеглУчета, ПараметрыТрансляции, ТаблицаПроводок, ОбработанныеПроводки)
	
	СоответствияОборотов = ПараметрыТрансляции.СоответствияОборотов;
	ВариантСовместногоПрименения = ПараметрыТрансляции.ВариантСовместногоПрименения;
	ДополнительныйОтборГруппы = ПараметрыТрансляции.ДополнительныйОтборГруппы;
	НастройкиЗаполненияСубконто = ПараметрыТрансляции.НастройкиЗаполненияСубконто;
	СтруктураПроводки = ПараметрыТрансляции.СтруктураПроводки;
	
	Для каждого СоответствиеОборотов Из СоответствияОборотов Цикл
		
		Если СоответствиеОборотов.ЭтоГруппа Тогда
			ВаринтСовместногоПримененияГруппы = СоответствиеОборотов.ВариантСовместногоПрименения;
			Если СоответствиеОборотов.УстановленДополнительныйОтбор Тогда
				ДополнительныйОтбор = Новый НастройкиКомпоновкиДанных;
				КомпоновкаДанныхКлиентСервер.СкопироватьОтборКомпоновкиДанных(
					СхемаКомпоновкиДанных, ДополнительныйОтбор, СоответствиеОборотов.ДополнительныйОтбор.Получить()); 
				Если ДополнительныйОтборГруппы <> Неопределено Тогда
					КомпоновкаДанныхКлиентСервер.СкопироватьОтборКомпоновкиДанных(
						СхемаКомпоновкиДанных, ДополнительныйОтбор, ДополнительныйОтборГруппы);
				КонецЕсли;
			КонецЕсли;
			ПараметрыТрансляцииГруппы = Новый Структура;
			ПараметрыТрансляцииГруппы.Вставить("СоответствияОборотов", СоответствиеОборотов.Строки);
			ПараметрыТрансляцииГруппы.Вставить("ВариантСовместногоПрименения", ВаринтСовместногоПримененияГруппы);
			ПараметрыТрансляцииГруппы.Вставить("ДополнительныйОтборГруппы", ДополнительныйОтбор);
			ПараметрыТрансляцииГруппы.Вставить("НастройкиЗаполненияСубконто", НастройкиЗаполненияСубконто);
			ПараметрыТрансляцииГруппы.Вставить("СтруктураПроводки", СтруктураПроводки);
			
			ТранслироватьПроводкиПоСоответствиямОборотовРекурсивно(
				СхемаКомпоновкиДанных, 
				ПроводкиРеглУчета, 
				ПараметрыТрансляцииГруппы, 
				ТаблицаПроводок,
				ОбработанныеПроводки);
		Иначе
			
			Настройки = Новый НастройкиКомпоновкиДанных;
			НоваяГруппировка = Настройки.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
			НоваяГруппировка.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
			НоваяГруппировка.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
			
			ФинансоваяОтчетностьСервер.НовоеПолеВыбора(Настройки, "Регистратор");
			ФинансоваяОтчетностьСервер.НовоеПолеВыбора(Настройки, "НомерСтроки");
			
			УстановитьЗаполнениеСубконтоПоСоответствиюОборотов(
				СхемаКомпоновкиДанных, 
				Настройки, 
				СоответствиеОборотов, 
				НастройкиЗаполненияСубконто);
				
			УстановитьОтборПроводокПоСоответствиюОборотов(
				СхемаКомпоновкиДанных, 
				Настройки, 
				СоответствиеОборотов, 
				ДополнительныйОтборГруппы);
			
			ВнешниеНаборы = Новый Структура;
			ВнешниеНаборы.Вставить("ТаблицаДанных", ПроводкиРеглУчета);
			
			РезультатТрансляцииОборота = 
				ФинансоваяОтчетностьСервер.ВыгрузитьРезультатСКД(СхемаКомпоновкиДанных, Настройки, ВнешниеНаборы);
				
			ИсключаемыеПоля = ИсключаемыеПоля(СоответствиеОборотов);
			Для каждого Строка Из РезультатТрансляцииОборота Цикл
				
				Отбор = Новый Структура;
				Отбор.Вставить("Регистратор", Строка.Регистратор);
				Отбор.Вставить("НомерСтроки", Строка.НомерСтроки);
				ПроводкаРеглУчета = ПроводкиРеглУчета.НайтиСтроки(Отбор)[0];
				
				ОбработанныеПроводки.Вставить(ПроводкаРеглУчета);
				
				Если Не СоответствиеОборотов.НеОтражаетсяВМеждународномУчете Тогда
					Проводка = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(СтруктураПроводки);
					
					ЗаполнитьЗначенияСвойств(Проводка, ПроводкаРеглУчета, , ИсключаемыеПоля);
					ЗаполнитьЗначенияСвойств(Проводка, СоответствиеОборотов);
					
					Проводка.СчетДт = СоответствиеОборотов.СчетМеждународногоУчетаДт;
					Проводка.СубконтоДт1 = Строка.СубконтоМеждународныйДт1;
					Проводка.СубконтоДт2 = Строка.СубконтоМеждународныйДт2;
					Проводка.СубконтоДт3 = Строка.СубконтоМеждународныйДт3;
					
					Проводка.СчетКт = СоответствиеОборотов.СчетМеждународногоУчетаКт;
					Проводка.СубконтоКт1 = Строка.СубконтоМеждународныйКт1;
					Проводка.СубконтоКт2 = Строка.СубконтоМеждународныйКт2;
					Проводка.СубконтоКт3 = Строка.СубконтоМеждународныйКт3;
					
					ЗаполнитьВалютнуюСумму(Проводка, 
						СоответствиеОборотов.СчетМеждународногоУчетаДтВалютный, 
						СоответствиеОборотов.СчетМеждународногоУчетаКтВалютный);
						
					Если СоответствиеОборотов.РучноеУточнениеПроводки Тогда
						ЗаполнитьСтатусПроводки(Проводка, 
							Перечисления.СтатусыОтраженияВМеждународномУчете.КОтражениюВУчетеВручную);
					КонецЕсли;
					
					ДобавитьПроводкуВТаблицуПроводок(ТаблицаПроводок, Проводка);
					
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;
		
		Если ВариантСовместногоПрименения = Перечисления.ВариантыСовместногоПримененияШаблоновПроводок.Вытеснение Тогда
			Для каждого Проводка Из ОбработанныеПроводки Цикл
				ПроводкиРеглУчета.Удалить(Проводка.Ключ);
			КонецЦикла;
			ОбработанныеПроводки.Очистить();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ТранслироватьПроводкиПоСоответствиямСчетов(ПроводкиРеглУчета, ТаблицаПроводок, СтруктураПроводки)

	#Область ТекстЗапросаДанныеТрансляцииСчетов
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПроводкиРеглУчета.Регистратор,
	|	ПроводкиРеглУчета.НомерСтроки,
	|	ПроводкиРеглУчета.Период,
	|	ПроводкиРеглУчета.Активность,
	|	ПроводкиРеглУчета.СчетДт,
	|	ПроводкиРеглУчета.СубконтоДт1,
	|	ПроводкиРеглУчета.ВидСубконтоДт1,
	|	ПроводкиРеглУчета.СубконтоДт2,
	|	ПроводкиРеглУчета.ВидСубконтоДт2,
	|	ПроводкиРеглУчета.СубконтоДт3,
	|	ПроводкиРеглУчета.ВидСубконтоДт3,
	|	ПроводкиРеглУчета.СчетКт,
	|	ПроводкиРеглУчета.СубконтоКт1,
	|	ПроводкиРеглУчета.ВидСубконтоКт1,
	|	ПроводкиРеглУчета.СубконтоКт2,
	|	ПроводкиРеглУчета.ВидСубконтоКт2,
	|	ПроводкиРеглУчета.СубконтоКт3,
	|	ПроводкиРеглУчета.ВидСубконтоКт3,
	|	ПроводкиРеглУчета.Организация,
	|	ПроводкиРеглУчета.ВалютаДт,
	|	ПроводкиРеглУчета.ВалютаКт,
	|	ПроводкиРеглУчета.ПодразделениеДт,
	|	ПроводкиРеглУчета.ПодразделениеКт,
	|	ПроводкиРеглУчета.НаправлениеДеятельностиДт,
	|	ПроводкиРеглУчета.НаправлениеДеятельностиКт,
	|	ПроводкиРеглУчета.Сумма,
	|	ПроводкиРеглУчета.Сумма КАК СуммаПредставления,
	|	ПроводкиРеглУчета.ВалютнаяСуммаДт,
	|	ПроводкиРеглУчета.ВалютнаяСуммаКт,
	|	ПроводкиРеглУчета.КоличествоДт,
	|	ПроводкиРеглУчета.КоличествоКт,
	|	ПроводкиРеглУчета.УчетнаяПолитика
	|ПОМЕСТИТЬ ПроводкиРеглУчета
	|ИЗ
	|	&ПроводкиРеглУчета КАК ПроводкиРеглУчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПроводкиРеглУчета.СчетДт,
	|	ПроводкиРеглУчета.СчетКт
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПроводкиРеглУчета.СчетДт,
	|	ПроводкиРеглУчета.СчетКт,
	|	ПроводкиРеглУчета.УчетнаяПолитика
	|ПОМЕСТИТЬ СчетаПроводокРеглУчета
	|ИЗ
	|	ПроводкиРеглУчета КАК ПроводкиРеглУчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПроводкиРеглУчета.СчетДт,
	|	ПроводкиРеглУчета.СчетКт
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СоответствияСчетов.Ссылка КАК Ссылка,
	|	СоответствияСчетов.СчетРеглУчета КАК СчетРеглУчета,
	|	СоответствияСчетов.СчетМеждународногоУчета КАК СчетМеждународногоУчета,
	|	СоответствияСчетов.СчетМеждународногоУчета.Валютный КАК СчетМеждународногоУчетаВалютный,
	|	СоответствияСчетов.СчетМеждународногоУчета.УчетПоПодразделениям КАК УчетПоПодразделениям,
	|	СоответствияСчетов.СчетМеждународногоУчета.УчетПоНаправлениямДеятельности КАК УчетПоНаправлениям,
	|	СоответствияСчетов.УстановленДополнительныйОтбор КАК УстановленДополнительныйОтбор,
	|	ПравилаОтраженияВУчете.УчетнаяПолитика КАК УчетнаяПолитика,
	|	ВЫБОР
	|		КОГДА ПроводкиРеглУчетаСчетПоДт.СчетДт ЕСТЬ НЕ NULL 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПрименятьДляСчетаДт,
	|	ВЫБОР
	|		КОГДА ПроводкиРеглУчетаСчетПоКт.СчетКт ЕСТЬ НЕ NULL 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПрименятьДляСчетаКт,
	|	СоответствияСчетов.РеквизитДопУпорядочивания
	|ПОМЕСТИТЬ ТаблицаСоответствияСчетов
	|ИЗ
	|	Справочник.СоответствияСчетовМеждународногоУчета КАК СоответствияСчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПравилаОтраженияВМеждународномУчете КАК ПравилаОтраженияВУчете
	|		ПО (ПравилаОтраженияВУчете.ШаблонПроводки = СоответствияСчетов.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаПроводокРеглУчета КАК ПроводкиРеглУчетаСчетПоДт
	|		ПО СоответствияСчетов.СчетРеглУчета = ПроводкиРеглУчетаСчетПоДт.СчетДт
	|			И (ПравилаОтраженияВУчете.УчетнаяПолитика = ПроводкиРеглУчетаСчетПоДт.УчетнаяПолитика)
	|		ЛЕВОЕ СОЕДИНЕНИЕ СчетаПроводокРеглУчета КАК ПроводкиРеглУчетаСчетПоКт
	|		ПО СоответствияСчетов.СчетРеглУчета = ПроводкиРеглУчетаСчетПоКт.СчетКт
	|			И (ПравилаОтраженияВУчете.УчетнаяПолитика = ПроводкиРеглУчетаСчетПоКт.УчетнаяПолитика)
	|ГДЕ
	|	НЕ СоответствияСчетов.ПометкаУдаления
	|	И (НЕ ПроводкиРеглУчетаСчетПоДт.СчетДт ЕСТЬ NULL 
	|			ИЛИ НЕ ПроводкиРеглУчетаСчетПоКт.СчетКт ЕСТЬ NULL )
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПроводкиРеглУчета.Регистратор,
	|	ПроводкиРеглУчета.НомерСтроки,
	|	ПроводкиРеглУчета.Период,
	|	ПроводкиРеглУчета.Активность,
	|	ПроводкиРеглУчета.СчетДт,
	|	ПроводкиРеглУчета.СубконтоДт1,
	|	ПроводкиРеглУчета.ВидСубконтоДт1,
	|	ПроводкиРеглУчета.СубконтоДт2,
	|	ПроводкиРеглУчета.ВидСубконтоДт2,
	|	ПроводкиРеглУчета.СубконтоДт3,
	|	ПроводкиРеглУчета.ВидСубконтоДт3,
	|	ПроводкиРеглУчета.СчетКт,
	|	ПроводкиРеглУчета.СубконтоКт1,
	|	ПроводкиРеглУчета.ВидСубконтоКт1,
	|	ПроводкиРеглУчета.СубконтоКт2,
	|	ПроводкиРеглУчета.ВидСубконтоКт2,
	|	ПроводкиРеглУчета.СубконтоКт3,
	|	ПроводкиРеглУчета.ВидСубконтоКт3,
	|	ПроводкиРеглУчета.Организация,
	|	ПроводкиРеглУчета.ВалютаДт,
	|	ПроводкиРеглУчета.ВалютаКт,
	|	ПроводкиРеглУчета.ПодразделениеДт,
	|	ПроводкиРеглУчета.ПодразделениеКт,
	|	ПроводкиРеглУчета.НаправлениеДеятельностиДт,
	|	ПроводкиРеглУчета.НаправлениеДеятельностиКт,
	|	ПроводкиРеглУчета.Сумма,
	|	ПроводкиРеглУчета.Сумма КАК СуммаПредставления,
	|	ПроводкиРеглУчета.ВалютнаяСуммаДт,
	|	ПроводкиРеглУчета.ВалютнаяСуммаКт,
	|	ПроводкиРеглУчета.КоличествоДт,
	|	ПроводкиРеглУчета.КоличествоКт,
	|	ПроводкиРеглУчета.УчетнаяПолитика,
	|	СоответствияСчетов.Ссылка КАК Соответствие
	|ИЗ
	|	ПроводкиРеглУчета КАК ПроводкиРеглУчета
	|	ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСоответствияСчетов КАК СоответствияСчетов
	|	ПО ПроводкиРеглУчета.СчетДт = СоответствияСчетов.СчетРеглУчета
	|		ИЛИ ПроводкиРеглУчета.СчетКт = СоответствияСчетов.СчетРеглУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкиЗаполненияСубконтоСоответствий.Ссылка КАК СоответствиеСчетов,
	|	НастройкиЗаполненияСубконтоСоответствий.ВидСубконто,
	|	НастройкиЗаполненияСубконтоСоответствий.ЗаполнятьИзИсточника,
	|	НастройкиЗаполненияСубконтоСоответствий.УказанноеЗначение,
	|	НастройкиЗаполненияСубконтоСоответствий.Выражение
	|ИЗ
	|	Справочник.СоответствияСчетовМеждународногоУчета.НастройкиЗаполненияСубконто КАК НастройкиЗаполненияСубконтоСоответствий
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СоответствияСчетовМеждународногоУчета КАК СоответствияСчетов
	|		ПО НастройкиЗаполненияСубконтоСоответствий.Ссылка = СоответствияСчетов.Ссылка
	|			И (НЕ СоответствияСчетов.ПометкаУдаления)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСоответствияСчетов.Ссылка,
	|	ТаблицаСоответствияСчетов.СчетРеглУчета,
	|	ТаблицаСоответствияСчетов.СчетМеждународногоУчета,
	|	ТаблицаСоответствияСчетов.СчетМеждународногоУчетаВалютный,
	|	ТаблицаСоответствияСчетов.УчетПоПодразделениям,
	|	ТаблицаСоответствияСчетов.УчетПоНаправлениям,
	|	ВидыСубконто1.ВидСубконто КАК ВидСубконто1,
	|	ВидыСубконто2.ВидСубконто КАК ВидСубконто2,
	|	ВидыСубконто3.ВидСубконто КАК ВидСубконто3,
	|	ТаблицаСоответствияСчетов.Ссылка.ДополнительныйОтбор КАК ДополнительныйОтбор,
	|	ТаблицаСоответствияСчетов.УстановленДополнительныйОтбор,
	|	ТаблицаСоответствияСчетов.УчетнаяПолитика,
	|	ТаблицаСоответствияСчетов.ПрименятьДляСчетаДт,
	|	ТаблицаСоответствияСчетов.ПрименятьДляСчетаКт,
	|	ТаблицаСоответствияСчетов.РеквизитДопУпорядочивания КАК Порядок,
	|	ВидыСубконто1Регл.ВидСубконто КАК ВидСубконто1Регл,
	|	ВидыСубконто2Регл.ВидСубконто КАК ВидСубконто2Регл,
	|	ВидыСубконто3Регл.ВидСубконто КАК ВидСубконто3Регл
	|ИЗ
	|	ТаблицаСоответствияСчетов КАК ТаблицаСоответствияСчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК ВидыСубконто1
	|		ПО ТаблицаСоответствияСчетов.СчетМеждународногоУчета = ВидыСубконто1.Ссылка
	|			И (ВидыСубконто1.НомерСтроки = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК ВидыСубконто2
	|		ПО ТаблицаСоответствияСчетов.СчетМеждународногоУчета = ВидыСубконто2.Ссылка
	|			И (ВидыСубконто2.НомерСтроки = 2)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Международный.ВидыСубконто КАК ВидыСубконто3
	|		ПО ТаблицаСоответствияСчетов.СчетМеждународногоУчета = ВидыСубконто3.Ссылка
	|			И (ВидыСубконто3.НомерСтроки = 3)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ВидыСубконто1Регл
	|		ПО ТаблицаСоответствияСчетов.СчетРеглУчета = ВидыСубконто1Регл.Ссылка
	|			И (ВидыСубконто1Регл.НомерСтроки = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ВидыСубконто2Регл
	|		ПО ТаблицаСоответствияСчетов.СчетРеглУчета = ВидыСубконто2Регл.Ссылка
	|			И (ВидыСубконто2Регл.НомерСтроки = 2)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ВидыСубконто3Регл
	|		ПО ТаблицаСоответствияСчетов.СчетРеглУчета = ВидыСубконто3Регл.Ссылка
	|			И (ВидыСубконто3Регл.НомерСтроки = 3)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаСоответствияСчетов.РеквизитДопУпорядочивания";
	#КонецОбласти
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ПроводкиРеглУчета", ПроводкиРеглУчета);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	СоответствияСчетов = РезультатЗапроса[5].Выгрузить();
	
	ПрименяемыеСоответствия = ПолучитьПрименяемыеСоответствия(СоответствияСчетов, РезультатЗапроса[3]);
	
	НастройкиЗаполненияСубконто = РезультатЗапроса[4].Выгрузить();
	НастройкиЗаполненияСубконто.Индексы.Добавить("СоответствиеСчетов");
	
	ПоложенияСчета = Новый Массив();
	ПоложенияСчета.Добавить("Дт");
	ПоложенияСчета.Добавить("Кт");
	
	СхемыКомпоновкиДанных = Новый Структура;
	Для каждого ПоложениеСчета Из ПоложенияСчета Цикл
		КлючСхемаКомпоновкиДанных = "СхемаКомпоновкиДанных" + ПоложениеСчета;
		СхемыКомпоновкиДанных.Вставить(КлючСхемаКомпоновкиДанных, СхемаКомпоновкиДанныхРеглУчета(ПроводкиРеглУчета, ПоложениеСчета));
	КонецЦикла;
	
	ПрименениеСоответствий = Новый ТаблицаЗначений;
	ПрименениеСоответствий.Колонки.Добавить("Регистратор");
	ПрименениеСоответствий.Колонки.Добавить("НомерСтроки");
	ПрименениеСоответствий.Колонки.Добавить("СчетДт");
	ПрименениеСоответствий.Колонки.Добавить("СчетДтВалютный");
	ПрименениеСоответствий.Колонки.Добавить("УчетПоПодразделениямДт");
	ПрименениеСоответствий.Колонки.Добавить("УчетПоНаправлениямДт");
	ПрименениеСоответствий.Колонки.Добавить("ВидСубконтоДт1");
	ПрименениеСоответствий.Колонки.Добавить("ВидСубконтоДт2");
	ПрименениеСоответствий.Колонки.Добавить("ВидСубконтоДт3");
	ПрименениеСоответствий.Колонки.Добавить("СубконтоДт1");
	ПрименениеСоответствий.Колонки.Добавить("СубконтоДт2");
	ПрименениеСоответствий.Колонки.Добавить("СубконтоДт3");
	ПрименениеСоответствий.Колонки.Добавить("СчетКт");
	ПрименениеСоответствий.Колонки.Добавить("СчетКтВалютный");
	ПрименениеСоответствий.Колонки.Добавить("УчетПоПодразделениямКт");
	ПрименениеСоответствий.Колонки.Добавить("УчетПоНаправлениямКт");
	ПрименениеСоответствий.Колонки.Добавить("ВидСубконтоКт1");
	ПрименениеСоответствий.Колонки.Добавить("ВидСубконтоКт2");
	ПрименениеСоответствий.Колонки.Добавить("ВидСубконтоКт3");
	ПрименениеСоответствий.Колонки.Добавить("СубконтоКт1");
	ПрименениеСоответствий.Колонки.Добавить("СубконтоКт2");
	ПрименениеСоответствий.Колонки.Добавить("СубконтоКт3");
	ПрименениеСоответствий.Индексы.Добавить("Регистратор, НомерСтроки");
	
	Для каждого СоответствиеСчетов Из СоответствияСчетов Цикл
		
		Если ПрименяемыеСоответствия[СоответствиеСчетов.Ссылка] = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ПроводкиРеглУчета.Количество() = 0 Тогда
			Прервать;
		КонецЕсли;
		
		Для каждого ПоложениеСчета Из ПоложенияСчета Цикл
			
			Если Не СоответствиеСчетов["ПрименятьДляСчета" + ПоложениеСчета] Тогда
				Продолжить;
			КонецЕсли;
			
			СхемаКомпоновкиДанных = СхемыКомпоновкиДанных["СхемаКомпоновкиДанных" + ПоложениеСчета];
			
			Настройки = Новый НастройкиКомпоновкиДанных;
			НоваяГруппировка = Настройки.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
			НоваяГруппировка.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
			НоваяГруппировка.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
			
			ФинансоваяОтчетностьСервер.НовоеПолеВыбора(Настройки, "Регистратор");
			ФинансоваяОтчетностьСервер.НовоеПолеВыбора(Настройки, "НомерСтроки");
			
			УстановитьЗаполнениеСубконтоПоСоответствиюСчетов(
				СхемаКомпоновкиДанных, 
				Настройки, 
				СоответствиеСчетов, 
				НастройкиЗаполненияСубконто);
				
			УстановитьОтборПроводокПоСоответствиюСчетов(
				СхемаКомпоновкиДанных, 
				Настройки, 
				СоответствиеСчетов);
			
			ВнешниеНаборы = Новый Структура;
			ВнешниеНаборы.Вставить("ТаблицаДанных", ПроводкиРеглУчета);
			
			РезультатПримененияСоответствия = 
				ФинансоваяОтчетностьСервер.ВыгрузитьРезультатСКД(СхемаКомпоновкиДанных, Настройки, ВнешниеНаборы);
				
			Для каждого Строка Из РезультатПримененияСоответствия Цикл
				
				Отбор = Новый Структура;
				Отбор.Вставить("Регистратор", Строка.Регистратор);
				Отбор.Вставить("НомерСтроки", Строка.НомерСтроки);
				РезультатПоиска = ПрименениеСоответствий.НайтиСтроки(Отбор);
				Если РезультатПоиска.Количество() Тогда
					СтрокаПримененияСоответствий = РезультатПоиска[0];
				Иначе
					СтрокаПримененияСоответствий = ПрименениеСоответствий.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаПримененияСоответствий, Отбор);
				КонецЕсли;
				
				ПроводкаРеглУчета = ПроводкиРеглУчета.НайтиСтроки(Отбор)[0];
				
				СтрокаПримененияСоответствий["Счет" + ПоложениеСчета] = СоответствиеСчетов.СчетМеждународногоУчета;
				СтрокаПримененияСоответствий["Счет" + ПоложениеСчета + "Валютный"] = СоответствиеСчетов.СчетМеждународногоУчетаВалютный;
				СтрокаПримененияСоответствий["УчетПоПодразделениям" + ПоложениеСчета] = СоответствиеСчетов.УчетПоПодразделениям;
				СтрокаПримененияСоответствий["УчетПоНаправлениям" + ПоложениеСчета] = СоответствиеСчетов.УчетПоНаправлениям;
				СтрокаПримененияСоответствий["ВидСубконто" + ПоложениеСчета + "1"] = СоответствиеСчетов.ВидСубконто1;
				СтрокаПримененияСоответствий["ВидСубконто" + ПоложениеСчета + "2"] = СоответствиеСчетов.ВидСубконто2;
				СтрокаПримененияСоответствий["ВидСубконто" + ПоложениеСчета + "3"] = СоответствиеСчетов.ВидСубконто3;
				СтрокаПримененияСоответствий["Субконто" + ПоложениеСчета + "1"] = Строка.СубконтоМеждународный1;
				СтрокаПримененияСоответствий["Субконто" + ПоложениеСчета + "2"] = Строка.СубконтоМеждународный2;
				СтрокаПримененияСоответствий["Субконто" + ПоложениеСчета + "3"] = Строка.СубконтоМеждународный3;
				
				ТранслироватьПроводку = Истина;
				Если Не ЗначениеЗаполнено(СтрокаПримененияСоответствий.СчетДт) 
					 И ЗначениеЗаполнено(ПроводкаРеглУчета.СчетДт) Тогда
					ТранслироватьПроводку = Ложь;
				КонецЕсли;
				
				Если Не ЗначениеЗаполнено(СтрокаПримененияСоответствий.СчетКт) 
					 И ЗначениеЗаполнено(ПроводкаРеглУчета.СчетКт) Тогда
					ТранслироватьПроводку = Ложь;
				КонецЕсли;
				
				Если Не ТранслироватьПроводку Тогда
					Продолжить;
				КонецЕсли;
				
				Проводка = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(СтруктураПроводки);
				ИсключаемыеПоля = ИсключаемыеПоля(СтрокаПримененияСоответствий);
				ЗаполнитьЗначенияСвойств(Проводка, ПроводкаРеглУчета, , ИсключаемыеПоля);
				ЗаполнитьЗначенияСвойств(Проводка, СтрокаПримененияСоответствий);
				
				ЗаполнитьВалютнуюСумму(Проводка, 
					СтрокаПримененияСоответствий.СчетДтВалютный, 
					СтрокаПримененияСоответствий.СчетКтВалютный);
				
				ДобавитьПроводкуВТаблицуПроводок(ТаблицаПроводок, Проводка);
				ПроводкиРеглУчета.Удалить(ПроводкаРеглУчета);
				
			КонецЦикла; // по результату применения соответствия
			
		КонецЦикла;// по Дт и Кт
		
	КонецЦикла;
	
	Если ПроводкиРеглУчета.Количество() Тогда
		
		#Область ТекстЗапросаПроводокБезСоответствия
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПроводкиРеглУчета.Период КАК Период,
		|	ПроводкиРеглУчета.Регистратор КАК Регистратор,
		|	ПроводкиРеглУчета.НомерСтроки КАК НомерСтроки,
		|	ПроводкиРеглУчета.ПодразделениеДт КАК ПодразделениеДт,
		|	ПроводкиРеглУчета.ПодразделениеКт КАК ПодразделениеКт,
		|	ПроводкиРеглУчета.СчетДт КАК СчетДт,
		|	ПроводкиРеглУчета.СчетКт КАК СчетКт,
		|	ПроводкиРеглУчета.УчетнаяПолитика КАК УчетнаяПолитика,
		|	ПроводкиРеглУчета.Организация КАК Организация
		|ПОМЕСТИТЬ ПроводкиРеглУчета
		|ИЗ
		|	&ПроводкиРеглУчета КАК ПроводкиРеглУчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПроводкиРеглУчета.Период КАК Период,
		|	ПроводкиРеглУчета.Регистратор КАК Регистратор,
		|	ПроводкиРеглУчета.НомерСтроки КАК НомерСтроки,
		|	""Дт"" КАК ПоложениеСчета,
		|	ПроводкиРеглУчета.ПодразделениеДт КАК ПодразделениеДт,
		|	ПроводкиРеглУчета.ПодразделениеКт КАК ПодразделениеКт,
		|	ПроводкиРеглУчета.СчетДт КАК Счет,
		|	ПроводкиРеглУчета.УчетнаяПолитика КАК УчетнаяПолитика,
		|	ПроводкиРеглУчета.Организация КАК Организация
		|ПОМЕСТИТЬ СчетаПроводокРеглУчета
		|ИЗ
		|	ПроводкиРеглУчета КАК ПроводкиРеглУчета
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПроводкиРеглУчета.Период КАК Период,
		|	ПроводкиРеглУчета.Регистратор,
		|	ПроводкиРеглУчета.НомерСтроки,
		|	""Кт"",
		|	ПроводкиРеглУчета.ПодразделениеДт КАК ПодразделениеДт,
		|	ПроводкиРеглУчета.ПодразделениеКт КАК ПодразделениеКт,
		|	ПроводкиРеглУчета.СчетКт,
		|	ПроводкиРеглУчета.УчетнаяПолитика,
		|	ПроводкиРеглУчета.Организация
		|ИЗ
		|	ПроводкиРеглУчета КАК ПроводкиРеглУчета
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	УчетнаяПолитика,
		|	Счет
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СчетаРеглУчета.Регистратор КАК Регистратор,
		|	СчетаРеглУчета.НомерСтроки КАК НомерСтроки,
		|	СчетаРеглУчета.ПодразделениеДт КАК ПодразделениеДт,
		|	СчетаРеглУчета.ПодразделениеКт КАК ПодразделениеКт,
		|	СчетаРеглУчета.Счет КАК Счет,
		|	СчетаРеглУчета.ПоложениеСчета КАК ПоложениеСчета,
		|	СчетаРеглУчета.УчетнаяПолитика КАК УчетнаяПолитика,
		|	СчетаРеглУчета.Организация КАК Организация
		|ИЗ
		|	СчетаПроводокРеглУчета КАК СчетаРеглУчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СчетаРеглУчетаИгнорируемыеПриОтраженииВМеждународномУчете КАК ИгнорируемыеСчета
		|		ПО СчетаРеглУчета.УчетнаяПолитика = ИгнорируемыеСчета.УчетнаяПолитика
		|			И СчетаРеглУчета.Счет = ИгнорируемыеСчета.СчетРеглУчета
		|ГДЕ
		|	ИгнорируемыеСчета.СчетРеглУчета ЕСТЬ NULL ";
		#КонецОбласти
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("ПроводкиРеглУчета", ПроводкиРеглУчета);

		Выборка = Запрос.Выполнить().Выбрать();
		
		ШаблонОшибки = НСтр("ru = 'Не указан соответствующий счет для %Счет.'");
		
		Пока Выборка.Следующий() Цикл
			
			Отбор = Новый Структура;
			Отбор.Вставить("Регистратор", Выборка.Регистратор);
			Отбор.Вставить("НомерСтроки", Выборка.НомерСтроки);
			
			РезультатПоиска = ПрименениеСоответствий.НайтиСтроки(Отбор);
			Если РезультатПоиска.Количество() Тогда
				СтрокаПримененияСоответствий = РезультатПоиска[0];
			Иначе
				СтрокаПримененияСоответствий = Неопределено;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(Выборка.Счет) Тогда
				Продолжить;
			КонецЕсли;
			
			Если СтрокаПримененияСоответствий <> Неопределено 
				И ЗначениеЗаполнено(СтрокаПримененияСоответствий["Счет" + Выборка.ПоложениеСчета]) Тогда
				Продолжить;
			КонецЕсли;
			
			ТекстОшибки = СтрЗаменить(ШаблонОшибки, "%Счет", Выборка.Счет);
			
			Проводка = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(СтруктураПроводки);
			ЗаполнитьЗначенияСвойств(Проводка, Отбор);
			ЗаполнитьЗначенияСвойств(Проводка, Выборка);
			ЗаполнитьСтатусПроводки(Проводка, Перечисления.СтатусыОтраженияВМеждународномУчете.ОтсутствуютПравилаОтраженияВУчете);
			ДобавитьОшибкуПроводки(Проводка, ТекстОшибки);
			ДобавитьПроводкуВТаблицуПроводок(ТаблицаПроводок, Проводка);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ИсключаемыеПоля(ДанныеСчета)
	
	МаксКоличествоСубконто = 3;
	ВидыСчета = СтрРазделить("Дт,Кт",",");
	ИсключаемыеПоля = Новый Массив;
	Для Каждого Вид Из ВидыСчета Цикл
		ИсключаемыеПоля.Добавить("Счет" + Вид);
		Если НЕ ДанныеСчета["УчетПоПодразделениям" + Вид] Тогда
			ИсключаемыеПоля.Добавить("Подразделение" + Вид);
		КонецЕсли;
		Если НЕ ДанныеСчета["УчетПоНаправлениям" + Вид] Тогда
			ИсключаемыеПоля.Добавить("НаправлениеДеятельности" + Вид);
		КонецЕсли;
		Для Номер = 1 По МаксКоличествоСубконто Цикл
			ВидНомер = Вид + Номер;
			ИсключаемыеПоля.Добавить("ВидСубконто"+ ВидНомер);
			ИсключаемыеПоля.Добавить("Субконто"+ ВидНомер);
		КонецЦикла;
	КонецЦикла;
	Возврат СтрСоединить(ИсключаемыеПоля,",");
	
КонецФункции

Функция ПолучитьПрименяемыеСоответствия(СоответствияСчетов, ПроводкиРеглУчета)
	
	ПрименяемыеСоответствия = Новый Соответствие;
	Если СоответствияСчетов.Количество() = 0 Тогда
		Возврат ПрименяемыеСоответствия;
	КонецЕсли;

	СхемаОтборов = СхемаКомпоновкиДанныхРеглУчета(ПроводкиРеглУчета);
	Настройки = Новый НастройкиКомпоновкиДанных;
	
	ФинансоваяОтчетностьСервер.НовоеПолеВыбора(Настройки, "Соответствие", "Соответствие");
	ФинансоваяОтчетностьСервер.НовоеПолеВыбора(Настройки, "Регистратор", "Регистратор");
	ФинансоваяОтчетностьСервер.НовоеПолеВыбора(Настройки, "НомерСтроки", "НомерСтроки");
	ФинансоваяОтчетностьСервер.НовоеПолеВыбора(Настройки, "СчетДт", "СчетДт");
	ФинансоваяОтчетностьСервер.НовоеПолеВыбора(Настройки, "СчетКт", "СчетКт");
	
	Для Каждого СоответствиеСчета Из СоответствияСчетов Цикл
		
		Если НЕ СоответствиеСчета.УстановленДополнительныйОтбор Тогда
			ПрименяемыеСоответствия.Вставить(СоответствиеСчета.Ссылка, Истина);
			Продолжить;
		КонецЕсли;
		
		ИмяПараметраСоответствия = "Соответствие"+СоответствиеСчета.Порядок;
		ФинансоваяОтчетностьСервер.НовыйПараметр(Настройки, ИмяПараметраСоответствия, СоответствиеСчета.Ссылка);
		НоваяГруппировка = ФинансоваяОтчетностьСервер.НоваяГруппировка(Настройки.Структура, "Соответствие");
		
		ОтборСоответствия = ФинансоваяОтчетностьСервер.НовыйОтбор(НоваяГруппировка.Отбор, "Соответствие");
		ОтборСоответствия.ПравоеЗначение = Новый ПолеКомпоновкиДанных("ПараметрыДанных."+ИмяПараметраСоответствия);
		НовыйОтборДт = НоваяГруппировка.Отбор;
		НовыйОтборКт = НоваяГруппировка.Отбор;
		Если СоответствиеСчета.ПрименятьДляСчетаДт И СоответствиеСчета.ПрименятьДляСчетаКт Тогда
			ГруппаИЛИ = ФинансоваяОтчетностьСервер.НовыйОтбор(НоваяГруппировка.Отбор,,,Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
			ГруппаИЛИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
			НовыйОтборДт = ФинансоваяОтчетностьСервер.НовыйОтбор(ГруппаИЛИ,,,Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
			НовыйОтборКт = ФинансоваяОтчетностьСервер.НовыйОтбор(ГруппаИЛИ,,,Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
		КонецЕсли;
		
		ДопОтбор = СоответствиеСчета.ДополнительныйОтбор.Получить();
		Если СоответствиеСчета.ПрименятьДляСчетаДт Тогда
			ФинансоваяОтчетностьСервер.НовыйОтбор(НовыйОтборДт, "СчетДт", СоответствиеСчета.СчетРеглУчета);
			ФинансоваяОтчетностьСервер.НовыйОтбор(НовыйОтборДт, "УчетнаяПолитика", СоответствиеСчета.УчетнаяПолитика);
			ФинансоваяОтчетностьСервер.СкопироватьОтбор(ДопОтбор.Отбор, НовыйОтборДт, Истина,,"Дт");
		КонецЕсли;
		
		Если СоответствиеСчета.ПрименятьДляСчетаКт Тогда
			ФинансоваяОтчетностьСервер.НовыйОтбор(НовыйОтборКт, "СчетКт", СоответствиеСчета.СчетРеглУчета);
			ФинансоваяОтчетностьСервер.НовыйОтбор(НовыйОтборКт, "УчетнаяПолитика", СоответствиеСчета.УчетнаяПолитика);
			ФинансоваяОтчетностьСервер.СкопироватьОтбор(ДопОтбор.Отбор, НовыйОтборКт, Истина,,"Кт");
		КонецЕсли;
		
		Если НоваяГруппировка.Отбор.Элементы.Количество() = 0 Тогда
			Настройки.Структура.Удалить(НоваяГруппировка);
		КонецЕсли;
		
	КонецЦикла;
	
	Если СоответствияСчетов.Количество() <> ПрименяемыеСоответствия.Количество() Тогда
		ВнешниеНаборы = Новый Структура("ТаблицаДанных", ПроводкиРеглУчета);
		РезультатПримененияОтборов = ФинансоваяОтчетностьСервер.ВыгрузитьРезультатСКД(СхемаОтборов, Настройки, ВнешниеНаборы);
		РезультатПримененияОтборов.Свернуть("Соответствие");
		Для Каждого Строка Из РезультатПримененияОтборов Цикл
			ПрименяемыеСоответствия.Вставить(Строка.Соответствие, Истина);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ПрименяемыеСоответствия;
	
КонецФункции

Функция СхемаКомпоновкиДанныхРеглУчета(ТаблицаДанных, ПоложениеСчета = Неопределено) Экспорт
	
	СхемаКомпоновкиДанных = ФинансоваяОтчетностьСервер.НоваяСхемаНаборОбъект("ТаблицаДанных");
	НаборДанных = СхемаКомпоновкиДанных.НаборыДанных.Найти("ТаблицаДанных");
	
	Для каждого Колонка Из ТаблицаДанных.Колонки Цикл
		ИмяПоля = Колонка.Имя;
		НовоеПоле = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
		НовоеПоле.Поле = ИмяПоля;
		ПутьКДанным = ИмяПоля;
		Если ПоложениеСчета = "Дт" Тогда
			ПутьКДанным = СтрЗаменить(ПутьКДанным, "Дт", "");
		ИначеЕсли ПоложениеСчета = "Кт" Тогда
			ПутьКДанным = СтрЗаменить(ПутьКДанным, "Кт", "");
		КонецЕсли;
		НовоеПоле.ПутьКДанным = ПутьКДанным;
		НовоеПоле.ТипЗначения = Колонка.ТипЗначения;
	КонецЦикла;
	
	Параметр = СхемаКомпоновкиДанных.Параметры.Добавить();
	Параметр.Использование = ИспользованиеПараметраКомпоновкиДанных.Всегда;
	Параметр.Имя = "Кэш";
	Параметр.Значение = Новый Соответствие;
	
	Возврат СхемаКомпоновкиДанных;
	
КонецФункции

Функция УстановитьЗаполнениеСубконтоПоСоответствиюОборотов(СхемаКомпоновкиДанных, Настройки, СоответствиеОборотов, НастройкиЗаполненияСубконто)
	
	СхемаКомпоновкиДанных.ВычисляемыеПоля.Очистить();
	
	Отбор = Новый Структура;
	Отбор.Вставить("СоответствиеОборотов", СоответствиеОборотов.Ссылка);
	Отбор.Вставить("ПоложениеСубконто");
	Отбор.Вставить("ВидСубконто");
	
	ПоложенияСубконто = Новый Массив;
	ПоложенияСубконто.Добавить("Дт");
	ПоложенияСубконто.Добавить("Кт");
	
	ТипСтрока = Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(10));
	Для НомерСубконто = 1 По 3 Цикл
		Для каждого ПоложениеСубконто Из ПоложенияСубконто Цикл
			Выражение   = "NULL";
			ВидСубконто = СоответствиеОборотов["ВидСубконто" + ПоложениеСубконто + НомерСубконто];
			Если ЗначениеЗаполнено(ВидСубконто) Тогда
				Отбор.ПоложениеСубконто = ПоложениеСубконто;
				Отбор.ВидСубконто = ВидСубконто;
				РезультатПоиска = НастройкиЗаполненияСубконто.НайтиСтроки(Отбор);
				Если РезультатПоиска.Количество() Тогда
					Выражение = ВыражениеЗаполненияСубконтоПоНастройке(СхемаКомпоновкиДанных, РезультатПоиска[0]);
				КонецЕсли;
			КонецЕсли;
			ПутьКДанным = "СубконтоМеждународный" + ПоложениеСубконто + НомерСубконто;
			ФинансоваяОтчетностьСервер.НовоеВычисляемоеПоле(СхемаКомпоновкиДанных, ПутьКДанным, Выражение); 
			ФинансоваяОтчетностьСервер.НовоеПолеВыбора(Настройки, ПутьКДанным);
			
			//типизируем поле набора
			ПолеСубконто = СхемаКомпоновкиДанных.НаборыДанных.ТаблицаДанных.Поля.Найти(Выражение);
			Если ПолеСубконто <> Неопределено Тогда
				ПолеСубконто.ТипЗначения = ВидСубконто.ТипЗначения;
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;
	
КонецФункции

Функция УстановитьЗаполнениеСубконтоПоСоответствиюСчетов(СхемаКомпоновкиДанных, Настройки, СоответствиеСчетов, НастройкиЗаполненияСубконто)
	
	СхемаКомпоновкиДанных.ВычисляемыеПоля.Очистить();
	
	Отбор = Новый Структура;
	Отбор.Вставить("СоответствиеСчетов", СоответствиеСчетов.Ссылка);
	Отбор.Вставить("ВидСубконто");
	
	ТипСтрока = Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(10));
	Для НомерСубконто = 1 По 3 Цикл
		ВидСубконто = СоответствиеСчетов["ВидСубконто" + НомерСубконто];
		Выражение = "NULL";
		Если ЗначениеЗаполнено(ВидСубконто) Тогда
			Отбор.ВидСубконто = ВидСубконто;
			РезультатПоиска = НастройкиЗаполненияСубконто.НайтиСтроки(Отбор);
			Если РезультатПоиска.Количество() Тогда
				Выражение = ВыражениеЗаполненияСубконтоПоНастройке(СхемаКомпоновкиДанных, РезультатПоиска[0]);
			КонецЕсли;
		КонецЕсли;
		ПутьКДанным = "СубконтоМеждународный" + НомерСубконто;
		ФинансоваяОтчетностьСервер.НовоеВычисляемоеПоле(СхемаКомпоновкиДанных, ПутьКДанным, Выражение); 
		ФинансоваяОтчетностьСервер.НовоеПолеВыбора(Настройки, ПутьКДанным);
		
		//типизируем поле набора
		ПолеСубконто = СхемаКомпоновкиДанных.НаборыДанных.ТаблицаДанных.Поля.Найти(Выражение);
		Если ПолеСубконто <> Неопределено Тогда
			ПолеСубконто.ТипЗначения = ВидСубконто.ТипЗначения;
		КонецЕсли;
		
	КонецЦикла;
	
КонецФункции

Функция ВыражениеЗаполненияСубконтоПоНастройке(СхемаКомпоновкиДанных, Настройка)
	
	ВыражениеВычисляемогоПоля = "Неопределено";
	Если Настройка.ЗаполнятьИзИсточника Тогда
		ВыражениеНастройки = Настройка.Выражение;
		Если Найти(ВыражениеНастройки, "СчетДт.") > 0 Тогда
			ВыражениеНастройки = СтрЗаменить(ВыражениеНастройки, "СчетДт.", "");
		ИначеЕсли Найти(ВыражениеНастройки, "СчетКт.") > 0 Тогда
			ВыражениеНастройки = СтрЗаменить(ВыражениеНастройки, "СчетКт.", "");
		ИначеЕсли ПустаяСтрока(ВыражениеНастройки) Тогда
			ВыражениеНастройки = "Неопределено";
		КонецЕсли;
		ВыражениеВычисляемогоПоля = ВыражениеНастройки;
	Иначе
		ИмяПараметра = "УказанноеЗначение_" + СтрЗаменить(Строка(Новый УникальныйИдентификатор), "-", "");
		Параметр = СхемаКомпоновкиДанных.Параметры.Добавить();
		Параметр.Использование = ИспользованиеПараметраКомпоновкиДанных.Всегда;
		Параметр.Имя = ИмяПараметра;
		Параметр.Значение = Настройка.УказанноеЗначение;
		ВыражениеВычисляемогоПоля = "&" + ИмяПараметра;
	КонецЕсли;
	
	Возврат ВыражениеВычисляемогоПоля;
	
КонецФункции

Функция УстановитьОтборПроводокПоСоответствиюОборотов(СхемаКомпоновкиДанных, Настройки, СоответствиеОборотов, ДополнительныйОтборГруппы = Неопределено) Экспорт
	
	ФинансоваяОтчетностьСервер.НовыйОтбор(Настройки.Отбор, "СчетДт", СоответствиеОборотов.СчетРеглУчетаДт);
	ФинансоваяОтчетностьСервер.НовыйОтбор(Настройки.Отбор, "СчетКт", СоответствиеОборотов.СчетРеглУчетаКт);
	ФинансоваяОтчетностьСервер.НовыйОтбор(Настройки.Отбор, "УчетнаяПолитика", СоответствиеОборотов.УчетнаяПолитика);
	
	Если ДополнительныйОтборГруппы <> Неопределено Тогда
		ПреобразоватьОтборСоответствияОборотов(ДополнительныйОтборГруппы.Отбор.Элементы);
		ФинансоваяОтчетностьСервер.СкопироватьОтбор(ДополнительныйОтборГруппы.Отбор, Настройки.Отбор);
	КонецЕсли;
	
	Если СоответствиеОборотов.УстановленДополнительныйОтбор Тогда
		ДополнительныйОтбор = СоответствиеОборотов.ДополнительныйОтбор.Получить();
		ПреобразоватьОтборСоответствияОборотов(ДополнительныйОтбор.Отбор.Элементы);
		ФинансоваяОтчетностьСервер.СкопироватьОтбор(ДополнительныйОтбор.Отбор, Настройки.Отбор);
	КонецЕсли;
	
КонецФункции

Функция УстановитьОтборПроводокПоСоответствиюСчетов(СхемаКомпоновкиДанных, Настройки, СоответствиеСчетов) Экспорт
	
	ФинансоваяОтчетностьСервер.НовыйОтбор(Настройки.Отбор, "Счет", СоответствиеСчетов.СчетРеглУчета);
	ФинансоваяОтчетностьСервер.НовыйОтбор(Настройки.Отбор, "УчетнаяПолитика", СоответствиеСчетов.УчетнаяПолитика);
	
	Если СоответствиеСчетов.УстановленДополнительныйОтбор Тогда
		ДопОтбор = СоответствиеСчетов.ДополнительныйОтбор.Получить();
		ФинансоваяОтчетностьСервер.СкопироватьОтбор(ДопОтбор.Отбор, Настройки.Отбор);
	КонецЕсли;
	
КонецФункции

Функция Подключаемый_РеквизитСубконто(Кэш, Субконто, Реквизит) Экспорт
	
	ЗначениеРеквизита = Неопределено;
	
	РеквизитыСубконто = Кэш[Субконто];
	Если РеквизитыСубконто <> Неопределено Тогда
		РеквизитыСубконто.Свойство(Реквизит, ЗначениеРеквизита);
		Если ЗначениеРеквизита <> Неопределено Тогда
			Возврат ЗначениеРеквизита;
		КонецЕсли;
	КонецЕсли;
	
	Попытка
		ЗначениеРеквизита = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Субконто, Реквизит)
	Исключение
		ЗначениеРеквизита = Неопределено;
	КонецПопытки;
	
	Если РеквизитыСубконто = Неопределено Тогда
		РеквизитыСубконто = Новый Структура;
		Кэш.Вставить(Субконто, РеквизитыСубконто); 
	КонецЕсли;
	
	РеквизитыСубконто.Вставить(Реквизит, ЗначениеРеквизита);
	
	Возврат ЗначениеРеквизита;
	
КонецФункции

Процедура ЗаполнитьВалютнуюСумму(Проводка, СчетДтВалютный, СчетКтВалютный)
	
	ВалютаРеглУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	ПризнакиСчетов = Новый Структура;
	ПризнакиСчетов.Вставить("Дт", СчетДтВалютный); 
	ПризнакиСчетов.Вставить("Кт", СчетКтВалютный); 
	
	Для каждого ПризнакСчета Из ПризнакиСчетов Цикл
		
		Валютный = ПризнакСчета.Значение;
		ВалютаЗаполнена = ЗначениеЗаполнено(Проводка["Валюта" + ПризнакСчета.Ключ]);
		
		Если Валютный И Не ВалютаЗаполнена Тогда
			Проводка["Валюта" + ПризнакСчета.Ключ]        = ВалютаРеглУчета;
			Проводка["ВалютнаяСумма" + ПризнакСчета.Ключ] = Проводка.Сумма;
		ИначеЕсли Не Валютный И ВалютаЗаполнена Тогда
			Проводка["Валюта" + ПризнакСчета.Ключ]        = Неопределено;
			Проводка["ВалютнаяСумма" + ПризнакСчета.Ключ] = 0;
		КонецЕсли;
		
	КонецЦикла;
	
	Проводка.СуммаПредставления = Проводка.Сумма;
	
КонецПроцедуры

Процедура ПреобразоватьОтборСоответствияОборотов(ЭлементыОтбора)
	
	Для каждого ЭлементОтбора Из ЭлементыОтбора Цикл
		
		Если ТипЗнч(ЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
			ПреобразоватьОтборСоответствияОборотов(ЭлементОтбора.Элементы);
		Иначе
			
			ЛевоеЗначениеОтбора = Строка(ЭлементОтбора.ЛевоеЗначение);
			
			Если СтрНайти(ЛевоеЗначениеОтбора, "СчетДт.") > 0 Тогда
				ЛевоеЗначениеОтбора = СтрЗаменить(ЛевоеЗначениеОтбора, "СчетДт.", "");
			ИначеЕсли СтрНайти(ЛевоеЗначениеОтбора, "СчетКт.") > 0 Тогда
				ЛевоеЗначениеОтбора = СтрЗаменить(ЛевоеЗначениеОтбора, "СчетКт.", "");
			КонецЕсли;
			
			ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ЛевоеЗначениеОтбора);
		КонецЕсли;
		
	КонецЦикла
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
