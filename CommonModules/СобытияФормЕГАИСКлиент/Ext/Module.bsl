
#Область ПрограммныйИнтерфейс

#Область ОбработчикиСобытийЭлементовФормы

// Возвращает структура параметров заполнения.
// 
// Возвращаемое значение:
//  Структура со свойствами:
//   * ПересчитатьСумму - Булево - Необходимость пересчета суммы.
//   * ЗаполнитьИндексАкцизнойМарки - Булево - Необходимость заполнения индекса акцизной марки.
//
Функция СтруктураПараметрыЗаполнения() Экспорт

	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("ПересчитатьСумму",                        Ложь);
	ПараметрыЗаполнения.Вставить("ЗаполнитьИндексАкцизнойМарки",            Ложь);
	ПараметрыЗаполнения.Вставить("МаркируемаяАлкогольнаяПродукцияВТЧ",      Ложь);
	ПараметрыЗаполнения.Вставить("ШтрихкодыВТЧ",                            Ложь);
	ПараметрыЗаполнения.Вставить("ПерезаполнитьНоменклатуруЕГАИС",          Ложь);
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

#КонецОбласти

#Область ПодключаемоеОборудование

Процедура ОбработкаОповещенияПолученыШтрихкоды(ОписаниеОповещения, Форма, ИмяСобытия, Параметр, Источник) Экспорт
	
	Если Источник = "ПодключаемоеОборудование" И Форма.ВводДоступен() Тогда
		
		Если ИмяСобытия = "ScanData" И ЕстьНеобработанноеСобытие() Тогда
			
			ДанныеШтрихкодов = ПреобразоватьДанныеСоСканераВМассив(Параметр);
			
			Если ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(Форма.Элементы, "Товары") Тогда
				ТекущиеДанные = Форма.Элементы.Товары.ТекущиеДанные;
			ИначеЕсли ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(Форма.Элементы, "АкцизныеМарки") Тогда
				ТекущиеДанные = Форма.Элементы.АкцизныеМарки.ТекущиеДанные;
			Иначе
				ТекущиеДанные = Неопределено;
			КонецЕсли;
			
			Если АкцизныеМаркиЕГАИСКлиент.СчитанаАкцизнаяМарка(Форма, ТекущиеДанные, ДанныеШтрихкодов) Тогда
				
				ОбработатьСобытие();
				
			Иначе
				
				СобытияФормЕГАИСКлиентПереопределяемый.ОбработкаОповещенияПолученыШтрихкоды(
					ОписаниеОповещения,
					Форма, ИмяСобытия, Параметр, Источник);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ВнешнееСобытиеПолученыШтрихкоды(ОписаниеОповещения, Форма, Источник, Событие, Данные) Экспорт
	
	Если НЕ Форма.ВводДоступен() Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеСобытия = Новый Структура;
	ОписаниеСобытия.Вставить("Источник", Источник);
	ОписаниеСобытия.Вставить("Событие",  Событие);
	ОписаниеСобытия.Вставить("Данные",   Данные);
	
	Результат = МенеджерОборудованияКлиент.ПолучитьСобытиеОтУстройства(ОписаниеСобытия);
	Если Результат <> Неопределено
		И Результат.Источник = "ПодключаемоеОборудование"
		И Результат.ИмяСобытия = "ScanData"
		И Найти(Форма.ПоддерживаемыеТипыПодключаемогоОборудования, "СканерШтрихкода") > 0 Тогда
			
			ДанныеШтрихкодов = ПреобразоватьДанныеСоСканераВМассив(Результат.Параметр);
			
			Если ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(Форма.Элементы, "Товары") Тогда
				ТекущиеДанные = Форма.Элементы.Товары.ТекущиеДанные;
			ИначеЕсли ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(Форма.Элементы, "АкцизныеМарки") Тогда
				ТекущиеДанные = Форма.Элементы.АкцизныеМарки.ТекущиеДанные;
			Иначе
				ТекущиеДанные = Неопределено;
			КонецЕсли;
			
			Если Не АкцизныеМаркиЕГАИСКлиент.СчитанаАкцизнаяМарка(Форма, ТекущиеДанные, ДанныеШтрихкодов) Тогда
				
				СобытияФормЕГАИСКлиентПереопределяемый.ВнешнееСобытиеПолученыШтрихкоды(
					ОписаниеОповещения,
					Форма, Источник, Событие, Данные);
				
			КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьКомандуПоискПоШтрихкоду(ОписаниеОповещения, Форма, ДанныеШтрихкода) Экспорт
	
	ДанныеШтрихкодов = Новый Массив;
	ДанныеШтрихкодов.Добавить(ДанныеШтрихкода);
	
	ТекущиеДанные = Форма.Элементы.Товары.ТекущиеДанные;
	Если Не АкцизныеМаркиЕГАИСКлиент.СчитанаАкцизнаяМарка(Форма, ТекущиеДанные, ДанныеШтрихкодов) Тогда
		
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, ДанныеШтрихкодов);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПодключаемоеОборудование

Функция ПреобразоватьДанныеСоСканераВМассив(Параметр)
	
	Данные = Новый Массив;
	Данные.Добавить(ПреобразоватьДанныеСоСканераВСтруктуру(Параметр));
	
	Возврат Данные;
	
КонецФункции

Функция ПреобразоватьДанныеСоСканераВСтруктуру(Параметр)
	
	Если Параметр[1] = Неопределено Тогда
		Данные = Новый Структура("Штрихкод, Количество", Параметр[0], 1);    // Достаем штрихкод из основных данных
	Иначе
		Данные = Новый Структура("Штрихкод, Количество", Параметр[1][1], 1); // Достаем штрихкод из дополнительных данных
	КонецЕсли;
	
	Возврат Данные;
	
КонецФункции

Функция ЕстьНеобработанноеСобытие()
	
	Возврат (глПодключаемоеОборудованиеСобытиеОбработано = Ложь);
	
КонецФункции

Процедура ОбработатьСобытие()
	
	глПодключаемоеОборудованиеСобытиеОбработано = Истина;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
