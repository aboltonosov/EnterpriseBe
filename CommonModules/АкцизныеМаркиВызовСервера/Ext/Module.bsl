
#Область СлужебныеПроцедурыИФункции

// Функция - Код классификатора номенклатуры ЕГАИС
//
// Параметры:
//  ШтрихкодАкцизнойМарки - Строка - Штрихкод акцизной марки
// 
// Возвращаемое значение:
//  Строка - строка с кодом номенклатуры по классификатору егаис
//
Функция КодКлассификатораНоменклатурыЕГАИС(ШтрихкодАкцизнойМарки) Экспорт
	
	Если Сред(ШтрихкодАкцизнойМарки, 4, 5) = "00000" Тогда
		
		Значение = Сред(ШтрихкодАкцизнойМарки, 9, 11);
		КоличествоИтераций = 11;
		
	Иначе
		
		Значение = Сред(ШтрихкодАкцизнойМарки, 8, 12);
		КоличествоИтераций = 12;
		
	КонецЕсли;
	
	Результат = 0;
	
	Для Итерация = 1 По КоличествоИтераций Цикл
		
		Сумматор = 1;
		Для Индекс = 1 По КоличествоИтераций - Итерация Цикл
			Сумматор = Сумматор * 36;
		КонецЦикла;
		
		Результат = Результат + Сумматор * (Найти("0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ", Сред(Значение, Итерация, 1)) - 1);
		
	КонецЦикла;
	
	Возврат Формат(Результат, "ЧЦ=19; ЧВН=; ЧГ=0");
	
КонецФункции

// Проверяет штрихкод акцизной марки
//
// Параметры:
//  Штрихкод - Строка - Штрихкод акцизной марки
//  СопоставленнаяНоменклатура - Структура со свойствами:
//   * НоменклатураЕГАИС - СправочникСсылка.КлассификаторАлкогольнойПродукцииЕГАИС
//   * Номенклатура - ОпределяемыйТип.Номенклатура
//   * Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатуры
//   * Упаковка - ОпределяемыйТип.Упаковка
// 
// Возвращаемое значение:
//   Булево - признак штрихкод является акцизной маркой
//
Функция ЭтоШтрихкодАкцизнойМарки(Штрихкод, СопоставленнаяНоменклатура = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ВидДокумента = Перечисления.ВидыДокументовЕГАИС.ЧекККМ;
	ТипШтрихкодМарки = ФабрикаXDTO.Тип(Перечисления.ВидыДокументовЕГАИС.ПространствоИмен(ВидДокумента, Неопределено, ""), "BK");
	
	Попытка
		ТипШтрихкодМарки.Проверить(Штрихкод);
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
	КодКлассификатораНоменклатурыЕГАИС = КодКлассификатораНоменклатурыЕГАИС(Штрихкод);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	КлассификаторАлкогольнойПродукцииЕГАИС.Ссылка         КАК Ссылка,
	|	КлассификаторАлкогольнойПродукцииЕГАИС.Номенклатура   КАК Номенклатура,
	|	КлассификаторАлкогольнойПродукцииЕГАИС.Характеристика КАК Характеристика,
	|	КлассификаторАлкогольнойПродукцииЕГАИС.Упаковки.(
	|		Ссылка,
	|		НомерСтроки,
	|		ИдентификаторУпаковки,
	|		Упаковка
	|	) КАК Упаковки
	|ИЗ
	|	Справочник.КлассификаторАлкогольнойПродукцииЕГАИС КАК КлассификаторАлкогольнойПродукцииЕГАИС
	|ГДЕ
	|	КлассификаторАлкогольнойПродукцииЕГАИС.Код = &КодКлассификатораНоменклатурыЕГАИС");
	Запрос.Параметры.Вставить("КодКлассификатораНоменклатурыЕГАИС", КодКлассификатораНоменклатурыЕГАИС);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		СопоставленнаяНоменклатура = Новый Структура;
		СопоставленнаяНоменклатура.Вставить("НоменклатураЕГАИС", Выборка.Ссылка);
		СопоставленнаяНоменклатура.Вставить("Номенклатура",      Выборка.Номенклатура);
		СопоставленнаяНоменклатура.Вставить("Характеристика",    Выборка.Характеристика);
		СопоставленнаяНоменклатура.Вставить("Упаковка",          Неопределено);
		
		ТаблицаУпаковок = Выборка.Упаковки.Выгрузить();
		Если ТаблицаУпаковок.Количество() <> 0 Тогда
			СопоставленнаяНоменклатура.Вставить("Упаковка", ТаблицаУпаковок[0].Упаковка);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти