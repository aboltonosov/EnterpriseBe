////////////////////////////////////////////////////////////////////////////////
//
//   Расширенная реализация подсистемы персонифицированного учета.
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Процедура СоздатьВТЗарегистрированныеКорректировкиСтажа(МенеджерВременныхТаблиц, Организация, ОтчетныйПериод) Экспорт
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ОтчетныйПериод", ОтчетныйПериод);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НеобходимыеКорректировкиСтажаПФР.Организация,
	|	НеобходимыеКорректировкиСтажаПФР.ОтчетныйПериод КАК КорректируемыйПериод,
	|	НеобходимыеКорректировкиСтажаПФР.ФизическоеЛицо
	|ПОМЕСТИТЬ ВТЗарегистрированныеКорректировкиСтажа
	|ИЗ
	|	РегистрСведений.НеобходимыеКорректировкиСтажаПФР КАК НеобходимыеКорректировкиСтажаПФР
	|ГДЕ
	|	НеобходимыеКорректировкиСтажаПФР.Организация = &Организация
	|	И НеобходимыеКорректировкиСтажаПФР.ОтчетныйПериод < &ОтчетныйПериод";	
	Запрос.Выполнить();
КонецПроцедуры

Процедура СоздатьВТЗаписиСтажаПоДаннымУчета(МенеджерВременныхТаблиц, Организация, ОтчетныйПериод, ПараметрыПолученияДанных, ПараметрыОтбора) Экспорт
	
	ПараметрыОтбораСтажа = УчетСтажаПФР.ПараметрыОтбораСоздатьВТДанныеУчетаСтажаПФР();
	ПараметрыОтбораСтажа.Организация = Организация;
	ПараметрыОтбораСтажа.НачалоПериода = ОтчетныйПериод;
	ПараметрыОтбораСтажа.ОкончаниеПериода = ПерсонифицированныйУчетКлиентСервер.ОкончаниеОтчетногоПериодаСтажаПерсУчета(ОтчетныйПериод);
	
	Если ПараметрыОтбора.СписокФизическихЛиц <> Неопределено Тогда	
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("СписокФизическихЛиц", ПараметрыОтбора.СписокФизическихЛиц);
		Запрос.УстановитьПараметр("ГоловнаяОрганизация", ЗарплатаКадрыПовтИсп.ГоловнаяОрганизация(Организация));
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	&ГоловнаяОрганизация КАК ГоловнаяОрганизация,
		|	ФизическиеЛица.Ссылка КАК ФизическоеЛицо
		|ПОМЕСТИТЬ ВТФильтрДляФормированияСтажа
		|ИЗ
		|	Справочник.ФизическиеЛица КАК ФизическиеЛица
		|ГДЕ
		|	ФизическиеЛица.Ссылка В(&СписокФизическихЛиц)";
		
		Запрос.Выполнить();
		
		ПараметрыОтбораСтажа.ИмяВТОтбор = "ВТФильтрДляФормированияСтажа";
		ПараметрыОтбораСтажа.ИзмеренияОтбора.Добавить("ФизическоеЛицо");
		ПараметрыОтбораСтажа.ИзмеренияОтбора.Добавить("ГоловнаяОрганизация");
		
	КонецЕсли;	
	
	Если ОтчетныйПериод >= '20130101' Тогда	
		УчетСтажаПФР.СоздатьВТДанныеУчетаСтажаПФР(МенеджерВременныхТаблиц, ПараметрыОтбораСтажа, "ВТЗаписиСтажаПоДаннымУчета");
		Возврат;
	Иначе	
		УчетСтажаПФР.СоздатьВТДанныеУчетаСтажаПФР(МенеджерВременныхТаблиц, ПараметрыОтбораСтажа, "ВТЗаписиСтажаПоДаннымУчетаВременная");	
		СоздатьВТЗаписиСтажаПоДаннымУчетаБезВыделенияГПХ(МенеджерВременныхТаблиц);
	КонецЕсли;
	
КонецПроцедуры	

Процедура СоздатьВТЗаписиСтажаПоДаннымУчетаБезВыделенияГПХ(МенеджерВременныхТаблиц)
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеУчетаСтажаПФРВременная.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДанныеУчетаСтажаПФРВременная.ТипДоговора,
	|	ДанныеУчетаСтажаПФРВременная.ДатаНачалаПериода КАК ДатаНачалаПериода,
	|	ДанныеУчетаСтажаПФРВременная.ОснованиеВыслугиЛет,
	|	ДанныеУчетаСтажаПФРВременная.ОсобыеУсловияТруда,
	|	ДанныеУчетаСтажаПФРВременная.КодПозицииСписка,
	|	ДанныеУчетаСтажаПФРВременная.ТерриториальныеУсловия,
	|	ДанныеУчетаСтажаПФРВременная.ПараметрТерриториальныхУсловий,
	|	ДанныеУчетаСтажаПФРВременная.ТретийПараметрВыслугиЛет,
	|	ДанныеУчетаСтажаПФРВременная.ДатаОкончанияПериода,
	|	ДанныеУчетаСтажаПФРВременная.ТретийПараметрИсчисляемогоСтажа,
	|	ДанныеУчетаСтажаПФРВременная.ОснованиеИсчисляемогоСтажа,
	|	ДанныеУчетаСтажаПФРВременная.ПервыйПараметрИсчисляемогоСтажа,
	|	ДанныеУчетаСтажаПФРВременная.ВторойПараметрИсчисляемогоСтажа,
	|	ДанныеУчетаСтажаПФРВременная.ПервыйПараметрВыслугиЛет,
	|	ДанныеУчетаСтажаПФРВременная.ВторойПараметрВыслугиЛет
	|ИЗ
	|	ВТЗаписиСтажаПоДаннымУчетаВременная КАК ДанныеУчетаСтажаПФРВременная
	|ГДЕ
	|	ДанныеУчетаСтажаПФРВременная.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоровСЗВ63.Трудовой)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ФизическоеЛицо,
	|	ДатаНачалаПериода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеУчетаСтажаПФРВременная.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДанныеУчетаСтажаПФРВременная.ТипДоговора,
	|	ДанныеУчетаСтажаПФРВременная.ДатаНачалаПериода КАК ДатаНачалаПериода,
	|	ДанныеУчетаСтажаПФРВременная.ОснованиеВыслугиЛет,
	|	ДанныеУчетаСтажаПФРВременная.ОсобыеУсловияТруда,
	|	ДанныеУчетаСтажаПФРВременная.КодПозицииСписка,
	|	ДанныеУчетаСтажаПФРВременная.ТерриториальныеУсловия,
	|	ДанныеУчетаСтажаПФРВременная.ПараметрТерриториальныхУсловий,
	|	ДанныеУчетаСтажаПФРВременная.ТретийПараметрВыслугиЛет,
	|	ДанныеУчетаСтажаПФРВременная.ДатаОкончанияПериода,
	|	ДанныеУчетаСтажаПФРВременная.ТретийПараметрИсчисляемогоСтажа,
	|	ДанныеУчетаСтажаПФРВременная.ОснованиеИсчисляемогоСтажа,
	|	ДанныеУчетаСтажаПФРВременная.ПервыйПараметрИсчисляемогоСтажа,
	|	ДанныеУчетаСтажаПФРВременная.ВторойПараметрИсчисляемогоСтажа,
	|	ДанныеУчетаСтажаПФРВременная.ПервыйПараметрВыслугиЛет,
	|	ДанныеУчетаСтажаПФРВременная.ВторойПараметрВыслугиЛет
	|ИЗ
	|	ВТЗаписиСтажаПоДаннымУчетаВременная КАК ДанныеУчетаСтажаПФРВременная
	|ГДЕ
	|	ДанныеУчетаСтажаПФРВременная.ТипДоговора = ЗНАЧЕНИЕ(Перечисление.ТипыДоговоровСЗВ63.ГражданскоПравовой)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ФизическоеЛицо,
	|	ДатаНачалаПериода";
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицаСтажа = Результат[0].Выгрузить();
	ТаблицаСтажаПоДоговорам = Результат[1].Выгрузить();
	
	ТаблицаСтажаПоДоговорам.Сортировать("ФизическоеЛицо,ДатаНачалаПериода");
	ПредшествующееФизЛицо = Справочники.ФизическиеЛица.ПустаяСсылка();
	МассивСтрок = Новый Массив;
	СтрокСтажа = 0;
	СтруктураПоиска = Новый Структура("ТипДоговора, ФизическоеЛицо");
	НомерСтроки = ТаблицаСтажаПоДоговорам.Количество();
	Пока НомерСтроки > 0 Цикл
		
		ТекущаяЗапись = ТаблицаСтажаПоДоговорам[НомерСтроки - 1];
		
		Если ТекущаяЗапись.ФизическоеЛицо <> ПредшествующееФизЛицо Тогда
			ЗаполнитьЗначенияСвойств(СтруктураПоиска,ТекущаяЗапись);
			МассивСтрок = ТаблицаСтажа.НайтиСтроки(СтруктураПоиска);
			ПредшествующееФизЛицо = ТекущаяЗапись.ФизическоеЛицо;
		КонецЕсли;
		
		СтрокСтажа = МассивСтрок.Количество();
		Если СтрокСтажа = 0 Тогда
			ЗаполнитьЗначенияСвойств(ТаблицаСтажа.Добавить(),ТекущаяЗапись);
			НомерСтроки = НомерСтроки - 1;
			Продолжить;
		КонецЕсли;
		
		ДатаНачалаПериода = НачалоДня(ТекущаяЗапись.ДатаНачалаПериода);
		ДатаОкончанияПериода = КонецДня(ТекущаяЗапись.ДатаОкончанияПериода);
		
		Для Сч = 1 По СтрокСтажа Цикл
			
			СтрокаСтажа = МассивСтрок[СтрокСтажа - Сч];
			ДатаНачалаСтрокиСтажа = НачалоДня(СтрокаСтажа.ДатаНачалаПериода);
			ДатаОкончанияСтрокиСтажа = КонецДня(СтрокаСтажа.ДатаОкончанияПериода);
			
			Если ДатаНачалаСтрокиСтажа > ДатаОкончанияПериода Тогда
			ИначеЕсли ДатаНачалаСтрокиСтажа > ДатаНачалаПериода Тогда
				Если ДатаОкончанияСтрокиСтажа < ДатаОкончанияПериода Тогда
					НоваяСтрока = ТаблицаСтажа.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока,ТекущаяЗапись);
					НоваяСтрока.ДатаНачалаПериода = ДатаОкончанияСтрокиСтажа + 1;
					НоваяСтрока.ДатаОкончанияПериода = ДатаОкончанияПериода;
				КонецЕсли;
				ДатаОкончанияПериода = НачалоДня(СтрокаСтажа.ДатаНачалаПериода - 1);
			Иначе
				Если ДатаОкончанияСтрокиСтажа >= ДатаНачалаПериода Тогда
					ДатаНачалаПериода = ДатаОкончанияСтрокиСтажа + 1;
				КонецЕсли;
				Прервать;
			КонецЕсли;
			МассивСтрок.Удалить(СтрокСтажа - Сч);
		КонецЦикла;
		
		Если ДатаОкончанияПериода >= ДатаНачалаПериода Тогда
			НоваяСтрока = ТаблицаСтажа.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,ТекущаяЗапись);
			НоваяСтрока.ДатаНачалаПериода = ДатаНачалаПериода;
			НоваяСтрока.ДатаОкончанияПериода = ДатаОкончанияПериода;
		КонецЕсли;
		
		НомерСтроки = НомерСтроки - 1;
	КонецЦикла;
	
	ТаблицаСтажа.Сортировать("ФизическоеЛицо, ДатаНачалаПериода");

	НомерСтроки = ТаблицаСтажа.Количество() - 1;
	Пока НомерСтроки > 0 Цикл
		
		ТекущаяЗапись = ТаблицаСтажа[НомерСтроки];
		ПредшествующаяЗапись = ТаблицаСтажа[НомерСтроки - 1];
		
		Если ТекущаяЗапись.ФизическоеЛицо = ПредшествующаяЗапись.ФизическоеЛицо // Если совпадает ФизическоеЛицо.
			И ТекущаяЗапись.ТипДоговора = ПредшествующаяЗапись.ТипДоговора
			И (ТекущаяЗапись.ДатаНачалаПериода = КонецДня(ПредшествующаяЗапись.ДатаОкончанияПериода)+1 
			// Если записи непрерывны (идут друг за другом по времени)
			ИЛИ КонецДня(ТекущаяЗапись.ДатаНачалаПериода) = КонецДня(ПредшествующаяЗапись.ДатаОкончанияПериода))
			// и совпадают параметры стажа.
			И ТекущаяЗапись.ОсобыеУсловияТруда = ПредшествующаяЗапись.ОсобыеУсловияТруда
			И СокрЛП(ТекущаяЗапись.КодПозицииСписка) = СокрЛП(ПредшествующаяЗапись.КодПозицииСписка)
			И ТекущаяЗапись.ОснованиеВыслугиЛет = ПредшествующаяЗапись.ОснованиеВыслугиЛет
			И ТекущаяЗапись.ТерриториальныеУсловия = ПредшествующаяЗапись.ТерриториальныеУсловия
			И ТекущаяЗапись.ПараметрТерриториальныхУсловий = ПредшествующаяЗапись.ПараметрТерриториальныхУсловий
			И ТекущаяЗапись.ТретийПараметрИсчисляемогоСтажа = ПредшествующаяЗапись.ТретийПараметрИсчисляемогоСтажа
			И ТекущаяЗапись.ТретийПараметрВыслугиЛет = ПредшествующаяЗапись.ТретийПараметрВыслугиЛет
			Тогда
			// объединим записи в одну
			ПредшествующаяЗапись.ДатаОкончанияПериода = ТекущаяЗапись.ДатаОкончанияПериода;
			ТаблицаСтажа.Удалить(ТекущаяЗапись);
		КонецЕсли;
		НомерСтроки = НомерСтроки - 1;
	КонецЦикла;

	Запрос.УстановитьПараметр("ПериодыРаботыСорт", ТаблицаСтажа);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИсходныйСтаж.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДоговоровСЗВ63.ПустаяСсылка) КАК ТипДоговора,
	|	ИсходныйСтаж.ДатаНачалаПериода,
	|	ИсходныйСтаж.ДатаОкончанияПериода,
	|	ИсходныйСтаж.ОсобыеУсловияТруда,
	|	ИсходныйСтаж.КодПозицииСписка,
	|	ИсходныйСтаж.ТретийПараметрИсчисляемогоСтажа,
	|	ИсходныйСтаж.ОснованиеВыслугиЛет,
	|	ИсходныйСтаж.ТерриториальныеУсловия,
	|	ИсходныйСтаж.ПараметрТерриториальныхУсловий,
	|	ИсходныйСтаж.ТретийПараметрВыслугиЛет,
	|	ЗНАЧЕНИЕ(Справочник.ОснованияИсчисляемогоСтраховогоСтажа.ПустаяСсылка) КАК ОснованиеИсчисляемогоСтажа,
	|	ЗНАЧЕНИЕ(Справочник.ЗамещениеГосударственныхМуниципальныхДолжностейПФР.ПустаяСсылка) КАК ЗамещениеГосударственныхМуниципальныхДолжностей,
	|	"""" КАК ПервыйПараметрИсчисляемогоСтажа,
	|	"""" КАК ВторойПараметрИсчисляемогоСтажа,
	|	"""" КАК ПервыйПараметрВыслугиЛет,
	|	"""" КАК ВторойПараметрВыслугиЛет,
	|	ЛОЖЬ КАК ФиксСтаж,
	|	ИСТИНА КАК СложныйСтаж
	|ПОМЕСТИТЬ ВТЗаписиСтажаПоДаннымУчета
	|ИЗ
	|	&ПериодыРаботыСорт КАК ИсходныйСтаж";

	Запрос.Выполнить();
	
КонецПроцедуры	

Функция ВидСтажаПРФ2014ПоСвойствамНачисления(КатегорияНачисления, ВидУчетаВремени, ВидПособияСоциальногоСтрахования, ВидСтажаСЗВ) Экспорт 
	ВидСтажаПерсУчета2014 = Неопределено;
	
	Если ВидСтажаСЗВ = Перечисления.ВидыСтажаСЗВ.ЧАЭС Тогда
		ВидСтажаПерсУчета2014 = Перечисления.ВидыСтажаПФР2014.ЧАЭС;
	ИначеЕсли ВидСтажаСЗВ = Перечисления.ВидыСтажаСЗВ.ПустаяСсылка() Тогда 	
		ВидСтажаПерсУчета2014 = Перечисления.ВидыСтажаПФР2014.ПустаяСсылка();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВидУчетаВремени) Тогда
		ОсновноеВремя = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидУчетаВремени, "ОсновноеВремя");
	Иначе
		ОсновноеВремя = Справочники.ВидыИспользованияРабочегоВремени.ПустаяСсылка();
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ВидПособияСоциальногоСтрахования)
		Или ОсновноеВремя = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.ОтпускПоБеременностиИРодам")
		Или ОсновноеВремя = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.Болезнь")
		Или ОсновноеВремя = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.ДополнительныйОтпуск")
		Или ОсновноеВремя = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.ОсновнойОтпуск")
		Или ОсновноеВремя = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.ОтпускПоУходуЗаРебенком")
		Или ОсновноеВремя = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.ПростойПоВинеРаботодателя")
		Или ОсновноеВремя = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.ПовышениеКвалификацииВДругойМестности")
		Или ОсновноеВремя = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.ОтпускНаОбучение")
		Или ОсновноеВремя = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.ГосударственныеОбязанности")
		Или ОсновноеВремя = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.ОтстранениеОтРаботыСОплатой")
		Или ОсновноеВремя = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.ОтстранениеОтРаботыБезОплаты") Тогда
		
		ВидСтажаПерсУчета2014 = ПорядокВключенияПериодаВСтраховойСтаж2014(ВидПособияСоциальногоСтрахования, ОсновноеВремя);
	ИначеЕсли ВидСтажаСЗВ = Перечисления.ВидыСтажаСЗВ.ВключаетсяВСтажДляДосрочногоНазначенияПенсии Тогда 	
		ВидСтажаПерсУчета2014 = Перечисления.ВидыСтажаПФР2014.ВключаетсяВСтажДляДосрочногоНазначенияПенсии;
	ИначеЕсли ВидСтажаСЗВ = Перечисления.ВидыСтажаСЗВ.ВключаетсяВСтраховойСтаж Тогда 	
		ВидСтажаПерсУчета2014 = Перечисления.ВидыСтажаПФР2014.ВключаетсяВСтраховойСтаж;
	ИначеЕсли ВидСтажаСЗВ = Перечисления.ВидыСтажаСЗВ.ВременнаяНетрудоспособность Тогда 	
		ВидСтажаПерсУчета2014 = Перечисления.ВидыСтажаПФР2014.ВременнаяНетрудоспособность;
	ИначеЕсли ВидСтажаСЗВ = Перечисления.ВидыСтажаСЗВ.Декрет Тогда 	
		ВидСтажаПерсУчета2014 = Перечисления.ВидыСтажаПФР2014.Декрет;
	ИначеЕсли ВидСтажаСЗВ = Перечисления.ВидыСтажаСЗВ.Дети Тогда 	
		ВидСтажаПерсУчета2014 = Перечисления.ВидыСтажаПФР2014.Дети;
	ИначеЕсли ВидСтажаСЗВ = Перечисления.ВидыСтажаСЗВ.ДЛДЕТИ Тогда 	
		ВидСтажаПерсУчета2014 = Перечисления.ВидыСтажаПФР2014.ДЛДЕТИ;
	ИначеЕсли ВидСтажаСЗВ = Перечисления.ВидыСтажаСЗВ.НеВключаетсяВСтраховойСтаж Тогда 	
		ВидСтажаПерсУчета2014 = Перечисления.ВидыСтажаПФР2014.НеВключаетсяВСтраховойСтаж;
	ИначеЕсли ВидСтажаСЗВ = Перечисления.ВидыСтажаСЗВ.НЕОПЛ Тогда 	
		ВидСтажаПерсУчета2014 = Перечисления.ВидыСтажаПФР2014.НЕОПЛ;
	ИначеЕсли ВидСтажаСЗВ = Перечисления.ВидыСтажаСЗВ.ОтпускБезСохраненияЗарплаты Тогда 	
		ВидСтажаПерсУчета2014 = Перечисления.ВидыСтажаПФР2014.ОтпускБезСохраненияЗарплаты;
	КонецЕсли; 
	
	Возврат ВидСтажаПерсУчета2014;
КонецФункции

Функция ПорядокВключенияПериодаВСтраховойСтаж2014(ВидПособияСоциальногоСтрахования, ОбозначениеВТабелеУчетаРабочегоВремени) Экспорт 
	
	ВидСтажа = Неопределено;
	
	Если ВидПособияСоциальногоСтрахования = Перечисления.ПереченьПособийСоциальногоСтрахования.БеременностьРоды 
		ИЛИ ОбозначениеВТабелеУчетаРабочегоВремени = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.ОтпускПоБеременностиИРодам") Тогда
		
		ВидСтажа = Перечисления.ВидыСтажаПФР2014.Декрет;
	ИначеЕсли ВидПособияСоциальногоСтрахования = Перечисления.ПереченьПособийСоциальногоСтрахования.ПоУходуЗаРебенком Тогда
		
		ВидСтажа = Перечисления.ВидыСтажаПФР2014.Дети;
	ИначеЕсли ВидПособияСоциальногоСтрахования = Перечисления.ПереченьПособийСоциальногоСтрахования.ДополнительныеВыходныеДниПоУходуЗаДетьмиИнвалидами Тогда
		
		ВидСтажа = Перечисления.ВидыСтажаПФР2014.ДОПВЫХ;
	ИначеЕсли ВидПособияСоциальногоСтрахования = Перечисления.ПереченьПособийСоциальногоСтрахования.Нетрудоспособность
		ИЛИ ВидПособияСоциальногоСтрахования = Перечисления.ПереченьПособийСоциальногоСтрахования.НетрудоспособностьНесчастныйСлучай
		ИЛИ ВидПособияСоциальногоСтрахования = Перечисления.ПереченьПособийСоциальногоСтрахования.НетрудоспособностьПрофзаболевание 
		ИЛИ ОбозначениеВТабелеУчетаРабочегоВремени = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.Болезнь") Тогда
		
		ВидСтажа = Перечисления.ВидыСтажаПФР2014.ВременнаяНетрудоспособность;
	ИначеЕсли ВидПособияСоциальногоСтрахования = Перечисления.ПереченьПособийСоциальногоСтрахования.ДополнительныеВыходныеДниПоУходуЗаДетьмиИнвалидами Тогда
		
		ВидСтажа = Перечисления.ВидыСтажаПФР2014.ВключаетсяВСтраховойСтаж;
	ИначеЕсли ВидПособияСоциальногоСтрахования = Перечисления.ПереченьПособийСоциальногоСтрахования.ДополнительныйОтпускПослеНесчастныхСлучаев
		Или ОбозначениеВТабелеУчетаРабочегоВремени = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.ДополнительныйОтпуск")
		Или ОбозначениеВТабелеУчетаРабочегоВремени = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.ОсновнойОтпуск") Тогда
		
		ВидСтажа = Перечисления.ВидыСтажаПФР2014.ДЛОТПУСК;
	ИначеЕсли ОбозначениеВТабелеУчетаРабочегоВремени = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.ОтпускПоУходуЗаРебенком") Тогда
		
		ВидСтажа = Перечисления.ВидыСтажаПФР2014.ДЛДЕТИ;
	ИначеЕсли ОбозначениеВТабелеУчетаРабочегоВремени = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.ПростойПоВинеРаботодателя") Тогда
		
		ВидСтажа = Перечисления.ВидыСтажаПФР2014.ПРОСТОЙ;
	ИначеЕсли ОбозначениеВТабелеУчетаРабочегоВремени = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.ПовышениеКвалификацииВДругойМестности") Тогда
		
		ВидСтажа = Перечисления.ВидыСтажаПФР2014.КВАЛИФ;
	ИначеЕсли ОбозначениеВТабелеУчетаРабочегоВремени = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.ОтпускНаОбучение") Тогда
		
		ВидСтажа = Перечисления.ВидыСтажаПФР2014.УЧОТПУСК;
	ИначеЕсли ОбозначениеВТабелеУчетаРабочегоВремени = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.ГосударственныеОбязанности") Тогда
		
		ВидСтажа = Перечисления.ВидыСтажаПФР2014.ОБЩЕСТ;
	ИначеЕсли ОбозначениеВТабелеУчетаРабочегоВремени = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.ОтстранениеОтРаботыСОплатой") Тогда
		
		ВидСтажа = Перечисления.ВидыСтажаПФР2014.ОТСТРАН;
	ИначеЕсли ОбозначениеВТабелеУчетаРабочегоВремени = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.ОтстранениеОтРаботыБезОплаты") Тогда
		
		ВидСтажа = Перечисления.ВидыСтажаПФР2014.НЕОПЛ;
	ИначеЕсли ОбозначениеВТабелеУчетаРабочегоВремени = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.Простой")
		ИЛИ ОбозначениеВТабелеУчетаРабочегоВремени = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.Командировка") Тогда
		
		ВидСтажа = Перечисления.ВидыСтажаПФР2014.ВключаетсяВСтраховойСтаж;
	ИначеЕсли ОбозначениеВТабелеУчетаРабочегоВремени = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.НеоплачиваемыйОтпускПоЗаконодательству")
		ИЛИ ОбозначениеВТабелеУчетаРабочегоВремени = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.НеоплачиваемыйОтпускПоРазрешениюРаботодателя") 
		ИЛИ ОбозначениеВТабелеУчетаРабочегоВремени = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.ОтпускНаОбучениеНеоплачиваемый") Тогда
		
		ВидСтажа = Перечисления.ВидыСтажаПФР2014.НЕОПЛ;
	ИначеЕсли ОбозначениеВТабелеУчетаРабочегоВремени = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.ДополнительныеВыходныеДниОплачиваемые") Тогда
		
		ВидСтажа = Перечисления.ВидыСтажаПФР2014.ВключаетсяВСтажДляДосрочногоНазначенияПенсии;
	КонецЕсли;
	
    Возврат ВидСтажа
	
КонецФункции 

Функция КвартальнаяОтчетностьПерераспределятьВзносыАвтоматически() Экспорт 
	Возврат Ложь;	
КонецФункции

Процедура КвартальнаяОтчетностьПФРДополнитьКомандыФормы(Форма) Экспорт
	Команда = Форма.Команды.Добавить("НеобходимыеКорректировкиСтажа");
	Команда.Действие = "Подключаемый_ВыполнитьНазначеннуюКоманду";
	Команда.Заголовок = НСтр("ru = 'Зарегистрированные изменения стажа'");

	Гиперссылка = Форма.Элементы.Добавить("НеобходимыеКорректировкиСтажа", Тип("КнопкаФормы"), Форма.Элементы.Шапка);
	Гиперссылка.Вид  = ВидКнопкиФормы.Гиперссылка;
	Гиперссылка.ИмяКоманды = "НеобходимыеКорректировкиСтажа";	
	
КонецПроцедуры

Процедура КвартальнаяОтчетностьПФРОбновитьДанныеФормы(Форма) Экспорт
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Форма.Организация);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НеобходимыеКорректировкиСтажаПФР.Организация
	|ИЗ
	|	РегистрСведений.НеобходимыеКорректировкиСтажаПФР КАК НеобходимыеКорректировкиСтажаПФР
	|ГДЕ
	|	НеобходимыеКорректировкиСтажаПФР.Организация = &Организация";
	
	Если Запрос.Выполнить().Пустой() Тогда
		Форма.Элементы.НеобходимыеКорректировкиСтажа.Видимость = Ложь;	
	Иначе
		Форма.Элементы.НеобходимыеКорректировкиСтажа.Видимость = Истина;	
	КонецЕсли;	
КонецПроцедуры

Функция ПараметрыДляСоздатьВТСотрудникиОрганизации(МенеджерВременныхТаблиц, Организация, ОтчетныйПериод, СписокФизическихЛиц) Экспорт
	
	ПараметрыПолученияСотрудников = ПерсонифицированныйУчетБазовый.ПараметрыДляСоздатьВТСотрудникиОрганизации(МенеджерВременныхТаблиц, Организация, ОтчетныйПериод, СписокФизическихЛиц);
	
	// Если используются договоры ГПХ, то проверяем должны ли они попадать в СЗВ_М
	Если ПараметрыПолученияСотрудников.Свойство("РаботникиПоДоговорамГПХ") Тогда
		
		ПараметрыПолученияСотрудников.Вставить("СписокСотрудников", Неопределено);
		Если СписокФизическихЛиц <> Неопределено Тогда
			ЗарплатаКадрыОбщиеНаборыДанных.ДобавитьВКоллекциюОтбор(ПараметрыПолученияСотрудников.Отборы, "ФизическоеЛицо", "В", СписокФизическихЛиц);
		КонецЕсли;
		
		// Сотрудники по трудовым договорам
		ПараметрыПолученияСотрудников.РаботникиПоДоговорамГПХ = Неопределено;
		КадровыйУчет.СоздатьВТРабочиеМестаСотрудников(МенеджерВременныхТаблиц, Истина, ПараметрыПолученияСотрудников, "ВТРаботникиПоТрудовымДоговорам");
		
		// Сотрудники по договорам ГПХ
		ПараметрыПолученияСотрудников.РаботникиПоДоговорамГПХ = Истина;
		ПараметрыПолученияСотрудников.РаботникиПоТрудовымДоговорам = Неопределено;
		КадровыйУчет.СоздатьВТРабочиеМестаСотрудников(МенеджерВременныхТаблиц, Истина, ПараметрыПолученияСотрудников, "ВТРаботникиПоДоговорамГПХ");
		
		// Соберем список физических лиц:
		// - все с трудовыми договорами
		// - с договорами ГПХ, облагаемыми страховыми взносами 
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("НеоблагаемыеВидыДоходов", УчетСтраховыхВзносовРасширенный.ВидыДоходовНеоблагаемыеСтраховымиВзносами());
		Запрос.УстановитьПараметр("ОтчетныйПериод", ОтчетныйПериод);
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Сотрудники.ФизическоеЛицо
		|ПОМЕСТИТЬ ВТФильтрФизическихЛиц
		|ИЗ
		|	ВТРаботникиПоТрудовымДоговорам КАК Сотрудники
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	УсловияДоговораГПХСрезПоследних.ФизическоеЛицо
		|ИЗ
		|	РегистрСведений.УсловияДоговораГПХ.СрезПоследних(
		|			КОНЕЦПЕРИОДА(&ОтчетныйПериод, МЕСЯЦ),
		|			(Сотрудник, ФизическоеЛицо, Договор) В
		|					(ВЫБРАТЬ
		|						Договорники.Сотрудник,
		|						Договорники.ФизическоеЛицо,
		|						Договорники.ДокументОснование
		|					ИЗ
		|						ВТРаботникиПоДоговорамГПХ КАК Договорники)
		|				И НЕ КодДоходаСтраховыеВзносы В (&НеоблагаемыеВидыДоходов)) КАК УсловияДоговораГПХСрезПоследних
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПлановыеНачисленияПоДоговорам.ФизическоеЛицо
		|ИЗ
		|	ВТРаботникиПоДоговорамГПХ КАК Договорники
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеНачисленияПоДоговорам КАК ПлановыеНачисленияПоДоговорам
		|		ПО Договорники.Сотрудник = ПлановыеНачисленияПоДоговорам.Сотрудник
		|			И Договорники.ФизическоеЛицо = ПлановыеНачисленияПоДоговорам.ФизическоеЛицо
		|			И Договорники.ДокументОснование = ПлановыеНачисленияПоДоговорам.Договор
		|			И (НЕ ПлановыеНачисленияПоДоговорам.КодДоходаСтраховыеВзносы В (&НеоблагаемыеВидыДоходов))";
		УстановитьПривилегированныйРежим(Истина);
		Запрос.Выполнить();
		УстановитьПривилегированныйРежим(Ложь);
		
		// Переопределим параметры получения сотрудников организации
		ПараметрыПолученияСотрудников = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоВременнойТаблице();
		ПараметрыПолученияСотрудников.Организация = Организация;
		ПараметрыПолученияСотрудников.НачалоПериода = НачалоМесяца(ОтчетныйПериод);
		ПараметрыПолученияСотрудников.ОкончаниеПериода = КонецМесяца(ОтчетныйПериод);
		ПараметрыПолученияСотрудников.КадровыеДанные = "ГоловнаяОрганизация, ЯвляетсяПрокурором, ЯвляетсяВоеннослужащим, РаботаетВСтуденческомОтряде, ВидЗастрахованногоЛица, СтраховойНомерПФР";
		ПараметрыПолученияСотрудников.РаботникиПоДоговорамГПХ = Истина;
		ПараметрыПолученияСотрудников.ИмяВТФизическиеЛица = "ВТФильтрФизическихЛиц";
		
		УдалитьВТ = Новый Массив;
		УдалитьВТ.Добавить("ВТРаботникиПоТрудовымДоговорам");
		УдалитьВТ.Добавить("ВТРаботникиПоДоговорамГПХ");
		ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УдалитьВТ);
		
	КонецЕсли;
	
	Возврат ПараметрыПолученияСотрудников;
	
КонецФункции

Процедура СоздатьВТДосрочноеНазначениеПенсииДляСЗВ_СТАЖ(МенеджерВременныхТаблиц, Организация, Год, ЗаписиОСтаже) Экспорт
	
	ФизическиеЛица = Новый Массив;
	  
	ОснованияВыслугиЛетСотрудников = Новый ТаблицаЗначений;
	ОснованияВыслугиЛетСотрудников.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ОснованияВыслугиЛетСотрудников.Колонки.Добавить("ОсобыеУсловияТруда", Новый ОписаниеТипов("СправочникСсылка.ОсобыеУсловияТрудаПФР,СправочникСсылка.ОсобыеУсловияТрудаДляСЗВКПФР"));
	ОснованияВыслугиЛетСотрудников.Колонки.Добавить("КодПозицииСписка", Новый ОписаниеТипов("СправочникСсылка.СпискиПрофессийДолжностейЛьготногоПенсионногоОбеспечения"));
	
	Для Каждого СтрокаСотрудника Из ЗаписиОСтаже Цикл 
		Если Не ЗначениеЗаполнено(СтрокаСотрудника.ОсобыеУсловияТруда) Тогда 
			Продолжить;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ОснованияВыслугиЛетСотрудников.Добавить(), СтрокаСотрудника);
		ФизическиеЛица.Добавить(СтрокаСотрудника.Сотрудник);
	КонецЦикла;
	
	ОснованияВыслугиЛетСотрудников.Свернуть("Сотрудник,ОсобыеУсловияТруда,КодПозицииСписка");
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ОснованияВыслугиЛетСотрудников", ОснованияВыслугиЛетСотрудников);
	
	Если ОснованияВыслугиЛетСотрудников.Количество() = 0 Тогда 
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 0
		               |	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК Подразделение,
		               |	ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка) КАК Должность,
		               |	0 КАК КоличествоРаботающих,
		               |	0 КАК КоличествоРабочихМест,
		               |	ЗНАЧЕНИЕ(Справочник.ОсобыеУсловияТрудаПФР.ПустаяСсылка) КАК ОснованиеВыслугиЛет,
		               |	ЗНАЧЕНИЕ(Справочник.СпискиПрофессийДолжностейЛьготногоПенсионногоОбеспечения.ПустаяСсылка) КАК КодПозицииСписка
		               |ПОМЕСТИТЬ ВТДосрочноеНазначениеПенсии";
		Запрос.Выполнить();
		Возврат;
	КонецЕсли;
	
	ОтчетныйПериод = Дата(Год, 1, 1);
	
	ПараметрыПолученияСотрудников = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	
	ПараметрыПолученияСотрудников.Организация = Организация;
	ПараметрыПолученияСотрудников.НачалоПериода = ОтчетныйПериод; 
	ПараметрыПолученияСотрудников.ОкончаниеПериода = КонецГода(ОтчетныйПериод); 
	ПараметрыПолученияСотрудников.КадровыеДанные = "ФизическоеЛицо, Подразделение, Должность, ДолжностьПоШтатномуРасписанию";
	
	Если ПараметрыПолученияСотрудников.Свойство("РаботникиПоДоговорамГПХ") Тогда
		ПараметрыПолученияСотрудников.РаботникиПоДоговорамГПХ = Истина;
	КонецЕсли;	
	
	ПараметрыПолученияСотрудников.СписокФизическихЛиц = ФизическиеЛица;
	
	УстановитьПривилегированныйРежим(Истина);
	КадровыйУчет.СоздатьВТСотрудникиОрганизации(Запрос.МенеджерВременныхТаблиц, Ложь, ПараметрыПолученияСотрудников);
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьШтатноеРасписание") Тогда 
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	ОснованияВыслугиЛетСотрудников.Сотрудник КАК ФизическоеЛицо,
		               |	ОснованияВыслугиЛетСотрудников.ОсобыеУсловияТруда КАК ОсобыеУсловияТруда,
		               |	ОснованияВыслугиЛетСотрудников.КодПозицииСписка КАК КодПозицииСписка
		               |ПОМЕСТИТЬ ВТОснованияВыслугиЛет
		               |ИЗ
		               |	&ОснованияВыслугиЛетСотрудников КАК ОснованияВыслугиЛетСотрудников
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	СотрудникиОрганизации.Подразделение КАК Подразделение,
		               |	СотрудникиОрганизации.Должность КАК Должность,
		               |	СотрудникиОрганизации.ФизическоеЛицо КАК ФизическоеЛицо,
		               |	0 КАК КоличествоРабочихМест,
		               |	СотрудникиОрганизации.Должность.ОсобыеУсловияТрудаПФР КАК ОсобыеУсловияТруда,
		               |	СотрудникиОрганизации.Должность.КодПозицииСпискаПФР КАК КодПозицииСписка
		               |ПОМЕСТИТЬ ВТКадровыеДанные
		               |ИЗ
		               |	ВТСотрудникиОрганизации КАК СотрудникиОрганизации
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	КадровыеДанные.Подразделение КАК Подразделение,
		               |	КадровыеДанные.Должность КАК Должность,
		               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ КадровыеДанные.ФизическоеЛицо) КАК КоличествоРаботающих,
		               |	КадровыеДанные.КоличествоРабочихМест КАК КоличествоРабочихМест,
		               |	ОснованияВыслугиЛет.ОсобыеУсловияТруда КАК ОснованиеВыслугиЛет,
		               |	ОснованияВыслугиЛет.КодПозицииСписка КАК КодПозицииСписка
		               |ПОМЕСТИТЬ ВТДосрочноеНазначениеПенсии
		               |ИЗ
		               |	ВТКадровыеДанные КАК КадровыеДанные
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОснованияВыслугиЛет КАК ОснованияВыслугиЛет
		               |		ПО КадровыеДанные.ФизическоеЛицо = ОснованияВыслугиЛет.ФизическоеЛицо
		               |			И КадровыеДанные.ОсобыеУсловияТруда = ОснованияВыслугиЛет.ОсобыеУсловияТруда
		               |			И КадровыеДанные.КодПозицииСписка = ОснованияВыслугиЛет.КодПозицииСписка
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	КадровыеДанные.Подразделение,
		               |	КадровыеДанные.Должность,
		               |	ОснованияВыслугиЛет.ОсобыеУсловияТруда,
		               |	ОснованияВыслугиЛет.КодПозицииСписка,
		               |	КадровыеДанные.КоличествоРабочихМест";
					   
		Запрос.Выполнить();
		Возврат;
	
	КонецЕсли;
	
	ПараметрыПолученияПозиций = УправлениеШтатнымРасписанием.ПараметрыПолученияПозицийШтатногоРасписания();
	ПараметрыПолученияПозиций.ДатаАктуальности = КонецГода(ОтчетныйПериод);
	ПараметрыПолученияПозиций.Организация = Организация;
	
	УстановитьПривилегированныйРежим(Истина);
	УправлениеШтатнымРасписанием.СоздатьВТПозицииШтатногоРасписания(Ложь, Запрос.МенеджерВременныхТаблиц, "ВТПозицииШтатногоРасписания", ПараметрыПолученияПозиций);
	УстановитьПривилегированныйРежим(Ложь);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПозицииШтатногоРасписания.ПозицияШтатногоРасписания.Подразделение КАК Подразделение,
	               |	ПозицииШтатногоРасписания.ПозицияШтатногоРасписания.Должность КАК Должность,
	               |	СУММА(ПозицииШтатногоРасписания.КоличествоСтавок) КАК КоличествоРабочихМест,
	               |	ПозицииШтатногоРасписания.ПозицияШтатногоРасписания.ОсобыеУсловияТрудаПФР КАК ОсобыеУсловияТруда,
	               |	ПозицииШтатногоРасписания.ПозицияШтатногоРасписания.КодПозицииСпискаПФР КАК КодПозицииСписка
	               |ПОМЕСТИТЬ ВТДанныеШтатногоРасписания
	               |ИЗ
	               |	ВТПозицииШтатногоРасписания КАК ПозицииШтатногоРасписания
	               |ГДЕ
	               |	ПозицииШтатногоРасписания.ПозицияШтатногоРасписания.ОсобыеУсловияТрудаПФР <> ЗНАЧЕНИЕ(Справочник.ОсобыеУсловияТрудаПФР.ПустаяСсылка)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПозицииШтатногоРасписания.ПозицияШтатногоРасписания.Подразделение,
	               |	ПозицииШтатногоРасписания.ПозицияШтатногоРасписания.Должность,
	               |	ПозицииШтатногоРасписания.ПозицияШтатногоРасписания.ОсобыеУсловияТрудаПФР,
	               |	ПозицииШтатногоРасписания.ПозицияШтатногоРасписания.КодПозицииСпискаПФР
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СотрудникиОрганизации.Подразделение КАК Подразделение,
	               |	СотрудникиОрганизации.Должность КАК Должность,
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СотрудникиОрганизации.ФизическоеЛицо) КАК КоличествоРаботающих,
	               |	СотрудникиОрганизации.ДолжностьПоШтатномуРасписанию.ОсобыеУсловияТрудаПФР КАК ОсобыеУсловияТруда,
	               |	СотрудникиОрганизации.ДолжностьПоШтатномуРасписанию.КодПозицииСпискаПФР КАК КодПозицииСписка
	               |ПОМЕСТИТЬ ВТДанныеСотрудников
	               |ИЗ
	               |	ВТСотрудникиОрганизации КАК СотрудникиОрганизации
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СотрудникиОрганизации.Подразделение,
	               |	СотрудникиОрганизации.Должность,
	               |	СотрудникиОрганизации.ДолжностьПоШтатномуРасписанию.ОсобыеУсловияТрудаПФР,
	               |	СотрудникиОрганизации.ДолжностьПоШтатномуРасписанию.КодПозицииСпискаПФР
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеШтатногоРасписания.Подразделение КАК Подразделение,
	               |	ДанныеШтатногоРасписания.Должность КАК Должность,
	               |	ЕСТЬNULL(ДанныеСотрудников.КоличествоРаботающих, 0) КАК КоличествоРаботающих,
	               |	ДанныеШтатногоРасписания.КоличествоРабочихМест КАК КоличествоРабочихМест,
	               |	ДанныеШтатногоРасписания.ОсобыеУсловияТруда КАК ОснованиеВыслугиЛет,
	               |	ДанныеШтатногоРасписания.КодПозицииСписка КАК КодПозицииСписка
	               |ПОМЕСТИТЬ ВТДосрочноеНазначениеПенсии
	               |ИЗ
	               |	ВТДанныеШтатногоРасписания КАК ДанныеШтатногоРасписания
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеСотрудников КАК ДанныеСотрудников
	               |		ПО ДанныеШтатногоРасписания.Подразделение = ДанныеСотрудников.Подразделение
	               |			И ДанныеШтатногоРасписания.Должность = ДанныеСотрудников.Должность
	               |			И ДанныеШтатногоРасписания.ОсобыеУсловияТруда = ДанныеСотрудников.ОсобыеУсловияТруда
	               |			И ДанныеШтатногоРасписания.КодПозицииСписка = ДанныеСотрудников.КодПозицииСписка";
				   
	Запрос.Выполнить();			   
	
КонецПроцедуры

Процедура СоздатьВТЗамещениеГосударственныхДолжностей(МенеджерВременныхТаблиц) Экспорт 
	
	Если ИспользоватьЗамещениеГосударственныхМуниципальныхДолжностей()
		И ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда 
		Модуль = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужба");
		Модуль.СоздатьВТЗамещениеГосударственныхДолжностей(МенеджерВременныхТаблиц);
	Иначе 
		ПерсонифицированныйУчетБазовый.СоздатьВТЗамещениеГосударственныхДолжностей(МенеджерВременныхТаблиц);
	КонецЕсли;
	
КонецПроцедуры

Функция ИспользоватьЗамещениеГосударственныхМуниципальныхДолжностей() Экспорт 
	
	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьГосударственнуюСлужбу") Или ПолучитьФункциональнуюОпцию("ИспользоватьМуниципальнуюСлужбу");
	
КонецФункции

Процедура ЗаполнитьДанныеДосрочногоНазначенияПенсии(Объект, Организация, ОтчетныйПериод) Экспорт
	
	ПараметрыПолученияСотрудников = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
	
	ПараметрыПолученияСотрудников.Организация = Организация;
	ПараметрыПолученияСотрудников.НачалоПериода = ОтчетныйПериод; 
	ПараметрыПолученияСотрудников.ОкончаниеПериода = ПерсонифицированныйУчетКлиентСервер.ОкончаниеОтчетногоПериодаСтажаПерсУчета(ОтчетныйПериод); 
	ПараметрыПолученияСотрудников.КадровыеДанные = "ФизическоеЛицо, Подразделение, Должность, ДолжностьПоШтатномуРасписанию";
	
	Если ПараметрыПолученияСотрудников.Свойство("РаботникиПоДоговорамГПХ") Тогда
		ПараметрыПолученияСотрудников.РаботникиПоДоговорамГПХ = Истина;
	КонецЕсли;	
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	УстановитьПривилегированныйРежим(Истина);
	КадровыйУчет.СоздатьВТСотрудникиОрганизации(МенеджерВременныхТаблиц, Ложь, ПараметрыПолученияСотрудников);
	УстановитьПривилегированныйРежим(Ложь);
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьШтатноеРасписание") Тогда 
		
		Запрос.Текст = "ВЫБРАТЬ
		               |	СотрудникиОрганизации.Подразделение,
		               |	СотрудникиОрганизации.Должность,
		               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СотрудникиОрганизации.ФизическоеЛицо) КАК КоличествоРаботающих,
		               |	0 КАК КоличествоРабочихМест,
		               |	Должности.ОснованиеДосрочногоНазначенияПенсии КАК ОснованиеВыслугиЛет,
		               |	Должности.КодПозицииСпискаПФР КАК КодПозицииСписка
		               |ИЗ
		               |	ВТСотрудникиОрганизации КАК СотрудникиОрганизации
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Должности КАК Должности
		               |		ПО СотрудникиОрганизации.Должность = Должности.Ссылка
		               |			И (Должности.ОснованиеДосрочногоНазначенияПенсии <> ЗНАЧЕНИЕ(Справочник.ОснованияДосрочногоНазначенияПенсии.ПустаяСсылка))
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	СотрудникиОрганизации.Подразделение,
		               |	СотрудникиОрганизации.Должность,
		               |	Должности.ОснованиеДосрочногоНазначенияПенсии,
		               |	Должности.КодПозицииСпискаПФР";
					   
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл 
			НоваяСтрока = Объект.ДосрочноеНазначениеПенсии.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		КонецЦикла;
		
		Возврат;
	
	КонецЕсли;
	
	ПараметрыПолученияПозиций = УправлениеШтатнымРасписанием.ПараметрыПолученияПозицийШтатногоРасписания();
	ПараметрыПолученияПозиций.ДатаАктуальности = КонецГода(ОтчетныйПериод);
	ПараметрыПолученияПозиций.Организация = Организация;
	
	УстановитьПривилегированныйРежим(Истина);
	УправлениеШтатнымРасписанием.СоздатьВТПозицииШтатногоРасписания(Ложь, Запрос.МенеджерВременныхТаблиц, "ВТПозицииШтатногоРасписания", ПараметрыПолученияПозиций);
	УстановитьПривилегированныйРежим(Ложь);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПозицииШтатногоРасписания.ПозицияШтатногоРасписания.Подразделение КАК Подразделение,
	               |	ПозицииШтатногоРасписания.ПозицияШтатногоРасписания.Должность КАК Должность,
	               |	СУММА(ПозицииШтатногоРасписания.КоличествоСтавок) КАК КоличествоРабочихМест,
	               |	ПозицииШтатногоРасписания.ПозицияШтатногоРасписания.ОснованиеДосрочногоНазначенияПенсии КАК ОснованиеВыслугиЛет,
	               |	ПозицииШтатногоРасписания.ПозицияШтатногоРасписания.КодПозицииСпискаПФР КАК КодПозицииСписка
	               |ПОМЕСТИТЬ ВТДанныеШтатногоРасписания
	               |ИЗ
	               |	ВТПозицииШтатногоРасписания КАК ПозицииШтатногоРасписания
	               |ГДЕ
	               |	ПозицииШтатногоРасписания.ПозицияШтатногоРасписания.ОснованиеДосрочногоНазначенияПенсии <> ЗНАЧЕНИЕ(Справочник.ОснованияДосрочногоНазначенияПенсии.ПустаяСсылка)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ПозицииШтатногоРасписания.ПозицияШтатногоРасписания.Подразделение,
	               |	ПозицииШтатногоРасписания.ПозицияШтатногоРасписания.Должность,
	               |	ПозицииШтатногоРасписания.ПозицияШтатногоРасписания.ОснованиеДосрочногоНазначенияПенсии,
	               |	ПозицииШтатногоРасписания.ПозицияШтатногоРасписания.КодПозицииСпискаПФР
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	СотрудникиОрганизации.Подразделение КАК Подразделение,
	               |	СотрудникиОрганизации.Должность КАК Должность,
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СотрудникиОрганизации.ФизическоеЛицо) КАК КоличествоРаботающих,
	               |	СотрудникиОрганизации.ДолжностьПоШтатномуРасписанию.ОснованиеДосрочногоНазначенияПенсии КАК ОснованиеВыслугиЛет,
	               |	СотрудникиОрганизации.ДолжностьПоШтатномуРасписанию.КодПозицииСпискаПФР КАК КодПозицииСписка
	               |ПОМЕСТИТЬ ВТДанныеСотрудников
	               |ИЗ
	               |	ВТСотрудникиОрганизации КАК СотрудникиОрганизации
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	СотрудникиОрганизации.Подразделение,
	               |	СотрудникиОрганизации.Должность,
	               |	СотрудникиОрганизации.ДолжностьПоШтатномуРасписанию.ОснованиеДосрочногоНазначенияПенсии,
	               |	СотрудникиОрганизации.ДолжностьПоШтатномуРасписанию.КодПозицииСпискаПФР
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДанныеШтатногоРасписания.Подразделение,
	               |	ДанныеШтатногоРасписания.Должность,
	               |	ЕСТЬNULL(ДанныеСотрудников.КоличествоРаботающих, 0) КАК КоличествоРаботающих,
	               |	ДанныеШтатногоРасписания.КоличествоРабочихМест,
	               |	ДанныеШтатногоРасписания.ОснованиеВыслугиЛет,
	               |	ДанныеШтатногоРасписания.КодПозицииСписка
	               |ИЗ
	               |	ВТДанныеШтатногоРасписания КАК ДанныеШтатногоРасписания
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеСотрудников КАК ДанныеСотрудников
	               |		ПО ДанныеШтатногоРасписания.Подразделение = ДанныеСотрудников.Подразделение
	               |			И ДанныеШтатногоРасписания.Должность = ДанныеСотрудников.Должность
	               |			И ДанныеШтатногоРасписания.ОснованиеВыслугиЛет = ДанныеСотрудников.ОснованиеВыслугиЛет
	               |			И ДанныеШтатногоРасписания.КодПозицииСписка = ДанныеСотрудников.КодПозицииСписка";
				   
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл 
		НоваяСтрока = Объект.ДосрочноеНазначениеПенсии.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
