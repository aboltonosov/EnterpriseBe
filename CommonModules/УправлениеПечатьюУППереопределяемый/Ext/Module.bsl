////////////////////////////////////////////////////////////////////////////////
// Подсистема "Печать".
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Переопределяет список команд печати, получаемый функцией УправлениеПечатью.КомандыПечатиФормы.
Процедура ПослеДобавленияКомандПечати(ИмяФормы, КомандыПечати, СтандартнаяОбработка) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьРоссийскиеМакеты") Тогда
		Возврат;
	КонецЕсли;	
	
	СписокОтключаемыхФорм = УправлениеПечатьюУППереопределяемыйПовтИсп.ПолучитьСписокОтключаемыхМакетов();
	СписокЛокализованныхПредставлений = УправлениеПечатьюУППереопределяемыйПовтИсп.ПолучитьСписокЛокализованныхПредставлений();
	
	// фильтр по идентификаторам
	Для НомерСтроки = -КомандыПечати.Количество() + 1 По 0 Цикл
		КомандаПечати = КомандыПечати[-НомерСтроки];
		Идентификатор = КомандаПечати.Идентификатор;
		Если СписокОтключаемыхФорм.Найти(Идентификатор) <> Неопределено Тогда
			КомандыПечати.Удалить(КомандаПечати);
		КонецЕсли;
	КонецЦикла;
	
	//переименование
	Для Каждого КомандаПечати Из КомандыПечати Цикл
		
		Идентификатор = СтрЗаменить(КомандаПечати.Идентификатор, " ", "");
		
		ЛокализованноеПредставление = СписокЛокализованныхПредставлений.Получить(Идентификатор);
		Если ЛокализованноеПредставление <> Неопределено Тогда 
			КомандаПечати.Представление = ЛокализованноеПредставление;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает макет печатной формы по полному пути к макету.
//
// Параметры:
//  ПутьКМетаданным - Строка - полный путь к макету в формате:
//								"Документ.<ИмяДокумента>"
//								"Обработка.<ИмяОбработки>"
//								"ОбщийМакет".
//  ПутьКОбъектуМетаданных - Строка - название макету в формате:
//								"<ИмяМакета>"
// Возвращаемое значение:                                        
//   Строка.
//
Процедура МакетПечатнойФормы(ПутьКМетаданным, ПутьКОбъектуМетаданных) Экспорт
	
	ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(ПутьКМетаданным);
	Если ОбъектМетаданных = Неопределено Тогда
		Возврат;
	КонецЕсли;	
	
	Если ОбъектМетаданных.Макеты.Найти(ПутьКОбъектуМетаданных + "_Локализация") <> Неопределено Тогда
		ПутьКОбъектуМетаданных = ПутьКОбъектуМетаданных + "_Локализация"; 	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
