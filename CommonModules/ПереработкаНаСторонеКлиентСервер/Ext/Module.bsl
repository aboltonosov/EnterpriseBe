////////////////////////////////////////////////////////////////////////////////
// Подсистема "Переработка на стороне".
// 
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Функция НомерГруппыЗатратВОтчетеПереработчика(Объект, СтрокаПродукция) Экспорт

	НомерГруппыЗатрат = 0;
	
	Если Объект.ПоЗаказам Тогда
		
		ИмяПоляГруппаЗатрат = ИмяПоляГруппаЗатратВОтчетеПереработчика(Объект);
	
		// Если отчет по заказу то группы затрат могут быть только из заказа
		// Если группа только одна то сразу подставим ее в текущую строку продукции
		Если Объект.Услуги.Количество() = 1 Тогда
			НомерГруппыЗатрат = Объект.Услуги[0][ИмяПоляГруппаЗатрат];
		КонецЕсли; 
		
	Иначе
		
		// Если отчет не по заказу то заполнение группы аналогично заполнению в заказе переработчику
		Если Объект.ГруппировкаЗатрат = ПредопределенноеЗначение("Перечисление.ГруппировкиЗатратВЗаказеПереработчику.ПоПродукции") Тогда
			
			Объект.МаксимальныйНомерГруппыЗатрат = Объект.МаксимальныйНомерГруппыЗатрат + 1;
			НомерГруппыЗатрат = Объект.МаксимальныйНомерГруппыЗатрат;
			
		ИначеЕсли Объект.ГруппировкаЗатрат = ПредопределенноеЗначение("Перечисление.ГруппировкиЗатратВЗаказеПереработчику.ПоСпецификациям") Тогда
			
			СтруктураПоиска = Новый Структура("Спецификация,ДокументПоступления", СтрокаПродукция.Спецификация, СтрокаПродукция.ДокументПоступления);
			СписокСтрок = Объект.Продукция.НайтиСтроки(СтруктураПоиска);
			Для каждого ДанныеСтроки Из СписокСтрок Цикл
				Если ДанныеСтроки.НомерГруппыЗатрат <> 0 Тогда
					НомерГруппыЗатрат = ДанныеСтроки.НомерГруппыЗатрат;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если НомерГруппыЗатрат = 0 Тогда
				Объект.МаксимальныйНомерГруппыЗатрат = Объект.МаксимальныйНомерГруппыЗатрат + 1;
				НомерГруппыЗатрат = Объект.МаксимальныйНомерГруппыЗатрат;
			КонецЕсли;
			
		ИначеЕсли Объект.ГруппировкаЗатрат = ПредопределенноеЗначение("Перечисление.ГруппировкиЗатратВЗаказеПереработчику.БезГруппировки") Тогда
			
			СтруктураПоиска = Новый Структура("ДокументПоступления", СтрокаПродукция.ДокументПоступления);
			СписокСтрок = Объект.Продукция.НайтиСтроки(СтруктураПоиска);
			Для каждого ДанныеСтроки Из СписокСтрок Цикл
				Если ДанныеСтроки.НомерГруппыЗатрат <> 0 Тогда
					НомерГруппыЗатрат = ДанныеСтроки.НомерГруппыЗатрат;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если НомерГруппыЗатрат = 0 Тогда
				Объект.МаксимальныйНомерГруппыЗатрат = Объект.МаксимальныйНомерГруппыЗатрат + 1;
				НомерГруппыЗатрат = Объект.МаксимальныйНомерГруппыЗатрат;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли; 

	Возврат НомерГруппыЗатрат;
	
КонецФункции

Функция ЗаголовокПоляГруппыЗатрат(ГруппировкаЗатрат) Экспорт

	Если ГруппировкаЗатрат = ПредопределенноеЗначение("Перечисление.ГруппировкиЗатратВЗаказеПереработчику.ПоПродукции") Тогда
		
		ЗаголовокГруппы = НСтр("ru = 'Продукция'");
		
	ИначеЕсли ГруппировкаЗатрат = ПредопределенноеЗначение("Перечисление.ГруппировкиЗатратВЗаказеПереработчику.ПоСпецификациям") Тогда
		
		ЗаголовокГруппы = НСтр("ru = 'Спецификация продукции'");
		
	ИначеЕсли ГруппировкаЗатрат = ПредопределенноеЗначение("Перечисление.ГруппировкиЗатратВЗаказеПереработчику.ПоЗаказамНаПроизводство") Тогда
		
		ЗаголовокГруппы = НСтр("ru = 'Заказ (этап, спецификация)'");
		
	ИначеЕсли ГруппировкаЗатрат = ПредопределенноеЗначение("Перечисление.ГруппировкиЗатратВЗаказеПереработчику.ПоЭтапамПроизводства") Тогда
		
		ЗаголовокГруппы = НСтр("ru = 'Этап производства'");
		
	КонецЕсли;

	Возврат ЗаголовокГруппы;
	
КонецФункции

Функция ИмяПоляГруппаЗатратВОтчетеПереработчика(Объект) Экспорт

	Если Объект.ГруппировкаЗатрат = ПредопределенноеЗначение("Перечисление.ГруппировкиЗатратВЗаказеПереработчику.ПоЭтапамПроизводства") Тогда
		Возврат "ЭтапПроизводства";
	Иначе
		Возврат "НомерГруппыЗатрат";
	КонецЕсли; 

КонецФункции

Функция ИмяПоляГруппаЗатратВЗаказеПереработчику(Объект) Экспорт

	Если Объект.ГруппировкаЗатрат = ПредопределенноеЗначение("Перечисление.ГруппировкиЗатратВЗаказеПереработчику.ПоЭтапамПроизводства") Тогда
		Возврат "Распоряжение";
	Иначе
		Возврат "НомерГруппыЗатрат";
	КонецЕсли;

КонецФункции

#КонецОбласти
