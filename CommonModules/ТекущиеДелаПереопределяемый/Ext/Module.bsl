////////////////////////////////////////////////////////////////////////////////
// Подсистема "Текущие дела".
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Вызывается для получения списка обработчиков для формирования (или обновления)
// списка всех текущих дел, предусмотренных в конфигурации. Обработчики - это модули
// менеджеров объектов или общие модули, в которых определена процедура ПриЗаполненииСпискаТекущихДел.
//
// Параметры:
//  Обработчики - Массив - массив ссылок на модули менеджеров или общие модули,
//                         например, Документы.ЗаказПокупателя, ТекущиеДелаПоПродажам.
//
Процедура ПриОпределенииОбработчиковТекущихДел(Обработчики) Экспорт
	
	ТекущиеДелаУТ.ПриОпределенииОбработчиковТекущихДел(Обработчики);
	
	ИнтернетПоддержкаПользователей.ПриОпределенииОбработчиковТекущихДел(Обработчики);
	
КонецПроцедуры

// Задает начальный порядок разделов в панели текущих дел.
//
// Параметры:
//  Разделы - Массив - массив разделов командного интерфейса.
//                     Разделы в панели текущих дел выводятся в
//                     том порядке, в котором они были добавлены в массив.
//
Процедура ПриОпределенииПорядкаРазделовКомандногоИнтерфейса(Разделы) Экспорт
	
	Разделы.Добавить(Метаданные.Подсистемы.CRMИМаркетинг);
	Разделы.Добавить(Метаданные.Подсистемы.Продажи);
	Разделы.Добавить(Метаданные.Подсистемы.Закупки);
	Разделы.Добавить(Метаданные.Подсистемы.Склад);
	//++ НЕ УТ
	Разделы.Добавить(Метаданные.Подсистемы.Производство);
	Разделы.Добавить(Метаданные.Подсистемы.Зарплата);
	Разделы.Добавить(Метаданные.Подсистемы.Кадры);
	//-- НЕ УТ
	Разделы.Добавить(Метаданные.Подсистемы.Казначейство);
	
	Подсистема = Планирование.ПодсистемаПланирование();
	Если Подсистема <> Неопределено Тогда
		Разделы.Добавить(Подсистема);
	КонецЕсли; 
	
	//++ НЕ УТ
	Разделы.Добавить(Метаданные.Подсистемы.РегламентированныйУчет);
	//-- НЕ УТ
	//++ НЕ УТКА
	Разделы.Добавить(Метаданные.Подсистемы.МеждународныйФинансовыйУчет);
	//-- НЕ УТКА
	Разделы.Добавить(Метаданные.Подсистемы.ФинансовыйРезультатИКонтроллинг);
	
	ИнтернетПоддержкаПользователей.ПриОпределенииПорядкаРазделовКомандногоИнтерфейса(Разделы);
	
	Разделы.Добавить(Метаданные.Подсистемы.Администрирование);
	
КонецПроцедуры

// Определяет текущие дела, которые не будут выводиться пользователю.
//
// Параметры:
//  ОтключаемыеДела - Массив - массив строк идентификаторов текущих дел, которые нужно отключать.
//
Процедура ПриОтключенииТекущихДел(ОтключаемыеДела) Экспорт
	
КонецПроцедуры

// Позволяет установить параметры запросов, общие для нескольких текущих дел.
//
// Например, если в нескольких обработчиках получения текущих дел устанавливается
// параметр "ТекущаяДата", то установку параметра можно прописать в данной
// процедуре, а в обработчике формирования дела сделать вызов процедуры
// ТекущиеДела.УстановитьОбщиеПараметрыЗапросов(), которая установить данный параметр.
//
// Параметры:
//  Запрос - выполняемый запрос.
//  ОбщиеПараметрыЗапросов - Структура - общие значения для расчета текущих дел.
//
Процедура УстановитьОбщиеПараметрыЗапросов(Запрос, ОбщиеПараметрыЗапросов) Экспорт
	
	Запрос.УстановитьПараметр("ДатаАктуальности", НачалоДня(ОбщиеПараметрыЗапросов.ТекущаяДата));
	
КонецПроцедуры

// Процедура-обработчик, которую можно вызывать в формах расшифровки текущих дел
// для переопределения параметров открытия формы и установки необходимых отборов списках формы.
//
// Параметры:
//  Форма - УправляемаяФорма
//  Список - ДинамическийСписок
//
Процедура ПриСозданииНаСервере(Форма, Список) Экспорт
	Перем СтруктураБыстрогоОтбора;
	Перем ИмяТекущейСтраницы;
	
	// Заполним структуру реквизитов формы
	СтруктураРеквизитов = Новый Структура;
	Для Каждого Реквизит Из Форма.ПолучитьРеквизиты() Цикл
		СтруктураРеквизитов.Вставить(Реквизит.Имя);
	КонецЦикла;
	
	Если Форма.Параметры.Свойство("СтруктураБыстрогоОтбора", СтруктураБыстрогоОтбора) Тогда
		
		Для Каждого КлючИЗначение Из СтруктураБыстрогоОтбора Цикл
			
			Если СтруктураРеквизитов.Свойство(КлючИЗначение.Ключ) Тогда
				
				// Элемент отбора динамического списка выведен на форму
				ОтборыСписковКлиентСервер.ОтборПоЗначениюСпискаПриСозданииНаСервере(
					Список,
					КлючИЗначение.Ключ,
					Форма[КлючИЗначение.Ключ],
					СтруктураБыстрогоОтбора);
				
			Иначе
				
				// Отбор выполняется непосредственно в свойствах динамического списка
				ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
					Список,
					КлючИЗначение.Ключ,
					КлючИЗначение.Значение,
					ВидСравненияКомпоновкиДанных.Равно,
					,
					Истина);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Установим текущий элемент формы
	Если Форма.Параметры.Свойство("ИмяТекущейСтраницы", ИмяТекущейСтраницы) Тогда
		Форма.ТекущийЭлемент = Форма.Элементы[ИмяТекущейСтраницы];
	КонецЕсли;
	
КонецПроцедуры

// Процедура-обработчик, которую можно вызывать в соответствующем обработчике форм
// расшифровки текущих дел для замены сохраненных значений реквизитов формы.
//
// Параметры:
//  Форма - УправляемаяФорма
//  Настройки - Соответствие
//
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Форма, Настройки) Экспорт
	Перем СтруктураБыстрогоОтбора;
	
	Если Форма.Параметры.Свойство("СтруктураБыстрогоОтбора", СтруктураБыстрогоОтбора) Тогда
		
		Для Каждого КлючИЗначение Из СтруктураБыстрогоОтбора Цикл
			
			ЗначениеНастройки = Настройки.Получить(КлючИЗначение.Ключ);
			
			// Если есть сохраненное значение реквизита формы и оно не совпадает со значением
			// из СтруктураБыстрогоОтбора, то заменим сохраненное значение на значение быстрого отбора.
			Если ЗначениеНастройки <> Неопределено И ЗначениеНастройки <> КлючИЗначение.Значение Тогда
				Настройки.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти