////////////////////////////////////////////////////////////////////////////////
// Подсистема "Расчет имущественных налогов".
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает необходимость уплаты авансов по налогу на имущество.
//
// Параметры:
// 	Организации - СправочникСсылка.Организации - Организация, для которой необходимо выполнить проверку.
// 	Период - Дата - Дата закрытия месяца.
//
// Возвращаемое значение:
// 	Результат - Булево - Признак необходимости уплаты аванса.
//
Функция УплачиваютсяАвансыПоНалогуНаИмущество(Организации, Период) Экспорт
	
	УплачиваютсяАвансы = Ложь;
	
	Запрос = Новый Запрос;         
	
	Запрос.УстановитьПараметр("Организации", Организации);
	Запрос.УстановитьПараметр("Период", Период);
	
	Запрос.Текст =                                     
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.НалоговыйОрган КАК НалоговыйОрган
	|ПОМЕСТИТЬ ВТ_НалоговыеОрганы
	|ИЗ
	|	(ВЫБРАТЬ
	|		СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Организация КАК Организация,
	|		СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НалоговыйОрган КАК НалоговыйОрган
	|	ИЗ
	|		РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.СрезПоследних(
	|				КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(&Период, ГОД, -1), ГОД),
	|				Организация В (&Организации)
	|					И ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)) КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Организация,
	|		СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НалоговыйОрган
	|	ИЗ
	|		РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.СрезПоследних(
	|				КОНЕЦПЕРИОДА(&Период, КВАРТАЛ),
	|				Организация В (&Организации)
	|					И ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)) КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Организация,
	|		СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НалоговыйОрган
	|	ИЗ
	|		РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам
	|	ГДЕ
	|		СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Организация В (&Организации)
	|		И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|		И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Период МЕЖДУ НАЧАЛОПЕРИОДА(&Период, ГОД) И КОНЕЦПЕРИОДА(&Период, КВАРТАЛ)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Организации.Ссылка,
	|		Организации.РегистрацияВНалоговомОргане
	|	ИЗ
	|		Справочник.Организации КАК Организации
	|	ГДЕ
	|		Организации.Ссылка В (&Организации)) КАК ВложенныйЗапрос
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	НалоговыйОрган
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.Организация КАК Организация,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.УплачиваютсяАвансы КАК УплачиваютсяАвансы
	|ПОМЕСТИТЬ ВТ_УплачиваютсяАвансыПоОрганизации
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах.СрезПоследних(
	|			&Период,
	|			Организация В (&Организации)
	|				И РегистрацияВНалоговомОргане = ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|				И Налог = ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.НалогНаИмущество)) КАК ПорядокУплатыНалоговНаМестахСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.Организация КАК Организация,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.УплачиваютсяАвансы КАК УплачиваютсяАвансы
	|ПОМЕСТИТЬ ВТ_УплачиваютсяАвансыПоИФНС
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах.СрезПоследних(
	|			&Период,
	|			Организация В (&Организации)
	|				И РегистрацияВНалоговомОргане <> ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|				И Налог = ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.НалогНаИмущество)) КАК ПорядокУплатыНалоговНаМестахСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	РегистрацияВНалоговомОргане
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(ВЫБОР
	|			КОГДА ВТ_УплачиваютсяАвансыПоИФНС.УплачиваютсяАвансы ЕСТЬ NULL 
	|				ТОГДА ВЫБОР
	|						КОГДА ВТ_УплачиваютсяАвансыПоОрганизации.УплачиваютсяАвансы ЕСТЬ NULL 
	|							ТОГДА ЛОЖЬ
	|						ИНАЧЕ ВТ_УплачиваютсяАвансыПоОрганизации.УплачиваютсяАвансы
	|					КОНЕЦ
	|			ИНАЧЕ ВТ_УплачиваютсяАвансыПоИФНС.УплачиваютсяАвансы
	|		КОНЕЦ), ЛОЖЬ) КАК УплачиваютсяАвансы
	|ИЗ
	|	ВТ_НалоговыеОрганы КАК ВТ_НалоговыеОрганы
	|		ЛЕВОЕ СОЕДИНЕНИЕ 
	|			ВТ_УплачиваютсяАвансыПоОрганизации КАК ВТ_УплачиваютсяАвансыПоОрганизации
	|		ПО 
	|			ВТ_НалоговыеОрганы.Организация = ВТ_УплачиваютсяАвансыПоОрганизации.Организация
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ 
	|			ВТ_УплачиваютсяАвансыПоИФНС КАК ВТ_УплачиваютсяАвансыПоИФНС
	|		ПО 
	|			ВТ_НалоговыеОрганы.Организация = ВТ_УплачиваютсяАвансыПоИФНС.Организация
	|			И ВТ_НалоговыеОрганы.НалоговыйОрган = ВТ_УплачиваютсяАвансыПоИФНС.РегистрацияВНалоговомОргане";
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		Пока Выборка.Следующий() Цикл
			УплачиваютсяАвансы = Выборка.УплачиваютсяАвансы;
		КонецЦикла;
		
	КонецЕсли;
		
	Возврат УплачиваютсяАвансы;
	
КонецФункции

// Возвращает результат расчета налога (или аванса по налогу) на имущество.
//
// Параметры:
// 	Организация - СправочникСсылка.Организации - Организация, для которой необходимо выполнить проверку.
// 	ПериодРасчета - Дата - Дата закрытия месяца.
//
// Возвращаемое значение:
// 	ТаблицаРасчетНалога - ТаблицаЗначений - Результат расчета налога.
//
Функция ПолучитьРасчетПоНалогуНаИмущество(Организация, ПериодРасчета) Экспорт
	
	ГодовойРасчет = Месяц(ПериодРасчета) = 12;
	
	РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, "РегистрацияВНалоговомОргане");
	
	мДатаНачалаПериодаОтчета = НачалоГода(ПериодРасчета);
	мДатаКонцаПериодаОтчета  = КонецКвартала(ПериодРасчета);
	
	ТаблицаРасчетНалога = ПустаяСправкаРасчет("РасчетНалогаНаИмущество");

	ЭтоНеЮрЛицо = НЕ ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация);
	
	Если Месяц(ПериодРасчета)%3 <> 0 ИЛИ ЭтоНеЮрЛицо Тогда
		Возврат ТаблицаРасчетНалога;
	КонецЕсли;
	
	// ПО СРЕДНЕЙ СТОИМОСТИ
	
	// Сведения о налоговых льготах в виде уменьшения суммы налога.
	ПроцентУменьшенияСуммыНалога = 0;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период",      мДатаКонцаПериодаОтчета);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕСТЬNULL(СтавкиНалогаНаИмуществоСрезПоследних.ПроцентУменьшения, 0) КАК ПроцентУменьшения
	|ИЗ
	|	РегистрСведений.СтавкиНалогаНаИмущество.СрезПоследних(&Период) КАК СтавкиНалогаНаИмуществоСрезПоследних
	|ГДЕ
	|	СтавкиНалогаНаИмуществоСрезПоследних.Организация = &Организация";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ПроцентУменьшенияСуммыНалога = РезультатЗапроса.Выгрузить()[0].ПроцентУменьшения;
	Иначе
		ПроцентУменьшенияСуммыНалога = 0;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Период",      мДатаКонцаПериодаОтчета);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	// Признак применения ЕНВД.
	ПрименениеЕНВД = УчетнаяПолитика.ПлательщикЕНВД(Организация, мДатаКонцаПериодаОтчета);
	Если ПрименениеЕНВД Тогда 
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СпособыОтраженияРасходовПоАмортизации.ОсновноеСредство,
		|	СпособыОтраженияРасходовПоАмортизации.СтатьяРасходов,
		|	СпособыОтраженияРасходовПоАмортизации.СпособОтраженияРасходовЗаданДокументом,
		|	СпособыОтраженияРасходовПоАмортизации.СпособОтраженияРасходов
		|ПОМЕСТИТЬ СпособыОтраженияРасходовПоАмортизации
		|ИЗ
		|	РегистрСведений.СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчет.СрезПоследних(&Период, Организация = &Организация) КАК СпособыОтраженияРасходовПоАмортизации
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	СпособыОтраженияРасходовПоАмортизации.СпособОтраженияРасходов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СпособыОтраженияРасходовПоАмортизации.ОсновноеСредство,
		|	ВЫБОР
		|		КОГДА СпособыОтраженияРасходовПоАмортизации.СпособОтраженияРасходовЗаданДокументом
		|			ТОГДА СпособыОтраженияРасходовПоАмортизацииТаблица.СтатьяРасходов
		|		ИНАЧЕ СпособыОтраженияРасходовПоАмортизации.СтатьяРасходов
		|	КОНЕЦ КАК СтатьяРасходов,
		|	ВЫБОР
		|		КОГДА СпособыОтраженияРасходовПоАмортизации.СпособОтраженияРасходовЗаданДокументом
		|			ТОГДА СпособыОтраженияРасходовПоАмортизацииТаблица.Коэффициент
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК Коэффициент
		|ПОМЕСТИТЬ ВТ_ВыборкаКоэффициентов
		|ИЗ
		|	СпособыОтраженияРасходовПоАмортизации КАК СпособыОтраженияРасходовПоАмортизации
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровОС.ОтражениеАмортизационныхРасходов КАК СпособыОтраженияРасходовПоАмортизацииТаблица
		|		ПО СпособыОтраженияРасходовПоАмортизации.СпособОтраженияРасходов = СпособыОтраженияРасходовПоАмортизацииТаблица.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СпособыОтраженияРасходовПоАмортизации.ОсновноеСредство КАК ОсновноеСредство,
		|	СУММА(СпособыОтраженияРасходовПоАмортизацииТаблица.Коэффициент) КАК СуммаКоэффициентов
		|ПОМЕСТИТЬ ВТ_ВыборкаСуммыКоэффициентов
		|ИЗ
		|	СпособыОтраженияРасходовПоАмортизации КАК СпособыОтраженияРасходовПоАмортизации
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровОС.ОтражениеАмортизационныхРасходов КАК СпособыОтраженияРасходовПоАмортизацииТаблица
		|		ПО СпособыОтраженияРасходовПоАмортизации.СпособОтраженияРасходов = СпособыОтраженияРасходовПоАмортизацииТаблица.Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	СпособыОтраженияРасходовПоАмортизации.ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВыборкаКоэффициентов.ОсновноеСредство КАК ОсновноеСредство,
		|	СУММА(ВЫБОР
		|			КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения)
		|				ТОГДА ВыборкаКоэффициентов.Коэффициент / ЕСТЬNULL(ВыборкаСуммыКоэффициентов.СуммаКоэффициентов, 1)
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК НеЕНВД,
		|	СУММА(ВЫБОР
		|			КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
		|				ТОГДА ВыборкаКоэффициентов.Коэффициент / ЕСТЬNULL(ВыборкаСуммыКоэффициентов.СуммаКоэффициентов, 1)
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Распределение
		|ПОМЕСТИТЬ КоэффициентыЕНВД
		|ИЗ
		|	ВТ_ВыборкаКоэффициентов КАК ВыборкаКоэффициентов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
		|		ПО ВыборкаКоэффициентов.СтатьяРасходов = СтатьиРасходов.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВыборкаСуммыКоэффициентов КАК ВыборкаСуммыКоэффициентов
		|		ПО ВыборкаКоэффициентов.ОсновноеСредство = ВыборкаСуммыКоэффициентов.ОсновноеСредство
		|
		|СГРУППИРОВАТЬ ПО
		|	ВыборкаКоэффициентов.ОсновноеСредство
		|";
		
		Запрос.Выполнить();
	КонецЕсли;
	ГоловнаяОрганизация = Организация.ГоловнаяОрганизация;
	Если Не ЗначениеЗаполнено(ГоловнаяОрганизация) Тогда
		ГоловнаяОрганизация = Организация;
	КонецЕсли;
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("КонецПериодаОтчета", КонецГода(ПериодРасчета));
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СостоянияОСОрганизаций.ОсновноеСредство КАК ОС,
	|	МИНИМУМ(СостоянияОСОрганизаций.Период) КАК ДатаСостояния
	|ПОМЕСТИТЬ ВТДвижимоеИмуществоПринятоеКУчетуПосле2013_ДатыСостояний
	|ИЗ
	|	РегистрСведений.СостоянияОСОрганизаций КАК СостоянияОСОрганизаций
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(
	|		&КонецПериодаОтчета,
	|		Организация = &Организация
	|			И НЕ ГруппаОС В (
	|				ЗНАЧЕНИЕ(Перечисление.ГруппыОС.Здания),
	|				ЗНАЧЕНИЕ(Перечисление.ГруппыОС.Сооружения), 
	|				ЗНАЧЕНИЕ(Перечисление.ГруппыОС.МноголетниеНасаждения), 
	|				ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ЗемельныеУчастки), 
	|				ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ПрочееИмуществоТребующееГосударственнойРегистрации), 
	|				ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ОбъектыПриродопользования))
	|	) КАК ПервоначальныеСведения
	|		ПО СостоянияОСОрганизаций.ОсновноеСредство = ПервоначальныеСведения.ОсновноеСредство
	|ГДЕ
	|	СостоянияОСОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|	И СостоянияОСОрганизаций.Организация В
	|			(ВЫБРАТЬ
	|				Организации.Ссылка КАК Ссылка
	|			ИЗ
	|				Справочник.Организации КАК Организации
	|			ГДЕ
	|				(Организации.Ссылка = &ГоловнаяОрганизация
	|					ИЛИ Организации.ГоловнаяОрганизация = &ГоловнаяОрганизация))
	|	И СостоянияОСОрганизаций.Период <= &КонецПериодаОтчета
	|
	|СГРУППИРОВАТЬ ПО
	|	СостоянияОСОрганизаций.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДвижимоеИмуществоПринятоеКУчетуПосле2013_ДатыСостояний.ОС
	|ПОМЕСТИТЬ ВТДвижимоеИмуществоПринятоеКУчетуПосле2013
	|ИЗ
	|	ВТДвижимоеИмуществоПринятоеКУчетуПосле2013_ДатыСостояний КАК ВТДвижимоеИмуществоПринятоеКУчетуПосле2013_ДатыСостояний
	|ГДЕ
	|	ВТДвижимоеИмуществоПринятоеКУчетуПосле2013_ДатыСостояний.ДатаСостояния >= ДАТАВРЕМЯ(2013, 1, 1)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТДвижимоеИмуществоПринятоеКУчетуПосле2013_ДатыСостояний";
	Запрос.Выполнить();
	
	ТекстОбъединяющегоЗапроса = "";
	
	Период  = мДатаНачалаПериодаОтчета;
	Счетчик = 1;
	
	Пока Период <= НачалоМесяца(ДобавитьМесяц(мДатаКонцаПериодаОтчета, 1)) Цикл
		
		ПрименениеУСН = УчетнаяПолитика.ПрименяетсяУСН(Организация,
			?(Год(Период) = Год(ПериодРасчета), КонецКвартала(Период), мДатаКонцаПериодаОтчета));
		
		Если НЕ ПрименениеУСН Тогда
			
			КонецПериода = ?(Год(Период) = Год(ПериодРасчета), Период, мДатаКонцаПериодаОтчета);
			
			Запрос.УстановитьПараметр("Период", Период);
			Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
			
			// Срез последних записей по основному средству в регистре "Ставки налога на имущество по отдельным основным средствам".
			Запрос.Текст =
			"ВЫБРАТЬ
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОсновноеСредство,
			|	МАКСИМУМ(СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Период) КАК ПериодСреза
			|ПОМЕСТИТЬ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследнихПериодов
			|ИЗ
			|	РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам
			|ГДЕ
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Период <= &КонецПериода
			|	И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Организация = &Организация
			|
			|СГРУППИРОВАТЬ ПО
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОсновноеСредство
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОсновноеСредство,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НалоговыйОрган,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ПорядокНалогообложения,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.НалоговаяБаза
			|ПОМЕСТИТЬ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних
			|ИЗ
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследнихПериодов КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследнихПериодов
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам
			|		ПО СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследнихПериодов.ОсновноеСредство = СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.ОсновноеСредство
			|			И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследнихПериодов.ПериодСреза = СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.Период
			|";
			Запрос.Выполнить();
			
			// Основные средства, находящиеся в состоянии "принято к учету".
			Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СостоянияОСОрганизаций.ОсновноеСредство КАК ОсновноеСредство
			|ПОМЕСТИТЬ
			|	ОСПринятыеКУчету
			|ИЗ
			|	РегистрСведений.СостоянияОСОрганизаций.СрезПоследних(&Период, Организация = &Организация) КАК СостоянияОСОрганизаций
			|ГДЕ
			|	СостоянияОСОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
			|	
			|ИНДЕКСИРОВАТЬ ПО
			|	ОсновноеСредство";
			Запрос.Выполнить();

			// Выборка объектов по основному налоговому органу организации (его нет в справочнике Регистрация в налоговом органе).
			Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ОС.Ссылка КАК ОсновноеСредство
			|ПОМЕСТИТЬ СписокРазрешенныхОС
			|ИЗ
			|	Справочник.ОбъектыЭксплуатации КАК ОС
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|		ОСПринятыеКУчету КАК ОСПринятыеКУчету
			|	ПО
			|		ОС.Ссылка = ОСПринятыеКУчету.ОсновноеСредство
			|		
			|ГДЕ
			|	(НЕ ОС.Ссылка В 
			|		(ВЫБРАТЬ
			|			ПервоначальныеСведения.ОсновноеСредство
			|		ИЗ
			|			РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет КАК ПервоначальныеСведения
			|		ГДЕ
			|			ПервоначальныеСведения.ГруппаОС В (
			|				ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ЗемельныеУчастки), ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ОбъектыПриродопользования))
			|			И ПервоначальныеСведения.Период < &КонецПериода
			|			И ПервоначальныеСведения.Организация = &Организация))
			|	И НЕ ОС.Ссылка В
			|				(ВЫБРАТЬ
			|					СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ОсновноеСредство
			|				ИЗ
			|					СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних
			|				ГДЕ
			|					СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.НалоговаяБаза = ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость))
			|	И НЕ ОС.Ссылка В
			|				(ВЫБРАТЬ
			|					СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ОсновноеСредство
			|				ИЗ
			|					СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних
			|				ГДЕ
			|					СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПорядокНалогообложения = ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.НеПодлежитНалогообложению))
			|ИНДЕКСИРОВАТЬ ПО
			|	ОС.Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ПервоначальныеСведенияОСБухгалтерскийУчетСрезПоследних.ОсновноеСредство КАК ОсновноеСредство,
			|	ПервоначальныеСведенияОСБухгалтерскийУчетСрезПоследних.ПорядокПогашенияСтоимости КАК ПорядокПогашенияСтоимости
			|ПОМЕСТИТЬ СписокОС
			|ИЗ
			|	РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(
			|			&Период,
			|			Организация = &Организация
			|				И ОсновноеСредство В
			|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
			|						СписокРазрешенныхОС.ОсновноеСредство
			|					ИЗ
			|						СписокРазрешенныхОС КАК СписокРазрешенныхОС)) КАК ПервоначальныеСведенияОСБухгалтерскийУчетСрезПоследних
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	ПервоначальныеСведенияОСБухгалтерскийУчетСрезПоследних.ОсновноеСредство";
			Запрос.Выполнить();
			
			// Выборка счетов учета ОС
			Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	СчетаБухгалтерскогоУчетаОССрезПоследних.СчетУчета,
			|	СчетаБухгалтерскогоУчетаОССрезПоследних.СчетНачисленияАмортизации
			|
			|ПОМЕСТИТЬ
			|	СчетаУчетаОС
			|
			|ИЗ
			|	РегистрСведений.СчетаБухгалтерскогоУчетаОС.СрезПоследних(
			|		&КонецПериода, 
			|		Организация = &Организация 
			|		И ОсновноеСредство В (ВЫБРАТЬ СписокОС.ОсновноеСредство ИЗ СписокОС)) КАК СчетаБухгалтерскогоУчетаОССрезПоследних
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	СчетаБухгалтерскогоУчета.СчетУчета,
			|	СчетаБухгалтерскогоУчета.СчетАмортизации
			|
			|ИЗ
			|	РегистрСведений.ПараметрыЦелевогоФинансированияОС.СрезПоследних(
			|		&КонецПериода, 
			|		ОсновноеСредство В (ВЫБРАТЬ СписокОС.ОсновноеСредство ИЗ СписокОС)) КАК СчетаБухгалтерскогоУчета
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВыбытиеОС),
			|	NULL
			|";
			Запрос.Выполнить();
			
			Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	МАКСИМУМ(СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.Период) КАК Период,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ОсновноеСредство КАК ОсновноеСредство
			|ПОМЕСТИТЬ ВТ_СрезПоследних
			|ИЗ
			|	РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.СрезПоследних(
			|			&КонецПериода,
			|			Организация = &Организация
			|				И ОсновноеСредство В
			|					(ВЫБРАТЬ
			|						СписокОС.ОсновноеСредство
			|					ИЗ
			|						СписокОС)) КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних
			|
			|СГРУППИРОВАТЬ ПО
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ОсновноеСредство
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	ОсновноеСредство,
			|	Период
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СрезПоследних.ОсновноеСредство,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.Организация,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.КодПоОКТМО,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПостановкаНаУчетВНалоговомОргане,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.НалоговыйОрган,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПорядокНалогообложения,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.НалоговаяСтавка,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.КодНалоговойЛьготыОсвобождениеОтНалогообложения,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.СниженнаяНалоговаяСтавка,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.КодВидаИмущества,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ДоляСтоимостиЧислитель,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ДоляСтоимостиЗнаменатель,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ДвижимоеИмуществоПодлежащееНалогообложению,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.КБК,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.УсловныйНомер,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.КадастровыйНомер,
			|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.КадастровыйНомерПомещения
			|ПОМЕСТИТЬ ВТ_СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних
			|ИЗ
			|	ВТ_СрезПоследних КАК СрезПоследних
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.СрезПоследних(
			|				&КонецПериода,
			|				Организация = &Организация
			|					И ОсновноеСредство В
			|						(ВЫБРАТЬ
			|							СписокОС.ОсновноеСредство
			|						ИЗ
			|							СписокОС)) КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних
			|		ПО СрезПоследних.Период = СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.Период
			|			И СрезПоследних.ОсновноеСредство = СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ОсновноеСредство
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СчетаБухгалтерскогоУчетаОССрезПоследних.Период,
			|	СчетаБухгалтерскогоУчетаОССрезПоследних.Регистратор,
			|	СчетаБухгалтерскогоУчетаОССрезПоследних.НомерСтроки,
			|	СчетаБухгалтерскогоУчетаОССрезПоследних.Активность,
			|	СчетаБухгалтерскогоУчетаОССрезПоследних.ОсновноеСредство,
			|	СчетаБухгалтерскогоУчетаОССрезПоследних.Организация,
			|	СчетаБухгалтерскогоУчетаОССрезПоследних.СчетУчета,
			|	СчетаБухгалтерскогоУчетаОССрезПоследних.СчетНачисленияАмортизации
			|ПОМЕСТИТЬ ВТ_СчетаБухгалтерскогоУчетаОССрезПоследних
			|ИЗ
			|	РегистрСведений.СчетаБухгалтерскогоУчетаОС.СрезПоследних(
			|			&КонецПериода,
			|			Организация = &Организация
			|				И ОсновноеСредство В
			|					(ВЫБРАТЬ
			|						СписокОС.ОсновноеСредство
			|					ИЗ
			|						СписокОС)) КАК СчетаБухгалтерскогоУчетаОССрезПоследних
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	СчетаБухгалтерскогоУчетаОССрезПоследних.Период,
			|	СчетаБухгалтерскогоУчетаОССрезПоследних.Регистратор,
			|	СчетаБухгалтерскогоУчетаОССрезПоследних.НомерСтроки,
			|	СчетаБухгалтерскогоУчетаОССрезПоследних.Активность,
			|	СчетаБухгалтерскогоУчетаОССрезПоследних.ОсновноеСредство,
			|	&Организация,
			|	СчетаБухгалтерскогоУчетаОССрезПоследних.СчетУчета,
			|	СчетаБухгалтерскогоУчетаОССрезПоследних.СчетАмортизации
			|ИЗ
			|	РегистрСведений.ПараметрыЦелевогоФинансированияОС.СрезПоследних(
			|			&КонецПериода,
			|			ОсновноеСредство В
			|					(ВЫБРАТЬ
			|						СписокОС.ОсновноеСредство
			|					ИЗ
			|						СписокОС)) КАК СчетаБухгалтерскогоУчетаОССрезПоследних
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	СчетаБухгалтерскогоУчетаОССрезПоследних.ОсновноеСредство
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ПервоначальныеСведения.Период,
			|	ПервоначальныеСведения.Регистратор,
			|	ПервоначальныеСведения.НомерСтроки,
			|	ПервоначальныеСведения.Активность,
			|	ПервоначальныеСведения.ОсновноеСредство,
			|	ПервоначальныеСведения.Организация,
			|	ПервоначальныеСведения.ИнвентарныйНомер,
			|	ПервоначальныеСведения.СпособПоступления,
			|	ПервоначальныеСведения.ПервоначальнаяСтоимость,
			|	ПервоначальныеСведения.СпособНачисленияАмортизации,
			|	ПервоначальныеСведения.ПоказательНаработки,
			|	ПервоначальныеСведения.ПорядокПогашенияСтоимости,
			|	ПервоначальныеСведения.ГруппаОС,
			|	ПервоначальныеСведения.КодПоОКОФ,
			|	ПервоначальныеСведения.ШифрПоЕНАОФ,
			|	ПервоначальныеСведения.АмортизационнаяГруппа,
			|	ПервоначальныеСведения.ДоговорЛизинга
			|ПОМЕСТИТЬ ВТ_ПервоначальныеСведенияОС
			|ИЗ
			|	РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(
			|			&Период,
			|			Организация = &Организация
			|				И ОсновноеСредство В
			|					(ВЫБРАТЬ
			|						СписокОС.ОсновноеСредство
			|					ИЗ
			|						СписокОС)) КАК ПервоначальныеСведения
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	ПервоначальныеСведения.ОсновноеСредство
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ПервоначальнаяСтоимостьОС.СуммаОстатокДт,
			|	ПервоначальнаяСтоимостьОС.Субконто1,
			|	ПервоначальнаяСтоимостьОС.Счет
			|ПОМЕСТИТЬ ВТ_ПервоначальнаяСтоимостьОС
			|ИЗ
			|	РегистрБухгалтерии.Хозрасчетный.Остатки(
			|			&Период,
			|			Счет В
			|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
			|					СчетаУчетаОС.СчетУчета
			|				ИЗ
			|					СчетаУчетаОС
			|				ГДЕ
			|					СчетаУчетаОС.СчетУчета ЕСТЬ НЕ NULL ),
			|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
			|			Организация = &Организация) КАК ПервоначальнаяСтоимостьОС
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	ПервоначальнаяСтоимостьОС.Субконто1
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СтавкиНалогаНаИмуществоСрезПоследних.Период,
			|	СтавкиНалогаНаИмуществоСрезПоследних.Организация,
			|	СтавкиНалогаНаИмуществоСрезПоследних.НалоговаяСтавка,
			|	СтавкиНалогаНаИмуществоСрезПоследних.ОсвобождениеОтНалогообложения,
			|	СтавкиНалогаНаИмуществоСрезПоследних.КодНалоговойЛьготыОсвобождениеОтНалогообложения,
			|	СтавкиНалогаНаИмуществоСрезПоследних.СнижениеНалоговойСтавки,
			|	СтавкиНалогаНаИмуществоСрезПоследних.СниженнаяНалоговаяСтавка,
			|	СтавкиНалогаНаИмуществоСрезПоследних.УменьшениеСуммыНалогаВПроцентах,
			|	СтавкиНалогаНаИмуществоСрезПоследних.ПроцентУменьшения
			|ПОМЕСТИТЬ ВТ_СтавкиНалогаНаИмуществоСрезПоследних
			|ИЗ
			|	РегистрСведений.СтавкиНалогаНаИмущество.СрезПоследних(&КонецПериода, Организация = &Организация) КАК СтавкиНалогаНаИмуществоСрезПоследних
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	АмортизацияОС.СуммаОстатокДт,
			|	АмортизацияОС.СуммаОстатокКт,
			|	АмортизацияОС.Субконто1,
			|	АмортизацияОС.Счет
			|ПОМЕСТИТЬ ВТ_АмортизацияОС
			|ИЗ
			|	РегистрБухгалтерии.Хозрасчетный.Остатки(
			|			&Период,
			|			Счет В
			|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
			|					СчетаУчетаОС.СчетНачисленияАмортизации
			|				ИЗ
			|					СчетаУчетаОС
			|				ГДЕ
			|					СчетаУчетаОС.СчетНачисленияАмортизации ЕСТЬ НЕ NULL ),
			|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства),
			|			Организация = &Организация) КАК АмортизацияОС
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	АмортизацияОС.Субконто1
			|;";
			
			Запрос.УстановитьПараметр("ОсновнойНалоговыйОрган", РеквизитыОрганизации.РегистрацияВНалоговомОргане);
			
			Если ПрименениеЕНВД Тогда
				
				// Получение коэффициента доли выручки от деятельности, облагаемой ЕНВД.
				ПериодПолученияКоэффициентаЕНВД = ?(Период = мДатаНачалаПериодаОтчета, Период, ДобавитьМесяц(Период, -1));
				КоэффициентРаспределенияЕНВД = УправлениеВнеоборотнымиАктивамиПереопределяемый.КоэффициентРаспределенияВыручкиПоВидамДеятельности(Организация, НачалоКвартала(ПериодПолученияКоэффициентаЕНВД), КонецКвартала(ПериодПолученияКоэффициентаЕНВД));
				Запрос.УстановитьПараметр("КоэффициентРаспределенияЕНВД", КоэффициентРаспределенияЕНВД);
				
				Запрос.Текст = Запрос.Текст +
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	СписокОС.ОсновноеСредство,
				|	ВЫБОР
				|		КОГДА ПервоначальныеСведенияОС.АмортизационнаяГруппа В (ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ПерваяГруппа), ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ВтораяГруппа))
				|			ТОГДА ИСТИНА
				|		ИНАЧЕ ЛОЖЬ
				|	КОНЕЦ КАК ЭтоПерваяВтораяАмортизационныеГруппы,
				|	ВЫБОР
				|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
				|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.НалоговыйОрган
				|		ИНАЧЕ &ОсновнойНалоговыйОрган
				|	КОНЕЦ КАК РегистрацияВНалоговомОргане,
				|	ВЫБОР
				|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.СДругимКодомПоОКАТО)
				|				ИЛИ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
				|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.КодПоОКТМО
				|		ИНАЧЕ ЕСТЬNULL(СтавкиНалогаНаИмуществоСрезПоследних.Организация.РегистрацияВНалоговомОргане.КодПоОКТМО, """")
				|	КОНЕЦ КАК КодПоОКТМО,
				|	ЕСТЬNULL(ВЫБОР
				|			КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПорядокНалогообложения = ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.ОблагаетсяПоСниженнойСтавке)
				|				ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.СниженнаяНалоговаяСтавка
				|			КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
				|				ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.НалоговаяСтавка
				|			ИНАЧЕ ВЫБОР
				|					КОГДА СтавкиНалогаНаИмуществоСрезПоследних.СнижениеНалоговойСтавки
				|						ТОГДА СтавкиНалогаНаИмуществоСрезПоследних.СниженнаяНалоговаяСтавка
				|					ИНАЧЕ СтавкиНалогаНаИмуществоСрезПоследних.НалоговаяСтавка
				|				КОНЕЦ
				|		КОНЕЦ, СтавкиНалогаНаИмуществоСрезПоследних.НалоговаяСтавка) КАК НалоговаяСтавка,
				|	ВЫБОР
				|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПорядокНалогообложения = ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.ОблагаетсяПоСниженнойСтавке)
				|			ТОГДА ИСТИНА
				|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
				|			ТОГДА ЛОЖЬ
				|		ИНАЧЕ ЕСТЬNULL(СтавкиНалогаНаИмуществоСрезПоследних.СнижениеНалоговойСтавки, ЛОЖЬ)
				|	КОНЕЦ КАК СнижениеНалоговойСтавки,
				|	ВЫБОР
				|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПорядокНалогообложения = ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.ОсвобождаетсяОтНалогообложения)
				|			ТОГДА ИСТИНА
				|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
				|			ТОГДА ЛОЖЬ
				|		ИНАЧЕ ЕСТЬNULL(СтавкиНалогаНаИмуществоСрезПоследних.ОсвобождениеОтНалогообложения, ЛОЖЬ)
				|	КОНЕЦ КАК ОсвобождениеОтНалогообложения,
				|	ВЫБОР
				|		КОГДА ПервоначальныеСведенияОС.ГруппаОС В (ЗНАЧЕНИЕ(Перечисление.ГруппыОС.Здания), ЗНАЧЕНИЕ(Перечисление.ГруппыОС.Сооружения), ЗНАЧЕНИЕ(Перечисление.ГруппыОС.МноголетниеНасаждения), ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ПрочееИмуществоТребующееГосударственнойРегистрации))
				|			ТОГДА ИСТИНА
				|		ИНАЧЕ ЛОЖЬ
				|	КОНЕЦ КАК ЭтоНедвижимость,
				|	ВЫБОР
				|		КОГДА СписокОС.ОсновноеСредство В
				|				(ВЫБРАТЬ
				|					Т.ОС
				|				ИЗ
				|					ВТДвижимоеИмуществоПринятоеКУчетуПосле2013 КАК Т)
				|			ТОГДА ИСТИНА
				|		ИНАЧЕ ЛОЖЬ
				|	КОНЕЦ КАК ДвижимоеИмуществоПринятоеКУчетуПосле2013,
				|	ВЫБОР
				|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПорядокНалогообложения = ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.ОсвобождаетсяОтНалогообложения)
				|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.КодНалоговойЛьготыОсвобождениеОтНалогообложения
				|		ИНАЧЕ СтавкиНалогаНаИмуществоСрезПоследних.КодНалоговойЛьготыОсвобождениеОтНалогообложения
				|	КОНЕЦ КАК КодНалоговойЛьготыОсвобождениеОтНалогообложения,
				|	ЕСТЬNULL(СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.КодВидаИмущества, ""03"") КАК КодВидаИмущества,
				|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ДоляСтоимостиЧислитель КАК ДоляСтоимостиЧислитель,
				|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ДоляСтоимостиЗнаменатель КАК ДоляСтоимостиЗнаменатель,
				|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ДвижимоеИмуществоПодлежащееНалогообложению КАК ДвижимоеИмуществоПодлежащееНалогообложению,
				|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.УсловныйНомер КАК УсловныйНомер,
				|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.КадастровыйНомер КАК КадастровыйНомер,
				|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.КадастровыйНомерПомещения КАК КадастровыйНомерПомещения,
				|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.КБК КАК КБК,
				|	&Счетчик КАК Период,
				|	ВЫРАЗИТЬ((ЕСТЬNULL(ПервоначальнаяСтоимостьОС.СуммаОстатокДт, 0) - ЕСТЬNULL(ВЫБОР
				|				КОГДА СписокОС.ПорядокПогашенияСтоимости = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзноса)
				|						ИЛИ СписокОС.ПорядокПогашенияСтоимости = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ)
				|					ТОГДА АмортизацияОС.СуммаОстатокДт
				|				ИНАЧЕ АмортизацияОС.СуммаОстатокКт
				|			КОНЕЦ, 0)) * КоэффициентыЕНВД.НеЕНВД + (ЕСТЬNULL(ПервоначальнаяСтоимостьОС.СуммаОстатокДт, 0) - ЕСТЬNULL(ВЫБОР
				|				КОГДА СписокОС.ПорядокПогашенияСтоимости = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзноса)
				|						ИЛИ СписокОС.ПорядокПогашенияСтоимости = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ)
				|					ТОГДА АмортизацияОС.СуммаОстатокДт
				|				ИНАЧЕ АмортизацияОС.СуммаОстатокКт
				|			КОНЕЦ, 0)) * КоэффициентыЕНВД.Распределение * &КоэффициентРаспределенияЕНВД КАК ЧИСЛО(15, 2)) КАК ОстаточнаяСтоимость
				|ПОМЕСТИТЬ ВыборкаСчетчик
				|ИЗ
				|	СписокОС КАК СписокОС
				|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
				|		ПО СписокОС.ОсновноеСредство = ПервоначальныеСведенияОС.ОсновноеСредство
				|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СчетаБухгалтерскогоУчетаОССрезПоследних КАК СчетаБухгалтерскогоУчетаОССрезПоследних
				|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПервоначальнаяСтоимостьОС КАК ПервоначальнаяСтоимостьОС
				|			ПО (СчетаБухгалтерскогоУчетаОССрезПоследних.СчетУчета = ПервоначальнаяСтоимостьОС.Счет
				|					ИЛИ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВыбытиеОС) = ПервоначальнаяСтоимостьОС.Счет)
				|				И СчетаБухгалтерскогоУчетаОССрезПоследних.ОсновноеСредство = ПервоначальнаяСтоимостьОС.Субконто1
				|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияОС КАК АмортизацияОС
				|			ПО СчетаБухгалтерскогоУчетаОССрезПоследних.ОсновноеСредство = АмортизацияОС.Субконто1
				|				И СчетаБухгалтерскогоУчетаОССрезПоследних.СчетНачисленияАмортизации = АмортизацияОС.Счет
				|		ПО СписокОС.ОсновноеСредство = СчетаБухгалтерскогоУчетаОССрезПоследних.ОсновноеСредство
				|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних
				|		ПО СписокОС.ОсновноеСредство = СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ОсновноеСредство
				|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчет.СрезПоследних(
				|				&Период,
				|				Организация = &Организация
				|					И ОсновноеСредство В
				|						(ВЫБРАТЬ
				|							СписокОС.ОсновноеСредство
				|						ИЗ
				|							СписокОС)) КАК СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчетСрезПоследних
				|			ЛЕВОЕ СОЕДИНЕНИЕ КоэффициентыЕНВД КАК КоэффициентыЕНВД
				|			ПО СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчетСрезПоследних.ОсновноеСредство = КоэффициентыЕНВД.ОсновноеСредство
				|		ПО СписокОС.ОсновноеСредство = СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчетСрезПоследних.ОсновноеСредство,
				|	ВТ_СтавкиНалогаНаИмуществоСрезПоследних КАК СтавкиНалогаНаИмуществоСрезПоследних";
				Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Счетчик", Счетчик);
				Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВыборкаСчетчик", "Выборка" + Формат(Счетчик, "ЧЦ=2; ЧВН="));
				Запрос.Выполнить();
				
			Иначе
				
				Запрос.Текст = Запрос.Текст +
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	СписокОС.ОсновноеСредство,
				|	ВЫБОР
				|		КОГДА ПервоначальныеСведенияОС.АмортизационнаяГруппа В (ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ПерваяГруппа), ЗНАЧЕНИЕ(Перечисление.АмортизационныеГруппы.ВтораяГруппа))
				|			ТОГДА ИСТИНА
				|		ИНАЧЕ ЛОЖЬ
				|	КОНЕЦ КАК ЭтоПерваяВтораяАмортизационныеГруппы,
				|	ВЫБОР
				|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
				|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.НалоговыйОрган
				|		ИНАЧЕ &ОсновнойНалоговыйОрган
				|	КОНЕЦ КАК РегистрацияВНалоговомОргане,
				|	ВЫБОР
				|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.СДругимКодомПоОКАТО)
				|				ИЛИ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
				|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.КодПоОКТМО
				|		ИНАЧЕ ЕСТЬNULL(СтавкиНалогаНаИмуществоСрезПоследних.Организация.РегистрацияВНалоговомОргане.КодПоОКТМО, """")
				|	КОНЕЦ КАК КодПоОКТМО,
				|	ЕСТЬNULL(ВЫБОР
				|			КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПорядокНалогообложения = ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.ОблагаетсяПоСниженнойСтавке)
				|				ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.СниженнаяНалоговаяСтавка
				|			КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
				|				ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.НалоговаяСтавка
				|			ИНАЧЕ ВЫБОР
				|					КОГДА СтавкиНалогаНаИмуществоСрезПоследних.СнижениеНалоговойСтавки
				|						ТОГДА СтавкиНалогаНаИмуществоСрезПоследних.СниженнаяНалоговаяСтавка
				|					ИНАЧЕ СтавкиНалогаНаИмуществоСрезПоследних.НалоговаяСтавка
				|				КОНЕЦ
				|		КОНЕЦ, СтавкиНалогаНаИмуществоСрезПоследних.НалоговаяСтавка) КАК НалоговаяСтавка,
				|	ВЫБОР
				|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПорядокНалогообложения = ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.ОблагаетсяПоСниженнойСтавке)
				|			ТОГДА ИСТИНА
				|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
				|			ТОГДА ЛОЖЬ
				|		ИНАЧЕ ЕСТЬNULL(СтавкиНалогаНаИмуществоСрезПоследних.СнижениеНалоговойСтавки, ЛОЖЬ)
				|	КОНЕЦ КАК СнижениеНалоговойСтавки,
				|	ВЫБОР
				|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПорядокНалогообложения = ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.ОсвобождаетсяОтНалогообложения)
				|			ТОГДА ИСТИНА
				|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
				|			ТОГДА ЛОЖЬ
				|		ИНАЧЕ ЕСТЬNULL(СтавкиНалогаНаИмуществоСрезПоследних.ОсвобождениеОтНалогообложения, ЛОЖЬ)
				|	КОНЕЦ КАК ОсвобождениеОтНалогообложения,
				|	ВЫБОР
				|		КОГДА ПервоначальныеСведенияОС.ГруппаОС В (ЗНАЧЕНИЕ(Перечисление.ГруппыОС.Здания), ЗНАЧЕНИЕ(Перечисление.ГруппыОС.Сооружения), ЗНАЧЕНИЕ(Перечисление.ГруппыОС.МноголетниеНасаждения), ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ПрочееИмуществоТребующееГосударственнойРегистрации))
				|			ТОГДА ИСТИНА
				|		ИНАЧЕ ЛОЖЬ
				|	КОНЕЦ КАК ЭтоНедвижимость,
				|	ВЫБОР
				|		КОГДА СписокОС.ОсновноеСредство В
				|				(ВЫБРАТЬ
				|					Т.ОС
				|				ИЗ
				|					ВТДвижимоеИмуществоПринятоеКУчетуПосле2013 КАК Т)
				|			ТОГДА ИСТИНА
				|		ИНАЧЕ ЛОЖЬ
				|	КОНЕЦ КАК ДвижимоеИмуществоПринятоеКУчетуПосле2013,
				|	ВЫБОР
				|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПорядокНалогообложения = ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.ОсвобождаетсяОтНалогообложения)
				|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.КодНалоговойЛьготыОсвобождениеОтНалогообложения
				|		ИНАЧЕ СтавкиНалогаНаИмуществоСрезПоследних.КодНалоговойЛьготыОсвобождениеОтНалогообложения
				|	КОНЕЦ КАК КодНалоговойЛьготыОсвобождениеОтНалогообложения,
				|	ЕСТЬNULL(СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.КодВидаИмущества, ""03"") КАК КодВидаИмущества,
				|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ДоляСтоимостиЧислитель КАК ДоляСтоимостиЧислитель,
				|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ДоляСтоимостиЗнаменатель КАК ДоляСтоимостиЗнаменатель,
				|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ДвижимоеИмуществоПодлежащееНалогообложению КАК ДвижимоеИмуществоПодлежащееНалогообложению,
				|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.УсловныйНомер КАК УсловныйНомер,
				|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.КадастровыйНомер КАК КадастровыйНомер,
				|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.КадастровыйНомерПомещения КАК КадастровыйНомерПомещения,
				|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.КБК КАК КБК,
				|	&Счетчик КАК Период,
				|	ВЫРАЗИТЬ(ЕСТЬNULL(ПервоначальнаяСтоимостьОС.СуммаОстатокДт, 0) - ЕСТЬNULL(ВЫБОР
				|				КОГДА СписокОС.ПорядокПогашенияСтоимости = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзноса)
				|						ИЛИ СписокОС.ПорядокПогашенияСтоимости = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ)
				|					ТОГДА АмортизацияОС.СуммаОстатокДт
				|				ИНАЧЕ АмортизацияОС.СуммаОстатокКт
				|			КОНЕЦ, 0) КАК ЧИСЛО(15, 2)) КАК ОстаточнаяСтоимость
				|ПОМЕСТИТЬ ВыборкаСчетчик
				|ИЗ
				|	СписокОС КАК СписокОС
				|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПервоначальныеСведенияОС КАК ПервоначальныеСведенияОС
				|		ПО СписокОС.ОсновноеСредство = ПервоначальныеСведенияОС.ОсновноеСредство
				|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СчетаБухгалтерскогоУчетаОССрезПоследних КАК СчетаБухгалтерскогоУчетаОССрезПоследних
				|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПервоначальнаяСтоимостьОС КАК ПервоначальнаяСтоимостьОС
				|			ПО (СчетаБухгалтерскогоУчетаОССрезПоследних.СчетУчета = ПервоначальнаяСтоимостьОС.Счет
				|					ИЛИ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВыбытиеОС) = ПервоначальнаяСтоимостьОС.Счет)
				|				И СчетаБухгалтерскогоУчетаОССрезПоследних.ОсновноеСредство = ПервоначальнаяСтоимостьОС.Субконто1
				|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АмортизацияОС КАК АмортизацияОС
				|			ПО СчетаБухгалтерскогоУчетаОССрезПоследних.ОсновноеСредство = АмортизацияОС.Субконто1
				|				И СчетаБухгалтерскогоУчетаОССрезПоследних.СчетНачисленияАмортизации = АмортизацияОС.Счет
				|		ПО СписокОС.ОсновноеСредство = СчетаБухгалтерскогоУчетаОССрезПоследних.ОсновноеСредство
				|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних
				|		ПО СписокОС.ОсновноеСредство = СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ОсновноеСредство,
				|	ВТ_СтавкиНалогаНаИмуществоСрезПоследних КАК СтавкиНалогаНаИмуществоСрезПоследних";
				Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Счетчик", Счетчик);
				Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВыборкаСчетчик", "Выборка" + Формат(Счетчик, "ЧЦ=2; ЧВН="));
				Запрос.Выполнить();
				
			КонецЕсли;
			
			Запрос.Текст = 
			"УНИЧТОЖИТЬ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследнихПериодов;
			|УНИЧТОЖИТЬ ОСПринятыеКУчету;
			|УНИЧТОЖИТЬ ВТ_СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних;
			|УНИЧТОЖИТЬ ВТ_СрезПоследних;
			|УНИЧТОЖИТЬ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних;
			|УНИЧТОЖИТЬ СписокОС;
			|УНИЧТОЖИТЬ СчетаУчетаОС;
			|УНИЧТОЖИТЬ ВТ_ПервоначальнаяСтоимостьОС;
			|УНИЧТОЖИТЬ ВТ_ПервоначальныеСведенияОС;
			|УНИЧТОЖИТЬ ВТ_СчетаБухгалтерскогоУчетаОССрезПоследних;
			|УНИЧТОЖИТЬ ВТ_СтавкиНалогаНаИмуществоСрезПоследних;
			|УНИЧТОЖИТЬ ВТ_АмортизацияОС;
			|УНИЧТОЖИТЬ СписокРазрешенныхОС";
			Запрос.Выполнить();
			
			ТекстОбъединяющегоЗапроса = 
				ТекстОбъединяющегоЗапроса
				+ ?(ПустаяСтрока(ТекстОбъединяющегоЗапроса), 
						"ВЫБРАТЬ РАЗРЕШЕННЫЕ *
						|",
						"ОБЪЕДИНИТЬ ВСЕ
						|ВЫБРАТЬ *
						|")
				+ ?(ПустаяСтрока(ТекстОбъединяющегоЗапроса), 
						"ПОМЕСТИТЬ НалоговаяБазаПоНалогуНаИмущество
						|",
						"")
				+ "ИЗ Выборка" + Формат(Счетчик, "ЧЦ=2; ЧВН=") + "
				|ГДЕ
				|	ОстаточнаяСтоимость > 0
				|";
			
		КонецЕсли;
		
		Счетчик = Счетчик + 1;
		Период  = ДобавитьМесяц(Период, 1);
	КонецЦикла;
	
	Если ТекстОбъединяющегоЗапроса <> "" Тогда
		
		Запрос.Текст = ТекстОбъединяющегоЗапроса;
		Запрос.Выполнить();
		
		РасчетИмущественныхНалогов.СоздатьНалоговыеОрганыСУстановленнойУплатойАвансов(
			Запрос.МенеджерВременныхТаблиц, Организация, ПериодРасчета, "НалогНаИмущество");
		
		Делитель = Месяц(ПериодРасчета) + 1;
		Запрос.УстановитьПараметр("КонечныйПериод", Делитель);
		Запрос.УстановитьПараметр("Год", Год(ПериодРасчета));
		Запрос.УстановитьПараметр("КодЛьготыДвижимоеИмуществоПринятоеКУчетуПосле2013", "2010257");
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	НалоговаяБазаПоНалогуНаИмущество.Период КАК Период,
		|	ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.КодВидаИмущества, ""03"") КАК КодВидаИмущества,
		|	ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.ДоляСтоимостиЧислитель, 0) КАК ДоляСтоимостиЧислитель,
		|	ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.ДоляСтоимостиЗнаменатель, 0) КАК ДоляСтоимостиЗнаменатель,
		|	ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.УсловныйНомер, """") КАК УсловныйНомер,
		|	ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.КадастровыйНомер, """") КАК КадастровыйНомер,
		|	ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.КадастровыйНомерПомещения, """") КАК КадастровыйНомерПомещения,
		|	ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.КБК, """") КАК КБК,
		|	НалоговаяБазаПоНалогуНаИмущество.РегистрацияВНалоговомОргане КАК ИФНС,
		|	НалоговаяБазаПоНалогуНаИмущество.КодПоОКТМО КАК КодПоОКТМО,
		|	ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.НалоговаяСтавка, 0) КАК НалоговаяСтавка,
		|	НалоговаяБазаПоНалогуНаИмущество.СнижениеНалоговойСтавки КАК ПонижениеНалоговойСтавки,
		|	ВЫБОР
		|		КОГДА НалоговаяБазаПоНалогуНаИмущество.ОсвобождениеОтНалогообложения
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ВЫБОР
		|				КОГДА &Год >= 2015
		|						И (НалоговаяБазаПоНалогуНаИмущество.ДвижимоеИмуществоПринятоеКУчетуПосле2013
		|							И НЕ ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.ДвижимоеИмуществоПодлежащееНалогообложению, ЛОЖЬ))
		|						И НЕ НалоговаяБазаПоНалогуНаИмущество.ЭтоПерваяВтораяАмортизационныеГруппы
		|					ТОГДА ИСТИНА
		|				ИНАЧЕ ЛОЖЬ
		|			КОНЕЦ
		|	КОНЕЦ КАК ОсвобождениеОтНалогообложения,
		|	ВЫБОР
		|		КОГДА НалоговаяБазаПоНалогуНаИмущество.ОсвобождениеОтНалогообложения
		|			ТОГДА НалоговаяБазаПоНалогуНаИмущество.КодНалоговойЛьготыОсвобождениеОтНалогообложения
		|		ИНАЧЕ ВЫБОР
		|				КОГДА &Год >= 2015
		|						И (НалоговаяБазаПоНалогуНаИмущество.ДвижимоеИмуществоПринятоеКУчетуПосле2013
		|							И НЕ ЕСТЬNULL(НалоговаяБазаПоНалогуНаИмущество.ДвижимоеИмуществоПодлежащееНалогообложению, ЛОЖЬ))
		|						И НЕ НалоговаяБазаПоНалогуНаИмущество.ЭтоПерваяВтораяАмортизационныеГруппы
		|					ТОГДА &КодЛьготыДвижимоеИмуществоПринятоеКУчетуПосле2013
		|				ИНАЧЕ """"
		|			КОНЕЦ
		|	КОНЕЦ КАК КодНалоговойЛьготыОсвобождениеОтНалогообложения,
		|	НалоговаяБазаПоНалогуНаИмущество.ОсновноеСредство КАК ОсновноеСредство,
		|	ВЫБОР
		|		КОГДА ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган ЕСТЬ NULL 
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК УплачиваютсяАвансы,
		|	ВЫБОР
		|		КОГДА &Год < 2015
		|					И НалоговаяБазаПоНалогуНаИмущество.ДвижимоеИмуществоПринятоеКУчетуПосле2013
		|				ИЛИ &Год >= 2015
		|					И НалоговаяБазаПоНалогуНаИмущество.ЭтоПерваяВтораяАмортизационныеГруппы
		|			ТОГДА 0
		|		ИНАЧЕ НалоговаяБазаПоНалогуНаИмущество.ОстаточнаяСтоимость
		|	КОНЕЦ КАК ОстаточнаяСтоимость,
		|	ВЫБОР
		|		КОГДА НалоговаяБазаПоНалогуНаИмущество.ЭтоНедвижимость
		|			ТОГДА НалоговаяБазаПоНалогуНаИмущество.ОстаточнаяСтоимость
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ОстаточнаяСтоимостьНедвижимости,
		|	НалоговаяБазаПоНалогуНаИмущество.ОстаточнаяСтоимость КАК ОстаточнаяСтоимостьОсновныхСредств
		|ПОМЕСТИТЬ ВТ_НалоговаяБаза
		|ИЗ
		|	НалоговаяБазаПоНалогуНаИмущество КАК НалоговаяБазаПоНалогуНаИмущество
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов КАК ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов
		|		ПО НалоговаяБазаПоНалогуНаИмущество.РегистрацияВНалоговомОргане = ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_НалоговаяБаза.КодВидаИмущества,
		|	ВТ_НалоговаяБаза.ДоляСтоимостиЧислитель,
		|	ВТ_НалоговаяБаза.ДоляСтоимостиЗнаменатель,
		|	ВТ_НалоговаяБаза.ИФНС,
		|	ВТ_НалоговаяБаза.КодПоОКТМО,
		|	ВТ_НалоговаяБаза.НалоговаяСтавка,
		|	ВТ_НалоговаяБаза.ПонижениеНалоговойСтавки,
		|	ВТ_НалоговаяБаза.ОсвобождениеОтНалогообложения,
		|	ВТ_НалоговаяБаза.КодНалоговойЛьготыОсвобождениеОтНалогообложения,
		|	ВТ_НалоговаяБаза.ОсновноеСредство,
		|	ВТ_НалоговаяБаза.УплачиваютсяАвансы,
		|	ВТ_НалоговаяБаза.УсловныйНомер,
		|	ВТ_НалоговаяБаза.КадастровыйНомер,
		|	ВТ_НалоговаяБаза.КадастровыйНомерПомещения,
		|	ВТ_НалоговаяБаза.КБК,
		|	СУММА(ВЫБОР
		|			КОГДА ВТ_НалоговаяБаза.Период = 1
		|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0101,
		|	СУММА(ВЫБОР
		|			КОГДА ВТ_НалоговаяБаза.Период = 2
		|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0102,
		|	СУММА(ВЫБОР
		|			КОГДА ВТ_НалоговаяБаза.Период = 3
		|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0103,
		|	СУММА(ВЫБОР
		|			КОГДА ВТ_НалоговаяБаза.Период = 4
		|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0104,
		|	СУММА(ВЫБОР
		|			КОГДА ВТ_НалоговаяБаза.Период = 5
		|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0105,
		|	СУММА(ВЫБОР
		|			КОГДА ВТ_НалоговаяБаза.Период = 6
		|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0106,
		|	СУММА(ВЫБОР
		|			КОГДА ВТ_НалоговаяБаза.Период = 7
		|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0107,
		|	СУММА(ВЫБОР
		|			КОГДА ВТ_НалоговаяБаза.Период = 8
		|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0108,
		|	СУММА(ВЫБОР
		|			КОГДА ВТ_НалоговаяБаза.Период = 9
		|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0109,
		|	СУММА(ВЫБОР
		|			КОГДА ВТ_НалоговаяБаза.Период = 10
		|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0110,
		|	СУММА(ВЫБОР
		|			КОГДА ВТ_НалоговаяБаза.Период = 11
		|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0111,
		|	СУММА(ВЫБОР
		|			КОГДА ВТ_НалоговаяБаза.Период = 12
		|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа0112,
		|	СУММА(ВЫБОР
		|			КОГДА ВТ_НалоговаяБаза.Период = 13
		|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимость
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОстаточнаяСтоимостьНа3112,
		|	СУММА(ВЫБОР
		|			КОГДА ВТ_НалоговаяБаза.Период = &КонечныйПериод
		|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимостьНедвижимости
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОстаточнаяСтоимостьНедвижимости,
		|	СУММА(ВЫБОР
		|			КОГДА ВТ_НалоговаяБаза.Период = &КонечныйПериод
		|				ТОГДА ВТ_НалоговаяБаза.ОстаточнаяСтоимостьОсновныхСредств
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОстаточнаяСтоимостьОсновныхСредств
		|ИЗ
		|	ВТ_НалоговаяБаза КАК ВТ_НалоговаяБаза
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_НалоговаяБаза.КодВидаИмущества,
		|	ВТ_НалоговаяБаза.ДоляСтоимостиЧислитель,
		|	ВТ_НалоговаяБаза.ДоляСтоимостиЗнаменатель,
		|	ВТ_НалоговаяБаза.ИФНС,
		|	ВТ_НалоговаяБаза.КодПоОКТМО,
		|	ВТ_НалоговаяБаза.НалоговаяСтавка,
		|	ВТ_НалоговаяБаза.ПонижениеНалоговойСтавки,
		|	ВТ_НалоговаяБаза.ОсвобождениеОтНалогообложения,
		|	ВТ_НалоговаяБаза.КодНалоговойЛьготыОсвобождениеОтНалогообложения,
		|	ВТ_НалоговаяБаза.ОсновноеСредство,
		|	ВТ_НалоговаяБаза.УплачиваютсяАвансы,
		|	ВТ_НалоговаяБаза.УсловныйНомер,
		|	ВТ_НалоговаяБаза.КадастровыйНомер,
		|	ВТ_НалоговаяБаза.КадастровыйНомерПомещения,
		|	ВТ_НалоговаяБаза.КБК";
		
		Результат = Запрос.Выполнить();
		
		Если Не Результат.Пустой() Тогда
			
			РасчетНалога = Результат.Выгрузить();
			
			КолонкиГруппировок = ""
			+ "УплачиваютсяАвансы,"
			+ "ИФНС,"
			+ "КодПоОКТМО,"
			+ "КодВидаИмущества,"
			+ "КБК,"
			+ "ДоляСтоимостиЧислитель,"
			+ "ДоляСтоимостиЗнаменатель,"
			+ "НалоговаяСтавка,"
			+ "ПонижениеНалоговойСтавки,"
			+ "ОсвобождениеОтНалогообложения,"
			+ "КодНалоговойЛьготыОсвобождениеОтНалогообложения";
			
			КолонкиСуммирования = ""
			+ "ОстаточнаяСтоимостьНа0101,"
			+ "ОстаточнаяСтоимостьНа0102,"
			+ "ОстаточнаяСтоимостьНа0103,"
			+ "ОстаточнаяСтоимостьНа0104,"
			+ "ОстаточнаяСтоимостьНа0105,"
			+ "ОстаточнаяСтоимостьНа0106,"
			+ "ОстаточнаяСтоимостьНа0107,"
			+ "ОстаточнаяСтоимостьНа0108,"
			+ "ОстаточнаяСтоимостьНа0109,"
			+ "ОстаточнаяСтоимостьНа0110,"
			+ "ОстаточнаяСтоимостьНа0111,"
			+ "ОстаточнаяСтоимостьНа0112,"
			+ "ОстаточнаяСтоимостьНа3112";
			
			КолонкиРасчета = "" 
			+ "СредняяСтоимость,"
			+ "НеоблагаемаяСредняяСтоимость,"
			+ "НалоговаяБаза,"
			+ "СуммаНалога,"
			+ "СуммаУменьшенияСуммыНалога,"
			+ "СуммаНалогаКУплате,"
			+ "СуммаАвансовыхПлатежей1Кв,"
			+ "СуммаАвансовыхПлатежей2Кв,"
			+ "СуммаАвансовыхПлатежей3Кв";
			
			РасчетНалогаПоЛистам = РасчетНалога.Скопировать();
			РасчетНалогаПоЛистам.Свернуть(КолонкиГруппировок, КолонкиСуммирования);
			
			ДопКолонки = Новый Структура(КолонкиРасчета);
			Для каждого Колонка Из ДопКолонки Цикл
				РасчетНалогаПоЛистам.Колонки.Добавить(Колонка.Ключ,
					ОбщегоНазначения.ОписаниеТипаЧисло(15, 0, ДопустимыйЗнак.Неотрицательный));
			КонецЦикла;
			
			// С целью оптимизации производительности список имён полей-периодов формируется заранее, а не динамически в циклах.
			КолонкиПериоды = Новый ТаблицаЗначений;
			КолонкиПериоды.Колонки.Добавить("Счетчик", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(2, 0, ДопустимыйЗнак.Неотрицательный)));
			КолонкиПериоды.Колонки.Добавить("ОстаточнаяСтоимостьИмя", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(25, ДопустимаяДлина.Фиксированная)));
			Для Счетчик = 1 По Делитель Цикл
				
				КолонкаПериод = КолонкиПериоды.Добавить();
				КолонкаПериод.Счетчик                = Счетчик;
				КолонкаПериод.ОстаточнаяСтоимостьИмя = "ОстаточнаяСтоимостьНа" + ?(Счетчик = 13, "3112", "01" + Формат(Счетчик, "ЧЦ=2; ЧВН="));
			
			КонецЦикла;
				
			Отбор = Новый Структура(КолонкиГруппировок);
			РасчетНалогаПоЛистам.Индексы.Добавить(КолонкиГруппировок);
				
			Для Каждого СтрокаТаблицы Из РасчетНалогаПоЛистам Цикл
				
				Если НЕ ГодовойРасчет И НЕ СтрокаТаблицы.УплачиваютсяАвансы Тогда
					Продолжить;
				КонецЕсли;
				
				Если НЕ ГодовойРасчет И (СтрокаТаблицы.КодВидаИмущества = "04" ИЛИ СтрокаТаблицы.КодВидаИмущества = "07") Тогда
					Продолжить;
				КонецЕсли;
				
				УчитыватьАвансы = ГодовойРасчет И СтрокаТаблицы.УплачиваютсяАвансы;
				
				ДоляСтоимости = 1;
				
				КодВидаИмущества = ?(ПустаяСтрока(СтрокаТаблицы.КодВидаИмущества), "03", СтрокаТаблицы.КодВидаИмущества);
				Если КодВидаИмущества = "04" Тогда
					ДоляСтоимости = 0;
				ИначеЕсли КодВидаИмущества = "02" Тогда
					ДоляСтоимости = СтрокаТаблицы.ДоляСтоимостиЧислитель / СтрокаТаблицы.ДоляСтоимостиЗнаменатель;
				КонецЕсли;
				
				ДелимоеСредняяСтоимость = 0;
				ДелимоеНеоблагаемаяСредняяСтоимость = 0;
				Для каждого КолонкаПериод Из КолонкиПериоды Цикл
					Счетчик = КолонкаПериод.Счетчик;
					
					СтрокаТаблицы[КолонкаПериод.ОстаточнаяСтоимостьИмя] = Окр(СтрокаТаблицы[КолонкаПериод.ОстаточнаяСтоимостьИмя]);
					ДелимоеСредняяСтоимость = ДелимоеСредняяСтоимость + СтрокаТаблицы[КолонкаПериод.ОстаточнаяСтоимостьИмя];
					
					Если СтрокаТаблицы.ОсвобождениеОтНалогообложения Тогда
						ДелимоеНеоблагаемаяСредняяСтоимость = ДелимоеНеоблагаемаяСредняяСтоимость + СтрокаТаблицы[КолонкаПериод.ОстаточнаяСтоимостьИмя];
					КонецЕсли;
					
					Если УчитыватьАвансы Тогда
						Если Счетчик = 4 Тогда
							СредняяСтоимость1Кв = ДелимоеСредняяСтоимость / Счетчик;
							НеоблагаемаяСредняяСтоимость1Кв = ДелимоеНеоблагаемаяСредняяСтоимость / Счетчик;
						ИначеЕсли Счетчик = 7 Тогда
							СредняяСтоимость2Кв = ДелимоеСредняяСтоимость / Счетчик;
							НеоблагаемаяСредняяСтоимость2Кв = ДелимоеНеоблагаемаяСредняяСтоимость / Счетчик;
						ИначеЕсли Счетчик = 10 Тогда
							СредняяСтоимость3Кв = ДелимоеСредняяСтоимость / Счетчик;
							НеоблагаемаяСредняяСтоимость3Кв = ДелимоеНеоблагаемаяСредняяСтоимость / Счетчик;
						КонецЕсли;			
					КонецЕсли;
					
				КонецЦикла;
				
				СтрокаТаблицы.СредняяСтоимость = Окр(ДелимоеСредняяСтоимость / Делитель);
				СтрокаТаблицы.НеоблагаемаяСредняяСтоимость = Окр(ДелимоеНеоблагаемаяСредняяСтоимость / Делитель);
				
				СтоимостьОблагаемогоНалогомИмущества = Макс(СтрокаТаблицы.СредняяСтоимость - СтрокаТаблицы.НеоблагаемаяСредняяСтоимость, 0);
				
				Если УчитыватьАвансы Тогда
					СтоимостьОблагаемогоНалогомИмущества1Кв = Макс(СредняяСтоимость1Кв - НеоблагаемаяСредняяСтоимость1Кв, 0);
					СтоимостьОблагаемогоНалогомИмущества2Кв = Макс(СредняяСтоимость2Кв - НеоблагаемаяСредняяСтоимость2Кв, 0);
					СтоимостьОблагаемогоНалогомИмущества3Кв = Макс(СредняяСтоимость3Кв - НеоблагаемаяСредняяСтоимость3Кв, 0);
				КонецЕсли;
				
				СтрокаТаблицы.НалоговаяБаза = Окр(СтоимостьОблагаемогоНалогомИмущества * ДоляСтоимости);	
				
				Если УчитыватьАвансы Тогда
					НалоговаяБаза1Кв = Окр(СтоимостьОблагаемогоНалогомИмущества1Кв * ДоляСтоимости);
					НалоговаяБаза2Кв = Окр(СтоимостьОблагаемогоНалогомИмущества2Кв * ДоляСтоимости);
					НалоговаяБаза3Кв = Окр(СтоимостьОблагаемогоНалогомИмущества3Кв * ДоляСтоимости);
				КонецЕсли;
				
				Если ГодовойРасчет Тогда
					СтрокаТаблицы.СуммаНалога = Окр(СтрокаТаблицы.НалоговаяБаза * СтрокаТаблицы.НалоговаяСтавка / 100);
				Иначе	
					СтрокаТаблицы.СуммаНалога = Окр(СтрокаТаблицы.НалоговаяБаза * СтрокаТаблицы.НалоговаяСтавка / 100 / 4);
				КонецЕсли;
				
				Если УчитыватьАвансы Тогда
					
					СтрокаТаблицы.СуммаАвансовыхПлатежей1Кв = Окр(НалоговаяБаза1Кв * СтрокаТаблицы.НалоговаяСтавка / 100 / 4);
					СтрокаТаблицы.СуммаАвансовыхПлатежей2Кв = Окр(НалоговаяБаза2Кв * СтрокаТаблицы.НалоговаяСтавка / 100 / 4);
					СтрокаТаблицы.СуммаАвансовыхПлатежей3Кв = Окр(НалоговаяБаза3Кв * СтрокаТаблицы.НалоговаяСтавка / 100 / 4);
					
				КонецЕсли;
				
				
				Если СтрокаТаблицы.ИФНС = РеквизитыОрганизации.РегистрацияВНалоговомОргане Тогда
				
					СтрокаТаблицы.СуммаУменьшенияСуммыНалога = Окр(СтрокаТаблицы.СуммаНалога * ПроцентУменьшенияСуммыНалога / 100);
					СтрокаТаблицы.СуммаНалогаКУплате = Макс(СтрокаТаблицы.СуммаНалога - СтрокаТаблицы.СуммаУменьшенияСуммыНалога, 0);
					
					Если УчитыватьАвансы Тогда
						
						СтрокаТаблицы.СуммаАвансовыхПлатежей1Кв = СтрокаТаблицы.СуммаАвансовыхПлатежей1Кв
							- Окр(СтрокаТаблицы.СуммаАвансовыхПлатежей1Кв * ПроцентУменьшенияСуммыНалога / 100);
						СтрокаТаблицы.СуммаАвансовыхПлатежей2Кв = СтрокаТаблицы.СуммаАвансовыхПлатежей2Кв
							- Окр(СтрокаТаблицы.СуммаАвансовыхПлатежей2Кв * ПроцентУменьшенияСуммыНалога / 100);
						СтрокаТаблицы.СуммаАвансовыхПлатежей3Кв = СтрокаТаблицы.СуммаАвансовыхПлатежей3Кв
							- Окр(СтрокаТаблицы.СуммаАвансовыхПлатежей3Кв * ПроцентУменьшенияСуммыНалога / 100);
							
					КонецЕсли;
					
				Иначе
					
					СтрокаТаблицы.СуммаНалогаКУплате = СтрокаТаблицы.СуммаНалога;
					
				КонецЕсли;
				
				// Распределим полученные суммы по объектам ОС
				
				ЗаполнитьЗначенияСвойств(Отбор, СтрокаТаблицы);
				РасчетНалогаПоОбъектамОС = РасчетНалога.Скопировать(Отбор);
					
				КоэффициентыРаспределения = Новый Массив;
				КоэффициентыРаспределения1Кв = Новый Массив;
				КоэффициентыРаспределения2Кв = Новый Массив;
				КоэффициентыРаспределения3Кв = Новый Массив;
				
				Для Каждого СтрокаПоОбъектуОС Из РасчетНалогаПоОбъектамОС Цикл
					
					ДелимоеСредняяСтоимость = 0;
					Для каждого КолонкаПериод Из КолонкиПериоды Цикл
						Счетчик = КолонкаПериод.Счетчик;
						
						СтрокаПоОбъектуОС[КолонкаПериод.ОстаточнаяСтоимостьИмя] = Окр(СтрокаПоОбъектуОС[КолонкаПериод.ОстаточнаяСтоимостьИмя], 2);
						ДелимоеСредняяСтоимость = ДелимоеСредняяСтоимость + СтрокаПоОбъектуОС[КолонкаПериод.ОстаточнаяСтоимостьИмя];
						
						Если УчитыватьАвансы Тогда
							Если Счетчик = 4 Тогда
								СредняяСтоимость1Кв = Окр(ДелимоеСредняяСтоимость / Счетчик, 2);
							ИначеЕсли Счетчик = 7 Тогда
								СредняяСтоимость2Кв = Окр(ДелимоеСредняяСтоимость / Счетчик, 2);
							ИначеЕсли Счетчик = 10 Тогда
								СредняяСтоимость3Кв = Окр(ДелимоеСредняяСтоимость / Счетчик, 2);
							КонецЕсли;			
						КонецЕсли;
						
					КонецЦикла;
					СредняяСтоимость = Окр(ДелимоеСредняяСтоимость / Делитель, 2);
					
					КоэффициентыРаспределения.Добавить(СредняяСтоимость);
					
					Если УчитыватьАвансы Тогда
						КоэффициентыРаспределения1Кв.Добавить(СредняяСтоимость1Кв);
						КоэффициентыРаспределения2Кв.Добавить(СредняяСтоимость2Кв);
						КоэффициентыРаспределения3Кв.Добавить(СредняяСтоимость3Кв);
					КонецЕсли;
					
				КонецЦикла;	
				
				Для каждого Колонка Из ДопКолонки Цикл
					
					РасчетНалогаПоОбъектамОС.Колонки.Добавить(Колонка.Ключ,
						ОбщегоНазначения.ОписаниеТипаЧисло(15, 2, ДопустимыйЗнак.Неотрицательный));
						
					Если Колонка.Ключ = "СуммаАвансовыхПлатежей1Кв" Тогда
						Коэффициенты = КоэффициентыРаспределения1Кв;
					ИначеЕсли Колонка.Ключ = "СуммаАвансовыхПлатежей2Кв" Тогда
						Коэффициенты = КоэффициентыРаспределения2Кв;
					ИначеЕсли Колонка.Ключ = "СуммаАвансовыхПлатежей3Кв" Тогда
						Коэффициенты = КоэффициентыРаспределения3Кв;
					Иначе
						Коэффициенты = КоэффициентыРаспределения;
					КонецЕсли;
						
					Если СтрокаТаблицы[Колонка.Ключ] > 0 Тогда
						РаспределенныеСуммы = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(
												СтрокаТаблицы[Колонка.Ключ], Коэффициенты);
						Если РаспределенныеСуммы <> Неопределено Тогда
							РасчетНалогаПоОбъектамОС.ЗагрузитьКолонку(РаспределенныеСуммы, Колонка.Ключ);
						КонецЕсли;
					КонецЕсли;	
					
				КонецЦикла;
				
				Для Каждого СтрокаПоОбъектуОС Из РасчетНалогаПоОбъектамОС Цикл
					
					НоваяСтрока = ТаблицаРасчетНалога.Добавить();
					НоваяСтрока.ВидНалоговойБазы = Перечисления.НалоговаяБазаПоНалогуНаИмущество.СреднегодоваяСтоимость;
					НоваяСтрока.КодВидаИмущества = КодВидаИмущества;
					
					СвойстваПоСтрокеТаблицы = ""
					+ "ИФНС,"
					+ "КодПоОКТМО,"
					+ "КодНалоговойЛьготыОсвобождениеОтНалогообложения,"
					+ "НалоговаяСтавка,"
					+ "КБК,"
					+ "ПонижениеНалоговойСтавки";
					
					Если КодВидаИмущества = "02" Тогда
						СвойстваПоСтрокеТаблицы = СвойстваПоСтрокеТаблицы + ","
						+ "ДоляСтоимостиЧислитель,"
						+ "ДоляСтоимостиЗнаменатель";
					КонецЕсли;
					
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы, СвойстваПоСтрокеТаблицы);
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПоОбъектуОС, ,СвойстваПоСтрокеТаблицы);
					
					Если УчитыватьАвансы Тогда
						
						НоваяСтрока.СуммаАвансовыхПлатежей = СтрокаПоОбъектуОС.СуммаАвансовыхПлатежей1Кв
															+ СтрокаПоОбъектуОС.СуммаАвансовыхПлатежей2Кв
															+ СтрокаПоОбъектуОС.СуммаАвансовыхПлатежей3Кв;
						
						НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалогаКУплате - НоваяСтрока.СуммаАвансовыхПлатежей;
						
					КонецЕсли;
					
					Если НЕ ГодовойРасчет Тогда
						
						НоваяСтрока.СуммаАвансовыхПлатежей = НоваяСтрока.СуммаНалогаКУплате;
						
					КонецЕсли;
						
				КонецЦикла;
				
			КонецЦикла;
						
		КонецЕсли;
			
	КонецЕсли;
	
	ПрименениеОСНО = УчетнаяПолитика.СистемаНалогообложения(Организация, ПериодРасчета) = Перечисления.СистемыНалогообложения.Общая;
	ПрименениеУСН = УчетнаяПолитика.СистемаНалогообложения(Организация, ПериодРасчета) = Перечисления.СистемыНалогообложения.Упрощенная;
	ПлательщикЕНВД = УчетнаяПолитика.ПлательщикЕНВД(Организация, ПериодРасчета);
	
	Если ПериодРасчета >= '20150101' ИЛИ ПрименениеОСНО 
		ИЛИ (ПериодРасчета >= '20140701' И ПериодРасчета < '20150101' И ПлательщикЕНВД) Тогда

		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц; // Хранит НалоговыеОрганыСУстановленнойУплатойАвансов
		РасчетИмущественныхНалогов.СоздатьНалоговыеОрганыСУстановленнойУплатойАвансов(
			МенеджерВременныхТаблиц, Организация, ПериодРасчета, "НалогНаИмущество");
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
		Запрос.УстановитьПараметр("ПериодРасчета", ПериодРасчета);
		Запрос.УстановитьПараметр("Организация", Организация);
		Запрос.УстановитьПараметр("ОсновнойНалоговыйОрган", РеквизитыОрганизации.РегистрацияВНалоговомОргане);
		Запрос.УстановитьПараметр("ПрименениеУСН", ПрименениеУСН);
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВЫБОР
		|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.НалоговыйОрган
		|		ИНАЧЕ &ОсновнойНалоговыйОрган
		|	КОНЕЦ КАК ИФНС,
		|	ВЫБОР
		|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.СДругимКодомПоОКАТО)
		|				ИЛИ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.КодПоОКТМО
		|		ИНАЧЕ ЕСТЬNULL(СтавкиНалогаНаИмуществоСрезПоследних.Организация.РегистрацияВНалоговомОргане.КодПоОКТМО, """")
		|	КОНЕЦ КАК КодПоОКТМО,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ОсновноеСредство,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.КадастровыйНомер КАК КадастровыйНомер,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.КадастровыйНомерПомещения КАК КадастровыйНомерПомещения,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.КадастроваяСтоимость,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.НеоблагаемаяКадастроваяСтоимость,
		|	ВЫБОР
		|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПорядокНалогообложения = ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.ОсвобождаетсяОтНалогообложения)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ОсвобождениеОтНалогообложения,
		|	ВЫБОР
		|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПорядокНалогообложения = ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.ОсвобождаетсяОтНалогообложения)
		|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.КодНалоговойЛьготыОсвобождениеОтНалогообложения
		|		ИНАЧЕ СтавкиНалогаНаИмуществоСрезПоследних.КодНалоговойЛьготыОсвобождениеОтНалогообложения
		|	КОНЕЦ КАК КодНалоговойЛьготыОсвобождениеОтНалогообложения,
		|	ВЫБОР
		|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПорядокНалогообложения = ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.ОблагаетсяПоСниженнойСтавке)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ПонижениеНалоговойСтавки,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ДоляСтоимостиЧислитель,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ДоляСтоимостиЗнаменатель,
		|	ВЫБОР
		|		КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПорядокНалогообложения = ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.ОблагаетсяПоСниженнойСтавке)
		|			ТОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.СниженнаяНалоговаяСтавка
		|		ИНАЧЕ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.НалоговаяСтавка
		|	КОНЕЦ КАК НалоговаяСтавка,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ДатаРегистрацииПраваСобственности,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ДатаПрекращенияПраваСобственности,
		|	&ПрименениеУСН
		|		ИЛИ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ИспользуетсяТолькоВДеятельностиОблагаемойЕНВД КАК ИспользуетсяТолькоВДеятельностиОблагаемойЕНВД,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.КодВидаИмущества,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ДоляВПравеОбщейСобственностиЧислитель,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ДоляВПравеОбщейСобственностиЗнаменатель,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ДоляПлощадиЧислитель,
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ДоляПлощадиЗнаменатель
		|ПОМЕСТИТЬ ОбъектыОблагаемыеПоКадастровойСтоимости
		|ИЗ
		|	РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам.СрезПоследних(КОНЕЦПЕРИОДА(&ПериодРасчета, МЕСЯЦ), Организация = &Организация) КАК СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНалогаНаИмущество.СрезПоследних(КОНЕЦПЕРИОДА(&ПериодРасчета, ГОД), Организация = &Организация) КАК СтавкиНалогаНаИмуществоСрезПоследних
		|		ПО (ИСТИНА)
		|ГДЕ
		|	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.НалоговаяБаза = ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость)
		|	И СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ПорядокНалогообложения <> ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.НеПодлежитНалогообложению)
		|	И НЕ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ДатаРегистрацииПраваСобственности > КОНЕЦПЕРИОДА(&ПериодРасчета, МЕСЯЦ)
		|	И ВЫБОР
		|			КОГДА СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ДатаПрекращенияПраваСобственности = ДАТАВРЕМЯ(1, 1, 1)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ НЕ СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствамСрезПоследних.ДатаПрекращенияПраваСобственности < НАЧАЛОПЕРИОДА(&ПериодРасчета, ГОД)
		|		КОНЕЦ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ИФНС
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОбъектыОблагаемыеПоКадастровойСтоимости.ИФНС,
		|	ОбъектыОблагаемыеПоКадастровойСтоимости.КодПоОКТМО,
		|	ОбъектыОблагаемыеПоКадастровойСтоимости.ОсновноеСредство,
		|	ОбъектыОблагаемыеПоКадастровойСтоимости.КадастровыйНомер,
		|	ОбъектыОблагаемыеПоКадастровойСтоимости.КадастровыйНомерПомещения,
		|	ОбъектыОблагаемыеПоКадастровойСтоимости.КадастроваяСтоимость,
		|	ОбъектыОблагаемыеПоКадастровойСтоимости.НеоблагаемаяКадастроваяСтоимость,
		|	ОбъектыОблагаемыеПоКадастровойСтоимости.ОсвобождениеОтНалогообложения,
		|	ОбъектыОблагаемыеПоКадастровойСтоимости.КодНалоговойЛьготыОсвобождениеОтНалогообложения,
		|	ОбъектыОблагаемыеПоКадастровойСтоимости.ПонижениеНалоговойСтавки,
		|	ОбъектыОблагаемыеПоКадастровойСтоимости.ДоляСтоимостиЧислитель,
		|	ОбъектыОблагаемыеПоКадастровойСтоимости.ДоляСтоимостиЗнаменатель,
		|	ОбъектыОблагаемыеПоКадастровойСтоимости.НалоговаяСтавка,
		|	ВЫБОР
		|		КОГДА ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган ЕСТЬ NULL
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК УплачиваютсяАвансы,
		|	ОбъектыОблагаемыеПоКадастровойСтоимости.ДатаРегистрацииПраваСобственности,
		|	ОбъектыОблагаемыеПоКадастровойСтоимости.ДатаПрекращенияПраваСобственности,
		|	ОбъектыОблагаемыеПоКадастровойСтоимости.ИспользуетсяТолькоВДеятельностиОблагаемойЕНВД,
		|	ОбъектыОблагаемыеПоКадастровойСтоимости.КодВидаИмущества,
		|	ОбъектыОблагаемыеПоКадастровойСтоимости.ДоляВПравеОбщейСобственностиЧислитель,
		|	ОбъектыОблагаемыеПоКадастровойСтоимости.ДоляВПравеОбщейСобственностиЗнаменатель,
		|	ОбъектыОблагаемыеПоКадастровойСтоимости.ДоляПлощадиЧислитель,
		|	ОбъектыОблагаемыеПоКадастровойСтоимости.ДоляПлощадиЗнаменатель
		|ИЗ
		|	ОбъектыОблагаемыеПоКадастровойСтоимости КАК ОбъектыОблагаемыеПоКадастровойСтоимости
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов КАК ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов
		|		ПО ОбъектыОблагаемыеПоКадастровойСтоимости.ИФНС = ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган";
		
		Результат = Запрос.Выполнить();
		
		Если Не Результат.Пустой() Тогда
		
			РасчетНалогаНаИмуществоПоКадастровойСтоимости = Результат.Выгрузить();
		
			Для Каждого СтрокаТаблицы Из РасчетНалогаНаИмуществоПоКадастровойСтоимости Цикл
				
				Если НЕ ГодовойРасчет И НЕ СтрокаТаблицы.УплачиваютсяАвансы Тогда
					Продолжить;
				КонецЕсли;
				
				УчитыватьАвансы = ГодовойРасчет И СтрокаТаблицы.УплачиваютсяАвансы;
				
				Если ПериодРасчета < '20140701' И СтрокаТаблицы.ИспользуетсяТолькоВДеятельностиОблагаемойЕНВД Тогда
					Продолжить;
				КонецЕсли;
				
				Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДатаРегистрацииПраваСобственности) Тогда
					Продолжить;
				КонецЕсли;
				
				ГодРасчета = Год(ПериодРасчета);
				
				Если ГодРасчета < 2016 ИЛИ ГодовойРасчет Тогда
					НачалоПериода = 1;
				Иначе
					НачалоПериода = Месяц(НачалоКвартала(ПериодРасчета));
				КонецЕсли;
				
				КоличествоМесяцевВладения = 0;
				КоличествоМесяцевВладенияЗаКвартал = 0;
				МесяцыВладения = Новый Соответствие;
				Для Сч = НачалоПериода По Месяц(ПериодРасчета) Цикл
					
					Если ГодРасчета < 2016 Тогда
						ПредельнаяДатаВозникновенияПрава = КонецМесяца(Дата(ГодРасчета, Сч, 1));
						ПредельнаяДатаПрекращенияПрава = Дата(ГодРасчета, Сч, 1);
					Иначе
						ПредельнаяДатаВозникновенияПрава = Дата(ГодРасчета, Сч, 15, 23, 59, 59);
						ПредельнаяДатаПрекращенияПрава = Дата(ГодРасчета, Сч, 16);
					КонецЕсли;
					
					Если СтрокаТаблицы.ДатаРегистрацииПраваСобственности <= ПредельнаяДатаВозникновенияПрава
						И (НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДатаПрекращенияПраваСобственности)
							ИЛИ СтрокаТаблицы.ДатаПрекращенияПраваСобственности >= ПредельнаяДатаПрекращенияПрава) Тогда
							
						КоличествоМесяцевВладения = КоличествоМесяцевВладения + 1;
						КоличествоМесяцевВладенияЗаКвартал = КоличествоМесяцевВладенияЗаКвартал + 1;
					КонецЕсли;
					
					Если ГодовойРасчет И Сч%3 = 0 Тогда
						МесяцыВладения.Вставить(Сч, КоличествоМесяцевВладенияЗаКвартал);
						
						Если ГодРасчета >= 2016 Тогда
							КоличествоМесяцевВладенияЗаКвартал = 0;
						КонецЕсли;
					КонецЕсли;
					
				КонецЦикла;
				
				Если КоличествоМесяцевВладения = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				НоваяСтрока = ТаблицаРасчетНалога.Добавить(); 
				
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
				
				НоваяСтрока.ВидНалоговойБазы = Перечисления.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость;
				
				Если СтрокаТаблицы.ОсвобождениеОтНалогообложения Тогда
					НоваяСтрока.НеоблагаемаяКадастроваяСтоимость = НоваяСтрока.КадастроваяСтоимость;
				КонецЕсли;
				
				Если ГодовойРасчет
				   И ГодРасчета = 2014
				   И ПлательщикЕНВД
				   И СтрокаТаблицы.ИспользуетсяТолькоВДеятельностиОблагаемойЕНВД Тогда
				   
					НоваяСтрока.КадастроваяСтоимость = НоваяСтрока.КадастроваяСтоимость / 2;
					НоваяСтрока.НеоблагаемаяКадастроваяСтоимость = НоваяСтрока.НеоблагаемаяКадастроваяСтоимость / 2;
					
				КонецЕсли;
				
				Если СтрокаТаблицы.ДоляСтоимостиЧислитель > 0 И СтрокаТаблицы.ДоляСтоимостиЗнаменатель > 0 Тогда
					ДоляСтоимости = СтрокаТаблицы.ДоляСтоимостиЧислитель / СтрокаТаблицы.ДоляСтоимостиЗнаменатель;	
				Иначе
					ДоляСтоимости = 1;
				КонецЕсли;
				
				Если СтрокаТаблицы.ДоляВПравеОбщейСобственностиЧислитель > 0 И СтрокаТаблицы.ДоляВПравеОбщейСобственностиЗнаменатель > 0 Тогда
					ДоляВПравеОбщейСобственности = 
						СтрокаТаблицы.ДоляВПравеОбщейСобственностиЧислитель / СтрокаТаблицы.ДоляВПравеОбщейСобственностиЗнаменатель;
				Иначе
					ДоляВПравеОбщейСобственности = 1;
				КонецЕсли;
				
				НоваяСтрока.НалоговаяБаза = Окр(Макс(НоваяСтрока.КадастроваяСтоимость - НоваяСтрока.НеоблагаемаяКадастроваяСтоимость, 0)
					* ДоляСтоимости * ДоляВПравеОбщейСобственности);
				
				НоваяСтрока.КоличествоМесяцевВладения = КоличествоМесяцевВладения;
				КоэффициентК = НоваяСтрока.КоличествоМесяцевВладения / ?(ГодРасчета < 2016 ИЛИ ГодовойРасчет, Месяц(ПериодРасчета), 3);
				
				Если ГодовойРасчет Тогда
					НоваяСтрока.СуммаНалога = Окр(НоваяСтрока.НалоговаяБаза * КоэффициентК * НоваяСтрока.НалоговаяСтавка / 100);
				Иначе
					НоваяСтрока.СуммаНалога = Окр(Макс(НоваяСтрока.КадастроваяСтоимость - НоваяСтрока.НеоблагаемаяКадастроваяСтоимость, 0)
						* ДоляСтоимости * ДоляВПравеОбщейСобственности * КоэффициентК * НоваяСтрока.НалоговаяСтавка / 100 / 4);
				КонецЕсли;
				
				Если УчитыватьАвансы Тогда
					
					КоэффициентК3 = МесяцыВладения[3] / 3;
					СуммаАвансовыхПлатежей3 = Окр(Макс(НоваяСтрока.КадастроваяСтоимость - НоваяСтрока.НеоблагаемаяКадастроваяСтоимость, 0)
						* ДоляСтоимости * ДоляВПравеОбщейСобственности * КоэффициентК3 * НоваяСтрока.НалоговаяСтавка / 100 / 4);
					
					КоэффициентК6 = МесяцыВладения[6] / ?(ГодРасчета < 2016, 6, 3);
					СуммаАвансовыхПлатежей6 = Окр(Макс(НоваяСтрока.КадастроваяСтоимость - НоваяСтрока.НеоблагаемаяКадастроваяСтоимость, 0)
						* ДоляСтоимости * ДоляВПравеОбщейСобственности * КоэффициентК6 * НоваяСтрока.НалоговаяСтавка / 100 / 4);
					
					КоэффициентК9 = МесяцыВладения[9] / ?(ГодРасчета < 2016, 9, 3);
					СуммаАвансовыхПлатежей9 = Окр(Макс(НоваяСтрока.КадастроваяСтоимость - НоваяСтрока.НеоблагаемаяКадастроваяСтоимость, 0)
						* ДоляСтоимости * ДоляВПравеОбщейСобственности * КоэффициентК9 * НоваяСтрока.НалоговаяСтавка / 100 / 4);
					
				КонецЕсли;
				
				Если НоваяСтрока.ИФНС = РеквизитыОрганизации.РегистрацияВНалоговомОргане Тогда
				
					НоваяСтрока.СуммаУменьшенияСуммыНалога = Окр(НоваяСтрока.СуммаНалога * ПроцентУменьшенияСуммыНалога / 100);
					НоваяСтрока.СуммаНалогаКУплате = Макс(НоваяСтрока.СуммаНалога - НоваяСтрока.СуммаУменьшенияСуммыНалога, 0);
					
					Если УчитыватьАвансы Тогда
						СуммаАвансовыхПлатежей3 = Макс(СуммаАвансовыхПлатежей3
							- Окр(СуммаАвансовыхПлатежей3 * ПроцентУменьшенияСуммыНалога / 100), 0);
						СуммаАвансовыхПлатежей6 = Макс(СуммаАвансовыхПлатежей6
							- Окр(СуммаАвансовыхПлатежей6 * ПроцентУменьшенияСуммыНалога / 100), 0);
						СуммаАвансовыхПлатежей9 = Макс(СуммаАвансовыхПлатежей9
							- Окр(СуммаАвансовыхПлатежей9 * ПроцентУменьшенияСуммыНалога / 100), 0);
					КонецЕсли;
					
				Иначе
					
					НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалога;
					
				КонецЕсли;
				
				Если УчитыватьАвансы Тогда
					
					Если ГодРасчета = 2014
					   И ПлательщикЕНВД
					   И СтрокаТаблицы.ИспользуетсяТолькоВДеятельностиОблагаемойЕНВД Тогда
						НоваяСтрока.СуммаАвансовыхПлатежей = 2 * СуммаАвансовыхПлатежей9;
					Иначе
						НоваяСтрока.СуммаАвансовыхПлатежей = СуммаАвансовыхПлатежей3 + СуммаАвансовыхПлатежей6 + СуммаАвансовыхПлатежей9;
					КонецЕсли;
					НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалогаКУплате - НоваяСтрока.СуммаАвансовыхПлатежей;
					
				КонецЕсли;
				
				Если НЕ ГодовойРасчет Тогда
					НоваяСтрока.СуммаАвансовыхПлатежей = НоваяСтрока.СуммаНалогаКУплате;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ТаблицаРасчетНалога.ЗаполнитьЗначения(мДатаКонцаПериодаОтчета, "ПериодРасчета");
	ТаблицаРасчетНалога.ЗаполнитьЗначения(Организация, "Организация");
	
	Возврат ТаблицаРасчетНалога;
	
КонецФункции

// Возвращает таблицы выполнения регламентных операций расчета имущественных налогов.
//
// Параметры:
// 	ТаблицаРеквизиты 
// 	Отказ - Булево - Флаг отказа проведения.
// 
// Возвращаемое значение:
// 	 СтруктураТаблиц - Структура - Таблицы расчета налога:
// 							Ключи:
// 								ТаблицаСправкиРасчета - ТаблицаЗначений - Справка-расчет налога.
// 								ТаблицаРасходыПоНалогу - ТаблицаЗначений - Отражение расходов по налогу.
//
Функция ПодготовитьТаблицыРасчетНалога(ТаблицаРеквизиты, Отказ) Экспорт
	
	СтруктураТаблиц = Новый Структура();
	СтруктураТаблиц.Вставить("ТаблицаСправкиРасчета");
	СтруктураТаблиц.Вставить("ТаблицаРасходыПоНалогу");
	
	Параметры = ПодготовитьПараметрыРасчетНалога(ТаблицаРеквизиты);
	Если Параметры.Реквизиты.Количество() = 0 Тогда
		Возврат СтруктураТаблиц;
	КонецЕсли;
	
	Реквизиты = Параметры.Реквизиты[0];
	СправкаРасчет   = РасчетИмущественныхНалогов.СправкаРасчет(Реквизиты);
	
	Результат = РасходыПоНалогу(Реквизиты, СправкаРасчет, Отказ);
	
	СтруктураТаблиц.Вставить("ТаблицаСправкиРасчета",      СправкаРасчет);
	СтруктураТаблиц.Вставить("ТаблицаРасходыПоНалогу",     Результат.ПрочиеРасходы);
	СтруктураТаблиц.Вставить("ТаблицаПрочиеАктивыПассивы", Результат.ПрочиеАктивыПассивы);
	
	Возврат СтруктураТаблиц;

КонецФункции

// Формирует движения регистров по данным расчета налога.
//
Процедура СформироватьДвиженияРасчетНалога(ТаблицыНачислениеНалога, ТаблицаРеквизитов, Движения, Отказ) Экспорт

	Параметры = ПодготовитьПараметрыДвиженийРасчетНалога(ТаблицаРеквизитов);
	Если Параметры.Реквизиты.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Реквизиты = Параметры.Реквизиты[0];
	
	РасчетИмущественныхНалогов.ДополнитьПараметрыДвиженийСправкаРасчет(Параметры, Реквизиты, ТаблицыНачислениеНалога.ТаблицаСправкиРасчета);
	
	// Данные для справки-расчета
	ЗакрытиеМесяца.ЗаписьВоВспомогательныеРегистрыСведений(
		Движения, 
		Параметры.СправкаРасчет, 
		Параметры.Реквизиты, 
		Реквизиты.ИмяРегистраРасчетНалогов);
		
	Если ТаблицыНачислениеНалога.ТаблицаРасходыПоНалогу.Количество() <> 0 Тогда
		Движения.ПрочиеРасходы.Записывать = Истина;
		Движения.ПрочиеРасходы.Загрузить(ТаблицыНачислениеНалога.ТаблицаРасходыПоНалогу);
	КонецЕсли;
	
	Если ТаблицыНачислениеНалога.ТаблицаПрочиеАктивыПассивы.Количество() <> 0 Тогда
		Движения.ПрочиеАктивыПассивы.Записывать = Истина;
		Движения.ПрочиеАктивыПассивы.Загрузить(ТаблицыНачислениеНалога.ТаблицаПрочиеАктивыПассивы);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПодготовитьПараметрыРасчетНалога(ТаблицаРеквизиты)
	
	Параметры = Новый Структура;
	
	// Подготовка таблицы Параметры.Реквизиты
	
	СписокОбязательныхКолонок = ""
	+ "Период,"						// <Дата>
	+ "Организация,"				// <СправочникСсылка.Организации>
	+ "Регистратор,"				// <ДокументСсылка.*>
	+ "ВидНалога,"					// <Перечисление.ВидыИмущественныхНалогов>
	+ "Содержание,"					// <Строка, 150>
	+ "ИмяРегистраРасчетНалогов"	// <Строка, 0>
	;
	
	Параметры.Вставить("Реквизиты", 
		УправлениеВнеоборотнымиАктивамиПереопределяемый.ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизиты, СписокОбязательныхКолонок));
	
	Возврат Параметры;
	
КонецФункции

Функция ПодготовитьПараметрыДвиженийРасчетНалога(ТаблицаРеквизитов)
	
	Параметры = Новый Структура;
	
	// Подготовка таблицы Параметры.Реквизиты
	
	СписокОбязательныхКолонок = ""
	+ "Период,"						// <Дата>
	+ "Организация,"				// <СправочникСсылка.Организации>
	+ "Регистратор,"				// <ДокументСсылка.*>
	+ "ВидНалога,"					// <ПеречислениеСсылка.ВидыИмущественныхНалогов>
	+ "ИмяРегистраРасчетНалогов"	// <Строка, 0>
	;
	
	Параметры.Вставить("Реквизиты", 
		УправлениеВнеоборотнымиАктивамиПереопределяемый.ПолучитьТаблицуПараметровПроведения(ТаблицаРеквизитов, СписокОбязательныхКолонок));
	
	Возврат Параметры;
	
КонецФункции

Функция ПустаяСправкаРасчет(ИмяРегистраСведений)

	СправкаРасчет = РегистрыСведений[ИмяРегистраСведений].СоздатьНаборЗаписей().ВыгрузитьКолонки();
	
	СправкаРасчет.Колонки.Удалить("Регистратор");
	СправкаРасчет.Колонки.Удалить("Активность");
	СправкаРасчет.Колонки.Удалить("НомерСтроки");
	
	Возврат СправкаРасчет;
	
КонецФункции

Функция РасходыПоНалогу(Реквизиты, СправкаРасчет, Отказ)
	
	ПрочиеРасходы = РегистрыНакопления.ПрочиеРасходы.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	ПрочиеАктивыПассивы = РегистрыНакопления.ПрочиеАктивыПассивы.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	ДвиженияДоходыРасходыПрочиеАктивыПассивы = РегистрыНакопления.ДвиженияДоходыРасходыПрочиеАктивыПассивы.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	
	СтатьяАктивовПассивов = ПланыВидовХарактеристик.СтатьиАктивовПассивов.Налоги;
	АналитикаАктивовПассивов = Неопределено;
	Если Реквизиты.ВидНалога = Перечисления.ВидыИмущественныхНалогов.ЗемельныйНалог Тогда
		АналитикаАктивовПассивов = Перечисления.ТипыНалогов.ЗемельныйНалог;
	ИначеЕсли Реквизиты.ВидНалога = Перечисления.ВидыИмущественныхНалогов.НалогНаИмущество Тогда
		АналитикаАктивовПассивов = Перечисления.ТипыНалогов.НалогНаИмущество;
	ИначеЕсли Реквизиты.ВидНалога = Перечисления.ВидыИмущественныхНалогов.ТранспортныйНалог Тогда
		АналитикаАктивовПассивов = Перечисления.ТипыНалогов.ТранспортныйНалог;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	//0
	"ВЫБРАТЬ
	|	СправкаРасчет.ОсновноеСредство КАК ОсновноеСредство,
	|	СправкаРасчет.ИФНС КАК РегистрацияВНалоговомОргане,
	|	СправкаРасчет.СуммаНалогаКУплате КАК СуммаРегл,
	|	СправкаРасчет.СуммаНалогаКУплате * &КоэффициенПересчетаВУпр КАК СуммаБезНДС,
	|	СправкаРасчет.СуммаНалогаКУплате * &КоэффициенПересчетаВУпр КАК Сумма
	|ПОМЕСТИТЬ СправкаРасчет
	|ИЗ
	|	&СправкаРасчет КАК СправкаРасчет
	|ГДЕ
	|	СправкаРасчет.СуммаНалогаКУплате <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	//1
	|ВЫБРАТЬ
	|	СпособыОтраженияРасходов.ОсновноеСредство КАК ОсновноеСредство,
	|	СпособыОтраженияРасходов.СтатьяРасходов КАК СтатьяРасходов,
	|	СпособыОтраженияРасходов.СпособОтраженияРасходовЗаданДокументом КАК СпособОтраженияРасходовЗаданДокументом,
	|	СпособыОтраженияРасходов.СпособОтраженияРасходов КАК СпособОтраженияРасходов,
	|	СпособыОтраженияРасходов.АналитикаРасходов КАК АналитикаРасходов
	|ПОМЕСТИТЬ СпособыОтраженияРасходов
	|ИЗ
	|	РегистрСведений.СпособыОтраженияРасходовПоИмущественнымНалогам.СрезПоследних(
	|		&Период, 
	|		ОсновноеСредство В (
	|			ВЫБРАТЬ 
	|				СправкаРасчет.ОсновноеСредство 
	|			ИЗ 
	|				СправкаРасчет КАК СправкаРасчет)
	|		И Организация = &Организация) КАК СпособыОтраженияРасходов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	СпособОтраженияРасходов
	|;
	|
	//2
	|ВЫБРАТЬ
	|	МестонахождениеОС.ОсновноеСредство КАК ОсновноеСредство,
	|	МестонахождениеОС.Местонахождение КАК Подразделение
	|ПОМЕСТИТЬ МестонахождениеОС
	|ИЗ
	|	РегистрСведений.МестонахождениеОСБухгалтерскийУчет.СрезПоследних(
	|		&Период, 
	|		ОсновноеСредство В (
	|			ВЫБРАТЬ 
	|				СправкаРасчет.ОсновноеСредство 
	|			ИЗ 
	|				СправкаРасчет КАК СправкаРасчет)
	|		И Организация = &Организация) КАК МестонахождениеОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	//3
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ОсновныеСредства.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СправкаРасчет.ОсновноеСредство КАК ОсновноеСредство,
	|	СправкаРасчет.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	СправкаРасчет.Сумма КАК Сумма,
	|	СправкаРасчет.СуммаРегл КАК СуммаРегл,
	|	СправкаРасчет.СуммаБезНДС КАК СуммаБезНДС,
	|	ВЫБОР КОГДА НЕ ЕСТЬNULL(СпособыОтраженияРасходов.СтатьяРасходов.ПринятиеКналоговомуУчету, ИСТИНА) ТОГДА
	|		СправкаРасчет.СуммаРегл
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК ПостояннаяРазница,
	|	МестонахождениеОС.Подразделение КАК Подразделение,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.СтатьяРасходов, НЕОПРЕДЕЛЕНО) КАК СтатьяРасходов,
	|	ЕСТЬNULL(СпособыОтраженияРасходов.АналитикаРасходов, НЕОПРЕДЕЛЕНО) КАК АналитикаРасходов,
	|	&ХозяйственнаяОперация
	|ИЗ
	|	СправкаРасчет КАК СправкаРасчет
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		СпособыОтраженияРасходов КАК СпособыОтраженияРасходов
	|	ПО
	|		СправкаРасчет.ОсновноеСредство = СпособыОтраженияРасходов.ОсновноеСредство
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		МестонахождениеОС КАК МестонахождениеОС
	|	ПО
	|		СправкаРасчет.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ОбъектыЭксплуатации КАК ОсновныеСредства
	|	ПО
	|		СправкаРасчет.ОсновноеСредство = ОсновныеСредства.Ссылка
	|	
	|ГДЕ
	|	НЕ ЕСТЬNULL(СпособыОтраженияРасходов.СпособОтраженияРасходовЗаданДокументом, ЛОЖЬ)
	|;
	|
	//4
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ОсновныеСредства.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СправкаРасчет.ОсновноеСредство КАК ОсновноеСредство,
	|	СправкаРасчет.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	СправкаРасчет.Сумма КАК Сумма,
	|	СправкаРасчет.СуммаРегл КАК СуммаРегл,
	|	СправкаРасчет.СуммаБезНДС КАК СуммаБезНДС,
	|	ИзменениеПараметровОС.ОтражениеАмортизационныхРасходов.(
	|		Подразделение КАК Подразделение,
	|		СтатьяРасходовНалог КАК СтатьяРасходов,
	|		ЕСТЬNULL(СтатьяРасходовНалог.ПринятиеКналоговомуУчету, ИСТИНА) КАК ПринятиеКНалоговомуУчету,
	|		АналитикаРасходовНалог КАК АналитикаРасходов,
	|		Коэффициент КАК Коэффициент
	|	) КАК ТаблицаРаспределения,
	|	&ХозяйственнаяОперация
	|ИЗ
	|	СправкаРасчет КАК СправкаРасчет
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		СпособыОтраженияРасходов КАК СпособыОтраженияРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			Документ.ИзменениеПараметровОС КАК ИзменениеПараметровОС
	|		ПО
	|			СпособыОтраженияРасходов.СпособОтраженияРасходов = ИзменениеПараметровОС.Ссылка
	|	ПО
	|		СправкаРасчет.ОсновноеСредство = СпособыОтраженияРасходов.ОсновноеСредство
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ОбъектыЭксплуатации КАК ОсновныеСредства
	|	ПО
	|		СправкаРасчет.ОсновноеСредство = ОсновныеСредства.Ссылка
	|	
	|ГДЕ
	|	ЕСТЬNULL(СпособыОтраженияРасходов.СпособОтраженияРасходовЗаданДокументом, ЛОЖЬ)
	|	И НЕ ИзменениеПараметровОС.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ОсновныеСредства.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СправкаРасчет.ОсновноеСредство КАК ОсновноеСредство,
	|	СправкаРасчет.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	СправкаРасчет.Сумма КАК Сумма,
	|	СправкаРасчет.СуммаРегл КАК СуммаРегл,
	|	СправкаРасчет.СуммаБезНДС КАК СуммаБезНДС,
	|	ДокументСпособ.ОтражениеРасходов.(
	|		Подразделение КАК Подразделение,
	|		СтатьяРасходов КАК СтатьяРасходов,
	|		ЕСТЬNULL(СтатьяРасходов.ПринятиеКналоговомуУчету, ИСТИНА) КАК ПринятиеКНалоговомуУчету,
	|		АналитикаРасходов КАК АналитикаРасходов,
	|		Коэффициент КАК Коэффициент
	|	) КАК ТаблицаРаспределения,
	|	&ХозяйственнаяОперация
	|ИЗ
	|	СправкаРасчет КАК СправкаРасчет
	|		ЛЕВОЕ СОЕДИНЕНИЕ СпособыОтраженияРасходов КАК СпособыОтраженияРасходов
	|		ПО СправкаРасчет.ОсновноеСредство = СпособыОтраженияРасходов.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РегистрацияЗемельныхУчастков КАК ДокументСпособ
	|		ПО СпособыОтраженияРасходов.СпособОтраженияРасходов = ДокументСпособ.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК ОсновныеСредства
	|		ПО СправкаРасчет.ОсновноеСредство = ОсновныеСредства.Ссылка
	|	
	|	
	|ГДЕ
	|	ЕСТЬNULL(СпособыОтраженияРасходов.СпособОтраженияРасходовЗаданДокументом, ЛОЖЬ)
	|	И НЕ ДокументСпособ.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ОсновныеСредства.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СправкаРасчет.ОсновноеСредство КАК ОсновноеСредство,
	|	СправкаРасчет.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	СправкаРасчет.Сумма КАК Сумма,
	|	СправкаРасчет.СуммаРегл КАК СуммаРегл,
	|	СправкаРасчет.СуммаБезНДС КАК СуммаБезНДС,
	|	ДокументСпособ.ОтражениеРасходов.(
	|		Подразделение КАК Подразделение,
	|		СтатьяРасходов КАК СтатьяРасходов,
	|		ЕСТЬNULL(СтатьяРасходов.ПринятиеКналоговомуУчету, ИСТИНА) КАК ПринятиеКНалоговомуУчету,
	|		АналитикаРасходов КАК АналитикаРасходов,
	|		Коэффициент КАК Коэффициент
	|	) КАК ТаблицаРаспределения,
	|	&ХозяйственнаяОперация
	|ИЗ
	|	СправкаРасчет КАК СправкаРасчет
	|		ЛЕВОЕ СОЕДИНЕНИЕ СпособыОтраженияРасходов КАК СпособыОтраженияРасходов
	|		ПО СправкаРасчет.ОсновноеСредство = СпособыОтраженияРасходов.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РегистрацияТранспортныхСредств КАК ДокументСпособ
	|		ПО СпособыОтраженияРасходов.СпособОтраженияРасходов = ДокументСпособ.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК ОсновныеСредства
	|		ПО СправкаРасчет.ОсновноеСредство = ОсновныеСредства.Ссылка
	|	
	|ГДЕ
	|	ЕСТЬNULL(СпособыОтраженияРасходов.СпособОтраженияРасходовЗаданДокументом, ЛОЖЬ)
	|	И НЕ ДокументСпособ.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ОсновныеСредства.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СправкаРасчет.ОсновноеСредство КАК ОсновноеСредство,
	|	СправкаРасчет.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	СправкаРасчет.Сумма КАК Сумма,
	|	СправкаРасчет.СуммаРегл КАК СуммаРегл,
	|	СправкаРасчет.СуммаБезНДС КАК СуммаБезНДС,
	|	ДокументСпособ.ОтражениеРасходов.(
	|		Подразделение КАК Подразделение,
	|		СтатьяРасходов КАК СтатьяРасходов,
	|		ЕСТЬNULL(СтатьяРасходов.ПринятиеКналоговомуУчету, ИСТИНА) КАК ПринятиеКНалоговомуУчету,
	|		АналитикаРасходов КАК АналитикаРасходов,
	|		Коэффициент КАК Коэффициент
	|	) КАК ТаблицаРаспределения,
	|	&ХозяйственнаяОперация
	|ИЗ
	|	СправкаРасчет КАК СправкаРасчет
	|		ЛЕВОЕ СОЕДИНЕНИЕ СпособыОтраженияРасходов КАК СпособыОтраженияРасходов
	|		ПО СправкаРасчет.ОсновноеСредство = СпособыОтраженияРасходов.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РегистрацияПорядкаНалогообложенияПоНалогуНаИмущество КАК ДокументСпособ
	|		ПО СпособыОтраженияРасходов.СпособОтраженияРасходов = ДокументСпособ.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК ОсновныеСредства
	|		ПО СправкаРасчет.ОсновноеСредство = ОсновныеСредства.Ссылка
	|	
	|ГДЕ
	|	ЕСТЬNULL(СпособыОтраженияРасходов.СпособОтраженияРасходовЗаданДокументом, ЛОЖЬ)
	|	И НЕ ДокументСпособ.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ОсновныеСредства.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СправкаРасчет.ОсновноеСредство КАК ОсновноеСредство,
	|	СправкаРасчет.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	СправкаРасчет.Сумма КАК Сумма,
	|	СправкаРасчет.СуммаРегл КАК СуммаРегл,
	|	СправкаРасчет.СуммаБезНДС КАК СуммаБезНДС,
	|	ДокументСпособ.ОтражениеРасходов.(
	|		Подразделение КАК Подразделение,
	|		СтатьяРасходов КАК СтатьяРасходов,
	|		ЕСТЬNULL(СтатьяРасходов.ПринятиеКналоговомуУчету, ИСТИНА) КАК ПринятиеКНалоговомуУчету,
	|		АналитикаРасходов КАК АналитикаРасходов,
	|		Коэффициент КАК Коэффициент
	|	) КАК ТаблицаРаспределения,
	|	&ХозяйственнаяОперация
	|ИЗ
	|	СправкаРасчет КАК СправкаРасчет
	|		ЛЕВОЕ СОЕДИНЕНИЕ СпособыОтраженияРасходов КАК СпособыОтраженияРасходов
	|		ПО СправкаРасчет.ОсновноеСредство = СпособыОтраженияРасходов.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеСпособаОтраженияИмущественныхНалогов КАК ДокументСпособ
	|		ПО СпособыОтраженияРасходов.СпособОтраженияРасходов = ДокументСпособ.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК ОсновныеСредства
	|		ПО СправкаРасчет.ОсновноеСредство = ОсновныеСредства.Ссылка
	|	
	|ГДЕ
	|	ЕСТЬNULL(СпособыОтраженияРасходов.СпособОтраженияРасходовЗаданДокументом, ЛОЖЬ)
	|	И НЕ ДокументСпособ.Ссылка ЕСТЬ NULL
	|";
	
	ВалютаУпр = Константы.ВалютаУправленческогоУчета.Получить();
	ВалютаРегл = Константы.ВалютаРегламентированногоУчета.Получить();

	КоэффициенПересчетаВУпр = РаботаСКурсамиВалютУТ.ПолучитьКоэффициентПересчетаИзВалютыВВалюту(
												ВалютаРегл, ВалютаУпр, Реквизиты.Период);
	
	Запрос.УстановитьПараметр("СправкаРасчет", СправкаРасчет);
	Запрос.УстановитьПараметр("Период",        Реквизиты.Период);
	Запрос.УстановитьПараметр("Организация",   Реквизиты.Организация);
	Запрос.УстановитьПараметр("КоэффициенПересчетаВУпр", КоэффициенПересчетаВУпр);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.НачислениеИмущественныхНалогов);
	
	Результат = Запрос.ВыполнитьПакет();
	
	ВыборкаРасходыБезРаспределения = Результат[3].Выбрать();
	ВыборкаРасходыСРаспределением = Результат[4].Выбрать();
	
	Пока ВыборкаРасходыБезРаспределения.Следующий() Цикл
		Если Не ЗначениеЗаполнено(ВыборкаРасходыБезРаспределения.СтатьяРасходов) Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru='Для основного средства ""%1"" не установлен способ отражения расходов по налогам.'"),
								ВыборкаРасходыБезРаспределения.ОсновноеСредство);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ВыборкаРасходыБезРаспределения.ОсновноеСредство, , , Отказ);
		КонецЕсли;
		
		Запись = ПрочиеРасходы.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, ВыборкаРасходыБезРаспределения);
		
		Запись = ПрочиеАктивыПассивы.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, ВыборкаРасходыБезРаспределения);
		Запись.Статья = СтатьяАктивовПассивов;
		Запись.Аналитика = АналитикаАктивовПассивов;
		
		Запись = ДвиженияДоходыРасходыПрочиеАктивыПассивы.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, ВыборкаРасходыБезРаспределения, "Период, ХозяйственнаяОперация, Организация, Сумма, СуммаРегл");
		
		Запись.Статья = СтатьяАктивовПассивов;
		Запись.АналитикаАктивовПассивов = АналитикаАктивовПассивов;
		Запись.Подразделение = ВыборкаРасходыБезРаспределения.Подразделение;
		
		Запись.КорСтатья = ВыборкаРасходыБезРаспределения.СтатьяРасходов;
		Запись.КорАналитикаРасходов = ВыборкаРасходыБезРаспределения.АналитикаРасходов;
		Запись.КорПодразделение = ВыборкаРасходыБезРаспределения.Подразделение;
		Запись.КорНаправлениеДеятельности = ВыборкаРасходыБезРаспределения.НаправлениеДеятельности;
		
	КонецЦикла;
	
	Пока ВыборкаРасходыСРаспределением.Следующий() Цикл
		
		ТаблицаРаспределения = ВыборкаРасходыСРаспределением.ТаблицаРаспределения.Выгрузить();
		Если ТаблицаРаспределения.Количество() = 0 Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru='Для основного средства ""%1"" не установлен способ отражения расходов по налогам.'"),
								ВыборкаРасходыСРаспределением.ОсновноеСредство);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ВыборкаРасходыСРаспределением.ОсновноеСредство, , , Отказ);
		КонецЕсли;
		
		Коэффициенты = ТаблицаРаспределения.ВыгрузитьКолонку("Коэффициент");
		Суммы = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(ВыборкаРасходыСРаспределением.Сумма, Коэффициенты);
		СуммыРегл = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(ВыборкаРасходыСРаспределением.СуммаРегл, Коэффициенты);
		СуммыБезНДС = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(ВыборкаРасходыСРаспределением.СуммаБезНДС, Коэффициенты);
		
		Если Суммы = Неопределено И СуммыРегл = Неопределено И СуммыБезНДС = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Для Индекс = 0 По ТаблицаРаспределения.Количество()-1 Цикл
			ПравилоОтражения = ТаблицаРаспределения[Индекс];
			Запись = ПрочиеРасходы.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, ВыборкаРасходыСРаспределением, "Период, Организация, РегистрацияВНалоговомОргане, НаправлениеДеятельности");
			ЗаполнитьЗначенияСвойств(Запись, ПравилоОтражения, "Подразделение, СтатьяРасходов, АналитикаРасходов");
			Запись.Сумма = ?(Суммы = Неопределено, 0, Суммы[Индекс]);
			Запись.СуммаРегл = ?(СуммыРегл = Неопределено, 0, СуммыРегл[Индекс]);
			Запись.СуммаБезНДС = ?(СуммыБезНДС = Неопределено, 0, СуммыБезНДС[Индекс]);
			Если НЕ ПравилоОтражения.ПринятиеКНалоговомуУчету Тогда
				Запись.ПостояннаяРазница = Запись.СуммаРегл; 
			КонецЕсли;
			
			Запись = ДвиженияДоходыРасходыПрочиеАктивыПассивы.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, ВыборкаРасходыСРаспределением, "Период, ХозяйственнаяОперация, Организация");
			
			Запись.Статья = СтатьяАктивовПассивов;
			Запись.АналитикаАктивовПассивов = АналитикаАктивовПассивов;
		
			Запись.КорСтатья = ПравилоОтражения.СтатьяРасходов;
			Запись.КорАналитикаРасходов = ПравилоОтражения.АналитикаРасходов;
			Запись.КорПодразделение = ПравилоОтражения.Подразделение;
			Запись.КорНаправлениеДеятельности = ВыборкаРасходыСРаспределением.НаправлениеДеятельности;
			Запись.Сумма = ?(Суммы = Неопределено, 0, Суммы[Индекс]);
			Запись.СуммаРегл = ?(СуммыРегл = Неопределено, 0, СуммыРегл[Индекс]);
			
		КонецЦикла;
		
		Запись = ПрочиеАктивыПассивы.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, ВыборкаРасходыСРаспределением);
		Запись.Статья = СтатьяАктивовПассивов;
		Запись.Аналитика = АналитикаАктивовПассивов;
		
	КонецЦикла;
	
	КолонкиГруппировки = "Период, Организация, Подразделение, НаправлениеДеятельности, СтатьяРасходов, АналитикаРасходов, РегистрацияВНалоговомОргане";
	КолонкиСуммирования = "Сумма, СуммаБезНДС, СуммаРегл, ПостояннаяРазница, ВременнаяРазница";
	ПрочиеРасходы.Свернуть(КолонкиГруппировки, КолонкиСуммирования);
	
	ПрочиеАктивыПассивы.ЗаполнитьЗначения(ВидДвиженияНакопления.Расход, "ВидДвижения");
	КолонкиГруппировки = "ВидДвижения, Период, Организация, Подразделение, Статья, Аналитика";
	КолонкиСуммирования = "Сумма";
	ПрочиеАктивыПассивы.Свернуть(КолонкиГруппировки, КолонкиСуммирования);
	
	КолонкиГруппировки = "Период, ХозяйственнаяОперация, Организация, 
		|Подразделение, Статья, АналитикаРасходов, АналитикаДоходов, АналитикаАктивовПассивов, НаправлениеДеятельности,
		|КорПодразделение, КорСтатья, КорАналитикаРасходов, КорАналитикаДоходов, КорАналитикаАктивовПассивов, КорНаправлениеДеятельности";
	КолонкиСуммирования = "Сумма, СуммаРегл";
	ДвиженияДоходыРасходыПрочиеАктивыПассивы.Свернуть(КолонкиГруппировки, КолонкиСуммирования);
	
	Результат = Новый Структура;
	Результат.Вставить("ПрочиеРасходы",       ПрочиеРасходы);
	Результат.Вставить("ПрочиеАктивыПассивы", ПрочиеАктивыПассивы);
	Результат.Вставить("ДвиженияДоходыРасходыПрочиеАктивыПассивы", ДвиженияДоходыРасходыПрочиеАктивыПассивы);
	
	Возврат Результат;
	
КонецФункции

#Область ЗаданияКЗакрытиюМесяца

// Формирует задание к расчету налога на имущество, на основании измененных данных.
//
// Параметры:
//  ДанныеТаблиц					 - Структура - Содержит список таблиц в которых произошли изменения.
//  ЕстьОсновныеСредства			 - Булево	 - Истина, если измененные данные содержат информацию об основных средствах.
//  ПроверитьЧтоОСПринятоКУчету		 - Булево	 - Истина, если нужно сформировать задание только если ОС приняты к учету.
//  ПринятиеКУчетуЗаданоДокументом	 - Булево	 - Истина, если документ сам изменяет состояние "Принят к учету".
//
Процедура СформироватьЗаданиеКРасчетуНалогаНаИмущество(ДанныеТаблиц, ЕстьОсновныеСредства = Истина, ПроверитьЧтоОСПринятоКУчету = Ложь, ПринятиеКУчетуЗаданоДокументом = Ложь) Экспорт

	СписокТаблиц = Новый Массив;
	СписокТаблиц.Добавить("ИзменениеПараметровОС");
	СписокТаблиц.Добавить("ИзменениеСпособаОтраженияИмущественныхНалогов");
	СписокТаблиц.Добавить("МестонахождениеОСБухгалтерскийУчет");
	СписокТаблиц.Добавить("ПервоначальныеСведенияОСБухгалтерскийУчет");
	СписокТаблиц.Добавить("РегламентнаяОперацияРасчетНалогаНаИмущество");
	СписокТаблиц.Добавить("РегистрацияПорядкаНалогообложенияПоНалогуНаИмущество");
	СписокТаблиц.Добавить("СостоянияОСОрганизаций");
	СписокТаблиц.Добавить("СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчет");
	СписокТаблиц.Добавить("СпособыОтраженияРасходовПоИмущественнымНалогам");
	СписокТаблиц.Добавить("СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам");
	СписокТаблиц.Добавить("СчетаБухгалтерскогоУчетаОС");
	СписокТаблиц.Добавить("СтавкиНалогаНаИмущество");
	
	ТекстОбъединенияДанных = УчетОСВызовСервера.СформироватьТекстОбъединенияДанныхДляФормированияЗадания(
									СписокТаблиц, ЕстьОсновныеСредства, ДанныеТаблиц);
	
	Если ТекстОбъединенияДанных = "" Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	МИНИМУМ(ВЫБОР 
	|				КОГДА ТИПЗНАЧЕНИЯ(ИзмененныеДанные.Документ) = ТИП(Документ.ВводОстатковВнеоборотныхАктивов)
	|					ТОГДА ДОБАВИТЬКДАТЕ(ВводОстатковВнеоборотныхАктивов.Дата, МЕСЯЦ, 1)
	|				ИНАЧЕ ИзмененныеДанные.Период 
	|			КОНЕЦ) КАК Период,
	|	ИзмененныеДанные.Организация       КАК Организация,
	|	" + ?(ЕстьОсновныеСредства, "ИзмененныеДанные.ОсновноеСредство КАК ОсновноеСредство,", "") + "
	|	ИзмененныеДанные.Документ          КАК Документ
	|ПОМЕСТИТЬ ИзмененныеДанные
	|ИЗ
	|	(" + ТекстОбъединенияДанных + ") КАК ИзмененныеДанные
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов КАК ВводОстатковВнеоборотныхАктивов
	|	ПО ВводОстатковВнеоборотныхАктивов.Ссылка = ИзмененныеДанные.Документ
	|
	|СГРУППИРОВАТЬ ПО
	|	ИзмененныеДанные.Организация,
	|	" + ?(ЕстьОсновныеСредства, "ИзмененныеДанные.ОсновноеСредство,", "") + "
	|	ИзмененныеДанные.Документ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|	" + ?(ЕстьОсновныеСредства, ",ОсновноеСредство", "");
	
	Если ЕстьОсновныеСредства Тогда
		
		ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияУТ.РазделительЗапросовПакета() +
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИзмененныеДанные.ОсновноеСредство КАК ОсновноеСредство
		|ПОМЕСТИТЬ СписокОС
		|ИЗ
		|	ИзмененныеДанные КАК ИзмененныеДанные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет КАК ПервоначальныеСведения
		|		ПО (ПервоначальныеСведения.ОсновноеСредство = ИзмененныеДанные.ОсновноеСредство)
		|			И (ПервоначальныеСведения.Период <= ИзмененныеДанные.Период)
		|ГДЕ
		|	НЕ ПервоначальныеСведения.ГруппаОС В (ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ЗемельныеУчастки), ЗНАЧЕНИЕ(Перечисление.ГруппыОС.ОбъектыПриродопользования))
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство";
		
	КонецЕсли; 
	
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияУТ.РазделительЗапросовПакета() +
	"ВЫБРАТЬ
	|	ИзмененныеДанные.Организация КАК Организация,
	|	УчетнаяПолитикаОрганизаций.УчетнаяПолитика КАК УчетнаяПолитика,
	|	ВЫБОР
	|		КОГДА УчетнаяПолитикаОрганизаций.Период > ИзмененныеДанные.Период
	|				И УчетнаяПолитикаОрганизаций.Период > &ДатаНачалаВеденияРеглУчета
	|			ТОГДА УчетнаяПолитикаОрганизаций.Период
	|		КОГДА &ДатаНачалаВеденияРеглУчета > ИзмененныеДанные.Период
	|			ТОГДА &ДатаНачалаВеденияРеглУчета
	|		ИНАЧЕ ИзмененныеДанные.Период
	|	КОНЕЦ КАК Начало
	|ПОМЕСТИТЬ УчетнаяПолитикаОрганизаций
	|ИЗ
	|	ИзмененныеДанные КАК ИзмененныеДанные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|		ПО (УчетнаяПолитикаОрганизаций.Организация = ИзмененныеДанные.Организация)
	|			И (УчетнаяПолитикаОрганизаций.УчетнаяПолитика.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Общая)
	|				ИЛИ ИзмененныеДанные.Период >= ДАТАВРЕМЯ(2015, 1, 1))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций КАК СледующаяУчетнаяПолитикаОрганизаций
	|		ПО (СледующаяУчетнаяПолитикаОрганизаций.Организация = УчетнаяПолитикаОрганизаций.Организация)
	|			И (СледующаяУчетнаяПолитикаОрганизаций.Период > УчетнаяПолитикаОрганизаций.Период)
	|
	|СГРУППИРОВАТЬ ПО
	|	ИзмененныеДанные.Организация,
	|	УчетнаяПолитикаОрганизаций.УчетнаяПолитика,
	|	ВЫБОР
	|		КОГДА УчетнаяПолитикаОрганизаций.Период > ИзмененныеДанные.Период
	|				И УчетнаяПолитикаОрганизаций.Период > &ДатаНачалаВеденияРеглУчета
	|			ТОГДА УчетнаяПолитикаОрганизаций.Период
	|		КОГДА &ДатаНачалаВеденияРеглУчета > ИзмененныеДанные.Период
	|			ТОГДА &ДатаНачалаВеденияРеглУчета
	|		ИНАЧЕ ИзмененныеДанные.Период
	|	КОНЕЦ
	|
	|ИМЕЮЩИЕ
	|	(ЕСТЬNULL(МИНИМУМ(СледующаяУчетнаяПолитикаОрганизаций.Период), ДАТАВРЕМЯ(1, 1, 1)) > МИНИМУМ(ИзмененныеДанные.Период)
	|			И ЕСТЬNULL(МИНИМУМ(СледующаяУчетнаяПолитикаОрганизаций.Период), ДАТАВРЕМЯ(1, 1, 1)) > &ДатаНачалаВеденияРеглУчета
	|		ИЛИ ЕСТЬNULL(МИНИМУМ(СледующаяУчетнаяПолитикаОрганизаций.Период), ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация";
	
	Если ПроверитьЧтоОСПринятоКУчету Тогда
		
		Если ПринятиеКУчетуЗаданоДокументом Тогда
			
			Если ДанныеТаблиц.МенеджерВременныхТаблиц.Таблицы.Найти("НачислениеАмортизацииОСБухгалтерскийУчет") <> Неопределено Тогда
			
				ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияУТ.РазделительЗапросовПакета() +
					"ВЫБРАТЬ
					|	УчетнаяПолитикаОрганизаций.Организация КАК Организация,
					|	МИНИМУМ(КОНЕЦПЕРИОДА(ВЫБОР 
					|							КОГДА ПринятыеКУчету.Период > УчетнаяПолитикаОрганизаций.Начало 
					|								ТОГДА ПринятыеКУчету.Период 
					|							ИНАЧЕ 
					|								УчетнаяПолитикаОрганизаций.Начало 
					|						КОНЕЦ, КВАРТАЛ)) КАК Период
					|ПОМЕСТИТЬ ПериодЗадания
					|ИЗ
					|	УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
					|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СостоянияОСОрганизаций КАК ПринятыеКУчету
					|		ПО (ПринятыеКУчету.Организация = УчетнаяПолитикаОрганизаций.Организация)
					|			И (ПринятыеКУчету.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету))
					|ГДЕ
					|	УчетнаяПолитикаОрганизаций.УчетнаяПолитика.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Общая)
					|	
					|СГРУППИРОВАТЬ ПО
					|	УчетнаяПолитикаОрганизаций.Организация
					|	
					|ИНДЕКСИРОВАТЬ ПО
					|	Организация";
					
			Иначе
					
				ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияУТ.РазделительЗапросовПакета() +
					"ВЫБРАТЬ
					|	УчетнаяПолитикаОрганизаций.Организация КАК Организация,
					|	МИНИМУМ(КОНЕЦПЕРИОДА(ВЫБОР 
					|							КОГДА ПринятыеКУчету.Период > УчетнаяПолитикаОрганизаций.Начало 
					|								ТОГДА ПринятыеКУчету.Период 
					|							ИНАЧЕ 
					|								УчетнаяПолитикаОрганизаций.Начало 
					|						КОНЕЦ, КВАРТАЛ)) КАК Период
					|ПОМЕСТИТЬ ПериодЗадания
					|ИЗ
					|	УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
					|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИзмененныеДанные КАК ИзмененныеДанные
					|		ПО ИзмененныеДанные.Организация = УчетнаяПолитикаОрганизаций.Организация
					|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияОСОрганизаций КАК ПринятыеКУчету
					|		ПО (ПринятыеКУчету.Организация = УчетнаяПолитикаОрганизаций.Организация)
					|			И (ПринятыеКУчету.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету))
					|			И (ПринятыеКУчету.Регистратор = ИзмененныеДанные.Документ)
					|			И (ПринятыеКУчету.Активность)
					|ГДЕ
					|	УчетнаяПолитикаОрганизаций.УчетнаяПолитика.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Общая)
					|	
					|СГРУППИРОВАТЬ ПО
					|	УчетнаяПолитикаОрганизаций.Организация
					|	
					|ИНДЕКСИРОВАТЬ ПО
					|	Организация";

			КонецЕсли;		
		Иначе
		
			ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияУТ.РазделительЗапросовПакета() +
				"ВЫБРАТЬ
				|	ВложенныйЗапрос.Организация,
				|	МИНИМУМ(НАЧАЛОПЕРИОДА(ВложенныйЗапрос.Период, МЕСЯЦ)) КАК Период
				|ПОМЕСТИТЬ ПериодЗадания
				|ИЗ
				|	(ВЫБРАТЬ
				|		УчетнаяПолитикаОрганизаций.Организация КАК Организация,
				|		МИНИМУМ(КОНЕЦПЕРИОДА(ВЫБОР 
				|								КОГДА ПринятыеКУчету.Период > УчетнаяПолитикаОрганизаций.Начало 
				|									ТОГДА ПринятыеКУчету.Период 
				|								ИНАЧЕ 
				|									УчетнаяПолитикаОрганизаций.Начало 
				|							КОНЕЦ, КВАРТАЛ)) КАК Период
				|	ИЗ
				|		УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
				|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияОСОрганизаций КАК ПринятыеКУчету
				|			ПО (ПринятыеКУчету.Организация = УчетнаяПолитикаОрганизаций.Организация)
				|				И (ПринятыеКУчету.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету))
				|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияОСОрганизаций КАК СледующееСостояниеОС
				|			ПО (СледующееСостояниеОС.Организация = ПринятыеКУчету.Организация)
				|				И (СледующееСостояниеОС.ОсновноеСредство = ПринятыеКУчету.ОсновноеСредство)
				|				И (СледующееСостояниеОС.Период > ПринятыеКУчету.Период)
				|				И (СледующееСостояниеОС.Состояние <> ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету))
				|	ГДЕ
				|		УчетнаяПолитикаОрганизаций.УчетнаяПолитика.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Общая)
				|		" + ?(ЕстьОсновныеСредства, "И ПринятыеКУчету.ОсновноеСредство В
				|				(ВЫБРАТЬ
				|					СписокОС.ОсновноеСредство
				|				ИЗ
				|					СписокОС КАК СписокОС)", "") + "
				|	
				|	СГРУППИРОВАТЬ ПО
				|		УчетнаяПолитикаОрганизаций.Организация
				|	
				|	ИМЕЮЩИЕ
				|		(МИНИМУМ(СледующееСостояниеОС.Период) >= МИНИМУМ(УчетнаяПолитикаОрганизаций.Начало)
				|			ИЛИ ЕСТЬNULL(МИНИМУМ(СледующееСостояниеОС.Период), ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1))
				|	
				|	ОБЪЕДИНИТЬ ВСЕ
				|	
				|	ВЫБРАТЬ
				|		УчетнаяПолитикаОрганизаций.Организация,
				|		МИНИМУМ(КОНЕЦПЕРИОДА(ВЫБОР 
				|								КОГДА СтавкиНалога.Период > УчетнаяПолитикаОрганизаций.Начало 
				|									ТОГДА СтавкиНалога.Период 
				|								ИНАЧЕ 
				|									УчетнаяПолитикаОрганизаций.Начало 
				|							КОНЕЦ, КВАРТАЛ))
				|	ИЗ
				|		УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
				|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СтавкиНалога
				|			ПО (СтавкиНалога.Организация = УчетнаяПолитикаОрганизаций.Организация)
				|				И (СтавкиНалога.НалоговаяБаза = ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость))
				|				И (СтавкиНалога.Период >= УчетнаяПолитикаОрганизаций.Начало)
				|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СледующееСтавкиНалога
				|			ПО (СледующееСтавкиНалога.Организация = СтавкиНалога.Организация)
				|				И (СледующееСтавкиНалога.ОсновноеСредство = СтавкиНалога.ОсновноеСредство)
				|				И (СледующееСтавкиНалога.Период > СтавкиНалога.Период)
				|				И (СледующееСтавкиНалога.НалоговаяБаза <> ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость))
				|	" + ?(ЕстьОсновныеСредства, "ГДЕ
				|		СтавкиНалога.ОсновноеСредство В
				|				(ВЫБРАТЬ
				|					СписокОС.ОсновноеСредство
				|				ИЗ
				|					СписокОС КАК СписокОС)", "") + "
				|	
				|	СГРУППИРОВАТЬ ПО
				|		УчетнаяПолитикаОрганизаций.Организация
				|	
				|	ИМЕЮЩИЕ
				|		(МИНИМУМ(СледующееСтавкиНалога.Период) >= МИНИМУМ(УчетнаяПолитикаОрганизаций.Начало)
				|			ИЛИ ЕСТЬNULL(МИНИМУМ(СледующееСтавкиНалога.Период), ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1))) КАК ВложенныйЗапрос
				|
				|СГРУППИРОВАТЬ ПО
				|	ВложенныйЗапрос.Организация
				|
				|ИНДЕКСИРОВАТЬ ПО
				|	Организация";
			
		КонецЕсли;
		
	Иначе

		ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияУТ.РазделительЗапросовПакета() +
			"ВЫБРАТЬ
			|	ВложенныйЗапрос.Организация,
			|	МИНИМУМ(НАЧАЛОПЕРИОДА(ВложенныйЗапрос.Период, МЕСЯЦ)) КАК Период
			|ПОМЕСТИТЬ ПериодЗадания
			|ИЗ
			|	(ВЫБРАТЬ
			|		УчетнаяПолитикаОрганизаций.Организация КАК Организация,
			|		МИНИМУМ(КОНЕЦПЕРИОДА(УчетнаяПолитикаОрганизаций.Начало, КВАРТАЛ)) КАК Период
			|	ИЗ
			|		УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
			|	ГДЕ
			|		УчетнаяПолитикаОрганизаций.УчетнаяПолитика.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Общая)
			|	
			|	СГРУППИРОВАТЬ ПО
			|		УчетнаяПолитикаОрганизаций.Организация
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		УчетнаяПолитикаОрганизаций.Организация,
			|		МИНИМУМ(КОНЕЦПЕРИОДА(ВЫБОР 
			|								КОГДА СтавкиНалога.Период > УчетнаяПолитикаОрганизаций.Начало 
			|									ТОГДА СтавкиНалога.Период 
			|								ИНАЧЕ 
			|									УчетнаяПолитикаОрганизаций.Начало 
			|							КОНЕЦ, КВАРТАЛ))
			|	ИЗ
			|		УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СтавкиНалога
			|			ПО (СтавкиНалога.Организация = УчетнаяПолитикаОрганизаций.Организация)
			|				И (СтавкиНалога.НалоговаяБаза = ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость))
			|				И (СтавкиНалога.Период >= УчетнаяПолитикаОрганизаций.Начало)
			|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам КАК СледующееСтавкиНалога
			|			ПО (СледующееСтавкиНалога.Организация = СтавкиНалога.Организация)
			|				И (СледующееСтавкиНалога.ОсновноеСредство = СтавкиНалога.ОсновноеСредство)
			|				И (СледующееСтавкиНалога.Период > СтавкиНалога.Период)
			|				И (СледующееСтавкиНалога.НалоговаяБаза <> ЗНАЧЕНИЕ(Перечисление.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость))
			|	" + ?(ЕстьОсновныеСредства, "ГДЕ
			|		СтавкиНалога.ОсновноеСредство В
			|				(ВЫБРАТЬ
			|					СписокОС.ОсновноеСредство
			|				ИЗ
			|					СписокОС КАК СписокОС)", "") + "
			|	
			|	СГРУППИРОВАТЬ ПО
			|		УчетнаяПолитикаОрганизаций.Организация
			|	
			|	ИМЕЮЩИЕ
			|		(МИНИМУМ(СледующееСтавкиНалога.Период) >= МИНИМУМ(УчетнаяПолитикаОрганизаций.Начало)
			|			ИЛИ ЕСТЬNULL(МИНИМУМ(СледующееСтавкиНалога.Период), ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1))) КАК ВложенныйЗапрос
			|
			|СГРУППИРОВАТЬ ПО
			|	ВложенныйЗапрос.Организация
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	ВложенныйЗапрос.Организация";
			
	КонецЕсли; 
	
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияУТ.РазделительЗапросовПакета() +
	"ВЫБРАТЬ
	|	ИзмененныеДанные.Организация,
	|	ИзмененныеДанные.Документ,
	|	&Операция КАК Операция,
	|	МИНИМУМ(НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(ПериодЗадания.Период, КВАРТАЛ), МЕСЯЦ)) КАК Месяц
	|ИЗ
	|	ИзмененныеДанные КАК ИзмененныеДанные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодЗадания КАК ПериодЗадания
	|		ПО (ПериодЗадания.Организация = ИзмененныеДанные.Организация)
	|
	|СГРУППИРОВАТЬ ПО
	|	ИзмененныеДанные.Организация,
	|	ИзмененныеДанные.Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПериодЗадания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ИзмененныеДанные";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДанныеТаблиц.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ДатаНачалаВеденияРеглУчета", Константы.ДатаНачалаВеденияРеглУчета.Получить());
	Запрос.УстановитьПараметр("Операция", Перечисления.ОперацииЗакрытияМесяца.РасчетНалогаНаИмущество);
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	РегистрыСведений.ЗаданияКЗакрытиюМесяца.СоздатьЗаписиРегистраПоДаннымВыборки(РезультатЗапроса.Выбрать());
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Формирует задание к расчету транспортного налога, на основании измененных данных.
//
// Параметры:
//  ДанныеТаблиц					 - Структура - Содержит список таблиц в которых произошли изменения.
//  ЕстьОсновныеСредства			 - Булево	 - Истина, если измененные данные содержат информацию об основных средствах.
//  ПроверитьЧтоОСЗарегистрировано	 - Булево	 - Истина, если нужно сформировать задание только если транспортное зарегистрировано.
//
Процедура СформироватьЗаданиеКРасчетуТранспортногоНалога(ДанныеТаблиц, ЕстьОсновныеСредства = Истина, ПроверитьЧтоОСЗарегистрировано = Истина) Экспорт

	СписокТаблиц = Новый Массив;
	СписокТаблиц.Добавить("ИзменениеПараметровОС");
	СписокТаблиц.Добавить("ИзменениеСпособаОтраженияИмущественныхНалогов");
	СписокТаблиц.Добавить("МестонахождениеОСБухгалтерскийУчет");
	СписокТаблиц.Добавить("ПорядокУплатыНалоговНаМестах");
	СписокТаблиц.Добавить("РегламентнаяОперацияРасчетТранспортногоНалога");
	СписокТаблиц.Добавить("РегистрацияТранспортныхСредств");
	СписокТаблиц.Добавить("СпособыОтраженияРасходовПоИмущественнымНалогам");
	
	ТекстОбъединенияДанных = УчетОСВызовСервера.СформироватьТекстОбъединенияДанныхДляФормированияЗадания(
								СписокТаблиц, ЕстьОсновныеСредства, ДанныеТаблиц);
	
	Если ТекстОбъединенияДанных = "" Тогда
		Возврат;
	КонецЕсли;
	
	// Условия формирования задания:
	// - если с начала квартала были зарегистрированы транспортные средства (дата задания - наименьшая дата регистрации)
	// - или если на начало квартала есть зарегистрированные транспортные средства (дата задания - начало квартала)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	МИНИМУМ(ВЫБОР 
	|				КОГДА ТИПЗНАЧЕНИЯ(ИзмененныеДанные.Документ) = ТИП(Документ.ВводОстатковВнеоборотныхАктивов)
	|					ТОГДА ДОБАВИТЬКДАТЕ(ВводОстатковВнеоборотныхАктивов.Дата, МЕСЯЦ, 1)
	|				ИНАЧЕ ИзмененныеДанные.Период 
	|			КОНЕЦ)   КАК Период,
	|	ИзмененныеДанные.Организация       КАК Организация,
	|	" + ?(ЕстьОсновныеСредства, "ИзмененныеДанные.ОсновноеСредство КАК ОсновноеСредство,", "") + "
	|	ИзмененныеДанные.Документ          КАК Документ
	|ПОМЕСТИТЬ ИзмененныеДанные
	|ИЗ
	|	(" + ТекстОбъединенияДанных + ") КАК ИзмененныеДанные
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов КАК ВводОстатковВнеоборотныхАктивов
	|	ПО ВводОстатковВнеоборотныхАктивов.Ссылка = ИзмененныеДанные.Документ
	|ГДЕ
	|	ВЫРАЗИТЬ(ИзмененныеДанные.Организация КАК Справочник.Организации).ЮридическоеФизическоеЛицо <> ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо)
	|
	|СГРУППИРОВАТЬ ПО
	|	ИзмененныеДанные.Организация,
	|	" + ?(ЕстьОсновныеСредства, "ИзмененныеДанные.ОсновноеСредство,", "") + "
	|	ИзмененныеДанные.Документ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|	" + ?(ЕстьОсновныеСредства, ",ОсновноеСредство", "");
	
	Если ПроверитьЧтоОСЗарегистрировано Тогда
		
		ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияУТ.РазделительЗапросовПакета() +
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	РегистрацияТранспортныхСредствПоследние.Организация КАК Организация
			|ПОМЕСТИТЬ РегистрацияТранспортныхСредствНаНачало
			|ИЗ
			|	(ВЫБРАТЬ
			|		РегистрацияТранспортныхСредств.Организация КАК Организация,
			|		РегистрацияТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство,
			|		МАКСИМУМ(РегистрацияТранспортныхСредств.Период) КАК Период
			|	ИЗ
			|		ИзмененныеДанные КАК ИзмененныеДанные
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
			|			ПО (РегистрацияТранспортныхСредств.Организация = ИзмененныеДанные.Организация)
			|				И (РегистрацияТранспортныхСредств.Период < НАЧАЛОПЕРИОДА(ИзмененныеДанные.Период, КВАРТАЛ))
			|				" + ?(ЕстьОсновныеСредства, "И РегистрацияТранспортныхСредств.ОсновноеСредство = ИзмененныеДанные.ОсновноеСредство", "") + "
			|	
			|	СГРУППИРОВАТЬ ПО
			|		РегистрацияТранспортныхСредств.Организация,
			|		РегистрацияТранспортныхСредств.ОсновноеСредство
			|	) КАК РегистрацияТранспортныхСредствПоследние
			|
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
			|		ПО (РегистрацияТранспортныхСредств.Организация = РегистрацияТранспортныхСредствПоследние.Организация)
			|			И (РегистрацияТранспортныхСредств.ОсновноеСредство = РегистрацияТранспортныхСредствПоследние.ОсновноеСредство)
			|			И (РегистрацияТранспортныхСредств.Период = РегистрацияТранспортныхСредствПоследние.Период)
			|ГДЕ
			|	РегистрацияТранспортныхСредств.ВключатьВНалоговуюБазу
			|
			|СГРУППИРОВАТЬ ПО
			|	РегистрацияТранспортныхСредствПоследние.Организация
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Организация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	РегистрацияТранспортныхСредств.Организация,
			|	МИНИМУМ(РегистрацияТранспортныхСредств.Период) КАК Период
			|ПОМЕСТИТЬ ПериодЗадания
			|ИЗ
			|	(ВЫБРАТЬ
			|		ИзмененныеДанные.Организация КАК Организация,
			|		МИНИМУМ(НАЧАЛОПЕРИОДА(ИзмененныеДанные.Период, МЕСЯЦ)) КАК Период
			|	ИЗ
			|		ИзмененныеДанные КАК ИзмененныеДанные
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрацияТранспортныхСредствНаНачало КАК РегистрацияТранспортныхСредствНаНачало
			|			ПО (РегистрацияТранспортныхСредствНаНачало.Организация = ИзмененныеДанные.Организация)
			|	
			|	СГРУППИРОВАТЬ ПО
			|		ИзмененныеДанные.Организация
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ИзмененныеДанные.Организация,
			|		МИНИМУМ(НАЧАЛОПЕРИОДА(РегистрацияТранспортныхСредств.Период, МЕСЯЦ))
			|	ИЗ
			|		ИзмененныеДанные КАК ИзмененныеДанные
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
			|			ПО (РегистрацияТранспортныхСредств.Организация = ИзмененныеДанные.Организация)
			|				И (РегистрацияТранспортныхСредств.Период >= НАЧАЛОПЕРИОДА(ИзмененныеДанные.Период, КВАРТАЛ))
			|				И (РегистрацияТранспортныхСредств.ВключатьВНалоговуюБазу)
			|				" + ?(ЕстьОсновныеСредства, "И РегистрацияТранспортныхСредств.ОсновноеСредство = ИзмененныеДанные.ОсновноеСредство", "") + "
			|	
			|	СГРУППИРОВАТЬ ПО
			|		ИзмененныеДанные.Организация) КАК РегистрацияТранспортныхСредств
			|
			|СГРУППИРОВАТЬ ПО
			|	РегистрацияТранспортныхСредств.Организация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ИзмененныеДанные.Организация,
			|	ИзмененныеДанные.Документ,
			|	&Операция КАК Операция,
			|	МИНИМУМ(НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(ПериодЗадания.Период, КВАРТАЛ), МЕСЯЦ)) КАК Месяц
			|ИЗ
			|	ИзмененныеДанные КАК ИзмененныеДанные
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодЗадания КАК ПериодЗадания
			|		ПО (ПериодЗадания.Организация = ИзмененныеДанные.Организация)
			|
			|СГРУППИРОВАТЬ ПО
			|	ИзмененныеДанные.Организация,
			|	ИзмененныеДанные.Документ
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ПериодЗадания
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ИзмененныеДанные";
	Иначе
		
		ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияУТ.РазделительЗапросовПакета() +
			"ВЫБРАТЬ
			|	ИзмененныеДанные.Организация,
			|	ИзмененныеДанные.Документ,
			|	&Операция КАК Операция,
			|	МИНИМУМ(НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(ИзмененныеДанные.Период, КВАРТАЛ), МЕСЯЦ)) КАК Месяц
			|ИЗ
			|	ИзмененныеДанные КАК ИзмененныеДанные
			|
			|СГРУППИРОВАТЬ ПО
			|	ИзмененныеДанные.Организация,
			|	ИзмененныеДанные.Документ
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ИзмененныеДанные";

	КонецЕсли; 
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДанныеТаблиц.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ДатаНачалаВеденияРеглУчета", Константы.ДатаНачалаВеденияРеглУчета.Получить());
	Запрос.УстановитьПараметр("Операция", Перечисления.ОперацииЗакрытияМесяца.РасчетТранспортногоНалога);
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	РегистрыСведений.ЗаданияКЗакрытиюМесяца.СоздатьЗаписиРегистраПоДаннымВыборки(РезультатЗапроса.Выбрать());
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Формирует задание к расчету земельного налога, на основании измененных данных.
//
// Параметры:
//  ДанныеТаблиц					- Структура - Содержит список таблиц в которых произошли изменения.
//  ЕстьОсновныеСредства 			- Булево 	- Истина, если измененные данные содержат информацию об основных средствах.
//  ПроверитьЧтоОСЗарегистрировано	- Булево	- Истина, если нужно сформировать задание только если земельный участок зарегистрирован.
//
Процедура СформироватьЗаданиеКРасчетуЗемельногоНалога(ДанныеТаблиц, ЕстьОсновныеСредства = Истина, ПроверитьЧтоОСЗарегистрировано = Истина) Экспорт
	
	СписокТаблиц = Новый Массив;
	СписокТаблиц.Добавить("ИзменениеПараметровОС");
	СписокТаблиц.Добавить("ИзменениеСпособаОтраженияИмущественныхНалогов");
	СписокТаблиц.Добавить("МестонахождениеОСБухгалтерскийУчет");
	СписокТаблиц.Добавить("ПорядокУплатыНалоговНаМестах");
	СписокТаблиц.Добавить("РегистрацияЗемельныхУчастков");
	СписокТаблиц.Добавить("РегламентнаяОперацияРасчетЗемельногоНалога");
	СписокТаблиц.Добавить("СпособыОтраженияРасходовПоИмущественнымНалогам");
	СписокТаблиц.Добавить("СтавкиНалогаНаИмущество");
	СписокТаблиц.Добавить("СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам");
	
	ТекстОбъединенияДанных = УчетОСВызовСервера.СформироватьТекстОбъединенияДанныхДляФормированияЗадания(
									СписокТаблиц, ЕстьОсновныеСредства, ДанныеТаблиц);
	
	Если ТекстОбъединенияДанных = "" Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	МИНИМУМ(ВЫБОР 
	|				КОГДА ТИПЗНАЧЕНИЯ(ИзмененныеДанные.Документ) = ТИП(Документ.ВводОстатковВнеоборотныхАктивов)
	|					ТОГДА ДОБАВИТЬКДАТЕ(ВводОстатковВнеоборотныхАктивов.Дата, МЕСЯЦ, 1)
	|				ИНАЧЕ ИзмененныеДанные.Период 
	|			КОНЕЦ)   КАК Период,
	|	ИзмененныеДанные.Организация       КАК Организация,
	|	" + ?(ЕстьОсновныеСредства, "ИзмененныеДанные.ОсновноеСредство КАК ОсновноеСредство,", "") + "
	|	ИзмененныеДанные.Документ          КАК Документ
	|ПОМЕСТИТЬ ИзмененныеДанные
	|ИЗ
	|	(" + ТекстОбъединенияДанных + ") КАК ИзмененныеДанные
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов КАК ВводОстатковВнеоборотныхАктивов
	|	ПО ВводОстатковВнеоборотныхАктивов.Ссылка = ИзмененныеДанные.Документ
	|
	|СГРУППИРОВАТЬ ПО
	|	ИзмененныеДанные.Организация,
	|	" + ?(ЕстьОсновныеСредства, "ИзмененныеДанные.ОсновноеСредство,", "") + "
	|	ИзмененныеДанные.Документ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|	" + ?(ЕстьОсновныеСредства, ",ОсновноеСредство", "");
	
	Если ПроверитьЧтоОСЗарегистрировано Тогда
		
		ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияУТ.РазделительЗапросовПакета() +
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	РегистрацияЗемельныхУчастковПоследние.Организация КАК Организация
			|ПОМЕСТИТЬ РегистрацияЗемельныхУчастковНаНачало
			|ИЗ
			|	(ВЫБРАТЬ
			|		РегистрацияЗемельныхУчастков.Организация КАК Организация,
			|		РегистрацияЗемельныхУчастков.ОсновноеСредство КАК ОсновноеСредство,
			|		МАКСИМУМ(РегистрацияЗемельныхУчастков.Период) КАК Период
			|	ИЗ
			|		ИзмененныеДанные КАК ИзмененныеДанные
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияЗемельныхУчастков КАК РегистрацияЗемельныхУчастков
			|			ПО (РегистрацияЗемельныхУчастков.Организация = ИзмененныеДанные.Организация)
			|				И (РегистрацияЗемельныхУчастков.Период < НАЧАЛОПЕРИОДА(ИзмененныеДанные.Период, КВАРТАЛ))
			|				" + ?(ЕстьОсновныеСредства, "И РегистрацияЗемельныхУчастков.ОсновноеСредство = ИзмененныеДанные.ОсновноеСредство", "") + "
			|	
			|	СГРУППИРОВАТЬ ПО
			|		РегистрацияЗемельныхУчастков.Организация,
			|		РегистрацияЗемельныхУчастков.ОсновноеСредство
			|	) КАК РегистрацияЗемельныхУчастковПоследние
			|
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияЗемельныхУчастков КАК РегистрацияЗемельныхУчастков
			|		ПО (РегистрацияЗемельныхУчастков.Организация = РегистрацияЗемельныхУчастковПоследние.Организация)
			|			И (РегистрацияЗемельныхУчастков.ОсновноеСредство = РегистрацияЗемельныхУчастковПоследние.ОсновноеСредство)
			|			И (РегистрацияЗемельныхУчастков.Период = РегистрацияЗемельныхУчастковПоследние.Период)
			|ГДЕ
			|	РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу
			|
			|СГРУППИРОВАТЬ ПО
			|	РегистрацияЗемельныхУчастковПоследние.Организация
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Организация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	РегистрацияЗемельныхУчастков.Организация,
			|	МИНИМУМ(РегистрацияЗемельныхУчастков.Период) КАК Период
			|ПОМЕСТИТЬ ПериодЗадания
			|ИЗ
			|	(ВЫБРАТЬ
			|		ИзмененныеДанные.Организация КАК Организация,
			|		МИНИМУМ(НАЧАЛОПЕРИОДА(ИзмененныеДанные.Период, МЕСЯЦ)) КАК Период
			|	ИЗ
			|		ИзмененныеДанные КАК ИзмененныеДанные
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрацияЗемельныхУчастковНаНачало КАК РегистрацияЗемельныхУчастковНаНачало
			|			ПО (РегистрацияЗемельныхУчастковНаНачало.Организация = ИзмененныеДанные.Организация)
			|	
			|	СГРУППИРОВАТЬ ПО
			|		ИзмененныеДанные.Организация
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ИзмененныеДанные.Организация,
			|		МИНИМУМ(НАЧАЛОПЕРИОДА(РегистрацияЗемельныхУчастков.Период, МЕСЯЦ))
			|	ИЗ
			|		ИзмененныеДанные КАК ИзмененныеДанные
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияЗемельныхУчастков КАК РегистрацияЗемельныхУчастков
			|			ПО (РегистрацияЗемельныхУчастков.Организация = ИзмененныеДанные.Организация)
			|				И (РегистрацияЗемельныхУчастков.Период >= НАЧАЛОПЕРИОДА(ИзмененныеДанные.Период, КВАРТАЛ))
			|				И (РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу)
			|				" + ?(ЕстьОсновныеСредства, "И РегистрацияЗемельныхУчастков.ОсновноеСредство = ИзмененныеДанные.ОсновноеСредство", "") + "
			|	
			|	СГРУППИРОВАТЬ ПО
			|		ИзмененныеДанные.Организация) КАК РегистрацияЗемельныхУчастков
			|
			|СГРУППИРОВАТЬ ПО
			|	РегистрацияЗемельныхУчастков.Организация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ИзмененныеДанные.Организация,
			|	ИзмененныеДанные.Документ,
			|	&Операция КАК Операция,
			|	МИНИМУМ(НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(ПериодЗадания.Период, КВАРТАЛ), МЕСЯЦ)) КАК Месяц
			|ИЗ
			|	ИзмененныеДанные КАК ИзмененныеДанные
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодЗадания КАК ПериодЗадания
			|		ПО (ПериодЗадания.Организация = ИзмененныеДанные.Организация)
			|
			|СГРУППИРОВАТЬ ПО
			|	ИзмененныеДанные.Организация,
			|	ИзмененныеДанные.Документ
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ПериодЗадания
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ИзмененныеДанные";
	Иначе
		
		ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияУТ.РазделительЗапросовПакета() +
			"ВЫБРАТЬ
			|	ИзмененныеДанные.Организация,
			|	ИзмененныеДанные.Документ,
			|	&Операция КАК Операция,
			|	МИНИМУМ(НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(ИзмененныеДанные.Период, КВАРТАЛ), МЕСЯЦ)) КАК Месяц
			|ИЗ
			|	ИзмененныеДанные КАК ИзмененныеДанные
			|
			|СГРУППИРОВАТЬ ПО
			|	ИзмененныеДанные.Организация,
			|	ИзмененныеДанные.Документ
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ИзмененныеДанные";
			
	КонецЕсли; 
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДанныеТаблиц.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ДатаНачалаВеденияРеглУчета", Константы.ДатаНачалаВеденияРеглУчета.Получить());
	Запрос.УстановитьПараметр("Операция", Перечисления.ОперацииЗакрытияМесяца.РасчетЗемельногоНалога);
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	РегистрыСведений.ЗаданияКЗакрытиюМесяца.СоздатьЗаписиРегистраПоДаннымВыборки(РезультатЗапроса.Выбрать());
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ПолучитьСостоянияОСОрганизацийДляФормированияЗаданийКЗакрытиюМесяца(ДанныеТаблиц, Регистратор) Экспорт

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СостоянияОСОрганизаций.Период КАК Период,
	|	СостоянияОСОрганизаций.Организация КАК Организация,
	|	СостоянияОСОрганизаций.Состояние КАК Состояние
	|ПОМЕСТИТЬ СостоянияОСОрганизаций
	|ИЗ
	|	РегистрСведений.СостоянияОСОрганизаций КАК СостоянияОСОрганизаций
	|ГДЕ
	|	СостоянияОСОрганизаций.Регистратор = &Регистратор
	|	И СостоянияОСОрганизаций.Активность";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДанныеТаблиц.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
