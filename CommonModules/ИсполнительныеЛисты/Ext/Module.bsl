
#Область СлужебныйПрограммныйИнтерфейс

// Заполняет движения условиями удержания по исполнительному документу.
//
// Параметры:
//	Движения
//	ДатаДействия
//	ИсполнительныйДокумент
//	УсловияИсполнительногоЛиста - структура с полями
//		СпособРасчета
//		ВидБазы.
//		Процент
//		Сумма
//		Числитель
//		Знаменатель
//		ПрожиточныйМинимум
//		Предел
//		УчитыватьБольничныеЛисты
//		ПлатежныйАгент
//		ТарифПлатежногоАгента
//
Процедура ЗарегистрироватьУсловияИсполнительногоЛиста(Движения, ДатаДействия, ИсполнительныйДокумент, УсловияИсполнительногоЛиста) Экспорт
	
	ДанныеДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ИсполнительныйДокумент, "ФизическоеЛицо,Организация");
	
	// Условия удержания
	НоваяСтрока = Движения.УсловияУдержанияПоИсполнительномуДокументу.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, УсловияИсполнительногоЛиста);
	НоваяСтрока.Период = ДатаДействия;
	НоваяСтрока.ИсполнительныйДокумент = ИсполнительныйДокумент;
	НоваяСтрока.ФизическоеЛицо = ДанныеДокумента.ФизическоеЛицо;
	НоваяСтрока.Организация = ДанныеДокумента.Организация;
	
	Движения.УсловияУдержанияПоИсполнительномуДокументу.Записывать = Истина;
	
КонецПроцедуры

// Выполняет расчет записей, если способ расчета - «Исполнительный лист».
//
Процедура РассчитатьУдержанияПоИсполнительнымЛистам(СпособРасчета, СтрокиРасчета, МенеджерВременныхТаблиц, ИсключаемыйРегистратор) Экспорт
	
	Если СпособРасчета <> Перечисления.СпособыРасчетаУдержаний.ИсполнительныйЛист Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторыСтрок = Новый Массив;
	СтрокиПоИдентификатору = Новый Соответствие;
	Для Каждого СтрокаТаблицы Из СтрокиРасчета Цикл		
		ИдентификаторыСтрок.Добавить(СтрокаТаблицы.ИдентификаторСтроки);
		СтрокиПоИдентификатору.Вставить(СтрокаТаблицы.ИдентификаторСтроки, СтрокаТаблицы);
	КонецЦикла;	
	
	УдалитьВТ = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ИдентификаторыСтрок", ИдентификаторыСтрок);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Удержания.ИдентификаторСтроки,
		|	Удержания.ДокументОснование КАК ИсполнительныйДокумент,
		|	Удержания.БазовыйПериодНачало КАК Период,
		|	НАЧАЛОПЕРИОДА(Удержания.БазовыйПериодНачало, МЕСЯЦ) КАК Месяц,
		|	Удержания.ФизическоеЛицо,
		|	Удержания.Организация
		|ПОМЕСТИТЬ ВТДанныеДляРасчета
		|ИЗ
		|	ВТРегистрРасчета_Удержания КАК Удержания
		|ГДЕ
		|	Удержания.ИдентификаторСтроки В(&ИдентификаторыСтрок)";
	Запрос.Выполнить();
	УдалитьВТ.Добавить("ВТДанныеДляРасчета");
	
	Если Не ЗарплатаКадры.ВТСодержитСтроки(МенеджерВременныхТаблиц, "ВТДанныеДляРасчета") Тогда
		ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, УдалитьВТ);
		Возврат;
	КонецЕсли;
	
	// Запрашиваем условия удержаний.
	ОписаниеВТ = ОписаниеВТУсловияУдержанийПоИсполнительнымДокументам();
	ОписаниеВТ.ИмяВТ = "ВТДанныеДляРасчета";
	СоздатьВТУсловияУдержанийПоИсполнительнымДокументам(МенеджерВременныхТаблиц, ОписаниеВТ);
	СоздатьВТУдержаноПоДокументам(МенеджерВременныхТаблиц, ОписаниеВТ, ИсключаемыйРегистратор);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеДляРасчета.ИсполнительныйДокумент,
		|	ДанныеДляРасчета.ИдентификаторСтроки,
		|	ЕСТЬNULL(УдержаноПоДокументам.СуммаУдержания, 0) КАК СуммаУдержания,
		|	УсловияУдержания.СпособРасчета,
		|	УсловияУдержания.ВидБазы,
		|	УсловияУдержания.Процент,
		|	УсловияУдержания.Сумма,
		|	УсловияУдержания.Числитель,
		|	УсловияУдержания.Знаменатель,
		|	УсловияУдержания.Предел,
		|	УсловияУдержания.ПрожиточныйМинимум,
		|	УсловияУдержания.УчитыватьБольничныеЛисты,
		|	УсловияУдержания.ПлатежныйАгент,
		|	УсловияУдержания.Получатель,
		|	УсловияУдержания.ВеличинаПрожиточногоМинимума
		|ИЗ
		|	ВТДанныеДляРасчета КАК ДанныеДляРасчета
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТУсловияУдержанийПоИсполнительнымДокументам КАК УсловияУдержания
		|		ПО (УсловияУдержания.ИсполнительныйДокумент = ДанныеДляРасчета.ИсполнительныйДокумент)
		|			И (УсловияУдержания.Период = ДанныеДляРасчета.Период)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУдержаноПоДокументам КАК УдержаноПоДокументам
		|		ПО (УдержаноПоДокументам.ИсполнительныйДокумент = ДанныеДляРасчета.ИсполнительныйДокумент)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДанныеДляРасчета.ИсполнительныйДокумент,
		|	ДанныеДляРасчета.ИдентификаторСтроки";
	Выборка = Запрос.Выполнить().Выбрать();
	
	КатегорииБольничного = ПланыВидовРасчета.Начисления.КатегорииОплатыБольничного();
	
	Пока Выборка.СледующийПоЗначениюПоля("ИсполнительныйДокумент") Цикл
		УдержаноПоДокументу = Выборка.СуммаУдержания;
		Пока Выборка.Следующий() Цикл
			Строка = СтрокиПоИдентификатору[Выборка.ИдентификаторСтроки];
			РасчетнаяБаза = Строка.Дополнительно.СуммаДополнения - Строка.Дополнительно.СуммаНДФЛ;
			Для Каждого СтрокаРасшифровки Из Строка.Дополнительно.РасшифровкаРасчетнойБазы Цикл
				РасчетнаяБаза = РасчетнаяБаза + СтрокаРасшифровки.Сумма + СтрокаРасшифровки.СуммаДополнения - СтрокаРасшифровки.СуммаНДФЛ;
				Если Не Выборка.УчитыватьБольничныеЛисты И КатегорииБольничного.Найти(СтрокаРасшифровки.КатегорияБазовогоНачисления) <> Неопределено Тогда
					// Больничный не учитывается, исключаем его сумму и долю пришедшегося на него НДФЛ, но дополнение оставляем.
					РасчетнаяБаза = РасчетнаяБаза - (СтрокаРасшифровки.Сумма - СтрокаРасшифровки.СуммаНДФЛ);
				КонецЕсли;
			КонецЦикла;
			СтрокаПоказателя = РасчетЗарплатыРасширенный.СтрокаЗначенияПоказателяПоИдентификатору(Строка.Показатели, "РасчетнаяБаза");
			СтрокаПоказателя.Значение = РасчетнаяБаза;
			Если Выборка.ВидБазы = Перечисления.ВидыБазыУдержанияПоИсполнительномуДокументу.ПрожиточныйМинимум Тогда
				// Расчетная база по данным прожиточного минимума.
				РасчетнаяБаза = Выборка.ВеличинаПрожиточногоМинимума;
			КонецЕсли;
			Если Выборка.СпособРасчета = Перечисления.СпособыРасчетаУдержанияПоИсполнительномуДокументу.ФиксированнойСуммой Тогда
				Строка.Результат = Выборка.Сумма;
			ИначеЕсли Выборка.СпособРасчета = Перечисления.СпособыРасчетаУдержанияПоИсполнительномуДокументу.Процентом Тогда
				Строка.Результат = РасчетнаяБаза * Выборка.Процент / 100;
			ИначеЕсли Выборка.СпособРасчета = Перечисления.СпособыРасчетаУдержанияПоИсполнительномуДокументу.Долей Тогда
				Строка.Результат = РасчетнаяБаза * Выборка.Числитель / Выборка.Знаменатель;
			КонецЕсли;
			// Если установлен предел удержания, то ограничиваем сумму.
			Если Выборка.Предел > 0 Тогда
				Строка.Результат = Мин(Выборка.Предел - УдержаноПоДокументу, Строка.Результат);
				УдержаноПоДокументу = УдержаноПоДокументу + Строка.Результат;
			КонецЕсли;
			Строка.Получатель = Выборка.Получатель;
			Строка.ЗаписьРасчета.Получатель = Выборка.Получатель;
			Строка.ПлатежныйАгент = Выборка.ПлатежныйАгент;
			Строка.ЗаписьРасчета.ПлатежныйАгент = Выборка.ПлатежныйАгент;
		КонецЦикла;
	КонецЦикла;
	
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, УдалитьВТ);
	
КонецПроцедуры

// Заполняет набор записей регистра УдержанийПоИсполнительнымДокументам при расчете для того, 
// чтобы учесть результаты расчета удержаний по исполнительным документам при расчете вознаграждения платежным агентам.
//
Процедура ЗарегистрироватьРасчетУдержанийПоИсполнительнымДокументам(ОчередностьРасчета, НаборыЗаписей, МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ОчередностьРасчета", ОчередностьРасчета);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Удержания.ДокументОснование КАК ИсполнительныйДокумент,
		|	Удержания.Получатель,
		|	Удержания.ПлатежныйАгент,
		|	НАЧАЛОПЕРИОДА(Удержания.БазовыйПериодНачало, МЕСЯЦ) КАК МесяцУдержания,
		|	СУММА(Удержания.Результат) КАК СуммаУдержания
		|ИЗ
		|	ВТРегистрРасчета_Удержания КАК Удержания
		|ГДЕ
		|	Удержания.ВидРасчета.СпособРасчета = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаУдержаний.ИсполнительныйЛист)
		|	И Удержания.ВидРасчета.ОчередностьРасчета = &ОчередностьРасчета
		|
		|СГРУППИРОВАТЬ ПО
		|	Удержания.Получатель,
		|	Удержания.ДокументОснование,
		|	Удержания.ПлатежныйАгент,
		|	НАЧАЛОПЕРИОДА(Удержания.БазовыйПериодНачало, МЕСЯЦ)
		|
		|ИМЕЮЩИЕ
		|	СУММА(Удержания.Результат) <> 0";
	Выборка = Запрос.Выполнить().Выбрать();
	
	УдержанияПоДокументам = ПустаяТаблицаУдержанияПоДокументам();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(УдержанияПоДокументам.Добавить(), Выборка);
	КонецЦикла;
	
	СформироватьУдержанияПоИсполнительнымДокументам(НаборыЗаписей, УдержанияПоДокументам);
	ЗарплатаКадры.СоздатьВТПоНаборуЗаписей(МенеджерВременныхТаблиц, НаборыЗаписей.УдержанияПоИсполнительнымДокументам);
	
КонецПроцедуры

// Выполняет расчет записей, если способ расчета - «Вознаграждение платежного агента».
//
Процедура РассчитатьВознагражденияПлатежныхАгентов(СпособРасчета, СтрокиРасчета, МенеджерВременныхТаблиц, ИсключаемыйРегистратор) Экспорт
	
	Если СпособРасчета <> Перечисления.СпособыРасчетаУдержаний.ВознаграждениеПлатежногоАгента Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторыСтрок = Новый Массив;
	СтрокиПоИдентификатору = Новый Соответствие;
	Для Каждого СтрокаТаблицы Из СтрокиРасчета Цикл		
		ИдентификаторыСтрок.Добавить(СтрокаТаблицы.ИдентификаторСтроки);
		СтрокиПоИдентификатору.Вставить(СтрокаТаблицы.ИдентификаторСтроки, СтрокаТаблицы);
	КонецЦикла;	
	
	УдалитьВТ = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ИдентификаторыСтрок", ИдентификаторыСтрок);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Удержания.ИдентификаторСтроки,
		|	Удержания.ДокументОснование КАК ИсполнительныйДокумент,
		|	Удержания.ФизическоеЛицо,
		|	Удержания.БазовыйПериодНачало КАК Период,
		|	Удержания.БазовыйПериодНачало,
		|	Удержания.БазовыйПериодКонец,
		|	Удержания.Получатель,
		|	Удержания.ПлатежныйАгент
		|ПОМЕСТИТЬ ВТДанныеДляРасчета
		|ИЗ
		|	ВТРегистрРасчета_Удержания КАК Удержания
		|ГДЕ
		|	Удержания.ИдентификаторСтроки В(&ИдентификаторыСтрок)";
	Запрос.Выполнить();
	УдалитьВТ.Добавить("ВТДанныеДляРасчета");
	
	Если Не ЗарплатаКадры.ВТСодержитСтроки(МенеджерВременныхТаблиц, "ВТДанныеДляРасчета") Тогда
		ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, УдалитьВТ);
		Возврат;
	КонецЕсли;
	
	// Запрашиваем условия удержаний.
	ОписаниеВТ = ОписаниеВТУсловияУдержанийПоИсполнительнымДокументам();
	ОписаниеВТ.ИмяВТ = "ВТДанныеДляРасчета";
	СоздатьВТУсловияУдержанийПоИсполнительнымДокументам(МенеджерВременныхТаблиц, ОписаниеВТ);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МИНИМУМ(Удержания.БазовыйПериодНачало) КАК НачалоПериода,
		|	МАКСИМУМ(Удержания.БазовыйПериодКонец) КАК ОкончаниеПериода
		|ИЗ
		|	ВТДанныеДляРасчета КАК Удержания";
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеДляРасчета.ФизическоеЛицо,
		|	УсловияУдержания.Получатель,
		|	УсловияУдержания.ПлатежныйАгент
		|ПОМЕСТИТЬ ВТФильтрУдержаний
		|ИЗ
		|	ВТУсловияУдержанийПоИсполнительнымДокументам КАК УсловияУдержания
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДанныеДляРасчета КАК ДанныеДляРасчета
		|		ПО УсловияУдержания.ИсполнительныйДокумент = ДанныеДляРасчета.ИсполнительныйДокумент
		|			И УсловияУдержания.Период = ДанныеДляРасчета.Период";
	Запрос.Выполнить();
	УдалитьВТ.Добавить("ВТФильтрУдержаний");
	
	ЗарплатаКадры.СоздатьПустуюВТ(МенеджерВременныхТаблиц, "РегистрНакопления.УдержанияПоИсполнительнымДокументам");
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Выборка.НачалоПериода));
	Запрос.УстановитьПараметр("ОкончаниеПериода", КонецМесяца(Выборка.ОкончаниеПериода));
	Запрос.УстановитьПараметр("Регистратор", ИсключаемыйРегистратор);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(УдержанияПоИсполнительнымДокументам.Период, МЕСЯЦ) КАК Месяц,
		|	УдержанияПоИсполнительнымДокументам.ФизическоеЛицо,
		|	УдержанияПоИсполнительнымДокументам.Получатель,
		|	УдержанияПоИсполнительнымДокументам.ПлатежныйАгент,
		|	СУММА(УдержанияПоИсполнительнымДокументам.СуммаУдержанияОборот) КАК СуммаУдержанияОборот,
		|	СУММА(УдержанияПоИсполнительнымДокументам.СуммаВознагражденияПлатежногоАгентаОборот) КАК СуммаВознагражденияПлатежногоАгентаОборот
		|ПОМЕСТИТЬ ВТСуммыУдержаний
		|ИЗ
		|	(ВЫБРАТЬ
		|		УдержанияОбороты.Период КАК Период,
		|		УдержанияОбороты.ФизическоеЛицо КАК ФизическоеЛицо,
		|		УдержанияОбороты.Получатель КАК Получатель,
		|		УдержанияОбороты.ПлатежныйАгент КАК ПлатежныйАгент,
		|		УдержанияОбороты.СуммаУдержанияОборот КАК СуммаУдержанияОборот,
		|		УдержанияОбороты.СуммаВознагражденияПлатежногоАгентаОборот КАК СуммаВознагражденияПлатежногоАгентаОборот
		|	ИЗ
		|		РегистрНакопления.УдержанияПоИсполнительнымДокументам.Обороты(
		|				&НачалоПериода,
		|				&ОкончаниеПериода,
		|				Месяц,
		|				(ФизическоеЛицо, Получатель, ПлатежныйАгент) В
		|					(ВЫБРАТЬ
		|						Условия.ФизическоеЛицо,
		|						Условия.Получатель,
		|						Условия.ПлатежныйАгент
		|					ИЗ
		|						ВТФильтрУдержаний КАК Условия)) КАК УдержанияОбороты
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		УдержанияОбороты.Период,
		|		УдержанияОбороты.ФизическоеЛицо,
		|		УдержанияОбороты.Получатель,
		|		УдержанияОбороты.ПлатежныйАгент,
		|		УдержанияОбороты.СуммаУдержания,
		|		УдержанияОбороты.СуммаВознагражденияПлатежногоАгента
		|	ИЗ
		|		ВТРегистрНакопления_УдержанияПоИсполнительнымДокументам КАК УдержанияОбороты
		|	ГДЕ
		|		УдержанияОбороты.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
		|		И (УдержанияОбороты.ФизическоеЛицо, УдержанияОбороты.Получатель, УдержанияОбороты.ПлатежныйАгент) В
		|				(ВЫБРАТЬ
		|					Условия.ФизическоеЛицо,
		|					Условия.Получатель,
		|					Условия.ПлатежныйАгент
		|				ИЗ
		|					ВТФильтрУдержаний КАК Условия)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		УдержанияРегистратора.Период,
		|		УдержанияРегистратора.ФизическоеЛицо,
		|		УдержанияРегистратора.Получатель,
		|		УдержанияРегистратора.ПлатежныйАгент,
		|		-УдержанияРегистратора.СуммаУдержания,
		|		-УдержанияРегистратора.СуммаВознагражденияПлатежногоАгента
		|	ИЗ
		|		РегистрНакопления.УдержанияПоИсполнительнымДокументам КАК УдержанияРегистратора
		|	ГДЕ
		|		УдержанияРегистратора.Регистратор = &Регистратор) КАК УдержанияПоИсполнительнымДокументам
		|
		|СГРУППИРОВАТЬ ПО
		|	УдержанияПоИсполнительнымДокументам.ФизическоеЛицо,
		|	УдержанияПоИсполнительнымДокументам.Получатель,
		|	УдержанияПоИсполнительнымДокументам.ПлатежныйАгент,
		|	НАЧАЛОПЕРИОДА(УдержанияПоИсполнительнымДокументам.Период, МЕСЯЦ)";
	Запрос.Выполнить();
	УдалитьВТ.Добавить("ВТСуммыУдержаний");
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеДляРасчета.ИдентификаторСтроки,
		|	ДанныеДляРасчета.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ДанныеДляРасчета.ИсполнительныйДокумент,
		|	ПрименяемыеТарифы.Получатель КАК Получатель,
		|	ПрименяемыеТарифы.ПлатежныйАгент КАК ПлатежныйАгент,
		|	ПрименяемыеТарифы.Период КАК Период,
		|	СуммыУдержаний.СуммаУдержанияОборот,
		|	ШкалаТарифа.Ссылка КАК Тариф,
		|	ВЫБОР
		|		КОГДА ШкалаТарифа.Порог = 0
		|			ТОГДА 2
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК ПриоритетПорога,
		|	ШкалаТарифа.Порог КАК Порог,
		|	ШкалаТарифа.Процент,
		|	ШкалаТарифа.Сумма,
		|	ШкалаТарифа.МинимальнаяСумма,
		|	ШкалаТарифа.МаксимальнаяСумма
		|ИЗ
		|	ВТДанныеДляРасчета КАК ДанныеДляРасчета
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТУсловияУдержанийПоИсполнительнымДокументам КАК ПрименяемыеТарифы
		|		ПО ДанныеДляРасчета.ИсполнительныйДокумент = ПрименяемыеТарифы.ИсполнительныйДокумент
		|			И ДанныеДляРасчета.Период = ПрименяемыеТарифы.Период
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ТарифыПлатежныхАгентов.Шкала КАК ШкалаТарифа
		|		ПО (ПрименяемыеТарифы.ТарифПлатежногоАгента = ШкалаТарифа.Ссылка)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСуммыУдержаний КАК СуммыУдержаний
		|		ПО (СуммыУдержаний.ФизическоеЛицо = ДанныеДляРасчета.ФизическоеЛицо)
		|			И (СуммыУдержаний.Получатель = ПрименяемыеТарифы.Получатель)
		|			И (СуммыУдержаний.ПлатежныйАгент = ПрименяемыеТарифы.ПлатежныйАгент)
		|			И (СуммыУдержаний.Месяц = НАЧАЛОПЕРИОДА(ДанныеДляРасчета.Период, МЕСЯЦ))
		|
		|УПОРЯДОЧИТЬ ПО
		|	ФизическоеЛицо,
		|	Получатель,
		|	ПлатежныйАгент,
		|	Период,
		|	ДанныеДляРасчета.ИсполнительныйДокумент,
		|	ДанныеДляРасчета.ИдентификаторСтроки,
		|	Тариф,
		|	ПриоритетПорога,
		|	Порог";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("ФизическоеЛицо") Цикл
		Пока Выборка.СледующийПоЗначениюПоля("Получатель") Цикл
			Пока Выборка.СледующийПоЗначениюПоля("ПлатежныйАгент") Цикл
				Пока Выборка.СледующийПоЗначениюПоля("Период") Цикл
					ИсполнительныйДокумент = Неопределено;
					Пока Выборка.СледующийПоЗначениюПоля("ИсполнительныйДокумент") Цикл
						Пока Выборка.СледующийПоЗначениюПоля("ИдентификаторСтроки") Цикл
							Если ИсполнительныйДокумент <> Неопределено Тогда
								Продолжить;
							КонецЕсли;
							Строка = СтрокиПоИдентификатору[Выборка.ИдентификаторСтроки];
							Тариф = Неопределено;
							Пока Выборка.Следующий() Цикл
								Тариф = Новый Структура("Процент, Сумма, МинимальнаяСумма, МаксимальнаяСумма");
								ЗаполнитьЗначенияСвойств(Тариф, Выборка);
								Если Выборка.СуммаУдержанияОборот <= Выборка.Порог Или Выборка.Порог = 0 Тогда
									Прервать;
								КонецЕсли;
							КонецЦикла;
							Если Тариф = Неопределено Тогда
								Продолжить;
							КонецЕсли;
							Строка.Получатель = Выборка.Получатель;
							Строка.ПлатежныйАгент = Выборка.ПлатежныйАгент;
							Строка.Результат = РазмерВознагражденияПлатежногоАгента(
								Выборка.СуммаУдержанияОборот, Тариф.Процент, Тариф.Сумма, Тариф.МинимальнаяСумма, Тариф.МаксимальнаяСумма);
						КонецЦикла;
						ИсполнительныйДокумент = Выборка.ИсполнительныйДокумент;
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, УдалитьВТ);

КонецПроцедуры

// Формирует движения по регистрам подсистемы.
//
// Параметры:
//		Движения - коллекция движений регистратора.
//		Отказ - признак отказа от заполнения движений.
//		Удержания - таблица значений с колонками
//			ИсполнительныйДокумент.
//			Получатель
//			ПлатежныйАгент
//			МесяцУдержания
//			СуммаУдержания
//			СуммаВознагражденияПлатежногоАгента.
//		ЗаписыватьДвижения - (необязательный), булево, по умолчанию Ложь, 
//			если Истина - наборы записей будут записаны после заполнения.
//
//		Допустимо присутствие других колонок в передаваемой таблице значений.
//
Процедура СформироватьУдержанияПоИсполнительнымДокументам(Движения, Удержания, ЗаписыватьДвижения = Ложь) Экспорт
	
	// Суммы в строках, где не заполнен исполнительный документ (это вознаграждения агента), 
	// распределяем пропорционально по получателю и платежному агенту.
	
	РаспределяемыеСуммы = Новый ТаблицаЗначений;
	РаспределяемыеСуммы.Колонки.Добавить("Получатель");
	РаспределяемыеСуммы.Колонки.Добавить("ПлатежныйАгент");
	РаспределяемыеСуммы.Колонки.Добавить("Сумма");
	
	ОтборСтрок = Новый Структура("Получатель, ПлатежныйАгент");
	
	// Переносим строки вознаграждения в таблицу распределяемых сумм.
	ИсполнительныеДокументы = Новый Массив;
	УдаляемыеСтроки = Новый Массив;
	Для Каждого Строка Из Удержания Цикл
		Если ЗначениеЗаполнено(Строка.ИсполнительныйДокумент) Тогда
			ИсполнительныеДокументы.Добавить(Строка.ИсполнительныйДокумент);
		Иначе
			ЗаполнитьЗначенияСвойств(ОтборСтрок, Строка);
			НайденныеСтроки = Удержания.НайтиСтроки(ОтборСтрок);
			Если Строка.СуммаВознагражденияПлатежногоАгента <> 0 Тогда
				Суммы = ЗарплатаКадрыКлиентСервер.РаспределитьПропорциональноКоэффициентам(
					Строка.СуммаВознагражденияПлатежногоАгента,
					ОбщегоНазначения.ВыгрузитьКолонку(НайденныеСтроки, "СуммаУдержания"));
				Индекс = 0;	
				Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
					НайденнаяСтрока.СуммаВознагражденияПлатежногоАгента = НайденнаяСтрока.СуммаВознагражденияПлатежногоАгента + Суммы[Индекс];
					Индекс = Индекс + 1;	
				КонецЦикла;
			КонецЕсли;
			УдаляемыеСтроки.Добавить(Строка);
		КонецЕсли;
	КонецЦикла;
	
	// Удаляем строки с не заполненным документом, эти суммы мы распределим по оставшимся строкам.
	Для Каждого Строка Из УдаляемыеСтроки Цикл
		Удержания.Удалить(Строка);
	КонецЦикла;
	
	ДанныеДокументов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
		ИсполнительныеДокументы, "ФизическоеЛицо, Организация");
	
	Для Каждого Строка Из Удержания Цикл
		ДанныеДокумента = ДанныеДокументов[Строка.ИсполнительныйДокумент];
		НоваяСтрока = Движения.УдержанияПоИсполнительнымДокументам.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		НоваяСтрока.Период = Строка.МесяцУдержания;
		НоваяСтрока.ФизическоеЛицо = ДанныеДокумента.ФизическоеЛицо;
		НоваяСтрока.Организация = ДанныеДокумента.Организация;
	КонецЦикла;
	Движения.УдержанияПоИсполнительнымДокументам.Записывать = Истина;
	
	Если ЗаписыватьДвижения Тогда
		УстановитьПривилегированныйРежим(Истина);
		Движения.УдержанияПоИсполнительнымДокументам.Записать();
		Движения.УдержанияПоИсполнительнымДокументам.Записывать = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Составляет временную таблицу с условиями удержаний по исполнительным листам
// ВТПоказателиУдержанийПоИсполнительнымДокументам.
//
Процедура СоздатьВТПоказателиУдержанийПоИсполнительнымДокументам(МенеджерВременныхТаблиц, СозданныеВТ) Экспорт
	
	Если СозданныеВТ.Найти("ВТПоказателиУдержанийПоИсполнительнымДокументам") <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	УдержанияЗаПериод.ДатаНачала КАК Период,
	|	УдержанияЗаПериод.ДокументОснование КАК ИсполнительныйДокумент
	|ПОМЕСТИТЬ ВТИзмеренияДаты
	|ИЗ
	|	ВТУдержанияЗаПериод КАК УдержанияЗаПериод";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних(
		"УсловияУдержанияПоИсполнительномуДокументу",
		МенеджерВременныхТаблиц,
		Истина,
		ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(
			"ВТИзмеренияДаты",
			"ИсполнительныйДокумент"));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УсловияУдержания.ИсполнительныйДокумент,
	|	УсловияУдержания.Получатель КАК Получатель,
	|	УсловияУдержания.Период,
	|	УсловияУдержания.СпособРасчета,
	|	УсловияУдержания.ВидБазы,
	|	УсловияУдержания.Процент,
	|	УсловияУдержания.Сумма,
	|	УсловияУдержания.Числитель,
	|	УсловияУдержания.Знаменатель,
	|	УсловияУдержания.Предел,
	|	УсловияУдержания.ПрожиточныйМинимум,
	|	УсловияУдержания.ПлатежныйАгент,
	|	УсловияУдержания.ТарифПлатежногоАгента,
	|	УсловияУдержания.УчитыватьБольничныеЛисты
	|ПОМЕСТИТЬ ВТУсловияУдержанияПоИсполнительнымДокументам
	|ИЗ
	|	ВТУсловияУдержанияПоИсполнительномуДокументуСрезПоследних КАК УсловияУдержания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПлатежныеАгенты.ИсполнительныйДокумент,
	|	МАКСИМУМ(ПлатежныеАгенты.Период) КАК Период
	|ПОМЕСТИТЬ ВТПлатежныеАгентыМаксимальныеПериоды
	|ИЗ
	|	ВТУсловияУдержанияПоИсполнительнымДокументам КАК ПлатежныеАгенты
	|
	|СГРУППИРОВАТЬ ПО
	|	ПлатежныеАгенты.ИсполнительныйДокумент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПлатежныеАгенты.ИсполнительныйДокумент,
	|	ПлатежныеАгенты.ПлатежныйАгент,
	|	ПлатежныеАгенты.ТарифПлатежногоАгента
	|ПОМЕСТИТЬ ВТПлатежныеАгенты
	|ИЗ
	|	ВТУсловияУдержанияПоИсполнительнымДокументам КАК ПлатежныеАгенты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПлатежныеАгентыМаксимальныеПериоды КАК МаксимальныеПериоды
	|		ПО (МаксимальныеПериоды.ИсполнительныйДокумент = ПлатежныеАгенты.ИсполнительныйДокумент)
	|			И (МаксимальныеПериоды.Период = ПлатежныеАгенты.Период)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УсловияУдержания.ИсполнительныйДокумент,
	|	УсловияУдержания.ИсполнительныйДокумент КАК ДокументОснование,
	|	УсловияУдержания.Период,
	|	ЗНАЧЕНИЕ(Перечисление.КатегорииУдержаний.ИсполнительныйЛист) КАК КатегорияУдержания,
	|	УсловияУдержания.Получатель КАК Получатель,
	|	ПлатежныеАгенты.ПлатежныйАгент,
	|	ПлатежныеАгенты.ТарифПлатежногоАгента,
	|	УсловияУдержания.СпособРасчета,
	|	УсловияУдержания.ВидБазы,
	|	УсловияУдержания.Процент,
	|	УсловияУдержания.Сумма,
	|	УсловияУдержания.Числитель,
	|	УсловияУдержания.Знаменатель,
	|	УсловияУдержания.Предел,
	|	УсловияУдержания.ПрожиточныйМинимум,
	|	УсловияУдержания.УчитыватьБольничныеЛисты
	|ПОМЕСТИТЬ ВТПоказателиУдержанийПоИсполнительнымДокументам
	|ИЗ
	|	ВТУсловияУдержанияПоИсполнительнымДокументам КАК УсловияУдержания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПлатежныеАгенты КАК ПлатежныеАгенты
	|		ПО (ПлатежныеАгенты.ИсполнительныйДокумент = УсловияУдержания.ИсполнительныйДокумент)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УсловияУдержания.ИсполнительныйДокумент,
	|	NULL,
	|	УсловияУдержания.Период,
	|	ЗНАЧЕНИЕ(Перечисление.КатегорииУдержаний.ВознаграждениеПлатежногоАгента),
	|	УсловияУдержания.Получатель,
	|	ПлатежныеАгенты.ПлатежныйАгент,
	|	ПлатежныеАгенты.ТарифПлатежногоАгента,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL
	|ИЗ
	|	ВТУсловияУдержанияПоИсполнительнымДокументам КАК УсловияУдержания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПлатежныеАгенты КАК ПлатежныеАгенты
	|		ПО (ПлатежныеАгенты.ИсполнительныйДокумент = УсловияУдержания.ИсполнительныйДокумент)";
	
	Запрос.Выполнить();
	
	СозданныеВТ.Добавить("ВТПоказателиУдержанийПоИсполнительнымДокументам");
	
КонецПроцедуры

// Заполнение сведений о показателях, используемых при расчете результата предопределенным способом.
//
// Параметры:
//	- ТаблицаПоказателей - таблица значений с колонками
//		СпособРасчета.
//		Показатель
//
Процедура ЗаполнитьПоказателиРасчетаИсполнительногоЛиста(ТаблицаПоказателей) Экспорт
	
	// Исполнительные листы
	НоваяСтрока = ТаблицаПоказателей.Добавить();
	НоваяСтрока.СпособРасчета = Перечисления.СпособыРасчетаУдержаний.ИсполнительныйЛист;
	НоваяСтрока.Показатель = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ПоказателиРасчетаЗарплаты.РасчетнаяБаза");
	
КонецПроцедуры

// Конструирует таблицу значений для регистрации движений при помощи
//	метода СформироватьУдержанияПоИсполнительнымДокументам.
//
Функция ПустаяТаблицаУдержанияПоДокументам() Экспорт
	
	УдержанияПоДокументам = Новый ТаблицаЗначений;
	УдержанияПоДокументам.Колонки.Добавить("ИсполнительныйДокумент", Новый ОписаниеТипов("ДокументСсылка.ИсполнительныйЛист"));
	УдержанияПоДокументам.Колонки.Добавить("Получатель", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	УдержанияПоДокументам.Колонки.Добавить("ПлатежныйАгент", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	УдержанияПоДокументам.Колонки.Добавить("МесяцУдержания", Новый ОписаниеТипов("Дата"));
	УдержанияПоДокументам.Колонки.Добавить("СуммаУдержания", Новый ОписаниеТипов("Число"));
	УдержанияПоДокументам.Колонки.Добавить("СуммаВознагражденияПлатежногоАгента", Новый ОписаниеТипов("Число"));
	
	Возврат УдержанияПоДокументам;
	
КонецФункции

Процедура ДополнитьЦеломесячнымиЗаписямиУдержаний(МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ИскусственныйРегистратор", Документы.НачислениеЗарплаты.ПолучитьСсылку());
	
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК Поле1
		|ИЗ
		|	ВТЗаписиУдержаний КАК ЗаписиУдержаний
		|ГДЕ
		|	ЗаписиУдержаний.ВидРасчета.КатегорияУдержания = ЗНАЧЕНИЕ(Перечисление.КатегорииУдержаний.ИсполнительныйЛист)";
		
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		// Нет исполнительных листов.
		Возврат;
	КонецЕсли;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаписиУдержаний.ФизическоеЛицо,
		|	ЗаписиУдержаний.Организация,
		|	ЗаписиУдержаний.ВидРасчета,
		|	ЗаписиУдержаний.ДокументОснование,
		|	ЗаписиУдержаний.ПериодРегистрации,
		|	ЗаписиУдержаний.МесяцУдержания,
		|	МИНИМУМ(ЗаписиУдержаний.БазовыйПериодНачало) КАК Начало,
		|	МАКСИМУМ(ЗаписиУдержаний.БазовыйПериодКонец) КАК Окончание
		|ПОМЕСТИТЬ ВТИзмеренияЗаписейНеполныйМесяц
		|ИЗ
		|	ВТЗаписиУдержаний КАК ЗаписиУдержаний
		|ГДЕ
		|	(ЗаписиУдержаний.Начинается
		|			ИЛИ ЗаписиУдержаний.Заканчивается)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаписиУдержаний.ФизическоеЛицо,
		|	ЗаписиУдержаний.Организация,
		|	ЗаписиУдержаний.ВидРасчета,
		|	ЗаписиУдержаний.ДокументОснование,
		|	ЗаписиУдержаний.ПериодРегистрации,
		|	ЗаписиУдержаний.МесяцУдержания
		|
		|ИМЕЮЩИЕ
		|	(МИНИМУМ(ЗаписиУдержаний.БазовыйПериодНачало) > НАЧАЛОПЕРИОДА(ЗаписиУдержаний.МесяцУдержания, МЕСЯЦ)
		|		ИЛИ МАКСИМУМ(ЗаписиУдержаний.БазовыйПериодКонец) < КОНЕЦПЕРИОДА(ЗаписиУдержаний.МесяцУдержания, МЕСЯЦ))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОсновныеЗаписи.ПериодРегистрации,
		|	ОсновныеЗаписи.Регистратор,
		|	ОсновныеЗаписи.НомерСтроки,
		|	ОсновныеЗаписи.ФизическоеЛицо,
		|	ОсновныеЗаписи.Организация,
		|	ОсновныеЗаписи.ВидРасчета,
		|	ОсновныеЗаписи.Сотрудник,
		|	ОсновныеЗаписи.ДокументОснование,
		|	ОсновныеЗаписи.БазовыйПериодНачало,
		|	ОсновныеЗаписи.БазовыйПериодКонец,
		|	ОсновныеЗаписи.ИдентификаторСтроки,
		|	ОсновныеЗаписи.МесяцУдержания,
		|	ОсновныеЗаписи.Начинается,
		|	ОсновныеЗаписи.Заканчивается,
		|	ОсновныеЗаписи.ОригинальнаяЗапись
		|ПОМЕСТИТЬ ВТВсеОсновныеЗаписи
		|ИЗ
		|	ВТОсновныеЗаписи КАК ОсновныеЗаписи
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТОсновныеЗаписи
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаписиУдержаний.ПериодРегистрации,
		|	ЗаписиУдержаний.Регистратор,
		|	ЗаписиУдержаний.НомерСтроки,
		|	ЗаписиУдержаний.ФизическоеЛицо,
		|	ЗаписиУдержаний.Организация,
		|	ЗаписиУдержаний.Сотрудник,
		|	ЗаписиУдержаний.ВидРасчета,
		|	ЗаписиУдержаний.ДокументОснование,
		|	ЗаписиУдержаний.БазовыйПериодНачало,
		|	ЗаписиУдержаний.БазовыйПериодКонец,
		|	ЗаписиУдержаний.ИдентификаторСтроки,
		|	ЗаписиУдержаний.МесяцУдержания,
		|	ЗаписиУдержаний.Начинается,
		|	ЗаписиУдержаний.Заканчивается,
		|	ЗаписиУдержаний.ОригинальнаяЗапись
		|ПОМЕСТИТЬ ВТОсновныеЗаписи
		|ИЗ
		|	ВТВсеОсновныеЗаписи КАК ЗаписиУдержаний
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗаписиУдержаний.ПериодРегистрации,
		|	&ИскусственныйРегистратор,
		|	ЗаписиУдержаний.НомерСтроки,
		|	ЗаписиУдержаний.ФизическоеЛицо,
		|	ЗаписиУдержаний.Организация,
		|	ЗаписиУдержаний.Сотрудник,
		|	ЗаписиУдержаний.ВидРасчета,
		|	ЗаписиУдержаний.ДокументОснование,
		|	НАЧАЛОПЕРИОДА(ЗаписиУдержаний.БазовыйПериодНачало, МЕСЯЦ),
		|	КОНЕЦПЕРИОДА(ЗаписиУдержаний.БазовыйПериодКонец, МЕСЯЦ),
		|	ЗаписиУдержаний.ИдентификаторСтроки,
		|	ЗаписиУдержаний.МесяцУдержания,
		|	ЛОЖЬ,
		|	ЛОЖЬ,
		|	ЛОЖЬ
		|ИЗ
		|	ВТЗаписиУдержаний КАК ЗаписиУдержаний
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИзмеренияЗаписейНеполныйМесяц КАК ЗаписиНеполныйМесяц
		|		ПО ЗаписиУдержаний.ФизическоеЛицо = ЗаписиНеполныйМесяц.ФизическоеЛицо
		|			И ЗаписиУдержаний.Организация = ЗаписиНеполныйМесяц.Организация
		|			И ЗаписиУдержаний.ВидРасчета = ЗаписиНеполныйМесяц.ВидРасчета
		|			И ЗаписиУдержаний.ДокументОснование = ЗаписиНеполныйМесяц.ДокументОснование
		|			И ЗаписиУдержаний.ПериодРегистрации = ЗаписиНеполныйМесяц.ПериодРегистрации
		|			И ЗаписиУдержаний.МесяцУдержания = ЗаписиНеполныйМесяц.МесяцУдержания";
		
	Запрос.Выполнить();	
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, "ВТИзмеренияЗаписейНеполныйМесяц");
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, "ВТВсеОсновныеЗаписи");
	
КонецПроцедуры

Процедура ЗаполнитьПризнакУдерживаетсяВЦеломЗаМесяц(МенеджерВременныхТаблиц) Экспорт
	
	УдалитьВТ = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Удержания.ДокументОснование КАК ИсполнительныйДокумент,
		|	Удержания.ДатаНачала КАК Период
		|ПОМЕСТИТЬ ВТИсходныеДанные
		|ИЗ
		|	ВТУдержания КАК Удержания
		|ГДЕ
		|	Удержания.Удержание.КатегорияУдержания = ЗНАЧЕНИЕ(Перечисление.КатегорииУдержаний.ИсполнительныйЛист)";
	Запрос.Выполнить();
	УдалитьВТ.Добавить("ВТИсходныеДанные");
	
	Если Не ЗарплатаКадры.ВТСодержитСтроки(МенеджерВременныхТаблиц, "ВТИсходныеДанные") Тогда
		ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, УдалитьВТ);
		Возврат;
	КонецЕсли;
	
	// Запрашиваем условия удержаний.
	ОписаниеВТ = ОписаниеВТУсловияУдержанийПоИсполнительнымДокументам();
	ОписаниеВТ.ИмяВТ = "ВТИсходныеДанные";
	СоздатьВТУсловияУдержанийПоИсполнительнымДокументам(МенеджерВременныхТаблиц, ОписаниеВТ);
	
	ПоляИсключения = Новый Массив;
	ПоляИсключения.Добавить("ДокументОснование");
	ПоляИсключения.Добавить("УдерживаетсяВЦеломЗаМесяц");
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	&ИменаПолей,
		|	Удержания.ДокументОснование,
		|	Удержания.УдерживаетсяВЦеломЗаМесяц
		|ПОМЕСТИТЬ ВТВсеУдержания
		|ИЗ
		|	ВТУдержания КАК Удержания";
	ЗарплатаКадры.ЗаполнитьИменаПолейВТ(Запрос.Текст, МенеджерВременныхТаблиц, "ВТУдержания", "Удержания", ПоляИсключения);
	Запрос.Выполнить();
	УдалитьВТ.Добавить("ВТВсеУдержания");
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, "ВТУдержания");
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	&ИменаПолей,
		|	Удержания.ДокументОснование,
		|	ВЫБОР
		|		КОГДА УсловияУдержаний.ИсполнительныйДокумент ЕСТЬ NULL 
		|			ТОГДА Удержания.УдерживаетсяВЦеломЗаМесяц
		|		ИНАЧЕ ВЫБОР
		|				КОГДА УсловияУдержаний.СпособРасчета = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаУдержанияПоИсполнительномуДокументу.ФиксированнойСуммой)
		|						ИЛИ УсловияУдержаний.ВидБазы = ЗНАЧЕНИЕ(Перечисление.ВидыБазыУдержанияПоИсполнительномуДокументу.ПрожиточныйМинимум)
		|					ТОГДА ИСТИНА
		|				ИНАЧЕ ЛОЖЬ
		|			КОНЕЦ
		|	КОНЕЦ КАК УдерживаетсяВЦеломЗаМесяц
		|ПОМЕСТИТЬ ВТУдержания
		|ИЗ
		|	ВТВсеУдержания КАК Удержания
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУсловияУдержанийПоИсполнительнымДокументам КАК УсловияУдержаний
		|		ПО (УсловияУдержаний.ИсполнительныйДокумент = Удержания.ДокументОснование)
		|			И (УсловияУдержаний.Период = Удержания.ДатаНачала)";
	ЗарплатаКадры.ЗаполнитьИменаПолейВТ(Запрос.Текст, МенеджерВременныхТаблиц, "ВТВсеУдержания", "Удержания", ПоляИсключения);
	Запрос.Выполнить();
	
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, УдалитьВТ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РазмерВознагражденияПлатежногоАгента(СуммаПлатежа, Процент, Сумма, МинимальнаяСумма, МаксимальнаяСумма)
	
	Вознаграждение = 0;
	
	// Рассчитываем процентом
	Если Процент > 0 Тогда
		Вознаграждение = СуммаПлатежа * Процент / 100;
	КонецЕсли;
	
	// Если указана сумма, то она взимается в дополнение к проценту.
	Вознаграждение = Вознаграждение + Сумма;
	
	// Обрабатываем ограничения
	Если Вознаграждение < МинимальнаяСумма Тогда
		Вознаграждение = МинимальнаяСумма;
	КонецЕсли;
	
	Если Вознаграждение > МаксимальнаяСумма И МаксимальнаяСумма > 0 Тогда
		Вознаграждение = МаксимальнаяСумма;
	КонецЕсли;
	
	Возврат Вознаграждение;
	
КонецФункции

Функция ОписаниеВТУсловияУдержанийПоИсполнительнымДокументам()
	
	ОписаниеВТ = Новый Структура(
		"ИмяВТ");
	
	Возврат ОписаниеВТ;
	
КонецФункции

// Ожидается ВТ с полями
//	- ИсполнительныйДокумент
//	- Период
//
Процедура СоздатьВТУсловияУдержанийПоИсполнительнымДокументам(МенеджерВременныхТаблиц, ОписаниеВТ)
	
	Если ЗарплатаКадры.ВТСуществует(МенеджерВременныхТаблиц, "ВТУсловияУдержанийПоИсполнительнымДокументам") Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьВТ = Новый Массив;
	
	// Запрашиваем условия удержаний.
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра(ОписаниеВТ.ИмяВТ, "ИсполнительныйДокумент");
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних("УсловияУдержанияПоИсполнительномуДокументу", МенеджерВременныхТаблиц, Истина, ОписаниеФильтра);
	УдалитьВТ.Добавить("ВТУсловияУдержанияПоИсполнительномуДокументуСрезПоследних");
	
	// Запрашиваем величину прожиточных минимумов.
	ОписаниеФильтра = ЗарплатаКадрыОбщиеНаборыДанных.ОписаниеФильтраДляСоздатьВТИмяРегистра("ВТУсловияУдержанияПоИсполнительномуДокументуСрезПоследних", "ПрожиточныйМинимум");
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТИмяРегистраСрезПоследних("ВеличинаПрожиточногоМинимума", МенеджерВременныхТаблиц, Истина, ОписаниеФильтра);
	УдалитьВТ.Добавить("ВТВеличинаПрожиточногоМинимумаСрезПоследних");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УсловияУдержания.ИсполнительныйДокумент,
		|	УсловияУдержания.Период,
		|	УсловияУдержания.СпособРасчета,
		|	УсловияУдержания.ВидБазы,
		|	УсловияУдержания.Процент,
		|	УсловияУдержания.Сумма,
		|	УсловияУдержания.Числитель,
		|	УсловияУдержания.Знаменатель,
		|	УсловияУдержания.Предел,
		|	УсловияУдержания.ПрожиточныйМинимум,
		|	УсловияУдержания.УчитыватьБольничныеЛисты,
		|	УсловияУдержания.Получатель,
		|	УсловияУдержания.ПлатежныйАгент,
		|	УсловияУдержания.ТарифПлатежногоАгента,
		|	ВеличинаПрожиточногоМинимума.Размер КАК ВеличинаПрожиточногоМинимума
		|ПОМЕСТИТЬ ВТУсловияУдержанийПоИсполнительнымДокументам
		|ИЗ
		|	ВТУсловияУдержанияПоИсполнительномуДокументуСрезПоследних КАК УсловияУдержания
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВеличинаПрожиточногоМинимумаСрезПоследних КАК ВеличинаПрожиточногоМинимума
		|		ПО УсловияУдержания.ПрожиточныйМинимум = ВеличинаПрожиточногоМинимума.ПрожиточныйМинимум
		|			И УсловияУдержания.Период = ВеличинаПрожиточногоМинимума.Период";
	Запрос.Выполнить();
	
	ЗарплатаКадры.УничтожитьВТ(МенеджерВременныхТаблиц, УдалитьВТ);
	
КонецПроцедуры

// Ожидается ВТ с полями
//	- ИсполнительныйДокумент
//
Процедура СоздатьВТУдержаноПоДокументам(МенеджерВременныхТаблиц, ОписаниеВТ, ИсключаемыйРегистратор)
	
	Если ЗарплатаКадры.ВТСуществует(МенеджерВременныхТаблиц, "ВТУдержаноПоДокументам") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ИсключаемыйРегистратор", ИсключаемыйРегистратор);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УдержаноПоДокументам.ИсполнительныйДокумент,
		|	СУММА(УдержаноПоДокументам.СуммаУдержания) КАК СуммаУдержания
		|ПОМЕСТИТЬ ВТУдержаноПоДокументам
		|ИЗ
		|	(ВЫБРАТЬ
		|		УдержанияПоРегистратору.ИсполнительныйДокумент КАК ИсполнительныйДокумент,
		|		УдержанияПоРегистратору.СуммаУдержания КАК СуммаУдержания
		|	ИЗ
		|		РегистрНакопления.УдержанияПоИсполнительнымДокументам КАК УдержанияПоРегистратору
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИсполнительныеДокументы КАК ИсполнительныеДокументы
		|			ПО (ИсполнительныеДокументы.ИсполнительныйДокумент = УдержанияПоРегистратору.ИсполнительныйДокумент)
		|				И (ИсполнительныеДокументы.Месяц > НАЧАЛОПЕРИОДА(УдержанияПоРегистратору.Период, МЕСЯЦ))
		|				И (УдержанияПоРегистратору.Регистратор <> &ИсключаемыйРегистратор)) КАК УдержаноПоДокументам
		|
		|СГРУППИРОВАТЬ ПО
		|	УдержаноПоДокументам.ИсполнительныйДокумент
		|
		|ИМЕЮЩИЕ
		|	СУММА(УдержаноПоДокументам.СуммаУдержания) <> 0";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТИсполнительныеДокументы", ОписаниеВТ.ИмяВТ);
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПроверитьДатуИсполнительногоЛиста(ИсполнительныйЛист, ДатаСобытия, Регистратор, ИмяРеквизитаДатаСобытия, Отказ) Экспорт 
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Период", ДатаСобытия);
	Запрос.УстановитьПараметр("ИсполнительныйЛист", ИсполнительныйЛист);
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	УсловияУдержанияПоИсполнительномуДокументу.Период,
	               |	УсловияУдержанияПоИсполнительномуДокументу.Регистратор
	               |ИЗ
	               |	РегистрСведений.УсловияУдержанияПоИсполнительномуДокументу КАК УсловияУдержанияПоИсполнительномуДокументу
	               |ГДЕ
	               |	УсловияУдержанияПоИсполнительномуДокументу.Регистратор <> &Регистратор
	               |	И УсловияУдержанияПоИсполнительномуДокументу.ИсполнительныйДокумент = &ИсполнительныйЛист
	               |	И УсловияУдержанияПоИсполнительномуДокументу.Период = &Период";
				   
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		ШаблонСообщения = НСтр("ru = 'На дату %1 уже проведен документ %2.'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, Формат(Выборка.Период, "ДЛФ=Д"), Выборка.Регистратор);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , ИмяРеквизитаДатаСобытия, "Объект", Отказ);
	КонецЕсли;
	
КонецПроцедуры

#Область БлокФункцийПервоначальногоЗаполненияИОбновленияИБ

// Добавляет в список Обработчики процедуры-обработчики обновления,
// необходимые данной подсистеме.
//
// Параметры:
//   Обработчики - ТаблицаЗначений - см. описание функции НоваяТаблицаОбработчиковОбновления
//                                   общего модуля ОбновлениеИнформационнойБазы.
// 
Процедура ЗарегистрироватьОбработчикиОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.9.25";
	Обработчик.Процедура = "ИсполнительныеЛисты.ЗаполнитьПолучателяИТарифПлатежногоАгентаВУсловияхУдержания";
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.0.26.8";
	Обработчик.Процедура = "ИсполнительныеЛисты.УстановитьИспользованиеПараметровНаборовСвойств";
		
КонецПроцедуры

Процедура ЗаполнитьПолучателяИТарифПлатежногоАгентаВУсловияхУдержания() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	УсловияУдержания.Регистратор
	|ПОМЕСТИТЬ ВТРегистраторы
	|ИЗ
	|	РегистрСведений.УсловияУдержанияПоИсполнительномуДокументу КАК УсловияУдержания
	|ГДЕ
	|	(УсловияУдержания.ПлатежныйАгент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|				И УсловияУдержания.ТарифПлатежногоАгента = ЗНАЧЕНИЕ(Справочник.ТарифыПлатежныхАгентов.ПустаяСсылка)
	|			ИЛИ УсловияУдержания.Получатель = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УсловияУдержания.Период,
	|	УсловияУдержания.Регистратор,
	|	УсловияУдержания.НомерСтроки,
	|	УсловияУдержания.Активность,
	|	УсловияУдержания.ИсполнительныйДокумент,
	|	УсловияУдержания.СпособРасчета,
	|	УсловияУдержания.ВидБазы,
	|	УсловияУдержания.Процент,
	|	УсловияУдержания.Сумма,
	|	УсловияУдержания.Числитель,
	|	УсловияУдержания.Знаменатель,
	|	УсловияУдержания.ПрожиточныйМинимум,
	|	УсловияУдержания.Предел,
	|	УсловияУдержания.УчитыватьБольничныеЛисты,
	|	УсловияУдержания.ПлатежныйАгент,
	|	ИсполнительныйЛист.ТарифПлатежногоАгента,
	|	УсловияУдержания.ФизическоеЛицо,
	|	УсловияУдержания.Организация,
	|	ИсполнительныйЛист.Получатель
	|ИЗ
	|	РегистрСведений.УсловияУдержанияПоИсполнительномуДокументу КАК УсловияУдержания
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИсполнительныйЛист КАК ИсполнительныйЛист
	|		ПО (ИсполнительныйЛист.Ссылка = УсловияУдержания.ИсполнительныйДокумент)
	|ГДЕ
	|	УсловияУдержания.Регистратор В
	|			(ВЫБРАТЬ
	|				ВТРегистраторы.Регистратор
	|			ИЗ
	|				ВТРегистраторы)
	|
	|УПОРЯДОЧИТЬ ПО
	|	УсловияУдержания.Регистратор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Регистратор") Цикл
		НаборЗаписей = РегистрыСведений.УсловияУдержанияПоИсполнительномуДокументу.СоздатьНаборЗаписей();
		Пока Выборка.Следующий() Цикл
		    ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
		КонецЦикла;
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		НаборЗаписей.Записать();
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьИспользованиеПараметровНаборовСвойств() Экспорт
	
	ПараметрыНабора = УправлениеСвойствами.СтруктураПараметровНабораСвойств();
	ПараметрыНабора.Используется = ПолучитьФункциональнуюОпцию("ИспользоватьИсполнительныеЛисты");
	
	УправлениеСвойствами.УстановитьПараметрыНабораСвойств("Документ_ИсполнительныйЛист", ПараметрыНабора);
	УправлениеСвойствами.УстановитьПараметрыНабораСвойств("Документ_ИзменениеУсловийИсполнительногоЛиста", ПараметрыНабора);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
