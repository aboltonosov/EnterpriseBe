
////////////////////////////////////////////////////////////////////////////////
// Модуль "ОтражениеЗарплатыВФинансовомУчетеУП" содержит процедуры и функции для 
// обслуживания механизмов отражения зарплаты в финансовом учете.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область ФормированиеДвижений

// Процедура формирования движений по регистру "Заказы на производство трудозатраты".
//
// Параметры:
//	ДополнительныеСвойства - Структура - структура с дополнительными свойствами документа, используемыми при проведении.
//	Движения - КоллекцияДвижений - коллекция движений документа.
//	Отказ - Булево - Признак отказа от проведения документа.
//
Процедура ОтразитьЗаказыНаПроизводствоТрудозатраты(ДополнительныеСвойства, Движения, Отказ) Экспорт

	Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗаказыНаПроизводствоТрудозатраты;
	
	Если Отказ ИЛИ Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Набор = Движения.ЗаказыНаПроизводствоТрудозатраты;
	Набор.Записывать = Истина;
	Набор.Загрузить(Таблица);
	
КонецПроцедуры

// Процедура формирования движений по регистру "Трудозатраты незавершенного производства".
//
// Параметры:
//	ДополнительныеСвойства - Структура - структура с дополнительными свойствами документа, используемыми при проведении.
//	Движения - КоллекцияДвижений - коллекция движений документа.
//	Отказ - Булево - Признак отказа от проведения документа.
//
Процедура ОтразитьТрудозатратыНезавершенногоПроизводства(ДополнительныеСвойства, Движения, Отказ) Экспорт
	
	Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаТрудозатратыНезавершенногоПроизводства;
	
	Если Отказ ИЛИ Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Набор = Движения.ТрудозатратыНезавершенногоПроизводства;
	Набор.Записывать = Истина;
	Набор.Загрузить(Таблица);
	
КонецПроцедуры

// Процедура формирует дополнительные движения для документов блока расчета зарплаты.
//
// Параметры:
//	Движения - КоллекцияДвижений - Коллекция наборов записей движений документа.
//	Отказ - Булево - Признак отказа от проведения документа.
//	Организация - СправочникСсылка.Организации - Организация, по которой вводится документ.
//	ПериодРегистрации - Период - Отчетный период.
//  ДанныеДляПроведения - Структура, ключ - имя регистра, значение - таблица значений с движениями документа
//						это источник данных, если Неопределено, тогда источник данных - движения регистров.
//	СтрокаСписокТаблиц - Строка - Список таблиц для отражения в финансовом учете.
//
Процедура СформироватьДвиженияПоДокументу(Движения, Отказ, Организация, ПериодРегистрации, ДанныеДляПроведения, СтрокаСписокТаблиц = "") Экспорт
	
	Если Движения.Найти("ВыплатаОтпусковЗаСчетРезерва") <> Неопределено Тогда
		
		ДанныеДляОтражения = ОтражениеЗарплатыВБухучете.ДанныеДляОтраженияЗарплатыВБухучете(ПериодРегистрации, Организация, СтрокаСписокТаблиц, ДанныеДляПроведения);
		
		ДокументСсылка = Движения.Найти("ВыплатаОтпусковЗаСчетРезерва").Отбор.Регистратор.Значение;  
		Движения.ВыплатаОтпусковЗаСчетРезерва.Очистить();
		Движения.ВыплатаОтпусковЗаСчетРезерва.Записать();
		
		МодульРезервОтпусков = ОбщегоНазначения.ОбщийМодуль("РезервОтпусков");
		НастройкиРезервовОтпусков = МодульРезервОтпусков.НастройкиРезервовОтпусков(Организация, ПериодРегистрации);
		
		Если НастройкиРезервовОтпусков.ФормироватьРезервОтпусковБУ Тогда
			
			КолонкиСуммирования = Неопределено;
			ОтражениеЗарплатыВБухучете.НоваяТаблицаБухучетНачисленнаяЗарплатаИВзносы(КолонкиСуммирования);
			НачисленныеОтпуска = ОтражениеЗарплатыВБухучете.НоваяТаблицаНачисленныеОтпуска();
			МодульРезервОтпусков.СписатьРасходыПоОплатеОтпускаЗаСчетОценочныхОбязательств(Организация, ПериодРегистрации, ДокументСсылка, ДанныеДляОтражения.НачисленнаяЗарплатаИВзносы, НачисленныеОтпуска, КолонкиСуммирования);
			МодульРезервОтпусков.СформироватьДвиженияВыплатаОтпусковЗаСчетРезерва(Движения, Отказ, Организация, ПериодРегистрации, НачисленныеОтпуска);
			ДвиженияВыплатаОтпусковЗаСчетРезерва = Движения.ВыплатаОтпусковЗаСчетРезерва.Выгрузить();
			МодульРезервОтпусков.СформироватьДвиженияСписаниеРезерваОтпусков (Движения, Отказ, Организация, ПериодРегистрации, ДвиженияВыплатаОтпусковЗаСчетРезерва);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура формирует дополнительные движения по регистрам финанового учета для документа НачислениеОценочныхОбязательствПоОтпускам.
//
// Параметры:
//	ЭтотОбъект - ДокументОбъект.НачислениеОценочныхОбязательствПоОтпускам - Документ к проведению.
//	Отказ - Булево - Признак отказа от проведения документа.
//	РежимПроведения - РежимПроведенияДокумента - Режим проведения документа.
//
Процедура ОбработкаПроведенияНачислениеОценочныхОбязательствПоОтпускам(ЭтотОбъект, Отказ, РежимПроведения) Экспорт
	
	// Инициализация дополнительных свойств для проведения документа
	ПроведениеСерверУТ.ИнициализироватьДополнительныеСвойстваДляПроведения(ЭтотОбъект.Ссылка, ЭтотОбъект.ДополнительныеСвойства, РежимПроведения);
	
	// Инициализация данных документа
	ИнициализироватьДанныеДокументаНачислениеОценочныхОбязательствПоОтпускам(ЭтотОбъект.Ссылка, ЭтотОбъект.ДополнительныеСвойства);
	
	// Движения по прочим расходам.
	ДоходыИРасходыСервер.ОтразитьПрочиеРасходы(ЭтотОбъект.ДополнительныеСвойства, ЭтотОбъект.Движения, Отказ);
	
	// Движения по партиям прочих расходов.
	ДоходыИРасходыСервер.ОтразитьПартииПрочихРасходов(ЭтотОбъект.ДополнительныеСвойства, ЭтотОбъект.Движения, Отказ);
	
	// Движения по оборотным регистрам управленческого учета
	УправленческийУчетПроведениеСервер.ОтразитьДвиженияДоходыРасходыПрочиеАктивыПассивы(ЭтотОбъект.ДополнительныеСвойства, ЭтотОбъект.Движения, Отказ);
	
	//Движения по отражению зарплаты в учете прочих пассивов
	ДоходыИРасходыСервер.ОтразитьПрочиеАктивыПассивы(ЭтотОбъект.ДополнительныеСвойства, ЭтотОбъект.Движения, Отказ);
	
	РеглУчетПроведениеСервер.ОтразитьПорядокОтраженияПрочихОпераций(ЭтотОбъект.ДополнительныеСвойства, Отказ);
	РеглУчетПроведениеСервер.ЗарегистрироватьКОтражению(ЭтотОбъект, ЭтотОбъект.ДополнительныеСвойства, ЭтотОбъект.Движения, Отказ);
	
	ПроведениеСерверУТ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	//++ НЕ УТКА
	МеждународныйУчетПроведениеСервер.ЗарегистрироватьКОтражению(ЭтотОбъект, ЭтотОбъект.ДополнительныеСвойства, ЭтотОбъект.Движения, Отказ);
	//-- НЕ УТКА
	
	ПроведениеСерверУТ.ОчиститьДополнительныеСвойстваДляПроведения(ЭтотОбъект.ДополнительныеСвойства);
	
КонецПроцедуры

#КонецОбласти

// Формирует документ начисления оценочных обязательств по отпускам за период по указанной организации. Вызывается
// при автоматическом выполнении операций закрытия месяца.
// 
// Параметры:
//   Организации - СправочникСсылка.Организации - Организация, по которой формируется документ
//   Период - Дата - Период, за который формируется документ
//
Процедура НачислениеООПриЗакрытииМесяца(Организации, Период) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	НачислениеОО.Ссылка КАК Ссылка,
	|	НачислениеОО.Организация КАК Организация,
	|	НачислениеОО.Проведен КАК Проведен
	|ИЗ
	|	Документ.НачислениеОценочныхОбязательствПоОтпускам КАК НачислениеОО
	|ГДЕ
	|	(НачислениеОО.Организация В (&Организации)
	|		ИЛИ &ПоВсемОрганизациям)
	|	И НЕ НачислениеОО.ПометкаУдаления
	|	И НАЧАЛОПЕРИОДА(НачислениеОО.ПериодРегистрации, МЕСЯЦ) = &НачалоМесяца
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация,
	|	Проведен УБЫВ");
	
	Запрос.УстановитьПараметр("Организации", Организации);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", НЕ ЗначениеЗаполнено(Организации));
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(Период));
	
	СуществующиеДокументы = Запрос.Выполнить().Выбрать();
	
	Для Каждого Организация Из Организации Цикл
		
		ДокументыПоОрганизацииОбработаны = Ложь;
		
		ДокументОбъект = Неопределено;
		
		СуществующиеДокументы.Сбросить();
		
		Пока СуществующиеДокументы.НайтиСледующий(Новый Структура("Организация, Проведен", Организация, Истина)) Цикл
			
			ДокументОбъект = СуществующиеДокументы.Ссылка.ПолучитьОбъект();
			
			Если ДокументОбъект.ПроверитьЗаполнение() Тогда
				ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			КонецЕсли;
			
			ДокументыПоОрганизацииОбработаны = Истина;
			
		КонецЦикла;
		
		Если ДокументыПоОрганизацииОбработаны Тогда
			Продолжить;
		КонецЕсли;
		
		НастройкиРезервовОтпусков = РезервОтпусков.НастройкиРезервовОтпусков(Организация, Период);
		
		Если Не НастройкиРезервовОтпусков.ФормироватьРезервОтпусковБУ Тогда
			Продолжить;
		КонецЕсли;
		
		СуществующиеДокументы.Сбросить();
		
		Если СуществующиеДокументы.НайтиСледующий(Новый Структура("Организация, Проведен", Организация, Ложь)) Тогда
			ДокументОбъект = СуществующиеДокументы.Ссылка.ПолучитьОбъект();
			ДокументОбъект.РасчетРезерваОтпусков.Очистить();
			ДокументОбъект.ОценочныеОбязательстваПоВознаграждениямРаботникам.Очистить();
			ДокументОбъект.ОценочныеОбязательства.Очистить();
			ДокументОбъект.ФизическиеЛица.Очистить();
		КонецЕсли;
		
		Если ДокументОбъект = Неопределено Тогда
			ДокументОбъект = Документы.НачислениеОценочныхОбязательствПоОтпускам.СоздатьДокумент();
		КонецЕсли;
		
		СтруктураЗаполнения = Новый Структура("Организация, ПериодРегистрации", Организация, Период);
		ДокументОбъект.Заполнить(СтруктураЗаполнения);
		ДокументОбъект.ПериодРегистрации = НачалоМесяца(Период);
		ДокументОбъект.Дата = КонецМесяца(Период);

		ОписаниеДокумента = Новый Структура("Ссылка,
		|Организация, Дата, ПериодРегистрации,
		|КорректировкаОбязательств, ДоляРасходов");
		ЗаполнитьЗначенияСвойств(ОписаниеДокумента, ДокументОбъект);
		
		ОписаниеДокумента.Вставить("РасчетРезерваОтпусков",                             ДокументОбъект.РасчетРезерваОтпусков.ВыгрузитьКолонки());
		ОписаниеДокумента.Вставить("ОценочныеОбязательства",                            ДокументОбъект.ОценочныеОбязательства.ВыгрузитьКолонки());
		ОписаниеДокумента.Вставить("ФизическиеЛица",                                    ДокументОбъект.ФизическиеЛица.ВыгрузитьКолонки());
		ОписаниеДокумента.Вставить("ОценочныеОбязательстваПоВознаграждениямРаботникам", ДокументОбъект.ОценочныеОбязательстваПоВознаграждениямРаботникам.ВыгрузитьКолонки());
		
		РезервОтпусков.ЗаполнитьДокументНачислениеОценочныхОбязательствПоОтпускам(ОписаниеДокумента);
		
		ДокументОбъект.РасчетРезерваОтпусков.Загрузить(ОписаниеДокумента.РасчетРезерваОтпусков);
		ДокументОбъект.ОценочныеОбязательства.Загрузить(ОписаниеДокумента.ОценочныеОбязательства);
		ДокументОбъект.ФизическиеЛица.Загрузить(ОписаниеДокумента.ФизическиеЛица);
		ДокументОбъект.ОценочныеОбязательстваПоВознаграждениямРаботникам.Загрузить(ОписаниеДокумента.ОценочныеОбязательстваПоВознаграждениямРаботникам);
		ДокументОбъект.ДоляРасходов = ОписаниеДокумента.ДоляРасходов;
		
		ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
		
		Если ДокументОбъект.ПроверитьЗаполнение() Тогда
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Процедура выполняет проверку заполнения расшифровки платежа в документах выдачи займа сотруднику.
// 
// Параметры:
//   ДокументОбъект - ДокументОбъект.РасходныйКассовыйОрдер,
//                    ДокументОбъект.ЗаявкаНаРасходованиеДенежныхСредств,
//                    ДокументОбъект.СписаниеБезналичныхДенежныхСредств - проверяемый документ
//   Отказ - Булево - признак отказа от проведения документа
//
Процедура ПроверитьЗаполнениеДокументаВыдачиЗайма(ДокументОбъект, Отказ) Экспорт
	
	Организация       = ДокументОбъект.Организация;
	Дата              = ДокументОбъект.Дата;
	ФизическоеЛицо    = ДокументОбъект.ПодотчетноеЛицо;
	Ссылка            = ДокументОбъект.Ссылка;
	
	ПараметрыПолученияСотрудниковОрганизаций = КадровыйУчет.ПараметрыПолученияРабочихМестВОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолученияСотрудниковОрганизаций.Организация 				= Организация;
	ПараметрыПолученияСотрудниковОрганизаций.НачалоПериода				= Дата;
	ПараметрыПолученияСотрудниковОрганизаций.ОкончаниеПериода			= Дата;
	ПараметрыПолученияСотрудниковОрганизаций.РаботникиПоДоговорамГПХ 	= Неопределено;
	
	КадровыйУчет.ПроверитьРаботающихФизическихЛиц(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо),
		ПараметрыПолученияСотрудниковОрганизаций,
		Отказ,
		Новый Структура("ИмяПоляСотрудник, ИмяОбъекта", "ФизическоеЛицо", "Объект"));
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Договора.ДоговорЗаймаСотруднику КАК ДоговорЗайма,
	|	Договора.Сумма
	|ПОМЕСТИТЬ ВТДоговора
	|ИЗ
	|	&Договора КАК Договора
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДоговора.ДоговорЗайма КАК ДоговорЗайма,
	|	СУММА(ВТДоговора.Сумма) КАК Сумма
	|ИЗ
	|	ВТДоговора КАК ВТДоговора
	|ГДЕ
	|	ВТДоговора.ДоговорЗайма <> ЗНАЧЕНИЕ(Документ.ДоговорЗаймаСотруднику.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТДоговора.ДоговорЗайма";
	Запрос.УстановитьПараметр("Договора", ДокументОбъект.РасшифровкаПлатежа.Выгрузить(,"ДоговорЗаймаСотруднику, Сумма"));
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДоговорЗайма = Выборка.ДоговорЗайма;
		Сумма = Выборка.Сумма;
		
		ДатаПредоставления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорЗайма, "ДатаПредоставления");
		ЗаймыСотрудникам.ПроверитьДатуВыдачиЗайма(Дата, ДатаПредоставления, "Объект.Дата", Отказ);
		
		НевыданныйОстаток = ЗаймыСотрудникам.ОстатокНевыданныхСумм(ДоговорЗайма, Дата, Ссылка);
		
		Если НевыданныйОстаток < Сумма Тогда 
			ТекстСообщения = НСтр("ru='На %1 по договору займа не выдано %2 руб. Сумма выдачи не может превышать невыданный остаток.'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Формат(Дата, "ДЛФ=Д"), НевыданныйОстаток);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Сумма", "Объект", Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Устанавливает условное оформление операций по зарплате.
//
// Параметры:
//    УсловноеОФормление - ЭлементУсловногоОформления - Коллекция элементов условного оформления формы;
//    ПутьКДанным        - Строка - Полное название объекта требующего условного оформления;
//    ЭлементФормы       - ЭлементФормы - элемент формы, в котором отображаются данные.
//
Процедура УсловноеОформлениеОперацийПоЗарплате(УсловноеОФормление, ПутьКДанным, ЭлементФормы) Экспорт
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементФормы.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКДанным);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательства;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Отпуск за счет обязательств'"));
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементФормы.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКДанным);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускРезервы;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Отпуск за счет резервов'"));
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементФормы.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКДанным);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ВидыОперацийПоЗарплате.ЕжегодныйОтпускОценочныеОбязательстваИРезервы;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Отпуск за счет обязательств и резервов'"));
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементФормы.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКДанным);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпуска;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Компенсация отпуска'"));
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементФормы.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКДанным);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ВидыОперацийПоЗарплате.КомпенсацияЕжегодногоОтпускаОценочныеОбязательства;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Компенсация отпуска за счет обязательств'"));
	
КонецПроцедуры

#Область ДополнительныеПроцедурыРасчетаРезервовОтпусков

// Добавляет в таблицу резервов отпусков колонку "Подразделение" и заполняет ее по
// кадровым данным сотрудников.
//
// Параметры:
//   ТаблицаРезервов   - ТаблицаЗначений - Таблица значений резервов отпусков;
//   ПериодРегистрации - Дата - Дата, на которую необходимо получить подразделение сотрудника.
//
Процедура ЗаполнитьПодразделениеПредприятиеПоМестамСотрудников(ТаблицаРезервов, ПериодРегистрации) Экспорт
	
	ТаблицаРезервов.Колонки.Добавить("ПодразделениеПредприятия",   Новый ОписаниеТипов("СправочникСсылка.СтруктураПредприятия"));
	
	// заполнение подразделений по кадровым данным сотрудников
	КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(
		Ложь,
		ТаблицаРезервов.ВыгрузитьКолонку("Сотрудник"),
		"МестоВСтруктуреПредприятия",
		КонецМесяца(ПериодРегистрации));
	
	ПодразделенияСотрудников = Новый Соответствие;
	Для Каждого Строка Из КадровыеДанные Цикл
		ПодразделенияСотрудников.Вставить(Строка.Сотрудник, Строка.МестоВСтруктуреПредприятия);
	КонецЦикла;
	
	Для Каждого Строка Из ТаблицаРезервов Цикл
		Строка.ПодразделениеПредприятия = ПодразделенияСотрудников[Строка.Сотрудник];
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

// Отключает учет по статьям финансирования, если используется.
// В ERP учет по статьям финансирования не предусмотрен.
Процедура НастроитьУчетПоСтатьямФинансирования() Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата") Тогда
		Константы.ИспользоватьСтатьиФинансированияЗарплата.Установить(Ложь);
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает заголовок "Способ отражения" элементу формы.
// Параметры:
//   Форма - Форма - Форма, элементу которой необходимо установить заголовок;
//   ИмяЭлемента - Строка - Имя элемента строкой, которому необходимо установить заголовок.
//
Процедура УстановитьЗаголовокСпособаОтражения(Форма, ИмяЭлемента) Экспорт
	
	ЭлементФормы = Форма.Элементы.Найти(ИмяЭлемента);
	
	Если ЭлементФормы <> Неопределено Тогда
		ЭлементФормы.Заголовок = НСтр("ru = 'Способ отражения'");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ФормированиеПроводок

// Возвращает текст запроса для формирования временной таблицы перед отражением в регламентированном учете
// документов Начисление оценочных обязательств по отпускам.
//	Возвращаемое значение:
//		Строка - текст запроса.
//
Функция ТекстЗапросаВТОтраженияВРеглУчетеОО() Экспорт
	
	Результат = 
		
		ТекстЗапросаСписокСпособов() + ";" +
		Справочники.СпособыОтраженияЗарплатыВБухУчете.ТекстЗапросаДанныхСпособов("ВТСпособыОтражения") + ";" +
		ТекстЗапросаТаблицаВТРасходыДокумента() + ";" +
		
		"ВЫБРАТЬ
		|	ВТРасходыДокумента.СтатьяРасходов,
		|	ВТРасходыДокумента.НаправлениеДеятельности,
		|	ВТРасходыДокумента.ВариантРаспределения,
		|	ВТРасходыДокумента.ПринятиеКналоговомуУчету,
		|	ВТРасходыДокумента.АналитикаРасходов,
		|	ВТРасходыДокумента.АналитикаАктивовПассивов,
		|	ВТРасходыДокумента.СчетУчета,
		|	ВТРасходыДокумента.Субконто1,
		|	ВТРасходыДокумента.Субконто2,
		|	ВТРасходыДокумента.Субконто3,
		|	ВТРасходыДокумента.СчетКт,
		|	СУММА(ВТРасходыДокумента.Сумма) КАК Сумма,
		|	СУММА(ВТРасходыДокумента.СуммаНУ) КАК СуммаНУ,
		|	МАКСИМУМ(ВТРасходыДокумента.НомерСтроки) КАК НомерСтроки,
		|	ВТРасходыДокумента.Подразделение,
		|	ВТРасходыДокумента.СтатьяАктивовПассивов,
		|	ВТРасходыДокумента.Ссылка
		|ПОМЕСТИТЬ РасходыДокумента
		|ИЗ
		|	ВТРасходыДокумента КАК ВТРасходыДокумента
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТРасходыДокумента.Ссылка,
		|	ВТРасходыДокумента.СчетКт,
		|	ВТРасходыДокумента.Подразделение,
		|	ВТРасходыДокумента.СтатьяАктивовПассивов,
		|	ВТРасходыДокумента.СтатьяРасходов,
		|	ВТРасходыДокумента.НаправлениеДеятельности,
		|	ВТРасходыДокумента.ВариантРаспределения,
		|	ВТРасходыДокумента.ПринятиеКналоговомуУчету,
		|	ВТРасходыДокумента.АналитикаРасходов,
		|	ВТРасходыДокумента.АналитикаАктивовПассивов,
		|	ВТРасходыДокумента.СчетУчета,
		|	ВТРасходыДокумента.Субконто1,
		|	ВТРасходыДокумента.Субконто2,
		|	ВТРасходыДокумента.Субконто3
		|
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|";
	
	Результат = СтрЗаменить(Результат, "&КоэффициентПересчетаВВалютуУпр", "1");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьДанныеДокументаНачислениеОценочныхОбязательствПоОтпускам(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстЗапросаДанныхСпособов = Справочники.СпособыОтраженияЗарплатыВБухУчете.ТекстЗапросаДанныхСпособов("ВТСпособыОтражения");
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстЗапросаТаблицаДокументовКОтражению(ТекстыЗапроса);
	ТекстЗапросаСписокСпособов(ТекстыЗапроса);
	ТекстыЗапроса.Добавить(ТекстЗапросаДанныхСпособов, "ДанныеСпособов");
	ТекстЗапросаТаблицаВТРасходыДокумента(ТекстыЗапроса);
	ТекстЗапросаПрочиеРасходы(ТекстыЗапроса, Регистры);
	ТекстЗапросаПартииПрочихРасходов(ТекстыЗапроса, Регистры);
	ТекстЗапросаПрочиеАктивыПассивы(ТекстыЗапроса, Регистры);
	ТекстЗапросаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПорядокОтраженияПрочихОпераций(ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры);

	ПроведениеСерверУТ.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеШапки.ПериодРегистрации КАК Период,
	|	ДанныеШапки.Организация КАК Организация,
	|	ДанныеШапки.КорректировкаОбязательств КАК КорректировкаОбязательств,
	|	ДанныеШапки.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.НачислениеОценочныхОбязательствПоОтпускам КАК ДанныеШапки
	|ГДЕ
	|	ДанныеШапки.Ссылка = &Ссылка";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	ВалютаРегл = Константы.ВалютаРегламентированногоУчета.Получить();
	Коэффициенты = РаботаСКурсамивалютУТ.ПолучитьКоэффициентыПересчетаВалюты(
		ВалютаРегл,
		ВалютаРегл,
		Реквизиты.Период);
	
	Запрос.УстановитьПараметр("Период",                         КонецМесяца(Реквизиты.Период));
	Запрос.УстановитьПараметр("КорректировкаОбязательств",      Реквизиты.КорректировкаОбязательств);
	Запрос.УстановитьПараметр("Организация",                    Реквизиты.Организация);
	Запрос.УстановитьПараметр("ИспользоватьНачислениеЗарплаты", ПолучитьФункциональнуюОпцию("ИспользоватьНачислениеЗарплатыУТ"));
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуУпр", Коэффициенты.КоэффициентПересчетаВВалютуУПР);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаДокументовКОтражению(ТекстыЗапроса)
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Документ.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ДокументыКОтражению
		|ИЗ
		|	Документ.НачислениеОценочныхОбязательствПоОтпускам КАК Документ
		|ГДЕ
		|	Документ.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ДокументыКОтражению");
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаСписокСпособов(ТекстыЗапроса = Неопределено)
	
	ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОценочныеОбязательства.СпособОтраженияЗарплатыВБухучете КАК СпособОтражения
		|ПОМЕСТИТЬ ВтСписокСпособовОтражения
		|ИЗ
		|	Документ.НачислениеОценочныхОбязательствПоОтпускам.ОценочныеОбязательства КАК ОценочныеОбязательства
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтражению КАК ДокументыКОтражению
		|		ПО ОценочныеОбязательства.Ссылка = ДокументыКОтражению.Ссылка";
	
	Если Не ТекстыЗапроса = Неопределено Тогда
		ТекстыЗапроса.Добавить(ТекстЗапроса, "ВтСписокСпособовОтражения");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВТРасходыДокумента(ТекстыЗапроса = Неопределено)
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	СтатьиРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	СтатьиРасходов.СтатьяРасходов КАК СтатьяРасходов,
		|	СтатьиРасходов.ВариантРаспределения КАК ВариантРаспределения,
		|	СтатьиРасходов.ПринятиеКналоговомуУчету КАК ПринятиеКналоговомуУчету,
		|
		|	ВЫБОР
		|		КОГДА СтатьиРасходов.АналитикаРасходов = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
		|			ТОГДА ОценочныеОбязательства.МестоВСтруктуреПредприятия
		|		КОГДА СтатьиРасходов.АналитикаРасходов = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|			ТОГДА ОценочныеОбязательства.Ссылка.Организация
		|		ИНАЧЕ СтатьиРасходов.АналитикаРасходов
		|	КОНЕЦ КАК АналитикаРасходов,
		|
		|	ВЫБОР
		|		КОГДА СтатьиРасходов.АналитикаАктивовПассивов = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
		|			ТОГДА ОценочныеОбязательства.МестоВСтруктуреПредприятия
		|		КОГДА СтатьиРасходов.АналитикаАктивовПассивов = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|			ТОГДА ОценочныеОбязательства.Ссылка.Организация
		|		ИНАЧЕ СтатьиРасходов.АналитикаАктивовПассивов
		|	КОНЕЦ КАК АналитикаАктивовПассивов,
		|
		|	СтатьиРасходов.СчетУчета КАК СчетУчета,
		|	СтатьиРасходов.Субконто1 КАК Субконто1,
		|	СтатьиРасходов.Субконто2 КАК Субконто2,
		|	СтатьиРасходов.Субконто3 КАК Субконто3,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОценочныеОбязательстваПоВознаграждениям) КАК СчетКт,
		|	ВЫРАЗИТЬ(ОценочныеОбязательства.СуммаРезерва * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2)) КАК Сумма,
		|	ВЫРАЗИТЬ(ОценочныеОбязательства.СуммаРезерваНУ * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2)) КАК СуммаНУ,
		|	ОценочныеОбязательства.СуммаРезерва КАК СуммаРегл,
		|	ОценочныеОбязательства.СуммаРезерваНУ КАК СуммаРеглНУ,
		|	ОценочныеОбязательства.НомерСтроки КАК НомерСтроки,
		|	ОценочныеОбязательства.МестоВСтруктуреПредприятия КАК Подразделение,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеРезервовПоВознаграждениям) КАК ХозяйственнаяОперация,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ОплатаТруда) КАК СтатьяАктивовПассивов,
		|	ОценочныеОбязательства.Ссылка 
		|ПОМЕСТИТЬ ВТРасходыДокумента
		|ИЗ
		|	Документ.НачислениеОценочныхОбязательствПоОтпускам.ОценочныеОбязательства КАК ОценочныеОбязательства
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтражению КАК ДокументыКОтражению
		|		ПО ОценочныеОбязательства.Ссылка = ДокументыКОтражению.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСпособыОтражения КАК СтатьиРасходов
		|		ПО ОценочныеОбязательства.СпособОтраженияЗарплатыВБухучете = СтатьиРасходов.СпособОтражения
		|			И ОценочныеОбязательства.ОблагаетсяЕНВД = СтатьиРасходов.ОблагаетсяЕНВД
		|ГДЕ
		|	НЕ ДокументыКОтражению.Ссылка.КорректировкаОбязательств
		|	И ОценочныеОбязательства.СуммаРезерва + ОценочныеОбязательства.СуммаРезерваНУ <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СтатьиРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	СтатьиРасходов.СтатьяРасходовВзносов,
		|	СтатьиРасходов.ВариантРаспределенияВзносов,
		|	СтатьиРасходов.ПринятиеКналоговомуУчетуВзносов,
		|
		|	ВЫБОР
		|		КОГДА СтатьиРасходов.АналитикаРасходовВзносов = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
		|			ТОГДА ОценочныеОбязательства.МестоВСтруктуреПредприятия
		|		КОГДА СтатьиРасходов.АналитикаРасходовВзносов = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|			ТОГДА ОценочныеОбязательства.Ссылка.Организация
		|		ИНАЧЕ СтатьиРасходов.АналитикаРасходовВзносов
		|	КОНЕЦ,
		|
		|	ВЫБОР
		|		КОГДА СтатьиРасходов.АналитикаАктивовПассивовВзносов = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
		|			ТОГДА ОценочныеОбязательства.МестоВСтруктуреПредприятия
		|		КОГДА СтатьиРасходов.АналитикаАктивовПассивовВзносов = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|			ТОГДА ОценочныеОбязательства.Ссылка.Организация
		|		ИНАЧЕ СтатьиРасходов.АналитикаАктивовПассивовВзносов
		|	КОНЕЦ,
		|
		|	СтатьиРасходов.СчетУчетаВзносов,
		|	СтатьиРасходов.Субконто1Взносов,
		|	СтатьиРасходов.Субконто2Взносов,
		|	СтатьиРасходов.Субконто3Взносов,
		|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОценочныеОбязательстваПостраховымВзносам),
		|	ВЫРАЗИТЬ((ОценочныеОбязательства.СуммаРезерваСтраховыхВзносов + ОценочныеОбязательства.СуммаРезерваФССНесчастныеСлучаи) * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2)),
		|	ВЫРАЗИТЬ((ОценочныеОбязательства.СуммаРезерваСтраховыхВзносовНУ + ОценочныеОбязательства.СуммаРезерваФССНесчастныеСлучаиНУ) * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2)),
		|	ОценочныеОбязательства.СуммаРезерваСтраховыхВзносов + ОценочныеОбязательства.СуммаРезерваФССНесчастныеСлучаи,
		|	ОценочныеОбязательства.СуммаРезерваСтраховыхВзносовНУ + ОценочныеОбязательства.СуммаРезерваФССНесчастныеСлучаиНУ,
		|	ОценочныеОбязательства.НомерСтроки,
		|	ОценочныеОбязательства.МестоВСтруктуреПредприятия,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеРезервовПоСтраховымВзносам),
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги),
		|	ОценочныеОбязательства.Ссылка 
		|ИЗ
		|	Документ.НачислениеОценочныхОбязательствПоОтпускам.ОценочныеОбязательства КАК ОценочныеОбязательства
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыКОтражению КАК ДокументыКОтражению
		|		ПО ОценочныеОбязательства.Ссылка = ДокументыКОтражению.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСпособыОтражения КАК СтатьиРасходов
		|		ПО ОценочныеОбязательства.СпособОтраженияЗарплатыВБухучете = СтатьиРасходов.СпособОтражения
		|			И ОценочныеОбязательства.ОблагаетсяЕНВД = СтатьиРасходов.ОблагаетсяЕНВД
		|ГДЕ
		|	НЕ ДокументыКОтражению.Ссылка.КорректировкаОбязательств
		|	И ОценочныеОбязательства.СуммаРезерваСтраховыхВзносов + ОценочныеОбязательства.СуммаРезерваФССНесчастныеСлучаи + 
		|		ОценочныеОбязательства.СуммаРезерваСтраховыхВзносовНУ + ОценочныеОбязательства.СуммаРезерваФССНесчастныеСлучаиНУ <> 0";
	
	Если Не ТекстыЗапроса = Неопределено Тогда	
		ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТРасходыДокумента");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПрочиеРасходы(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВТДанныеРасходы.НомерСтроки            КАК НомерСтроки,
	|	&Период                                КАК Период,
	|	&Организация                           КАК Организация,
	|	ВТДанныеРасходы.Подразделение          КАК Подразделение,
	|	ВТДанныеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВТДанныеРасходы.СтатьяРасходов         КАК СтатьяРасходов,
	|	ВТДанныеРасходы.АналитикаРасходов      КАК АналитикаРасходов,
	
	|	СУММА(ВТДанныеРасходы.Сумма) КАК Сумма,
	
	|	ВЫБОР КОГДА ВТДанныеРасходы.ВариантРаспределения = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты) ТОГДА
	|		СУММА(ВТДанныеРасходы.Сумма) 
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаБезНДС,

	|	СУММА(ВЫБОР КОГДА ВТДанныеРасходы.ВариантРаспределения = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы) ТОГДА
	|		0
	|	ИНАЧЕ
	|		ВТДанныеРасходы.СуммаРегл
	|	КОНЕЦ) КАК СуммаРегл,

	|	СУММА(ВЫБОР КОГДА ВтДанныеРасходы.ВариантРаспределения =
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|	ТОГДА
	|		0
	|	КОГДА НЕ ВТДанныеРасходы.ПринятиеКНалоговомуУчету ТОГДА
	|		0
	|	ИНАЧЕ
	|		ВТДанныеРасходы.СуммаРегл - ВТДанныеРасходы.СуммаРеглНУ
	|	КОНЕЦ)                                 КАК ВременнаяРазница,

	|	СУММА(ВЫБОР КОГДА ВтДанныеРасходы.ВариантРаспределения =
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|	ТОГДА
	|		0
	|	КОГДА НЕ ВТДанныеРасходы.ПринятиеКНалоговомуУчету ТОГДА
	|		ВТДанныеРасходы.СуммаРегл
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ)                                 КАК ПостояннаяРазница

	|ИЗ
	|	ВТРасходыДокумента КАК ВТДанныеРасходы
	|
	|ГДЕ
	|	ВТДанныеРасходы.ВариантРаспределения <> ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|	И ВТДанныеРасходы.СтатьяРасходов Ссылка ПланВидовХарактеристик.СтатьиРасходов
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТДанныеРасходы.ВариантРаспределения,
	|	ВТДанныеРасходы.НомерСтроки,
	|	ВТДанныеРасходы.СтатьяРасходов,
	|	ВТДанныеРасходы.НаправлениеДеятельности,
	|	ВТДанныеРасходы.АналитикаРасходов,
	|	ВТДанныеРасходы.Подразделение
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПартииПрочихРасходов(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПартииПрочихРасходов";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
		
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)      КАК ВидДвижения,
	|	&Период                                     КАК Период,
	|	&Организация                                КАК Организация,
	|	МАКСИМУМ(ВТДанныеРасходы.НомерСтроки)       КАК НомерСтроки,
	|	ВТДанныеРасходы.Подразделение               КАК Подразделение,
	|	ВТДанныеРасходы.СтатьяРасходов              КАК СтатьяРасходов,
	|	ВТДанныеРасходы.АналитикаРасходов           КАК АналитикаРасходов,
	|	ВТДанныеРасходы.Ссылка                      КАК ДокументПоступленияРасходов,
	|	НЕОПРЕДЕЛЕНО                                КАК АналитикаУчетаПартий,
	|	СУММА(ВТДанныеРасходы.Сумма)                КАК Стоимость,
	|	СУММА(ВТДанныеРасходы.Сумма)                КАК СтоимостьБезНДС,
	|	СУММА(ВТДанныеРасходы.СуммаРегл)            КАК СтоимостьРегл,
	|
	|	СУММА(ВЫБОР КОГДА НЕ ВТДанныеРасходы.ПринятиеКналоговомуУчету ТОГДА
	|		ВТДанныеРасходы.СуммаРегл
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ)                                      КАК ПостояннаяРазница,
	
	|	СУММА(ВЫБОР КОГДА НЕ ВТДанныеРасходы.ПринятиеКналоговомуУчету ТОГДА
	|		0
	|	ИНАЧЕ
	|		ВТДанныеРасходы.СуммаРегл - ВТДанныеРасходы.СуммаРеглНУ
	|	КОНЕЦ)                                      КАК ВременнаяРазница,

	|	0 КАК НДСРегл
	
	|ИЗ
	|	ВТРасходыДокумента КАК ВТДанныеРасходы
	|
	|ГДЕ
	|	ВТДанныеРасходы.ВариантРаспределения = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|	И ВТДанныеРасходы.СтатьяРасходов Ссылка ПланВидовХарактеристик.СтатьиРасходов
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТДанныеРасходы.Подразделение,
	|	ВТДанныеРасходы.СтатьяРасходов,
	|	ВТДанныеРасходы.АналитикаРасходов,
	|	ВТДанныеРасходы.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПрочиеАктивыПассивы(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ПрочиеАктивыПассивы";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
		
	ТекстЗапроса = 
	// расходное движение по предопределенным статьям Оплата труда и Взносы с оплаты труда
	"ВЫБРАТЬ
	|	&Период                                  КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)   КАК ВидДвижения,
	|	МАКСИМУМ(ВТДанныеРасходы.НомерСтроки)    КАК НомерСтроки,
	|	&Организация                             КАК Организация,
	|	ВТДанныеРасходы.Подразделение            КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО                             КАК НаправлениеДеятельности,
	|	ВТДанныеРасходы.СтатьяАктивовПассивов    КАК Статья,
	|	ВЫБОР КОГДА ВТДанныеРасходы.СтатьяАктивовПассивов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги) ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка)
	|	ИНАЧЕ
	|		НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Аналитика,
	|	СУММА(ВТДанныеРасходы.Сумма)             КАК Сумма
	|ИЗ
	|	ВТРасходыДокумента КАК ВТДанныеРасходы
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТДанныеРасходы.Подразделение,
	|	ВТДанныеРасходы.СтатьяАктивовПассивов,
	|	ВТДанныеРасходы.НаправлениеДеятельности
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// приходное движение по статьям пассивов, указанных в способах отражения
	|ВЫБРАТЬ
	|	&Период                                  КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)   КАК ВидДвижения,
	|	МАКСИМУМ(ВТДанныеРасходы.НомерСтроки)    КАК НомерСтроки,
	|	&Организация                             КАК Организация,
	|	ВТДанныеРасходы.Подразделение            КАК Подразделение,
	|	ВТДанныеРасходы.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	ВТДанныеРасходы.СтатьяРасходов           КАК Статья,
	|	ВТДанныеРасходы.АналитикаАктивовПассивов КАК Аналитика,
	|	СУММА(ВТДанныеРасходы.Сумма)             КАК Сумма
	|ИЗ
	|	ВТРасходыДокумента КАК ВТДанныеРасходы
	|
	|ГДЕ
	|	ВТДанныеРасходы.СтатьяРасходов Ссылка ПланВидовХарактеристик.СтатьиАктивовПассивов
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТДанныеРасходы.Подразделение,
	|	ВТДанныеРасходы.СтатьяРасходов,
	|	ВТДанныеРасходы.НаправлениеДеятельности,
	|	ВТДанныеРасходы.АналитикаАктивовПассивов
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДвиженияДоходыРасходыПрочиеАктивыПассивы(ТекстыЗапроса, Регистры)

	ИмяРегистра = "ДвиженияДоходыРасходыПрочиеАктивыПассивы";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	// расходы - пассивы
	"ВЫБРАТЬ
	|	&Период                                  КАК Период,
	|	МАКСИМУМ(ВТДанныеРасходы.НомерСтроки)    КАК НомерСтроки,
	|	ВТДанныеРасходы.ХозяйственнаяОперация    КАК ХозяйственнаяОперация,
	|
	|	&Организация                             КАК Организация,
	
	|	ВТДанныеРасходы.Подразделение            КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО                             КАК НаправлениеДеятельности,
	|	ВТДанныеРасходы.СтатьяАктивовПассивов    КАК Статья,
	|	НЕОПРЕДЕЛЕНО                             КАК АналитикаРасходов,
	|	ВЫБОР КОГДА ВТДанныеРасходы.СтатьяАктивовПассивов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги) ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка)
	|	ИНАЧЕ
	|		НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК АналитикаАктивовПассивов,
	
	|	ВТДанныеРасходы.Подразделение            КАК КорПодразделение,
	|	ВТДанныеРасходы.НаправлениеДеятельности  КАК КорНаправлениеДеятельности,
	|	ВТДанныеРасходы.СтатьяРасходов           КАК КорСтатья,
	|	ВТДанныеРасходы.АналитикаРасходов        КАК КорАналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                             КАК КорАналитикаАктивовПассивов,
	|
	|	СУММА(ВТДанныеРасходы.Сумма)             КАК Сумма,
	|	СУММА(ВТДанныеРасходы.СуммаРегл)         КАК СуммаРегл
	|
	|ИЗ
	|	ВТРасходыДокумента КАК ВТДанныеРасходы
	|
	|ГДЕ
	|	ВТДанныеРасходы.Сумма <> 0
	|	И ВТДанныеРасходы.СтатьяРасходов Ссылка ПланВидовХарактеристик.СтатьиРасходов
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТДанныеРасходы.СтатьяРасходов,
	|	ВТДанныеРасходы.АналитикаРасходов,
	|	ВТДанныеРасходы.СтатьяАктивовПассивов,
	|	ВТДанныеРасходы.ХозяйственнаяОперация,
	|	ВТДанныеРасходы.Подразделение,
	|	ВТДанныеРасходы.НаправлениеДеятельности
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// пассивы - пассивы
	|ВЫБРАТЬ
	|	&Период                                  КАК Период,
	|	МАКСИМУМ(ВТДанныеРасходы.НомерСтроки)    КАК НомерСтроки,
	|
	|	ВТДанныеРасходы.ХозяйственнаяОперация    КАК ХозяйственнаяОперация,
	|
	|	&Организация                             КАК Организация,
	
	|	ВТДанныеРасходы.Подразделение            КАК Подразделение,
	|	ВТДанныеРасходы.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	ВТДанныеРасходы.СтатьяАктивовПассивов    КАК Статья,
	|	НЕОПРЕДЕЛЕНО                             КАК АналитикаРасходов,
	|	ВЫБОР КОГДА ВТДанныеРасходы.СтатьяАктивовПассивов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.Налоги) ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНалогов.ПустаяСсылка)
	|	ИНАЧЕ
	|		НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК АналитикаАктивовПассивов,
	|
	|	ВТДанныеРасходы.Подразделение            КАК КорПодразделение,
	|	ВТДанныеРасходы.НаправлениеДеятельности  КАК КорНаправлениеДеятельности,
	|	ВТДанныеРасходы.СтатьяРасходов           КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                             КАК КорАналитикаРасходов,
	|	ВТДанныеРасходы.АналитикаАктивовПассивов КАК КорАналитикаАктивовПассивов,
	
	|	СУММА(ВТДанныеРасходы.Сумма)             КАК Сумма,
	|	СУММА(ВТДанныеРасходы.СуммаРегл)         КАК СуммаРегл
	|
	|ИЗ
	|	ВТРасходыДокумента КАК ВТДанныеРасходы
	|
	|ГДЕ
	|	ВТДанныеРасходы.Сумма > 0
	|	И ВТДанныеРасходы.СтатьяРасходов Ссылка ПланВидовХарактеристик.СтатьиАктивовПассивов
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТДанныеРасходы.СтатьяРасходов,
	|	ВТДанныеРасходы.АналитикаАктивовПассивов,
	|	ВТДанныеРасходы.СтатьяАктивовПассивов,
	|	ВТДанныеРасходы.ХозяйственнаяОперация,
	|	ВТДанныеРасходы.Подразделение,
	|	ВТДанныеРасходы.НаправлениеДеятельности
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПорядокОтраженияПрочихОпераций(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПорядокОтраженияПрочихОпераций";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	&Период	КАК Дата,
	|	&Организация КАК Организация,
	|	&Ссылка КАК Документ,
	|	"""" КАК ИдентификаторСтроки
	|ИЗ
	|	Документ.НачислениеОценочныхОбязательствПоОтпускам.ОценочныеОбязательства КАК Строки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияЗарплатыВБухУчете КАК СпособыОтражения
	|		ПО СпособыОтражения.Ссылка = Строки.СпособОтраженияЗарплатыВБухучете
	|ГДЕ
	|	Строки.Ссылка = &Ссылка
	|	И СпособыОтражения.ЕстьПрочиеОперации
	|	И НЕ &КорректировкаОбязательств";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период           КАК Период,
	|	&Организация      КАК Организация,
	|	&Период           КАК ДатаОтражения";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

#Область ОбновленияИнформационнойБазы

#Область КА_2_0_3

#КонецОбласти

#КонецОбласти

#КонецОбласти