////////////////////////////////////////////////////////////////////////////////
// Проверка контрагентов
// переопределяемые клиент - серверные процедуры УТ
//////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Процедура - Отображение результата проверки контрагента в справочнике.
//
// Параметры:
//  Форма                          - УправляемаяФорма - Форма справочника, в котором выполнялась проверка контрагента.
//      Результат проверки хранится в реквизите РеквизитыПроверкиКонтрагентов(Структура) формы контрагента.
//      Структуру полей РеквизитыПроверкиКонтрагентов см. в процедуре ИнициализироватьРеквизитыФормыКонтрагент ОМ
//      ПроверкаКонтрагентов.
//  ПредставлениеРезультатаПроверки - ФорматированнаяСтрока или пустая Строка - представление результата проверки
//                                    контрагента.
Процедура ОтобразитьРезультатПроверкиКонтрагентаВСправочнике(Форма, ПредставлениеРезультатаПроверки) Экспорт
	
	ПредставлениеРезультатаПроверки = ПредставлениеРезультатаПроверкиКонтрагента(Форма.РеквизитыПроверкиКонтрагентов.СостояниеКонтрагента,
	                                                                             ДополнительныеПараметрыКонтрагентаПоФорме(Форма));
	УстановитьСтраницуКартинкиРезультатаПроверки(Форма);
	Форма.РезультатПроверкиКонтрагента = ПредставлениеРезультатаПроверки;
	
КонецПроцедуры

// Устанавливает текст посказки и управляет элементами формы
//
// Параметры:
//  ПараметрыПрорисовки - Структура - содержит данные необходимые для отображения результата проверки в элементе формы.
//  СостояниеПроверки   - ПеречислениеСсылка.СостояниеПроверкиКонтрагентов - состояние проверки контрагентов
// 
Процедура УстановитьТекстПодсказкиВДокументе(ПараметрыПрорисовки, СостояниеПроверки) Экспорт
	
	// Определение цвета и текста
	ПодсказкаВДокументе = ПроверкаКонтрагентовКлиентСервер.ПодсказкаВДокументе(ПараметрыПрорисовки, СостояниеПроверки);
	ПараметрыЭлементаПрорисовки = ПараметрыПрорисовки.Элемент;
	
	Если ПараметрыЭлементаПрорисовки.ВидОтображения = "УправлениеСтраницами" Тогда
		ПараметрыЭлементаПрорисовки.ГруппаСтраниц.ТекущаяСтраница = ?(ПодсказкаВДокументе.ЕстьОшибка,
		                                                              ПараметрыЭлементаПрорисовки.СтраницаЕстьПроблема,
		                                                              ПараметрыЭлементаПрорисовки.СтраницаНетПроблемы)
	ИначеЕсли ПараметрыЭлементаПрорисовки.ВидОтображения = "Видимость" Тогда
		ПараметрыЭлементаПрорисовки.ЭлементУправлениеВидимостью.Видимость = ПодсказкаВДокументе.ЕстьОшибка;
	КонецЕсли;
	
	Если ПараметрыЭлементаПрорисовки.ЭлементФормыПодсказка <> Неопределено Тогда
		
		ПараметрыЭлементаПрорисовки.ЭлементФормыПодсказка.Подсказка = СтрЗаменить(ПодсказкаВДокументе.Текст, НСтр("ru = 'Подробнее о проверке'"), "");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПредставлениеРезультатаПроверкиКонтрагента(СостояниеКонтрагента, ДополнительныеПараметры, Знач ИсходныйТекст = "")
	
	Результат = ИсходныйТекст;
	
	ЦветТекста = ЦветНадписиРезультатаПроверкиПоСостояниюКонтрагента(СостояниеКонтрагента);
	
	// Формируем строку
	МассивПодстрок = Новый Массив;
	Если ЗначениеЗаполнено(ИсходныйТекст) Тогда
		МассивПодстрок.Добавить(Новый ФорматированнаяСтрока(ИсходныйТекст));
		МассивПодстрок.Добавить("   ");
	КонецЕсли;
	
	ТекстСостояниеКонтрагента = СтрокаСостояниеКонтрагента(СостояниеКонтрагента, ДополнительныеПараметры);
	
	МассивПодстрок.Добавить(Новый ФорматированнаяСтрока(Строка(ТекстСостояниеКонтрагента),,ЦветТекста,, 
	ПроверкаКонтрагентовКлиентСерверПовтИсп.ПутьКИнструкции()));
	
	Результат = Новый ФорматированнаяСтрока(МассивПодстрок);
	
	Возврат Результат;
	
КонецФункции

Функция ЦветНадписиРезультатаПроверкиПоСостояниюКонтрагента(СостояниеКонтрагента)
	
	Цвета = ПроверкаКонтрагентовВызовСервераПовтИсп.ЦветаРезультатовПроверки();
	
	Если СостояниеКонтрагента = ПредопределенноеЗначение("Перечисление.СостоянияСуществованияКонтрагента.НеДействуетИлиИзмененКПП") Тогда
		ЦветТекста = Цвета.ЦветТекстаКонтрагентаПрекратившегоДеятельность;
	ИначеЕсли СостояниеКонтрагента = ПредопределенноеЗначение("Перечисление.СостоянияСуществованияКонтрагента.КонтрагентЕстьВБазеФНС") 
		ИЛИ СостояниеКонтрагента = ПредопределенноеЗначение("Перечисление.СостоянияСуществованияКонтрагента.КонтрагентНеПодлежитПроверке") Тогда
		ЦветТекста = Цвета.ЦветТекстаКонтрагентаДействующего;
	Иначе
		ЦветТекста = Цвета.ЦветТекстаНекорректногоКонтрагента;
	КонецЕсли;
	
	Возврат ЦветТекста;
	
КонецФункции

Функция СтрокаСостояниеКонтрагента(СостояниеКонтрагента, ДополнительныеПараметры)
	
	ТекстСостояние = Строка(СостояниеКонтрагента);
	
	// 4D:ERP для Беларуси, Дмитрий, 21.04.2017 17:40:34 
	// Проверка контрагентов, №14822 
	// {
	Если СостояниеКонтрагента = ПредопределенноеЗначение("Перечисление.СостоянияСуществованияКонтрагента.ПустойИННИлиКПП") Тогда
		
		ТекстСостояние = НСтр("ru = 'Проверка контрагента по данным МНC не осуществлялась.'");
		
		Если ДополнительныеПараметры.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицо") Тогда
			
			ТекстПроблемы = "";
			
			Если ПустаяСтрока(ДополнительныеПараметры.ИНН) Тогда
				ТекстПроблемы = НСтр("ru = 'Не заполнен УНП.'");
			КонецЕсли;
			
		ИначеЕсли ДополнительныеПараметры.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ИндивидуальныйПредприниматель") Тогда
			
			Если ПустаяСтрока(ДополнительныеПараметры.ИНН) Тогда
				ТекстПроблемы = НСтр("ru = 'Не заполнен УНП.'");
			КонецЕсли;
			
		КонецЕсли;
		
		ТекстСостояние = ТекстСостояние + ?(ПустаяСтрока(ТекстПроблемы),"", " " + ТекстПроблемы);
		
	ИначеЕсли  СостояниеКонтрагента = ПредопределенноеЗначение("Перечисление.СостоянияСуществованияКонтрагента.КонтрагентНеПодлежитПроверке") Тогда
		
		ТекстСостояние = НСтр("ru = 'Не подлежит проверке по данным МНС'")
		
	ИначеЕсли СостояниеКонтрагента = ПредопределенноеЗначение("Перечисление.СостоянияСуществованияКонтрагента.КонтрагентСодержитОшибкиВДанных") Тогда
		
		ТекстПроблемы = ".";
		
		Если ДополнительныеПараметры.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицо") Тогда
			
			ТекстПроблемы = " " + НСтр("ru = 'УНП.'");
			
		ИначеЕсли ДополнительныеПараметры.ЮрФизЛицо = ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ИндивидуальныйПредприниматель") Тогда
			
			ТекстПроблемы = " " + НСтр("ru = 'УНП.'");
			
		КонецЕсли;
		
		ТекстСостояние = НСтр("ru = 'Проверка по данным МНС закончилась неудачей'") + ". " + ТекстСостояние + ?(ПустаяСтрока(ТекстПроблемы),"", " " + ТекстПроблемы);
		
	КонецЕсли;
	// }
	// 4D
	
	Возврат ТекстСостояние;

КонецФункции

Функция ДополнительныеПараметрыКонтрагентаПоФорме(Форма)
	
	ДополнительныеПараметры = Новый Структура("ИНН, КПП, ЮрФизЛицо");
	
	Если Форма.ИмяФормы = "Справочник.Контрагенты.Форма.ФормаЭлемента" Тогда
		ЗаполнитьЗначенияСвойств(ДополнительныеПараметры, Форма.Объект);
	ИначеЕсли Форма.ИмяФормы = "Справочник.Партнеры.Форма.ФормаЭлементаРеквизитыКонтрагента" Тогда
		ЗаполнитьЗначенияСвойств(ДополнительныеПараметры, Форма);
	КонецЕсли;
	
	Возврат ДополнительныеПараметры;
	
КонецФункции

Функция УстановитьСтраницуКартинкиРезультатаПроверки(Форма)

	СостояниеКонтрагента = Форма.РеквизитыПроверкиКонтрагентов.СостояниеКонтрагента;
	
	Если СостояниеКонтрагента = ПредопределенноеЗначение("Перечисление.СостоянияСуществованияКонтрагента.НеДействуетИлиИзмененКПП") Тогда
		Форма.Элементы.СтраницыКартинкиРезультатовПроверкиКонтрагента.ТекущаяСтраница = Форма.Элементы.СтраницаНеДействующийКонтрагент;
	ИначеЕсли СостояниеКонтрагента = ПредопределенноеЗначение("Перечисление.СостоянияСуществованияКонтрагента.ПустойИННИлиКПП") Тогда
		Форма.Элементы.СтраницыКартинкиРезультатовПроверкиКонтрагента.ТекущаяСтраница = Форма.Элементы.СтраницаКонтрагентНеДостаточноДанных;
	ИначеЕсли СостояниеКонтрагента = ПредопределенноеЗначение("Перечисление.СостоянияСуществованияКонтрагента.КонтрагентЕстьВБазеФНС") 
		Или СостояниеКонтрагента = ПредопределенноеЗначение("Перечисление.СостоянияСуществованияКонтрагента.КонтрагентНеПодлежитПроверке")
		Или Не ЗначениеЗаполнено(СостояниеКонтрагента) Тогда
		Форма.Элементы.СтраницыКартинкиРезультатовПроверкиКонтрагента.ТекущаяСтраница = Форма.Элементы.СтраницаКорректныйКонтрагент;
	Иначе
		Форма.Элементы.СтраницыКартинкиРезультатовПроверкиКонтрагента.ТекущаяСтраница = Форма.Элементы.СтраницаНеКорректныйКонтрагент;
	КонецЕсли;
	
	Форма.Элементы.СтраницыКартинкиРезультатовПроверкиКонтрагента.Видимость = 
	          НЕ (Форма.Элементы.СтраницыКартинкиРезультатовПроверкиКонтрагента.ТекущаяСтраница = Форма.Элементы.СтраницаКорректныйКонтрагент);
КонецФункции 

#КонецОбласти



