
#Область СлужебныйПрограммныйИнтерфейс

#Область ДатыИзмененийЗаконодательства

// Возвращает дату вступления в силу Федерального закона от 3 июля 2016 года № 243-ФЗ.
//
// Параметры:
//  нет
//
// Возвращаемое значение:
//   дата
//
Функция ДатаПередачиАдминистрированияВзносовФНС() Экспорт 

	Возврат '20170101'

КонецФункции

// Возвращает дату вступления в силу нормативного акта.
//
// Параметры:
//  нет
//
// Возвращаемое значение:
//   дата
//
Функция ДатаВводаВзносовНаДоплатуШахтерам() Экспорт 

	Возврат '20110101'

КонецФункции 

// Возвращает дату вступления в силу нормативного акта.
//
// Параметры:
//  нет
//
// Возвращаемое значение:
//   дата
//
Функция ДатаВводаВзносовЗаЗанятыхНаРаботахСДосрочнойПенсией() Экспорт 

	Возврат '20130101'

КонецФункции 

// Возвращает дату вступления в силу нормативного акта.
//
// Параметры:
//  нет
//
// Возвращаемое значение:
//   дата
//
Функция ДатаОтменыТФОМС() Экспорт 

	Возврат '20120101'

КонецФункции 

// Возвращает дату вступления в силу Федерального закона "О внесении изменений в отдельные законодательные акты
// Российской Федерации по вопросам обязательного пенсионного страхования в части права выбора застрахованными лицами
// варианта пенсионного обеспечения".
//
// Параметры:
//  нет
//
// Возвращаемое значение:
//   дата
//
Функция ДатаОбъединенияСтраховойИНакопительнойЧастейВзносовПФР() Экспорт 

	Возврат '20140101'

КонецФункции 

// Возвращает дату вступления в силу приказа Минфина РФ.
//
// Параметры:
//  нет
//
// Возвращаемое значение:
//   дата
//
Функция ДатаВыделенияВзносовНаОПСсСуммПревышенияПредельнойВеличины() Экспорт 

	Возврат '20160101'

КонецФункции 

/////////////////////////////////////////////////////////////////////////////////////////

// Возвращает строку-перечень полей ресурсов страховых взносов.
//
// Параметры:
//	БезЕНВД - булево - необязательный, если Истина, в строку будут включены парные ЕНВДные поля.
//  ИмяТаблицы - строка - необязательный, если задан, строка результат может использоваться как часть текста запроса к
//                        таблице, содержащей соответствующие поля.
//	ВключатьДопТарифы - булево - необязательный, если Истина, в строку будут включены поля взносов по дополнительным
//	                             тарифам для вредников.
//
// Возвращаемое значение:
//	Строка.
// 
Функция РассчитываемыеВзносыВПФР(БезЕНВД = Ложь, ИмяТаблицы = "", ВключатьДопТарифы = Истина, ВключатьСпецоценку = Истина) Экспорт
	
	ИмяТаблицыСТочкой = "";
	Если ЗначениеЗаполнено(ИмяТаблицы) Тогда
		ИмяТаблицыСТочкой = ИмяТаблицы + "."
	КонецЕсли;
	
	ПереченьРесурсов = "ПФРДоПредельнойВеличины,ПФРСПревышения,ПФРПоСуммарномуТарифу,ПФРСтраховая,ПФРНакопительная" + ?(ВключатьДопТарифы, "," + ВзносыПоДополнительнымТарифам(),"") + ?(БезЕНВД,"",",ПФРДоПредельнойВеличиныЕНВД,ПФРСПревышенияЕНВД,ПФРПоСуммарномуТарифуЕНВД,ПФРСтраховаяЕНВД,ПФРНакопительнаяЕНВД");
	Если ВключатьДопТарифы И ВключатьСпецоценку Тогда
		ПереченьРесурсов = ПереченьРесурсов + "," + ВзносыПоКодамСпециальнойОценки();
	КонецЕсли;
	
	Возврат ИмяТаблицыСТочкой + СтрЗаменить(ПереченьРесурсов, ",", "," + ИмяТаблицыСТочкой)
	
КонецФункции 

Функция РассчитываемыеВзносыВФСС(БезЕНВД = Ложь, ИмяТаблицы = "") Экспорт
	
	ИмяТаблицыСТочкой = "";
	Если ЗначениеЗаполнено(ИмяТаблицы) Тогда
		ИмяТаблицыСТочкой = ИмяТаблицы + "."
	КонецЕсли;
	
	ПереченьРесурсов = "ФСС,ФССНесчастныеСлучаи" + ?(БезЕНВД,"",",ФССЕНВД");
	
	Возврат ИмяТаблицыСТочкой + СтрЗаменить(ПереченьРесурсов, ",", "," + ИмяТаблицыСТочкой)
	
КонецФункции 

Функция РассчитываемыеВзносыВФОМС(БезЕНВД = Ложь, ИмяТаблицы = "") Экспорт
	
	ИмяТаблицыСТочкой = "";
	Если ЗначениеЗаполнено(ИмяТаблицы) Тогда
		ИмяТаблицыСТочкой = ИмяТаблицы + "."
	КонецЕсли;
	
	ПереченьРесурсов = "ФФОМС,ТФОМС" + ?(БезЕНВД,"",",ФФОМСЕНВД,ТФОМСЕНВД");
	
	Возврат ИмяТаблицыСТочкой + СтрЗаменить(ПереченьРесурсов, ",", "," + ИмяТаблицыСТочкой)
	
КонецФункции 

// 4D:ERP для Беларуси, Яна, 04.09.2017 12:28:11 
// Локализовать документ "Отражение зарплаты в финансовом учете", №15826
// {
Функция РассчитываемыеВзносыРБ(ИмяТаблицы = "") Экспорт
	
	ИмяТаблицыСТочкой = "";
	Если ЗначениеЗаполнено(ИмяТаблицы) Тогда
		ИмяТаблицыСТочкой = ИмяТаблицы + "."
	КонецЕсли;
	
	ПереченьРесурсов = "ФСЗН,ПенсионныйФонд,Белгосстрах,ПС,СС";
	
	Возврат ИмяТаблицыСТочкой + СтрЗаменить(ПереченьРесурсов, ",", "," + ИмяТаблицыСТочкой)
	
КонецФункции 
// }
// 4D

// 4D:ERP для Беларуси, АлексейЧ, 20.12.2017 15:37:49 
// Локализовать документ "Начисление зарплаты и взносов", №15928
// {
// Возвращает дату начала локализации ERP в Беларуси.
//
// Параметры:
//  нет
//
// Возвращаемое значение:
//   дата
//
Функция ДатаНачалаЛокализацииВБеларуси() Экспорт 

	Возврат '20150101'

КонецФункции 
// }
// 4D

// Возвращает строку-перечень полей ресурсов страховых взносов.
//
// Параметры:
//	БезЕНВД - булево - необязательный, если Истина, в строку будут включены парные ЕНВДные поля.
//  ИмяТаблицы - строка - необязательный, если задан, строка результат может использоваться как часть текста запроса к
//                        таблице, содержащей соответствующие поля.
//	ВключатьДопТарифы - булево - необязательный, если Истина, в строку будут включены поля взносов по дополнительным
//	                             тарифам для вредников.
//
// Возвращаемое значение:
//	Строка.
// 
Функция РассчитываемыеВзносы(БезЕНВД = Ложь, ИмяТаблицы = "", ВключатьДопТарифы = Истина, ВключатьСпецоценку = Истина) Экспорт
	
	// 4D:ERP для Беларуси, Яна, 04.09.2017 12:28:11 
	// Локализовать документ "Отражение зарплаты в финансовом учете", №15826
	// {
	Возврат РассчитываемыеВзносыВПФР(БезЕНВД, ИмяТаблицы, ВключатьДопТарифы, ВключатьСпецоценку) + "," + РассчитываемыеВзносыВФСС(БезЕНВД, ИмяТаблицы) + "," + РассчитываемыеВзносыВФОМС(БезЕНВД, ИмяТаблицы) + "," + РассчитываемыеВзносыРБ(ИмяТаблицы) 
	// }
	// 4D
	
КонецФункции 

// Возвращает строку-перечень полей ресурсов страховых взносов, по которым ведутся расчеты с фондами.
//
// Параметры:
//	БезЕНВД - булево - необязательный, если Истина, в строку будут включены парные ЕНВДные поля.
//  ИмяТаблицы - строка - необязательный, если задан, строка результат может использоваться как часть текста запроса к
//                        таблице, содержащей соответствующие поля.
//	ВключатьДопТарифы - булево - необязательный, если Истина, в строку будут включены поля взносов по дополнительным
//	                             тарифам для вредников.
//
// Возвращаемое значение:
//	Строка.
// 
Функция ОтражаемыеВУчетеВзносы(БезЕНВД = Ложь, ИмяТаблицы = "", ВключатьДопТарифы = Истина) Экспорт
	
	ИмяТаблицыСТочкой = "";
	Если ЗначениеЗаполнено(ИмяТаблицы) Тогда
		ИмяТаблицыСТочкой = ИмяТаблицы + "."
	КонецЕсли;
	
	ПереченьРесурсов = СтрЗаменить(ВзносыЗаВредников(), ",", "БезСпецОценки,") + СтрЗаменить(ВзносыЗаВредников(), ",", "СпецОценка,");
	
	Возврат РассчитываемыеВзносы(БезЕНВД, ИмяТаблицы, ВключатьДопТарифы, Ложь) + ?(ВключатьДопТарифы, "," + ИмяТаблицыСТочкой + СтрЗаменить(Лев(ПереченьРесурсов, СтрДлина(ПереченьРесурсов) - 1), ",", "," + ИмяТаблицыСТочкой), "");
	
КонецФункции 

// Возвращает строку-перечень полей ресурсов страховых взносов по дополнительным тарифам для вредников.
//
// Параметры:
//	нет
//
// Возвращаемое значение:
//	Строка.
// 
Функция ВзносыЗаВредников() 
	
	Возврат "ПФРЗаЗанятыхНаПодземныхИВредныхРаботах,ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах,"
	
КонецФункции 

// Возвращает строку-перечень полей ресурсов страховых взносов по дополнительным тарифам для вредников.
//
// Параметры:
//	нет
//
// Возвращаемое значение:
//	Строка.
// 
Функция ВзносыПоДополнительнымТарифам() Экспорт
	
	Возврат ВзносыЗаВредников() + "ПФРНаДоплатуЛетчикам,ПФРНаДоплатуШахтерам" 
	
КонецФункции 

// Возвращает строку-перечень полей ресурсов страховых взносов по дополнительному тарифу по классам условий труда.
//
// Параметры:
//	нет
//
// Возвращаемое значение:
//	Строка.
// 
Функция ВзносыПоКодамСпециальнойОценки() Экспорт
	
	Возврат "ПФРЗаЗанятыхНаПодземныхИВредныхРаботахОпасный,ПФРЗаЗанятыхНаПодземныхИВредныхРаботахВредный1,ПФРЗаЗанятыхНаПодземныхИВредныхРаботахВредный2,ПФРЗаЗанятыхНаПодземныхИВредныхРаботахВредный3,ПФРЗаЗанятыхНаПодземныхИВредныхРаботахВредный4,ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахОпасный,ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахВредный1,ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахВредный2,ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахВредный3,ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахВредный4"
	
КонецФункции 

/////////////////////////////////////////////////////////////////////////////////////////

// Возвращает текстовое описание для тарифа страховых взносов.
//
// Параметры:
//	ТарифСтраховыхВзносов - СправочникСсылка.ВидыТарифовСтраховыхВзносов -
//
// Возвращаемое значение:
//	Строка.
// 
Функция ОписаниеВидаТарифа(ТарифСтраховыхВзносов) Экспорт
	
	ОписаниеТарифа = "";	
	
	// 4D:ERP для Беларуси, Яна, 14.06.2017 17:30:08 
	// Справочник "Виды тарифов страховых взносов" привести в соответствие с законодательством РБ, №15100
	// Изменить справки для видов тарифов страховых взносов, №16512
	// {	
	Если ТарифСтраховыхВзносов = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыТарифовСтраховыхВзносов.ОбщийНалоговыйРежим") Тогда
		ОписаниеТарифа = "Применяется основной тариф обязательных страховых взносов"
	ИначеЕсли ТарифСтраховыхВзносов = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыТарифовСтраховыхВзносов.ОрганизацияИнвалидов") Тогда
		ОписаниеТарифа = "Применяются ставки тарифа обязательных страховых взносов, установленные для организаций, применяющих труд инвалидов"
	ИначеЕсли ТарифСтраховыхВзносов = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыТарифовСтраховыхВзносов.СельХозПроизводители") Тогда
		ОписаниеТарифа = "Применяются ставки тарифа обязательных страховых взносов, установленные для с.-х. производителей"
	ИначеЕсли ТарифСтраховыхВзносов = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыТарифовСтраховыхВзносов.КоммОргМогилевскОбл") Тогда
		ОписаниеТарифа = "Применяются ставки тарифа обязательных страховых взносов, установленные для нанимателей, являющихся коммерческими организациями, зарегистрированными на территории юго-восточного региона Могилевской области с 1 июля 2015 г. по 31 декабря 2020 г."	
	КонецЕсли;
	// }
	// 4D
	
	Возврат ОписаниеТарифа;
	
КонецФункции	

// Возвращает текстовое описание для вида застрахованного лица.
//
// Параметры:
//	ВидЗастрахованногоЛица - ПеречислениеСсылка.ВидыЗастрахованныхЛицОбязательногоСтрахования -
//
// Возвращаемое значение:
//	Строка.
// 
Функция ОписаниеВидаЗастрахованногоЛица(ВидЗастрахованногоЛица) Экспорт

	Если ВидЗастрахованногоЛица = ПредопределенноеЗначение("Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ГражданеРФ") Тогда
		Возврат НСтр("ru='На граждан России распространяется обязательное пенсионное, медицинское и социальное страхование в полном объеме.'");
	ИначеЕсли ВидЗастрахованногоЛица = ПредопределенноеЗначение("Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ПостоянноПроживающиеИностранцы") Тогда
		Возврат НСтр("ru='На приравненных к гражданам РФ иностранных граждан и лиц без гражданства распространяется обязательное пенсионное, медицинское и социальное страхование в полном объеме. Страховые взносы уплачиваются в общеустановленном порядке.'");
	ИначеЕсли ВидЗастрахованногоЛица = ПредопределенноеЗначение("Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВысококвалифицированныеСпециалистыПостоянноПроживающие") Тогда
		Возврат НСтр("ru='На постоянно проживающих на территории РФ иностранных граждан и лиц без гражданства, являющихся высококвалифицированными специалистами и членами их семей, распространяется обязательное пенсионное и социальное страхование в полном объеме. Страховые взносы в ПФР и ФСС уплачиваются в том же порядке, что и за граждан РФ'");
	ИначеЕсли ВидЗастрахованногоЛица = ПредопределенноеЗначение("Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПроживающиеИностранцы") Тогда
		Возврат НСтр("ru='На временно проживающих на территории РФ иностранных граждан и лиц без гражданства распространяется обязательное медицинское и социальное страхование в полном объеме. В части пенсионного страхования все уплачиваемые взносы с 2012 года относятся к страховой части пенсии.'");
	ИначеЕсли ВидЗастрахованногоЛица = ПредопределенноеЗначение("Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВысококвалифицированныеСпециалистыВременноПроживающие") Тогда
		Возврат НСтр("ru='На временно проживающих на территории РФ иностранных граждан и лиц без гражданства, являющихся высококвалифицированными специалистами и членами их семей, распространяется обязательное пенсионное и социальное страхование в полном объеме. В части пенсионного страхования все уплачиваемые взносы с 2012 года относятся к страховой части пенсии.'");
	ИначеЕсли ВидЗастрахованногоЛица = ПредопределенноеЗначение("Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВысококвалифицированныеСпециалистыИзЕАЭС") Тогда
		Возврат НСтр("ru='На временно пребывающих на территории РФ граждан стран ЕАЭС, являющихся высококвалифицированными специалистами и членами их семей, с 1 января 2015 года распространяется обязательное медицинское и социальное страхование в полном объеме.'");
	ИначеЕсли ВидЗастрахованногоЛица = ПредопределенноеЗначение("Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПребывающиеИностранцыСтрахуемыеФСС") Тогда
		Возврат НСтр("ru='На временно пребывающих на территории РФ иностранных граждан стран, с которыми заключены соответствующие международные договоры, не распространяется медицинское страхование и пенсионное страхование в РФ. Обязательное социальное распространяется на таких лиц с 1 января 2015 года.'");
	ИначеЕсли ВидЗастрахованногоЛица = ПредопределенноеЗначение("Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПребывающиеИностранцы") Тогда
		Возврат НСтр("ru='На временно пребывающих на территории РФ иностранных граждан и лиц без гражданства не распространяется медицинское страхование в РФ. Обязательное социальное и пенсионное страхование распространяется на таких лиц с 1 января 2015 года, с 1 января 2012 года по 31 декабря 2014 года право на пенсионное страхование распространяется при условии заключения трудового договора на срок не менее 6 месяцев.'");
	ИначеЕсли ВидЗастрахованногоЛица = ПредопределенноеЗначение("Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПребывающиеИностранцыПризнанныеБеженцами") Тогда
		Возврат НСтр("ru='На временно пребывающих на территории РФ иностранных граждан и лиц без гражданства, получивших временное убежище на территории РФ или свидетельство о рассмотрении ходатайства о признании беженцем, распространяется обязательное медицинское страхование в РФ. Обязательное социальное и пенсионное страхование распространяется на таких лиц с 1 января 2015 года, с 1 января 2012 года по 31 декабря 2014 года право на пенсионное страхование распространяется при условии заключения трудового договора на срок не менее 6 месяцев.'");
	ИначеЕсли ВидЗастрахованногоЛица = ПредопределенноеЗначение("Перечисление.ВидыЗастрахованныхЛицОбязательногоСтрахования.ВременноПребывающиеИностранцыНестрахуемые") Тогда
		Возврат НСтр("ru='На нестрахуемых временно пребывающих на территории РФ иностранных граждан и лиц без гражданства (в том числе высококвалифицированных иностранных специалистов) не распространяется пенсионное, медицинское и социальное страхование в РФ.'");
	КонецЕсли;
	
	Возврат "";

КонецФункции 

/////////////////////////////////////////////////////////////////////////////////////////

// Формирует соответствие описания реквизитов, содержащих суммы взносов.
//      	 
// Параметры:
//		Месяц - дата - необязательный, указывает на месяц начисления, для которого могут использоваться те или иные колонки
//		               взносов.
//		ВключатьДопТарифы - булево - необязательный, если выставлен в Ложь, взносы по доп.тарифам в описание не включаются.
//
// Возвращаемое значение:
//		Соответствие
//
Функция ОписаниеПолейВзносовВСоответствии(Месяц = '00010101', ВключатьДопТарифы = Истина, ВключатьКолонкиЕНВД = Истина) Экспорт

	// 4D:ERP для Беларуси, Яна, 06.09.2017 15:02:26 
	// Локализовать документ "Начисление зарплаты и взносов", №15928
	// {	
	Описание = Новый Соответствие;
	
	Описание.Вставить("ФСЗН", "ФСЗН");
	Описание.Вставить("Белгосстрах", "Белгосстрах");
	// }
	// 4D

	Возврат Описание
	
КонецФункции

/////////////////////////////////////////////////////////////////////////////////////////

// Возвращает текст подсказки для поля "ДатаРегистрацииИзменений" периодических регистров сведений:
// "ВременноПребывающиеПринятыеПоДолгосрочнымДоговорам", "КлассыУсловийТрудаПоДолжностям", 
// "ПрименяемыеТарифыСтраховыхВзносов", "СведенияОбИнвалидностиФизическихЛиц", "СтатусыЗастрахованныхФизическихЛиц"
//
Функция ТекстПодсказкиПоляДатаРегистрацииПериодическихРегистров() Экспорт 
	Возврат НСтр("ru = 'При регистрации изменений задним числом может потребоваться перерасчет страховых взносов.'");	 	
КонецФункции	

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формирует из любой даты каноническую дату получения дохода с точки зрения страховых взносов.
//      	 
// Параметры:
//		Дата - дата -
//
// Возвращаемое значение:
//		Дата
//
Функция ДатаПолученияДохода(Дата) Экспорт
	Возврат НачалоДня(КонецМесяца(Дата));	
КонецФункции	

#КонецОбласти
