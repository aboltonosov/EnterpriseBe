
#Область ПрограммныйИнтерфейс

// Проверяет заполнение подразделения в табличной части.
//
// Параметры:
//	ДокументОбъект - ДокументОбъект.ЗаказПоставщику, ДокументОбъект.ПоступлениеТоваровИУслуг - Проверяемый документ
//	Товары - ДанныеФормыКоллекция - Табличная часть товаров
//	ТипыНоменклатуры - Массив - Массив проверяемых типов номенклатуры
//	ТекстОшибки - Строка - Текст ошибки с параметром "%1" для номера строки
//	Отказ - Булево - Признак наличия ошибок
//
Процедура ПроверитьЗаполнениеПодразделенияВТабличнойЧасти(ДокументОбъект, Товары, ТипыНоменклатуры, ТекстОшибки, Отказ) Экспорт
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьПодразделения") Тогда
		Возврат;
	КонецЕсли;  
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Подразделение КАК Подразделение,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ ТаблицаТовары
	|ИЗ
	|	&ТаблицаТовары КАК ТаблицаТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|ГДЕ
	|	ВЫРАЗИТЬ(ТаблицаТовары.Номенклатура КАК Справочник.Номенклатура).ТипНоменклатуры В (&ТипыНоменклатуры)
	|	И ТаблицаТовары.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("ТаблицаТовары",     Товары.Выгрузить(, "Номенклатура, Подразделение, НомерСтроки"));
	Запрос.УстановитьПараметр("ТипыНоменклатуры",  ТипыНоменклатуры);
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, Выборка.НомерСтроки);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				ТекстОшибки,
				ДокументОбъект,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", Выборка.НомерСтроки, "Подразделение"),
				,
				Отказ);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполняет номера ГТД в строках табличной части документа.
//
// Параметры:
//	Товары - ТабличнаяЧасть - Таб.часть, в которой заполняются номера ГТД.
//	НомерГТД - СправочникСсылка.НомераГТД - Номер ГТД, значением которого заполняются строки в таб.части.
//	МассивСтрок - Массив - Исходные строки.
//	ЗаполненыНомераГТД - Булево - Признак успешности заполнения номеров ГТД.
Процедура ЗаполнитьНомераГТДвТабличнойЧасти(Товары, НомерГТД, МассивСтрок, ЗаполненыНомераГТД) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ДанныеСправочника.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК ДанныеСправочника
	|ГДЕ
	|	ДанныеСправочника.Ссылка В (&МассивНоменклатуры)
	|	И ДанныеСправочника.ТипНоменклатуры В
	|		(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И ДанныеСправочника.ВестиУчетПоГТД
	|");
	Запрос.УстановитьПараметр("МассивНоменклатуры", Товары.Выгрузить(, "Номенклатура").ВыгрузитьКолонку("Номенклатура"));
	МассивВедетсяУчетПоГТД = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
	ЗаполненыНомераГТД = Ложь;
	Для Каждого ИдентификаторСтроки Из МассивСтрок Цикл
		СтрокаТаблицы = Товары.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если МассивВедетсяУчетПоГТД.Найти(СтрокаТаблицы.Номенклатура) <> Неопределено Тогда
			СтрокаТаблицы.НомерГТД = НомерГТД;
			ЗаполненыНомераГТД = Истина;
			
			ДанныеГТД = Новый Структура("НомерГТД, СтранаПроисхождения", НомерГТД, Неопределено);
			НоменклатураВызовСервера.ЗаполнитьСтрануПроисхожденияДляНомераГТД(ДанныеГТД);
			ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ДанныеГТД, "СтранаПроисхождения");
			
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

// Процедура проверяет указание номеров ГТД в табличной части "Товары".
//
// Параметры:
//	ДокументОбъект - ДокументОбъект - Текущий документ
//	Товары - ДанныеФормыКоллекция - Табличная часть товаров
//	Отказ - Булево - Признак отказа от проведения документа
//
Процедура ПроверитьЗаполнениеНомеровГТД(ДокументОбъект, Отказ, ИмяТЧ = "Товары") Экспорт
	
	МетаданныеОбъекта = ДокументОбъект.Метаданные();
	ПредставлениеТЧ = МетаданныеОбъекта.ТабличныеЧасти[ИмяТЧ].Синоним;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ИсходнаяТаблица.НомерСтроки КАК НомерСтроки,
	|	ИсходнаяТаблица.Номенклатура КАК Номенклатура,
	|	ИсходнаяТаблица.НомерГТД КАК НомерГТД
	|
	|ПОМЕСТИТЬ ТаблицаТовары
	|ИЗ
	|	&ТаблицаТовары КАК ИсходнаяТаблица
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.НомерГТД = ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|	И ТаблицаТовары.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	И ТаблицаТовары.Номенклатура.ВестиУчетПоГТД
	|");
	ТаблицаТовары = ДокументОбъект[ИмяТЧ].Выгрузить(, "НомерСтроки, Номенклатура, НомерГТД");
	Запрос.УстановитьПараметр("ТаблицаТовары", ТаблицаТовары);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не заполнена колонка ""Номер ГТД"" в строке %1 списка ""%2""'"),
			Выборка.НомерСтроки,
			ПредставлениеТЧ);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			Текст,
			ДокументОбъект,
			ИмяТЧ + "[" + (Выборка.НомерСтроки - 1) + "].НомерГТД",
			,
			Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

// Процедура помещает табличные части "Товары" и "Виды запасов" во временное хранилище.
//
// Параметры:
//	Товары - ТабличнаяЧасть - Помещаемая таб.часть "Товары" во временое хранилище
//	ВидыЗапасов - ТабличнаяЧасть - Помещаемая таб.часть "Виды запасов" во временое хранилище
//	УникальныйИдентификатор - УникальныйИдентификатор - Идентификатор формы, в который помещаются таб.части
//	АдресТоваровВХранилище - Строка - Адрес таб.части "Товары" во временном хранилище
//	АдресВидовЗапасовВХранилище - Строка - Адрес таб.части "Виды запасов" во временном хранилище
//
Процедура ПоместитьТоварыИВидыЗапасовВХранилище(
	Знач Товары,
	Знач ВидыЗапасов,
	УникальныйИдентификатор, 
	АдресТоваровВХранилище, 
	АдресВидовЗапасовВХранилище
	) Экспорт
	
	КолонкиТаблицаТоваров = "НомерСтроки, АналитикаУчетаНоменклатуры, Номенклатура, Характеристика, Количество";
	КолонкиТаблицаВидовЗапасов = "АналитикаУчетаНоменклатуры, ВидЗапасов, НомерГТД, Количество";
	
	Если Товары.Количество() > 0 Тогда
		
		Если Товары[0].Свойство("Сумма") Тогда
			КолонкиТаблицаТоваров = КолонкиТаблицаТоваров + ",Сумма";
		КонецЕсли;
		Если Товары[0].Свойство("СуммаНДС") Тогда
			КолонкиТаблицаТоваров = КолонкиТаблицаТоваров + ",СуммаНДС,СтавкаНДС";
		КонецЕсли;
		Если Товары[0].Свойство("СуммаПродажи") Тогда
			КолонкиТаблицаТоваров = КолонкиТаблицаТоваров + ",СуммаПродажи";
		КонецЕсли;
		Если Товары[0].Свойство("Склад") Тогда
			КолонкиТаблицаТоваров = КолонкиТаблицаТоваров + ",Склад";
		КонецЕсли;
		Если Товары[0].Свойство("ДокументРеализации") Тогда
			КолонкиТаблицаТоваров = КолонкиТаблицаТоваров + ",ДокументРеализации";
		КонецЕсли;
		Если Товары[0].Свойство("Серия") Тогда
			КолонкиТаблицаТоваров = КолонкиТаблицаТоваров + ",Серия";
		КонецЕсли;
		Если Товары[0].Свойство("Назначение") Тогда
			КолонкиТаблицаТоваров = КолонкиТаблицаТоваров + ",Назначение";
		КонецЕсли;
		
	КонецЕсли;
	
	Если ВидыЗапасов.Количество() > 0 Тогда
		
		Если ВидыЗапасов[0].Свойство("СуммаСНДС") Тогда
			КолонкиТаблицаВидовЗапасов = КолонкиТаблицаВидовЗапасов + ",СуммаСНДС";
		КонецЕсли;
		Если ВидыЗапасов[0].Свойство("СуммаНДС") Тогда
			КолонкиТаблицаВидовЗапасов = КолонкиТаблицаВидовЗапасов + ",СуммаНДС,СтавкаНДС";
		КонецЕсли;
		Если ВидыЗапасов[0].Свойство("Склад") Тогда
			КолонкиТаблицаВидовЗапасов = КолонкиТаблицаВидовЗапасов + ",Склад";
		КонецЕсли;
		Если ВидыЗапасов[0].Свойство("СкладОтгрузки") Тогда
			КолонкиТаблицаВидовЗапасов = КолонкиТаблицаВидовЗапасов + ",СкладОтгрузки";
		КонецЕсли;
		Если ВидыЗапасов[0].Свойство("ДокументРеализации") Тогда
			КолонкиТаблицаВидовЗапасов = КолонкиТаблицаВидовЗапасов + ",ДокументРеализации";
		КонецЕсли;
		
	КонецЕсли;
	
	ТаблицаТоваров = Товары.Выгрузить(,КолонкиТаблицаТоваров);
	ТаблицаВидовЗапасов = ВидыЗапасов.Выгрузить(,КолонкиТаблицаВидовЗапасов);
	
	АдресТоваровВХранилище = ПоместитьВоВременноеХранилище(
		ТаблицаТоваров,
		УникальныйИдентификатор);
	АдресВидовЗапасовВХранилище = ПоместитьВоВременноеХранилище(
		ТаблицаВидовЗапасов,
		УникальныйИдентификатор);
	
КонецПроцедуры // ПоместитьТоварыИВидыЗапасовВХранилище()

// Процедура устанавливает видимость выбора типов предназначений видов запасов в форме.
//
// Параметры:
//	Форма - УправляемаяФорма - Текущая форма
//	Поле - ПолеФормы - Поле формы для выбора предназначения
//
Процедура УстановитьВидимостьТиповПредназначенийВидовЗапасов(Форма, Поле) Экспорт
	
	МассивПредназначений = Новый Массив;
	
	Если Не Форма.ПолучитьФункциональнуюОпциюФормы("ФормироватьВидыЗапасовПоПодразделениямМенеджерам") Тогда
		МассивПредназначений.Добавить(Перечисления.ТипыПредназначенияВидовЗапасов.ПредназначенДляМенеджера);
		МассивПредназначений.Добавить(Перечисления.ТипыПредназначенияВидовЗапасов.ПредназначенДляПодразделения);
	КонецЕсли;
	
	Если Не Форма.ПолучитьФункциональнуюОпциюФормы("ФормироватьВидыЗапасовПоСделкам") Тогда
		МассивПредназначений.Добавить(Перечисления.ТипыПредназначенияВидовЗапасов.ПредназначенДляСделки);
	КонецЕсли;	
		
	Для Каждого Предназначение Из МассивПредназначений Цикл
	
		ЭлементСписка = Поле.СписокВыбора.НайтиПоЗначению(Предназначение);
		Если ЭлементСписка <> Неопределено Тогда
			Поле.СписокВыбора.Удалить(ЭлементСписка);
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры // УстановитьВидимостьТиповПредназначенийВидовЗапасов()

// Процедура заполняет список выбора налогообложения для документа закупки.
//
// Параметры:
//	Поле - ПолеФормы - Поле формы для выбора налогообложения
//
Процедура ЗаполнитьСписокВыбораНалогообложения(Поле, ХозяйственнаяОперация) Экспорт
	
	СписокНалогообложений = Поле.СписокВыбора;
	
	СписокНалогообложений.Очистить();
	СписокНалогообложений.Добавить(Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС, НСтр("ru = 'Закупка облагается НДС'"));
	СписокНалогообложений.Добавить(Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС, НСтр("ru = 'Закупка не облагается НДС'"));
	ЗакупкиСервер.ЗаполнитьСписокВыбораНалогообложения(Поле, ХозяйственнаяОперация)
	
КонецПроцедуры

#Область ПроцедурыФормированияТаблицыДоступныхВидовЗапасов

// Процедура формирует временную таблицу доступных видов запасов.
//
// Параметры:
//	Организация - СправочникСсылка.Организации - Организация документа
//	Сделки - СправочникСсылка.Сделка, Массив - Сделка/сделки документа
//	Менеджер - СправочникСсылка.Пользователи - Менеджер документа
//	Подразделение - СправочникСсылка.СтруктураПредприятия - Подразделение документа
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц
//  ДоступнаПередачаНаКомиссию - Булево - Разрешается побирать запасы организаций со способом между организациями "передача на комиссию"
//  ХозОперация - ПеречислениеСсылка.ХозяйственныеОперации - хоз.операция документа
//
Процедура ВидыЗапасовНеОбособленныеИОбособленные(Организация, Сделки, Менеджер, Подразделение, МенеджерВременныхТаблиц, ДоступнаПередачаНаКомиссию = Ложь, ХозОперация = Неопределено) Экспорт
	
	Запрос = Новый Запрос("
	|// Собственные виды запасов
	|ВЫБРАТЬ
	|	ВидыЗапасов.Ссылка КАК ВидЗапасов,
	|	ВидыЗапасов.Ссылка КАК ВидЗапасовПродавца,
	|	ВидыЗапасов.Предназначение КАК Предназначение,
	|	ВидыЗапасов.Сделка КАК Сделка,
	|	ВидыЗапасов.Менеджер КАК Менеджер,
	|	ВидыЗапасов.Подразделение КАК Подразделение
	|
	|ПОМЕСТИТЬ ВидыЗапасов
	|ИЗ
	|	Справочник.ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	Не ВидыЗапасов.РеализацияЗапасовДругойОрганизации
	|	И ВидыЗапасов.Организация = &Организация
	|	И Не ВидыЗапасов.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Виды запасов интеркомпани
	|ВЫБРАТЬ
	|	ВидыЗапасов.Ссылка КАК ВидЗапасов,
	|	ЕСТЬNULL(ВидыЗапасовПродавца.Ссылка, Неопределено) КАК ВидЗапасовПродавца,
	|	ВидыЗапасов.Предназначение КАК Предназначение,
	|	ВидыЗапасов.Сделка КАК Сделка,
	|	ВидыЗапасов.Менеджер КАК Менеджер,
	|	ВидыЗапасов.Подразделение КАК Подразделение
	|ИЗ
	|	Справочник.ВидыЗапасов КАК ВидыЗапасов
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.НастройкаПередачиТоваровМеждуОрганизациями КАК Настройка
	|	ПО
	|		ВидыЗапасов.Организация = Настройка.ОрганизацияВладелец
	|		И ВидыЗапасов.ТипЗапасов = Настройка.ТипЗапасов
	|		И Настройка.СпособПередачиТоваров <> ЗНАЧЕНИЕ(Перечисление.СпособыПередачиТоваров.НеПередается)
	|		И Настройка.ОрганизацияПродавец = &Организация
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ВидыЗапасов КАК ВидыЗапасовПродавца
	|	ПО
	|		ВидыЗапасов.Ссылка = ВидыЗапасовПродавца.ВидЗапасовВладельца
	|		И Настройка.ОрганизацияПродавец = ВидыЗапасовПродавца.Организация
	|		И Настройка.СпособПередачиТоваров = ВидыЗапасовПродавца.СпособПередачиТоваров
	|		И Настройка.Валюта = ВидыЗапасовПродавца.Валюта
	|		// Если не указать отбор по налогообложению, то рискуем получить удвоение остатков,
	|		// в любом случае новый вид запасов, при необходимости, будет создан
	|		И ВидыЗапасов.НалогообложениеНДС = ВидыЗапасовПродавца.НалогообложениеНДС
	|ГДЕ
	|	&ИспользоватьПередачиТоваровМеждуОрганизациями
	|	И Не ВидыЗапасов.РеализацияЗапасовДругойОрганизации
	|	И ВидыЗапасов.Организация <> &Организация
	|	И Не ВидыЗапасов.ПометкаУдаления
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Предназначение
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|
	|// Не обособленные виды запасов
	|ВЫБРАТЬ
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ВидыЗапасов.ВидЗапасовПродавца КАК ВидЗапасовПродавца
	|
	|ПОМЕСТИТЬ ДоступныеВидыЗапасов
	|ИЗ
	|	ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	ВидыЗапасов.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначениеНеОграничено)
	|	ИЛИ ВидыЗапасов.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Обособленные виды запасов по сделке
	|ВЫБРАТЬ
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ВидыЗапасов.ВидЗапасовПродавца КАК ВидЗапасовПродавца
	|ИЗ
	|	ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	ВидыЗапасов.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляСделки)
	|	И ВидыЗапасов.Сделка В (&Сделки)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Обособленные виды запасов по менеджеру
	|ВЫБРАТЬ
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ВидыЗапасов.ВидЗапасовПродавца КАК ВидЗапасовПродавца
	|ИЗ
	|	ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	ВидыЗапасов.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляМенеджера)
	|	И ВидыЗапасов.Менеджер = &Менеджер
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Обособленные виды запасов по подразделению
	|ВЫБРАТЬ
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ВидыЗапасов.ВидЗапасовПродавца КАК ВидЗапасовПродавца
	|ИЗ
	|	ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	ВидыЗапасов.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляПодразделения)
	|	И ВидыЗапасов.Подразделение = &Подразделение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Обособленные виды запасов по заказам
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ВидыЗапасов.ВидЗапасовПродавца КАК ВидЗапасовПродавца
	|ИЗ
	|	ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	ВидыЗапасов.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляЗаказа)
	|");
	Запрос.УстановитьПараметр("ХозОперация", ХозОперация);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Сделки", ОбщегоНазначенияУТКлиентСервер.Массив(Сделки));
	Запрос.УстановитьПараметр("Менеджер", Менеджер);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("ИспользоватьПередачиТоваровМеждуОрганизациями", ПолучитьФункциональнуюОпцию("ИспользоватьПередачиТоваровМеждуОрганизациями"));
	Запрос.УстановитьПараметр("ДоступнаПередачаНаКомиссию", ДоступнаПередачаНаКомиссию);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Процедура формирует временную таблицу доступных собственных видов запасов.
//
// Параметры:
//	Организация - СправочникСсылка.Организации - Организация документа
//	Сделки - СправочникСсылка.Сделка, Массив - Сделка/сделки документа
//	Менеджер - СправочникСсылка.Пользователи - Менеджер документа
//	Подразделение - СправочникСсылка.СтруктураПредприятия - Подразделение документа
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц
//
Процедура ВидыЗапасовСобственныеНеОбособленныеИОбособленные(Организация, Сделки, Менеджер, Подразделение, МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос("
	|// Собственные виды запасов
	|ВЫБРАТЬ
	|	ВидыЗапасов.Ссылка КАК ВидЗапасов,
	|	ВидыЗапасов.Ссылка КАК ВидЗапасовПродавца,
	|	ВидыЗапасов.Предназначение КАК Предназначение,
	|	ВидыЗапасов.Сделка КАК Сделка,
	|	ВидыЗапасов.Менеджер КАК Менеджер,
	|	ВидыЗапасов.Подразделение КАК Подразделение
	|
	|ПОМЕСТИТЬ ВидыЗапасов
	|ИЗ
	|	Справочник.ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	Не ВидыЗапасов.РеализацияЗапасовДругойОрганизации
	|	И ВидыЗапасов.Организация = &Организация
	|	И Не ВидыЗапасов.ПометкаУдаления
	|	И ВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар) 
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Виды запасов интеркомпани
	|ВЫБРАТЬ
	|	ВидыЗапасов.Ссылка КАК ВидЗапасов,
	|	ЕСТЬNULL(ВидыЗапасовПродавца.Ссылка, Неопределено) КАК ВидЗапасовПродавца,
	|	ВидыЗапасов.Предназначение КАК Предназначение,
	|	ВидыЗапасов.Сделка КАК Сделка,
	|	ВидыЗапасов.Менеджер КАК Менеджер,
	|	ВидыЗапасов.Подразделение КАК Подразделение
	|ИЗ
	|	Справочник.ВидыЗапасов КАК ВидыЗапасов
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.НастройкаПередачиТоваровМеждуОрганизациями КАК Настройка
	|	ПО
	|		ВидыЗапасов.Организация = Настройка.ОрганизацияВладелец
	|		И ВидыЗапасов.ТипЗапасов = Настройка.ТипЗапасов
	|		И Настройка.СпособПередачиТоваров <> ЗНАЧЕНИЕ(Перечисление.СпособыПередачиТоваров.НеПередается)
	|		И Настройка.ОрганизацияПродавец = &Организация
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ВидыЗапасов КАК ВидыЗапасовПродавца
	|	ПО
	|		ВидыЗапасов.Ссылка = ВидыЗапасовПродавца.ВидЗапасовВладельца
	|		И Настройка.ОрганизацияПродавец = ВидыЗапасовПродавца.Организация
	|		И Настройка.СпособПередачиТоваров = ВидыЗапасовПродавца.СпособПередачиТоваров
	|		И Настройка.Валюта = ВидыЗапасовПродавца.Валюта
	|		// Если не указать отбор по налогообложению, то рискуем получить удвоение остатков,
	|		// в любом случае новый вид запасов, при необходимости, будет создан
	|		И ВидыЗапасов.НалогообложениеНДС = ВидыЗапасовПродавца.НалогообложениеНДС
	|ГДЕ
	|	&ИспользоватьПередачиТоваровМеждуОрганизациями
	|	И Не ВидыЗапасов.РеализацияЗапасовДругойОрганизации
	|	И ВидыЗапасов.Организация <> &Организация
	|	И Не ВидыЗапасов.ПометкаУдаления
	|	И ВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар) 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Предназначение
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|
	|// Не обособленные виды запасов
	|ВЫБРАТЬ
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ВидыЗапасов.ВидЗапасовПродавца КАК ВидЗапасовПродавца
	|
	|ПОМЕСТИТЬ ДоступныеВидыЗапасов
	|ИЗ
	|	ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	ВидыЗапасов.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначениеНеОграничено)
	|	ИЛИ ВидыЗапасов.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Обособленные виды запасов по сделке
	|ВЫБРАТЬ
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ВидыЗапасов.ВидЗапасовПродавца КАК ВидЗапасовПродавца
	|ИЗ
	|	ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	ВидыЗапасов.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляСделки)
	|	И ВидыЗапасов.Сделка В (&Сделки)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Обособленные виды запасов по менеджеру
	|ВЫБРАТЬ
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ВидыЗапасов.ВидЗапасовПродавца КАК ВидЗапасовПродавца
	|ИЗ
	|	ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	ВидыЗапасов.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляМенеджера)
	|	И ВидыЗапасов.Менеджер = &Менеджер
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Обособленные виды запасов по подразделению
	|ВЫБРАТЬ
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ВидыЗапасов.ВидЗапасовПродавца КАК ВидЗапасовПродавца
	|ИЗ
	|	ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	ВидыЗапасов.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляПодразделения)
	|	И ВидыЗапасов.Подразделение = &Подразделение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Обособленные виды запасов по заказам
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ВидыЗапасов.ВидЗапасовПродавца КАК ВидЗапасовПродавца
	|ИЗ
	|	ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	ВидыЗапасов.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляЗаказа)
	|");
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Сделки", ОбщегоНазначенияУТКлиентСервер.Массив(Сделки));
	Запрос.УстановитьПараметр("Менеджер", Менеджер);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("ИспользоватьПередачиТоваровМеждуОрганизациями", ПолучитьФункциональнуюОпцию("ИспользоватьПередачиТоваровМеждуОрганизациями"));
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Процедура формирует временную таблицу доступных видов запасов.
//
// Параметры:
//	Организация - СправочникСсылка.Организации - Организация документа
//	ОтборЗапапасов - Структура:
//		* НалогообложениеНДС - ПеречислениеСсылка.ТипыНалогообложенияНДС - Налогообложение НДС документа
//		* КонтролироватьНалогообложение - Булево - Признак необходимости контроля налогообложения НДС
//		* Дата - Дата документа
//		* МассивСделок - Массив, СправочникСсылка.Сделка - Сделки документа
//		* Менеджер - СправочникСсылка.Пользователи - Менеджер документа
//		* Подразделение - СправочникСсылка.СтруктураПредприятия - Подразделение документа
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц
//
Процедура ВидыЗапасовПоНалогообложению(Организация, ОтборЗапапасов, МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Настройка.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС) КАК НалогообложениеНДС
	|	
	|ПОМЕСТИТЬ Налогообложение
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(
	|		&Дата, &ИспользоватьПередачиТоваровМеждуОрганизациями) КАК Настройка
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УчетныеПолитикиОрганизаций КАК СпрУчетнаяПолитика
	|	ПО Настройка.УчетнаяПолитика = СпрУчетнаяПолитика.Ссылка
	|	   И СпрУчетнаяПолитика.СистемаНалогообложения =
	|			ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Упрощенная)
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|
	|// Собственные виды запасов
	|ВЫБРАТЬ
	|	ВидыЗапасов.Ссылка КАК ВидЗапасов,
	|	ВидыЗапасов.Ссылка КАК ВидЗапасовПродавца,
	|
	|	ВидыЗапасов.Предназначение КАК Предназначение,
	|	ВидыЗапасов.Сделка КАК Сделка,
	|	ВидыЗапасов.Менеджер КАК Менеджер,
	|	ВидыЗапасов.Подразделение КАК Подразделение,
	|
	|	ВидыЗапасов.ТипЗапасов КАК ТипЗапасов,
	|	ВидыЗапасов.НалогообложениеНДС КАК НалогообложениеНДС
	|
	|ПОМЕСТИТЬ ВозможныеВидыЗапасов
	|ИЗ
	|	Справочник.ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	Не ВидыЗапасов.РеализацияЗапасовДругойОрганизации
	|	И ВидыЗапасов.Организация = &Организация
	|	И Не ВидыЗапасов.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Виды запасов интеркампани
	|ВЫБРАТЬ
	|	ВидыЗапасов.Ссылка КАК ВидЗапасов,
	|	ЕСТЬNULL(ВидыЗапасовПродавца.Ссылка, Неопределено) КАК ВидЗапасовПродавца,
	|
	|	ВидыЗапасов.Предназначение КАК Предназначение,
	|	ВидыЗапасов.Сделка КАК Сделка,
	|	ВидыЗапасов.Менеджер КАК Менеджер,
	|	ВидыЗапасов.Подразделение КАК Подразделение,
	|	
	|	ВЫБОР КОГДА Настройка.СпособПередачиТоваров = ЗНАЧЕНИЕ(Перечисление.СпособыПередачиТоваров.Продажа) ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|	КОГДА Настройка.СпособПередачиТоваров = ЗНАЧЕНИЕ(Перечисление.СпособыПередачиТоваров.ПередачаНаКомиссию) ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК ТипЗапасов,
	|	
	|	ВЫБОР КОГДА Настройка.СпособПередачиТоваров = ЗНАЧЕНИЕ(Перечисление.СпособыПередачиТоваров.ПередачаНаКомиссию) ТОГДА
	|		ВЫБОР КОГДА ВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар) ТОГДА
	|			ВидыЗапасов.НалогообложениеНДС
	|		ИНАЧЕ
	|			ЕСТЬNULL(Налогообложение.НалогообложениеНДС, ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС))
	|		КОНЕЦ
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК НалогообложениеНДС
	|ИЗ
	|	Справочник.ВидыЗапасов КАК ВидыЗапасов
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.НастройкаПередачиТоваровМеждуОрганизациями КАК Настройка
	|	ПО
	|		ВидыЗапасов.Организация = Настройка.ОрганизацияВладелец
	|		И ВидыЗапасов.ТипЗапасов = Настройка.ТипЗапасов
	|		И Настройка.СпособПередачиТоваров <> ЗНАЧЕНИЕ(Перечисление.СпособыПередачиТоваров.НеПередается)
	|		И Настройка.ОрганизацияПродавец = &Организация
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Налогообложение КАК Налогообложение
	|	ПО
	|		Настройка.ОрганизацияВладелец = Налогообложение.Организация
	|		
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ВидыЗапасов КАК ВидыЗапасовПродавца
	|	ПО
	|		ВидыЗапасов.Ссылка = ВидыЗапасовПродавца.ВидЗапасовВладельца
	|		И Настройка.ОрганизацияПродавец = ВидыЗапасовПродавца.Организация
	|		И Настройка.СпособПередачиТоваров = ВидыЗапасовПродавца.СпособПередачиТоваров
	|		И Настройка.Валюта = ВидыЗапасовПродавца.Валюта
	|		И ВидыЗапасовПродавца.НалогообложениеНДС = &ПередачаПодДеятельность
	|ГДЕ
	|	&ИспользоватьПередачиТоваровМеждуОрганизациями
	|	И Не ВидыЗапасов.РеализацияЗапасовДругойОрганизации
	|	И ВидыЗапасов.Организация <> &Организация
	|	И Не ВидыЗапасов.ПометкаУдаления
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Предназначение
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ // Собственные товары
	|	ВозможныеВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ВозможныеВидыЗапасов.ВидЗапасовПродавца КАК ВидЗапасовПродавца,
	|
	|	ВозможныеВидыЗапасов.Предназначение КАК Предназначение,
	|	ВозможныеВидыЗапасов.Сделка КАК Сделка,
	|	ВозможныеВидыЗапасов.Менеджер КАК Менеджер,
	|	ВозможныеВидыЗапасов.Подразделение КАК Подразделение
	|
	|ПОМЕСТИТЬ ВидыЗапасов
	|ИЗ
	|	ВозможныеВидыЗапасов КАК ВозможныеВидыЗапасов
	|ГДЕ
	|	ВозможныеВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|	И Не &ТолькоКомиссионныйТовар
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // Комиссионные товары
	|	ВозможныеВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ВозможныеВидыЗапасов.ВидЗапасовПродавца КАК ВидЗапасовПродавца,
	|
	|	ВозможныеВидыЗапасов.Предназначение КАК Предназначение,
	|	ВозможныеВидыЗапасов.Сделка КАК Сделка,
	|	ВозможныеВидыЗапасов.Менеджер КАК Менеджер,
	|	ВозможныеВидыЗапасов.Подразделение КАК Подразделение
	|ИЗ
	|	ВозможныеВидыЗапасов КАК ВозможныеВидыЗапасов
	|ГДЕ
	|	ВозможныеВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|	И (&НалогообложениеНДС = Неопределено
	|		ИЛИ ВозможныеВидыЗапасов.НалогообложениеНДС = &НалогообложениеНДС
	|		)
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|
	|// Не обособленные виды запасов
	|ВЫБРАТЬ
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ВидыЗапасов.ВидЗапасовПродавца КАК ВидЗапасовПродавца
	|
	|ПОМЕСТИТЬ ДоступныеВидыЗапасов
	|ИЗ
	|	ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	ВидыЗапасов.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначениеНеОграничено)
	|	ИЛИ ВидыЗапасов.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Обособленные виды запасов по сделке
	|ВЫБРАТЬ
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ВидыЗапасов.ВидЗапасовПродавца КАК ВидЗапасовПродавца
	|ИЗ
	|	ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	ВидыЗапасов.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляСделки)
	|	И ВидыЗапасов.Сделка В (&МассивСделок)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Обособленные виды запасов по менеджеру
	|ВЫБРАТЬ
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ВидыЗапасов.ВидЗапасовПродавца КАК ВидЗапасовПродавца
	|ИЗ
	|	ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	ВидыЗапасов.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляМенеджера)
	|	И ВидыЗапасов.Менеджер = &Менеджер
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Обособленные виды запасов по подразделению
	|ВЫБРАТЬ
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ВидыЗапасов.ВидЗапасовПродавца КАК ВидЗапасовПродавца
	|ИЗ
	|	ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	ВидыЗапасов.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляПодразделения)
	|	И ВидыЗапасов.Подразделение = &Подразделение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Обособленные виды запасов по заказам
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ВидыЗапасов.ВидЗапасовПродавца КАК ВидЗапасовПродавца
	|ИЗ
	|	ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	ВидыЗапасов.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляЗаказа)
	|");
	Запрос.УстановитьПараметр("Дата", ОтборЗапапасов.Дата);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Запрос.УстановитьПараметр("МассивСделок", ОтборЗапапасов.МассивСделок);
	Запрос.УстановитьПараметр("Менеджер", ОтборЗапапасов.Менеджер);
	Запрос.УстановитьПараметр("Подразделение", ОтборЗапапасов.Подразделение);
	Запрос.УстановитьПараметр("ИспользоватьПередачиТоваровМеждуОрганизациями", ПолучитьФункциональнуюОпцию("ИспользоватьПередачиТоваровМеждуОрганизациями"));
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Если Не ОтборЗапапасов.КонтролироватьНалогообложение
	 ИЛИ ОтборЗапапасов.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаНаЭкспорт
	 ИЛИ ОтборЗапапасов.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД Тогда
		Запрос.УстановитьПараметр("НалогообложениеНДС", Неопределено);
		Запрос.УстановитьПараметр("ТолькоКомиссионныйТовар", Ложь);
	Иначе
		НалогообложениеНДСОрганизации = Справочники.Организации.НалогообложениеНДС(Организация, Неопределено, ОтборЗапапасов.Дата);
		Запрос.УстановитьПараметр("НалогообложениеНДС", ОтборЗапапасов.НалогообложениеНДС);
		Запрос.УстановитьПараметр("ТолькоКомиссионныйТовар", (НалогообложениеНДСОрганизации <> ОтборЗапапасов.НалогообложениеНДС));
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьРаздельныйУчетПоНалогообложению") Тогда
		Запрос.УстановитьПараметр("ПередачаПодДеятельность", ОтборЗапапасов.НалогообложениеНДС);
	Иначе
		Запрос.УстановитьПараметр("ПередачаПодДеятельность", Перечисления.ТипыНалогообложенияНДС.ПустаяСсылка());
	КонецЕсли; 
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Процедура формирует временную таблицу доступных видов запасов.
//
// Параметры:
//	Организация - СправочникСсылка.Организации - Организация документа
//	Сделки - СправочникСсылка.Сделка, Массив - Сделка/сделки документа
//	Менеджер - СправочникСсылка.Пользователи - Менеджер документа
//	Подразделение - СправочникСсылка.СтруктураПредприятия - Подразделение документа
//	МенеджерВременныхТаблиц - Менеджер временных таблиц
//
Процедура ВидыЗапасовНеОбособленныеИОбособленныеБезИнтеркомпани(Организация, Сделки, Менеджер, Подразделение, МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос("
	|// Собственные виды запасов
	|ВЫБРАТЬ
	|	ВидыЗапасов.Ссылка КАК ВидЗапасов,
	|	ВидыЗапасов.Ссылка КАК ВидЗапасовПродавца,
	|	ВидыЗапасов.Предназначение КАК Предназначение,
	|	ВидыЗапасов.Сделка КАК Сделка,
	|	ВидыЗапасов.Менеджер КАК Менеджер,
	|	ВидыЗапасов.Подразделение КАК Подразделение
	|
	|ПОМЕСТИТЬ ВидыЗапасов
	|ИЗ
	|	Справочник.ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	Не ВидыЗапасов.РеализацияЗапасовДругойОрганизации
	|	И ВидыЗапасов.Организация = &Организация
	|	И Не ВидыЗапасов.ПометкаУдаления
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Предназначение
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|
	|// Не обособленные виды запасов
	|ВЫБРАТЬ
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ВидыЗапасов.ВидЗапасовПродавца КАК ВидЗапасовПродавца
	|
	|ПОМЕСТИТЬ ДоступныеВидыЗапасов
	|ИЗ
	|	ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	ВидыЗапасов.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначениеНеОграничено)
	|	ИЛИ ВидыЗапасов.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Обособленные виды запасов по сделке
	|ВЫБРАТЬ
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ВидыЗапасов.ВидЗапасовПродавца КАК ВидЗапасовПродавца
	|ИЗ
	|	ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	ВидыЗапасов.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляСделки)
	|	И ВидыЗапасов.Сделка В (&Сделки)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Обособленные виды запасов по менеджеру
	|ВЫБРАТЬ
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ВидыЗапасов.ВидЗапасовПродавца КАК ВидЗапасовПродавца
	|ИЗ
	|	ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	ВидыЗапасов.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляМенеджера)
	|	И ВидыЗапасов.Менеджер = &Менеджер
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Обособленные виды запасов по подразделению
	|ВЫБРАТЬ
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ВидыЗапасов.ВидЗапасовПродавца КАК ВидЗапасовПродавца
	|ИЗ
	|	ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	ВидыЗапасов.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляПодразделения)
	|	И ВидыЗапасов.Подразделение = &Подразделение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Обособленные виды запасов по заказам
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ВидыЗапасов.ВидЗапасовПродавца КАК ВидЗапасовПродавца
	|ИЗ
	|	ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	ВидыЗапасов.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляЗаказа)
	|");
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Сделки", ОбщегоНазначенияУТКлиентСервер.Массив(Сделки));
	Запрос.УстановитьПараметр("Менеджер", Менеджер);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Процедура формирует временную таблицу доступных видов запасов.
//
// Параметры:
//	Организация - СправочникСсылка.Организации - Организация документа
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц
//
Процедура ВидыЗапасовЛюбыеБезИнтеркомпани(Организация, МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос("
	|// Собственные виды запасов
	|ВЫБРАТЬ
	|	ВидыЗапасов.Ссылка КАК ВидЗапасов,
	|	ВидыЗапасов.Ссылка КАК ВидЗапасовПродавца
	|
	|ПОМЕСТИТЬ ДоступныеВидыЗапасов
	|ИЗ
	|	Справочник.ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	Не ВидыЗапасов.РеализацияЗапасовДругойОрганизации
	|	И ВидыЗапасов.Организация = &Организация
	|	И ВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|	И Не ВидыЗапасов.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Комиссионные виды запасов
	|ВЫБРАТЬ
	|	ВидыЗапасов.Ссылка КАК ВидЗапасов,
	|	ВидыЗапасов.Ссылка КАК ВидЗапасовПродавца
	|ИЗ
	|	Справочник.ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	Не ВидыЗапасов.РеализацияЗапасовДругойОрганизации
	|	И ВидыЗапасов.Организация = &Организация
	|	И ВидыЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|	И ВидыЗапасов.Комитент ССЫЛКА Справочник.Партнеры
	|	И Не ВидыЗапасов.ПометкаУдаления
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|");
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Выполнить();
	
КонецПроцедуры // ВидыЗапасовЛюбыеБезИнтеркомпани()

// Функция формирует таблицу ошибок заполнения видов запасов.
//
// ВозвращаемоеЗначение:
//	ТаблицаЗначений - Таблица, для фиксации остатков.
//
Функция ТаблицаОшибокЗаполненияВидовЗапасов() Экспорт
	
	ТаблицаОшибок = Новый ТаблицаЗначений;
	ТаблицаОшибок.Колонки.Добавить("Номенклатура");
	ТаблицаОшибок.Колонки.Добавить("Характеристика");
	ТаблицаОшибок.Колонки.Добавить("Серия");
	ТаблицаОшибок.Колонки.Добавить("Склад");
	ТаблицаОшибок.Колонки.Добавить("Количество");
	ТаблицаОшибок.Колонки.Добавить("Сумма");
	ТаблицаОшибок.Колонки.Добавить("ЕдиницаИзмерения");
	ТаблицаОшибок.Колонки.Добавить("Сделка");
	ТаблицаОшибок.Колонки.Добавить("НеУказанНомерГТД");

	Возврат ТаблицаОшибок;
	
КонецФункции 

// Функция определяет аналитику обособленного учета документа.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц
//
// Возвращаемое значение:
//	Структура - данные аналитики обособленного учета
//
Функция АналитикаОбособленноУчетаДокумента(МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ВЫБОР КОГДА ДанныеДокумента.Сделка <> ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка) ТОГДА
	|		ДанныеДокумента.Сделка
	|	КОГДА ДанныеДокумента.Менеджер <> ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка) ТОГДА
	|		ДанныеДокумента.Менеджер
	|	КОГДА ДанныеДокумента.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) ТОГДА
	|		ДанныеДокумента.Подразделение
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК АналитикаОбособленногоУчета,
	|
	|	(ВЫБОР
	|		КОГДА ДанныеДокумента.Сделка <> ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка) ТОГДА &ДляСделки
	|		КОГДА ДанныеДокумента.Менеджер <> ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка) ТОГДА &ДляМенеджера
	|		КОГДА ДанныеДокумента.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) ТОГДА &ДляПодразделения
	|		ИНАЧЕ """" КОНЕЦ) КАК СтрокаАналитикиОбособленногоУчета
	|ИЗ
	|	ТаблицаДанныхДокумента КАК ДанныеДокумента
	|");
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ДляСделки", НСтр("ru = 'для сделки'"));
	Запрос.УстановитьПараметр("ДляМенеджера", НСтр("ru = 'для менеджера'"));
	Запрос.УстановитьПараметр("ДляПодразделения", НСтр("ru = 'для подразделения'"));
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Структура = Новый Структура;
	Структура.Вставить("СтрокаАналитики", Выборка.СтрокаАналитикиОбособленногоУчета);
	Структура.Вставить("Аналитика", Выборка.АналитикаОбособленногоУчета);

	Возврат Структура;
	
КонецФункции // АналитикаОбособленноУчетаДокумента()

// Функция проверяет изменение табличной части "Товары" относительно табличной части "Виды запасов" документа.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц
//
// Возвращаемое значение:
//	Булево - Истина - товары изменены
//           Ложь - товары не изменены
//
Функция ПроверитьИзменениеТоваровПоКоличеству(МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
	|ИЗ (
	|	ВЫБРАТЬ
	|		ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ТаблицаТоваров.Количество КАК Количество
	|	ИЗ
	|		ТаблицаТоваров КАК ТаблицаТоваров
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		-ТаблицаВидыЗапасов.Количество КАК Количество
	|	ИЗ
	|		ТаблицаВидыЗапасов КАК ТаблицаВидыЗапасов
	|
	|	) КАК ТаблицаТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаТоваров.Количество) <> 0
	|");
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;

	РезультатЗапрос = Запрос.Выполнить();
	
	Возврат (Не РезультатЗапрос.Пустой());
	
КонецФункции

// Функция проверяет изменение табличной части "Товары" относительно табличной части "Виды запасов" документа.
//
// Параметры:
//	МенеджерВременныхТаблиц - Менеджер временных таблиц
//
// Возвращаемое значение:
//	Булево - Истина - товары изменены
//           Ложь - товары не изменены
//
Функция ПроверитьИзменениеТоваровПоКоличествуИСумме(МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТоваров.СтавкаНДС КАК СтавкаНДС
	|ИЗ (
	|	ВЫБРАТЬ
	|		ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ТаблицаТоваров.СтавкаНДС КАК СтавкаНДС,
	|		ТаблицаТоваров.Количество КАК Количество,
	|		ТаблицаТоваров.СуммаСНДС КАК СуммаСНДС,
	|		ТаблицаТоваров.СуммаНДС КАК СуммаНДС,
	|		ТаблицаТоваров.СуммаВознаграждения КАК СуммаВознаграждения,
	|		ТаблицаТоваров.СуммаНДСВознаграждения КАК СуммаНДСВознаграждения
	|	ИЗ
	|		ТаблицаТоваров КАК ТаблицаТоваров
	|	ГДЕ
	|		ТаблицаТоваров.Номенклатура.ТипНоменклатуры  В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ТаблицаВидыЗапасов.СтавкаНДС КАК СтавкаНДС,
	|		-ТаблицаВидыЗапасов.Количество КАК Количество,
	|		-ТаблицаВидыЗапасов.СуммаСНДС КАК СуммаСНДС,
	|		-ТаблицаВидыЗапасов.СуммаНДС КАК СуммаНДС,
	|		-ТаблицаВидыЗапасов.СуммаВознаграждения КАК СуммаВознаграждения,
	|		-ТаблицаВидыЗапасов.СуммаНДСВознаграждения КАК СуммаНДСВознаграждения
	|	ИЗ
	|		ТаблицаВидыЗапасов КАК ТаблицаВидыЗапасов
	|	) КАК ТаблицаТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры,
	|	ТаблицаТоваров.СтавкаНДС
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаТоваров.Количество) <> 0
	|	ИЛИ СУММА(ТаблицаТоваров.СуммаСНДС) <> 0
	|	ИЛИ СУММА(ТаблицаТоваров.СуммаНДС) <> 0
	|	ИЛИ СУММА(ТаблицаТоваров.СуммаВознаграждения) <> 0
	|	ИЛИ СУММА(ТаблицаТоваров.СуммаНДСВознаграждения) <> 0
	|");
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;

	РезультатЗапрос = Запрос.Выполнить();
	
	Возврат (Не РезультатЗапрос.Пустой());
	
КонецФункции

// Процедура устанавливает исключительную блокировку на остатки по регистру "Товары организаций".
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер, содержащий в себе таблицы ТаблицаТоваров и ТаблицаДанныхДокумента.
//
Процедура УстановитьБлокировкуОстатковТоваровОрганизаций(МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаДанныхДокумента.Организация КАК Организация
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокумента КАК ТаблицаДанныхДокумента
	|	ПО
	|		Истина
	|ГДЕ
	|	ТаблицаТоваров.Номенклатура.ТипНоменклатуры В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), 
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	)
	|");
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ТоварыОрганизаций");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("АналитикаУчетаНоменклатуры", "АналитикаУчетаНоменклатуры");
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьПередачиТоваровМеждуОрганизациями") Тогда
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Организация", "Организация");
	КонецЕсли;
	
	Блокировка.Заблокировать();
	
КонецПроцедуры

// Процедура формирует таблицу остатков товаров организаций.
//
// Параметры:
//	ДокументСсылка - Текущий документ
//	Организация - СправочникСсылка.Организации - Организация документа
//	Дата - Дата документа
//	ДополнительныеСвойства - Структура - Дополнительные свойства документа
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц
//	ПоВсемВидамЗапасов - Булево - отбор только по доступным или по всем видам запасов
//
Процедура ТаблицаОстатковТоваровОрганизаций(ДокументСсылка, Организация, Дата, ДополнительныеСвойства, МенеджерВременныхТаблиц, ПоВсемВидамЗапасов = Ложь) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ // Движения документа
	|	МАКСИМУМ(Товары.Период) КАК Период,
	|	Товары.Организация,
	|	Товары.АналитикаУчетаНоменклатуры,
	|	Товары.ВидЗапасов,
	|	Товары.НомерГТД,
	|	СУММА(Товары.Количество) КАК Количество
	|ПОМЕСТИТЬ ДвиженияДокумента
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций КАК Товары
	|	ЛЕВОЕ СОЕДИНЕНИЕ ДоступныеВидыЗапасов КАК ДоступныеВидыЗапасов
	|		ПО Товары.ВидЗапасов = ДоступныеВидыЗапасов.ВидЗапасов
	|ГДЕ
	|	Товары.Регистратор = &Ссылка
	|	И Товары.Активность
	|	И Товары.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И (Товары.АналитикаУчетаНоменклатуры) В (
	|		ВЫБРАТЬ
	|			ТаблицаТоваров.АналитикаУчетаНоменклатуры
	|		ИЗ
	|			ТаблицаТоваров КАК ТаблицаТоваров
	|		)
	|	И ((Не &ПоВсемВидамЗапасов И Не ДоступныеВидыЗапасов.ВидЗапасов ЕСТЬ NULL) ИЛИ &ПоВсемВидамЗапасов)
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.Организация,
	|	Товары.АналитикаУчетаНоменклатуры,
	|	Товары.ВидЗапасов,
	|	Товары.НомерГТД
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Товары.Организация,
	|	Товары.АналитикаУчетаНоменклатуры,
	|	Товары.ВидЗапасов,
	|	Товары.НомерГТД
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ // Остатки товаров организации на конец месяца
	|	Остатки.Организация,
	|	Остатки.АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
	|	Остатки.ВидЗапасов,
	|	Остатки.НомерГТД,
	|	Остатки.КоличествоОстаток КАК КоличествоОстаток
	|ПОМЕСТИТЬ ОстаткиНаКонецМесяца
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.Остатки(&ГраницаМесяца,
	|		(АналитикаУчетаНоменклатуры) В (
	|			ВЫБРАТЬ
	|				ТаблицаТоваров.АналитикаУчетаНоменклатуры
	|			ИЗ
	|				ТаблицаТоваров КАК ТаблицаТоваров
	|			)
	|		И (
	|			ВидЗапасов В (
	|				ВЫБРАТЬ
	|					ДоступныеВидыЗапасов.ВидЗапасов
	|				ИЗ
	|					ДоступныеВидыЗапасов КАК ДоступныеВидыЗапасов
	|				)
	|			ИЛИ &ПоВсемВидамЗапасов
	|			)
	|		И Организация = ВидЗапасов.Организация
	|	) КАК Остатки
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ // Остатки товаров организации на конец дня
	|	Остатки.Организация,
	|	Остатки.АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
	|	Остатки.ВидЗапасов,
	|	Остатки.НомерГТД,
	|	Остатки.КоличествоОстаток КАК КоличествоОстаток
	|ПОМЕСТИТЬ ОстаткиНаКонецДня
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.Остатки(&ГраницаДня,
	|		(АналитикаУчетаНоменклатуры) В (
	|			ВЫБРАТЬ
	|				ТаблицаТоваров.АналитикаУчетаНоменклатуры
	|			ИЗ
	|				ТаблицаТоваров КАК ТаблицаТоваров
	|			)
	|		И (
	|			ВидЗапасов В (
	|				ВЫБРАТЬ
	|					ДоступныеВидыЗапасов.ВидЗапасов
	|				ИЗ
	|					ДоступныеВидыЗапасов КАК ДоступныеВидыЗапасов
	|				)
	|			ИЛИ &ПоВсемВидамЗапасов
	|			)
	|		И Организация = ВидЗапасов.Организация
	|	) КАК Остатки
	|ГДЕ
	|	&КонтролироватьНаКонецДня
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НЕОПРЕДЕЛЕНО КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
	|	НЕОПРЕДЕЛЕНО КАК ВидЗапасов,
	|	НЕОПРЕДЕЛЕНО КАК НомерГТД,
	|	0 КАК КоличествоОстаток
	|ГДЕ
	|	НЕ &КонтролироватьНаКонецДня
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ // Остатки товаров организации на дату последнего движения
	|	Остатки.Организация,
	|	Остатки.АналитикаУчетаНоменклатуры,
	|	Остатки.ВидЗапасов,
	|	Остатки.НомерГТД,
	|	Остатки.КоличествоОстаток КАК КоличествоОстаток
	|ПОМЕСТИТЬ ОстаткиНаПоследнееДвижение
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.Остатки(,
	|		Не &ИспользоватьОстаткиНаКонецМесяца
	|		И (АналитикаУчетаНоменклатуры) В (
	|			ВЫБРАТЬ
	|				ТаблицаТоваров.АналитикаУчетаНоменклатуры
	|			ИЗ
	|				ТаблицаТоваров КАК ТаблицаТоваров
	|			)
	|		И (
	|			ВидЗапасов В (
	|				ВЫБРАТЬ
	|					ДоступныеВидыЗапасов.ВидЗапасов
	|				ИЗ
	|					ДоступныеВидыЗапасов КАК ДоступныеВидыЗапасов
	|				)
	|			ИЛИ &ПоВсемВидамЗапасов
	|			)
	|		И Организация = ВидЗапасов.Организация
	|	) КАК Остатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Остатки.Организация,
	|	Остатки.АналитикаУчетаНоменклатуры,
	|	Остатки.ВидЗапасов,
	|	Остатки.НомерГТД
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыОрганизаций.Организация КАК Организация,
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК СкладОтгрузки,
	|	Аналитика.Номенклатура КАК Номенклатура,
	|	Аналитика.Характеристика КАК Характеристика,
	|	Аналитика.Серия КАК Серия,
	|	Аналитика.Склад КАК Склад,
	|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
	|	ВЫБОР КОГДА ТоварыОрганизаций.Организация <> &Организация
	|		ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ТоварыОрганизаций.ВидЗапасов
	|	КОНЕЦ КАК ВидЗапасов,
	|	ТоварыОрганизаций.ВидЗапасовВладельца КАК ВидЗапасовВладельца,
	|	Неопределено КАК ВидЗапасовПолучателя,
	|	Неопределено КАК ВидЗапасовОтгрузки,
	|	ВЫБОР КОГДА ТоварыОрганизаций.Организация <> &Организация
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК РеализацияЗапасовДругойОрганизации,
	|	Аналитика.Назначение КАК Назначение,
	|	ТоварыОрганизаций.НомерГТД КАК НомерГТД,
	|	МАКСИМУМ(
	|		ВЫБОР КОГДА ТоварыОрганизаций.ДатаПоступления > &КонецМесяца
	|			ТОГДА &НачалоМесяца
	|			ИНАЧЕ ТоварыОрганизаций.ДатаПоступления
	|		КОНЕЦ
	|	) КАК ДатаПоступления,
	|	СУММА(ТоварыОрганизаций.Количество) КАК КоличествоОстаток,
	|	СУММА(ТоварыОрганизаций.НаДатуКонтроля) КАК НаДатуКонтроля,
	|	СУММА(ТоварыОрганизаций.НаДатуАктуальности) КАК НаДатуАктуальности,
	|	0 КАК СуммаОстаток,
	|
	|	ВЫБОР КОГДА ТоварыОрганизаций.ВидЗапасов.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляСделки) ТОГДА
	|		ТоварыОрганизаций.ВидЗапасов.Сделка
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка)
	|	КОНЕЦ КАК Сделка
	|
	|ПОМЕСТИТЬ ТаблицаОстатков
	|ИЗ (
	|	ВЫБРАТЬ // Движения документа
	|		ТоварыОрганизаций.Организация КАК Организация,
	|		ТоварыОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ЕСТЬNULL(ДоступныеВидыЗапасов.ВидЗапасовПродавца, ТоварыОрганизаций.ВидЗапасов) КАК ВидЗапасов,
	|		ТоварыОрганизаций.ВидЗапасов КАК ВидЗапасовВладельца,
	|		ТоварыОрганизаций.НомерГТД КАК НомерГТД,
	|		ТоварыОрганизаций.Количество КАК Количество,
	|		0 КАК НаДатуКонтроля,
	|		0 КАК НаДатуАктуальности,
	|
	|		ДатыПоступления.ДатаПоступления КАК ДатаПоступления
	|	ИЗ
	|		РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.ДатыПоступленияТоваровОрганизаций КАК ДатыПоступления
	|		ПО
	|			ТоварыОрганизаций.ВидЗапасов = ДатыПоступления.ВидЗапасов
	|			И ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Номенклатура = ДатыПоступления.Номенклатура
	|			И ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Характеристика = ДатыПоступления.Характеристика
	|			И ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Серия = ДатыПоступления.Серия
	|			И ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Назначение = ДатыПоступления.Назначение
	|			И ТоварыОрганизаций.НомерГТД = ДатыПоступления.НомерГТД
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиНаКонецМесяца КАК НаКонецМесяца
	|			ПО ТоварыОрганизаций.Организация = НаКонецМесяца.Организация
	|			И ТоварыОрганизаций.АналитикаУчетаНоменклатуры = НаКонецМесяца.АналитикаУчетаНоменклатуры
	|			И ТоварыОрганизаций.ВидЗапасов = НаКонецМесяца.ВидЗапасов
	|			И ТоварыОрганизаций.НомерГТД = НаКонецМесяца.НомерГТД
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			ДоступныеВидыЗапасов КАК ДоступныеВидыЗапасов
	|		ПО
	|			ТоварыОрганизаций.ВидЗапасов = ДоступныеВидыЗапасов.ВидЗапасов
	|	ГДЕ
	|		ТоварыОрганизаций.Период <= &ДатаДокумента
	|		И ТоварыОрганизаций.Регистратор = &Ссылка
	|		И НаКонецМесяца.ВидЗапасов ЕСТЬ NULL
	|		И ТоварыОрганизаций.Активность
	|		И ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И (ТоварыОрганизаций.АналитикаУчетаНоменклатуры) В (
	|			ВЫБРАТЬ
	|				ТаблицаТоваров.АналитикаУчетаНоменклатуры
	|			ИЗ
	|				ТаблицаТоваров КАК ТаблицаТоваров
	|			)
	|		И ((Не &ПоВсемВидамЗапасов И Не ДоступныеВидыЗапасов.ВидЗапасов ЕСТЬ NULL) ИЛИ &ПоВсемВидамЗапасов)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ // Остатки товаров организации
	|		НаКонецМесяца.Организация КАК Организация,
	|		НаКонецМесяца.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ВЫБОР 
	|			КОГДА ДоступныеВидыЗапасов.ВидЗапасовПродавца.РеализацияЗапасовДругойОрганизации
	|				И НаКонецМесяца.АналитикаУчетаНоменклатуры.Номенклатура.ПодакцизныйТовар
	|			ТОГДА Неопределено
	|			ИНАЧЕ ЕСТЬNULL(ДоступныеВидыЗапасов.ВидЗапасовПродавца, НаКонецМесяца.ВидЗапасов)
	|		КОНЕЦ КАК ВидЗапасов,
	|		НаКонецМесяца.ВидЗапасов КАК ВидЗапасовВладельца,
	|		НаКонецМесяца.НомерГТД КАК НомерГТД,
	|
	|		ВЫБОР 
	|			КОГДА &ИспользоватьОстаткиНаКонецМесяца 
	|				ТОГДА НаКонецМесяца.КоличествоОстаток  + ЕСТЬNULL(Движения.Количество, 0)
	|			КОГДА НаКонецМесяца.КоличествоОстаток <= ЕСТЬNULL(НаПоследнееДвижение.КоличествоОстаток, 0)
	|					И (НаКонецМесяца.КоличествоОстаток <= ЕСТЬNULL(НаКонецДня.КоличествоОстаток, 0) ИЛИ НЕ &КонтролироватьНаКонецДня)
	|			ТОГДА
	|				НаКонецМесяца.КоличествоОстаток + ЕСТЬNULL(Движения.Количество, 0)
	|			КОГДА ЕСТЬNULL(НаПоследнееДвижение.КоличествоОстаток, 0) <= НаКонецМесяца.КоличествоОстаток
	|					И (ЕСТЬNULL(НаПоследнееДвижение.КоличествоОстаток, 0) <= ЕСТЬNULL(НаКонецДня.КоличествоОстаток, 0) ИЛИ НЕ &КонтролироватьНаКонецДня)
	|			ТОГДА
	|				ЕСТЬNULL(НаПоследнееДвижение.КоличествоОстаток, 0) + ЕСТЬNULL(Движения.Количество, 0)
	|			КОГДА ЕСТЬNULL(НаКонецДня.КоличествоОстаток, 0) <= ЕСТЬNULL(НаПоследнееДвижение.КоличествоОстаток, 0)
	|					И ЕСТЬNULL(НаКонецДня.КоличествоОстаток, 0) <= НаКонецМесяца.КоличествоОстаток
	|					И &КонтролироватьНаКонецДня
	|			ТОГДА
	|				ЕСТЬNULL(НаКонецДня.КоличествоОстаток, 0) + ЕСТЬNULL(Движения.Количество, 0)
	|		КОНЕЦ КАК Количество,
	|
	|		НаКонецМесяца.КоличествоОстаток КАК НаДатуКонтроля,
	|		0 КАК НаДатуАктуальности,
	|		ДатыПоступления.ДатаПоступления КАК ДатаПоступления
	|	ИЗ
	|		ОстаткиНаКонецМесяца КАК НаКонецМесяца
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДвиженияДокумента КАК Движения
	|			ПО Движения.Организация = НаКонецМесяца.Организация
	|			И Движения.АналитикаУчетаНоменклатуры = НаКонецМесяца.АналитикаУчетаНоменклатуры
	|			И Движения.ВидЗапасов = НаКонецМесяца.ВидЗапасов
	|			И Движения.НомерГТД = НаКонецМесяца.НомерГТД
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиНаПоследнееДвижение КАК НаПоследнееДвижение
	|			ПО НаКонецМесяца.Организация = НаПоследнееДвижение.Организация
	|			И НаКонецМесяца.АналитикаУчетаНоменклатуры = НаПоследнееДвижение.АналитикаУчетаНоменклатуры
	|			И НаКонецМесяца.ВидЗапасов = НаПоследнееДвижение.ВидЗапасов
	|			И НаКонецМесяца.НомерГТД = НаПоследнееДвижение.НомерГТД
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиНаКонецДня КАК НаКонецДня
	|			ПО НаКонецМесяца.Организация = НаКонецДня.Организация
	|			И НаКонецМесяца.АналитикаУчетаНоменклатуры = НаКонецДня.АналитикаУчетаНоменклатуры
	|			И НаКонецМесяца.ВидЗапасов = НаКонецДня.ВидЗапасов
	|			И НаКонецМесяца.НомерГТД = НаКонецДня.НомерГТД
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыПоступленияТоваровОрганизаций КАК ДатыПоступления
	|			ПО НаКонецМесяца.ВидЗапасов = ДатыПоступления.ВидЗапасов
	|			И НаКонецМесяца.АналитикаУчетаНоменклатуры.Номенклатура = ДатыПоступления.Номенклатура
	|			И НаКонецМесяца.АналитикаУчетаНоменклатуры.Характеристика = ДатыПоступления.Характеристика
	|			И НаКонецМесяца.АналитикаУчетаНоменклатуры.Серия = ДатыПоступления.Серия
	|			И НаКонецМесяца.АналитикаУчетаНоменклатуры.Назначение = ДатыПоступления.Назначение
	|			И НаКонецМесяца.НомерГТД = ДатыПоступления.НомерГТД
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДоступныеВидыЗапасов КАК ДоступныеВидыЗапасов
	|			ПО НаКонецМесяца.ВидЗапасов = ДоступныеВидыЗапасов.ВидЗапасов
	|	ГДЕ
	|		((Не &ПоВсемВидамЗапасов И Не ДоступныеВидыЗапасов.ВидЗапасов ЕСТЬ NULL) ИЛИ &ПоВсемВидамЗапасов)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ // Остатки товаров организации на дату актуальности
	|		ТоварыОрганизаций.Организация КАК Организация,
	|		ТоварыОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ВЫБОР КОГДА ДоступныеВидыЗапасов.ВидЗапасовПродавца.РеализацияЗапасовДругойОрганизации
	|		 И ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Номенклатура.ПодакцизныйТовар ТОГДА
	|			Неопределено
	|		ИНАЧЕ
	|			ЕСТЬNULL(ДоступныеВидыЗапасов.ВидЗапасовПродавца, ТоварыОрганизаций.ВидЗапасов)
	|		КОНЕЦ КАК ВидЗапасов,
	|		ТоварыОрганизаций.ВидЗапасов КАК ВидЗапасовВладельца,
	|		ТоварыОрганизаций.НомерГТД КАК НомерГТД,
	|
	|		0 КАК Количество,
	|		0 КАК НаДатуКонтроля,
	|		ТоварыОрганизаций.КоличествоОстаток КАК НаДатуАктуальности,
	|		ДатыПоступления.ДатаПоступления КАК ДатаПоступления
	|	ИЗ
	|		ОстаткиНаПоследнееДвижение КАК ТоварыОрганизаций
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.ДатыПоступленияТоваровОрганизаций КАК ДатыПоступления
	|		ПО
	|			ТоварыОрганизаций.ВидЗапасов = ДатыПоступления.ВидЗапасов
	|			И ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Номенклатура = ДатыПоступления.Номенклатура
	|			И ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Характеристика = ДатыПоступления.Характеристика
	|			И ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Серия = ДатыПоступления.Серия
	|			И ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Назначение = ДатыПоступления.Назначение
	|			И ТоварыОрганизаций.НомерГТД = ДатыПоступления.НомерГТД
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			ДоступныеВидыЗапасов КАК ДоступныеВидыЗапасов
	|		ПО
	|			ТоварыОрганизаций.ВидЗапасов = ДоступныеВидыЗапасов.ВидЗапасов
	|	ГДЕ
	|		((Не &ПоВсемВидамЗапасов И Не ДоступныеВидыЗапасов.ВидЗапасов ЕСТЬ NULL) ИЛИ &ПоВсемВидамЗапасов)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ // Поступление товаров организаций, отраженные текущим документом.
	|		ТоварыОрганизаций.Организация КАК Организация,
	|		ТоварыОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ТоварыОрганизаций.ВидЗапасов КАК ВидЗапасов,
	|		ТоварыОрганизаций.ВидЗапасов КАК ВидЗапасовВладельца,
	|		ТоварыОрганизаций.НомерГТД КАК НомерГТД,
	|		(-ТоварыОрганизаций.Количество) КАК Количество,
	|		0 КАК НаДатуКонтроля,
	|		0 КАК НаДатуАктуальности,
	|
	|		ДАТАВРЕМЯ(1, 1, 1) КАК ДатаПоступления
	|	ИЗ
	|		РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
	|
	|	ГДЕ
	|		ТоварыОрганизаций.Регистратор = &Ссылка
	|		И ТоварыОрганизаций.Активность
	|		И ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		И (ТоварыОрганизаций.АналитикаУчетаНоменклатуры) В (
	|			ВЫБРАТЬ
	|				ТаблицаТоваров.АналитикаУчетаНоменклатуры
	|			ИЗ
	|				ТаблицаТоваров КАК ТаблицаТоваров
	|			)
	|	//%ДополнительныеВидыЗапасов%
	|) КАК ТоварыОрганизаций
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО
	|		ТоварыОрганизаций.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыОрганизаций.Организация,
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры,
	|	Аналитика.Номенклатура,
	|	Аналитика.Характеристика,
	|	Аналитика.Серия,
	|	Аналитика.Склад,
	|	Аналитика.Назначение,
	|	ВЫБОР КОГДА ТоварыОрганизаций.Организация <> &Организация
	|		ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ТоварыОрганизаций.ВидЗапасов
	|	КОНЕЦ,
	|	ВЫБОР КОГДА ТоварыОрганизаций.Организация <> &Организация
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ТоварыОрганизаций.ВидЗапасовВладельца,
	|	ТоварыОрганизаций.НомерГТД,
	|	ВЫБОР КОГДА ТоварыОрганизаций.ВидЗапасов.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляСделки) ТОГДА
	|		ТоварыОрганизаций.ВидЗапасов.Сделка
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка)
	|	КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|");
	
	Если ДополнительныеСвойства.Свойство("ДополнительныеВидыЗапасов") Тогда
		
		ТекстДополнительныеВидыЗапасов =
		"
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Дополнительно.Организация,
		|	Дополнительно.АналитикаУчетаНоменклатуры,
		|	Дополнительно.ВидЗапасов,
		|	Дополнительно.ВидЗапасов,
		|	Дополнительно.НомерГТД,
		|	Дополнительно.Количество,
		|	0,
		|	0,
		|	Дополнительно.ДатаОтгрузки
		|ИЗ
		|	ДополнительныеВидыЗапасов КАК Дополнительно";
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//%ДополнительныеВидыЗапасов%", ТекстДополнительныеВидыЗапасов);
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПоВсемВидамЗапасов", ПоВсемВидамЗапасов);
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("КонецДня", КонецДня(Дата));
	Запрос.УстановитьПараметр("ГраницаМесяца", Новый Граница(КонецМесяца(Дата), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ГраницаДня", Новый Граница(КонецДня(Дата), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ДатаДокумента", Дата);
	Запрос.УстановитьПараметр("КонтролироватьНаКонецДня", Константы.ВидКонтроляТоваровОрганизаций.Получить() = Перечисления.ВидыКонтроляТоваровОрганизаций.КонецДняКонецМесяцаИДатаПоследнегоДвижения);
	
	ИгнорироватьОперативныеОстатки = ДополнительныеСвойства.Свойство("ИгнорироватьОперативныеОстатки");
	Запрос.УстановитьПараметр("ИспользоватьОстаткиНаКонецМесяца", ?(ИгнорироватьОперативныеОстатки, Истина, Ложь));
	Запрос.УстановитьПараметр("ПерезаполнениеВидовЗапасов", ДополнительныеСвойства.Свойство("ПерезаполнитьВидыЗапасов"));
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Выполнить();
КонецПроцедуры

// Процедура формирует таблицу остатков товаров организаций в разрезе периодов. 
//	Используется для заполнения видов запасов в документах Этап производства.
//
// Параметры:
//	ДокументОбъект - ДокументОбъект.ЭтапПроизводства - Заполняемый документ
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер временных таблиц
//	Отказ - Булево - Признак наличия ошибок
//	ДополнительныеСвойства - Структура - Дополнительные свойства документа
//	ТаблицаОшибок - ТаблицаЗначений - Таблица для отражения ошибок при заполнении видов запасов
//
Процедура ЗаполнитьВидыЗапасовРазныхПериодов(ДокументОбъект, МенеджерВременныхТаблиц, Отказ, ДополнительныеСвойства, ТаблицаОшибок) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ Т.Период КАК Период ИЗ ТаблицаПериодов КАК Т УПОРЯДОЧИТЬ ПО Период");
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Выборка = Запрос.Выполнить().Выбрать();
	
	ТаблицаОстатковТоваровОрганизацийПоПериодам(ДокументОбъект, МенеджерВременныхТаблиц, ДополнительныеСвойства, Истина);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВидыЗапасов.ВидЗапасов                 КАК ВидЗапасов,
	|	ВидыЗапасов.НомерГТД                   КАК НомерГТД,
	|	ВидыЗапасов.Количество                 КАК Количество
	|ПОМЕСТИТЬ ВтПодобранныеВидыЗапасов
	|ИЗ
	|	&ТаблицаВидыЗапасов КАК ВидыЗапасов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВидыЗапасов.ВидЗапасов                 КАК ВидЗапасов,
	|	ВидыЗапасов.НомерГТД                   КАК НомерГТД,
	|	СУММА(ВидыЗапасов.Количество)          КАК Количество
	|ПОМЕСТИТЬ ПодобранныеВидыЗапасов
	|ИЗ
	|	ВтПодобранныеВидыЗапасов КАК ВидыЗапасов
	|
	|СГРУППИРОВАТЬ ПО
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры,
	|	ВидыЗапасов.ВидЗапасов,
	|	ВидыЗапасов.НомерГТД
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры,
	|	ВидЗапасов,
	|	НомерГТД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиПоПериодам.Организация                        КАК Организация,
	|	ОстаткиПоПериодам.АналитикаУчетаНоменклатуры         КАК АналитикаУчетаНоменклатуры,
	|	ОстаткиПоПериодам.ВидЗапасов                         КАК ВидЗапасов,
	|	ОстаткиПоПериодам.НомерГТД                           КАК НомерГТД,
	|	МИНИМУМ(ОстаткиПоПериодам.КоличествоОстаток - ЕСТЬNULL(ВидыЗапасов.Количество, 0)) КАК КоличествоОстаток,
	|	ОстаткиПоПериодам.ДатаПоступления                    КАК ДатаПоступления,
	|	ОстаткиПоПериодам.СкладОтгрузки                      КАК СкладОтгрузки,
	|	ОстаткиПоПериодам.Номенклатура                       КАК Номенклатура,
	|	ОстаткиПоПериодам.Характеристика                     КАК Характеристика,
	|	ОстаткиПоПериодам.Серия                              КАК Серия,
	|	ОстаткиПоПериодам.Склад                              КАК Склад,
	|	ОстаткиПоПериодам.Назначение                         КАК Назначение,
	|	ОстаткиПоПериодам.Сделка                             КАК Сделка,
	|	ОстаткиПоПериодам.ВидЗапасовВладельца                КАК ВидЗапасовВладельца,
	|	ОстаткиПоПериодам.ДокументРеализации                 КАК ДокументРеализации,
	|	ОстаткиПоПериодам.ВидЗапасовПолучателя               КАК ВидЗапасовПолучателя,
	|	ОстаткиПоПериодам.ВидЗапасовОтгрузки                 КАК ВидЗапасовОтгрузки,
	|	ОстаткиПоПериодам.РеализацияЗапасовДругойОрганизации КАК РеализацияЗапасовДругойОрганизации,
	|	ОстаткиПоПериодам.СуммаОстаток                       КАК СуммаОстаток
	|ПОМЕСТИТЬ ТаблицаОстатков
	|ИЗ
	|	ТаблицаОстатковПоПериодам КАК ОстаткиПоПериодам
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПодобранныеВидыЗапасов КАК ВидыЗапасов
	|		ПО ОстаткиПоПериодам.АналитикаУчетаНоменклатуры = ВидыЗапасов.АналитикаУчетаНоменклатуры
	|			И ОстаткиПоПериодам.ВидЗапасов = ВидыЗапасов.ВидЗапасов
	|			И ОстаткиПоПериодам.НомерГТД = ВидыЗапасов.НомерГТД
	|ГДЕ
	|	(ОстаткиПоПериодам.Период >= &ТекущийПериод
	|			ИЛИ ОстаткиПоПериодам.Период = ДАТАВРЕМЯ(1, 1, 1))
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиПоПериодам.СуммаОстаток,
	|	ОстаткиПоПериодам.Характеристика,
	|	ОстаткиПоПериодам.Номенклатура,
	|	ОстаткиПоПериодам.ВидЗапасовОтгрузки,
	|	ОстаткиПоПериодам.НомерГТД,
	|	ОстаткиПоПериодам.СкладОтгрузки,
	|	ОстаткиПоПериодам.ВидЗапасов,
	|	ОстаткиПоПериодам.АналитикаУчетаНоменклатуры,
	|	ОстаткиПоПериодам.Организация,
	|	ОстаткиПоПериодам.РеализацияЗапасовДругойОрганизации,
	|	ОстаткиПоПериодам.ВидЗапасовПолучателя,
	|	ОстаткиПоПериодам.ДокументРеализации,
	|	ОстаткиПоПериодам.Склад,
	|	ОстаткиПоПериодам.Назначение,
	|	ОстаткиПоПериодам.Сделка,
	|	ОстаткиПоПериодам.ВидЗапасовВладельца,
	|	ОстаткиПоПериодам.Серия,
	|	ОстаткиПоПериодам.ДатаПоступления
	|
	|ИМЕЮЩИЕ
	|	МИНИМУМ(ОстаткиПоПериодам.КоличествоОстаток - ЕСТЬNULL(ВидыЗапасов.Количество, 0)) > 0
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки                    КАК НомерСтроки,
	|	ТаблицаТоваров.Номенклатура                   КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика                 КАК Характеристика,
	|	ТаблицаТоваров.Склад                          КАК Склад,
	|	ТаблицаТоваров.Назначение                     КАК Назначение,
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры     КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТоваров.Количество                     КАК Количество,
	|	ТаблицаТоваров.ДатаОтгрузки                   КАК ДатаОтгрузки,
	|	ТаблицаТоваров.ДатаПоступления                КАК ДатаПоступления,
	|	ТаблицаТоваров.ДокументРеализации             КАК ДокументРеализации,
	|	ТаблицаТоваров.Сделка                         КАК Сделка,
	|	ТаблицаТоваров.СтавкаНДС                      КАК СтавкаНДС,
	|	ТаблицаТоваров.СуммаСНДС                      КАК СуммаСНДС,
	|	ТаблицаТоваров.СуммаНДС                       КАК СуммаНДС,
	|	ТаблицаТоваров.СуммаВознаграждения            КАК СуммаВознаграждения,
	|	ТаблицаТоваров.СуммаНДСВознаграждения         КАК СуммаНДСВознаграждения,
	|	ТаблицаТоваров.ГруппаПродукции                КАК ГруппаПродукции,
	|	ТаблицаТоваров.ПодбиратьВидыЗапасов           КАК ПодбиратьВидыЗапасов,
	|	ТаблицаТоваров.НомерГТД                       КАК НомерГТД
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	ТаблицаТоваровПоПериодам КАК ТаблицаТоваров
	|ГДЕ
	|	КОНЕЦПЕРИОДА(ТаблицаТоваров.ДатаОтгрузки, Месяц) = &ТекущийПериод
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки                 КАК НомерСтроки,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры  КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.Номенклатура                КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Характеристика              КАК Характеристика,
	|	ТаблицаВидыЗапасов.Серия                       КАК Серия,
	|	ТаблицаВидыЗапасов.Склад                       КАК Склад,
	|	ТаблицаВидыЗапасов.Назначение                  КАК Назначение,
	|	ТаблицаВидыЗапасов.СкладОтгрузки               КАК СкладОтгрузки,
	|	ТаблицаВидыЗапасов.ДокументРеализации          КАК ДокументРеализации,
	|	ТаблицаВидыЗапасов.ВидЗапасов                  КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.НомерГТД                    КАК НомерГТД,
	|	ТаблицаВидыЗапасов.Количество                  КАК Количество,
	|	ТаблицаВидыЗапасов.Сделка                      КАК Сделка,
	|	ТаблицаВидыЗапасов.ГруппаПродукции             КАК ГруппаПродукции,
	|	ТаблицаВидыЗапасов.ДатаОтгрузки                КАК ДатаОтгрузки,
	|	ТаблицаВидыЗапасов.ВидыЗапасовУказаныВручную   КАК ВидыЗапасовУказаныВручную
	|ПОМЕСТИТЬ ТаблицаВидыЗапасов
	|ИЗ
	|	ТаблицаВидыЗапасовПоПериодам КАК ТаблицаВидыЗапасов
	|ГДЕ
	|	КОНЕЦПЕРИОДА(ТаблицаВидыЗапасов.ДатаОтгрузки, Месяц) = &ТекущийПериод
	|";
	
	ЗапросУничтоженияТаблиц = Новый Запрос();
	ЗапросУничтоженияТаблиц.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ЗапросУничтоженияТаблиц.Текст = "
	|///////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаДокумента
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВтВидыЗапасовСводно
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВтОстатки
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВтПодобранныеВидыЗапасов
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПодобранныеВидыЗапасов
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаОстатков
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаТоваров
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаВидыЗапасов
	|;
	|";
	
	Если ДополнительныеСвойства.ТаблицаВидыЗапасов.Колонки.Найти("Период") = Неопределено Тогда
		ДополнительныеСвойства.ТаблицаВидыЗапасов.Колонки.Добавить("Период",
		Новый ОписаниеТипов("Дата", , , Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
	КонецЕсли;
	
	ТаблицаВидыЗапасов = ДополнительныеСвойства.ТаблицаВидыЗапасов.СкопироватьКолонки();
	
	Пока Выборка.Следующий() Цикл
		
		Запрос.УстановитьПараметр("ТаблицаВидыЗапасов", ТаблицаВидыЗапасов);
		Запрос.УстановитьПараметр("ТекущийПериод",      Выборка.Период);
		Запрос.Выполнить();
		
		ДополнительныеСвойства.ТаблицаВидыЗапасов.Очистить();
		
		ЗапасыСервер.ЗаполнитьВидыЗапасовДокумента(МенеджерВременныхТаблиц, ДополнительныеСвойства, Неопределено, ТаблицаОшибок, Отказ);
		
		ДополнительныеСвойства.ТаблицаВидыЗапасов.ЗаполнитьЗначения(Выборка.Период, "Период");
		
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДополнительныеСвойства.ТаблицаВидыЗапасов, ТаблицаВидыЗапасов);
		
		ЗапросУничтоженияТаблиц.Выполнить();
		
	КонецЦикла;
	
	ДополнительныеСвойства.ТаблицаВидыЗапасов = ТаблицаВидыЗапасов;
	
КонецПроцедуры

// Процедура формирует таблицу остатков товаров, переданных на комиссию.
//
// Параметры:
//	ДокументСсылка - ДокументСсылка - Ссылка на документ, для которого формируется таблица остатков.
//	ОтборОстатков - Структура:
//		* Организация - Справочник.Организации - Организация, для которой получаются остатки
//		* Партнер - Справочник.Партнеры - Парнер, для которого получаются остатки
//		* Соглашение - Справочник.Соглашения - Соглашение, для которого получаются остатки
//		* Дата - Дата - Дата, на которую формируются остатки
//		* Склад - Справочник.Склады - Склад, в разрезе которого формируются остатки
//	ДополнительныеСвойства - Структура:
//		* ИгнорироватьОперативныеОстатки - Булево - Признак контроля оперативных остатков
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер, в который будет помещена временная таблица остатков
//
Процедура ТаблицаОстатковТоваровПереданныхНаКомиссию(ДокументСсылка, ОтборОстатков, ДополнительныеСвойства, МенеджерВременныхТаблиц)Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.ДокументРеализации	
	|ПОМЕСТИТЬ
	|	Основания
	|ИЗ
	|	ТаблицаТоваров КАК Товары
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Движения.АналитикаУчетаНоменклатуры,
	|	Движения.ВидЗапасов,
	|	Движения.НомерГТД,
	|	СУММА(Движения.Количество) КАК Количество
	|ПОМЕСТИТЬ
	|	ПереданныеТовары
	|ИЗ
	|	РегистрНакопления.ТоварыПереданныеНаКомиссию КАК Движения
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Основания КАК Основания
	|		ПО Основания.ДокументРеализации = Движения.Регистратор
	|СГРУППИРОВАТЬ ПО
	|	Движения.АналитикаУчетаНоменклатуры,
	|	Движения.ВидЗапасов,
	|	Движения.НомерГТД
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ // Остатки товаров организации на конец месяца
	|	Остатки.Организация КАК Организация,
	|	Остатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Остатки.Соглашение КАК Соглашение,
	|	Остатки.ВидЗапасов КАК ВидЗапасов,
	|	Остатки.НомерГТД КАК НомерГТД,
	|	Остатки.КоличествоОстаток КАК КоличествоОстаток
	|
	|ПОМЕСТИТЬ НаКонецМесяца
	|ИЗ
	|	РегистрНакопления.ТоварыПереданныеНаКомиссию.Остатки(&Дата,
	|		Организация = &Организация
	|		И Соглашение = &Соглашение
	|		И АналитикаУчетаНоменклатуры В (
	|			ВЫБРАТЬ
	|				Аналитика.КлючАналитики
	|			ИЗ
	|				РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТоваров КАК Товары
	|				ПО Товары.Номенклатура = Аналитика.Номенклатура
	|				И Товары.Характеристика = Аналитика.Характеристика
	|				И Товары.Назначение = Аналитика.Назначение
	|				И Аналитика.Склад = &Партнер
	//++ НЕ УТ 
	|				И ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) = Аналитика.СтатьяКалькуляции
	//-- НЕ УТ
	|				И Товары.ДокументРеализации = НЕОПРЕДЕЛЕНО
	|				)
	|	) КАК Остатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	Остатки.Организация,
	|	Остатки.АналитикаУчетаНоменклатуры,
	|	Остатки.Соглашение,
	|	Остатки.ВидЗапасов,
	|	Остатки.НомерГТД,
	|	Остатки.КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.ТоварыПереданныеНаКомиссию.Остатки(&Дата,
	|		Организация = &Организация
	|		И Соглашение = &Соглашение
	|		И (АналитикаУчетаНоменклатуры, ВидЗапасов, НомерГТД) В (
	|			ВЫБРАТЬ
	|				АналитикаУчетаНоменклатуры, ВидЗапасов, НомерГТД
	|			ИЗ
	|				ПереданныеТовары КАК ПереданныеТовары
	|			)
	|	) КАК Остатки
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ // Остатки товаров организации на дату актуальности
	|	Остатки.Организация КАК Организация,
	|	Остатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Остатки.Соглашение КАК Соглашение,
	|	Остатки.ВидЗапасов КАК ВидЗапасов,
	|	Остатки.НомерГТД КАК НомерГТД,
	|	Остатки.КоличествоОстаток КАК КоличествоОстаток
	|
	|ПОМЕСТИТЬ НаДатуАктуальности
	|ИЗ
	|	РегистрНакопления.ТоварыПереданныеНаКомиссию.Остатки(,
	|		Не &ИспользоватьОстаткиНаКонецМесяца
	|		И Организация = &Организация
	|		И Соглашение = &Соглашение
	|		И АналитикаУчетаНоменклатуры В (
	|			ВЫБРАТЬ
	|				Аналитика.КлючАналитики
	|			ИЗ
	|				РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТоваров КАК Товары
	|				ПО Товары.Номенклатура = Аналитика.Номенклатура
	|				И Товары.Характеристика = Аналитика.Характеристика
	|				И Товары.Назначение = Аналитика.Назначение
	|				И Аналитика.Склад = &Партнер
	//++ НЕ УТ 
	|				И ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) = Аналитика.СтатьяКалькуляции
	//-- НЕ УТ
	|				И Товары.ДокументРеализации = НЕОПРЕДЕЛЕНО
	|				)
	|	) КАК Остатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	Остатки.Организация,
	|	Остатки.АналитикаУчетаНоменклатуры,
	|	Остатки.Соглашение,
	|	Остатки.ВидЗапасов,
	|	Остатки.НомерГТД,
	|	(ВЫБОР
	|		КОГДА ПереданныеТовары.Количество < Остатки.КоличествоОстаток ТОГДА ПереданныеТовары.Количество
	|		ИНАЧЕ Остатки.КоличествоОстаток КОНЕЦ) КАК КоличествоОстаток
	|ИЗ
	|	РегистрНакопления.ТоварыПереданныеНаКомиссию.Остатки(,
	|		Не &ИспользоватьОстаткиНаКонецМесяца
	|		И Организация = &Организация
	|		И Соглашение = &Соглашение
	|		И (АналитикаУчетаНоменклатуры, ВидЗапасов, НомерГТД) В (
	|			ВЫБРАТЬ
	|				АналитикаУчетаНоменклатуры, ВидЗапасов, НомерГТД
	|			ИЗ
	|				ПереданныеТовары КАК ПереданныеТовары
	|			)
	|	) КАК Остатки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПереданныеТовары КАК ПереданныеТовары
	|		ПО ПереданныеТовары.АналитикаУчетаНоменклатуры = Остатки.АналитикаУчетаНоменклатуры
	|		И ПереданныеТовары.ВидЗапасов = Остатки.ВидЗапасов
	|		И ПереданныеТовары.НомерГТД = Остатки.НомерГТД
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	АналитикаУчетаНоменклатуры,
	|	Соглашение,
	|	ВидЗапасов,
	|	НомерГТД
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыОрганизаций.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК СкладОтгрузки,
	|	ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка) КАК Сделка,
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Аналитика.Номенклатура КАК Номенклатура,
	|	Аналитика.Характеристика КАК Характеристика,
	|	Аналитика.Серия КАК Серия,
	|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
	|	(ВЫБОР
	|		КОГДА &ЭтоВозврат И Не ТоварыОрганизаций.КВозвратуМеждуОрганизациями
	|	 	 И ТоварыОрганизаций.ВидЗапасов.РеализацияЗапасовДругойОрганизации
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
	|		ИНАЧЕ ТоварыОрганизаций.ВидЗапасов КОНЕЦ) КАК ВидЗапасов,
	|	(ВЫБОР 
	|		КОГДА &ЭтоВозврат И Не ТоварыОрганизаций.КВозвратуМеждуОрганизациями
	|	 	 И ТоварыОрганизаций.ВидЗапасов.РеализацияЗапасовДругойОрганизации
	|			ТОГДА ТоварыОрганизаций.ВидЗапасов.ВидЗапасовВладельца
	|		ИНАЧЕ ТоварыОрганизаций.ВидЗапасов КОНЕЦ) КАК ВидЗапасовВладельца,
	|	Неопределено КАК ВидЗапасовПолучателя,
	|	(ВЫБОР КОГДА &ЭтоВозврат ТОГДА ТоварыОрганизаций.ВидЗапасов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ) КАК ВидЗапасовОтгрузки,
	|	(ВЫБОР
	|		КОГДА &ЭтоВозврат И ТоварыОрганизаций.КВозвратуМеждуОрганизациями
	|			ТОГДА ТоварыОрганизаций.ВидЗапасов.РеализацияЗапасовДругойОрганизации
	|		ИНАЧЕ ЛОЖЬ КОНЕЦ) КАК РеализацияЗапасовДругойОрганизации,
	|	Аналитика.Назначение КАК Назначение,
	|	ТоварыОрганизаций.НомерГТД КАК НомерГТД,
	|	ТоварыОрганизаций.Знак,
	|	МАКСИМУМ(ТоварыОрганизаций.ДатаПоступления) КАК ДатаПоступления,
	|	СУММА(ТоварыОрганизаций.Количество) КАК КоличествоОстаток,
	|	0 КАК СуммаОстаток
	|
	|ПОМЕСТИТЬ ТаблицаОстатков
	|ИЗ (
	|	ВЫБРАТЬ // ВидыЗапасов, списанные документом Отчет комиссионера
	|		ТоварыПереданныеНаКомиссию.Организация КАК Организация,
	|		ТоварыПереданныеНаКомиссию.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ТоварыПереданныеНаКомиссию.ВидЗапасов КАК ВидЗапасов,
	|		ТоварыПереданныеНаКомиссию.НомерГТД КАК НомерГТД,
	|		ТоварыПереданныеНаКомиссию.Количество КАК Количество,
	|		0 КАК Знак,
	|		ДатыПередачиТоваровНаКомиссию.ДатаПередачи КАК ДатаПоступления,
	|		ЕСТЬNULL(Настройка.ИспользоватьПриВозвратеОтКлиента, Ложь) КАК КВозвратуМеждуОрганизациями
	|	ИЗ
	|		РегистрНакопления.ТоварыПереданныеНаКомиссию КАК ТоварыПереданныеНаКомиссию
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|		ПО
	|			ТоварыПереданныеНаКомиссию.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаПередачиТоваровМеждуОрганизациями КАК Настройка
	|			ПО Настройка.ОрганизацияВладелец = ТоварыПереданныеНаКомиссию.ВидЗапасов.ВидЗапасовВладельца.Организация
	|			И Настройка.ТипЗапасов = ТоварыПереданныеНаКомиссию.ВидЗапасов.ВидЗапасовВладельца.ТипЗапасов
	|			И Настройка.ОрганизацияПродавец = &Организация
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.ДатыПередачиТоваровНаКомиссию КАК ДатыПередачиТоваровНаКомиссию
	|		ПО
	|			ТоварыПереданныеНаКомиссию.ВидЗапасов = ДатыПередачиТоваровНаКомиссию.ВидЗапасов
	|			И ТоварыПереданныеНаКомиссию.АналитикаУчетаНоменклатуры = ДатыПередачиТоваровНаКомиссию.АналитикаУчетаНоменклатуры
	|			И ТоварыПереданныеНаКомиссию.НомерГТД = ДатыПередачиТоваровНаКомиссию.НомерГТД
	|			И ТоварыПереданныеНаКомиссию.Соглашение = ДатыПередачиТоваровНаКомиссию.Соглашение
	|			И ТоварыПереданныеНаКомиссию.Организация = ДатыПередачиТоваровНаКомиссию.Организация
	|	ГДЕ
	|		ТоварыПереданныеНаКомиссию.Период <= &Дата
	|		И ТоварыПереданныеНаКомиссию.Регистратор = &Ссылка
	|		И ТоварыПереданныеНаКомиссию.Активность
	|		И ТоварыПереданныеНаКомиссию.Организация = &Организация
	|		И Аналитика.Склад = &Партнер
	|		И ТоварыПереданныеНаКомиссию.Соглашение = &Соглашение
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ // ВидыЗапасов, переданные и возвращенные между организациями
	|		ТоварыПереданныеНаКомиссию.Организация КАК Организация,
	|		ТоварыПереданныеНаКомиссию.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ТоварыПереданныеНаКомиссию.ВидЗапасов КАК ВидЗапасов,
	|		ТоварыПереданныеНаКомиссию.НомерГТД КАК НомерГТД,
	|		(ВЫБОР
	|			КОГДА ТоварыПереданныеНаКомиссию.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА -ТоварыПереданныеНаКомиссию.Количество
	|			ИНАЧЕ ТоварыПереданныеНаКомиссию.Количество КОНЕЦ) КАК Количество,
	|		(ВЫБОР
	|			КОГДА ТоварыПереданныеНаКомиссию.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -1
	|			ИНАЧЕ 1 КОНЕЦ) КАК Знак,
	|		ДатыПередачиТоваровНаКомиссию.ДатаПередачи КАК ДатаПоступления,
	|		ЕСТЬNULL(Настройка.ИспользоватьПриВозвратеОтКлиента, Ложь) КАК КВозвратуМеждуОрганизациями
	|	ИЗ
	|		РегистрНакопления.ТоварыПереданныеНаКомиссию КАК ТоварыПереданныеНаКомиссию
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|			ПО ТоварыПереданныеНаКомиссию.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаПередачиТоваровМеждуОрганизациями КАК Настройка
	|			ПО Настройка.ОрганизацияВладелец = ТоварыПереданныеНаКомиссию.ВидЗапасов.ВидЗапасовВладельца.Организация
	|			И Настройка.ТипЗапасов = ТоварыПереданныеНаКомиссию.ВидЗапасов.ВидЗапасовВладельца.ТипЗапасов
	|			И Настройка.ОрганизацияПродавец = &Организация
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыПередачиТоваровНаКомиссию КАК ДатыПередачиТоваровНаКомиссию
	|			ПО ТоварыПереданныеНаКомиссию.ВидЗапасов = ДатыПередачиТоваровНаКомиссию.ВидЗапасов
	|			И ТоварыПереданныеНаКомиссию.АналитикаУчетаНоменклатуры = ДатыПередачиТоваровНаКомиссию.АналитикаУчетаНоменклатуры
	|			И ТоварыПереданныеНаКомиссию.НомерГТД = ДатыПередачиТоваровНаКомиссию.НомерГТД
	|			И ТоварыПереданныеНаКомиссию.Соглашение = ДатыПередачиТоваровНаКомиссию.Соглашение
	|			И ТоварыПереданныеНаКомиссию.Организация = ДатыПередачиТоваровНаКомиссию.Организация
	|	ГДЕ
	|		&ЭтоОтчетПоКомиссии
	|		И ТоварыПереданныеНаКомиссию.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|		И ТоварыПереданныеНаКомиссию.Регистратор <> &Ссылка
	|		И ТоварыПереданныеНаКомиссию.Активность
	|		И ТоварыПереданныеНаКомиссию.Организация = &Организация
	|		И Аналитика.Склад = &Партнер
	|		И ТоварыПереданныеНаКомиссию.Соглашение = &Соглашение
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ // ВидыЗапасов, переданные на комисию
	|		НаКонецМесяца.Организация КАК Организация,
	|		НаКонецМесяца.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		НаКонецМесяца.ВидЗапасов КАК ВидЗапасов,
	|		НаКонецМесяца.НомерГТД КАК НомерГТД,
	|		ВЫБОР КОГДА Не &ИспользоватьОстаткиНаКонецМесяца
	|		 И НаКонецМесяца.КоличествоОстаток >=  ЕСТЬNULL(НаДатуАктуальности.КоличествоОстаток, 0) ТОГДА
	|			ЕСТЬNULL(НаДатуАктуальности.КоличествоОстаток, 0)
	|		ИНАЧЕ
	|			НаКонецМесяца.КоличествоОстаток
	|		КОНЕЦ КАК Количество,
	|		0 КАК Знак,
	|		ДатыПередачиТоваровНаКомиссию.ДатаПередачи КАК ДатаПоступления,
	|		ЕСТЬNULL(Настройка.ИспользоватьПриВозвратеОтКлиента, Ложь) КАК КВозвратуМеждуОрганизациями
	|	ИЗ
	|		НаКонецМесяца КАК НаКонецМесяца
	|		ЛЕВОЕ СОЕДИНЕНИЕ НаДатуАктуальности КАК НаДатуАктуальности
	|			ПО НаДатуАктуальности.Организация = НаКонецМесяца.Организация
	|			И НаДатуАктуальности.АналитикаУчетаНоменклатуры = НаКонецМесяца.АналитикаУчетаНоменклатуры
	|			И НаДатуАктуальности.Соглашение = НаКонецМесяца.Соглашение
	|			И НаДатуАктуальности.ВидЗапасов = НаКонецМесяца.ВидЗапасов
	|			И НаДатуАктуальности.НомерГТД = НаКонецМесяца.НомерГТД
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаПередачиТоваровМеждуОрганизациями КАК Настройка
	|			ПО Настройка.ОрганизацияВладелец = НаКонецМесяца.ВидЗапасов.ВидЗапасовВладельца.Организация
	|			И Настройка.ТипЗапасов = НаКонецМесяца.ВидЗапасов.ВидЗапасовВладельца.ТипЗапасов
	|			И Настройка.ОрганизацияПродавец = &Организация
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.ДатыПередачиТоваровНаКомиссию КАК ДатыПередачиТоваровНаКомиссию
	|		ПО
	|			НаКонецМесяца.ВидЗапасов = ДатыПередачиТоваровНаКомиссию.ВидЗапасов
	|			И НаКонецМесяца.АналитикаУчетаНоменклатуры = ДатыПередачиТоваровНаКомиссию.АналитикаУчетаНоменклатуры
	|			И НаКонецМесяца.НомерГТД = ДатыПередачиТоваровНаКомиссию.НомерГТД
	|			И НаКонецМесяца.Соглашение = ДатыПередачиТоваровНаКомиссию.Соглашение
	|			И НаКонецМесяца.Организация = ДатыПередачиТоваровНаКомиссию.Организация
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ // Возвратная тара, списанная документом
	|		Движения.ВидЗапасов.Организация КАК Организация,
	|		Аналитика.КлючАналитики КАК АналитикаУчетаНоменклатуры,
	|		Движения.ВидЗапасов КАК ВидЗапасов,
	|		Движения.НомерГТД КАК НомерГТД,
	|		Движения.Количество КАК Количество,
	|		0 КАК Знак,
	|		Движения.ДокументПередачи.Дата КАК ДатаПоступления,
	|		ЕСТЬNULL(Настройка.ИспользоватьПриВозвратеОтКлиента, Ложь) КАК КВозвратуМеждуОрганизациями
	|	ИЗ
	|		РегистрНакопления.ПереданнаяВозвратнаяТара КАК Движения
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|		ПО
	|			Движения.Номенклатура = Аналитика.Номенклатура
	|			И Движения.Характеристика = Аналитика.Характеристика
	|			И Движения.Партнер = Аналитика.Склад
	|			И Аналитика.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			И Аналитика.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	//++ НЕ УТ 
	|			И ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) = Аналитика.СтатьяКалькуляции
	//-- НЕ УТ
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаПередачиТоваровМеждуОрганизациями КАК Настройка
	|			ПО Настройка.ОрганизацияВладелец = Движения.ВидЗапасов.ВидЗапасовВладельца.Организация
	|			И Настройка.ТипЗапасов = Движения.ВидЗапасов.ВидЗапасовВладельца.ТипЗапасов
	|			И Настройка.ОрганизацияПродавец = &Организация
	|	ГДЕ
	|		Движения.Регистратор = &Ссылка
	|		И Движения.Активность
	|		И Движения.ВидЗапасов.Организация = &Организация
	|		И Движения.Партнер = &Партнер
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ // Переданная возвратная тара
	|		Остатки.ВидЗапасов.Организация КАК Организация,
	|		Аналитика.КлючАналитики КАК АналитикаУчетаНоменклатуры,
	|		Остатки.ВидЗапасов КАК ВидЗапасов,
	|		Остатки.НомерГТД КАК НомерГТД,
	|		Остатки.КоличествоОстаток КАК Количество,
	|		0 КАК Знак,
	|		ДокументПередачи.Дата КАК ДатаПоступления,
	|		ЕСТЬNULL(Настройка.ИспользоватьПриВозвратеОтКлиента, Ложь) КАК КВозвратуМеждуОрганизациями
	|	ИЗ
	|		РегистрНакопления.ПереданнаяВозвратнаяТара.Остатки(&Дата,
	|			ВидЗапасов.Организация = &Организация
	|			И Партнер = &Партнер
	|			И (Номенклатура, Характеристика) В (
	|				ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					Аналитика.Номенклатура,
	|					Аналитика.Характеристика
	|				ИЗ
	|					ТаблицаТоваров КАК ТаблицаТоваров
	|				
	|					ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|						Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|					ПО
	|						ТаблицаТоваров.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|				ГДЕ
	|					Аналитика.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|				)
	|		) КАК Остатки
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|		ПО
	|			Остатки.Номенклатура = Аналитика.Номенклатура
	|			И Остатки.Характеристика = Аналитика.Характеристика
	|			И Остатки.Партнер = Аналитика.Склад
	|			И Аналитика.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|			И Аналитика.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	//++ НЕ УТ 
	|			И ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) = Аналитика.СтатьяКалькуляции
	//-- НЕ УТ

	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаПередачиТоваровМеждуОрганизациями КАК Настройка
	|			ПО Настройка.ОрганизацияВладелец = Остатки.ВидЗапасов.ВидЗапасовВладельца.Организация
	|			И Настройка.ТипЗапасов = Остатки.ВидЗапасов.ВидЗапасовВладельца.ТипЗапасов
	|			И Настройка.ОрганизацияПродавец = &Организация
	|
	|	) КАК ТоварыОрганизаций
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|	ПО
	|		ТоварыОрганизаций.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыОрганизаций.Организация,
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры,
	|	Аналитика.Номенклатура,
	|	Аналитика.Характеристика,
	|	Аналитика.Назначение,
	|	Аналитика.Серия,
	|	(ВЫБОР
	|		КОГДА &ЭтоВозврат И Не ТоварыОрганизаций.КВозвратуМеждуОрганизациями
	|	 	 И ТоварыОрганизаций.ВидЗапасов.РеализацияЗапасовДругойОрганизации
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
	|		ИНАЧЕ ТоварыОрганизаций.ВидЗапасов КОНЕЦ),
	|	(ВЫБОР 
	|		КОГДА &ЭтоВозврат И Не ТоварыОрганизаций.КВозвратуМеждуОрганизациями
	|	 	 И ТоварыОрганизаций.ВидЗапасов.РеализацияЗапасовДругойОрганизации
	|			ТОГДА ТоварыОрганизаций.ВидЗапасов.ВидЗапасовВладельца
	|		ИНАЧЕ ТоварыОрганизаций.ВидЗапасов КОНЕЦ),
	|	(ВЫБОР КОГДА &ЭтоВозврат ТОГДА ТоварыОрганизаций.ВидЗапасов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ),
	|	(ВЫБОР
	|		КОГДА &ЭтоВозврат И ТоварыОрганизаций.КВозвратуМеждуОрганизациями
	|			ТОГДА ТоварыОрганизаций.ВидЗапасов.РеализацияЗапасовДругойОрганизации
	|		ИНАЧЕ ЛОЖЬ КОНЕЦ),
	|	ТоварыОрганизаций.НомерГТД,
	|	ТоварыОрганизаций.Знак
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|");
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Организация", ОтборОстатков.Организация);
	Запрос.УстановитьПараметр("Партнер", ОтборОстатков.Партнер);
	Запрос.УстановитьПараметр("Соглашение", ОтборОстатков.Соглашение);
	Запрос.УстановитьПараметр("Склад", ОтборОстатков.Склад);
	Запрос.УстановитьПараметр("Дата", ОтборОстатков.Дата + 1);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(ОтборОстатков.Дата));
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(ОтборОстатков.Дата));
	Запрос.УстановитьПараметр("ЭтоВозврат", ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВозвратТоваровОтКлиента"));
	Запрос.УстановитьПараметр("ЭтоОтчетПоКомиссии", ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями"));
	
	ИгнорироватьОперативныеОстатки = ДополнительныеСвойства.Свойство("ИгнорироватьОперативныеОстатки");
	Запрос.УстановитьПараметр("ИспользоватьОстаткиНаКонецМесяца", ?(ИгнорироватьОперативныеОстатки, Истина, Ложь));
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
КонецПроцедуры

// Процедура формирует таблицу остатков товаров к оформлению отчетов комитенту.
//
// Параметры:
//	ДокументСсылка - ДокументСсылка - Документ, для которого формируются остатки
//	Дата - Дата - Дата, на которую формируются остатки.
//	Валюта - СправочникСсылка.Валюты - Валюта остатков.
//	ЭтоОтчетКомитентуОСписании - Булево - Признак того, что это отчет комитенту о списании
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Временные таблицы, в которые будет помещена ТаблицаОстатков.
//
Процедура ТаблицаОстатковКОформлениюОтчетаКомитенту(ДокументСсылка, Дата, Валюта, ЭтоОтчетКомитентуОСписании, МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос("
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыОрганизаций.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК СкладОтгрузки,
	|	ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка) КАК Сделка,
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Аналитика.Номенклатура КАК Номенклатура,
	|	Аналитика.Характеристика КАК Характеристика,
	|	Аналитика.Серия КАК Серия,
	|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
	|	ТоварыОрганизаций.ВидЗапасов КАК ВидЗапасов,
	|	ТоварыОрганизаций.ВидЗапасов КАК ВидЗапасовВладельца,
	|	Неопределено КАК ВидЗапасовПолучателя,
	|	Неопределено КАК ВидЗапасовОтгрузки,
	|	Ложь КАК РеализацияЗапасовДругойОрганизации,
	|	Аналитика.Назначение КАК Назначение,
	|	ТоварыОрганизаций.НомерГТД КАК НомерГТД,
	|	МАКСИМУМ(ТоварыОрганизаций.ДатаПоступления) КАК ДатаПоступления,
	|	СУММА(ТоварыОрганизаций.Количество) КАК КоличествоОстаток,
	|	СУММА(ТоварыОрганизаций.СуммаВыручки) КАК СуммаОстаток
	|
	|ПОМЕСТИТЬ ТаблицаОстатков
	|ИЗ (
	|	ВЫБРАТЬ // ВидыЗапасов, списанные документом
	|		ТоварыКОформлению.ВидЗапасов.Организация КАК Организация,
	|		ТоварыКОформлению.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ТоварыКОформлению.ВидЗапасов КАК ВидЗапасов,
	|		ТоварыКОформлению.НомерГТД КАК НомерГТД,
	|		ВЫБОР КОГДА &ЭтоОтчетКомитентуОСписании ТОГДА
	|			ТоварыКОформлению.КоличествоСписано
	|		ИНАЧЕ
	|			ТоварыКОформлению.Количество
	|		КОНЕЦ КАК Количество,
	|		ВЫБОР КОГДА &ЭтоОтчетКомитентуОСписании ТОГДА
	|			0
	|		ИНАЧЕ
	|			ТоварыКОформлению.СуммаВыручки
	|		КОНЕЦ КАК СуммаВыручки,
	|
	|		ДатыПередачиТоваровНаКомиссию.ДатаПередачи КАК ДатаПоступления
	|	ИЗ
	|		РегистрНакопления.ТоварыКОформлениюОтчетовКомитенту КАК ТоварыКОформлению
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.ДатыПередачиТоваровНаКомиссию КАК ДатыПередачиТоваровНаКомиссию
	|		ПО
	|			ТоварыКОформлению.ВидЗапасов = ДатыПередачиТоваровНаКомиссию.ВидЗапасов
	|			И ТоварыКОформлению.АналитикаУчетаНоменклатуры = ДатыПередачиТоваровНаКомиссию.АналитикаУчетаНоменклатуры
	|			И ТоварыКОформлению.НомерГТД = ДатыПередачиТоваровНаКомиссию.НомерГТД
	|			И ТоварыКОформлению.ВидЗапасов.Соглашение = ДатыПередачиТоваровНаКомиссию.Соглашение
	|			И ТоварыКОформлению.ВидЗапасов.Организация = ДатыПередачиТоваровНаКомиссию.Организация
	|	ГДЕ
	|		ТоварыКОформлению.Регистратор = &Ссылка
	|		И ТоварыКОформлению.Активность
	|		И ТоварыКОформлению.ВидЗапасов В (
	|			ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				ДоступныеВидыЗапасов.ВидЗапасов
	|			ИЗ
	|				ДоступныеВидыЗапасов КАК ДоступныеВидыЗапасов
	|			)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ // Реализованные виды запасов
	|		ТоварыКОформлению.ВидЗапасов.Организация КАК Организация,
	|		ТоварыКОформлению.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ТоварыКОформлению.ВидЗапасов КАК ВидЗапасов,
	|		ТоварыКОформлению.НомерГТД КАК НомерГТД,
	|		ВЫБОР КОГДА &ЭтоОтчетКомитентуОСписании ТОГДА
	|			ТоварыКОформлению.КоличествоСписаноОстаток
	|		ИНАЧЕ
	|			ТоварыКОформлению.КоличествоОстаток
	|		КОНЕЦ КАК Количество,
	|
	|		ВЫБОР КОГДА &ЭтоОтчетКомитентуОСписании ТОГДА
	|			0
	|		ИНАЧЕ
	|			ТоварыКОформлению.СуммаВыручкиОстаток
	|		КОНЕЦ КАК СуммаВыручки,
	|
	|		ДатыПередачиТоваровНаКомиссию.ДатаПередачи КАК ДатаПоступления
	|	ИЗ
	|		РегистрНакопления.ТоварыКОформлениюОтчетовКомитенту.Остатки(&Дата,
	|			Валюта = &Валюта
	|			И ВидЗапасов В (
	|				ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					ДоступныеВидыЗапасов.ВидЗапасов
	|				ИЗ
	|					ДоступныеВидыЗапасов КАК ДоступныеВидыЗапасов
	|				)
	|			И (АналитикаУчетаНоменклатуры) В (
	|				ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					ТаблицаТоваров.АналитикаУчетаНоменклатуры
	|				ИЗ
	|					ТаблицаТоваров КАК ТаблицаТоваров
	|				)
	|		) КАК ТоварыКОформлению
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.ДатыПередачиТоваровНаКомиссию КАК ДатыПередачиТоваровНаКомиссию
	|		ПО
	|			ТоварыКОформлению.ВидЗапасов = ДатыПередачиТоваровНаКомиссию.ВидЗапасов
	|			И ТоварыКОформлению.АналитикаУчетаНоменклатуры = ДатыПередачиТоваровНаКомиссию.АналитикаУчетаНоменклатуры
	|			И ТоварыКОформлению.НомерГТД = ДатыПередачиТоваровНаКомиссию.НомерГТД
	|			И ТоварыКОформлению.ВидЗапасов.Соглашение = ДатыПередачиТоваровНаКомиссию.Соглашение
	|			И ТоварыКОформлению.ВидЗапасов.Организация = ДатыПередачиТоваровНаКомиссию.Организация
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ // Виды запасов, указанные в документе вручную.
	|		Запасы.ВидЗапасов.Организация КАК Организация,
	|		Запасы.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		Запасы.ВидЗапасов КАК ВидЗапасов,
	|		Запасы.НомерГТД КАК НомерГТД,
	|		Запасы.Количество КАК Количество,
	|		Запасы.СуммаСНДС КАК СуммаВыручки,
	|		ДАТАВРЕМЯ(1,1,1) КАК ДатаПоступления
	|	ИЗ
	|		ТаблицаВидыЗапасов КАК Запасы
	|	ГДЕ
	|		Запасы.ВидыЗапасовУказаныВручную
	|		И (Запасы.АналитикаУчетаНоменклатуры) В (
	|			ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				ТаблицаТоваров.АналитикаУчетаНоменклатуры
	|			ИЗ
	|				ТаблицаТоваров КАК ТаблицаТоваров
	|			)
	|
	|	) КАК ТоварыОрганизаций
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО ТоварыОрганизаций.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыОрганизаций.Организация,
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры,
	|	Аналитика.Номенклатура,
	|	Аналитика.Характеристика,
	|	Аналитика.Назначение,
	|	Аналитика.Серия,
	|	ТоварыОрганизаций.ВидЗапасов,
	|	ТоварыОрганизаций.НомерГТД
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|");
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Валюта", Валюта);
	Запрос.УстановитьПараметр("ЭтоОтчетКомитентуОСписании", ЭтоОтчетКомитентуОСписании);
	Запрос.УстановитьПараметр("Дата", КонецДня(Дата) + 1);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Выполнить();
	
КонецПроцедуры 

// Формирование врем.табл. ТаблицаОстатков к передаче или к возврату в зависимости от вида ссылки.
// Параметры:
//	Ссылка - ДокументСсылка.{ПередачаТоваровМеждуОрганизациями | ВозвратТоваровМеждуОрганизациями} - Ссылка на документ
//	Владелец - СправочникСсылка.Организации - Владелец передаваемого/возвращаемого запаса
//	Склад - СправочникСсылка.Склады - Склад подбора остатков в операции
//	Дата - Дата - Дата подбора остатков
//	ВременныеТаблицы - МенеджерВременныхТаблиц - менеджер временных таблиц для ТаблицаОстатков, должен содержать ВТ ДоступныеВидыЗапасов
//	ПоВсемВидамЗапасов - Булево - Признак игнорирования доступных видов запасов
Процедура ТаблицаОстатковТоваровКПередаче(Ссылка, Владелец, Склад, Дата, ВременныеТаблицы, ПоВсемВидамЗапасов = Ложь) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	КПередаче.Организация КАК Организация,
	|	КПередаче.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Аналитика.Номенклатура КАК Номенклатура,
	|	Аналитика.Характеристика КАК Характеристика,
	|	Аналитика.Серия КАК Серия,
	|	Аналитика.Склад КАК Склад,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК СкладОтгрузки,
	|	ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка) КАК Сделка,
	|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
	|
	|	ВЫБОР КОГДА &ПередачаТоваров ТОГДА
	|		ВидыЗапасов.ВидЗапасовВладельца
	|	ИНАЧЕ
	|		КПередаче.ВидЗапасовПродавца
	|	КОНЕЦ КАК ВидЗапасов,
	|
	|	ВидыЗапасов.ВидЗапасовВладельца КАК ВидЗапасовВладельца,
	|
	|	ВЫБОР КОГДА &ПередачаТоваров ТОГДА
	|		КПередаче.ВидЗапасовПродавца
	|	ИНАЧЕ
	|		ВидыЗапасов.ВидЗапасовВладельца
	|	КОНЕЦ КАК ВидЗапасовПолучателя,
	|
	|	Неопределено КАК ВидЗапасовОтгрузки,
	|	Ложь КАК РеализацияЗапасовДругойОрганизации,
	|	Аналитика.Назначение КАК Назначение,
	|	КПередаче.НомерГТД КАК НомерГТД,
	|	МАКСИМУМ(КПередаче.ДатаПоступления) КАК ДатаПоступления,
	|	СУММА(КПередаче.Количество) КАК КоличествоОстаток,
	|	0 КАК НаДатуКонтроля,
	|	0 КАК НаДатуАктуальности,
	|	0 КАК СуммаОстаток
	|
	|ПОМЕСТИТЬ ТаблицаОстатков
	|ИЗ (
	|	ВЫБРАТЬ // ВидыЗапасов, переданные документом Передача товаров между организациями
	|		КПередаче.ОрганизацияВладелец КАК Организация,
	|		КПередаче.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		КПередаче.ВидЗапасовПродавца.ВидЗапасовВладельца КАК ВидЗапасов,
	|		КПередаче.ВидЗапасовПродавца КАК ВидЗапасовПродавца,
	|		КПередаче.НомерГТД КАК НомерГТД,
	|		ВЫБОР КОГДА &ПередачаТоваров ТОГДА
	|			КПередаче.Количество
	|		ИНАЧЕ
	|			КПередаче.Возвращено
	|		КОНЕЦ КАК Количество,
	|		ДАТАВРЕМЯ(1, 1, 1) КАК ДатаПоступления
	|	ИЗ
	|		РегистрНакопления.ТоварыОрганизацийКПередаче КАК КПередаче
	|	ГДЕ
	|		КПередаче.Регистратор = &Ссылка
	|		И КПередаче.Активность
	|		И (Не &ПоВсемВидамЗапасов И КПередаче.ВидЗапасовПродавца В (
	|			ВЫБРАТЬ
	|				Доступные.ВидЗапасовПродавца
	|			ИЗ
	|				ДоступныеВидыЗапасов КАК Доступные
	|			)
	|			ИЛИ &ПоВсемВидамЗапасов
	|		)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ // ВидыЗапасов, указанные в документе
	|		КПередаче.ОрганизацияВладелец КАК Организация,
	|		КПередаче.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		КПередаче.ВидЗапасовПродавца.ВидЗапасовВладельца КАК ВидЗапасов,
	|		КПередаче.ВидЗапасовПродавца КАК ВидЗапасовПродавца,
	|		КПередаче.НомерГТД КАК НомерГТД,
	|		ВЫБОР КОГДА &ПередачаТоваров ТОГДА
	|			КПередаче.КоличествоОстаток
	|		ИНАЧЕ
	|			КПередаче.ВозвращеноОстаток
	|		КОНЕЦ КАК Количество,
	|		ДАТАВРЕМЯ(1, 1, 1) КАК ДатаПоступления
	|	ИЗ
	|		РегистрНакопления.ТоварыОрганизацийКПередаче.Остатки(&Дата,
	|			ОрганизацияВладелец = &Владелец
	|			И (АналитикаУчетаНоменклатуры, ВидЗапасовПродавца) В (
	|				ВЫБРАТЬ
	|					ВидыЗапасов.АналитикаУчетаНоменклатуры,
	|					ВидыЗапасов.ВидЗапасовПолучателя
	|				ИЗ
	|					ТаблицаВидыЗапасов КАК ВидыЗапасов
	|				)
	|		) КАК КПередаче
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ // ВидыЗапасов, проданные другими оранизациями
	|		КПередаче.ОрганизацияВладелец КАК Организация,
	|		КПередаче.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		КПередаче.ВидЗапасовПродавца.ВидЗапасовВладельца КАК ВидЗапасов,
	|		КПередаче.ВидЗапасовПродавца КАК ВидЗапасовПродавца,
	|		КПередаче.НомерГТД КАК НомерГТД,
	|		ВЫБОР КОГДА &ПередачаТоваров ТОГДА
	|			КПередаче.КоличествоОстаток
	|		ИНАЧЕ
	|			КПередаче.ВозвращеноОстаток
	|		КОНЕЦ КАК Количество,
	|		ДАТАВРЕМЯ(1, 1, 1) КАК ДатаПоступления
	|	ИЗ
	|		РегистрНакопления.ТоварыОрганизацийКПередаче.Остатки(&Дата,
	|			ОрганизацияВладелец = &Владелец
	|			И АналитикаУчетаНоменклатуры В (
	|				ВЫБРАТЬ
	|					ТаблицаТоваров.АналитикаУчетаНоменклатуры
	|				ИЗ
	|					ТаблицаТоваров КАК ТаблицаТоваров
	|				)
	|			И (Не &ПоВсемВидамЗапасов И ВидЗапасовПродавца В (
	|				ВЫБРАТЬ
	|					Доступные.ВидЗапасовПродавца
	|				ИЗ
	|					ДоступныеВидыЗапасов КАК Доступные
	|				)
	|				ИЛИ &ПоВсемВидамЗапасов
	|			)
	|		) КАК КПередаче
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ТаблицаВидыЗапасов КАК ВидыЗапасов
	|	ПО
	|		КПередаче.АналитикаУчетаНоменклатуры = ВидыЗапасов.АналитикаУчетаНоменклатуры
	|		И КПередаче.ВидЗапасовПродавца = ВидыЗапасов.ВидЗапасовПолучателя
	|	ГДЕ
	|		ВидыЗапасов.ВидЗапасов ЕСТЬ NULL
	|
	|) КАК КПередаче
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.ВидыЗапасов КАК ВидыЗапасов
	|	ПО
	|		ВидыЗапасов.Ссылка = КПередаче.ВидЗапасовПродавца
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО
	|		КПередаче.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|
	|СГРУППИРОВАТЬ ПО
	|	КПередаче.Организация,
	|	КПередаче.АналитикаУчетаНоменклатуры,
	|	Аналитика.Номенклатура,
	|	Аналитика.Характеристика,
	|	Аналитика.Назначение,
	|	Аналитика.Серия,
	|	Аналитика.Склад,
	|	ВидыЗапасов.ВидЗапасовВладельца,
	|	КПередаче.ВидЗапасовПродавца,
	|	КПередаче.НомерГТД
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры
	|;
	|///////////////////////////////////////
	|");
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Владелец", Владелец);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("ПоВсемВидамЗапасов", ПоВсемВидамЗапасов);
	Запрос.УстановитьПараметр("Дата", Дата + 1);
	Запрос.УстановитьПараметр("ПередачаТоваров",
		ТипЗнч(Ссылка) = Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями"));
	
	Запрос.МенеджерВременныхТаблиц = ВременныеТаблицы;
	Запрос.Выполнить();
КонецПроцедуры

// Процедура формирует таблицу остатков реализованных товаров.
//
// Параметры:
//	ДокументСсылка - ДокументСсылка - Документ, для которого формируются остатки
//	Организация - СправочникСсылка.Организация - Организация, по которой формируются остатки.
//	Склад - СправочникСсылка.Склады - Склад, по которому формируются остатки.
//	ХозяйственнаяОперация - Перечисления.ХозяйственныеОперации - Хоз.операция по которой фильтруются операции продажи и возврата.
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Временные таблицы, в которые будет помещена ТаблицаОстатков.
//
Процедура ТаблицаОстатковРеализованныхТоваров(ДокументСсылка, Организация, Склад, ХозяйственнаяОперация, МенеджерВременныхТаблиц, ПереданнаяТара = Ложь) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Дата
	|
	|ПОМЕСТИТЬ ДокументыРеализации
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокумента КАК ТаблицаДанныхДокумента
	|	ПО
	|		ИСТИНА
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РеализацияТоваровУслуг КАК ДанныеДокумента
	|	ПО
	|		ДанныеДокумента.Ссылка = ТаблицаТоваров.ДокументРеализации
	|		ИЛИ (ТаблицаТоваров.ПодбиратьВидыЗапасов
	|			И ТаблицаТоваров.ДокументРеализации = НЕОПРЕДЕЛЕНО
	|			И ДанныеДокумента.Организация = ТаблицаДанныхДокумента.Организация
	|			И ДанныеДокумента.Партнер = ТаблицаДанныхДокумента.Партнер
	|			И ДанныеДокумента.Контрагент = ТаблицаДанныхДокумента.Контрагент)
	|
	|ГДЕ
	|	(ДанныеДокумента.ХозяйственнаяОперация = &ХозяйственнаяОперацияДокументаРеализации
	|	И ДанныеДокумента.Дата <= КОНЕЦПЕРИОДА(ТаблицаДанныхДокумента.Дата, ДЕНЬ))
	|	ИЛИ ДанныеДокумента.Ссылка = ТаблицаТоваров.ДокументРеализации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Дата
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокумента КАК ТаблицаДанныхДокумента
	|	ПО
	|		ИСТИНА
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ОтчетОРозничныхПродажах КАК ДанныеДокумента
	|	ПО
	|		ДанныеДокумента.Ссылка = ТаблицаТоваров.ДокументРеализации
	|		ИЛИ (ТаблицаТоваров.ДокументРеализации = НЕОПРЕДЕЛЕНО
	|			И ДанныеДокумента.Организация = ТаблицаДанныхДокумента.Организация)
	|
	|ГДЕ
	|	&ХозяйственнаяОперацияДокументаРеализации = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияВРозницу)
	|	И ДанныеДокумента.Дата <= КОНЕЦПЕРИОДА(ТаблицаДанныхДокумента.Дата, ДЕНЬ)
	|
	//++ НЕ УТ
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Дата
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокумента КАК ТаблицаДанныхДокумента
	|	ПО
	|		ИСТИНА
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПередачаСырьяПереработчику КАК ДанныеДокумента
	|	ПО
	|		ДанныеДокумента.Ссылка = ТаблицаТоваров.ДокументРеализации
	|		ИЛИ (ТаблицаТоваров.ДокументРеализации = НЕОПРЕДЕЛЕНО
	|			И ДанныеДокумента.Организация = ТаблицаДанныхДокумента.Организация)
	|
	|ГДЕ
	|	(&ХозяйственнаяОперацияДокументаРеализации = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаПереработчику)
	|		ИЛИ &ВыкупТары)
	|	И ДанныеДокумента.Дата <= КОНЕЦПЕРИОДА(ТаблицаДанныхДокумента.Дата, ДЕНЬ)
	//-- НЕ УТ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка
	|
	|ПОМЕСТИТЬ ДокументыВозврата
	|ИЗ
	|	Документ.ВозвратТоваровОтКлиента КАК ДанныеДокумента
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокумента КАК ТаблицаДанныхДокумента
	|	ПО
	|		ДанныеДокумента.Организация = ТаблицаДанныхДокумента.Организация
	|		И ДанныеДокумента.Партнер = ТаблицаДанныхДокумента.Партнер
	|		И ДанныеДокумента.Контрагент = ТаблицаДанныхДокумента.Контрагент
	|		И (ДанныеДокумента.ДокументРеализации = ТаблицаДанныхДокумента.ДокументРеализации
	|			ИЛИ ТаблицаДанныхДокумента.ДокументРеализации = НЕОПРЕДЕЛЕНО)
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка <> &Ссылка
	|	И ДанныеДокумента.ХозяйственнаяОперация = &ХозяйственнаяОперацияДокументаВозврата
	|
	//++ НЕ УТ
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ВозвратСырьяОтПереработчика КАК ДанныеДокумента
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокумента КАК ТаблицаДанныхДокумента
	|	ПО
	|		ДанныеДокумента.Организация = ТаблицаДанныхДокумента.Организация
	|		И ДанныеДокумента.Партнер = ТаблицаДанныхДокумента.Партнер
	|		И ДанныеДокумента.Контрагент = ТаблицаДанныхДокумента.Контрагент
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка <> &Ссылка
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтПереработчика)
	//-- НЕ УТ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	
	|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(&Ссылка) = ТИП(Документ.КорректировкаРеализации) ТОГДА
	|		Аналитика.Склад
	|	ИНАЧЕ
	|		&Склад
	|	КОНЕЦ КАК Склад, 
	|	
	|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(&Ссылка) = ТИП(Документ.КорректировкаРеализации) ТОГДА
	|		ТоварыОрганизаций.АналитикаУчетаНоменклатуры
	|	ИНАЧЕ
	|		ЕСТЬNULL(Аналитика.КлючАналитики, ТоварыОрганизаций.АналитикаУчетаНоменклатуры)
	|	КОНЕЦ КАК АналитикаУчетаНоменклатуры,
	|
	|	ТоварыОрганизаций.СкладОтгрузки КАК СкладОтгрузки,
	|	ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка) КАК Сделка,
	|
	|	АналитикаОтгрузки.Номенклатура КАК Номенклатура,
	|	АналитикаОтгрузки.Характеристика КАК Характеристика,
	|	АналитикаОтгрузки.Серия КАК Серия,
	|	ТоварыОрганизаций.ДокументРеализации КАК ДокументРеализации,
	|
	|	ВЫБОР КОГДА ТоварыОрганизаций.ВидЗапасов.Организация <> &Организация
	|		ИЛИ (ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(&Ссылка) = ТИП(Документ.КорректировкаРеализации)
	|					ТОГДА ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Назначение
	|					ИНАЧЕ ЕСТЬNULL(Аналитика.КлючАналитики.Назначение, ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Назначение)
	|				КОНЕЦ
	|			) <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	ТОГДА
	|		&ВидЗапасовПоУмолчанию
	|	ИНАЧЕ
	|		ТоварыОрганизаций.ВидЗапасов
	|	КОНЕЦ КАК ВидЗапасов,
	|
	|	ТоварыОрганизаций.ВидЗапасов КАК ВидЗапасовВладельца,
	|	Неопределено КАК ВидЗапасовПолучателя,
	|	
	|	ТоварыОрганизаций.ВидЗапасов КАК ВидЗапасовОтгрузки,
	|
	|	ВЫБОР КОГДА ТоварыОрганизаций.ВидЗапасов.Организация <> &Организация
	|		И ТоварыОрганизаций.КВозвратуМеждуОрганизациями
	|	ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		Ложь
	|	КОНЕЦ КАК РеализацияЗапасовДругойОрганизации,
	|
	|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(&Ссылка) = ТИП(Документ.КорректировкаРеализации)
	|		ИЛИ ТИПЗНАЧЕНИЯ(&Ссылка) = ТИП(Документ.ВозвратТоваровОтКлиента) ТОГДА
	|		ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Назначение
	|	ИНАЧЕ 
	|		ЕСТЬNULL(Аналитика.КлючАналитики.Назначение, ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Назначение)
	|	КОНЕЦ КАК Назначение,
	|	ТоварыОрганизаций.НомерГТД КАК НомерГТД,
	|	МИНИМУМ(
	|		РАЗНОСТЬДАТ(ТоварыОрганизаций.ДатаРеализации, &МаксимальнаяДата, ДЕНЬ)
	|	) КАК ДатаПоступления,
	|	СУММА(ТоварыОрганизаций.Количество) КАК КоличествоОстаток,
	|	0 КАК СуммаОстаток
	|
	|ПОМЕСТИТЬ ТаблицаОстатков
	|ИЗ (
	|	ВЫБРАТЬ // ВидыЗапасов, отгруженные документом реализации
	|		ТоварыОрганизаций.Регистратор КАК ДокументРеализации,
	|		Аналитика.Склад КАК СкладОтгрузки,
	|		ТоварыОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ТоварыОрганизаций.ВидЗапасов КАК ВидЗапасов,
	|		ТоварыОрганизаций.НомерГТД КАК НомерГТД,
	|		ТоварыОрганизаций.Количество КАК Количество,
	|		ДокументыРеализации.Дата КАК ДатаРеализации,
	|		ЕСТЬNULL(Настройка.ИспользоватьПриВозвратеОтКлиента, Ложь) КАК КВозвратуМеждуОрганизациями
	|	ИЗ
	|		РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			ДокументыРеализации КАК ДокументыРеализации
	|		ПО
	|			ТоварыОрганизаций.Регистратор = ДокументыРеализации.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.НастройкаПередачиТоваровМеждуОрганизациями КАК Настройка
	|		ПО
	|			ТоварыОрганизаций.ВидЗапасов.Организация = Настройка.ОрганизацияВладелец
	|			И ТоварыОрганизаций.ВидЗапасов.ТипЗапасов = Настройка.ТипЗапасов
	|			И Настройка.ОрганизацияПродавец = &Организация
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|		ПО
	|			ТоварыОрганизаций.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|	ГДЕ
	|		ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И (&ПереданнаяТара И Аналитика.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|			ИЛИ Не &ПереданнаяТара)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ // ВидыЗапасов, возвращенные от клиентов
	|		ТоварыОрганизаций.ДокументРеализации КАК ДокументРеализации,
	|		ЕСТЬNULL(КорАналитика.Склад, Аналитика.Склад) КАК СкладОтгрузки,
	|		ЕСТЬNULL(КорАналитика.КлючАналитики, Аналитика.КлючАналитики) КАК АналитикаУчетаНоменклатуры,
	|		ВЫБОР КОГДА НЕ ТоварыОрганизаций.КорВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) ТОГДА
	|			ВЫБОР КОГДА ТоварыОрганизаций.КорВидЗапасов.РеализацияЗапасовДругойОрганизации ТОГДА
	|				ТоварыОрганизаций.КорВидЗапасов.ВидЗапасовВладельца
	|			ИНАЧЕ
	|				ТоварыОрганизаций.КорВидЗапасов
	|			КОНЕЦ
	|		ИНАЧЕ
	|			ТоварыОрганизаций.ВидЗапасов
	|		КОНЕЦ КАК ВидЗапасов,
	|		ТоварыОрганизаций.НомерГТД КАК НомерГТД,
	|		ВЫБОР КОГДА ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
	|			ТоварыОрганизаций.Количество
	|		ИНАЧЕ
	|			0
	|		КОНЕЦ КАК Количество,
	|		ДАТАВРЕМЯ(1, 1, 1) КАК ДатаРеализации,
	|		ЕСТЬNULL(ТоварыОрганизаций.КорВидЗапасов.РеализацияЗапасовДругойОрганизации, Ложь) КАК КВозвратуМеждуОрганизациями
	|	ИЗ
	|		РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			ДокументыВозврата КАК ДокументыВозврата
	|		ПО
	|			ТоварыОрганизаций.Регистратор = ДокументыВозврата.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|		ПО
	|			ТоварыОрганизаций.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаУчетаНоменклатуры КАК КорАналитика
	|		ПО
	|			ТоварыОрганизаций.КорАналитикаУчетаНоменклатуры = КорАналитика.КлючАналитики

	|	ГДЕ
	|		НЕ (Аналитика.КлючАналитики ЕСТЬ NULL И КорАналитика.КлючАналитики ЕСТЬ NULL)
	|		И (&ПереданнаяТара И Аналитика.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|			ИЛИ Не &ПереданнаяТара)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ // ВидыЗапасов, с учетом корректировок
	|		ТоварыОрганизаций.ДокументРеализации КАК ДокументРеализации,
	|		Аналитика.Склад КАК СкладОтгрузки,
	|		ТоварыОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ТоварыОрганизаций.ВидЗапасов КАК ВидЗапасов,
	|		ТоварыОрганизаций.НомерГТД КАК НомерГТД,
	|		ВЫБОР КОГДА ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
	|			ТоварыОрганизаций.Количество
	|		ИНАЧЕ
	|			0
	|		КОНЕЦ КАК Количество,
	|		ДАТАВРЕМЯ(1, 1, 1) КАК ДатаРеализации,
	|		ЕСТЬNULL(Настройка.ИспользоватьПриВозвратеОтКлиента, Ложь) КАК КВозвратуМеждуОрганизациями
	|	ИЗ
	|		РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			Документ.КорректировкаРеализации КАК ДокументыКорректировки
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|				ДокументыРеализации КАК ДокументыРеализации
	|			ПО
	|				ДокументыКорректировки.ДокументОснование = ДокументыРеализации.Ссылка
	|
	|		ПО
	|			ТоварыОрганизаций.Регистратор = ДокументыКорректировки.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.НастройкаПередачиТоваровМеждуОрганизациями КАК Настройка
	|		ПО
	|			ТоварыОрганизаций.ВидЗапасов.Организация = Настройка.ОрганизацияВладелец
	|			И ТоварыОрганизаций.ВидЗапасов.ТипЗапасов = Настройка.ТипЗапасов
	|			И Настройка.ОрганизацияПродавец = &Организация
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|		ПО
	|			ТоварыОрганизаций.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|	ГДЕ
	|		&ХозяйственнаяОперацияДокументаРеализации = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту)
	|		И (&ПереданнаяТара И Аналитика.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|			ИЛИ Не &ПереданнаяТара)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ // ВидыЗапасов, указанные в документе
	|		Неопределено КАК ДокументРеализации,
	|		ТаблицаВидыЗапасов.СкладОтгрузки КАК СкладОтгрузки,
	|		ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ТаблицаВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|		ТаблицаВидыЗапасов.НомерГТД КАК НомерГТД,
	|		ТаблицаВидыЗапасов.Количество КАК Количество,
	|		ДАТАВРЕМЯ(1, 1, 1) КАК ДатаРеализации,
	|		Ложь КАК КВозвратуМеждуОрганизациями
	|	ИЗ
	|		ТаблицаВидыЗапасов КАК ТаблицаВидыЗапасов
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|		ПО
	|			ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|	ГДЕ
	|		ТаблицаВидыЗапасов.ВидыЗапасовУказаныВручную
	|		И (Аналитика.Номенклатура, Аналитика.Характеристика) В (
	|			ВЫБРАТЬ
	|				ТаблицаТоваров.Номенклатура,
	|				ТаблицаТоваров.Характеристика
	|			ИЗ
	|				ТаблицаТоваров КАК ТаблицаТоваров
	|			ГДЕ
	|				ТаблицаТоваров.ДокументРеализации = Неопределено
	|		)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ // Переданная возвратная тара
	|		Остатки.ДокументПередачи КАК ДокументРеализации,
	|		Аналитика.Склад КАК СкладОтгрузки,
	|		Аналитика.КлючАналитики КАК АналитикаУчетаНоменклатуры,
	|		Остатки.ВидЗапасов КАК ВидЗапасов,
	|		Остатки.НомерГТД КАК НомерГТД,
	|		Остатки.КоличествоОстаток КАК Количество,
	|		ЕСТЬNULL(РеализацияТоваровУслуг.Дата, Остатки.ДокументПередачи.Дата) КАК ДатаРеализации,
	|		Ложь КАК КВозвратуМеждуОрганизациями
	|	ИЗ
	|		РегистрНакопления.ПереданнаяВозвратнаяТара.Остатки(,
	|			&ПереданнаяТара
	|			И (Номенклатура, Характеристика, ДокументПередачи) В (
	|			ВЫБРАТЬ
	|				ТаблицаТоваров.Номенклатура,
	|				ТаблицаТоваров.Характеристика,
	|				ТаблицаТоваров.ДокументРеализации
	|			ИЗ
	|				ТаблицаТоваров КАК ТаблицаТоваров
	|			)
	|		) КАК Остатки
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ПО
	|			РеализацияТоваровУслуг.Ссылка = Остатки.ДокументПередачи
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|		ПО
	|			Аналитика.Номенклатура = Остатки.Номенклатура
	|			И Аналитика.Характеристика = Остатки.Характеристика
	|			И Аналитика.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			И Аналитика.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|			И Аналитика.Склад = ЕСТЬNULL(РеализацияТоваровУслуг.Склад, &Склад)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ // Переданная возвратная тара, списанная текущим документом
	|		Движения.ДокументПередачи КАК ДокументРеализации,
	|		Аналитика.Склад КАК СкладОтгрузки,
	|		Аналитика.КлючАналитики КАК АналитикаУчетаНоменклатуры,
	|		Движения.ВидЗапасов КАК ВидЗапасов,
	|		Движения.НомерГТД КАК НомерГТД,
	|		Движения.Количество КАК Количество,
	|		ЕСТЬNULL(РеализацияТоваровУслуг.Дата, Движения.ДокументПередачи.Дата) КАК ДатаРеализации,
	|		Ложь КАК КВозвратуМеждуОрганизациями
	|	ИЗ
	|		РегистрНакопления.ПереданнаяВозвратнаяТара КАК Движения
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ПО
	|			РеализацияТоваровУслуг.Ссылка = Движения.ДокументПередачи
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|		ПО
	|			Аналитика.Номенклатура = Движения.Номенклатура
	|			И Аналитика.Характеристика = Движения.Характеристика
	|			И Аналитика.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			И Аналитика.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|			И Аналитика.Склад = ЕСТЬNULL(РеализацияТоваровУслуг.Склад, &Склад)
	|	ГДЕ	
	|		Движения.Регистратор = &Ссылка
	|		И &ПереданнаяТара
	|	) КАК ТоварыОрганизаций
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаОтгрузки
	|	ПО
	|		ТоварыОрганизаций.АналитикаУчетаНоменклатуры = АналитикаОтгрузки.КлючАналитики
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО
	|		АналитикаОтгрузки.Номенклатура = Аналитика.Номенклатура
	|		И АналитикаОтгрузки.Характеристика = Аналитика.Характеристика
	|		И АналитикаОтгрузки.Серия = Аналитика.Серия
	|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = Аналитика.Назначение
	|		И &Склад = Аналитика.Склад
	//++ НЕ УТ 
	|		И ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) = Аналитика.СтатьяКалькуляции
	//-- НЕ УТ
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументРеализации,
	|	ТоварыОрганизаций.СкладОтгрузки,
	|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(&Ссылка) = ТИП(Документ.КорректировкаРеализации) ТОГДА
	|		ТоварыОрганизаций.АналитикаУчетаНоменклатуры
	|	ИНАЧЕ
	|		ЕСТЬNULL(Аналитика.КлючАналитики, ТоварыОрганизаций.АналитикаУчетаНоменклатуры)
	|	КОНЕЦ,
	|	АналитикаОтгрузки.Номенклатура,
	|	АналитикаОтгрузки.Характеристика,
	|	АналитикаОтгрузки.Серия,
	|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(&Ссылка) = ТИП(Документ.КорректировкаРеализации) ТОГДА
	|		Аналитика.Склад
	|	ИНАЧЕ
	|		&Склад
	|	КОНЕЦ, 
	|	ВЫБОР КОГДА ТоварыОрганизаций.ВидЗапасов.Организация <> &Организация
	|		ИЛИ (ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(&Ссылка) = ТИП(Документ.КорректировкаРеализации) ТОГДА
	|				ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Назначение
	|			ИНАЧЕ
	|				ЕСТЬNULL(Аналитика.КлючАналитики.Назначение, ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Назначение)
	|			КОНЕЦ) <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	ТОГДА
	|		&ВидЗапасовПоУмолчанию
	|	ИНАЧЕ
	|		ТоварыОрганизаций.ВидЗапасов
	|	КОНЕЦ,
	|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(&Ссылка) = ТИП(Документ.КорректировкаРеализации)
	|		ИЛИ ТИПЗНАЧЕНИЯ(&Ссылка) = ТИП(Документ.ВозвратТоваровОтКлиента) ТОГДА
	|		ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Назначение
	|	ИНАЧЕ 
	|		ЕСТЬNULL(Аналитика.КлючАналитики.Назначение, ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Назначение)
	|	КОНЕЦ,
	|	ТоварыОрганизаций.ВидЗапасов,
	|	ВЫБОР КОГДА ТоварыОрганизаций.ВидЗапасов.Организация <> &Организация
	|		ИЛИ (ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(&Ссылка) = ТИП(Документ.КорректировкаРеализации) ТОГДА
	|				ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Назначение
	|			ИНАЧЕ
	|				ЕСТЬNULL(Аналитика.КлючАналитики.Назначение, ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Назначение)
	|			КОНЕЦ) <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	ТОГДА
	|		&ВидЗапасовПоУмолчанию
	|	ИНАЧЕ
	|		ТоварыОрганизаций.ВидЗапасов
	|	КОНЕЦ,
	|	ВЫБОР КОГДА ТоварыОрганизаций.ВидЗапасов.Организация <> &Организация
	|		И ТоварыОрганизаций.КВозвратуМеждуОрганизациями
	|	ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		Ложь
	|	КОНЕЦ,
	|	ТоварыОрганизаций.НомерГТД
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|");
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("ВидЗапасовПоУмолчанию", Справочники.ВидыЗапасов.ПустаяСсылка());
	Запрос.УстановитьПараметр("МаксимальнаяДата", Дата(2399, 1, 1));
	Запрос.УстановитьПараметр("ПереданнаяТара", ПереданнаяТара);
	//++ НЕ УТ
	Запрос.УстановитьПараметр("ВыкупТары", (ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВыкупВозвратнойТарыКлиентом")));
	//-- НЕ УТ
	
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратОтКомиссионера Тогда
		ХозяйственнаяОперацияДокументаРеализации = Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратОтРозничногоПокупателя Тогда
		ХозяйственнаяОперацияДокументаРеализации = Перечисления.ХозяйственныеОперации.РеализацияВРозницу
	//++ НЕ УТ
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратОтПереработчика Тогда
		ХозяйственнаяОперацияДокументаРеализации = Перечисления.ХозяйственныеОперации.ПередачаПереработчику
	//-- НЕ УТ
	Иначе
		ХозяйственнаяОперацияДокументаРеализации = Перечисления.ХозяйственныеОперации.РеализацияКлиенту
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ХозяйственнаяОперацияДокументаРеализации", ХозяйственнаяОперацияДокументаРеализации);
	Запрос.УстановитьПараметр("ХозяйственнаяОперацияДокументаВозврата", ХозяйственнаяОперация);
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Процедура формирует таблицу остатков списанных на расходы товаров.
//
// Параметры:
//	ДокументСсылка - ДокументСсылка - Документ, для которого формируются остатки
//	Организация - СправочникСсылка.Организация - Организация, по которой формируются остатки.
//	Склад - СправочникСсылка.Склады - Склад, по которому формируются остатки.
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Временные таблицы, в которые будет помещена ТаблицаОстатков.
//
Процедура ТаблицаОстатковСписанныхНаРасходыТоваров(ДокументСсылка, Организация, Склад, МенеджерВременныхТаблиц) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Дата
	|
	|ПОМЕСТИТЬ ДокументыСписания
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ВнутреннееПотреблениеТоваров КАК ДанныеДокумента
	|	ПО
	|		ДанныеДокумента.Ссылка = ТаблицаТоваров.ДокументРеализации
	|		И ДанныеДокумента.Проведен
	|		И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	&Склад КАК Склад,
	|	ЕСТЬNULL(Аналитика.КлючАналитики, ТоварыОрганизаций.АналитикаУчетаНоменклатуры) КАК АналитикаУчетаНоменклатуры,
	|	ТоварыОрганизаций.СкладСписания КАК СкладОтгрузки,
	|	ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка) КАК Сделка,
	|	АналитикаСписания.Номенклатура КАК Номенклатура,
	|	АналитикаСписания.Характеристика КАК Характеристика,
	|	АналитикаСписания.Серия КАК Серия,
	|	ТоварыОрганизаций.ДокументСписания КАК ДокументРеализации,
	|	ТоварыОрганизаций.СтатьяРасходов КАК СтатьяРасходов,
	|	ТоварыОрганизаций.АналитикаРасходов КАК АналитикаРасходов,
	|
	|	ВЫБОР КОГДА ТоварыОрганизаций.ВидЗапасов.Организация <> &Организация
	|		ИЛИ ЕСТЬNULL(Аналитика.КлючАналитики.Назначение, ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Назначение) <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	ТОГДА
	|		&ВидЗапасовПоУмолчанию
	|	ИНАЧЕ
	|		ТоварыОрганизаций.ВидЗапасов
	|	КОНЕЦ КАК ВидЗапасов,
	|
	|	ТоварыОрганизаций.ВидЗапасов КАК ВидЗапасовВладельца,
	|	Неопределено КАК ВидЗапасовПолучателя,
	|	ТоварыОрганизаций.ВидЗапасов КАК ВидЗапасовОтгрузки,
	|
	|	ВЫБОР КОГДА ТоварыОрганизаций.ВидЗапасов.Организация <> &Организация
	|		И ТоварыОрганизаций.КВозвратуМеждуОрганизациями
	|	ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		Ложь
	|	КОНЕЦ КАК РеализацияЗапасовДругойОрганизации,
	|
	|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
	|	ТоварыОрганизаций.НомерГТД КАК НомерГТД,
	|	МИНИМУМ(
	|		РАЗНОСТЬДАТ(ТоварыОрганизаций.ДатаСписания, &МаксимальнаяДата, ДЕНЬ)
	|	) КАК ДатаПоступления,
	|	СУММА(ТоварыОрганизаций.Количество) КАК КоличествоОстаток,
	|	0 КАК СуммаОстаток
	|
	|ПОМЕСТИТЬ ТаблицаОстатков
	|ИЗ (
	|	ВЫБРАТЬ // ВидыЗапасов, списанные внутренним потреблением
	|		ТоварыОрганизаций.Регистратор КАК ДокументСписания,
	|		Аналитика.Склад КАК СкладСписания,
	|		ТоварыОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ТоварыОрганизаций.ВидЗапасов КАК ВидЗапасов,
	|		ТоварыОрганизаций.НомерГТД КАК НомерГТД,
	|		ТоварыОрганизаций.Количество КАК Количество,
	|		ДокументыСписания.Дата КАК ДатаСписания,
	|		ТоварыОрганизаций.СтатьяРасходов КАК СтатьяРасходов,
	|		ТоварыОрганизаций.АналитикаРасходов КАК АналитикаРасходов,
	|		ЕСТЬNULL(Настройка.ИспользоватьПриВозвратеОтКлиента, Ложь) КАК КВозвратуМеждуОрганизациями
	|	ИЗ
	|		РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			ДокументыСписания КАК ДокументыСписания
	|		ПО
	|			ТоварыОрганизаций.Регистратор = ДокументыСписания.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.НастройкаПередачиТоваровМеждуОрганизациями КАК Настройка
	|		ПО
	|			ТоварыОрганизаций.ВидЗапасов.Организация = Настройка.ОрганизацияВладелец
	|			И ТоварыОрганизаций.ВидЗапасов.ТипЗапасов = Настройка.ТипЗапасов
	|			И Настройка.ОрганизацияПродавец = &Организация
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|		ПО
	|			ТоварыОрганизаций.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|
	|	) КАК ТоварыОрганизаций
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаСписания
	|	ПО
	|		ТоварыОрганизаций.АналитикаУчетаНоменклатуры = АналитикаСписания.КлючАналитики
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО
	|		АналитикаСписания.Номенклатура = Аналитика.Номенклатура
	|		И АналитикаСписания.Характеристика = Аналитика.Характеристика
	|		И АналитикаСписания.Назначение = Аналитика.Назначение
	|		И АналитикаСписания.Серия = Аналитика.Серия
	|		И &Склад = Аналитика.Склад
	//++ НЕ УТ 
	|		И ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) = Аналитика.СтатьяКалькуляции
	//-- НЕ УТ
	|
	|СГРУППИРОВАТЬ ПО
	|	&Организация,
	|	&Склад,
	|	ЕСТЬNULL(Аналитика.КлючАналитики, ТоварыОрганизаций.АналитикаУчетаНоменклатуры),
	|	ТоварыОрганизаций.СкладСписания,
	|	ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка),
	|	АналитикаСписания.Номенклатура,
	|	АналитикаСписания.Характеристика,
	|	АналитикаСписания.Серия,
	|	ТоварыОрганизаций.ДокументСписания,
	|	ТоварыОрганизаций.СтатьяРасходов,
	|	ТоварыОрганизаций.АналитикаРасходов,
	|
	|	ВЫБОР КОГДА ТоварыОрганизаций.ВидЗапасов.Организация <> &Организация
	|		ИЛИ ЕСТЬNULL(Аналитика.КлючАналитики.Назначение, ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Назначение) <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	ТОГДА
	|		&ВидЗапасовПоУмолчанию
	|	ИНАЧЕ
	|		ТоварыОрганизаций.ВидЗапасов
	|	КОНЕЦ,
	|
	|	ТоварыОрганизаций.ВидЗапасов,
	|	Неопределено,
	|	ТоварыОрганизаций.ВидЗапасов,
	|
	|	ВЫБОР КОГДА ТоварыОрганизаций.ВидЗапасов.Организация <> &Организация
	|		И ТоварыОрганизаций.КВозвратуМеждуОрганизациями
	|	ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		Ложь
	|	КОНЕЦ,
	|
	|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка),
	|	ТоварыОрганизаций.НомерГТД
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|");
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("ВидЗапасовПоУмолчанию", Справочники.ВидыЗапасов.ПустаяСсылка());
	Запрос.УстановитьПараметр("МаксимальнаяДата", Дата(2399, 1, 1));
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Выполнить();
	
КонецПроцедуры

//++ НЕ УТ

// Процедура формирует таблицу остатков материалов в подразделении.
//
//Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Менеджер, в который помещается результирующая таблица.
//	СтруктураПараметров - Структура - Параметры построения: Организация, Подразделение, ДокументВозврата, Номенклатура, Характеристика.
//
Процедура ТаблицаОстатковМатериаловВПодразделении(МенеджерВременныхТаблиц, СтруктураПараметров) Экспорт
	
	Перем Подразделение, Назначение, Номенклатура, Характеристика;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МатериалыИРаботыВПроизводстве.Подразделение КАК Подразделение,
	|	МатериалыИРаботыВПроизводстве.Номенклатура КАК Номенклатура,
	|	МатериалыИРаботыВПроизводстве.Характеристика КАК Характеристика,
	|	МатериалыИРаботыВПроизводстве.Серия КАК Серия,
	|	МатериалыИРаботыВПроизводстве.Назначение КАК Назначение,
	|	МатериалыИРаботыВПроизводстве.КоличествоОстаток КАК Количество
	|ПОМЕСТИТЬ втОстатки
	|ИЗ
	|	РегистрНакопления.МатериалыИРаботыВПроизводстве.Остатки(
	|			,
	|			Организация = &Организация
	|				И (Подразделение В (&Подразделение) Или &ПоВсемПодразделениям)
	|				И (Назначение = &Назначение Или &ПоВсемНазначениям)
	|				И (Номенклатура = &Номенклатура
	|						И Характеристика = &Характеристика Или &ПоВсейНоменклатуре)) КАК МатериалыИРаботыВПроизводстве
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Движения.Подразделение,
	|	Движения.Номенклатура,
	|	Движения.Характеристика,
	|	Движения.Серия,
	|	Движения.Назначение,
	|	Движения.Количество
	|ИЗ
	|	РегистрНакопления.МатериалыИРаботыВПроизводстве КАК Движения
	|ГДЕ
	|	Движения.Регистратор = &ДокументИсключение
	|				И (Движения.Подразделение В (&Подразделение) Или &ПоВсемПодразделениям)
	|				И (Движения.Назначение = &Назначение Или &ПоВсемНазначениям)
	|				И (Движения.Номенклатура = &Номенклатура
	|						И Движения.Характеристика = &Характеристика Или &ПоВсейНоменклатуре) 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОстатков.Подразделение КАК Подразделение,
	|	ТаблицаОстатков.Номенклатура КАК Номенклатура,
	|	ТаблицаОстатков.Характеристика КАК Характеристика,
	|	ТаблицаОстатков.Серия КАК Серия,
	|	СУММА(ТаблицаОстатков.Количество) КАК Количество,
	|	ТаблицаОстатков.Назначение КАК Назначение
	|ПОМЕСТИТЬ ТаблицаОстатков
	|ИЗ
	|	втОстатки КАК ТаблицаОстатков
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаОстатков.Подразделение,
	|	ТаблицаОстатков.Номенклатура,
	|	ТаблицаОстатков.Характеристика,
	|	ТаблицаОстатков.Серия,
	|	ТаблицаОстатков.Назначение";
	
	СтруктураПараметров.Свойство("Номенклатура",      Номенклатура);
	СтруктураПараметров.Свойство("Характеристика",    Характеристика);
	СтруктураПараметров.Свойство("Подразделение",     Подразделение);
	СтруктураПараметров.Свойство("Назначение",        Назначение);
	
	Запрос.УстановитьПараметр("Организация",          СтруктураПараметров.Организация);
	Запрос.УстановитьПараметр("ДокументИсключение",   СтруктураПараметров.ДокументВозврата);
	
	Запрос.УстановитьПараметр("Назначение",           Назначение);
	Запрос.УстановитьПараметр("ПоВсемНазначениям",    Не ЗначениеЗаполнено(Назначение));
	
	Запрос.УстановитьПараметр("Номенклатура",         Номенклатура);
	Запрос.УстановитьПараметр("ПоВсейНоменклатуре",   Не ЗначениеЗаполнено(Номенклатура));
	
	Запрос.УстановитьПараметр("Характеристика",       Характеристика);
	
	Если ТипЗнч(Подразделение) = Тип("СправочникСсылка.СтруктураПредприятия") И ЗначениеЗаполнено(Подразделение) Тогда
		Запрос.УстановитьПараметр("Подразделение",        ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Подразделение));
		Запрос.УстановитьПараметр("ПоВсемПодразделениям", Ложь);
	ИначеЕсли ТипЗнч(Подразделение) = Тип("Массив") И Подразделение.Количество() > 0 Тогда
		Запрос.УстановитьПараметр("Подразделение",        Подразделение);
		Запрос.УстановитьПараметр("ПоВсемПодразделениям", Ложь);
	Иначе
		Запрос.УстановитьПараметр("Подразделение",        Неопределено);
		Запрос.УстановитьПараметр("ПоВсемПодразделениям", Истина);
	КонецЕсли;
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Метод возвращает таблицу остатков товаров, переданных переработчику.
//
// Параметры:
//	ДокументСсылка - ДокументСсылка - Ссылка на документ, для которого получаются остатки.
//	Материалы - ТабличнаяЧасть - Таб.часть "Материалы".
//	Назначение - СправочникСсылка.Назначения - Назначение, под которое были переданы товары.
//	Дата - Дата - Дата остатков.
//	ДополнительныеСвойства - Структура:
//		* ИгнорироватьОперативныеОстатки - Булево - Признак проверки оперативных остатков.
// ВозвращаемоеЗначение:
//	ТаблицаЗначений - Остатки товаров переданных переработчику.
//
Функция ТаблицаОстатковТоваровПереданныхПереработчику(ДокументСсылка, Материалы, Назначение, Организация, Дата, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
	|ПОМЕСТИТЬ ВтМатериалы
	|ИЗ
	|	&Материалы КАК Т
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ // Движения документа
	|	МАКСИМУМ(Товары.Период) КАК Период,
	|	Товары.АналитикаУчетаНоменклатуры,
	|	Товары.ВидЗапасов,
	|	СУММА(Товары.Количество) КАК Количество
	|ПОМЕСТИТЬ ДвиженияДокумента
	|ИЗ
	|	РегистрНакопления.ТоварыПереданныеПереработчику КАК Товары
	|ГДЕ
	|	Товары.Регистратор = &Ссылка
	|	И Товары.Активность
	|	И Товары.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И (Товары.АналитикаУчетаНоменклатуры) В (
	|		ВЫБРАТЬ
	|			ТаблицаТоваров.АналитикаУчетаНоменклатуры
	|		ИЗ
	|			ВтМатериалы КАК ТаблицаТоваров
	|		)
	|	И (Товары.АналитикаУчетаНоменклатуры.Назначение = &Назначение
	|		ИЛИ Товары.АналитикаУчетаНоменклатуры.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка))
	|	И Товары.ВидЗапасов.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.АналитикаУчетаНоменклатуры,
	|	Товары.ВидЗапасов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Товары.АналитикаУчетаНоменклатуры,
	|	Товары.ВидЗапасов
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ // Остатки товаров переданных переработчику на конец месяца
	|	Остатки.ВидЗапасов КАК ВидЗапасов,
	|	Остатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Остатки.КоличествоОстаток КАК КоличествоОстаток
	|ПОМЕСТИТЬ НаКонецМесяца
	|ИЗ
	|	РегистрНакопления.ТоварыПереданныеПереработчику.Остатки(&ГраницаМесяца,
	|		АналитикаУчетаНоменклатуры В
	|			(ВЫБРАТЬ 
	|				Т.АналитикаУчетаНоменклатуры
	|			ИЗ
	|				ВтМатериалы КАК Т)
	|		И ВидЗапасов.Организация = &Организация
	|	) КАК Остатки
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ // Остатки товаров переданных переработчику на конец дня
	|	Остатки.ВидЗапасов КАК ВидЗапасов,
	|	Остатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Остатки.КоличествоОстаток КАК КоличествоОстаток
	|ПОМЕСТИТЬ НаКонецДня
	|ИЗ
	|	РегистрНакопления.ТоварыПереданныеПереработчику.Остатки(&ГраницаДня,
	|		АналитикаУчетаНоменклатуры В
	|			(ВЫБРАТЬ 
	|				Т.АналитикаУчетаНоменклатуры
	|			ИЗ
	|				ВтМатериалы КАК Т)
	|		И ВидЗапасов.Организация = &Организация
	|	) КАК Остатки
	|ГДЕ
	|	&КонтролироватьНаКонецДня
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ // Остатки товаров переданных переработчику на дату последнего движения
	|	Остатки.ВидЗапасов КАК ВидЗапасов,
	|	Остатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Остатки.КоличествоОстаток КАК КоличествоОстаток
	|ПОМЕСТИТЬ НаПоследнееДвижение
	|ИЗ
	|	РегистрНакопления.ТоварыПереданныеПереработчику.Остатки(,
	|		НЕ &ИспользоватьОстаткиНаКонецМесяца
	|			И АналитикаУчетаНоменклатуры В
	|				(ВЫБРАТЬ
	|					Т.АналитикаУчетаНоменклатуры
	|				ИЗ
	|					ВтМатериалы КАК Т)
	|		И ВидЗапасов.Организация = &Организация
	|	) КАК Остатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидЗапасов,
	|	АналитикаУчетаНоменклатуры
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	0                                 КАК НомерСтроки,
	|	Товары.ВидЗапасов                 КАК ВидЗапасов,
	|	Товары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	СУММА(Товары.Количество)          КАК Количество
	|ИЗ (
	|	ВЫБРАТЬ // Ранее списанные товары данным документом
	|		Товары.ВидЗапасов КАК ВидЗапасов,
	|		Товары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		Товары.Количество КАК Количество
	|	ИЗ
	|		РегистрНакопления.ТоварыПереданныеПереработчику КАК Товары
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтМатериалы КАК Отбор
	|		ПО Товары.АналитикаУчетаНоменклатуры = Отбор.АналитикаУчетаНоменклатуры
	|	ГДЕ
	|		Товары.Период <= &Дата
	|		И Товары.Регистратор = &Ссылка
	|		И Товары.Активность
	|		И Товары.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ // Остатки товаров переданных переработчику
	|		НаКонецМесяца.ВидЗапасов КАК ВидЗапасов,
	|		НаКонецМесяца.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|
	|		ВЫБОР 
	|			КОГДА &ИспользоватьОстаткиНаКонецМесяца 
	|				ТОГДА НаКонецМесяца.КоличествоОстаток
	|			КОГДА НаКонецМесяца.КоличествоОстаток <= ЕСТЬNULL(НаПоследнееДвижение.КоличествоОстаток, 0)
	|					И (НаКонецМесяца.КоличествоОстаток <= ЕСТЬNULL(НаКонецДня.КоличествоОстаток, 0) ИЛИ НЕ &КонтролироватьНаКонецДня)
	|			ТОГДА
	|				НаКонецМесяца.КоличествоОстаток + ЕСТЬNULL(Движения.Количество, 0)
	|			КОГДА ЕСТЬNULL(НаПоследнееДвижение.КоличествоОстаток, 0) <= НаКонецМесяца.КоличествоОстаток
	|					И (ЕСТЬNULL(НаПоследнееДвижение.КоличествоОстаток, 0) <= ЕСТЬNULL(НаКонецДня.КоличествоОстаток, 0) ИЛИ НЕ &КонтролироватьНаКонецДня)
	|			ТОГДА
	|				ЕСТЬNULL(НаПоследнееДвижение.КоличествоОстаток, 0) + ЕСТЬNULL(Движения.Количество, 0)
	|			КОГДА ЕСТЬNULL(НаКонецДня.КоличествоОстаток, 0) <= ЕСТЬNULL(НаПоследнееДвижение.КоличествоОстаток, 0)
	|					И ЕСТЬNULL(НаКонецДня.КоличествоОстаток, 0) <= НаКонецМесяца.КоличествоОстаток
	|					И &КонтролироватьНаКонецДня
	|			ТОГДА
	|				ЕСТЬNULL(НаКонецДня.КоличествоОстаток, 0) + ЕСТЬNULL(Движения.Количество, 0)
	|		КОНЕЦ КАК Количество
	|	ИЗ
	|		НаКонецМесяца КАК НаКонецМесяца
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДвиженияДокумента КАК Движения
	|			ПО Движения.АналитикаУчетаНоменклатуры = НаКонецМесяца.АналитикаУчетаНоменклатуры
	|			И Движения.ВидЗапасов = НаКонецМесяца.ВидЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ НаПоследнееДвижение КАК НаПоследнееДвижение
	|			ПО НаКонецМесяца.АналитикаУчетаНоменклатуры = НаПоследнееДвижение.АналитикаУчетаНоменклатуры
	|			И НаКонецМесяца.ВидЗапасов = НаПоследнееДвижение.ВидЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ НаКонецДня КАК НаКонецДня
	|			ПО НаКонецМесяца.АналитикаУчетаНоменклатуры = НаКонецДня.АналитикаУчетаНоменклатуры
	|			И НаКонецМесяца.ВидЗапасов = НаКонецДня.ВидЗапасов
	|	) КАК Товары
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.ВидЗапасов,
	|	Товары.АналитикаУчетаНоменклатуры
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|");
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Материалы", Материалы.Выгрузить(, "АналитикаУчетаНоменклатуры"));
	Запрос.УстановитьПараметр("Назначение", Назначение);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Дата", Дата + 1);
	Запрос.УстановитьПараметр("ГраницаМесяца", Новый Граница(КонецМесяца(Дата), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ГраницаДня", Новый Граница(КонецДня(Дата), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("КонтролироватьНаКонецДня", Константы.ВидКонтроляТоваровОрганизаций.Получить() = Перечисления.ВидыКонтроляТоваровОрганизаций.КонецДняКонецМесяцаИДатаПоследнегоДвижения);
	
	ИгнорироватьОперативныеОстатки = ДополнительныеСвойства.Свойство("ИгнорироватьОперативныеОстатки");
	Запрос.УстановитьПараметр("ИспользоватьОстаткиНаКонецМесяца", ?(ИгнорироватьОперативныеОстатки, Истина, Ложь));
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Метод возвращает в документ "Возврат сырья от перерабочика" таблицу остатков товаров.
//
Процедура ТаблицаОстатковДляВозвратаПереданныхПереработчикуТоваров(ДокументСсылка, Организация, Партнер, Дата, ДополнительныеСвойства, МенеджерВременныхТаблиц, ПереданнаяТара) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Дата
	|
	|ПОМЕСТИТЬ ДокументыРеализации
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокумента КАК ТаблицаДанныхДокумента
	|	ПО
	|		ИСТИНА
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПередачаСырьяПереработчику КАК ДанныеДокумента
	|	ПО
	|		ДанныеДокумента.Ссылка = ТаблицаТоваров.ДокументРеализации
	|		ИЛИ (ТаблицаТоваров.ПодбиратьВидыЗапасов
	|			И ТаблицаТоваров.ДокументРеализации = НЕОПРЕДЕЛЕНО
	|			И ДанныеДокумента.Организация = ТаблицаДанныхДокумента.Организация
	|			И ДанныеДокумента.Партнер     = ТаблицаДанныхДокумента.Партнер
	|			И ДанныеДокумента.Контрагент  = ТаблицаДанныхДокумента.Контрагент)
	|ГДЕ
	|	(НЕ &ИспользоватьОстаткиНаКонецМесяца ИЛИ ДанныеДокумента.Дата <= &Дата)
	|	ИЛИ ДанныеДокумента.Ссылка = ТаблицаТоваров.ДокументРеализации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ДокументыВозврата
	|ИЗ
	|	Документ.ВозвратСырьяОтПереработчика КАК ДанныеДокумента
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокумента КАК ТаблицаДанныхДокумента
	|	ПО
	|		ДанныеДокумента.Организация = ТаблицаДанныхДокумента.Организация
	|		И ДанныеДокумента.Партнер = ТаблицаДанныхДокумента.Партнер
	|		И ДанныеДокумента.Контрагент = ТаблицаДанныхДокумента.Контрагент
	|
	|ГДЕ
	|	ДанныеДокумента.Ссылка <> &Ссылка
	|	И (НЕ &ИспользоватьОстаткиНаКонецМесяца ИЛИ ДанныеДокумента.Дата <= &Дата)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ДокументыВводаОстатков
	|ИЗ
	|	Документ.ВводОстатков КАК ДанныеДокумента
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокумента КАК ТаблицаДанныхДокумента
	|	ПО
	|		ДанныеДокумента.Организация = ТаблицаДанныхДокумента.Организация
	|		И ДанныеДокумента.Партнер = ТаблицаДанныхДокумента.Партнер
	|		И ДанныеДокумента.Контрагент = ТаблицаДанныхДокумента.Контрагент
	|
	|ГДЕ
	|	ДанныеДокумента.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиМатериаловПереданныхПереработчикам)
	|	И ДанныеДокумента.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыВводаНачальныхОстатков.РегламентированныйУчет)
	|	И ДанныеДокумента.Проведен
	|	И (НЕ &ИспользоватьОстаткиНаКонецМесяца ИЛИ ДанныеДокумента.Дата <= &Дата)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ // ВидыЗапасов, отгруженные документом передачи
	|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
	|	Аналитика.КлючАналитики КАК АналитикаУчетаНоменклатуры,
	|	ТоварыОрганизаций.ВидЗапасов КАК ВидЗапасов,
	|	ТоварыОрганизаций.НомерГТД КАК НомерГТД,
	|	ТоварыОрганизаций.Количество КАК Количество,
	|	ДокументыРеализации.Дата КАК ДатаРеализации
	|ПОМЕСТИТЬ втТоварыОрганизаций
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыРеализации КАК ДокументыРеализации
	|	ПО ТоварыОрганизаций.Регистратор = ДокументыРеализации.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаДокумента
	|	ПО ТоварыОрганизаций.АналитикаУчетаНоменклатуры = АналитикаДокумента.КлючАналитики
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО АналитикаДокумента.Номенклатура = Аналитика.Номенклатура
	|		И АналитикаДокумента.Характеристика = Аналитика.Характеристика
	|		И Аналитика.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		И Аналитика.Склад = &Партнер
	|		И Аналитика.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		И Аналитика.СтатьяКалькуляции = ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
	|	
	|ГДЕ
	|	ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И (&ПереданнаяТара И Аналитика.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|		ИЛИ НЕ &ПереданнаяТара)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // ВидыЗапасов, возвращенные от переработчика
	|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
	|	Аналитика.КлючАналитики КАК АналитикаУчетаНоменклатуры,
	|	ВЫБОР КОГДА НЕ ТоварыОрганизаций.КорВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) ТОГДА
	|		ВЫБОР КОГДА ТоварыОрганизаций.КорВидЗапасов.РеализацияЗапасовДругойОрганизации ТОГДА
	|			ТоварыОрганизаций.КорВидЗапасов.ВидЗапасовВладельца
	|		ИНАЧЕ
	|			ТоварыОрганизаций.КорВидЗапасов
	|		КОНЕЦ
	|	ИНАЧЕ
	|		ТоварыОрганизаций.ВидЗапасов
	|	КОНЕЦ КАК ВидЗапасов,
	|	ТоварыОрганизаций.НомерГТД КАК НомерГТД,
	|	ВЫБОР КОГДА ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|		-ТоварыОрганизаций.Количество
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК Количество,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаРеализации
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыВозврата КАК ДокументыВозврата
	|	ПО ТоварыОрганизаций.Регистратор = ДокументыВозврата.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаДокумента
	|	ПО ТоварыОрганизаций.АналитикаУчетаНоменклатуры = АналитикаДокумента.КлючАналитики
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО АналитикаДокумента.Номенклатура = Аналитика.Номенклатура
	|		И АналитикаДокумента.Характеристика = Аналитика.Характеристика
	|		И Аналитика.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		И Аналитика.Склад = &Партнер
	|		И Аналитика.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		И Аналитика.СтатьяКалькуляции = ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
	|	
	|ГДЕ
	|	НЕ (Аналитика.КлючАналитики ЕСТЬ NULL)
	|	И (&ПереданнаяТара И Аналитика.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|		ИЛИ НЕ &ПереданнаяТара)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // ВидыЗапасов, указанные в документах ввода остатков
	|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
	|	Аналитика.КлючАналитики КАК АналитикаУчетаНоменклатуры,
	|	ПереданныеПереработчикамТовары.ВидЗапасов КАК ВидЗапасов,
	|	ПереданныеПереработчикамТовары.НомерГТД КАК НомерГТД,
	|	ПереданныеПереработчикамТовары.Количество КАК Количество,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаРеализации
	|ИЗ
	|	Документ.ВводОстатков.Товары КАК ПереданныеПереработчикамТовары
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыВводаОстатков КАК ДокументыВводаОстатков
	|	ПО ПереданныеПереработчикамТовары.Ссылка = ДокументыВводаОстатков.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаДокумента
	|	ПО ПереданныеПереработчикамТовары.АналитикаУчетаНоменклатуры = АналитикаДокумента.КлючАналитики
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО АналитикаДокумента.Номенклатура = Аналитика.Номенклатура
	|		И АналитикаДокумента.Характеристика = Аналитика.Характеристика
	|		И Аналитика.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		И Аналитика.Склад = &Партнер
	|		И Аналитика.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		И Аналитика.СтатьяКалькуляции = ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
	|ГДЕ
	|	(Аналитика.Номенклатура, Аналитика.Характеристика, НЕОПРЕДЕЛЕНО) В (
	|		ВЫБРАТЬ
	|			ТаблицаТоваров.Номенклатура,
	|			ТаблицаТоваров.Характеристика,
	|			ТаблицаТоваров.ДокументРеализации
	|		ИЗ
	|			ТаблицаТоваров КАК ТаблицаТоваров)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // ВидыЗапасов, указанные в документе
	|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
	|	Аналитика.КлючАналитики КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.НомерГТД КАК НомерГТД,
	|	ТаблицаВидыЗапасов.Количество КАК Количество,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаРеализации
	|ИЗ
	|	ТаблицаВидыЗапасов КАК ТаблицаВидыЗапасов
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаДокумента
	|	ПО ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры = АналитикаДокумента.КлючАналитики
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО АналитикаДокумента.Номенклатура = Аналитика.Номенклатура
	|		И АналитикаДокумента.Характеристика = Аналитика.Характеристика
	|		И Аналитика.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		И Аналитика.Склад = &Партнер
	|		И Аналитика.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		И Аналитика.СтатьяКалькуляции = ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
	|ГДЕ
	|	ТаблицаВидыЗапасов.ВидыЗапасовУказаныВручную
	|	И (Аналитика.Номенклатура, Аналитика.Характеристика, НЕОПРЕДЕЛЕНО) В (
	|		ВЫБРАТЬ
	|			ТаблицаТоваров.Номенклатура,
	|			ТаблицаТоваров.Характеристика,
	|			ТаблицаТоваров.ДокументРеализации
	|		ИЗ
	|			ТаблицаТоваров КАК ТаблицаТоваров)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // Переданная возвратная тара
	|	Остатки.ДокументПередачи КАК ДокументРеализации,
	|	Аналитика.КлючАналитики КАК АналитикаУчетаНоменклатуры,
	|	Остатки.ВидЗапасов КАК ВидЗапасов,
	|	Остатки.НомерГТД КАК НомерГТД,
	|	Остатки.КоличествоОстаток КАК Количество,
	|	Остатки.ДокументПередачи.Дата КАК ДатаРеализации
	|ИЗ
	|	РегистрНакопления.ПереданнаяВозвратнаяТара.Остатки(,
	|		&ПереданнаяТара
	|		И (Номенклатура, Характеристика, ДокументПередачи) В (
	|		ВЫБРАТЬ
	|			ТаблицаТоваров.Номенклатура,
	|			ТаблицаТоваров.Характеристика,
	|			ТаблицаТоваров.ДокументРеализации
	|		ИЗ
	|			ТаблицаТоваров КАК ТаблицаТоваров
	|		)
	|	) КАК Остатки
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО Аналитика.Номенклатура = Остатки.Номенклатура
	|		И Аналитика.Характеристика = Остатки.Характеристика
	|		И Аналитика.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		И Аналитика.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		И Аналитика.Склад = &Партнер
	|		И Аналитика.СтатьяКалькуляции = ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // Переданная возвратная тара, списанная текущим документом
	|	Движения.ДокументПередачи КАК ДокументРеализации,
	|	Аналитика.КлючАналитики КАК АналитикаУчетаНоменклатуры,
	|	Движения.ВидЗапасов КАК ВидЗапасов,
	|	Движения.НомерГТД КАК НомерГТД,
	|	Движения.Количество КАК Количество,
	|	Движения.ДокументПередачи.Дата КАК ДатаРеализации
	|ИЗ
	|	РегистрНакопления.ПереданнаяВозвратнаяТара КАК Движения
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО Аналитика.Номенклатура = Движения.Номенклатура
	|		И Аналитика.Характеристика = Движения.Характеристика
	|		И Аналитика.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		И Аналитика.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|		И Аналитика.Склад = &Партнер
	|		И Аналитика.СтатьяКалькуляции = ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
	|ГДЕ
	|	Движения.Регистратор = &Ссылка
	|	И &ПереданнаяТара
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыОрганизаций.ДокументРеализации КАК ДокументРеализации,
	|	Аналитика.КлючАналитики КАК АналитикаУчетаНоменклатуры,
	|	Аналитика.Номенклатура,
	|	Аналитика.Характеристика,
	|	Аналитика.Серия,
	|	ТоварыОрганизаций.ВидЗапасов КАК ВидЗапасов,
	|	ТоварыОрганизаций.НомерГТД КАК НомерГТД,
	|	МИНИМУМ(РАЗНОСТЬДАТ(ТоварыОрганизаций.ДатаРеализации, &МаксимальнаяДата, ДЕНЬ)) КАК ДатаПоступления,
	|	СУММА(ТоварыОрганизаций.Количество) КАК КоличествоОстаток,
	|	0.000 КАК Распределено
	|ИЗ
	|	втТоварыОрганизаций КАК ТоварыОрганизаций
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО ТоварыОрганизаций.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыОрганизаций.ДокументРеализации,
	|	Аналитика.КлючАналитики,
	|	Аналитика.Номенклатура,
	|	Аналитика.Характеристика,
	|	Аналитика.Серия,
	|	ТоварыОрганизаций.ВидЗапасов,
	|	ТоварыОрганизаций.НомерГТД
	|УПОРЯДОЧИТЬ ПО
	|	Аналитика.Номенклатура, Аналитика.Характеристика, Аналитика.Серия, ВидЗапасов, ДатаПоступления
	|	
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втТоварыОрганизаций;
	|///////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДокументыРеализации;
	|///////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДокументыВозврата;
	|///////////////////////////////////////////////////////////////////////////////
	|";
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("МаксимальнаяДата", Дата(2399, 1, 1));
	Запрос.УстановитьПараметр("ПереданнаяТара", ПереданнаяТара);
	ИгнорироватьОперативныеОстатки = ДополнительныеСвойства.Свойство("ИгнорироватьОперативныеОстатки");
	Запрос.УстановитьПараметр("ИспользоватьОстаткиНаКонецМесяца", ?(ИгнорироватьОперативныеОстатки, Истина, Ложь));
	Запрос.УстановитьПараметр("ДатаКонцаМесяца", Новый Граница(КонецМесяца(Дата), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Партнер", Партнер);
	Запрос.УстановитьПараметр("Дата", Дата);
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ТаблицаОстаткиПоГТД = Запрос.ВыполнитьПакет()[4].Выгрузить();
	ТаблицаОстаткиПоГТД.Индексы.Добавить("Номенклатура, Характеристика, Серия, ВидЗапасов");
	
	Запрос.Текст =
	"ВЫБРАТЬ // Остатки товаров, переданных переработчику на конец месяца
	|	Остатки.ВидЗапасов КАК ВидЗапасов,
	|	Остатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Остатки.КоличествоОстаток КАК КоличествоОстаток
	|
	|ПОМЕСТИТЬ НаКонецМесяца
	|ИЗ
	|	РегистрНакопления.ТоварыПереданныеПереработчику.Остатки(&ДатаКонцаМесяца,
	|		ВидЗапасов В(
	|			ВЫБРАТЬ
	|				ДоступныеВидыЗапасов.ВидЗапасов
	|			ИЗ
	|				ДоступныеВидыЗапасов КАК ДоступныеВидыЗапасов)
	|		И АналитикаУчетаНоменклатуры В (
	|			ВЫБРАТЬ
	|				Аналитика.КлючАналитики
	|			ИЗ
	|				РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТоваров КАК Товары
	|				ПО Товары.Номенклатура = Аналитика.Номенклатура
	|				И Товары.Характеристика = Аналитика.Характеристика
	|				И Аналитика.Склад = &Партнер
	|				И ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) = Аналитика.СтатьяКалькуляции
	|				)
	|	) КАК Остатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидЗапасов,
	|	АналитикаУчетаНоменклатуры
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ // Остатки товаров организации на дату актуальности
	|	Остатки.ВидЗапасов КАК ВидЗапасов,
	|	Остатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Остатки.КоличествоОстаток КАК КоличествоОстаток
	|
	|ПОМЕСТИТЬ НаДатуАктуальности
	|ИЗ
	|	РегистрНакопления.ТоварыПереданныеПереработчику.Остатки(,
	|		Не &ИспользоватьОстаткиНаКонецМесяца 
	|		И ВидЗапасов В(
	|			ВЫБРАТЬ
	|				ДоступныеВидыЗапасов.ВидЗапасов
	|			ИЗ
	|				ДоступныеВидыЗапасов КАК ДоступныеВидыЗапасов)
	|		И АналитикаУчетаНоменклатуры В (
	|			ВЫБРАТЬ
	|				Аналитика.КлючАналитики
	|			ИЗ
	|				РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТоваров КАК Товары
	|				ПО Товары.Номенклатура = Аналитика.Номенклатура
	|				И Товары.Характеристика = Аналитика.Характеристика
	|				И Аналитика.Склад = &Партнер
	|				И ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) = Аналитика.СтатьяКалькуляции
	|				)
	|	) КАК Остатки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидЗапасов,
	|	АналитикаУчетаНоменклатуры
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабТовары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Аналитика.Номенклатура КАК Номенклатура,
	|	Аналитика.Характеристика КАК Характеристика,
	|	Аналитика.Серия КАК Серия,
	|	ТабТовары.ВидЗапасов КАК ВидЗапасов,
	|	СУММА(ТабТовары.Количество) КАК КоличествоОстаток
	|
	|ПОМЕСТИТЬ ТаблицаОстатковБезНомеровГТД
	|ИЗ (
	|	ВЫБРАТЬ // ВидыЗапасов, списанные текущим документом
	|		ТоварыПереданныеПереработчику.ВидЗапасов КАК ВидЗапасов,
	|		ТоварыПереданныеПереработчику.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		- ТоварыПереданныеПереработчику.Количество КАК Количество
	|	ИЗ
	|		РегистрНакопления.ТоварыПереданныеПереработчику КАК ТоварыПереданныеПереработчику
	|
	|	ГДЕ
	|		ТоварыПереданныеПереработчику.Регистратор = &Ссылка
	|		И ТоварыПереданныеПереработчику.Активность
	|		И ТоварыПереданныеПереработчику.ВидЗапасов В(
	|			ВЫБРАТЬ
	|				ДоступныеВидыЗапасов.ВидЗапасов
	|			ИЗ
	|				ДоступныеВидыЗапасов КАК ДоступныеВидыЗапасов)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ // ВидыЗапасов, переданные переработчику
	|		НаКонецМесяца.ВидЗапасов КАК ВидЗапасов,
	|		НаКонецМесяца.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ВЫБОР КОГДА Не &ИспользоватьОстаткиНаКонецМесяца
	|		 И НаКонецМесяца.КоличествоОстаток >=  ЕСТЬNULL(НаДатуАктуальности.КоличествоОстаток, 0) ТОГДА
	|			ЕСТЬNULL(НаДатуАктуальности.КоличествоОстаток, 0)
	|		ИНАЧЕ
	|			НаКонецМесяца.КоличествоОстаток
	|		КОНЕЦ КАК Количество
	|	ИЗ
	|		НаКонецМесяца КАК НаКонецМесяца
	|		ЛЕВОЕ СОЕДИНЕНИЕ НаДатуАктуальности КАК НаДатуАктуальности
	|			ПО НаДатуАктуальности.ВидЗапасов = НаКонецМесяца.ВидЗапасов
	|			И НаДатуАктуальности.АналитикаУчетаНоменклатуры = НаКонецМесяца.АналитикаУчетаНоменклатуры
	|
	|	) КАК ТабТовары
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|	ПО
	|		ТабТовары.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТабТовары.АналитикаУчетаНоменклатуры,
	|	Аналитика.Номенклатура,
	|	Аналитика.Характеристика,
	|	Аналитика.Серия,
	|	ТабТовары.ВидЗапасов
	|
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОстатков.Номенклатура      КАК Номенклатура,
	|	ТаблицаОстатков.Характеристика    КАК Характеристика,
	|	ТаблицаОстатков.Серия             КАК Серия,
	|	ТаблицаОстатков.ВидЗапасов        КАК ВидЗапасов,
	|	ТаблицаОстатков.КоличествоОстаток КАК Количество
	|ИЗ
	|	ТаблицаОстатковБезНомеровГТД КАК ТаблицаОстатков";
	ТаблицаКРаспределению = Запрос.Выполнить().Выгрузить();
	
	ОтборСтрок = Новый Структура("Номенклатура, Характеристика, Серия, ВидЗапасов");
	
	Для Каждого СтрокаТаблицы Из ТаблицаКРаспределению Цикл
		
		ЗаполнитьЗначенияСвойств(ОтборСтрок, СтрокаТаблицы);
		СтрокиСНомеромГТД = ТаблицаОстаткиПоГТД.НайтиСтроки(ОтборСтрок);
		
		Для Каждого СтрокаСНомеромГТД Из СтрокиСНомеромГТД Цикл
			
			Если СтрокаТаблицы.Количество <= 0 Тогда
				Прервать;
			КонецЕсли;
			ДоступноКРаспределению = Макс(СтрокаСНомеромГТД.КоличествоОстаток - СтрокаСНомеромГТД.Распределено, 0);
			КоличествоКРаспределению = Мин(ДоступноКРаспределению, СтрокаТаблицы.Количество);
			СтрокаТаблицы.Количество = СтрокаТаблицы.Количество - КоличествоКРаспределению;
			СтрокаСНомеромГТД.Распределено = СтрокаСНомеромГТД.Распределено + КоличествоКРаспределению;
			
		КонецЦикла;
		
	КонецЦикла;
	Запрос.УстановитьПараметр("ТаблицаНомеровГТД", ТаблицаОстаткиПоГТД);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Номенклатура   КАК Номенклатура,
	|	Таблица.Характеристика КАК Характеристика,
	|	Таблица.Серия          КАК Серия,
	|	Таблица.ВидЗапасов     КАК ВидЗапасов,
	|	Таблица.ДокументРеализации         КАК ДокументРеализации,
	|	Таблица.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Таблица.НомерГТД       КАК НомерГТД,
	|	Таблица.Распределено   КАК Распределено,
	|	Таблица.КоличествоОстаток КАК КоличествоОстаток
	|ПОМЕСТИТЬ ВтТаблицаНомеровГТД
	|ИЗ
	|	&ТаблицаНомеровГТД КАК Таблица
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура, Характеристика, Серия, ВидЗапасов
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК СкладОтгрузки,
	|	ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка) КАК Сделка,
	|	ТабТовары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТабТовары.Номенклатура КАК Номенклатура,
	|	ТабТовары.Характеристика КАК Характеристика,
	|	ТабТовары.Серия КАК Серия,
	|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
	|	ТабТовары.ВидЗапасов КАК ВидЗапасов,
	|	ТабТовары.ВидЗапасов КАК ВидЗапасовВладельца,
	|	НЕОПРЕДЕЛЕНО КАК ВидЗапасовПолучателя,
	|	ТабТовары.ВидЗапасов КАК ВидЗапасовОтгрузки,
	|	ЛОЖЬ КАК РеализацияЗапасовДругойОрганизации,
	|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
	|	&Дата КАК ДатаПоступления,
	|	ЕСТЬNULL(ТаблицаНомеровГТД.НомерГТД, ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)) КАК НомерГТД,
	|	ЕСТЬNULL(ТаблицаНомеровГТД.Распределено, ТабТовары.КоличествоОстаток) КАК КоличествоОстаток,
	|	0 КАК СуммаОстаток
	|
	|ПОМЕСТИТЬ ТаблицаОстатков
	|ИЗ
	|	ТаблицаОстатковБезНомеровГТД КАК ТабТовары
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО СпрНоменклатура.Ссылка = ТабТовары.Номенклатура
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтТаблицаНомеровГТД КАК ТаблицаНомеровГТД
	|		ПО ТаблицаНомеровГТД.Номенклатура   = ТабТовары.Номенклатура
	|		 И ТаблицаНомеровГТД.Характеристика = ТабТовары.Характеристика
	|		 И ТаблицаНомеровГТД.Серия          = ТабТовары.Серия
	|		 И ТаблицаНомеровГТД.ВидЗапасов     = ТабТовары.ВидЗапасов
	|		 И ТаблицаНомеровГТД.Распределено > 0
	|		 И СпрНоменклатура.ВестиУчетПоГТД
	|		
	|ГДЕ
	|	(НЕ ТаблицаНомеровГТД.Номенклатура ЕСТЬ NULL ИЛИ НЕ СпрНоменклатура.ВестиУчетПоГТД)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК СкладОтгрузки,
	|	ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка) КАК Сделка,
	|	ТаблицаНомеровГТД.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаНомеровГТД.Номенклатура КАК Номенклатура,
	|	ТаблицаНомеровГТД.Характеристика КАК Характеристика,
	|	ТаблицаНомеровГТД.Серия КАК Серия,
	|	ТаблицаНомеровГТД.ДокументРеализации КАК ДокументРеализации,
	|	ТаблицаНомеровГТД.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаНомеровГТД.ВидЗапасов КАК ВидЗапасовВладельца,
	|	НЕОПРЕДЕЛЕНО КАК ВидЗапасовПолучателя,
	|	ТаблицаНомеровГТД.ВидЗапасов КАК ВидЗапасовОтгрузки,
	|	ЛОЖЬ КАК РеализацияЗапасовДругойОрганизации,
	|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
	|	&Дата КАК ДатаПоступления,
	|	ТаблицаНомеровГТД.НомерГТД КАК НомерГТД,
	|	ТаблицаНомеровГТД.КоличествоОстаток КАК КоличествоОстаток,
	|	0 КАК СуммаОстаток
	|
	|ИЗ
	|	ВтТаблицаНомеровГТД КАК ТаблицаНомеровГТД
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО СпрНоменклатура.Ссылка = ТаблицаНомеровГТД.Номенклатура
	|		
	|ГДЕ
	|	СпрНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	ТабТовары.АналитикаУчетаНоменклатуры
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаОстатковБезНомеровГТД;
	|///////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВтТаблицаНомеровГТД;
	|///////////////////////////////////////////////////////////////////////////////";
	Запрос.Выполнить();
	
КонецПроцедуры

//-- НЕ УТ
// Процедура формирует таблицу остатков комиссионных товаров инеркампани.
//
// Параметры:
//	Организация - СправочникСсылка.Организация - Организация, по которой формируются остатки.
//	НачалоПериода - Дата - Начало периода за который анализириются движения.
//	КонецПериода - Дата - Конец периода за который анализириются движения.
//	ВременныеТаблицы - МенеджерВременныхТаблиц - Временные таблицы, в которые будет помещена ТаблицаОстатков.
//	Ссылка - ДокументСсылка - Ссылка на документ, для которого получаются остатки.
//	ЭтоОтчетОСписании - Булево - Признак того, что ссылка на документ является Отчетом по комиссии между организациии о списании.
//
Процедура ТаблицаОстатковКомиссионныхТоваровИнтеркампани(Организация, НачалоПериода, КонецПериода, ВременныеТаблицы, Ссылка, ЭтоОтчетОСписании = Ложь) Экспорт
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
	|ПОМЕСТИТЬ ОтборПоАналитике
	|ИЗ
	|	ТаблицаТоваров КАК Товары
	|;
	|/////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОформлению.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТоварыКОформлению.ВидЗапасов КАК ВидЗапасов,
	|	ТоварыКОформлению.НомерГТД КАК НомерГТД,
	|	НАЧАЛОПЕРИОДА(ЕСТЬNULL(ДанныеСчетаФактуры.Дата, ТоварыКОформлению.ДатаСчетаФактуры), ДЕНЬ) КАК ДатаСчетаФактуры,
	|	ЕСТЬNULL(ДанныеСчетаФактуры.Ссылка,
	|		ЕСТЬNULL(ДанныеСчетаФактурыОтчетаКомитенту.Ссылка,
	|		ДанныеСчетаФактурыВыставленныйКомиссионеру.Ссылка)) КАК СчетФактура,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ТоварыКОформлению.ДокументРеализации) = ТИП(Документ.ОтчетОРозничныхПродажах)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Контрагенты.РозничныйПокупатель)
	|		КОГДА ТИПЗНАЧЕНИЯ(ТоварыКОформлению.ДокументРеализации) = ТИП(Документ.РеализацияТоваровУслуг)
	|			ТОГДА ДанныеРеализации.Контрагент
	|		КОГДА ТИПЗНАЧЕНИЯ(ТоварыКОформлению.ДокументРеализации) = ТИП(Документ.ПередачаТоваровМеждуОрганизациями)
	|			ТОГДА ПередачаТоваров.ОрганизацияПолучатель
	|		КОГДА ТИПЗНАЧЕНИЯ(ТоварыКОформлению.Регистратор) = ТИП(Документ.ВозвратТоваровОтКлиента)
	|			ТОГДА Возврат.Контрагент
	|		ИНАЧЕ ТоварыКОформлению.Покупатель
	|	КОНЕЦ КАК Покупатель,
	|	(ВЫБОР
	|		КОГДА ТоварыКОформлению.Количество >= 0 ТОГДА 1
	|		ИНАЧЕ -1 КОНЕЦ) КАК Знак,
	|	
	|	СУММА(ВЫБОР КОГДА ТоварыКОформлению.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|		ВЫБОР КОГДА &ЭтоОтчетОСписании 
	|			ТОГДА ТоварыКОформлению.КоличествоСписано
	|			ИНАЧЕ ТоварыКОформлению.Количество
	|		КОНЕЦ
	|	ИНАЧЕ
	|		ВЫБОР
	|			КОГДА ТоварыКОформлению.Регистратор = &ТекущийОтчет ТОГДА 0
	|			КОГДА &ЭтоОтчетОСписании ТОГДА -ТоварыКОформлению.КоличествоСписано
	|			ИНАЧЕ -ТоварыКОформлению.Количество КОНЕЦ
	|	КОНЕЦ) КАК Количество,
	|	СУММА(ВЫБОР КОГДА ТоварыКОформлению.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|		ТоварыКОформлению.СуммаВыручки
	|	ИНАЧЕ
	|		-ТоварыКОформлению.СуммаВыручки
	|	КОНЕЦ) КАК СуммаВыручки,
	|	СУММА(
	|		ВЫБОР
	|			КОГДА ТоварыКОформлению.Регистратор = &ТекущийОтчет И &ЭтоОтчетОСписании ТОГДА ТоварыКОформлению.КоличествоСписано
	|			КОГДА ТоварыКОформлению.Регистратор = &ТекущийОтчет ТОГДА ТоварыКОформлению.Количество
	|			ИНАЧЕ 0 КОНЕЦ
	|	) КАК КоличествоВДокументе
	|ПОМЕСТИТЬ
	|	Остатки
	|ИЗ
	|	РегистрНакопления.ТоварыКОформлениюОтчетовКомитенту КАК ТоварыКОформлению
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ОтборПоАналитике КАК ОтборПоАналитике
	|	ПО
	|		ОтборПоАналитике.АналитикаУчетаНоменклатуры = ТоварыКОформлению.АналитикаУчетаНоменклатуры
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ДоступныеВидыЗапасов КАК ВидыЗапасов
	|	ПО
	|		ТоварыКОформлению.ВидЗапасов = ВидыЗапасов.ВидЗапасов
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.ВозвратТоваровОтКлиента КАК Возврат
	|	ПО
	|		Возврат.Ссылка = ТоварыКОформлению.Регистратор
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.СчетФактураВыданный КАК ДанныеСчетаФактуры
	|	ПО
	|		ТоварыКОформлению.ДокументРеализации = ДанныеСчетаФактуры.ДокументОснование
	|		И ТоварыКОформлению.ДокументРеализации <> НЕОПРЕДЕЛЕНО
	|		И НЕ ДанныеСчетаФактуры.ПометкаУдаления
	|		И (НЕ ТоварыКОформлению.Регистратор ССЫЛКА Документ.ВозвратТоваровОтКлиента ИЛИ Возврат.ПокупательНеПлательщикНДС)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураВыданный КАК ДанныеСчетаФактурыОтчетаКомитенту
	|	ПО ТоварыКОформлению.НомерСчетаФактуры = ДанныеСчетаФактурыОтчетаКомитенту.Номер
	|		И ТоварыКОформлению.ДатаСчетаФактуры = НАЧАЛОПЕРИОДА(ДанныеСчетаФактурыОтчетаКомитенту.Дата, ДЕНЬ)
	|		И ДанныеСчетаФактурыОтчетаКомитенту.Организация = &Организация
	|		И ТоварыКОформлению.Покупатель = ДанныеСчетаФактурыОтчетаКомитенту.Контрагент
	|		И НЕ ДанныеСчетаФактурыОтчетаКомитенту.ПометкаУдаления
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Документ.СчетФактураКомиссионеру.Покупатели КАК ДанныеСчетаФактурыВыставленныйКомиссионеру 
	|	ПО 
	|		ТоварыКОформлению.Регистратор = ДанныеСчетаФактурыВыставленныйКомиссионеру.Ссылка.ДокументОснование 
	|		И ТоварыКОформлению.ДокументРеализации = НЕОПРЕДЕЛЕНО 
	|		И ТоварыКОформлению.ДатаСчетаФактуры = НАЧАЛОПЕРИОДА(ДанныеСчетаФактурыВыставленныйКомиссионеру.Ссылка.Дата, ДЕНЬ) 
	|		И ТоварыКОформлению.НомерСчетаФактуры = ДанныеСчетаФактурыВыставленныйКомиссионеру.НомерСчетаФактуры 
	|		И НЕ ДанныеСчетаФактурыВыставленныйКомиссионеру.Ссылка.ПометкаУдаления
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.РеализацияТоваровУслуг КАК ДанныеРеализации
	|	ПО
	|		ТоварыКОформлению.ДокументРеализации = ДанныеРеализации.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.ПередачаТоваровМеждуОрганизациями КАК ПередачаТоваров
	|	ПО
	|		ТоварыКОформлению.ДокументРеализации = ПередачаТоваров.Ссылка
	|ГДЕ
	|	ТоварыКОформлению.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ТоварыКОформлению.Активность
	|	И ТоварыКОформлению.Регистратор <> &ТекущийОтчет
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(ЕСТЬNULL(ДанныеСчетаФактуры.Дата, ТоварыКОформлению.ДатаСчетаФактуры), ДЕНЬ),
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ТоварыКОформлению.ДокументРеализации) = ТИП(Документ.ОтчетОРозничныхПродажах)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Контрагенты.РозничныйПокупатель)
	|		КОГДА ТИПЗНАЧЕНИЯ(ТоварыКОформлению.ДокументРеализации) = ТИП(Документ.РеализацияТоваровУслуг)
	|			ТОГДА ДанныеРеализации.Контрагент
	|		КОГДА ТИПЗНАЧЕНИЯ(ТоварыКОформлению.ДокументРеализации) = ТИП(Документ.ПередачаТоваровМеждуОрганизациями)
	|			ТОГДА ПередачаТоваров.ОрганизацияПолучатель
	|		КОГДА ТИПЗНАЧЕНИЯ(ТоварыКОформлению.Регистратор) = ТИП(Документ.ВозвратТоваровОтКлиента)
	|			ТОГДА Возврат.Контрагент
	|		ИНАЧЕ ТоварыКОформлению.Покупатель
	|	КОНЕЦ,
	|	ЕСТЬNULL(ДанныеСчетаФактуры.Ссылка,
	|		ЕСТЬNULL(ДанныеСчетаФактурыОтчетаКомитенту.Ссылка,
	|		ДанныеСчетаФактурыВыставленныйКомиссионеру.Ссылка)),
	|	ТоварыКОформлению.АналитикаУчетаНоменклатуры,
	|	ТоварыКОформлению.ВидЗапасов,
	|	ТоварыКОформлению.НомерГТД,
	|	(ВЫБОР
	|		КОГДА ТоварыКОформлению.Количество >= 0 ТОГДА 1
	|		ИНАЧЕ -1 КОНЕЦ)
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВЫБОР КОГДА ТоварыКОформлению.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|		ВЫБОР КОГДА &ЭтоОтчетОСписании 
	|			ТОГДА ТоварыКОформлению.КоличествоСписано
	|			ИНАЧЕ ТоварыКОформлению.Количество
	|		КОНЕЦ
	|	ИНАЧЕ
	|		ВЫБОР
	|			КОГДА ТоварыКОформлению.Регистратор = &ТекущийОтчет ТОГДА 0
	|			КОГДА &ЭтоОтчетОСписании ТОГДА -ТоварыКОформлению.КоличествоСписано
	|			ИНАЧЕ -ТоварыКОформлению.Количество КОНЕЦ
	|	КОНЕЦ) <> 0
	|");
	
	Запрос.МенеджерВременныхТаблиц = ВременныеТаблицы;
	Граница = Новый Граница(КонецПериода, ВидГраницы.Включая);
	
	Запрос.УстановитьПараметр("Граница", Граница);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецМесяца(КонецПериода));
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ТекущийОтчет", Ссылка);
	Запрос.УстановитьПараметр("ЭтоОтчетОСписании", ЭтоОтчетОСписании);
	
	Запрос.Выполнить();
КонецПроцедуры

// Процедура заполняет табличную часть "Виды запасов" документов.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Требуется временные таблицы:
//		ТаблицаТоваров, ТаблицаДанныхДокумента, ТаблицаВидыЗапасов, ТаблицаОстатков
//	ДополнительныеСвойства - Структура - Дополнительные свойства документа
//	ТабличнаяЧастьВидыЗапасов - ТабличнаяЧасть - Табличная часть "Виды запасов" документа
//	ТаблицаОшибок - ТаблицаЗначений - Таблица для отражения ошибок при заполнении видов запасов
//	Отказ - Булево - Признак отказа от записи документа
//
Процедура ЗаполнитьВидыЗапасовДокумента(МенеджерВременныхТаблиц, ДополнительныеСвойства, ТабличнаяЧастьВидыЗапасов, ТаблицаОшибок, Отказ = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ДополнительныеСвойства.Свойство("ТаблицаВидыЗапасов") Тогда
		ТаблицаВидыЗапасов = ДополнительныеСвойства.ТаблицаВидыЗапасов;
	Иначе
		ТаблицаВидыЗапасов = ТабличнаяЧастьВидыЗапасов.Выгрузить();
	КонецЕсли;
	
	ТаблицаВидыЗапасов.Очистить();
	
	КонтролироватьИзменениеНомеровГТД = ДополнительныеСвойства.Свойство("КонтролироватьИзменениеНомеровГТД");
	Если КонтролироватьИзменениеНомеровГТД Тогда
		ТаблицаНомеровГТД = ТаблицаВидыЗапасов.Скопировать(, "АналитикаУчетаНоменклатуры, НомерГТД, Количество");
	КонецЕсли;
	
	ВидЗапасовПоУмолчанию = Неопределено;
	ВидЗапасовПолучателяПоУмолчанию = Неопределено;
	НеУказанНомерГТД = Ложь;
	
	ПерезаполнитьВидыЗапасов = ДополнительныеСвойства.Свойство("ПерезаполнитьВидыЗапасов");
	Если Не ПерезаполнитьВидыЗапасов Тогда
		КонтролироватьОстаткиТоваровОрганизаций = ПолучитьФункциональнуюОпцию("КонтролироватьОстаткиТоваровОрганизаций");
	Иначе
		КонтролироватьОстаткиТоваровОрганизаций = Ложь;
	КонецЕсли;
	КонтролироватьОстаткиТоваровОрганизаций = КонтролироватьОстаткиТоваровОрганизаций
		Или ДополнительныеСвойства.Свойство("КонтролироватьОстаткиТоваровОрганизаций");
	
	Запрос = ЗапросДанныеЗаполненияВидовЗапасов(МенеджерВременныхТаблиц, ДополнительныеСвойства);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаДанныеДокумента = МассивРезультатов[1].Выбрать();
	ВыборкаТаблицаТоваров = МассивРезультатов[2].Выбрать();
	
	ВыборкаДанныеДокумента.Следующий();
	Организация = ВыборкаДанныеДокумента.Организация;
	ХозяйственнаяОперация = ВыборкаДанныеДокумента.ХозяйственнаяОперация;
	
	СоответствиеВидовЗапасов = Новый Соответствие;
	СоответствиеВидовЗапасовВладельца = Новый Соответствие;
	СоответствиеВидовЗапасовПодакцизныйТовар = Новый Соответствие;
	СоответствиеВидовЗапасовВозвратаБезПередачи = Новый Соответствие;
	
	СтруктураДанныхСтроки = Новый Структура("
		|Склад,
		|СкладОтгрузки,
		|Сделка,
		|Назначение,
		|Количество,
		|СуммаСНДС,
		|СтавкаНДС,
		|СуммаНДС,
		|СуммаВознаграждения,
		|СуммаНДСВознаграждения,
		|ВидЗапасовПолучателя,
		|ВидЗапасовОтгрузки,
		|ДокументРеализации,
		|АналитикаУчетаНоменклатурыОтгрузки,
		|ЗаказКлиента
		|");
	
	СтруктураВидаЗапасовПоУмолчанию = Новый Структура("
		|ГруппаФинансовогоУчета,
		|ОбособленныйУчетТоваровПоСделке,
		|ВариантОбособленногоУчетаТоваров,
		|Назначение,
		|Поставщик,
		|Контрагент,
		|Соглашение,
		|Договор,
		|Валюта,
		|НалогообложениеНДС,
		|НалогообложениеОрганизации
		|");
	
	СтруктураВидаЗапасовПоУмолчанию.ГруппаФинансовогоУчета				= Справочники.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка();
	СтруктураВидаЗапасовПоУмолчанию.ОбособленныйУчетТоваровПоСделке		= Ложь;
	СтруктураВидаЗапасовПоУмолчанию.ВариантОбособленногоУчетаТоваров	= Неопределено;
	СтруктураВидаЗапасовПоУмолчанию.Поставщик							= ВыборкаДанныеДокумента.Партнер;
	СтруктураВидаЗапасовПоУмолчанию.Контрагент							= ВыборкаДанныеДокумента.Контрагент;
	СтруктураВидаЗапасовПоУмолчанию.Соглашение							= ВыборкаДанныеДокумента.Соглашение;
	СтруктураВидаЗапасовПоУмолчанию.Договор								= ВыборкаДанныеДокумента.Договор;
	СтруктураВидаЗапасовПоУмолчанию.Валюта								= ВыборкаДанныеДокумента.Валюта;
	СтруктураВидаЗапасовПоУмолчанию.НалогообложениеНДС					= ВыборкаДанныеДокумента.НалогообложениеНДС;
	СтруктураВидаЗапасовПоУмолчанию.НалогообложениеОрганизации			= ВыборкаДанныеДокумента.НалогообложениеНДС;
	
	ПередачаПодДеятельность = ВыборкаДанныеДокумента.НалогообложениеНДС;
	
	РезультатЗапроса = МассивРезультатов[МассивРезультатов.ВГраница()];
	ВыборкаПоСтрокам = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоСтрокам.Следующий() Цикл
		
		КоличествоТовара			= ВыборкаПоСтрокам.Количество;
		СуммаТовараСНДС				= ВыборкаПоСтрокам.СуммаСНДС;
		СуммаНДСТовара				= ВыборкаПоСтрокам.СуммаНДС;
		СуммаВознагражденияВсего	= ВыборкаПоСтрокам.СуммаВознаграждения;
		СуммаНДСВознагражденияВсего	= ВыборкаПоСтрокам.СуммаНДСВознаграждения;
		
		ЕстьНулевыеОстаткиТоваровОрганизаций = Ложь;
		
		// Детальные записи
		Выборка = ВыборкаПоСтрокам.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если (Выборка.Количество > 0 И Выборка.КоличествоОстаток < 0) // количество в документе и количество остаток должны быть одного знака.
				ИЛИ (Выборка.Количество < 0 И Выборка.КоличествоОстаток > 0)
				ИЛИ Выборка.КоличествоОстаток = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			ВидЗапасовОтгрузки = Неопределено;
			
			Если ЗначениеЗаполнено(Выборка.ВидЗапасов) Тогда
				
				ВидЗапасов =  Выборка.ВидЗапасов;
				
			// Для возврата товаров от клиента формируется новый вид запасов по виду запасов отгрузки.	
			ИначеЕсли ЗначениеЗаполнено(Выборка.ВидЗапасовОтгрузки) И Не Выборка.РеализацияЗапасовДругойОрганизации
				И Выборка.ОрганизацияОтгрузки = Организация
			Тогда
			
				Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратНедопоставленногоТовара
				 ИЛИ ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ИсправлениеОшибок
				 ИЛИ ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КорректировкаПоСогласованиюСторон
				Тогда
					ВидЗапасов = Выборка.ВидЗапасовОтгрузки;
				Иначе
					ВидЗапасов = СоответствиеВидовЗапасов.Получить(Выборка.ВидЗапасовОтгрузки);
				КонецЕсли;
				
				Если ВидЗапасов = Неопределено Тогда
					ВидЗапасов = Справочники.ВидыЗапасов.ВидЗапасовДляВозвратаТоваровОтКлиента(Выборка.ВидЗапасовОтгрузки, Организация);
					СоответствиеВидовЗапасов.Вставить(Выборка.ВидЗапасовОтгрузки, ВидЗапасов);
				КонецЕсли;
			
			ИначеЕсли ЗначениеЗаполнено(Выборка.ВидЗапасовВладельца) Тогда
				
				ВидЗапасовОтгрузки = ВидЗапасовОтгрузки(Выборка,
					Организация,
					ПередачаПодДеятельность,
					СоответствиеВидовЗапасовВозвратаБезПередачи,
					СоответствиеВидовЗапасовПодакцизныйТовар,
					СоответствиеВидовЗапасов);
				
				Если ЗначениеЗаполнено(Выборка.ВидЗапасовОтгрузки) Тогда
					
					ВидЗапасовВладельца = СоответствиеВидовЗапасовВладельца.Получить(Выборка.ВидЗапасовВладельца);
					Если ВидЗапасовВладельца = Неопределено Тогда
						ВидЗапасовВладельца = Справочники.ВидыЗапасов.ВидЗапасовДляВозвратаТоваровОтКлиента(Выборка.ВидЗапасовВладельца);
						СоответствиеВидовЗапасовВладельца.Вставить(Выборка.ВидЗапасовВладельца, ВидЗапасовВладельца);
					КонецЕсли;
					
				Иначе
					
					ВидЗапасовВладельца = Выборка.ВидЗапасовВладельца;
					
				КонецЕсли;
				
				Если Выборка.ЭтоПодакцизныйТовар Тогда
					ВидЗапасов = СоответствиеВидовЗапасовПодакцизныйТовар.Получить(ВидЗапасовВладельца);
				Иначе
					ВидЗапасов = СоответствиеВидовЗапасов.Получить(ВидЗапасовВладельца);
				КонецЕсли;
				
				Если ВидЗапасов = Неопределено 
					Или ВыборкаДанныеДокумента.НалогообложениеНДС <> ВидЗапасов.НалогообложениеНДС Тогда
					Если Не Выборка.РеализацияЗапасовДругойОрганизации
						И Выборка.ОрганизацияОтгрузки <> Организация
					Тогда // теперь это собственный товар
						ВидЗапасов = Справочники.ВидыЗапасов.ВидЗапасовДляВозвратаТоваровОтКлиента(ВидЗапасовОтгрузки, Организация);
						СоответствиеВидовЗапасов.Вставить(Выборка.ВидЗапасовОтгрузки, ВидЗапасов);
					Иначе
						ВидЗапасов = Справочники.ВидыЗапасов.ВидЗапасовДляПередачиМеждуОрганизациями(
							ВидЗапасовВладельца,
							Организация,
							ПередачаПодДеятельность,
							Выборка.ДатаДокумента,
							Выборка.СырьевойТовар);
					
						Если Выборка.ЭтоПодакцизныйТовар Тогда
							СоответствиеВидовЗапасовПодакцизныйТовар.Вставить(ВидЗапасовВладельца, ВидЗапасов);
						Иначе
							СоответствиеВидовЗапасов.Вставить(ВидЗапасовВладельца, ВидЗапасов);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
			Иначе
				
				ВидЗапасов = Справочники.ВидыЗапасов.ПустаяСсылка();
				
			КонецЕсли;
			
			Если ВидЗапасовОтгрузки = Неопределено Тогда
				ВидЗапасовОтгрузки = ВидЗапасовОтгрузки(Выборка,
					Организация,
					ПередачаПодДеятельность,
					СоответствиеВидовЗапасовВозвратаБезПередачи,
					СоответствиеВидовЗапасовПодакцизныйТовар,
					СоответствиеВидовЗапасов);
			КонецЕсли;
			
			КоличествоОстаток	= Выборка.КоличествоОстаток;
			СуммаОстаток		= Выборка.СуммаОстаток;
			НеУказанНомерГТД	= Ложь;
			
			Если Выборка.НеУказанНомерГТД Тогда
				
				КоличествоОстаток	= 0;
				СуммаОстаток		= 0;
				НеУказанНомерГТД	= Выборка.НеУказанНомерГТД;
				
			ИначеЕсли (КоличествоТовара > 0 И КоличествоОстаток > 0)
			 ИЛИ (КоличествоТовара < 0 И КоличествоОстаток < 0) Тогда
				
				НоваяСтрока = ТаблицаВидыЗапасов.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
				
				Если ЗначениеЗаполнено(ВидЗапасов) Тогда
					
					НоваяСтрока.ВидЗапасов = ВидЗапасов;
					
				Иначе
					
					Если ВидЗапасовПоУмолчанию = Неопределено Тогда
						ВидЗапасовПоУмолчанию = Справочники.ВидыЗапасов.ВидЗапасовДокумента(
							Организация,
							ХозяйственнаяОперация,
							СтруктураВидаЗапасовПоУмолчанию);
						
					КонецЕсли;
					
					НоваяСтрока.ВидЗапасов = ВидЗапасовПоУмолчанию;
					
				КонецЕсли;
				
				Если КоличествоТовара > 0 Тогда
					
					КоличествоОстаток = Макс(КоличествоОстаток, 0);
					Количество = Мин(КоличествоТовара, КоличествоОстаток);
					
					Если СуммаОстаток <> 0 Тогда
						СуммаОстаток = Макс(СуммаОстаток, 0);
						СуммаСНДС = ?(КоличествоОстаток = Количество, СуммаОстаток, Мин(СуммаТовараСНДС, СуммаОстаток));
					Иначе
						СуммаСНДС = ?(КоличествоТовара <> 0, Окр(Количество * СуммаТовараСНДС / КоличествоТовара, 2, 1), 0);
					КонецЕсли;
					
				Иначе
					
					КоличествоОстаток = Мин(КоличествоОстаток, 0);
					Количество = Макс(КоличествоТовара, КоличествоОстаток);
					
					Если СуммаОстаток <> 0 Тогда
						СуммаОстаток = Мин(СуммаОстаток, 0);
						СуммаСНДС = ?(КоличествоОстаток = Количество, СуммаОстаток, Макс(СуммаТовараСНДС, СуммаОстаток));
					Иначе
						СуммаСНДС = ?(КоличествоТовара <> 0, Окр(Количество * СуммаТовараСНДС / КоличествоТовара, 2, 1), 0);
					КонецЕсли;
					
				КонецЕсли;
				
				СтруктураДанныхСтроки.Количество				= Количество;
				СтруктураДанныхСтроки.СуммаСНДС					= СуммаСНДС;
				СтруктураДанныхСтроки.СуммаНДС					= ?(СуммаТовараСНДС <> 0, Окр(СуммаСНДС * СуммаНДСТовара / СуммаТовараСНДС, 2, 1), 0);
				СтруктураДанныхСтроки.СтавкаНДС					= Выборка.СтавкаНДС;
				СтруктураДанныхСтроки.СуммаВознаграждения		= ?(КоличествоТовара <> 0, Окр(Количество * СуммаВознагражденияВсего / КоличествоТовара, 2, 1), 0);
				СтруктураДанныхСтроки.СуммаНДСВознаграждения	= ?(КоличествоТовара <> 0, Окр(Количество * СуммаНДСВознагражденияВсего / КоличествоТовара, 2, 1), 0);
				СтруктураДанныхСтроки.Склад						= Выборка.Склад;
				СтруктураДанныхСтроки.Сделка					= Выборка.Сделка;
				СтруктураДанныхСтроки.Назначение				= Выборка.Назначение;
				СтруктураДанныхСтроки.СкладОтгрузки				= Выборка.СкладОтгрузки;
				СтруктураДанныхСтроки.ВидЗапасовПолучателя		= Выборка.ВидЗапасовПолучателя;
				СтруктураДанныхСтроки.ВидЗапасовОтгрузки		= ВидЗапасовОтгрузки;
				СтруктураДанныхСтроки.ДокументРеализации		= Выборка.ДокументРеализации;
				СтруктураДанныхСтроки.ЗаказКлиента				= Выборка.Заказ;
				СтруктураДанныхСтроки.АналитикаУчетаНоменклатурыОтгрузки = Выборка.АналитикаУчетаНоменклатурыОтгрузки;
				
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтруктураДанныхСтроки);
				
				КоличествоТовара			= КоличествоТовара				- СтруктураДанныхСтроки.Количество;
				СуммаТовараСНДС				= СуммаТовараСНДС				- СтруктураДанныхСтроки.СуммаСНДС;
				СуммаНДСТовара				= СуммаНДСТовара				- СтруктураДанныхСтроки.СуммаНДС;
				СуммаВознагражденияВсего	= СуммаВознагражденияВсего		- СтруктураДанныхСтроки.СуммаВознаграждения;
				СуммаНДСВознагражденияВсего	= СуммаНДСВознагражденияВсего	- СтруктураДанныхСтроки.СуммаНДСВознаграждения;
				СуммаОстаток				= СуммаОстаток					- СтруктураДанныхСтроки.СуммаСНДС;
				
			КонецЕсли;
			
		КонецЦикла; // Выборка по остаткам
			
		Если КоличествоТовара <> 0 ИЛИ СуммаТовараСНДС <> 0 Тогда
			
			ВыборкаТаблицаТоваров.Сбросить();
			ВидыЗапасовПоУмолчанию = Новый Соответствие;
			
			Если ВыборкаТаблицаТоваров.НайтиСледующий(Новый Структура("НомерСтроки", ВыборкаПоСтрокам.НомерСтроки)) Тогда
				
				ГруппаФинансовогоУчета = ВыборкаТаблицаТоваров.ГруппаФинансовогоУчета;
				ВидЗапасовПоУмолчанию = ВидыЗапасовПоУмолчанию[ГруппаФинансовогоУчета];
				Если ВидЗапасовПоУмолчанию = Неопределено Тогда
					СтруктураВидаЗапасовПоУмолчанию.ГруппаФинансовогоУчета = ГруппаФинансовогоУчета;
					ВидЗапасовПоУмолчанию = Справочники.ВидыЗапасов.ВидЗапасовДокумента(
						Организация,
						ХозяйственнаяОперация,
						СтруктураВидаЗапасовПоУмолчанию);
					ВидыЗапасовПоУмолчанию.Вставить(ГруппаФинансовогоУчета, ВидЗапасовПоУмолчанию);
				КонецЕсли;
				
				ОрганизацияПолучатель = Неопределено;
				Если ДополнительныеСвойства.Свойство("ЗаполнятьВидЗапасовПолучателя", ОрганизацияПолучатель)
						И ВидЗапасовПолучателяПоУмолчанию = Неопределено Тогда
				 
					ВидЗапасовПолучателяПоУмолчанию = Справочники.ВидыЗапасов.ВидЗапасовДляПередачиМеждуОрганизациями(
						ВидЗапасовПоУмолчанию,
						ОрганизацияПолучатель,
						ПередачаПодДеятельность,
						Выборка.ДатаДокумента,
						Выборка.СырьевойТовар);
					
				КонецЕсли;
				
				НоваяСтрока = ТаблицаВидыЗапасов.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаТаблицаТоваров);
				НоваяСтрока.ВидЗапасов = ВидЗапасовПоУмолчанию;
				
				СтруктураДанныхСтроки.Количество				= КоличествоТовара;
				СтруктураДанныхСтроки.СуммаСНДС					= СуммаТовараСНДС;
				СтруктураДанныхСтроки.СуммаНДС					= СуммаНДСТовара;
				СтруктураДанныхСтроки.СуммаВознаграждения		= 0;
				СтруктураДанныхСтроки.СуммаНДСВознаграждения	= 0;
				СтруктураДанныхСтроки.СтавкаНДС					= ВыборкаТаблицаТоваров.СтавкаНДС;
				СтруктураДанныхСтроки.Склад						= ВыборкаТаблицаТоваров.Склад;
				СтруктураДанныхСтроки.Сделка					= ВыборкаТаблицаТоваров.Сделка;
				СтруктураДанныхСтроки.Назначение				= Выборка.Назначение;
				СтруктураДанныхСтроки.СкладОтгрузки				= ВыборкаТаблицаТоваров.Склад;
				СтруктураДанныхСтроки.ВидЗапасовПолучателя		= ВидЗапасовПолучателяПоУмолчанию;
				СтруктураДанныхСтроки.ДокументРеализации		= Неопределено;
				СтруктураДанныхСтроки.ЗаказКлиента				= Неопределено;
				СтруктураДанныхСтроки.АналитикаУчетаНоменклатурыОтгрузки = ВыборкаТаблицаТоваров.АналитикаУчетаНоменклатуры;
				
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтруктураДанныхСтроки);
				
				Если (КоличествоТовара > 0 ИЛИ (КоличествоТовара = 0 И СуммаТовараСНДС <> 0)) Тогда
					
					ДополнительныеСвойства.Вставить("НедостаточноОстатковТоваровОрганизаций", Истина);
					Если КонтролироватьОстаткиТоваровОрганизаций Тогда
						
						Выборка = ВыборкаПоСтрокам.Выбрать();
						Выборка.Следующий();
						
						НоваяСтрокаОшибок = ТаблицаОшибок.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрокаОшибок, ВыборкаТаблицаТоваров);
						
						НоваяСтрокаОшибок.Количество		= КоличествоТовара;
						НоваяСтрокаОшибок.Сумма				= СуммаТовараСНДС;
						НоваяСтрокаОшибок.ЕдиницаИзмерения	= Выборка.ЕдиницаИзмерения;
						НоваяСтрокаОшибок.НеУказанНомерГТД	= НеУказанНомерГТД;
						
						Если Не ДополнительныеСвойства.Свойство("ПерезаполнитьВидыЗапасов") Тогда
							Отказ = Истина;
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла; // Выборка по строкам документа
	
	Если Не ДополнительныеСвойства.Свойство("ТаблицаВидыЗапасов") Тогда
		ТабличнаяЧастьВидыЗапасов.Загрузить(ТаблицаВидыЗапасов);
	КонецЕсли;
	
	Если КонтролироватьИзменениеНомеровГТД Тогда
		
		ТаблицаНомеровГТД.Колонки.Добавить("НовоеКоличество");
		
		Для Каждого СтрокаТаблицы Из ТаблицаВидыЗапасов Цикл
			
			НоваяСтрока = ТаблицаНомеровГТД.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.Количество = 0;
			НоваяСтрока.НовоеКоличество = СтрокаТаблицы.Количество;
			
		КонецЦикла;
		
		ТаблицаНомеровГТД.Свернуть("АналитикаУчетаНоменклатуры, НомерГТД", "Количество, НовоеКоличество");
		Для Каждого СтрокаТаблицы Из ТаблицаНомеровГТД Цикл
			Если СтрокаТаблицы.Количество <> СтрокаТаблицы.НовоеКоличество Тогда
				ДополнительныеСвойства.Вставить("ИзменилисьНомераГТД", Истина);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполняет табличную часть "Виды запасов" документов "Отчет по комиссии между организациями" и "Отчет по комиссии между организациями о списании".
//
// Параметры:
//	ВременныеТаблицы - МенеджерВременныхТаблиц - Требуется временные таблицы:
//		ТаблицаТоваров, ТаблицаДанныхДокумента, ТаблицаВидыЗапасов, ТаблицаОстатков
//	ДополнительныеСвойства - Структура - Дополнительные свойства документа
//	ВидыЗапасов - ТабличнаяЧасть - Табличная часть "Виды запасов" документа
//	ТаблицаОшибок - ТаблицаЗначений - Таблица для отражения ошибок при заполнении видов запасов
//	Отказ - Булево - Признак отказа от записи документа
//  ЗначенияПоУмолчанию - Структура - Содержит в себе перечень необходимых полей для генерации вида запасов.
Процедура ЗаполнитьВидыЗапасовИнтеркампаниОтчетаПоКомиссии(ВременныеТаблицы, ДополнительныеСвойства, ВидыЗапасов, ТаблицаОшибок, Отказ, ЗначенияПоУмолчанию) Экспорт
	
	ПерезаполнитьВидыЗапасов = ДополнительныеСвойства.Свойство("ПерезаполнитьВидыЗапасов");
	ЭтоОтчетОПродажах = ДополнительныеСвойства.Свойство("ЭтоОтчетОПродажах");
	Если Не ПерезаполнитьВидыЗапасов Тогда
		КонтролироватьОстаткиТоваровОрганизаций = ПолучитьФункциональнуюОпцию("КонтролироватьОстаткиТоваровОрганизаций");
	Иначе
		КонтролироватьОстаткиТоваровОрганизаций = Ложь;
	КонецЕсли;
	
	ВидЗапасовПоУмолчанию = Неопределено;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Остатки.ВидЗапасов                 КАК ВидЗапасов,
	|	Остатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Остатки.НомерГТД                   КАК НомерГТД,
	|	Остатки.ДатаСчетаФактуры           КАК ДатаСчетаФактурыКомиссионера,
	|	Остатки.Покупатель                 КАК Покупатель,
	|	Остатки.СчетФактура                КАК СчетФактураВыставленныйКомиссионера,
	|	Остатки.Знак                       КАК Знак,
	|	ВЫБОР КОГДА Остатки.Количество < 0
	|		ТОГДА 0 - Остатки.Количество
	|		ИНАЧЕ Остатки.Количество
	|	КОНЕЦ                              КАК Количество,
	|	ВЫБОР КОГДА Остатки.Количество < 0
	|		ТОГДА 0 - Остатки.СуммаВыручки
	|		ИНАЧЕ Остатки.СуммаВыручки
	|	КОНЕЦ                              КАК СуммаВыручки
	|ИЗ
	|	Остатки КАК Остатки
	|УПОРЯДОЧИТЬ ПО
	|	АналитикаУчетаНоменклатуры,
	|	ДатаСчетаФактурыКомиссионера,
	|	Покупатель,
	|	Количество УБЫВ,
	|	Знак УБЫВ
	|;
	|//////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.АналитикаУчетаНоменклатуры          КАК АналитикаУчетаНоменклатуры,
	|	Товары.Упаковка                            КАК Упаковка,
	|	Товары.ДатаСчетаФактурыКомиссионера        КАК ДатаСчетаФактурыКомиссионера,
	|	Товары.СчетФактураВыставленныйКомиссионера КАК СчетФактураВыставленныйКомиссионера,
	|	Товары.Покупатель                          КАК Покупатель,
	|	ВЫБОР КОГДА Товары.Количество < 0
	|		ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ                                      КАК Знак,
	|	СУММА(ВЫБОР КОГДА Товары.Количество < 0
	|		ТОГДА 0 - Товары.Количество
	|		ИНАЧЕ Товары.Количество
	|	КОНЕЦ)                                     КАК Количество,
	|	СУММА(ВЫБОР КОГДА Товары.Количество < 0
	|		ТОГДА 0 - Товары.КоличествоУпаковок
	|		ИНАЧЕ Товары.КоличествоУпаковок
	|	КОНЕЦ)                                     КАК КоличествоУпаковок,
	|	СУММА(ВЫБОР КОГДА Товары.Количество < 0
	|		ТОГДА 0 - Товары.СуммаСНДС
	|		ИНАЧЕ Товары.СуммаСНДС
	|	КОНЕЦ)                                     КАК СуммаСНДС,
	|	СУММА(ВЫБОР КОГДА Товары.Количество < 0
	|		ТОГДА 0 - Товары.СуммаВознаграждения
	|		ИНАЧЕ Товары.СуммаВознаграждения
	|	КОНЕЦ)                                     КАК СуммаВознаграждения,
	|	СУММА(ВЫБОР КОГДА Товары.Количество < 0
	|		ТОГДА 0 - Товары.СуммаНДСВознаграждения
	|		ИНАЧЕ Товары.СуммаНДСВознаграждения
	|	КОНЕЦ)                                     КАК СуммаНДСВознаграждения,
	|	СУММА(ВЫБОР КОГДА Товары.Количество < 0
	|		ТОГДА 0 - Товары.СуммаНДС
	|		ИНАЧЕ Товары.СуммаНДС
	|	КОНЕЦ)                                     КАК СуммаНДС,
	|	Товары.СтавкаНДС                           КАК СтавкаНДС,
	|	Товары.Склад                               КАК Склад,
	|	Товары.Сделка                              КАК Сделка,
	|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
	|	ВЫБОР 
	|		КОГДА &ИспользоватьГруппыФинУчета
	|		ТОГДА ЕСТЬNULL(Товары.Номенклатура.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка))
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ                                      КАК ГруппаФинансовогоУчета,
	|	Аналитика.Номенклатура.ЕдиницаИзмерения    КАК ЕдиницаИзмерения
	|ИЗ
	|	ТаблицаТоваров КАК Товары
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО Аналитика.КлючАналитики = Товары.АналитикаУчетаНоменклатуры
	|
	|СГРУППИРОВАТЬ ПО 
	|	Товары.АналитикаУчетаНоменклатуры,
	|	Товары.Упаковка,
	|	Товары.ДатаСчетаФактурыКомиссионера,
	|	Товары.СчетФактураВыставленныйКомиссионера,
	|	Товары.Покупатель,
	|	Товары.СтавкаНДС,
	|	Товары.Склад,
	|	Товары.Сделка,
	|	Товары.Назначение,
	|	ВЫБОР 
	|		КОГДА &ИспользоватьГруппыФинУчета
	|		ТОГДА ЕСТЬNULL(Товары.Номенклатура.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка))
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ,
	|	Аналитика.Номенклатура.ЕдиницаИзмерения,
	|	ВЫБОР КОГДА Товары.Количество < 0
	|		ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ
	|УПОРЯДОЧИТЬ ПО
	|	АналитикаУчетаНоменклатуры,
	|	ДатаСчетаФактурыКомиссионера,
	|	Покупатель,
	|	Количество УБЫВ,
	|	Знак УБЫВ
	|");
	
	Запрос.МенеджерВременныхТаблиц = ВременныеТаблицы;
	Запрос.УстановитьПараметр("ИспользоватьГруппыФинУчета", ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоГруппамФинансовогоУчета"));
	Результат = Запрос.ВыполнитьПакет();
	Остатки = Результат[0].Выгрузить();
	ВыборкаТовары = Результат[1].Выбрать();
	
	СтруктураПоиска = Новый Структура("АналитикаУчетаНоменклатуры, Покупатель, ДатаСчетаФактурыКомиссионера, Знак");
	Пока ВыборкаТовары.Следующий() Цикл
		Количество = ВыборкаТовары.Количество;
		СуммаСНДС = ВыборкаТовары.СуммаСНДС;
		СуммаНДС = ВыборкаТовары.СуммаНДС;
		СуммаВознаграждения = ВыборкаТовары.СуммаВознаграждения;
		СуммаНДСВознаграждения = ВыборкаТовары.СуммаНДСВознаграждения;
		КоличествоУпаковок = ВыборкаТовары.КоличествоУпаковок;

		ЗаполнитьЗначенияСвойств(СтруктураПоиска, ВыборкаТовары);
		Для Каждого СтрокаОстатка Из Остатки.НайтиСтроки(СтруктураПоиска) Цикл
			Если СтрокаОстатка.Количество = 0 Тогда
				Продолжить;
			КонецЕсли;
			НоваяСтрока = ВидыЗапасов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаТовары);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаОстатка, "ВидЗапасов, НомерГТД");
			Если Количество <= СтрокаОстатка.Количество Тогда
				Если СуммаСНДС <= СтрокаОстатка.СуммаВыручки ИЛИ СуммаСНДС = 0 ИЛИ НЕ ЭтоОтчетОПродажах Тогда
					НоваяСтрока.СуммаСНДС = СуммаСНДС * ВыборкаТовары.Знак;
					НоваяСтрока.СуммаНДС = СуммаНДС * ВыборкаТовары.Знак;
				Иначе
					НоваяСтрока.СуммаСНДС = СтрокаОстатка.СуммаВыручки * СтрокаОстатка.Знак;
					НоваяСтрока.СуммаНДС = СуммаНДС * ВыборкаТовары.Знак * СтрокаОстатка.СуммаВыручки / СуммаСНДС;
				КонецЕсли;
				НоваяСтрока.КоличествоУпаковок = КоличествоУпаковок * ВыборкаТовары.Знак;
				НоваяСтрока.Количество = Количество * ВыборкаТовары.Знак;
				Если ЭтоОтчетОПродажах Тогда
					НоваяСтрока.СуммаВознаграждения = СуммаВознаграждения * ВыборкаТовары.Знак;
					НоваяСтрока.СуммаНДСВознаграждения = СуммаНДСВознаграждения * ВыборкаТовары.Знак;
				КонецЕсли;
				СтрокаОстатка.Количество = СтрокаОстатка.Количество - Количество;
				СтрокаОстатка.СуммаВыручки = СтрокаОстатка.СуммаВыручки - НоваяСтрока.СуммаСНДС * СтрокаОстатка.Знак;
			Иначе
				НоваяСтрока.СуммаСНДС = СтрокаОстатка.СуммаВыручки * СтрокаОстатка.Знак;
				НоваяСтрока.СуммаНДС = СуммаНДС * ВыборкаТовары.Знак * СтрокаОстатка.СуммаВыручки / СуммаСНДС;
				НоваяСтрока.КоличествоУпаковок = Окр(КоличествоУпаковок * СтрокаОстатка.Количество / Количество, 2, 1) * СтрокаОстатка.Знак;
				НоваяСтрока.Количество = СтрокаОстатка.Количество * СтрокаОстатка.Знак;
				Если ЭтоОтчетОПродажах Тогда
					НоваяСтрока.СуммаВознаграждения = Окр(СуммаВознаграждения * СтрокаОстатка.Количество / Количество, 2, 1) * СтрокаОстатка.Знак;
					НоваяСтрока.СуммаНДСВознаграждения = Окр(СуммаНДСВознаграждения * СтрокаОстатка.Количество / Количество, 2, 1) * СтрокаОстатка.Знак;
				КонецЕсли;
				СтрокаОстатка.Количество = 0;
			КонецЕсли;
			СуммаСНДС = СуммаСНДС - НоваяСтрока.СуммаСНДС * ВыборкаТовары.Знак;
			СуммаНДС = СуммаНДС - НоваяСтрока.СуммаНДС * ВыборкаТовары.Знак;
			КоличествоУпаковок = КоличествоУпаковок - НоваяСтрока.КоличествоУпаковок * ВыборкаТовары.Знак;
			Количество = Количество - НоваяСтрока.Количество * ВыборкаТовары.Знак;
			Если ЭтоОтчетОПродажах Тогда
				СуммаВознаграждения = СуммаВознаграждения - НоваяСтрока.СуммаВознаграждения * ВыборкаТовары.Знак;
				СуммаНДСВознаграждения = СуммаНДСВознаграждения - НоваяСтрока.СуммаНДСВознаграждения * ВыборкаТовары.Знак;
			КонецЕсли;
			Если Количество = 0 Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если Количество <> 0 Тогда // остатков не хватило, генерируем вид запасов - собственный.
			НоваяСтрока = ВидыЗапасов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаТовары);
			НоваяСтрока.СуммаСНДС = СуммаСНДС * ВыборкаТовары.Знак;
			НоваяСтрока.СуммаНДС = СуммаНДС * ВыборкаТовары.Знак;
			НоваяСтрока.КоличествоУпаковок = КоличествоУпаковок * ВыборкаТовары.Знак;
			НоваяСтрока.Количество = Количество * ВыборкаТовары.Знак;
			Если ЭтоОтчетОПродажах Тогда
				НоваяСтрока.СуммаВознаграждения = СуммаВознаграждения * ВыборкаТовары.Знак;
				НоваяСтрока.СуммаНДСВознаграждения = СуммаНДСВознаграждения * ВыборкаТовары.Знак;
			КонецЕсли;
			Если ВидЗапасовПоУмолчанию = Неопределено Тогда
				ВидЗапасовПоУмолчанию = Справочники.ВидыЗапасов.ВидЗапасовДокумента(
					ЗначенияПоУмолчанию.Организация,
					Перечисления.ХозяйственныеОперации.ОтчетПоКомиссииМеждуОрганизациями,
					ЗначенияПоУмолчанию);
			КонецЕсли;
			НоваяСтрока.ВидЗапасов = ВидЗапасовПоУмолчанию;
			
			ДополнительныеСвойства.Вставить("НедостаточноОстатковТоваровОрганизаций", Истина);
			Если КонтролироватьОстаткиТоваровОрганизаций Тогда // добавим в таблицу ошибок
				НоваяСтрокаОшибок = ТаблицаОшибок.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаОшибок, ВыборкаТовары);
				НоваяСтрокаОшибок.Количество = Количество;
				НоваяСтрокаОшибок.Сумма = СуммаСНДС;
				НоваяСтрокаОшибок.ЕдиницаИзмерения	= ВыборкаТовары.ЕдиницаИзмерения;
				Если Не ПерезаполнитьВидыЗапасов Тогда
					Отказ = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

// Функция дополняет таблицы товаров или видов запасов обязательными колонками
//	для формирования временных таблиц данных документа.
//
// Параметры:
//	Таблица - ТаблицаЗначений - Таблица, которая дополняется обязательными колонками.
// Возвращаемое значение:
//	ТаблицаЗначений - Таблица, дополненная обязательными колонками.
//
Функция ТаблицаДополненнаяОбязательнымиКолонками(Таблица) Экспорт
	
	Если Таблица.Колонки.Найти("ДокументРеализации") = Неопределено Тогда
		Таблица.Колонки.Добавить("ДокументРеализации", Новый ОписаниеТипов("ДокументСсылка.РеализацияТоваровУслуг, ДокументСсылка.ОтчетОРозничныхПродажах"));
	КонецЕсли;
	
	Возврат Таблица;
	
КонецФункции

// Процедура заполняет виды запасов в табличной части "Товары" документа поступления товаров.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Требуется временные таблица ИсходнаяТаблицаТоваров
// 			(НомерСтроки, Номенклатура, Сделка, Подразделение, Менеджер, Организация, ХозяйственнаяОперация, ТипЗапасов, Поставщик, Соглашение, Валюта, НалогообложениеНДС) 
//	ТабличнаяЧастьТовары - ТабличнаяЧасть - Табличная часть документа
//	ВидЗапасовДокумента - СправочникСсылка.ВидыЗапасов - Найденный вид запасов (возвращаемое значение). Используется если параметр "ТабличнаяЧастьТовары" имеет значение Неопределено.
//		
Процедура ЗаполнитьВидыЗапасовВТабличнойЧастиТовары(МенеджерВременныхТаблиц, ТабличнаяЧастьТовары, ВидЗапасовДокумента = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Подразделение КАК Подразделение,
	|	ТаблицаТоваров.Менеджер КАК Менеджер,
	|	ТаблицаТоваров.Сделка КАК Сделка,
	|	ТаблицаТоваров.Назначение КАК Назначение,
	|	ТаблицаТоваров.ЭтоВозвратнаяТара КАК ЭтоВозвратнаяТара,
	|	
	|	ЕСТЬNULL(Сделки.ОбособленныйУчетТоваровПоСделке, ЛОЖЬ) КАК ОбособленныйУчетТоваровПоСделке,
	|	СтруктураПредприятия.ВариантОбособленногоУчетаТоваров КАК ВариантОбособленногоУчетаТоваров,
	|	
	|	ВЫБОР
	|		КОГДА
	|			ТаблицаТоваров.ЭтоВозвратнаяТара
	|		ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|		ИНАЧЕ
	|			ТаблицаТоваров.ТипЗапасов
	|	КОНЕЦ КАК ТипЗапасов,
	|	ТаблицаТоваров.Организация,
	|	ВЫБОР
	|		КОГДА
	|			ТаблицаТоваров.ЭтоВозвратнаяТара
	|		ТОГДА
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
	|		ИНАЧЕ
	|			ТаблицаТоваров.ХозяйственнаяОперация
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	ТаблицаТоваров.Соглашение,
	|	ТаблицаТоваров.Валюта,
	|	ТаблицаТоваров.НалогообложениеНДС,
	|	ТаблицаТоваров.НалогообложениеОрганизации,
	|	ТаблицаТоваров.Поставщик,
	|	ТаблицаТоваров.Контрагент,
	|	ТаблицаТоваров.Договор,
	|	
	|	ВЫБОР КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета ТОГДА
	|		ТаблицаТоваров.Номенклатура.ГруппаФинансовогоУчета
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК ГруппаФинансовогоУчета,
	|
	|	ВЫБОР КОГДА ТаблицаТоваров.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) ТОГДА
	|		ТаблицаТоваров.Назначение
	|
	|	КОГДА ЕСТЬNULL(Сделки.ОбособленныйУчетТоваровПоСделке, ЛОЖЬ)
	|		И &ФормироватьВидыЗапасовПоСделкам
	|	ТОГДА
	|		Сделки.Ссылка
	|
	|	КОГДА СтруктураПредприятия.ВариантОбособленногоУчетаТоваров = ЗНАЧЕНИЕ(Перечисление.ВариантыОбособленногоУчетаТоваров.ПоМенеджерамПодразделения)
	|		И &ФормироватьВидыЗапасовПоПодразделениямМенеджерам
	|	ТОГДА
	|		ТаблицаТоваров.Менеджер
	|
	|	КОГДА СтруктураПредприятия.ВариантОбособленногоУчетаТоваров = ЗНАЧЕНИЕ(Перечисление.ВариантыОбособленногоУчетаТоваров.ПоПодразделению)
	|		И &ФормироватьВидыЗапасовПоПодразделениямМенеджерам
	|	ТОГДА
	|		ТаблицаТоваров.Подразделение
	|	ИНАЧЕ
	|		Неопределено
	|	КОНЕЦ КАК АналитикаПредназначения,
	|	ТаблицаТоваров.ГруппаПродукции
	|
	|ПОМЕСТИТЬ ТаблицаТоваровСАналитикой
	|ИЗ
	|	ИсходнаяТаблицаТоваров КАК ТаблицаТоваров
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.СтруктураПредприятия КАК СтруктураПредприятия
	|	ПО
	|		ТаблицаТоваров.Подразделение = СтруктураПредприятия.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.СделкиСКлиентами КАК Сделки
	|	ПО
	|		ТаблицаТоваров.Сделка = Сделки.Ссылка
	|	
	|ГДЕ
	|	ТаблицаТоваров.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), 
	|												   ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки												КАК НомерСтроки,
	|	ТаблицаТоваров.Номенклатура												КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)					КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)							КАК Менеджер,
	|	ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка)						КАК Сделка,
	|	ТаблицаТоваров.Назначение												КАК Назначение,
	|	ЛОЖЬ																	КАК ЭтоВозвратнаяТара,
	|	ЛОЖЬ																	КАК ОбособленныйУчетТоваровПоСделке,
	|	НЕОПРЕДЕЛЕНО															КАК ВариантОбособленногоУчетаТоваров,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга)								КАК ТипЗапасов,
	|	ТаблицаТоваров.Организация												КАК Организация,
	|	ТаблицаТоваров.ХозяйственнаяОперация									КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.СоглашенияСПоставщиками.ПустаяСсылка)				КАК Соглашение,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)								КАК Валюта,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)				КАК НалогообложениеНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)				КАК НалогообложениеОрганизации,
	|	НЕОПРЕДЕЛЕНО															КАК Поставщик,
	|	ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)							КАК Контрагент,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)					КАК Договор,
	|	ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка)	КАК ГруппаФинансовогоУчета,
	|	ТаблицаТоваров.Назначение												КАК АналитикаПредназначения,
	|	ТаблицаТоваров.ГруппаПродукции											КАК ГруппаПродукции
	|	
	|ИЗ
	|	ИсходнаяТаблицаТоваров КАК ТаблицаТоваров
	|ГДЕ
	|	ТаблицаТоваров.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТоваров.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	ТаблицаТоваров.АналитикаПредназначения КАК АналитикаПредназначения,
	|	ТаблицаТоваров.ТипЗапасов КАК ТипЗапасов,
	|	ТаблицаТоваров.Организация,
	|	ТаблицаТоваров.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ТаблицаТоваров.Соглашение,
	|	ТаблицаТоваров.Валюта,
	|	ТаблицаТоваров.НалогообложениеНДС,
	|	ТаблицаТоваров.НалогообложениеОрганизации,
	|	ТаблицаТоваров.Поставщик,
	|	ТаблицаТоваров.Контрагент,
	|	ТаблицаТоваров.Договор,
	|	ТаблицаТоваров.ГруппаПродукции,
	|
	|	ТаблицаТоваров.Менеджер КАК Менеджер,
	|	ТаблицаТоваров.Подразделение КАК Подразделение,
	|	ТаблицаТоваров.Сделка КАК Сделка,
	|	ТаблицаТоваров.Назначение КАК Назначение,
	|	ТаблицаТоваров.ОбособленныйУчетТоваровПоСделке КАК ОбособленныйУчетТоваровПоСделке,
	|	ТаблицаТоваров.ВариантОбособленногоУчетаТоваров КАК ВариантОбособленногоУчетаТоваров,
	|
	|	АналитикаВидовЗапасов.КлючАналитики КАК ВидЗапасов
	|ИЗ
	|	ТаблицаТоваровСАналитикой КАК ТаблицаТоваров
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаВидовЗапасов КАК АналитикаВидовЗапасов
	|	ПО
	|		ТаблицаТоваров.Организация = АналитикаВидовЗапасов.Организация
	|		И ТаблицаТоваров.ТипЗапасов = АналитикаВидовЗапасов.ТипЗапасов
	|		И ТаблицаТоваров.Поставщик = АналитикаВидовЗапасов.Поставщик
	|		И ТаблицаТоваров.Контрагент = АналитикаВидовЗапасов.Контрагент
	|		И ТаблицаТоваров.Соглашение = АналитикаВидовЗапасов.Соглашение
	|		И ТаблицаТоваров.Договор = АналитикаВидовЗапасов.Договор
	|		И ТаблицаТоваров.Валюта = АналитикаВидовЗапасов.Валюта
	|		И ТаблицаТоваров.НалогообложениеНДС = АналитикаВидовЗапасов.НалогообложениеНДС
	|		И ТаблицаТоваров.АналитикаПредназначения = АналитикаВидовЗапасов.АналитикаПредназначения
	|		И ТаблицаТоваров.ГруппаФинансовогоУчета = АналитикаВидовЗапасов.ГруппаФинансовогоУчета
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|
	|ИТОГИ ПО
	|	ГруппаФинансовогоУчета,
	|	АналитикаПредназначения,
	|	ТаблицаТоваров.ГруппаПродукции,
	|	ТипЗапасов,
	|	ЭтоВозвратнаяТара
	|");
	Запрос.УстановитьПараметр("ФормироватьВидыЗапасовПоСделкам", ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоСделкам"));
	Запрос.УстановитьПараметр("ФормироватьВидыЗапасовПоПодразделениямМенеджерам", ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоПодразделениямМенеджерам"));
	Запрос.УстановитьПараметр("ФормироватьВидыЗапасовПоГруппамФинансовогоУчета", ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоГруппамФинансовогоУчета"));
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ВыборкаПоГруппам = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоГруппам.Следующий() Цикл
		
		ВыборкаПоАналитике = ВыборкаПоГруппам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоАналитике.Следующий() Цикл
			
			ВыборкаПоГруппеПродукции = ВыборкаПоАналитике.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаПоГруппеПродукции.Следующий() Цикл
				
				ВыборкаПоТипу = ВыборкаПоГруппеПродукции.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаПоТипу.Следующий() Цикл
				
					ВидЗапасов = Неопределено;
					
					ВыборкаПоТаре = ВыборкаПоТипу.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					Пока ВыборкаПоТаре.Следующий() Цикл
					
						ВидЗапасов = Неопределено;
						
						Выборка = ВыборкаПоТаре.Выбрать();
						Пока Выборка.Следующий() Цикл
					
							Если Не ЗначениеЗаполнено(ВидЗапасов) Тогда
								Если ЗначениеЗаполнено(Выборка.ВидЗапасов) Тогда
									ВидЗапасов = Выборка.ВидЗапасов;
								Иначе
									ВидЗапасов = Справочники.ВидыЗапасов.ВидЗапасовДокумента(
										Выборка.Организация,
										Выборка.ХозяйственнаяОперация,
										Выборка);
								КонецЕсли;
							КонецЕсли;
							
							Если ТабличнаяЧастьТовары <> Неопределено Тогда
								СтрокаТаблицы = ТабличнаяЧастьТовары.Найти(Выборка.НомерСтроки, "НомерСтроки");
								СтрокаТаблицы.ВидЗапасов = ВидЗапасов;
							Иначе
								ВидЗапасовДокумента = ВидЗапасов;
							КонецЕсли;
						
					КонецЦикла;
					
				КонецЦикла
				
			КонецЦикла
			
			КонецЦикла
			
		КонецЦикла
		
	КонецЦикла;
	
КонецПроцедуры

// Процедура выполняет проведение документов по регистру "Себестоимость товаров".
//
// Параметры:
//	Параметры - Строка - Параметры вызова метода.
//	АдресХранилища - Строка - Адрес хранилища, в который помещается результат работы.
//
Процедура ПроведениеДокументовПоРегиструСебестоимостьТоваров(Параметры="", АдресХранилища="") Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	КодЯзыка = Метаданные.ОсновнойЯзык.КодЯзыка;
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Запасы сервер.Проведение документов по регистру себестоимость товаров'", КодЯзыка), 
		УровеньЖурналаРегистрации.Информация, , ,
		НСтр("ru = 'Начато перепроведение документов по регистру ""Себестоимость товаров""'", КодЯзыка), 
		РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
		
	ИспользоватьВидыЗапасов = Константы.УчитыватьСебестоимостьТоваровПоВидамЗапасов.Получить();
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Себестоимость.Регистратор КАК Ссылка,
	|	Себестоимость.МоментВремени КАК МоментВремени
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Себестоимость
	|ГДЕ
	| ТИПЗНАЧЕНИЯ(Себестоимость.Регистратор) <> ТИП(Документ.РасчетСебестоимостиТоваров)
	|
	|УПОРЯДОЧИТЬ ПО
	|	МоментВремени Возр
	|");
	
	Запрос.УстановитьПараметр("ОчиститьВидыЗапасов", НЕ ИспользоватьВидыЗапасов);
	ДопСвойства = Новый Структура("ЭтоНовый, РежимЗаписи", Ложь, РежимЗаписиДокумента.Проведение);
	ДопСвойства.Вставить("ПериодЗаданияКРасчетуСебестоимости", Дата(1,1,1));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		// Проводим документы по регистрам "Себестоимость товаров" и "Выручка и себестоимость продаж".
		НачатьТранзакцию();
		Попытка
			Блокировка = Новый БлокировкаДанных;
			ИмяДокумента = Выборка.Ссылка.Метаданные().Имя;
			ЭлементБлокировки = Блокировка.Добавить("Документ." + ИмяДокумента);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			Блокировка.Заблокировать();
			
			Отказ = Ложь;
			
			ПроведениеСерверУТ.ИнициализироватьДополнительныеСвойстваДляПроведения(Выборка.Ссылка, ДопСвойства, РежимПроведенияДокумента.Неоперативный);
			Документы[ИмяДокумента].ИнициализироватьДанныеДокумента(Выборка.Ссылка, ДопСвойства);
			
			Если ДопСвойства.ТаблицыДляДвижений.Свойство("ТаблицаСебестоимостьТоваров")
			 И ДопСвойства.ТаблицыДляДвижений.ТаблицаСебестоимостьТоваров.Количество() > 0 Тогда
				ЗаписатьДвиженияВБазу(
					РегистрыНакопления.СебестоимостьТоваров, 
					ДопСвойства.ТаблицыДляДвижений.ТаблицаСебестоимостьТоваров,
					Выборка.Ссылка);
				РегистрыСведений.ЗаданияКРасчетуСебестоимости.СоздатьЗаписьРегистра(
					ДопСвойства.ТаблицыДляДвижений.ТаблицаСебестоимостьТоваров[0].Период,
					Выборка.Ссылка,
					ДопСвойства.ТаблицыДляДвижений.ТаблицаСебестоимостьТоваров[0].Организация);
			КонецЕсли;
			Если ДопСвойства.ТаблицыДляДвижений.Свойство("ТаблицаВыручкаИСебестоимостьПродаж") Тогда
				ЗаписатьДвиженияВБазу(
					РегистрыНакопления.ВыручкаИСебестоимостьПродаж, 
					ДопСвойства.ТаблицыДляДвижений.ТаблицаВыручкаИСебестоимостьПродаж,
					Выборка.Ссылка);
			КонецЕсли;
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Ошибка при проведении документа'", КодЯзыка), 
				УровеньЖурналаРегистрации.Ошибка,
				,
				, 
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры 

// Процедура выполняет перепроведение документов, которые некорректно проведены по товарам организаций
//
Процедура ПерепроведениеДокументовПоТоварамОрганизаций() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	КодЯзыка = Метаданные.ОсновнойЯзык.КодЯзыка;
	ЗаписьЖурналаРегистрации(
		НСтр("ru = 'Запасы сервер.Перепроведение документов по товарам организаций'", КодЯзыка), 
		УровеньЖурналаРегистрации.Информация, , ,
		НСтр("ru = 'Начато перепроведение документов по товарам организаций'", КодЯзыка), 
		РежимТранзакцииЗаписиЖурналаРегистрации.Транзакционная);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|    ТоварыОрганизаций.Регистратор КАК Регистратор
	|ИЗ (
	|	ВЫБРАТЬ
	|		ТоварыОрганизаций.АналитикаУчетаНоменклатуры,
	|		ТоварыОрганизаций.Организация,
	|		ТоварыОрганизаций.Регистратор,
	|		МАКСИМУМ(ТоварыОрганизаций.ВидЗапасов) КАК ВидЗапасов,
	|		МАКСИМУМ(ТоварыОрганизаций.НомерГТД) КАК НомерГТД
	|	ИЗ
	|		РегистрНакопления.ТоварыОрганизаций.ОстаткиИОбороты(,, Регистратор) КАК ТоварыОрганизаций
	|	ГДЕ
	|		ТоварыОрганизаций.КоличествоКонечныйОстаток < 0
	|		И Не ТоварыОрганизаций.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|		
	|	СГРУППИРОВАТЬ ПО
	|		ТоварыОрганизаций.АналитикаУчетаНоменклатуры,
	|		ТоварыОрганизаций.Организация,
	|		ТоварыОрганизаций.Регистратор
	|		
	|	) КАК ТоварыОрганизаций
	|	
	|СГРУППИРОВАТЬ ПО
	|	ТоварыОрганизаций.Регистратор
	|    
	|УПОРЯДОЧИТЬ ПО
	|	ТоварыОрганизаций.Регистратор.Дата Возр,
	|	ТоварыОрганизаций.Регистратор 
	|");
	
	Отказ = Ложь;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ЗначениеЗаполнено(Выборка.Регистратор) Тогда
			ДокументОбъект = Выборка.Регистратор.ПолучитьОбъект();
			ДокументОбъект.ДополнительныеСвойства.Вставить("ПерезаполнитьВидыЗапасов", Истина);
			Попытка
				ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			Исключение
				ЗаписьЖурналаРегистрации(
					НСтр("ru = 'Ошибка при проведении документа'", КодЯзыка), 
					УровеньЖурналаРегистрации.Ошибка,
					,
					, 
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ПерепроведениеДокументовПоТоварамОрганизаций()

#КонецОбласти

#Область ПроцедурыРегламентныхЗаданий

// Процедуры обновления информации о доступности товаров.
//
Процедура ОбновитьИнформациюОДоступностиТоваровДляВнешнихПользователей() Экспорт

	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания();
	
	УстановитьПривилегированныйРежим(Истина);

	НаборЗаписей = РегистрыСведений.ДоступностьТоваровДляВнешнихПользователей.СоздатьНаборЗаписей();
	НаборЗаписей.Записать(Истина);

	ВыгрузитьГрафикДоступностьТоваров();

КонецПроцедуры

#КонецОбласти

#Область Прочее

// Получает таблицу остатков к заказу с учетом введенных на основании документов и состояния обеспечения потребности.
//
// Параметры:
//	ДокументСсылка - ДокументСсылка.ЗаказКлиента, ДокументСсылка.ЗаказНаПроизводство, ДокументСсылка.ЗаказНаПеремещение,
//		ДокументСсылка.ЗаказНаВнутреннееПотребление, ДокументСсылка.ЗаказНаРемонт, ДокументСсылка.ЗаказНаСборкуРазборку,
//		ДокументСсылка.ВозвратТоваровОтКлиента - документ-основание
//	ПолучатьТовары - Булево - признак получения товаров
//	ПолучатьУслуги - Булево - признак получения услуг
//	ПолучатьРаботы - Булево - признак получения работ
//	Отбор		   - Произвольный - отбор, реализованный в методе ВременныеТаблицыТоваровЗаказа конкретного документа
//
// Возвращаемое значение:
//	ТаблицаЗначений - таблица остатков
//
Функция ТаблицаОстатковКЗаказу(ДокументСсылка, ПолучатьТовары = Истина, ПолучатьУслуги = Истина, ПолучатьРаботы = Истина, Отбор = Неопределено) Экспорт
	
	ИмяДокумента = ДокументСсылка.Метаданные().Имя;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ЗначениеЗаполнено(Отбор) Тогда
		Документы[ИмяДокумента].ВременныеТаблицыТоваровЗаказа(МенеджерВременныхТаблиц, ДокументСсылка, Отбор);
	Иначе
		Документы[ИмяДокумента].ВременныеТаблицыТоваровЗаказа(МенеджерВременныхТаблиц, ДокументСсылка);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОбеспечениеТоварами.Номенклатура КАК Номенклатура,
	|	ОбеспечениеТоварами.Характеристика КАК Характеристика,
	|	ОбеспечениеТоварами.Склад КАК Склад,
	|	ОбеспечениеТоварами.Назначение КАК Назначение,
	|	ОбеспечениеТоварами.КЗаказуОстаток КАК КЗаказу
	|ПОМЕСТИТЬ Потребность
	|ИЗ
	|	РегистрНакопления.ОбеспечениеЗаказов.Остатки(
	|			,
	|			(Номенклатура, Характеристика, Склад, Назначение) В
	|				(ВЫБРАТЬ
	|					Т.Номенклатура,
	|					Т.Характеристика,
	|					Т.Склад,
	|					Т.Назначение
	|				ИЗ
	|					НоменклатураЗаказаОбособленная КАК Т)) КАК ОбеспечениеТоварами
	|ГДЕ
	|	ОбеспечениеТоварами.КЗаказуОстаток > 0
	|	И &ПолучатьТовары
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОбеспечениеРаботами.Номенклатура,
	|	ОбеспечениеРаботами.Характеристика,
	|	ОбеспечениеРаботами.Подразделение,
	|	ОбеспечениеРаботами.Назначение,
	|	ОбеспечениеРаботами.КОбеспечениюОстаток
	|ИЗ
	|	РегистрНакопления.ОбеспечениеЗаказовРаботами.Остатки(
	|			,
	|			(Номенклатура, Характеристика, Подразделение, Назначение) В
	|				(ВЫБРАТЬ
	|					Т.Номенклатура,
	|					Т.Характеристика,
	|					Т.Подразделение,
	|					Т.Назначение
	|				ИЗ
	|					НоменклатураЗаказаОбособленная КАК Т)) КАК ОбеспечениеРаботами
	|ГДЕ
	|	ОбеспечениеРаботами.КОбеспечениюОстаток > 0
	|	И &ПолучатьРаботы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Назначение,
	|	Номенклатура,
	|	Характеристика,
	|	Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таб.НомерСтроки КАК НомерСтроки,
	|	Таб.Номенклатура КАК Номенклатура,
	|	Таб.Характеристика КАК Характеристика,
	|	Таб.Склад КАК Склад,
	|	Таб.Подразделение КАК Подразделение,
	|	Таб.Назначение КАК Назначение,
	|	Таб.Упаковка КАК Упаковка,
	|	Таб.Количество КАК Количество
	|ПОМЕСТИТЬ ВТОбособленные
	|ИЗ
	|	(ВЫБРАТЬ
	|		Т.Номенклатура КАК Номенклатура,
	|		Т.Характеристика КАК Характеристика,
	|		Т.Склад КАК Склад,
	|		Т.Подразделение КАК Подразделение,
	|		Т.Назначение КАК Назначение,
	|		ВЫБОР
	|			КОГДА Т.Заказано > ВтКЗаказу.КЗаказу
	|				ТОГДА ВтКЗаказу.КЗаказу
	|			ИНАЧЕ Т.Заказано
	|		КОНЕЦ КАК Количество,
	|		Т.Упаковка КАК Упаковка,
	|		Т.НомерСтроки КАК НомерСтроки
	|	ИЗ
	|		НоменклатураЗаказаОбособленная КАК Т
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Потребность КАК ВтКЗаказу
	|			ПО Т.Номенклатура = ВтКЗаказу.Номенклатура
	|				И Т.Характеристика = ВтКЗаказу.Характеристика
	|				И Т.Склад = ВтКЗаказу.Склад
	|				И Т.Назначение = ВтКЗаказу.Назначение
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Т.Номенклатура,
	|		Т.Характеристика,
	|		НЕОПРЕДЕЛЕНО,
	|		Т.Подразделение,
	|		Т.Назначение,
	|		ВЫБОР
	|			КОГДА Т.Заказано > ВтКЗаказу.КЗаказу
	|				ТОГДА ВтКЗаказу.КЗаказу
	|			ИНАЧЕ Т.Заказано
	|		КОНЕЦ,
	|		Т.Упаковка,
	|		Т.НомерСтроки
	|	ИЗ
	|		НоменклатураЗаказаОбособленная КАК Т
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Потребность КАК ВтКЗаказу
	|			ПО Т.Номенклатура = ВтКЗаказу.Номенклатура
	|				И Т.Характеристика = ВтКЗаказу.Характеристика
	|				И Т.Подразделение = ВтКЗаказу.Склад
	|				И Т.Назначение = ВтКЗаказу.Назначение) КАК Таб
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураЗаказаНеОбособленная.Номенклатура,
	|	НоменклатураЗаказаНеОбособленная.Характеристика,
	|	НоменклатураЗаказаНеОбособленная.Заказано КАК Количество,
	|	НоменклатураЗаказаНеОбособленная.Упаковка,
	|	НоменклатураЗаказаНеОбособленная.Склад,
	|	НоменклатураЗаказаНеОбособленная.Подразделение,
	|	НоменклатураЗаказаНеОбособленная.НомерСтроки
	|ПОМЕСТИТЬ ВТНеОбособленные
	|ИЗ
	|	НоменклатураЗаказаНеОбособленная КАК НоменклатураЗаказаНеОбособленная
	|ГДЕ
	|	(НоменклатураЗаказаНеОбособленная.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|				И &ПолучатьТовары
	|			ИЛИ НоменклатураЗаказаНеОбособленная.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|				И &ПолучатьУслуги
	|			ИЛИ НоменклатураЗаказаНеОбособленная.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|				И &ПолучатьРаботы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТОбособленные.НомерСтроки КАК НомерСтроки,
	|	ВТОбособленные.Номенклатура КАК Номенклатура,
	|	ВТОбособленные.Характеристика КАК Характеристика,
	|	ВТОбособленные.Номенклатура.СтавкаНДС КАК СтавкаНДС,
	|	ВТОбособленные.Назначение КАК Назначение,
	|	ВТОбособленные.Упаковка КАК Упаковка,
	|	ВТОбособленные.Количество КАК Количество,
	|	ВТОбособленные.Количество / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки1, 1) КАК КоличествоУпаковок,
	|	ВТОбособленные.Склад КАК Склад,
	|	ВТОбособленные.Подразделение КАК Подразделение,
	|	ВЫБОР КОГДА ВТОбособленные.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга) ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК СписатьНаРасходы
	|ИЗ
	|	ВТОбособленные КАК ВТОбособленные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТНеОбособленные.НомерСтроки,
	|	ВТНеОбособленные.Номенклатура,
	|	ВТНеОбособленные.Характеристика,
	|	ВТНеОбособленные.Номенклатура.СтавкаНДС,
	|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка),
	|	ВТНеОбособленные.Упаковка,
	|	ВТНеОбособленные.Количество,
	|	ВТНеОбособленные.Количество / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки2, 1) КАК КоличествоУпаковок,
	|	ВТНеОбособленные.Склад,
	|	ВТНеОбособленные.Подразделение,
	|	ВЫБОР КОГДА ВТНеОбособленные.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга) ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ
	|ИЗ
	|	ВТНеОбособленные КАК ВТНеОбособленные
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ВТОбособленные.Упаковка",
		"ВТОбособленные.Номенклатура"));
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ВТНеОбособленные.Упаковка",
		"ВТНеОбособленные.Номенклатура"));
		
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ПолучатьТовары", ПолучатьТовары);
	Запрос.УстановитьПараметр("ПолучатьУслуги", ПолучатьУслуги);
	Запрос.УстановитьПараметр("ПолучатьРаботы", ПолучатьРаботы);
	
	ТаблицаТоваров = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаТоваров;
	
КонецФункции

// Дополняет таблицу видов запасов документом реализации и складом отгрузки
//
// Параметры:
//	ДокументСсылка - ДокументСсылка.ВозвратТоваровОтКлиента, ДокументСсылка.ВозвратТоваровМеждуОрганизациями - документ, формирующий движения
//	Организация - СправочникСсылка.Организации - Организация-владелец товара.
//	Комиссионер - СправочникСсылка.Партнеры - Комиссионер, которому был передан товар на реализацию.
//	Дата - Дата - период получения остатков
//	Соглашение - СправочникСсылка.СоглашенияСКлиентами - Соглашение, по которому производится реализация.
//	ВидыЗапасов - ТабличнаяЧасть - таб. часть для дополнения
//	Ресурсы - Строка - ресурсы табличной части виды запасов, которые необходимо распределить проворционально количеству
//
// Возвращаемое значение:
//	ТаблицаЗначений - таблица ошибок подбора документов реализации
//
Функция ДополнитьВидыЗапасовДокументомРеализации(ДокументСсылка, Организация, Комиссионер, Дата, Соглашение, ВидыЗапасов, Знач Ресурсы = "") Экспорт
	
#Область ТекстЗапроса
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Т.АналитикаУчетаНоменклатуры,
	|	&ВидЗапасов,
	|	Т.НомерГТД,
	|	Т.НомерСтроки
	|ПОМЕСТИТЬ ВтВидыЗапасов
	|ИЗ
	|	&ВидыЗапасов КАК Т
	|ГДЕ
	|	Т.ДокументРеализации = НЕОПРЕДЕЛЕНО
	|	ИЛИ Т.ДокументРеализации = ЗНАЧЕНИЕ(Документ.ПередачаТоваровМеждуОрганизациями.ПустаяСсылка)
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	(ВЫБОР
	|		КОГДА ЕСТЬNULL(Возврат.ХозяйственнаяОперация, НЕОПРЕДЕЛЕНО)
	|		 = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровМеждуОрганизациями)
	|			ТОГДА Т.АналитикаУчетаНоменклатуры
	|		ИНАЧЕ АналитикаПередачи.КлючАналитики КОНЕЦ) КАК АналитикаУчетаНоменклатуры,
	|	Аналитика.Номенклатура,
	|	Аналитика.Характеристика,
	|	Аналитика.Серия,
	|	Т.ВидЗапасов,
	|	Т.НомерГТД,
	|	Т.НомерСтроки
	|ПОМЕСТИТЬ ВидыЗапасов
	|ИЗ
	|	ВтВидыЗапасов КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|		ПО Аналитика.КлючАналитики = Т.АналитикаУчетаНоменклатуры
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПередачи
	|		ПО АналитикаПередачи.Номенклатура = Аналитика.Номенклатура
	|		И АналитикаПередачи.Характеристика = Аналитика.Характеристика
	|		И АналитикаПередачи.Назначение = Аналитика.Назначение
	|		И АналитикаПередачи.Серия = &Серия
	|		И АналитикаПередачи.Склад = &Комиссионер
	//++ НЕ УТ 
	|		И ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) = АналитикаПередачи.СтатьяКалькуляции
	//-- НЕ УТ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасов
	|		ПО ВидыЗапасов.Ссылка = Т.ВидЗапасов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровМеждуОрганизациями КАК Возврат
	|		ПО Возврат.Ссылка = &Ссылка
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры,
	|	ВидЗапасов,
	|	НомерГТД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.АналитикаУчетаНоменклатуры,
	|	Товары.ВидЗапасов,
	|	Товары.НомерГТД,
	|	Товары.СкладОтгрузки,
	|	Товары.ДокументРеализации,
	|	СУММА(Товары.Количество) КАК КоличествоОстаток
	|ПОМЕСТИТЬ ПереданныеТовары
	|ИЗ (
	|	ВЫБРАТЬ // ВидыЗапасов, переданные на комисию
	|		ВЫБОР КОГДА Товары.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
	|			Товары.ДокументРеализации
	|		ИНАЧЕ
	|			Товары.Регистратор
	|		КОНЕЦ КАК ДокументРеализации,
	|		ЕСТЬNULL(АналитикаОтгрузки.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)) КАК СкладОтгрузки,
	|		Товары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		Товары.ВидЗапасов КАК ВидЗапасов,
	|		Товары.НомерГТД КАК НомерГТД,
	|		ВЫБОР КОГДА Товары.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
	|			-Товары.Количество
	|		ИНАЧЕ
	|			Товары.Количество
	|		КОНЕЦ КАК Количество
	|	ИЗ
	|		РегистрНакопления.ТоварыПереданныеНаКомиссию КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВидыЗапасов КАК ВидыЗапасов
	|			ПО Товары.АналитикаУчетаНоменклатуры = ВидыЗапасов.АналитикаУчетаНоменклатуры
	|			И Товары.ВидЗапасов = ВидыЗапасов.ВидЗапасов
	|			И Товары.НомерГТД = ВидыЗапасов.НомерГТД
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаОтгрузки
	|			ПО Товары.КорАналитикаУчетаНоменклатуры = АналитикаОтгрузки.КлючАналитики
	|	ГДЕ
	|		НЕ Товары.Регистратор = &Ссылка
	|		И (Товары.ДокументРеализации <> НЕОПРЕДЕЛЕНО ИЛИ Товары.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
	|		И Товары.Период <= &Дата
	|		И Товары.Организация = &Организация
	|		И Товары.Соглашение = &Соглашение
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ
	|		ВЫБОР КОГДА Товары.Количество < 0 ТОГДА
	|			Товары.ДокументРеализации
	|		ИНАЧЕ
	|			Товары.Регистратор
	|		КОНЕЦ КАК ДокументРеализации,
	|		ЕСТЬNULL(Аналитика.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)) КАК СкладОтгрузки,
	|		ВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		Товары.ВидЗапасов КАК ВидЗапасов,
	|		Товары.НомерГТД КАК НомерГТД,
	|		Товары.Количество КАК Количество
	|	ИЗ
	|		РегистрНакопления.ТоварыОрганизаций КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|			ПО Аналитика.Ссылка = Товары.АналитикаУчетаНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВидыЗапасов КАК ВидыЗапасов
	|			ПО Аналитика.Номенклатура = ВидыЗапасов.Номенклатура
	|			И Аналитика.Характеристика = ВидыЗапасов.Характеристика
	|			И Товары.ВидЗапасов = ВидыЗапасов.ВидЗапасов
	|			И Товары.НомерГТД = ВидыЗапасов.НомерГТД
	|	ГДЕ
	|		НЕ Товары.Регистратор = &Ссылка
	|		И Товары.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Товары.Период <= &Дата
	|		И Товары.Организация = &Организация
	|		И Товары.Организация = Товары.ОрганизацияОтгрузки
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	ВЫБРАТЬ
	|		ВЫБОР КОГДА КПередаче.Возвращено > 0 ТОГДА
	|			КПередаче.ДокументРеализации
	|		ИНАЧЕ
	|			КПередаче.Регистратор
	|		КОНЕЦ КАК ДокументРеализации,
	|		Аналитика.Склад КАК СкладОтгрузки,
	|		ВидыЗапасовДокумента.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ВидыЗапасовВладельца.Ссылка КАК ВидЗапасов,
	|		КПередаче.НомерГТД КАК НомерГТД,
	|		КПередаче.Количество - КПередаче.Возвращено КАК Количество
	|	ИЗ
	|		РегистрНакопления.ТоварыОрганизацийКПередаче КАК КПередаче
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|			ПО Аналитика.Ссылка = КПередаче.АналитикаУчетаНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВидыЗапасов КАК ВидыЗапасовДокумента
	|			ПО Аналитика.Номенклатура = ВидыЗапасовДокумента.Номенклатура
	|			И Аналитика.Характеристика = ВидыЗапасовДокумента.Характеристика
	|			И Аналитика.Серия = ВидыЗапасовДокумента.Серия
	|			И КПередаче.ВидЗапасовПродавца.ВидЗапасовВладельца = ВидыЗапасовДокумента.ВидЗапасов
	|			И КПередаче.НомерГТД = ВидыЗапасовДокумента.НомерГТД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасовВладельца
	|			ПО ВидыЗапасовВладельца.Ссылка = КПередаче.ВидЗапасовПродавца.ВидЗапасовВладельца
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаТоваровМеждуОрганизациями КАК Передача
	|			ПО Передача.Ссылка = КПередаче.Регистратор
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаТоваровМеждуОрганизациями КАК ПередачаВозвращено
	|			ПО ПередачаВозвращено.Ссылка = КПередаче.ДокументРеализации
	|	ГДЕ
	|		НЕ КПередаче.Регистратор = &Ссылка
	|		И КПередаче.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И КПередаче.Период <= &Дата
	|		И КПередаче.ВидЗапасовПродавца.Организация = &Комиссионер
	|		И КПередаче.ОрганизацияВладелец = &Организация
	|
	|	) КАК Товары
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.СкладОтгрузки,
	|	Товары.ДокументРеализации,
	|	Товары.АналитикаУчетаНоменклатуры,
	|	Товары.ВидЗапасов,
	|	Товары.НомерГТД
	|
	|ИМЕЮЩИЕ
	|	СУММА(Товары.Количество) <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры,
	|	ВидЗапасов,
	|	НомерГТД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыЗапасов.НомерСтроки,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	Т.НомерГТД,
	|	Т.СкладОтгрузки,
	|	Т.ДокументРеализации,
	|	Т.КоличествоОстаток
	|ИЗ
	|	ПереданныеТовары КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВидыЗапасов КАК ВидыЗапасов
	|		ПО Т.АналитикаУчетаНоменклатуры = ВидыЗапасов.АналитикаУчетаНоменклатуры
	|		И Т.ВидЗапасов = ВидыЗапасов.ВидЗапасов
	|		И Т.НомерГТД = ВидыЗапасов.НомерГТД
	|УПОРЯДОЧИТЬ ПО
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	Т.НомерГТД,
	|	Т.ДокументРеализации.МоментВремени УБЫВ,
	|	Т.СкладОтгрузки,
	|	ВидыЗапасов.НомерСтроки ВОЗР
	|ИТОГИ МАКСИМУМ(Т.КоличествоОстаток)
	|ПО
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидЗапасов,
	|	Т.НомерГТД,
	|	Т.ДокументРеализации,
	|	Т.СкладОтгрузки
	|");
#КонецОбласти
	
	Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями") Тогда
		СписокПолей = "НомерСтроки, АналитикаУчетаНоменклатуры, ВидЗапасовПолучателя, НомерГТД, ДокументРеализации";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ВидЗапасов", "Т.ВидЗапасовПолучателя КАК ВидЗапасов");
	Иначе
		СписокПолей = "НомерСтроки, АналитикаУчетаНоменклатуры, ВидЗапасов, НомерГТД, ДокументРеализации";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ВидЗапасов", "Т.ВидЗапасов КАК ВидЗапасов");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ВидыЗапасов",	ВидыЗапасов.Выгрузить(, СписокПолей));
	Запрос.УстановитьПараметр("Ссылка",			ДокументСсылка);
	Запрос.УстановитьПараметр("Организация",	Организация);
	Запрос.УстановитьПараметр("Комиссионер",	Комиссионер);
	Запрос.УстановитьПараметр("Соглашение",		Соглашение);
	Запрос.УстановитьПараметр("Дата",			Дата + 1);
	Запрос.УстановитьПараметр("Серия",			Справочники.СерииНоменклатуры.ПустаяСсылка());
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Ресурсы = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(?(Ресурсы = "", "СуммаСНДС,СуммаНДС", Ресурсы), ",");
	
	СтрокКПроверке = ВидыЗапасов.Количество();
	
	Выборки = ИнициализироватьВыборки(Запрос, 5);
	ВыборкаБезНомеровСтрок = Неопределено;
	МассивСтрокКУдалению = Новый Массив;
	Пока СледующаяВыборка(Выборки, ВыборкаБезНомеровСтрок, 5) Цикл
		
		Остаток = ВыборкаБезНомеровСтрок.КоличествоОстаток;
		
		Выборка = ВыборкаБезНомеровСтрок.Выбрать(ОбходРезультатаЗапроса.Прямой);
		Пока Выборка.Следующий() Цикл
			
			СтрокаЗапасов = ВидыЗапасов[Выборка.НомерСтроки - 1];
			
			Если СтрокаЗапасов.Количество = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Если СтрокаЗапасов.Количество > 0 Тогда
				Количество = Мин(Остаток, СтрокаЗапасов.Количество);
			Иначе
				Количество = Макс(Остаток, СтрокаЗапасов.Количество);
			КонецЕсли;
			
			НоваяСтрока = ВидыЗапасов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗапасов);
			
			НоваяСтрока.ДокументРеализации = Выборка.ДокументРеализации;
			НоваяСтрока.СкладОтгрузки = Выборка.СкладОтгрузки;
			
			Если Количество <> СтрокаЗапасов.Количество И СтрокаЗапасов.Количество <> 0 Тогда
				Для Каждого Ресурс Из Ресурсы Цикл
					НоваяСтрока[Ресурс] = ?(СтрокаЗапасов[Ресурс] <> 0, Количество * СтрокаЗапасов[Ресурс] / СтрокаЗапасов.Количество, 0);
				КонецЦикла;
				НоваяСтрока.Количество = Количество;
			КонецЕсли;
			
			Для Каждого Ресурс Из Ресурсы Цикл
				СтрокаЗапасов[Ресурс] = СтрокаЗапасов[Ресурс] - НоваяСтрока[Ресурс];
			КонецЦикла;
			СтрокаЗапасов.Количество = СтрокаЗапасов.Количество - НоваяСтрока.Количество;
			
			Остаток = Остаток - НоваяСтрока.Количество;
			
			Если СтрокаЗапасов.Количество = 0 Тогда
				МассивСтрокКУдалению.Добавить(СтрокаЗапасов);
			КонецЕсли;
			
			Если Остаток = 0 Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Для Каждого СтрокаЗапасов Из МассивСтрокКУдалению Цикл
		ВидыЗапасов.Удалить(СтрокаЗапасов);
	КонецЦикла;
	
#Область Проверка
	ТаблицаОшибок = Новый ТаблицаЗначений;
	БезДокументаРеализации = СтрокКПроверке - МассивСтрокКУдалению.Количество();
	
	Если БезДокументаРеализации > 0 Тогда
		
		МассивСтрокСОшибками = Новый Массив;
		Для Индекс = 0 По БезДокументаРеализации - 1 Цикл
			// В этом диапазоне остались строки, к которым не удалось подобрать соответствующий документ передачи на комиссию
			МассивСтрокСОшибками.Добавить(ВидыЗапасов[Индекс]);
		КонецЦикла;
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Т.АналитикаУчетаНоменклатуры,
		|	Т.Количество
		|ПОМЕСТИТЬ ВидыЗапасов
		|ИЗ
		|	&ВидыЗапасов КАК Т
		|ГДЕ
		|	Т.ДокументРеализации = НЕОПРЕДЕЛЕНО
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	А.Номенклатура,
		|	А.Характеристика,
		|	Н.ЕдиницаИзмерения,
		|	СУММА(Т.Количество) КАК Количество
		|ИЗ
		|	ВидыЗапасов КАК Т
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК А
		|		ПО А.КлючАналитики = Т.АналитикаУчетаНоменклатуры
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Н
		|		ПО Н.Ссылка = А.Номенклатура
		|СГРУППИРОВАТЬ ПО
		|	А.Номенклатура,
		|	А.Характеристика,
		|	Н.ЕдиницаИзмерения
		|");
		
		Запрос.УстановитьПараметр(
			"ВидыЗапасов",
			ВидыЗапасов.Выгрузить(МассивСтрокСОшибками, "АналитикаУчетаНоменклатуры, Количество, ДокументРеализации"));
		ТаблицаОшибок = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.Прямой);
		
	КонецЕсли;
#КонецОбласти
	
	Возврат ТаблицаОшибок;
	
КонецФункции

#КонецОбласти

#Область УсловноеОформление

// Устанавливаем условное оформление для подразделения, если используются виды запасов по подразделениям
//
// Параметры:
// 		Форма - Форма - Содержит данную форму 
// 		ИмяПоляВводаПодразделения - Строка - Наименование элемента формы, содержащего подразделение,
//											   если оно отличается от "Подразделение"
// 		ПутьКПолюОтбора - Строка - Полный путь к реквизиту "Подразделение",
//									если он отличается от "Объект.Подразделение"
// 
Процедура УстановитьУсловноеОформлениеПодразделенияДляВидовЗапасов(Форма,
																	ИмяПоляВводаПодразделения = "Подразделение",
																	ПутьКПолюОтбора = "Объект.Подразделение") Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоПодразделениямМенеджерам") Тогда
		Возврат;
	КонецЕсли;
	
	УсловноеОформление = Форма.УсловноеОформление;
	ЭлементыФормы = Форма.Элементы;
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементыФормы[ИмяПоляВводаПодразделения].Имя);
	
	ЭлементыФормы[ИмяПоляВводаПодразделения].АвтоОтметкаНезаполненного = Истина;
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьКПолюОтбора);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;

	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СлужебныйПрограммныйИнтерфейс

#Область ПроцедурыЗаписиДвиженийВРегистр

Процедура ОтразитьТоварыПереданныеПереработчику(ДополнительныеСвойства, Движения, Отказ) Экспорт
	
	Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаТоварыПереданныеПереработчику;
	
	Если Отказ ИЛИ Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Движения.ТоварыПереданныеПереработчику.Записывать = Истина;
	Движения.ТоварыПереданныеПереработчику.Загрузить(Таблица);
	
КонецПроцедуры

Процедура ОтразитьТоварыПолученныеОтПереработчика(ДополнительныеСвойства, Движения, Отказ) Экспорт
	
	Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаТоварыПолученныеОтПереработчика;
	
	Если Отказ ИЛИ Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Движения.ТоварыПолученныеОтПереработчика.Записывать = Истина;
	Движения.ТоварыПолученныеОтПереработчика.Загрузить(Таблица);
	
КонецПроцедуры

Процедура ОтразитьТоварыНаСкладах(ДополнительныеСвойства, Движения, Отказ) Экспорт

	Таблица= ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаТоварыНаСкладах;
	
	Если Отказ ИЛИ Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Движения.ТоварыНаСкладах.Записывать = Истина;
	Движения.ТоварыНаСкладах.Загрузить(Таблица);
	
КонецПроцедуры

Процедура ОтразитьДатыПоступленияТоваровОрганизаций(ДополнительныеСвойства, Отказ) Экспорт

	Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДатыПоступленияТоваровОрганизаций;
	
	Если Отказ ИЛИ Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из Таблица Цикл
		МенеджерЗаписи = РегистрыСведений.ДатыПоступленияТоваровОрганизаций.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, СтрокаТаблицы);
		МенеджерЗаписи.Записать(Истина);	
	КонецЦикла;

КонецПроцедуры // ОтразитьДатыПоступленияТоваровОрганизаций()

Процедура ОтразитьТоварыОрганизаций(ДополнительныеСвойства, Движения, Отказ) Экспорт

	Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаТоварыОрганизаций;
	
	Если Отказ ИЛИ Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Движения.ТоварыОрганизаций.Записывать = Истина;
	Движения.ТоварыОрганизаций.Загрузить(Таблица);
	
КонецПроцедуры

Процедура ОтразитьТоварыОрганизацийКПередаче(ДополнительныеСвойства, Движения, Отказ) Экспорт

	Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаТоварыОрганизацийКПередаче;
	
	Если Отказ ИЛИ Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Движения.ТоварыОрганизацийКПередаче.Записывать = Истина;
	Движения.ТоварыОрганизацийКПередаче.Загрузить(Таблица);
	
КонецПроцедуры 

Процедура ОтразитьТоварыОрганизацийКОформлению(ДополнительныеСвойства, Движения, Отказ) Экспорт

	Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаТоварыОрганизацийКОформлению;
	
	Если Отказ ИЛИ Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Движения.ТоварыОрганизацийКОформлению.Записывать = Истина;
	Движения.ТоварыОрганизацийКОформлению.Загрузить(Таблица);
	
КонецПроцедуры

Процедура ОтразитьТоварыПереданныеНаКомиссию(ДополнительныеСвойства, Движения, Отказ) Экспорт

	Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаТоварыПереданныеНаКомиссию;
	
	Если Отказ ИЛИ Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Движения.ТоварыПереданныеНаКомиссию.Записывать = Истина;
	Движения.ТоварыПереданныеНаКомиссию.Загрузить(Таблица);

КонецПроцедуры

Процедура ОтразитьДатыПередачиТоваровНаКомиссию(ДополнительныеСвойства, Отказ) Экспорт

	Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДатыПередачиТоваровНаКомиссию;
	
	Если Отказ ИЛИ Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из Таблица Цикл
		МенеджерЗаписи = РегистрыСведений.ДатыПередачиТоваровНаКомиссию.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, СтрокаТаблицы);
		МенеджерЗаписи.Записать(Истина);	
	КонецЦикла;

КонецПроцедуры 

Процедура ОтразитьТоварыКОформлениюОтчетовКомитента(ДополнительныеСвойства, Движения, Отказ) Экспорт

	Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаТоварыКОформлениюОтчетовКомитенту;
	
	Если Отказ ИЛИ Таблица .Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Движения.ТоварыКОформлениюОтчетовКомитенту.Записывать = Истина;
	Движения.ТоварыКОформлениюОтчетовКомитенту.Загрузить(Таблица);
	
КонецПроцедуры 

Процедура ОтразитьУслугиКОформлениюОтчетовПринципалу(ДополнительныеСвойства, Движения, Отказ) Экспорт

	Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаУслугиКОформлениюОтчетовПринципалу;
	
	Если Отказ ИЛИ Таблица .Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Движения.УслугиКОформлениюОтчетовПринципалу.Записывать = Истина;
	Движения.УслугиКОформлениюОтчетовПринципалу.Загрузить(Таблица);
	
КонецПроцедуры

Процедура ОтразитьСвободныеОстатки(ДополнительныеСвойства, Движения, Отказ) Экспорт

	Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаСвободныеОстатки;
	
	Если Отказ ИЛИ Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Движения.СвободныеОстатки.Записывать = Истина;
	Движения.СвободныеОстатки.Загрузить(Таблица);

КонецПроцедуры

Процедура ОтразитьТоварыКОформлениюИзлишковНедостач(ДополнительныеСвойства, Движения, Отказ) Экспорт

	Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаТоварыКОформлениюИзлишковНедостач;

	Если Отказ ИЛИ Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Движения.ТоварыКОформлениюИзлишковНедостач.Записывать = Истина;
	Движения.ТоварыКОформлениюИзлишковНедостач.Загрузить(Таблица);

КонецПроцедуры

Процедура ОтразитьОбеспечениеЗаказов(ДополнительныеСвойства, Движения, Отказ) Экспорт
	
	Таблица= ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаОбеспечениеЗаказов;
	
	Если Отказ ИЛИ Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Движения.ОбеспечениеЗаказов.Записывать = Истина;
	Движения.ОбеспечениеЗаказов.Загрузить(Таблица);

КонецПроцедуры

Процедура ОтразитьОбеспечениеЗаказовРаботами(ДополнительныеСвойства, Движения, Отказ) Экспорт
	
	Таблица = ДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаОбеспечениеЗаказовРаботами;
	
	Если Отказ Или Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Движения.ОбеспечениеЗаказовРаботами.Записывать = Истина;
	Движения.ОбеспечениеЗаказовРаботами.Загрузить(Таблица);

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ПроцедурыФормированияТаблицыДоступныхВидовЗапасов

// Процедура формирует запрос по данным для заполнения видов запасов документа.
//
Функция ЗапросДанныеЗаполненияВидовЗапасов(МенеджерВременныхТаблиц, ДополнительныеСвойства)
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	МАКСИМУМ(ТаблицаТоваров.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ЕСТЬNULL(АналитикаКомиссионера.КлючАналитики, ТаблицаТоваров.АналитикаУчетаНоменклатуры) КАК АналитикаОстатков,
	|	СпрНоменклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СпрНоменклатура.ПодакцизныйТовар КАК ЭтоПодакцизныйТовар,
	|	СпрНоменклатура.ВестиУчетПоГТД КАК ВестиУчетПоГТД,
	|	Аналитика.Номенклатура КАК Номенклатура,
	|	Аналитика.Характеристика КАК Характеристика,
	|	Аналитика.Серия КАК Серия,
	|	ТаблицаТоваров.Склад КАК Склад,
	|	ТаблицаТоваров.ДокументРеализации КАК ДокументРеализации,
	|	ТаблицаТоваров.ПодбиратьВидыЗапасов КАК ПодбиратьВидыЗапасов,
	|
	|	ВЫБОР КОГДА ТаблицаТоваров.Сделка.ОбособленныйУчетТоваровПоСделке
	|		И &ФормироватьВидыЗапасовПоСделкам
	|	ТОГДА
	|		ТаблицаТоваров.Сделка
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка)
	|	КОНЕЦ КАК Сделка,
	|	Аналитика.Назначение КАК Назначение,
	|	ЕСТЬNULL(Аналитика.Назначение.Заказ, Неопределено) КАК Заказ,
	|
	|	МАКСИМУМ(ТаблицаТоваров.СтавкаНДС) КАК СтавкаНДС,
	|	СУММА(ТаблицаТоваров.Количество) КАК Количество,
	|	СУММА(ТаблицаТоваров.СуммаСНДС) КАК СуммаСНДС,
	|	СУММА(ТаблицаТоваров.СуммаНДС) КАК СуммаНДС,
	|	СУММА(ТаблицаТоваров.СуммаВознаграждения) КАК СуммаВознаграждения,
	|	СУММА(ТаблицаТоваров.СуммаНДСВознаграждения) КАК СуммаНДСВознаграждения
	|	
	|ПОМЕСТИТЬ ТаблицаДокумента
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО
	|		ТаблицаТоваров.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.Номенклатура КАК СпрНоменклатура
	|	ПО
	|		Аналитика.Номенклатура = СпрНоменклатура.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокумента КАК ТаблицаДанныхДокумента
	|	ПО
	|		ИСТИНА
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаКомиссионера
	|	ПО
	|		ТаблицаТоваров.Номенклатура = АналитикаКомиссионера.Номенклатура
	|		И ТаблицаТоваров.Характеристика = АналитикаКомиссионера.Характеристика
	|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаКомиссионера.Назначение
	|		И ТаблицаДанныхДокумента.Партнер = АналитикаКомиссионера.Склад
	|		И ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) = АналитикаКомиссионера.Серия
	//++ НЕ УТ 
	|		И ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) = АналитикаКомиссионера.СтатьяКалькуляции
	//-- НЕ УТ
	|		И (ТаблицаДанныхДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера)
	|			ИЛИ ТаблицаДанныхДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтПереработчика))
	|	
	|ГДЕ
	|	ТаблицаТоваров.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры,
	|	ЕСТЬNULL(АналитикаКомиссионера.КлючАналитики, ТаблицаТоваров.АналитикаУчетаНоменклатуры),
	|	СпрНоменклатура.ЕдиницаИзмерения,
	|	СпрНоменклатура.ПодакцизныйТовар,
	|	СпрНоменклатура.ВестиУчетПоГТД,
	|	Аналитика.Номенклатура,
	|	Аналитика.Характеристика,
	|	Аналитика.Серия,
	|	ТаблицаТоваров.Склад,
	|	ТаблицаТоваров.ДокументРеализации,
	|	ВЫБОР КОГДА ТаблицаТоваров.Сделка.ОбособленныйУчетТоваровПоСделке
	|		И &ФормироватьВидыЗапасовПоСделкам
	|	ТОГДА
	|		ТаблицаТоваров.Сделка
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка)
	|	КОНЕЦ,
	|	Аналитика.Назначение,
	|	ЕСТЬNULL(Аналитика.Назначение.Заказ, Неопределено),
	|	ТаблицаТоваров.ПодбиратьВидыЗапасов
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДанныхДокумента.Организация КАК Организация,
	|	Неопределено КАК Партнер,
	|	Неопределено КАК Контрагент,
	|	ЗНАЧЕНИЕ(Справочник.СоглашенияСПоставщиками.ПустаяСсылка) КАК Соглашение,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) КАК Договор,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК Валюта,
	|	ТаблицаДанныхДокумента.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ТаблицаДанныхДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|ИЗ
	|	ТаблицаДанныхДокумента КАК ТаблицаДанныхДокумента
	|ГДЕ
	|	ТаблицаДанныхДокумента.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровКомитенту)
	|	И ТаблицаДанныхДокумента.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаКомиссиюВДругуюОрганизацию)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДанныхДокумента.Организация КАК Организация,
	|	ТаблицаДанныхДокумента.Партнер КАК Партнер,
	|	ТаблицаДанныхДокумента.Контрагент КАК Контрагент,
	|	ТаблицаДанныхДокумента.Соглашение КАК Соглашение,
	|	ТаблицаДанныхДокумента.Договор КАК Договор,
	|	ТаблицаДанныхДокумента.Валюта КАК Валюта,
	|	ТаблицаДанныхДокумента.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ТаблицаДанныхДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|ИЗ
	|	ТаблицаДанныхДокумента КАК ТаблицаДанныхДокумента
	|ГДЕ
	|	ТаблицаДанныхДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровКомитенту)
	|	ИЛИ ТаблицаДанныхДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаКомиссиюВДругуюОрганизацию)
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.Склад КАК Склад,
	|	ТаблицаДокумента.Сделка КАК Сделка,
	|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаДокумента.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТаблицаДокумента.Характеристика КАК Характеристика,
	|	ТаблицаДокумента.Серия КАК Серия,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаДокумента.Количество КАК Количество,
	|	ВЫБОР 
	|		КОГДА &ИспользоватьГруппыФинУчета
	|		ТОГДА ЕСТЬNULL(Аналитика.Номенклатура.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка))
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК ГруппаФинансовогоУчета
	|ИЗ
	|	ТаблицаДокумента КАК ТаблицаДокумента
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО ТаблицаДокумента.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ТаблицаВидыЗапасов.НомерСтроки) КАК НомерСтроки,
	|	ЕСТЬNULL(АналитикаКомиссионера.КлючАналитики, ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры) КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.СкладОтгрузки КАК СкладОтгрузки,
	|	ТаблицаВидыЗапасов.ДокументРеализации КАК ДокументРеализации,
	|	ТаблицаВидыЗапасов.Сделка КАК Сделка,
	|
	|	ТаблицаВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.НомерГТД КАК НомерГТД,
	|	ТаблицаВидыЗапасов.ВидыЗапасовУказаныВручную КАК ВидыЗапасовУказаныВручную,
	|	СУММА(ТаблицаВидыЗапасов.Количество) КАК Количество,
	|	ВЫБОР КОГДА СУММА(ТаблицаВидыЗапасов.Количество) > 0 ТОГДА
	|		1
	|	ИНАЧЕ
	|		-1
	|	КОНЕЦ КАК Знак
	|
	|ПОМЕСТИТЬ ВтВидыЗапасовСводно
	|ИЗ
	|	ТаблицаВидыЗапасов КАК ТаблицаВидыЗапасов
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокумента КАК ТаблицаДанныхДокумента
	|	ПО
	|		ИСТИНА
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаКомиссионера
	|	ПО
	|		ТаблицаВидыЗапасов.Номенклатура = АналитикаКомиссионера.Номенклатура
	|		И ТаблицаВидыЗапасов.Характеристика = АналитикаКомиссионера.Характеристика
	|		И ТаблицаДанныхДокумента.Партнер = АналитикаКомиссионера.Склад
	|		И ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры.Назначение = АналитикаКомиссионера.Назначение
	//++ НЕ УТ 
	|		И ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) = АналитикаКомиссионера.СтатьяКалькуляции
	//-- НЕ УТ
	|		И ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) = АналитикаКомиссионера.Серия
	|		И ТаблицаДанныхДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСТЬNULL(АналитикаКомиссионера.КлючАналитики, ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры),
	|	ТаблицаВидыЗапасов.СкладОтгрузки,
	|	ТаблицаВидыЗапасов.ДокументРеализации,
	|	ТаблицаВидыЗапасов.Сделка,
	|	ТаблицаВидыЗапасов.ВидЗапасов,
	|	ТаблицаВидыЗапасов.НомерГТД,
	|	ТаблицаВидыЗапасов.ВидыЗапасовУказаныВручную
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры,
	|	СкладОтгрузки,
	|	ДокументРеализации,
	|	Сделка,
	|	ВидЗапасов,
	|	НомерГТД
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ // ОСТАТКИ, УКАЗАННЫЕ В ДОКУМЕНТЕ
	|	ВЫБОР КОГДА РеквизитыВидаЗапасовВладельца.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
	|			И РеквизитыДокумента.НалогообложениеНДС  = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт) ТОГДА
	|		1
	|		КОГДА РеквизитыДокумента.НалогообложениеНДС  = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
	|			И ((Ключи.Номенклатура.СырьевойТовар И РеквизитыВидаЗапасовВладельца.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг))
	|				ИЛИ (НЕ Ключи.Номенклатура.СырьевойТовар И РеквизитыВидаЗапасовВладельца.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров))) ТОГДА
	|		4
	|	КОГДА РеквизитыВидаЗапасовВладельца.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляЗаказа) ТОГДА
	|		7
	|	КОГДА РеквизитыВидаЗапасовВладельца.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляСделки) ТОГДА
	|		10
	|	КОГДА РеквизитыВидаЗапасовВладельца.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляМенеджера) ТОГДА
	|		13
	|	КОГДА РеквизитыВидаЗапасовВладельца.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляПодразделения) ТОГДА
	|		16
	|	КОГДА &ИспользоватьРаздельныйУчетПоНалогообложению И (РеквизитыВидаЗапасовВладельца.НалогообложениеНДС ЕСТЬ NULL
	|		ИЛИ РеквизитыВидаЗапасовВладельца.НалогообложениеНДС В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка),	РеквизитыДокумента.НалогообложениеНДС)) ТОГДА
	|		18
	|	КОГДА ЕСТЬNULL(Остатки.ДатаПоступления, РеквизитыДокумента.Дата) <= КОНЕЦПЕРИОДА(РеквизитыДокумента.Дата, ДЕНЬ) ТОГДА
	|		21
	|	ИНАЧЕ
	|		23
	|	КОНЕЦ КАК Приоритет,
	|
	|	ИСТИНА КАК ОстаткиДокумента,
	|
	|	Остатки.Организация КАК Организация,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВидыЗапасов.СкладОтгрузки КАК СкладОтгрузки,
	|	Остатки.Назначение КАК НазначениеОтгрузки,
	|	ВидыЗапасов.ДокументРеализации КАК ДокументРеализации,
	|	ВидыЗапасов.Сделка КАК Сделка,
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры.Назначение КАК Назначение,
	|
	|	ВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	Остатки.ВидЗапасовВладельца КАК ВидЗапасовВладельца,
	|	Остатки.ВидЗапасовПолучателя КАК ВидЗапасовПолучателя,
	|	Остатки.ВидЗапасовОтгрузки КАК ВидЗапасовОтгрузки,
	|	РеквизитыВидаЗапасовОтгрузки.Организация КАК ОрганизацияОтгрузки,
	|	Остатки.РеализацияЗапасовДругойОрганизации КАК РеализацияЗапасовДругойОрганизации,
	|
	|	ВидыЗапасов.НомерГТД КАК НомерГТД,
	|	Остатки.ДатаПоступления КАК ДатаПоступления,
	|
	|	ВЫБОР КОГДА ВидыЗапасов.ВидыЗапасовУказаныВручную И Не &ПерезаполнениеВидовЗапасов ТОГДА
	|		ВидыЗапасов.Количество
	|	КОГДА ВидыЗапасов.Знак * Остатки.КоличествоОстаток > ВидыЗапасов.Знак * ВидыЗапасов.Количество ТОГДА
	|		ВидыЗапасов.Количество
	|	ИНАЧЕ
	|		Остатки.КоличествоОстаток
	|	КОНЕЦ КАК КоличествоОстаток,
	|
	|	ВЫБОР КОГДА (ВидыЗапасов.Знак * Остатки.КоличествоОстаток > ВидыЗапасов.Знак * ВидыЗапасов.Количество)
	|	 И Остатки.КоличествоОстаток <> 0 ТОГДА
	|		ВидыЗапасов.Количество * Остатки.СуммаОстаток / Остатки.КоличествоОстаток
	|	ИНАЧЕ
	|		Остатки.СуммаОстаток
	|	КОНЕЦ КАК СуммаОстаток
	|
	|ПОМЕСТИТЬ ВтОстатки
	|ИЗ
	|	ВтВидыЗапасовСводно КАК ВидыЗапасов
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокумента КАК РеквизитыДокумента
	|	ПО
	|		Истина
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ТаблицаОстатков КАК Остатки
	|	ПО
	|		Остатки.АналитикаУчетаНоменклатуры = ВидыЗапасов.АналитикаУчетаНоменклатуры
	|		И Остатки.СкладОтгрузки = ВидыЗапасов.СкладОтгрузки
	|		И Остатки.ДокументРеализации = ВидыЗапасов.ДокументРеализации
	|		И Остатки.Сделка = ВидыЗапасов.Сделка
	|		И Остатки.ВидЗапасов = ВидыЗапасов.ВидЗапасов
	|		И Остатки.НомерГТД = ВидыЗапасов.НомерГТД
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Ключи
	|	ПО Остатки.АналитикаУчетаНоменклатуры = Ключи.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.ВидыЗапасов КАК РеквизитыВидаЗапасовВладельца
	|	ПО
	|		ЕСТЬNULL(Остатки.ВидЗапасовВладельца, ВидыЗапасов.ВидЗапасов) = РеквизитыВидаЗапасовВладельца.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ВидыЗапасов КАК РеквизитыВидаЗапасовОтгрузки
	|	ПО
	|		Остатки.ВидЗапасовОтгрузки = РеквизитыВидаЗапасовОтгрузки.Ссылка
	|ГДЕ
	|	(НЕ Остатки.ВидЗапасов ЕСТЬ NULL
	|		ИЛИ (ВидыЗапасов.ВидыЗапасовУказаныВручную И Не &ПерезаполнениеВидовЗапасов))
	|	И Остатки.КоличествоОстаток <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // ОСТАТКИ, НЕ УКАЗАННЫЕ В ДОКУМЕНТЕ
	|	ВЫБОР КОГДА Остатки.Организация = РеквизитыДокумента.Организация ТОГДА
	|		ВЫБОР КОГДА РеквизитыВидаЗапасовВладельца.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
	|				И РеквизитыДокумента.НалогообложениеНДС  = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт) ТОГДА
	|			2
	|		КОГДА РеквизитыДокумента.НалогообложениеНДС  = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
	|			И ((Ключи.Номенклатура.СырьевойТовар И РеквизитыВидаЗапасовВладельца.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг))
	|				ИЛИ (НЕ Ключи.Номенклатура.СырьевойТовар И РеквизитыВидаЗапасовВладельца.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров))) ТОГДА
	|			5
	|		КОГДА РеквизитыВидаЗапасовВладельца.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляЗаказа) ТОГДА
	|			8
	|		КОГДА РеквизитыВидаЗапасовВладельца.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляСделки) ТОГДА
	|			11
	|		КОГДА РеквизитыВидаЗапасовВладельца.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляМенеджера) ТОГДА
	|			14
	|		КОГДА РеквизитыВидаЗапасовВладельца.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляПодразделения) ТОГДА
	|			17
	|		КОГДА &ИспользоватьРаздельныйУчетПоНалогообложению И (РеквизитыВидаЗапасовВладельца.НалогообложениеНДС ЕСТЬ NULL
	|			ИЛИ РеквизитыВидаЗапасовВладельца.НалогообложениеНДС В (
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка),	РеквизитыДокумента.НалогообложениеНДС)) ТОГДА
	|			20
	|		КОГДА Остатки.ДатаПоступления <= КОНЕЦПЕРИОДА(РеквизитыДокумента.Дата, ДЕНЬ) ТОГДА
	|			23
	|		ИНАЧЕ
	|			26
	|		КОНЕЦ
	|	ИНАЧЕ
	|		ВЫБОР КОГДА РеквизитыВидаЗапасовВладельца.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
	|				И РеквизитыДокумента.НалогообложениеНДС  = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт) ТОГДА
	|			3
	|		КОГДА РеквизитыДокумента.НалогообложениеНДС  = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт)
	|			И ((Ключи.Номенклатура.СырьевойТовар И РеквизитыВидаЗапасовВладельца.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг))
	|				ИЛИ (НЕ Ключи.Номенклатура.СырьевойТовар И РеквизитыВидаЗапасовВладельца.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров))) ТОГДА
	|			6
	|		КОГДА РеквизитыВидаЗапасовВладельца.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляЗаказа) ТОГДА
	|			9
	|		КОГДА РеквизитыВидаЗапасовВладельца.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляСделки) ТОГДА
	|			12
	|		КОГДА РеквизитыВидаЗапасовВладельца.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляМенеджера) ТОГДА
	|			19
	|		КОГДА РеквизитыВидаЗапасовВладельца.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляПодразделения) ТОГДА
	|			21
	|		КОГДА Остатки.ДатаПоступления <= КОНЕЦПЕРИОДА(РеквизитыДокумента.Дата, ДЕНЬ) ТОГДА
	|			25
	|		ИНАЧЕ
	|			27
	|		КОНЕЦ
	|	КОНЕЦ КАК Приоритет,
	|
	|	ЛОЖЬ КАК ОстаткиДокумента,
	|
	|	Остатки.Организация КАК Организация,
	|	Остатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Остатки.СкладОтгрузки КАК СкладОтгрузки,
	|	Остатки.Назначение КАК НазначениеОтгрузки,	
	|	Остатки.ДокументРеализации КАК ДокументРеализации,
	|	Остатки.Сделка КАК Сделка,
	|	Остатки.АналитикаУчетаНоменклатуры.Назначение КАК Назначение,
	|
	|	Остатки.ВидЗапасов КАК ВидЗапасов,
	|	Остатки.ВидЗапасовВладельца КАК ВидЗапасовВладельца,
	|	Остатки.ВидЗапасовПолучателя КАК ВидЗапасовПолучателя,
	|	Остатки.ВидЗапасовОтгрузки КАК ВидЗапасовОтгрузки,
	|	РеквизитыВидаЗапасовОтгрузки.Организация КАК ОрганизацияОтгрузки,
	|	Остатки.РеализацияЗапасовДругойОрганизации КАК РеализацияЗапасовДругойОрганизации,
	|
	|	Остатки.НомерГТД КАК НомерГТД,
	|	Остатки.ДатаПоступления КАК ДатаПоступления,
	|
	|	Остатки.КоличествоОстаток - ЕСТЬNULL(ВидыЗапасов.Количество, 0) КАК КоличествоОстаток,
	|	ВЫБОР КОГДА Остатки.КоличествоОстаток <> 0 ТОГДА
	|		Остатки.СуммаОстаток
	|			- (ЕСТЬNULL(ВидыЗапасов.Количество, 0) * Остатки.СуммаОстаток / Остатки.КоличествоОстаток)
	|	КОГДА ЕСТЬNULL(ВидыЗапасов.Количество, 0) = 0 ТОГДА
	|		Остатки.СуммаОстаток
	|	ИНАЧЕ
	|		0 // Сумма остатка подобралась в строках с видом запасов
	|	КОНЕЦ КАК СуммаОстаток
	|ИЗ
	|	ТаблицаОстатков КАК Остатки
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокумента КАК РеквизитыДокумента
	|	ПО
	|		Истина
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.ВидыЗапасов КАК РеквизитыВидаЗапасовВладельца
	|	ПО
	|		Остатки.ВидЗапасовВладельца = РеквизитыВидаЗапасовВладельца.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ВидыЗапасов КАК РеквизитыВидаЗапасовОтгрузки
	|	ПО
	|		Остатки.ВидЗапасовОтгрузки = РеквизитыВидаЗапасовОтгрузки.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Ключи
	|	ПО Остатки.АналитикаУчетаНоменклатуры = Ключи.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтВидыЗапасовСводно КАК ВидыЗапасов
	|	ПО
	|		Остатки.АналитикаУчетаНоменклатуры = ВидыЗапасов.АналитикаУчетаНоменклатуры
	|		И Остатки.СкладОтгрузки = ВидыЗапасов.СкладОтгрузки
	|		И Остатки.ДокументРеализации = ВидыЗапасов.ДокументРеализации
	|		И Остатки.Сделка = ВидыЗапасов.Сделка
	|		И Остатки.ВидЗапасов = ВидыЗапасов.ВидЗапасов
	|		И Остатки.НомерГТД = ВидыЗапасов.НомерГТД
	|ГДЕ
	|	ВЫБОР КОГДА ВидыЗапасов.Количество ЕСТЬ NULL ТОГДА
	|		ИСТИНА
	|	КОГДА ВидыЗапасов.Количество = 0 ТОГДА
	|		НЕ ВидыЗапасов.НомерСтроки = 99999
	|	КОГДА ВидыЗапасов.Знак * ВидыЗапасов.Количество > 0 ТОГДА
	|		ВидыЗапасов.Знак * Остатки.КоличествоОстаток
	|			- ВидыЗапасов.Знак * ВидыЗапасов.Количество > 0
	|	ИНАЧЕ
	|		ВидыЗапасов.Знак * Остатки.КоличествоОстаток
	|			- ВидыЗапасов.Знак * ВидыЗапасов.Количество < 0
	|	КОНЕЦ 
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(Остатки.Приоритет, 0) КАК Приоритет,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.Склад КАК Склад,
	|	ТаблицаДокумента.Сделка КАК Сделка,
	|	ЕСТЬNULL(Остатки.АналитикаУчетаНоменклатуры.Назначение, Неопределено) КАК Назначение,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
	|	ТаблицаДокумента.Номенклатура.СырьевойТовар КАК СырьевойТовар,
	|	ТаблицаДокумента.ЭтоПодакцизныйТовар КАК ЭтоПодакцизныйТовар,
	|	ТаблицаДокумента.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТаблицаДокумента.Характеристика КАК Характеристика,
	|	ТаблицаДокумента.Количество КАК Количество,
	|	ТаблицаДокумента.СуммаСНДС КАК СуммаСНДС,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
	|	ТаблицаДокумента.СуммаВознаграждения КАК СуммаВознаграждения,
	|	ТаблицаДокумента.СуммаНДСВознаграждения КАК СуммаНДСВознаграждения,
	|	ТаблицаДанныхДокумента.Дата КАК ДатаДокумента,
	|
	|	ЕСТЬNULL(Остатки.ВидЗапасов, Неопределено) КАК ВидЗапасов,
	|	ЕСТЬNULL(Остатки.ВидЗапасовВладельца, Неопределено) КАК ВидЗапасовВладельца,
	|	ЕСТЬNULL(Остатки.ВидЗапасовПолучателя, Неопределено) КАК ВидЗапасовПолучателя,
	|	ЕСТЬNULL(Остатки.ВидЗапасовОтгрузки, Неопределено) КАК ВидЗапасовОтгрузки,
	|	ЕСТЬNULL(Остатки.ОрганизацияОтгрузки, Неопределено) КАК ОрганизацияОтгрузки,
	|	ЕСТЬNULL(Остатки.РеализацияЗапасовДругойОрганизации, Ложь) КАК РеализацияЗапасовДругойОрганизации,
	|	ЕСТЬNULL(Остатки.НомерГТД, Неопределено) КАК НомерГТД,
	|
	|	ВЫБОР КОГДА ТаблицаДокумента.ВестиУчетПоГТД
	|		И &ЗапретитьОформлениеОперацийСИмпортнымиТоварамиБезНомеровГТД
	|		И (Остатки.НомерГТД = ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка) ИЛИ Остатки.НомерГТД ЕСТЬ NULL)
	|		И НЕ (ЕСТЬNULL(РеквизитыВидаЗапасов.ТипЗапасов, ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПустаяСсылка)) В (ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца), ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПродукцияДавальца)))
	|	ТОГДА
	|		Истина
	|	ИНАЧЕ
	|		Ложь
	|	КОНЕЦ КАК НеУказанНомерГТД,
	|
	|	ЕСТЬNULL(Остатки.ДатаПоступления, ДАТАВРЕМЯ(2399, 1, 1)) КАК ДатаПоступления,
	|	ЕСТЬNULL(Остатки.СкладОтгрузки, Неопределено) КАК СкладОтгрузки,
	|	ВЫБОР КОГДА ТаблицаДанныхДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера)
	|		И ТаблицаДокумента.ДокументРеализации <> НЕОПРЕДЕЛЕНО
	|	ТОГДА
	|		ТаблицаДокумента.ДокументРеализации
	|	ИНАЧЕ
	|		ЕСТЬNULL(Остатки.ДокументРеализации, Неопределено)
	|	КОНЕЦ КАК ДокументРеализации,
	|	ЕСТЬNULL(ТаблицаДокумента.Заказ, Неопределено) КАК Заказ,
	|
	|	ЕСТЬNULL(Остатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	|	ЕСТЬNULL(Остатки.СуммаОстаток, 0) КАК СуммаОстаток,
	|	ЕСТЬNULL(АналитикаОтгрузки.КлючАналитики, ТаблицаДокумента.АналитикаУчетаНоменклатуры) КАК АналитикаУчетаНоменклатурыОтгрузки
	|ИЗ
	|	ТаблицаДокумента КАК ТаблицаДокумента
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаДанныхДокумента КАК ТаблицаДанныхДокумента
	|	ПО
	|		Истина
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтОстатки КАК Остатки
	|	ПО
	|		ТаблицаДокумента.АналитикаОстатков.Номенклатура = Остатки.АналитикаУчетаНоменклатуры.Номенклатура
	|		И ТаблицаДокумента.АналитикаОстатков.Характеристика = Остатки.АналитикаУчетаНоменклатуры.Характеристика
	|		И ТаблицаДокумента.АналитикаОстатков.Серия = Остатки.АналитикаУчетаНоменклатуры.Серия
	|		И ТаблицаДокумента.АналитикаОстатков.Склад = Остатки.АналитикаУчетаНоменклатуры.Склад
	|		И (ТаблицаДокумента.ДокументРеализации = Остатки.ДокументРеализации
	|			ИЛИ (ТаблицаДокумента.ДокументРеализации = НЕОПРЕДЕЛЕНО И ТаблицаДокумента.ПодбиратьВидыЗапасов)
	|			ИЛИ ТаблицаДанныхДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера)
	|			)
	|		И (Не ТаблицаДанныхДокумента.ЕстьСделкиВТабличнойЧасти
	|			ИЛИ ТаблицаДокумента.Сделка = Остатки.Сделка
	|			ИЛИ Остатки.Сделка = ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка)
	|			)
	|		И (ТаблицаДокумента.АналитикаОстатков.Назначение = Остатки.АналитикаУчетаНоменклатуры.Назначение
	|			ИЛИ ТаблицаДанныхДокумента.ХозяйственнаяОперация В (
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиента),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтПереработчика))
	|		)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.КлючиАналитикиУчетаНоменклатуры КАК Ключи
	|	ПО
	|		Ключи.Ссылка = Остатки.АналитикаУчетаНоменклатуры
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаОтгрузки
	|	ПО
	|		АналитикаОтгрузки.Номенклатура = Ключи.Номенклатура
	|		И АналитикаОтгрузки.Характеристика = Ключи.Характеристика
	|		И АналитикаОтгрузки.Серия = Ключи.Серия
	|		И АналитикаОтгрузки.Склад = Остатки.СкладОтгрузки
	|		И АналитикаОтгрузки.Назначение = Остатки.НазначениеОтгрузки
	|		И АналитикаОтгрузки.СтатьяКалькуляции = Ключи.СтатьяКалькуляции
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ДатыПоступленияТоваровОрганизаций КАК ДатыПоступления
	|	ПО
	|		ДатыПоступления.ВидЗапасов = Остатки.ВидЗапасов
	|		И ДатыПоступления.Номенклатура = Остатки.АналитикаУчетаНоменклатуры.Номенклатура
	|		И ДатыПоступления.Характеристика = Остатки.АналитикаУчетаНоменклатуры.Характеристика
	|		И ДатыПоступления.Серия = Остатки.АналитикаУчетаНоменклатуры.Серия
	|		И ДатыПоступления.Назначение = Остатки.АналитикаУчетаНоменклатуры.Назначение
	|		И ДатыПоступления.НомерГТД = Остатки.НомерГТД
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ВидыЗапасов КАК РеквизитыВидаЗапасов
	|	ПО
	|		РеквизитыВидаЗапасов.Ссылка = Остатки.ВидЗапасов
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет Возр,
	|	НомерСтроки Возр,
	|	ДатаПоступления Возр,
	|	ДатыПоступления.ДатаПоступления Возр
	|
	|ИТОГИ
	|	МАКСИМУМ(Количество),
	|	МАКСИМУМ(СуммаСНДС),
	|	МАКСИМУМ(СтавкаНДС),
	|	МАКСИМУМ(СуммаНДС),
	|	МАКСИМУМ(СуммаВознаграждения),
	|	МАКСИМУМ(СуммаНДСВознаграждения),
	|	СУММА(КоличествоОстаток)
	|ПО
	|	НомерСтроки
	|");
	Запрос.УстановитьПараметр("ИспользоватьРаздельныйУчетПоНалогообложению", ПолучитьФункциональнуюОпцию("ИспользоватьРаздельныйУчетПоНалогообложению"));
	Запрос.УстановитьПараметр("ФормироватьВидыЗапасовПоСделкам", ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоСделкам"));
	Запрос.УстановитьПараметр("ПерезаполнениеВидовЗапасов", ДополнительныеСвойства.Свойство("ПерезаполнитьВидыЗапасов"));
	Запрос.УстановитьПараметр("ИспользоватьГруппыФинУчета", ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоГруппамФинансовогоУчета"));
	Если ДополнительныеСвойства.Свойство("КонтролироватьНомераГТД") Тогда
		Запрос.УстановитьПараметр("ЗапретитьОформлениеОперацийСИмпортнымиТоварамиБезНомеровГТД", ДополнительныеСвойства.КонтролироватьНомераГТД);
	Иначе
		Запрос.УстановитьПараметр("ЗапретитьОформлениеОперацийСИмпортнымиТоварамиБезНомеровГТД", ПолучитьФункциональнуюОпцию("ЗапретитьОформлениеОперацийСИмпортнымиТоварамиБезНомеровГТД"));
	КонецЕсли;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Возврат Запрос;
	
КонецФункции

#КонецОбласти

// Возвращает вид запасов отгрузки
Функция ВидЗапасовОтгрузки(Выборка, Организация, ПередачаПодДеятельность, ВозвратБезПередачи, ПодакцизныйТовар, ОстальныеВидыЗапасов)
	ВидЗапасовОтгрузки = Неопределено;
	Если ЗначениеЗаполнено(Выборка.ВидЗапасовОтгрузки) Тогда
		Если Не Выборка.РеализацияЗапасовДругойОрганизации И Выборка.ОрганизацияОтгрузки = Организация Тогда
			ВидЗапасовОтгрузки = Выборка.ВидЗапасовОтгрузки;
		ИначеЕсли Не Выборка.РеализацияЗапасовДругойОрганизации Тогда
			ВидЗапасовОтгрузки = ВозвратБезПередачи.Получить(Выборка.ВидЗапасовОтгрузки);
		ИначеЕсли Выборка.ЭтоПодакцизныйТовар Тогда
			ВидЗапасовОтгрузки = ПодакцизныйТовар.Получить(Выборка.ВидЗапасовОтгрузки);
		Иначе
			ВидЗапасовОтгрузки = ОстальныеВидыЗапасов.Получить(Выборка.ВидЗапасовОтгрузки);
		КонецЕсли;
		Если ВидЗапасовОтгрузки = Неопределено Тогда
			ВидЗапасовОтгрузки = Справочники.ВидыЗапасов.ВидЗапасовДляПередачиМеждуОрганизациями(
				Выборка.ВидЗапасовОтгрузки,
				Организация,
				ПередачаПодДеятельность,
				Выборка.ДатаДокумента,
				Выборка.СырьевойТовар);
			Если Не Выборка.РеализацияЗапасовДругойОрганизации Тогда
				ВозвратБезПередачи.Вставить(Выборка.ВидЗапасовОтгрузки, ВидЗапасовОтгрузки);
			ИначеЕсли Выборка.ЭтоПодакцизныйТовар Тогда
				ПодакцизныйТовар.Вставить(Выборка.ВидЗапасовОтгрузки, ВидЗапасовОтгрузки);
			Иначе
				ОстальныеВидыЗапасов.Вставить(Выборка.ВидЗапасовОтгрузки, ВидЗапасовОтгрузки);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат ВидЗапасовОтгрузки;
КонецФункции

Функция ИнициализироватьВыборки(Запрос, КоличествоГруппировок)
	
	Выборки = Новый Массив(КоличествоГруппировок + 1); // Количество группировок +1 для детальных записей
	Выборки[0] = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Возврат Выборки;
	
КонецФункции

Функция СледующаяВыборка(Выборки, Выборка, ТребуемыйУровень = Неопределено)
	
	Максимум = Выборки.Количество() - 1;
	
	// Приведем необходимый уровень выборки к индексу массива
	СтартовыйИндекс = Максимум - ?(ТребуемыйУровень = Неопределено, Максимум, ТребуемыйУровень - 1);
	
	// Перебираем массивы выборок начиная с самого детального уровня
	Для Индекс = СтартовыйИндекс По Максимум Цикл
		
		ТекИндекс = Максимум - Индекс;
		Если Выборки[ТекИндекс] = Неопределено Или Не Выборки[ТекИндекс].Следующий() Тогда
			Продолжить; // Не удолось выбрать следующую запись из выборки текущего уровня, спустимся ниже
		ИначеЕсли Индекс = СтартовыйИндекс Тогда
			Выборка = Выборки[ТекИндекс];
			Возврат Истина; // Выбрали следующую запись из выборки нужного уровня
		КонецЕсли;
		
		// Инициализируем новую выборку более детального уровня ...
		Выборки[ТекИндекс + 1] = Выборки[ТекИндекс].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		// ... и обозначим переход на эту выборку, поднявшись выше
		Индекс = Индекс - 2;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Процедура ВыгрузитьГрафикДоступностьТоваров()

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоДня",   НачалоДня(ТекущаяДатаСеанса()));

	Запрос.Текст =
		"ВЫБРАТЬ
		|	Т.Номенклатура           КАК Номенклатура,
		|	Т.Характеристика         КАК Характеристика,
		|	Т.Склад                  КАК Склад,
		|	Т.ВНаличииОстаток
		|		- Т.ВРезервеСоСкладаОстаток
		|		- Т.ВРезервеПодЗаказОстаток КАК Количество
		|
		|ПОМЕСТИТЬ ВтОстаткиСклада
		|ИЗ
		|	РегистрНакопления.СвободныеОстатки.Остатки(,) КАК Т
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура, Характеристика, Склад
		|;
		|
		|//////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.ДатаДоступности         КАК Период,
		|
		|	Т.Номенклатура            КАК Номенклатура,
		|	Т.Характеристика          КАК Характеристика,
		|	Т.Склад                   КАК Склад,
		|
		|	Т.Количество + ЕСТЬNULL(ОстаткиСклада.Количество, 0) КАК Доступно
		|
		|ИЗ
		|	РегистрСведений.ДоступныеОстаткиПланируемыхПоступлений КАК Т
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтОстаткиСклада КАК ОстаткиСклада
		|		ПО Т.Склад          = ОстаткиСклада.Склад
		|		 И Т.Номенклатура   = ОстаткиСклада.Номенклатура
		|		 И Т.Характеристика = ОстаткиСклада.Характеристика
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаХарактеристика
		|		ПО Т.Склад          = НастройкаХарактеристика.Склад
		|		 И Т.Номенклатура   = НастройкаХарактеристика.Номенклатура
		|		 И Т.Характеристика = НастройкаХарактеристика.Характеристика
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаНоменклатура
		|		ПО Т.Склад        = НастройкаНоменклатура.Склад
		|		 И Т.Номенклатура = НастройкаНоменклатура.Номенклатура
		|		 И НастройкаНоменклатура.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|		 И НастройкаХарактеристика.Склад ЕСТЬ NULL
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаСклад
		|		ПО Т.Склад = НастройкаСклад.Склад
		|		 И НастройкаСклад.Номенклатура   = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|		 И НастройкаСклад.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|		 И НастройкаХарактеристика.Склад ЕСТЬ NULL
		|		 И НастройкаНоменклатура.Склад ЕСТЬ NULL
		|
		|ГДЕ
		|	ЕСТЬNULL(НастройкаХарактеристика.Контролировать, ЕСТЬNULL(НастройкаНоменклатура.Контролировать, НастройкаСклад.Контролировать))
		|	И Т.ДатаДоступности > &НачалоДня
		|	
		|ОБЪЕДИНИТЬ ВСЕ
		|	
		|ВЫБРАТЬ
		|	&НачалоДня               КАК Период,
		|
		|	Т.Номенклатура           КАК Номенклатура,
		|	Т.Характеристика         КАК Характеристика,
		|	Т.Склад                  КАК Склад,
		|
		|	Т.Количество + ЕСТЬNULL(ОстаткиИзЗаказов.Количество, 0) КАК Доступно
		|
		|ИЗ
		|	ВтОстаткиСклада КАК Т
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДоступныеОстаткиПланируемыхПоступлений КАК ОстаткиИзЗаказов
		|		ПО Т.Склад          = ОстаткиИзЗаказов.Склад
		|		 И Т.Номенклатура   = ОстаткиИзЗаказов.Номенклатура
		|		 И Т.Характеристика = ОстаткиИзЗаказов.Характеристика
		|		 И ОстаткиИзЗаказов.ДатаДоступности = ДАТАВРЕМЯ(1, 1, 1)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаХарактеристика
		|		ПО Т.Склад          = НастройкаХарактеристика.Склад
		|		 И Т.Номенклатура   = НастройкаХарактеристика.Номенклатура
		|		 И Т.Характеристика = НастройкаХарактеристика.Характеристика
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаНоменклатура
		|		ПО Т.Склад        = НастройкаНоменклатура.Склад
		|		 И Т.Номенклатура = НастройкаНоменклатура.Номенклатура
		|		 И НастройкаНоменклатура.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|		 И НастройкаХарактеристика.Склад ЕСТЬ NULL
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаКонтроляОбеспечения КАК НастройкаСклад
		|		ПО Т.Склад = НастройкаСклад.Склад
		|		 И НастройкаСклад.Номенклатура   = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|		 И НастройкаСклад.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|		 И НастройкаХарактеристика.Склад ЕСТЬ NULL
		|		 И НастройкаНоменклатура.Склад ЕСТЬ NULL
		|
		|ГДЕ
		|	ЕСТЬNULL(НастройкаХарактеристика.Контролировать, ЕСТЬNULL(НастройкаНоменклатура.Контролировать, НастройкаСклад.Контролировать))";

	НаборЗаписей = РегистрыСведений.ДоступностьТоваровДляВнешнихПользователей.СоздатьНаборЗаписей();
	НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
	Если НаборЗаписей.Модифицированность() Тогда
		НаборЗаписей.Записать(Ложь);
	КонецЕсли;

КонецПроцедуры

Процедура ЗаписатьДвиженияВБазу(МенеджерДвижений, Таблица, Ссылка, ЗаполнятьЦиклом = Ложь, УстановитьАктивность = Истина, ДопСвойства = Неопределено) Экспорт
	Набор = МенеджерДвижений.СоздатьНаборЗаписей();
	Набор.Отбор.Регистратор.Установить(Ссылка);
	Набор.Прочитать();
	Если ДопСвойства <> Неопределено Тогда
		Для Каждого Свойство Из ДопСвойства Цикл
			Набор.ДополнительныеСвойства.Вставить(Свойство.Ключ, Свойство.Значение);
		КонецЦикла;
	КонецЕсли;
	Если Набор.Количество() > 0 Или Таблица.Количество() > 0 Тогда
		Если ЗаполнятьЦиклом Тогда
			Для Каждого Строка Из Таблица Цикл
				Запись = Набор.Добавить();
				ЗаполнитьЗначенияСвойств(Запись, Строка);
			КонецЦикла;
		Иначе
			Набор.Загрузить(Таблица);
		КонецЕсли;
		Если УстановитьАктивность Тогда
			Набор.УстановитьАктивность(Истина);
		КонецЕсли;
		Набор.Записать();
	КонецЕсли;
КонецПроцедуры

Процедура ТаблицаОстатковТоваровОрганизацийПоПериодам(ДокументОбъект, МенеджерВременныхТаблиц, ПараметрыЗаполненияВидовЗапасов, ВычитатьДвиженияПоТоварамОрганизаций)
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ // Движения документа
	|	КОНЕЦПЕРИОДА(Товары.Период, МЕСЯЦ) КАК ПериодМесяц,
	|	КОНЕЦПЕРИОДА(Товары.Период, ДЕНЬ)  КАК ПериодДень,
	|	Товары.Организация                 КАК Организация,
	|	Товары.АналитикаУчетаНоменклатуры  КАК АналитикаУчетаНоменклатуры,
	|	Товары.ВидЗапасов                  КАК ВидЗапасов,
	|	Товары.НомерГТД                    КАК НомерГТД,
	|	СУММА(ВЫБОР
	|			КОГДА Товары.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА Товары.Количество
	|			ИНАЧЕ - Товары.Количество
	|		КОНЕЦ)                         КАК Количество
	|ПОМЕСТИТЬ ДвиженияДокумента
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций КАК Товары
	|	ЛЕВОЕ СОЕДИНЕНИЕ ДоступныеВидыЗапасов КАК ДоступныеВидыЗапасов
	|		ПО Товары.ВидЗапасов = ДоступныеВидыЗапасов.ВидЗапасов
	|ГДЕ
	|	Товары.Регистратор = &Ссылка
	|	И Товары.Активность
	|	И (Товары.АналитикаУчетаНоменклатуры) В (
	|		ВЫБРАТЬ
	|			ТаблицаТоваров.АналитикаУчетаНоменклатуры
	|		ИЗ
	|			ТаблицаТоваровПоПериодам КАК ТаблицаТоваров
	|		)
	|//%ТекстУсловиеВычитаемыхДвиженийРегистратора%
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.Организация,
	|	Товары.АналитикаУчетаНоменклатуры,
	|	Товары.ВидЗапасов,
	|	Товары.НомерГТД,
	|	КОНЕЦПЕРИОДА(Товары.Период, МЕСЯЦ),
	|	КОНЕЦПЕРИОДА(Товары.Период, ДЕНЬ)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Товары.Организация,
	|	Товары.АналитикаУчетаНоменклатуры,
	|	Товары.ВидЗапасов,
	|	Товары.НомерГТД
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДАТАВРЕМЯ(1,1,1)                   КАК Период,
	|	Остатки.Организация                КАК Организация,
	|	Остатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Остатки.ВидЗапасов                 КАК ВидЗапасов,
	|	Остатки.НомерГТД                   КАК НомерГТД,
	|	СУММА(Остатки.КоличествоОстаток)   КАК КоличествоОстаток
	|ПОМЕСТИТЬ ОстаткиПоПериодам
	|	ИЗ
	|		(ВЫБРАТЬ //ОстаткиНаПоследнееДвижение
	|		Остатки.Организация,
	|		Остатки.АналитикаУчетаНоменклатуры,
	|		Остатки.ВидЗапасов,
	|		Остатки.НомерГТД,
	|		Остатки.КоличествоОстаток КАК КоличествоОстаток
	|	ИЗ
	|		РегистрНакопления.ТоварыОрганизаций.Остатки(,
	|			(АналитикаУчетаНоменклатуры) В (
	|				ВЫБРАТЬ
	|					ТаблицаТоваров.АналитикаУчетаНоменклатуры
	|				ИЗ
	|					ТаблицаТоваровПоПериодам КАК ТаблицаТоваров
	|				)
	|			И (
	|				ВидЗапасов В (
	|					ВЫБРАТЬ
	|						ДоступныеВидыЗапасов.ВидЗапасов
	|					ИЗ
	|						ДоступныеВидыЗапасов КАК ДоступныеВидыЗапасов
	|					)
	|				)
	|			И Организация = ВидЗапасов.Организация
	|		) КАК Остатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияДокумента.Организация,
	|		ДвиженияДокумента.АналитикаУчетаНоменклатуры,
	|		ДвиженияДокумента.ВидЗапасов,
	|		ДвиженияДокумента.НомерГТД,
	|		ДвиженияДокумента.Количество
	|	ИЗ
	|		ДвиженияДокумента КАК ДвиженияДокумента
	|	ГДЕ
	|		ДвиженияДокумента.ПериодМесяц >= &ПериодЗаполнения
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаДанныхДокумента.Организация,
	|		ТаблицаТоваров.АналитикаУчетаНоменклатуры,
	|		ВидыЗапасов.ВидЗапасов,
	|		ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка),
	|		0
	|	ИЗ
	|		ТаблицаДанныхДокумента КАК ТаблицаДанныхДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТоваровПоПериодам КАК ТаблицаТоваров
	|		ПО ИСТИНА
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДоступныеВидыЗапасов КАК ВидыЗапасов
	|		ПО ИСТИНА
	|
	|//%ТекстПриходуемыеВидыЗапасовНаПоследнееДвижение%
	|
	|) КАК Остатки
	|
	|СГРУППИРОВАТЬ ПО
	|	Остатки.Организация,
	|	Остатки.АналитикаУчетаНоменклатуры,
	|	Остатки.ВидЗапасов,
	|	Остатки.НомерГТД
	|
	|//%ТекстПромежуточныеМесяцаКонтроля%
	|
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыОрганизаций.Период                      КАК Период,
	|	ТоварыОрганизаций.Организация                 КАК Организация,
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры  КАК АналитикаУчетаНоменклатуры,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)      КАК СкладОтгрузки,
	|	Аналитика.Номенклатура                        КАК Номенклатура,
	|	Аналитика.Характеристика                      КАК Характеристика,
	|	Аналитика.Серия                               КАК Серия,
	|	Аналитика.Склад                               КАК Склад,
	|	НЕОПРЕДЕЛЕНО                                  КАК ДокументРеализации,
	|	ВЫБОР
	|		КОГДА ТоварыОрганизаций.Организация <> &Организация
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ТоварыОрганизаций.ВидЗапасов
	|	КОНЕЦ                                         КАК ВидЗапасов,
	|	ТоварыОрганизаций.ВидЗапасовВладельца         КАК ВидЗапасовВладельца,
	|	Неопределено                                  КАК ВидЗапасовПолучателя,
	|	Неопределено                                  КАК ВидЗапасовОтгрузки,
	|	ВЫБОР
	|		КОГДА ТоварыОрганизаций.Организация <> &Организация
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                                         КАК РеализацияЗапасовДругойОрганизации,
	|	Аналитика.Назначение КАК Назначение,
	|	ТоварыОрганизаций.НомерГТД                    КАК НомерГТД,
	|	МАКСИМУМ(
	|		ВЫБОР 
	|			КОГДА ТоварыОрганизаций.ДатаПоступления > КОНЕЦПЕРИОДА(&ПериодЗаполнения, МЕСЯЦ)
	|				ТОГДА НАЧАЛОПЕРИОДА(&ПериодЗаполнения, МЕСЯЦ)
	|			ИНАЧЕ ТоварыОрганизаций.ДатаПоступления
	|		КОНЕЦ
	|	)                                             КАК ДатаПоступления,
	|	СУММА(ТоварыОрганизаций.КоличествоОстаток)    КАК КоличествоОстаток,
	|	СУММА(ТоварыОрганизаций.НаДатуКонтроля)       КАК НаДатуКонтроля,
	|	СУММА(ТоварыОрганизаций.НаДатуАктуальности)   КАК НаДатуАктуальности,
	|	0 КАК СуммаОстаток,
	|
	|	ВЫБОР КОГДА ТоварыОрганизаций.ВидЗапасов.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляСделки) ТОГДА
	|		ТоварыОрганизаций.ВидЗапасов.Сделка
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка)
	|	КОНЕЦ                                         КАК Сделка
	|
	|ПОМЕСТИТЬ ТаблицаОстатковПоПериодам
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОстаткиПоПериодам.Период                     КАК Период,
	|		ОстаткиПоПериодам.Организация                КАК Организация,
	|		ОстаткиПоПериодам.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ВЫБОР 
	|			КОГДА ДоступныеВидыЗапасов.ВидЗапасовПродавца.РеализацияЗапасовДругойОрганизации
	|				И ОстаткиПоПериодам.АналитикаУчетаНоменклатуры.Номенклатура.ПодакцизныйТовар
	|			ТОГДА Неопределено
	|			ИНАЧЕ ЕСТЬNULL(ДоступныеВидыЗапасов.ВидЗапасовПродавца, ОстаткиПоПериодам.ВидЗапасов)
	|		КОНЕЦ                                        КАК ВидЗапасов,
	|		ОстаткиПоПериодам.ВидЗапасов                 КАК ВидЗапасовВладельца,
	|		ОстаткиПоПериодам.НомерГТД                   КАК НомерГТД,
	|		ОстаткиПоПериодам.КоличествоОстаток          КАК КоличествоОстаток,
	|		ОстаткиПоПериодам.КоличествоОстаток          КАК НаДатуКонтроля,
	|		ОстаткиПоПериодам.КоличествоОстаток          КАК НаДатуАктуальности,
	|		ДатыПоступления.ДатаПоступления              КАК ДатаПоступления
	|	ИЗ
	|		ОстаткиПоПериодам КАК ОстаткиПоПериодам
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыПоступленияТоваровОрганизаций КАК ДатыПоступления
	|			ПО ОстаткиПоПериодам.ВидЗапасов = ДатыПоступления.ВидЗапасов
	|			И ОстаткиПоПериодам.АналитикаУчетаНоменклатуры.Номенклатура = ДатыПоступления.Номенклатура
	|			И ОстаткиПоПериодам.АналитикаУчетаНоменклатуры.Характеристика = ДатыПоступления.Характеристика
	|			И ОстаткиПоПериодам.АналитикаУчетаНоменклатуры.Серия = ДатыПоступления.Серия
	|			И ОстаткиПоПериодам.АналитикаУчетаНоменклатуры.Назначение = ДатыПоступления.Назначение
	|			И ОстаткиПоПериодам.НомерГТД = ДатыПоступления.НомерГТД
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДоступныеВидыЗапасов КАК ДоступныеВидыЗапасов
	|			ПО ОстаткиПоПериодам.ВидЗапасов = ДоступныеВидыЗапасов.ВидЗапасов
	|
	|) КАК ТоварыОрганизаций
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО
	|		ТоварыОрганизаций.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыОрганизаций.Период,
	|	ТоварыОрганизаций.Организация,
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры,
	|	Аналитика.Номенклатура,
	|	Аналитика.Характеристика,
	|	Аналитика.Серия,
	|	Аналитика.Склад,
	|	Аналитика.Назначение,
	|	ВЫБОР КОГДА ТоварыОрганизаций.Организация <> &Организация
	|		ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ТоварыОрганизаций.ВидЗапасов
	|	КОНЕЦ,
	|	ВЫБОР КОГДА ТоварыОрганизаций.Организация <> &Организация
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ТоварыОрганизаций.ВидЗапасовВладельца,
	|	ТоварыОрганизаций.НомерГТД,
	|	ВЫБОР КОГДА ТоварыОрганизаций.ВидЗапасов.Предназначение = ЗНАЧЕНИЕ(Перечисление.ТипыПредназначенияВидовЗапасов.ПредназначенДляСделки) ТОГДА
	|		ТоварыОрганизаций.ВидЗапасов.Сделка
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка)
	|	КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период,
	|	АналитикаУчетаНоменклатуры,
	|	ВидЗапасов,
	|	НомерГТД
	|;
	|///////////////////////////////////////////////////////////////////////////////
	|");
	
	Если ПараметрыЗаполненияВидовЗапасов.ЕстьТаблицаПриходуемыхВидовЗапасов Тогда
		ТекстПриходуемыеВидыЗапасов = "
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ //Дополнительные виды запасов, приходуемые документом
		|	ТаблицаВидыЗапасов.Организация                КАК Организация,
		|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ТаблицаВидыЗапасов.ВидЗапасов                 КАК ВидЗапасов,
		|	ТаблицаВидыЗапасов.НомерГТД                   КАК НомерГТД,
		|	ТаблицаВидыЗапасов.Количество                 КАК Количество
		|ИЗ
		|	ПриходуемыеВидыЗапасов КАК ТаблицаВидыЗапасов
		|";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//%ТекстПриходуемыеВидыЗапасовНаПоследнееДвижение%", ТекстПриходуемыеВидыЗапасов);
	КонецЕсли;
	
	Если ПараметрыЗаполненияВидовЗапасов.ДополнительноеУсловиеВычитаемыхДвижений Тогда
		ТекстДополнительноеУсловиеВычитаемыхДвижений = "
		|	И Товары.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И Товары.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРасходовНаПартииПроизводства)
		|";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//%ТекстУсловиеВычитаемыхДвиженийРегистратора%", ТекстДополнительноеУсловиеВычитаемыхДвижений);
	КонецЕсли;
	
	ПервыйПериод = ?(ЗначениеЗаполнено(ПараметрыЗаполненияВидовЗапасов.ПериодЗаполнения),
		ПараметрыЗаполненияВидовЗапасов.ПериодЗаполнения, КонецМесяца(ТекущаяДатаСеанса()));
	
	ТекущийПериод = КонецМесяца(ТекущаяДатаСеанса());
	Если ПервыйПериод < ТекущийПериод И НачалоГода(ПервыйПериод) >= НачалоГода(ТекущийПериод) - 94608000 Тогда
		// проверка актуальности на каждый месяц выполняется за последние три года,
		// т.к. за это время возможна налоговая проверка корректности первичных данных.
		ДополнитьЗапросКонтролемОстатковПоПериодам(Запрос, ПервыйПериод, ТекущийПериод, ПараметрыЗаполненияВидовЗапасов);
	Иначе
		// проверка выполняется только на конец дня, конец месяца и дату актуальности
		ДополнитьЗапросКонтролемОстатковПоПериодам(Запрос, ТекущийПериод, ТекущийПериод, ПараметрыЗаполненияВидовЗапасов);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("Организация", ДокументОбъект.Организация);
	Запрос.УстановитьПараметр("ПериодЗаполнения", ПараметрыЗаполненияВидовЗапасов.ПериодЗаполнения);
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ДополнитьЗапросКонтролемОстатковПоПериодам(Запрос, ПервыйМесяц, ПоследнийМесяц, ПараметрыЗаполненияВидовЗапасов)
	
	ТекстВТ = "";
	ШаблонВТ = "
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // Остатки товаров организации на дату #МесяцКонтроля
	|	&КонецМесяцКонтроля КАК Период,
	|	Остатки.Организация,
	|	Остатки.АналитикаУчетаНоменклатуры,
	|	Остатки.ВидЗапасов,
	|	Остатки.НомерГТД,
	|	СУММА(Остатки.Количество) КАК КоличествоОстаток
	|ИЗ
	|	(ВЫБРАТЬ // Остатки товаров организации на дату #МесяцКонтроля
	|		Остатки.Организация,
	|		Остатки.АналитикаУчетаНоменклатуры,
	|		Остатки.ВидЗапасов,
	|		Остатки.НомерГТД,
	|		Остатки.КоличествоОстаток КАК Количество
	|	ИЗ
	|		РегистрНакопления.ТоварыОрганизаций.Остатки(&ГраницаМесяцКонтроля,
	|			(АналитикаУчетаНоменклатуры) В (
	|				ВЫБРАТЬ
	|					ТаблицаТоваров.АналитикаУчетаНоменклатуры
	|				ИЗ
	|					ТаблицаТоваровПоПериодам КАК ТаблицаТоваров
	|				)
	|			И (
	|				ВидЗапасов В (
	|					ВЫБРАТЬ
	|						ДоступныеВидыЗапасов.ВидЗапасов
	|					ИЗ
	|						ДоступныеВидыЗапасов КАК ДоступныеВидыЗапасов
	|					)
	|				)
	|		) КАК Остатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияДокумента.Организация,
	|		ДвиженияДокумента.АналитикаУчетаНоменклатуры,
	|		ДвиженияДокумента.ВидЗапасов,
	|		ДвиженияДокумента.НомерГТД,
	|		ДвиженияДокумента.Количество
	|	ИЗ
	|		ДвиженияДокумента КАК ДвиженияДокумента
	|	ГДЕ
	|		ДвиженияДокумента.ПериодМесяц МЕЖДУ &ПериодЗаполнения И &КонецМесяцКонтроля
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаДанныхДокумента.Организация,
	|		ТаблицаТоваров.АналитикаУчетаНоменклатуры,
	|		ВидыЗапасов.ВидЗапасов,
	|		ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка),
	|		0
	|	ИЗ
	|		ТаблицаДанныхДокумента КАК ТаблицаДанныхДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТоваровПоПериодам КАК ТаблицаТоваров
	|		ПО ИСТИНА
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДоступныеВидыЗапасов КАК ВидыЗапасов
	|		ПО ИСТИНА
	|
	|//%ТекстПриходуемыеВидыЗапасов%
	|
	|	) КАК Остатки
	|
	|СГРУППИРОВАТЬ ПО
	|	Остатки.Организация,
	|	Остатки.АналитикаУчетаНоменклатуры,
	|	Остатки.ВидЗапасов,
	|	Остатки.НомерГТД
	|
	|";
	
	ТекстПриходуемыеВидыЗапасов = "
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ //Дополнительные виды запасов, приходуемые документом
	|	ТаблицаВидыЗапасов.Организация                КАК Организация,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.ВидЗапасов                 КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.НомерГТД                   КАК НомерГТД,
	|	ТаблицаВидыЗапасов.Количество                 КАК Количество
	|ИЗ
	|	ПриходуемыеВидыЗапасов КАК ТаблицаВидыЗапасов
	|ГДЕ
	|	ТаблицаВидыЗапасов.Период <= &КонецМесяцКонтроля
	|";
	
	ТекущийМесяц = КонецМесяца(ПервыйМесяц);
	Пока ТекущийМесяц <= ПоследнийМесяц Цикл
		
		ИмяТаблицы = Формат(ТекущийМесяц, "ДФ=ММММггг");
		
		ТекстВТ = ТекстВТ + СтрЗаменить(ШаблонВт, "#МесяцКонтроля", "На" + ИмяТаблицы);
		Если ПараметрыЗаполненияВидовЗапасов.ЕстьТаблицаПриходуемыхВидовЗапасов Тогда
			ТекстВТ = СтрЗаменить(ТекстВТ, "//%ТекстПриходуемыеВидыЗапасов%", ТекстПриходуемыеВидыЗапасов);
		КонецЕсли;
		ТекстВТ = СтрЗаменить(ТекстВТ, "&ГраницаМесяцКонтроля", "&Граница" + ИмяТаблицы);
		ТекстВТ = СтрЗаменить(ТекстВТ, "&КонецМесяцКонтроля", "&Конец" + ИмяТаблицы);
		
		Запрос.УстановитьПараметр("Граница" + ИмяТаблицы, Новый Граница(ТекущийМесяц, ВидГраницы.Включая));
		Запрос.УстановитьПараметр("Конец" + ИмяТаблицы, ТекущийМесяц);
		
		ТекущийМесяц = КонецМесяца(КонецМесяца(ТекущийМесяц) + 1);
		
	КонецЦикла;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "//%ТекстПромежуточныеМесяцаКонтроля%", ТекстВт);
	
КонецПроцедуры

#КонецОбласти
