////////////////////////////////////////////////////////////////////////////////
// Подсистема "Расчет имущественных налогов".
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

#Область НалогНаИмущество

// Возвращает необходимость уплаты авансов по налогу на имущество.
//
// Параметры:
// 	Организации - СправочникСсылка.Организации - Организация, для которой необходимо выполнить проверку.
// 	Период - Дата - Дата закрытия месяца.
//
// Возвращаемое значение:
// 	Результат - Булево - Признак необходимости уплаты аванса.
//
Функция УплачиваютсяАвансыПоНалогуНаИмущество(Организации, Период) Экспорт
	
	Возврат РасчетИмущественныхНалоговПереопределяемый.УплачиваютсяАвансыПоНалогуНаИмущество(Организации, Период);
	
КонецФункции

// Возвращает результат расчета налога (или аванса по налогу) на имущество.
//
// Параметры:
// 	Организация - СправочникСсылка.Организации - Организация, для которой необходимо выполнить проверку.
// 	ПериодРасчета - Дата - Дата закрытия месяца.
//
// Возвращаемое значение:
// 	ТаблицаРасчетНалога - ТаблицаЗначений - Результат расчета налога.
//
Функция ПолучитьРасчетПоНалогуНаИмущество(Организация, ПериодРасчета) Экспорт
	
	Возврат РасчетИмущественныхНалоговПереопределяемый.ПолучитьРасчетПоНалогуНаИмущество(Организация, ПериодРасчета);
	
КонецФункции

#КонецОбласти

#Область ТранспортныйНалог

// Возвращает необходимость уплаты авансов по транспортному налогу.
//
// Параметры:
// 	Организации - СправочникСсылка.Организация - Организация, для которой необходимо выполнить расчет.
// 	Период - Дата - Дата закрытия месяца.
//
// Возвращаемое значение:
// 	Результат - Булево - Признак необходимости уплаты аванса.
//
Функция УплачиваютсяАвансыПоТранспортномуНалогу(Организации, Период) Экспорт
	
	УплачиваютсяАвансы = Ложь;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организации", Организации);
	Запрос.УстановитьПараметр("Период", Период);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.НалоговыйОрган КАК НалоговыйОрган
	|ПОМЕСТИТЬ ВТ_НалоговыеОрганы
	|ИЗ
	|	(ВЫБРАТЬ
	|		РегистрацияТранспортныхСредств.Организация КАК Организация,
	|		РегистрацияТранспортныхСредств.НалоговыйОрган КАК НалоговыйОрган
	|	ИЗ
	|		РегистрСведений.РегистрацияТранспортныхСредств.СрезПоследних(
	|				КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(&Период, КВАРТАЛ, -1), КВАРТАЛ),
	|				Организация В (&Организации)
	|					И ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|					И ВключатьВНалоговуюБазу) КАК РегистрацияТранспортныхСредств
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РегистрацияТранспортныхСредств.Организация,
	|		РегистрацияТранспортныхСредств.НалоговыйОрган
	|	ИЗ
	|		РегистрСведений.РегистрацияТранспортныхСредств.СрезПоследних(
	|				КОНЕЦПЕРИОДА(&Период, КВАРТАЛ),
	|				Организация В (&Организации)
	|					И ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|					И ВключатьВНалоговуюБазу) КАК РегистрацияТранспортныхСредств
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РегистрацияТранспортныхСредств.Организация,
	|		РегистрацияТранспортныхСредств.НалоговыйОрган
	|	ИЗ
	|		РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
	|	ГДЕ
	|		РегистрацияТранспортныхСредств.Организация В (&Организации)
	|		И РегистрацияТранспортныхСредств.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|		И РегистрацияТранспортныхСредств.Период МЕЖДУ НАЧАЛОПЕРИОДА(&Период, КВАРТАЛ) И КОНЕЦПЕРИОДА(&Период, КВАРТАЛ)
	|		И РегистрацияТранспортныхСредств.ВключатьВНалоговуюБазу
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Организации.Ссылка,
	|		Организации.РегистрацияВНалоговомОргане
	|	ИЗ
	|		Справочник.Организации КАК Организации
	|	ГДЕ
	|		Организации.Ссылка В (&Организации)) КАК ВложенныйЗапрос
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	НалоговыйОрган
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.Организация КАК Организация,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.УплачиваютсяАвансы КАК УплачиваютсяАвансы
	|ПОМЕСТИТЬ ВТ_УплачиваютсяАвансыПоОрганизации
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах.СрезПоследних(
	|			&Период,
	|			Организация В (&Организации)
	|				И РегистрацияВНалоговомОргане = ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|				И Налог = ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ТранспортныйНалог)) КАК ПорядокУплатыНалоговНаМестахСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.Организация КАК Организация,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.УплачиваютсяАвансы КАК УплачиваютсяАвансы
	|ПОМЕСТИТЬ ВТ_УплачиваютсяАвансыПоИФНС
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах.СрезПоследних(
	|			&Период,
	|			Организация В (&Организации)
	|				И РегистрацияВНалоговомОргане <> ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|				И Налог = ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ТранспортныйНалог)) КАК ПорядокУплатыНалоговНаМестахСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	РегистрацияВНалоговомОргане
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(ВЫБОР
	|			КОГДА ВТ_УплачиваютсяАвансыПоИФНС.УплачиваютсяАвансы ЕСТЬ NULL 
	|				ТОГДА ВЫБОР
	|						КОГДА ВТ_УплачиваютсяАвансыПоОрганизации.УплачиваютсяАвансы ЕСТЬ NULL 
	|							ТОГДА ЛОЖЬ
	|						ИНАЧЕ ВТ_УплачиваютсяАвансыПоОрганизации.УплачиваютсяАвансы
	|					КОНЕЦ
	|			ИНАЧЕ ВТ_УплачиваютсяАвансыПоИФНС.УплачиваютсяАвансы
	|		КОНЕЦ), ЛОЖЬ) КАК УплачиваютсяАвансы
	|ИЗ
	|	ВТ_НалоговыеОрганы КАК ВТ_НалоговыеОрганы
	|		ЛЕВОЕ СОЕДИНЕНИЕ 
	|			ВТ_УплачиваютсяАвансыПоОрганизации КАК ВТ_УплачиваютсяАвансыПоОрганизации
	|		ПО 
	|			ВТ_НалоговыеОрганы.Организация = ВТ_УплачиваютсяАвансыПоОрганизации.Организация
	|			
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_УплачиваютсяАвансыПоИФНС КАК ВТ_УплачиваютсяАвансыПоИФНС
	|		ПО 
	|			ВТ_НалоговыеОрганы.Организация = ВТ_УплачиваютсяАвансыПоИФНС.Организация
	|			И ВТ_НалоговыеОрганы.НалоговыйОрган = ВТ_УплачиваютсяАвансыПоИФНС.РегистрацияВНалоговомОргане";
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		Пока Выборка.Следующий() Цикл
			УплачиваютсяАвансы = Выборка.УплачиваютсяАвансы;
		КонецЦикла;
		
	КонецЕсли;
		
	Возврат УплачиваютсяАвансы;
	
КонецФункции

// Возвращает результат расчета транспортного налога (или аванса по налогу).
//
// Параметры:
// 	Организация - СправочникСсылка.Организации - Организация, для которой необходимо выполнить расчет.
// 	ПериодРасчета - Дата - Дата закрытия месяца.
//
// Возвращаемое значение:
// 	ТаблицаРасчетТранспортногоНалога - ТаблицаЗначений - Результат расчета транспортного налога.
//
Функция ПолучитьРасчетПоТранспортномуНалогу(Организация, ПериодРасчета) Экспорт
	
	РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, "РегистрацияВНалоговомОргане");
	
	ГодовойРасчет = КонецКвартала(ПериодРасчета) = КонецГода(ПериодРасчета);
	
	ДатаНачалаПериодаОтчета = ?(ГодовойРасчет, НачалоГода(ПериодРасчета), НачалоКвартала(ПериодРасчета));
	ДатаКонцаПериодаОтчета = КонецКвартала(ПериодРасчета);
	
	ТаблицаРасчетТранспортногоНалога = ПустаяСправкаРасчет("РасчетТранспортногоНалога");
	
	ЭтоНеЮрЛицо = НЕ ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация);
	
	Если Месяц(ПериодРасчета)%3 <> 0 ИЛИ ЭтоНеЮрЛицо Тогда
		Возврат ТаблицаРасчетТранспортногоНалога;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПериодРасчета", ПериодРасчета);
	Запрос.УстановитьПараметр("ОсновнойНалоговыйОрган", РеквизитыОрганизации.РегистрацияВНалоговомОргане);
	
	ТекстОбъединяющегоЗапроса = "";
	
	Период = ДатаНачалаПериодаОтчета;
	
	Пока Период <= ДатаКонцаПериодаОтчета Цикл
		
		// С 2016 года, если регистрация ТС произошла до 15-го числа соответствующего месяца включительно
		// или снятие транспортного средства с регистрации (снятие с учета, исключение из государственного
		// судового реестра и так далее) произошло после 15-го числа соответствующего месяца, за полный 
		// месяц принимается месяц регистрации (снятия с регистрации) транспортного средства.
		
		Если Год(Период) < 2016 Тогда
			КонецПериода = КонецМесяца(Период);
			КонецПрошлогоПериода = НачалоМесяца(Период) - 1;
		Иначе
			КонецПериода = Дата(Год(Период), Месяц(Период), 15, 23, 59, 59);
			КонецПрошлогоПериода = КонецПериода;
		КонецЕсли;
		
		Запрос.УстановитьПараметр("КонецПрошлогоПериода", КонецПрошлогоПериода);
		Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(Период));
		Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РегистрацияТранспортныхСредствСрезПоследних.Период КАК Период,
		|	РегистрацияТранспортныхСредствСрезПоследних.Организация КАК Организация,
		|	РегистрацияТранспортныхСредствСрезПоследних.ОсновноеСредство КАК ОсновноеСредство
		|ПОМЕСТИТЬ ВТ_РегистрацииНаКонецПрошлогоПериода
		|ИЗ
		|	РегистрСведений.РегистрацияТранспортныхСредств.СрезПоследних(&КонецПрошлогоПериода, Организация = &Организация) КАК РегистрацияТранспортныхСредствСрезПоследних
		|ГДЕ
		|	РегистрацияТранспортныхСредствСрезПоследних.ВключатьВНалоговуюБазу
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РегистрацияТранспортныхСредств.Период КАК Период,
		|	РегистрацияТранспортныхСредств.Организация КАК Организация,
		|	РегистрацияТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство
		|ПОМЕСТИТЬ ВТ_РегистрацииВПервыйДеньМесяца
		|ИЗ
		|	РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
		|ГДЕ
		|	РегистрацияТранспортныхСредств.Организация = &Организация
		|	И РегистрацияТранспортныхСредств.Период = &НачалоМесяца
		|	И РегистрацияТранспортныхСредств.ВключатьВНалоговуюБазу
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЕСТЬNULL(ВТ_РегистрацииВПервыйДеньМесяца.Период, ВТ_РегистрацииНаКонецПрошлогоПериода.Период) КАК Период,
		|	ВТ_РегистрацииНаКонецПрошлогоПериода.Организация,
		|	ВТ_РегистрацииНаКонецПрошлогоПериода.ОсновноеСредство
		|ПОМЕСТИТЬ ВТ_СписокОсновныхСредств
		|ИЗ
		|	ВТ_РегистрацииНаКонецПрошлогоПериода КАК ВТ_РегистрацииНаКонецПрошлогоПериода
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РегистрацииВПервыйДеньМесяца КАК ВТ_РегистрацииВПервыйДеньМесяца
		|		ПО ВТ_РегистрацииНаКонецПрошлогоПериода.Организация = ВТ_РегистрацииВПервыйДеньМесяца.Организация
		|			И ВТ_РегистрацииНаКонецПрошлогоПериода.ОсновноеСредство = ВТ_РегистрацииВПервыйДеньМесяца.ОсновноеСредство
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РегистрацияТранспортныхСредств.Период,
		|	РегистрацияТранспортныхСредств.Организация,
		|	РегистрацияТранспортныхСредств.ОсновноеСредство
		|ИЗ
		|	РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
		|ГДЕ
		|	РегистрацияТранспортныхСредств.Организация = &Организация
		|	И РегистрацияТранспортныхСредств.Период МЕЖДУ &НачалоМесяца И &КонецПериода
		|	И РегистрацияТранспортныхСредств.ВключатьВНалоговуюБазу
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_РегистрацииНаКонецПрошлогоПериода
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_РегистрацииВПервыйДеньМесяца
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МИНИМУМ(СписокОсновныхСредств.Период) КАК Период,
		|	СписокОсновныхСредств.Организация КАК Организация,
		|	СписокОсновныхСредств.ОсновноеСредство КАК ОсновноеСредство
		|ПОМЕСТИТЬ ВТ_ПериодРегистрацииТранспортныхСредств
		|ИЗ
		|	ВТ_СписокОсновныхСредств КАК СписокОсновныхСредств
		|
		|СГРУППИРОВАТЬ ПО
		|	СписокОсновныхСредств.ОсновноеСредство,
		|	СписокОсновныхСредств.Организация
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Период,
		|	Организация,
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВТ_ПериодРегистрацииТранспортныхСредств.Период КАК ДатаРегистрационныхДанных,
		|	ВТ_ПериодРегистрацииТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство,
		|	РегистрацияТранспортныхСредств.РегистрационныйЗнак КАК РегистрационныйЗнак,
		|	РегистрацияТранспортныхСредств.Марка КАК Марка,
		|	РегистрацияТранспортныхСредств.ИдентификационныйНомер КАК ИдентификационныйНомер,
		|	ВЫБОР
		|		КОГДА РегистрацияТранспортныхСредств.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
		|			ТОГДА РегистрацияТранспортныхСредств.НалоговыйОрган
		|		ИНАЧЕ &ОсновнойНалоговыйОрган
		|	КОНЕЦ КАК РегистрацияВНалоговомОргане,
		|	ВЫБОР
		|		КОГДА РегистрацияТранспортныхСредств.НалоговыйОрган = ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
		|			ТОГДА ЕСТЬNULL(РегистрацияТранспортныхСредств.Организация.РегистрацияВНалоговомОргане.КодПоОКТМО, """")
		|		ИНАЧЕ РегистрацияТранспортныхСредств.КодПоОКТМО
		|	КОНЕЦ КАК КодПоОКТМО,
		|	РегистрацияТранспортныхСредств.КодВидаТранспортногоСредства КАК КодВидаТранспортногоСредства,
		|	РегистрацияТранспортныхСредств.НалоговаяБаза КАК НалоговаяБаза,
		|	РегистрацияТранспортныхСредств.ЕдиницаИзмеренияНалоговойБазы КАК ЕдиницаИзмеренияНалоговойБазы,
		|	РегистрацияТранспортныхСредств.НалоговаяСтавка КАК НалоговаяСтавка,
		|	РегистрацияТранспортныхСредств.НалоговаяЛьгота КАК НалоговаяЛьгота,
		|	РегистрацияТранспортныхСредств.КодНалоговойЛьготы КАК КодНалоговойЛьготы,
		|	РегистрацияТранспортныхСредств.РегиональныйКодЛьготы КАК РегиональныйКодЛьготы,
		|	РегистрацияТранспортныхСредств.ЛьготнаяСтавка КАК ЛьготнаяСтавка,
		|	РегистрацияТранспортныхСредств.ПроцентУменьшения КАК ПроцентУменьшения,
		|	РегистрацияТранспортныхСредств.СуммаУменьшения КАК СуммаУменьшения,
		|	РегистрацияТранспортныхСредств.ЭкологическийКласс КАК ЭкологическийКласс,
		|	ВЫБОР
		|		КОГДА РегистрацияТранспортныхСредств.ОбщаяСобственность
		|			ТОГДА РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЧислитель
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК ДоляВПравеОбщейСобственностиЧислитель,
		|	ВЫБОР
		|		КОГДА РегистрацияТранспортныхСредств.ОбщаяСобственность
		|			ТОГДА РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЗнаменатель
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК ДоляВПравеОбщейСобственностиЗнаменатель,
		|	РегистрацияТранспортныхСредств.ПовышающийКоэффициент КАК ПовышающийКоэффициент,
		|	РегистрацияТранспортныхСредств.НалоговаяСтавкаЗависитОтГодаВыпускаТС КАК НалоговаяСтавкаЗависитОтГодаВыпускаТС,
		|	1 КАК КоличествоМесяцев
		|ПОМЕСТИТЬ РегистрацияТранспортныхСредств
		|ИЗ
		|	ВТ_ПериодРегистрацииТранспортныхСредств КАК ВТ_ПериодРегистрацииТранспортныхСредств
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
		|		ПО ВТ_ПериодРегистрацииТранспортныхСредств.Период = РегистрацияТранспортныхСредств.Период
		|			И ВТ_ПериодРегистрацииТранспортныхСредств.Организация = РегистрацияТранспортныхСредств.Организация
		|			И ВТ_ПериодРегистрацииТранспортныхСредств.ОсновноеСредство = РегистрацияТранспортныхСредств.ОсновноеСредство";
		
		Подстрока = "ПОМЕСТИТЬ РегистрацияТранспортныхСредств";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, Подстрока, Подстрока + Формат(Период, "ДФ=MM"));
		
		Запрос.Выполнить();
		
		ТекстОбъединяющегоЗапроса = ТекстОбъединяющегоЗапроса
								  + ?(ПустаяСтрока(ТекстОбъединяющегоЗапроса), 
									  "ВЫБРАТЬ РАЗРЕШЕННЫЕ *
		                              |",
									  "ОБЪЕДИНИТЬ ВСЕ
									  |ВЫБРАТЬ *
		                              |")
								  + ?(ПустаяСтрока(ТекстОбъединяющегоЗапроса), 
									  "ПОМЕСТИТЬ РегистрацияТранспортныхСредств
									  |",
									  "")
								  + "ИЗ РегистрацияТранспортныхСредств" + Формат(Период, "ДФ=MM") + "
									|";
									
		Запрос.Текст = 
		"УНИЧТОЖИТЬ ВТ_СписокОсновныхСредств
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТ_ПериодРегистрацииТранспортныхСредств";
		Запрос.Выполнить();
		
		Период = ДобавитьМесяц(Период, 1);
		
	КонецЦикла;
	
	Запрос.Текст = ТекстОбъединяющегоЗапроса;
	РезультатЗапроса = Запрос.Выполнить();
	
	СоздатьНалоговыеОрганыСУстановленнойУплатойАвансов(
		Запрос.МенеджерВременныхТаблиц, Организация, ПериодРасчета, "ТранспортныйНалог");
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РегистрацияТранспортныхСредств.Период КАК Период,
	|	РегистрацияТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ ВТ_Регистрации
	|ИЗ
	|	РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
	|ГДЕ
	|	РегистрацияТранспортныхСредств.Организация = &Организация
	|	И РегистрацияТранспортныхСредств.Период <= &ПериодРасчета
	|	И РегистрацияТранспортныхСредств.ВидЗаписи = ЗНАЧЕНИЕ(Перечисление.ВидЗаписиОРегистрации.Регистрация)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегистрацияТранспортныхСредств.Период КАК Период,
	|	РегистрацияТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ ВТ_Снятия
	|ИЗ
	|	РегистрСведений.РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
	|ГДЕ
	|	РегистрацияТранспортныхСредств.Организация = &Организация
	|	И РегистрацияТранспортныхСредств.Период <= &ПериодРасчета
	|	И РегистрацияТранспортныхСредств.ВидЗаписи = ЗНАЧЕНИЕ(Перечисление.ВидЗаписиОРегистрации.СнятиеСРегистрационногоУчета)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Регистрации.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_Регистрации.Период КАК Период,
	|	МАКСИМУМ(ЕСТЬNULL(ВТ_Снятия.Период, ДАТАВРЕМЯ(1, 1, 1))) КАК ПериодПредыдущегоСнятия
	|ПОМЕСТИТЬ ВТ_ПредыдущиеСнятия
	|ИЗ
	|	ВТ_Регистрации КАК ВТ_Регистрации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Снятия КАК ВТ_Снятия
	|		ПО ВТ_Регистрации.ОсновноеСредство = ВТ_Снятия.ОсновноеСредство
	|			И ВТ_Регистрации.Период > ВТ_Снятия.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Регистрации.ОсновноеСредство,
	|	ВТ_Регистрации.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПредыдущиеСнятия.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_ПредыдущиеСнятия.Период КАК Период,
	|	МИНИМУМ(ВТ_Регистрации.Период) КАК ПериодПервоначальнойРегистрации
	|ПОМЕСТИТЬ ВТ_ПервоначальныеРегистрации
	|ИЗ
	|	ВТ_ПредыдущиеСнятия КАК ВТ_ПредыдущиеСнятия
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Регистрации КАК ВТ_Регистрации
	|		ПО ВТ_ПредыдущиеСнятия.ОсновноеСредство = ВТ_Регистрации.ОсновноеСредство
	|			И ВТ_ПредыдущиеСнятия.ПериодПредыдущегоСнятия < ВТ_Регистрации.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПредыдущиеСнятия.ОсновноеСредство,
	|	ВТ_ПредыдущиеСнятия.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Регистрации.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_Регистрации.Период КАК Период,
	|	МИНИМУМ(ЕСТЬNULL(ВТ_Снятия.Период, ДАТАВРЕМЯ(1, 1, 1))) КАК ПериодСнятияСУчета
	|ПОМЕСТИТЬ ВТ_СнятияСУчета
	|ИЗ
	|	ВТ_Регистрации КАК ВТ_Регистрации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Снятия КАК ВТ_Снятия
	|		ПО ВТ_Регистрации.ОсновноеСредство = ВТ_Снятия.ОсновноеСредство
	|			И ВТ_Регистрации.Период < ВТ_Снятия.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Регистрации.ОсновноеСредство,
	|	ВТ_Регистрации.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Регистрации.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_Регистрации.Период КАК Период,
	|	МАКСИМУМ(ВТ_ПервоначальныеРегистрации.ПериодПервоначальнойРегистрации) КАК ПериодПервоначальнойРегистрации,
	|	МАКСИМУМ(ВТ_СнятияСУчета.ПериодСнятияСУчета) КАК ПериодСнятияСУчета
	|ПОМЕСТИТЬ ВТ_ДатыПостановкиИСнятия
	|ИЗ
	|	ВТ_Регистрации КАК ВТ_Регистрации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПервоначальныеРегистрации КАК ВТ_ПервоначальныеРегистрации
	|		ПО ВТ_Регистрации.ОсновноеСредство = ВТ_ПервоначальныеРегистрации.ОсновноеСредство
	|			И ВТ_Регистрации.Период = ВТ_ПервоначальныеРегистрации.Период
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СнятияСУчета КАК ВТ_СнятияСУчета
	|		ПО ВТ_Регистрации.ОсновноеСредство = ВТ_СнятияСУчета.ОсновноеСредство
	|			И ВТ_Регистрации.Период = ВТ_СнятияСУчета.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Регистрации.ОсновноеСредство,
	|	ВТ_Регистрации.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(РегистрацияТранспортныхСредств.КоличествоМесяцев) КАК КоличествоМесяцев,
	|	РегистрацияТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство,
	|	РегистрацияТранспортныхСредств.РегистрационныйЗнак КАК РегистрационныйЗнак,
	|	РегистрацияТранспортныхСредств.Марка КАК Марка,
	|	РегистрацияТранспортныхСредств.ИдентификационныйНомер КАК ИдентификационныйНомер,
	|	РегистрацияТранспортныхСредств.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	РегистрацияТранспортныхСредств.КодПоОКТМО КАК КодПоОКТМО,
	|	РегистрацияТранспортныхСредств.КодВидаТранспортногоСредства КАК КодВидаТранспортногоСредства,
	|	РегистрацияТранспортныхСредств.НалоговаяБаза КАК НалоговаяБаза,
	|	РегистрацияТранспортныхСредств.ЕдиницаИзмеренияНалоговойБазы КАК ЕдиницаИзмеренияНалоговойБазы,
	|	РегистрацияТранспортныхСредств.НалоговаяСтавка КАК НалоговаяСтавка,
	|	РегистрацияТранспортныхСредств.НалоговаяЛьгота КАК НалоговаяЛьгота,
	|	РегистрацияТранспортныхСредств.КодНалоговойЛьготы КАК КодНалоговойЛьготы,
	|	РегистрацияТранспортныхСредств.РегиональныйКодЛьготы КАК РегиональныйКодЛьготы,
	|	РегистрацияТранспортныхСредств.ЛьготнаяСтавка КАК ЛьготнаяСтавка,
	|	РегистрацияТранспортныхСредств.ПроцентУменьшения КАК ПроцентУменьшения,
	|	РегистрацияТранспортныхСредств.СуммаУменьшения КАК СуммаУменьшения,
	|	РегистрацияТранспортныхСредств.ЭкологическийКласс КАК ЭкологическийКласс,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РегистрацияТранспортныхСредств.ПовышающийКоэффициент КАК ПовышающийКоэффициент,
	|	РегистрацияТранспортныхСредств.НалоговаяСтавкаЗависитОтГодаВыпускаТС КАК НалоговаяСтавкаЗависитОтГодаВыпускаТС
	|ПОМЕСТИТЬ ВТ_РазличныеЗаписи
	|ИЗ
	|	РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
	|
	|СГРУППИРОВАТЬ ПО
	|	РегистрацияТранспортныхСредств.ОсновноеСредство,
	|	РегистрацияТранспортныхСредств.РегистрационныйЗнак,
	|	РегистрацияТранспортныхСредств.Марка,
	|	РегистрацияТранспортныхСредств.ИдентификационныйНомер,
	|	РегистрацияТранспортныхСредств.РегистрацияВНалоговомОргане,
	|	РегистрацияТранспортныхСредств.КодПоОКТМО,
	|	РегистрацияТранспортныхСредств.КодВидаТранспортногоСредства,
	|	РегистрацияТранспортныхСредств.НалоговаяБаза,
	|	РегистрацияТранспортныхСредств.ЕдиницаИзмеренияНалоговойБазы,
	|	РегистрацияТранспортныхСредств.НалоговаяСтавка,
	|	РегистрацияТранспортныхСредств.НалоговаяЛьгота,
	|	РегистрацияТранспортныхСредств.КодНалоговойЛьготы,
	|	РегистрацияТранспортныхСредств.РегиональныйКодЛьготы,
	|	РегистрацияТранспортныхСредств.ЛьготнаяСтавка,
	|	РегистрацияТранспортныхСредств.ПроцентУменьшения,
	|	РегистрацияТранспортныхСредств.СуммаУменьшения,
	|	РегистрацияТранспортныхСредств.ЭкологическийКласс,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЧислитель,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РегистрацияТранспортныхСредств.ПовышающийКоэффициент,
	|	РегистрацияТранспортныхСредств.НалоговаяСтавкаЗависитОтГодаВыпускаТС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВТ_РазличныеЗаписи.КоличествоМесяцев) КАК КоличествоМесяцев,
	|	ВТ_РазличныеЗаписи.ОсновноеСредство КАК ОсновноеСредство,
	|	ВТ_РазличныеЗаписи.РегистрационныйЗнак КАК РегистрационныйЗнак,
	|	ВТ_РазличныеЗаписи.Марка КАК Марка,
	|	ВТ_РазличныеЗаписи.ИдентификационныйНомер КАК ИдентификационныйНомер,
	|	ВТ_РазличныеЗаписи.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ВТ_РазличныеЗаписи.КодПоОКТМО КАК КодПоОКТМО,
	|	ВТ_РазличныеЗаписи.КодВидаТранспортногоСредства КАК КодВидаТранспортногоСредства,
	|	ВТ_РазличныеЗаписи.НалоговаяБаза КАК НалоговаяБаза,
	|	ВТ_РазличныеЗаписи.ЕдиницаИзмеренияНалоговойБазы КАК ЕдиницаИзмеренияНалоговойБазы,
	|	ВТ_РазличныеЗаписи.НалоговаяСтавка КАК НалоговаяСтавка,
	|	ВТ_РазличныеЗаписи.ЭкологическийКласс КАК ЭкологическийКласс,
	|	ВТ_РазличныеЗаписи.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	ВТ_РазличныеЗаписи.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	ВТ_РазличныеЗаписи.ПовышающийКоэффициент КАК ПовышающийКоэффициент,
	|	ВТ_РазличныеЗаписи.НалоговаяСтавкаЗависитОтГодаВыпускаТС КАК НалоговаяСтавкаЗависитОтГодаВыпускаТС,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_РазличныеЗаписи.НалоговаяЛьгота <> ЗНАЧЕНИЕ(Перечисление.ВидыНалоговыхЛьготПоТранспортномуНалогу.НеПрименяется)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоЗаписейЛьгот
	|ПОМЕСТИТЬ ВТ_ЗаписиБезУчетаЛьгот
	|ИЗ
	|	ВТ_РазличныеЗаписи КАК ВТ_РазличныеЗаписи
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_РазличныеЗаписи.ОсновноеСредство,
	|	ВТ_РазличныеЗаписи.РегистрационныйЗнак,
	|	ВТ_РазличныеЗаписи.Марка,
	|	ВТ_РазличныеЗаписи.ИдентификационныйНомер,
	|	ВТ_РазличныеЗаписи.РегистрацияВНалоговомОргане,
	|	ВТ_РазличныеЗаписи.КодПоОКТМО,
	|	ВТ_РазличныеЗаписи.КодВидаТранспортногоСредства,
	|	ВТ_РазличныеЗаписи.НалоговаяБаза,
	|	ВТ_РазличныеЗаписи.ЕдиницаИзмеренияНалоговойБазы,
	|	ВТ_РазличныеЗаписи.НалоговаяСтавка,
	|	ВТ_РазличныеЗаписи.ЭкологическийКласс,
	|	ВТ_РазличныеЗаписи.ДоляВПравеОбщейСобственностиЧислитель,
	|	ВТ_РазличныеЗаписи.ДоляВПравеОбщейСобственностиЗнаменатель,
	|	ВТ_РазличныеЗаписи.ПовышающийКоэффициент,
	|	ВТ_РазличныеЗаписи.НалоговаяСтавкаЗависитОтГодаВыпускаТС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(РегистрацияТранспортныхСредств.ДатаРегистрационныхДанных) КАК ДатаРегистрационныхДанных,
	|	СУММА(РегистрацияТранспортныхСредств.КоличествоМесяцев) КАК КоличествоМесяцевЛьготы,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТ_ЗаписиБезУчетаЛьгот.КоличествоМесяцев, 0) = 0
	|			ТОГДА СУММА(РегистрацияТранспортныхСредств.КоличествоМесяцев)
	|		ИНАЧЕ ВТ_ЗаписиБезУчетаЛьгот.КоличествоМесяцев
	|	КОНЕЦ КАК КоличествоМесяцевВладения,
	|	РегистрацияТранспортныхСредств.ОсновноеСредство КАК ОсновноеСредство,
	|	РегистрацияТранспортныхСредств.РегистрационныйЗнак КАК РегистрационныйЗнак,
	|	РегистрацияТранспортныхСредств.Марка КАК Марка,
	|	РегистрацияТранспортныхСредств.ИдентификационныйНомер КАК ИдентификационныйНомер,
	|	РегистрацияТранспортныхСредств.РегистрацияВНалоговомОргане КАК ИФНС,
	|	РегистрацияТранспортныхСредств.КодПоОКТМО КАК КодПоОКТМО,
	|	РегистрацияТранспортныхСредств.КодВидаТранспортногоСредства КАК КодВидаТранспортногоСредства,
	|	РегистрацияТранспортныхСредств.НалоговаяБаза КАК НалоговаяБаза,
	|	РегистрацияТранспортныхСредств.ЕдиницаИзмеренияНалоговойБазы КАК ЕдиницаИзмерения,
	|	РегистрацияТранспортныхСредств.НалоговаяСтавка КАК НалоговаяСтавка,
	|	РегистрацияТранспортныхСредств.НалоговаяЛьгота КАК НалоговаяЛьгота,
	|	РегистрацияТранспортныхСредств.КодНалоговойЛьготы КАК КодНалоговойЛьготы,
	|	РегистрацияТранспортныхСредств.РегиональныйКодЛьготы КАК РегиональныйКодЛьготы,
	|	РегистрацияТранспортныхСредств.ЛьготнаяСтавка КАК ЛьготнаяСтавка,
	|	РегистрацияТранспортныхСредств.ПроцентУменьшения КАК ПроцентУменьшения,
	|	РегистрацияТранспортныхСредств.СуммаУменьшения КАК СуммаУменьшения,
	|	РегистрацияТранспортныхСредств.ЭкологическийКласс КАК ЭкологическийКласс,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РегистрацияТранспортныхСредств.ПовышающийКоэффициент КАК ПовышающийКоэффициент,
	|	РегистрацияТранспортныхСредств.НалоговаяСтавкаЗависитОтГодаВыпускаТС КАК НалоговаяСтавкаЗависитОтГодаВыпускаТС,
	|	ЕСТЬNULL(ВТ_ЗаписиБезУчетаЛьгот.КоличествоЗаписейЛьгот, 0) КАК КоличествоЗаписейЛьгот,
	|	ВЫБОР
	|		КОГДА ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК УплачиваютсяАвансы,
	|	ОсновныеСредства.ДатаВыпуска КАК ДатаВыпуска,
	|	ВТ_ДатыПостановкиИСнятия.ПериодПервоначальнойРегистрации КАК ДатаРегистрации,
	|	ВТ_ДатыПостановкиИСнятия.ПериодСнятияСУчета КАК ДатаПрекращенияРегистрации
	|ИЗ
	|	РегистрацияТранспортныхСредств КАК РегистрацияТранспортныхСредств
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЗаписиБезУчетаЛьгот КАК ВТ_ЗаписиБезУчетаЛьгот
	|		ПО РегистрацияТранспортныхСредств.ОсновноеСредство = ВТ_ЗаписиБезУчетаЛьгот.ОсновноеСредство
	|			И РегистрацияТранспортныхСредств.РегистрационныйЗнак = ВТ_ЗаписиБезУчетаЛьгот.РегистрационныйЗнак
	|			И РегистрацияТранспортныхСредств.Марка = ВТ_ЗаписиБезУчетаЛьгот.Марка
	|			И РегистрацияТранспортныхСредств.ИдентификационныйНомер = ВТ_ЗаписиБезУчетаЛьгот.ИдентификационныйНомер
	|			И РегистрацияТранспортныхСредств.РегистрацияВНалоговомОргане = ВТ_ЗаписиБезУчетаЛьгот.РегистрацияВНалоговомОргане
	|			И РегистрацияТранспортныхСредств.КодПоОКТМО = ВТ_ЗаписиБезУчетаЛьгот.КодПоОКТМО
	|			И РегистрацияТранспортныхСредств.КодВидаТранспортногоСредства = ВТ_ЗаписиБезУчетаЛьгот.КодВидаТранспортногоСредства
	|			И РегистрацияТранспортныхСредств.НалоговаяБаза = ВТ_ЗаписиБезУчетаЛьгот.НалоговаяБаза
	|			И РегистрацияТранспортныхСредств.ЕдиницаИзмеренияНалоговойБазы = ВТ_ЗаписиБезУчетаЛьгот.ЕдиницаИзмеренияНалоговойБазы
	|			И РегистрацияТранспортныхСредств.НалоговаяСтавка = ВТ_ЗаписиБезУчетаЛьгот.НалоговаяСтавка
	|			И РегистрацияТранспортныхСредств.ЭкологическийКласс = ВТ_ЗаписиБезУчетаЛьгот.ЭкологическийКласс
	|			И РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЧислитель = ВТ_ЗаписиБезУчетаЛьгот.ДоляВПравеОбщейСобственностиЧислитель
	|			И РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЗнаменатель = ВТ_ЗаписиБезУчетаЛьгот.ДоляВПравеОбщейСобственностиЗнаменатель
	|			И РегистрацияТранспортныхСредств.ПовышающийКоэффициент = ВТ_ЗаписиБезУчетаЛьгот.ПовышающийКоэффициент
	|			И РегистрацияТранспортныхСредств.НалоговаяСтавкаЗависитОтГодаВыпускаТС = ВТ_ЗаписиБезУчетаЛьгот.НалоговаяСтавкаЗависитОтГодаВыпускаТС
	|			И (ВТ_ЗаписиБезУчетаЛьгот.КоличествоЗаписейЛьгот = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов КАК ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов
	|		ПО РегистрацияТранспортныхСредств.РегистрацияВНалоговомОргане = ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыЭксплуатации КАК ОсновныеСредства
	|		ПО РегистрацияТранспортныхСредств.ОсновноеСредство = ОсновныеСредства.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДатыПостановкиИСнятия КАК ВТ_ДатыПостановкиИСнятия
	|		ПО РегистрацияТранспортныхСредств.ОсновноеСредство = ВТ_ДатыПостановкиИСнятия.ОсновноеСредство
	|			И РегистрацияТранспортныхСредств.ДатаРегистрационныхДанных = ВТ_ДатыПостановкиИСнятия.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	РегистрацияТранспортныхСредств.ОсновноеСредство,
	|	РегистрацияТранспортныхСредств.РегистрационныйЗнак,
	|	РегистрацияТранспортныхСредств.Марка,
	|	РегистрацияТранспортныхСредств.ИдентификационныйНомер,
	|	РегистрацияТранспортныхСредств.РегистрацияВНалоговомОргане,
	|	РегистрацияТранспортныхСредств.КодПоОКТМО,
	|	РегистрацияТранспортныхСредств.КодВидаТранспортногоСредства,
	|	РегистрацияТранспортныхСредств.НалоговаяБаза,
	|	РегистрацияТранспортныхСредств.ЕдиницаИзмеренияНалоговойБазы,
	|	РегистрацияТранспортныхСредств.НалоговаяСтавка,
	|	РегистрацияТранспортныхСредств.НалоговаяЛьгота,
	|	РегистрацияТранспортныхСредств.КодНалоговойЛьготы,
	|	РегистрацияТранспортныхСредств.РегиональныйКодЛьготы,
	|	РегистрацияТранспортныхСредств.ЛьготнаяСтавка,
	|	РегистрацияТранспортныхСредств.ПроцентУменьшения,
	|	РегистрацияТранспортныхСредств.СуммаУменьшения,
	|	РегистрацияТранспортныхСредств.ЭкологическийКласс,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЧислитель,
	|	РегистрацияТранспортныхСредств.ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РегистрацияТранспортныхСредств.ПовышающийКоэффициент,
	|	РегистрацияТранспортныхСредств.НалоговаяСтавкаЗависитОтГодаВыпускаТС,
	|	ВТ_ЗаписиБезУчетаЛьгот.КоличествоМесяцев,
	|	ВЫБОР
	|		КОГДА ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	ОсновныеСредства.ДатаВыпуска,
	|	ЕСТЬNULL(ВТ_ЗаписиБезУчетаЛьгот.КоличествоЗаписейЛьгот, 0),
	|	ВТ_ДатыПостановкиИСнятия.ПериодПервоначальнойРегистрации,
	|	ВТ_ДатыПостановкиИСнятия.ПериодСнятияСУчета
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИФНС,
	|	КодПоОКТМО,
	|	ОсновноеСредство";
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		РасчетТранспортногоНалога = Результат.Выгрузить();
		
		Если ГодовойРасчет Тогда
			
			РасчетНалога1Кв = ТаблицаРасчетТранспортногоНалога.СкопироватьКолонки();
			РасчетНалога2Кв = ТаблицаРасчетТранспортногоНалога.СкопироватьКолонки();
			РасчетНалога3Кв = ТаблицаРасчетТранспортногоНалога.СкопироватьКолонки();
			
			Отбор = Новый Структура("ИФНС, КодПоОКТМО, ОсновноеСредство, ДатаРегистрационныхДанных");
			
			НайденнаяСтрока = РасчетТранспортногоНалога.Найти(Истина, "УплачиваютсяАвансы");
			Если НайденнаяСтрока <> Неопределено Тогда
				РасчетНалога1Кв = ПолучитьАвансовыйРасчетПоТранспортномуНалогу(Организация, Дата(Год(ПериодРасчета), 03, 31));
				РасчетНалога1Кв.Индексы.Добавить("ИФНС, КодПоОКТМО, ОсновноеСредство, ДатаРегистрационныхДанных");

				РасчетНалога2Кв = ПолучитьАвансовыйРасчетПоТранспортномуНалогу(Организация, Дата(Год(ПериодРасчета), 06, 30));
				РасчетНалога2Кв.Индексы.Добавить("ИФНС, КодПоОКТМО, ОсновноеСредство, ДатаРегистрационныхДанных");

				РасчетНалога3Кв = ПолучитьАвансовыйРасчетПоТранспортномуНалогу(Организация, Дата(Год(ПериодРасчета), 09, 30));
				РасчетНалога3Кв.Индексы.Добавить("ИФНС, КодПоОКТМО, ОсновноеСредство, ДатаРегистрационныхДанных");
			КонецЕсли;
			
		КонецЕсли;
		
		Для Каждого СтрокаРасчета Из РасчетТранспортногоНалога Цикл
			
			Если НЕ ГодовойРасчет И НЕ СтрокаРасчета.УплачиваютсяАвансы Тогда
				Продолжить;
			КонецЕсли;
			
			// Для расчета используется та строка, в которой содержится информация о льготе.
			Если СтрокаРасчета.КоличествоЗаписейЛьгот = 1
			   И СтрокаРасчета.НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.НеПрименяется Тогда
				Продолжить;
			КонецЕсли;

			Если Год(ДатаКонцаПериодаОтчета) > 2015 
			   И НачалоМесяца(СтрокаРасчета.ДатаРегистрации) = НачалоМесяца(СтрокаРасчета.ДатаПрекращенияРегистрации)
			   И ((День(СтрокаРасчета.ДатаРегистрации) <= 15 И День(СтрокаРасчета.ДатаПрекращенияРегистрации) <= 15)
			       ИЛИ (День(СтрокаРасчета.ДатаРегистрации) > 15 И День(СтрокаРасчета.ДатаПрекращенияРегистрации) > 15)) Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = ТаблицаРасчетТранспортногоНалога.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРасчета, ,
				"КоличествоМесяцевЛьготы,КодНалоговойЛьготы,РегиональныйКодЛьготы,
				|ПроцентУменьшения,ЛьготнаяСтавка");
				
			Если СтрокаРасчета.НалоговаяСтавкаЗависитОтГодаВыпускаТС И ЗначениеЗаполнено(СтрокаРасчета.ДатаВыпуска) Тогда
					НоваяСтрока.КоличествоЛетПрошедшихСГодаВыпускаТС = 
						Год(ДатаКонцаПериодаОтчета) - Год(СтрокаРасчета.ДатаВыпуска);
			КонецЕсли;
			
			КоличествоМесяцевВПериоде = ?(ГодовойРасчет, 12, 3);
			Коэффициент = Окр(НоваяСтрока.КоличествоМесяцевВладения / КоличествоМесяцевВПериоде, 4);
			
			Если Год(ДатаКонцаПериодаОтчета) > 2014 ИЛИ (ГодовойРасчет И Год(ДатаКонцаПериодаОтчета) = 2014) Тогда
				
				Если НоваяСтрока.ДоляВПравеОбщейСобственностиЗнаменатель = 0 
					ИЛИ НоваяСтрока.ДоляВПравеОбщейСобственностиЧислитель = 0 Тогда
					
					ДоляВПравеОбщейСобственности = 1;
					
				Иначе
					
					ДоляВПравеОбщейСобственности = НоваяСтрока.ДоляВПравеОбщейСобственностиЧислитель 
						/ НоваяСтрока.ДоляВПравеОбщейСобственностиЗнаменатель;
					
				КонецЕсли;
					
			Иначе
				
				НоваяСтрока.ДоляВПравеОбщейСобственностиЗнаменатель = 0;
				НоваяСтрока.ДоляВПравеОбщейСобственностиЧислитель = 0;
				ДоляВПравеОбщейСобственности = 1;
				
			КонецЕсли;
			
			Если СтрокаРасчета.ПовышающийКоэффициент <> 0
				И (Год(ДатаКонцаПериодаОтчета) >= 2015 ИЛИ (ГодовойРасчет И Год(ДатаКонцаПериодаОтчета) = 2014)) Тогда
				
				ПовышающийКоэффициент = СтрокаРасчета.ПовышающийКоэффициент;
				
			Иначе
				
				ПовышающийКоэффициент = 1;
				НоваяСтрока.ПовышающийКоэффициент = 0;
				
			КонецЕсли;
			
			НоваяСтрока.СуммаНалога = Окр(Окр(НоваяСтрока.НалоговаяБаза 
				* НоваяСтрока.НалоговаяСтавка 
				* ДоляВПравеОбщейСобственности 
				* Коэффициент
				* ПовышающийКоэффициент
				* ?(ГодовойРасчет, 1, 1 / 4), 2));
			
			НалоговаяЛьгота = СтрокаРасчета.НалоговаяЛьгота;
			
			Если НалоговаяЛьгота <> Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.НеПрименяется
				И НалоговаяЛьгота <> Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаНаСумму Тогда 
				
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРасчета,
					"КоличествоМесяцевЛьготы,КодНалоговойЛьготы,РегиональныйКодЛьготы,ПроцентУменьшения,ЛьготнаяСтавка");
				
				КоэффициентЛьготы = Окр(НоваяСтрока.КоличествоМесяцевЛьготы / 12, 4);
					
				Если НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.ОсвобождениеОтНалогообложения Тогда 
					
					НоваяСтрока.СуммаНалоговойЛьготы = 
						Окр(Окр(НоваяСтрока.НалоговаяБаза 
							* НоваяСтрока.НалоговаяСтавка 
							* КоэффициентЛьготы
							* ДоляВПравеОбщейСобственности
							* ПовышающийКоэффициент, 2));
					
				ИначеЕсли НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.УменьшениеСуммыНалогаВПроцентах Тогда 
					
					НоваяСтрока.СуммаНалоговойЛьготы = 
						Окр(Окр(НоваяСтрока.НалоговаяБаза 
							* НоваяСтрока.НалоговаяСтавка 
							* КоэффициентЛьготы 
							* (НоваяСтрока.ПроцентУменьшения / 100)
							* ДоляВПравеОбщейСобственности
							* ПовышающийКоэффициент, 2));
					
				ИначеЕсли НалоговаяЛьгота = Перечисления.ВидыНалоговыхЛьготПоТранспортномуНалогу.СнижениеНалоговойСтавки Тогда 
					
					НоваяСтрока.СуммаНалоговойЛьготы = 
						Окр(Окр(НоваяСтрока.НалоговаяБаза 
						* (НоваяСтрока.НалоговаяСтавка - НоваяСтрока.ЛьготнаяСтавка)
						* КоэффициентЛьготы
						* ДоляВПравеОбщейСобственности
						* ПовышающийКоэффициент, 2));

				КонецЕсли;
				
				
			КонецЕсли;
			
			НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалога - НоваяСтрока.СуммаНалоговойЛьготы;
			
			Если ГодовойРасчет И СтрокаРасчета.УплачиваютсяАвансы Тогда
				
				ЗаполнитьЗначенияСвойств(Отбор, НоваяСтрока);
				
				НоваяСтрока.СуммаАвансовыхПлатежей1Кв = 0;
				СтрокиРасчетаАвансовыхПлатежей = РасчетНалога1Кв.НайтиСтроки(Отбор);
				Для Каждого СтрокаРасчетаАвансовыхПлатежей Из СтрокиРасчетаАвансовыхПлатежей Цикл
					НоваяСтрока.СуммаАвансовыхПлатежей1Кв = НоваяСтрока.СуммаАвансовыхПлатежей1Кв
						+ СтрокаРасчетаАвансовыхПлатежей.СуммаНалогаКУплате;
				КонецЦикла;
				
				НоваяСтрока.СуммаАвансовыхПлатежей2Кв = 0;
				СтрокиРасчетаАвансовыхПлатежей = РасчетНалога2Кв.НайтиСтроки(Отбор);
				Для Каждого СтрокаРасчетаАвансовыхПлатежей Из СтрокиРасчетаАвансовыхПлатежей Цикл
					НоваяСтрока.СуммаАвансовыхПлатежей2Кв = НоваяСтрока.СуммаАвансовыхПлатежей2Кв
						+ СтрокаРасчетаАвансовыхПлатежей.СуммаНалогаКУплате;
				КонецЦикла;
				
				НоваяСтрока.СуммаАвансовыхПлатежей3Кв = 0;
				СтрокиРасчетаАвансовыхПлатежей = РасчетНалога3Кв.НайтиСтроки(Отбор);
				Для Каждого СтрокаРасчетаАвансовыхПлатежей Из СтрокиРасчетаАвансовыхПлатежей Цикл
					НоваяСтрока.СуммаАвансовыхПлатежей3Кв = НоваяСтрока.СуммаАвансовыхПлатежей3Кв
						+ СтрокаРасчетаАвансовыхПлатежей.СуммаНалогаКУплате;
				КонецЦикла;
				
				НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалогаКУплате - НоваяСтрока.СуммаАвансовыхПлатежей1Кв
					- НоваяСтрока.СуммаАвансовыхПлатежей2Кв - НоваяСтрока.СуммаАвансовыхПлатежей3Кв;
				
			КонецЕсли;

			Если НЕ ГодовойРасчет Тогда
				НоваяСтрока["СуммаАвансовыхПлатежей" + Формат(ПериодРасчета, "ДФ='q''Кв'''")] = НоваяСтрока.СуммаНалогаКУплате;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ТаблицаРасчетТранспортногоНалога.ЗаполнитьЗначения(ДатаКонцаПериодаОтчета, "ПериодРасчета");
	ТаблицаРасчетТранспортногоНалога.ЗаполнитьЗначения(Организация, "Организация");
	
	Возврат ТаблицаРасчетТранспортногоНалога;

КонецФункции

#КонецОбласти

#Область ЗемельныйНалог

// Возвращает необходимость уплаты авансов по земельному налогу.
//
// Параметры:
// 	Организации - СправочникСсылка.Организация - Организация, для которой необходимо выполнить проверку.
// 	Период - Дата - Дата закрытия месяца.
//
// Возвращаемое значение:
// 	Результат - Булево - Признак необходимости уплаты аванса.
//
Функция УплачиваютсяАвансыПоЗемельномуНалогу(Организации, Период) Экспорт
	
	УплачиваютсяАвансы = Ложь;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организации", Организации);
	Запрос.УстановитьПараметр("Период", Период);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.НалоговыйОрган КАК НалоговыйОрган
	|ПОМЕСТИТЬ ВТ_НалоговыеОрганы
	|ИЗ
	|	(ВЫБРАТЬ
	|		РегистрацияЗемельныхУчастков.Организация КАК Организация,
	|		РегистрацияЗемельныхУчастков.НалоговыйОрган КАК НалоговыйОрган
	|	ИЗ
	|		РегистрСведений.РегистрацияЗемельныхУчастков.СрезПоследних(
	|				КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(&Период, КВАРТАЛ, -1), КВАРТАЛ),
	|				Организация В (&Организации)
	|					И ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|					И ВключатьВНалоговуюБазу) КАК РегистрацияЗемельныхУчастков
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РегистрацияЗемельныхУчастков.Организация,
	|		РегистрацияЗемельныхУчастков.НалоговыйОрган
	|	ИЗ
	|		РегистрСведений.РегистрацияЗемельныхУчастков.СрезПоследних(
	|				КОНЕЦПЕРИОДА(&Период, КВАРТАЛ),
	|				Организация В (&Организации)
	|					И ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|					И ВключатьВНалоговуюБазу) КАК РегистрацияЗемельныхУчастков
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РегистрацияЗемельныхУчастков.Организация,
	|		РегистрацияЗемельныхУчастков.НалоговыйОрган
	|	ИЗ
	|		РегистрСведений.РегистрацияЗемельныхУчастков КАК РегистрацияЗемельныхУчастков
	|	ГДЕ
	|		РегистрацияЗемельныхУчастков.Организация В (&Организации)
	|		И РегистрацияЗемельныхУчастков.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|		И РегистрацияЗемельныхУчастков.Период МЕЖДУ НАЧАЛОПЕРИОДА(&Период, КВАРТАЛ) И КОНЕЦПЕРИОДА(&Период, КВАРТАЛ)
	|		И РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Организации.Ссылка,
	|		Организации.РегистрацияВНалоговомОргане
	|	ИЗ
	|		Справочник.Организации КАК Организации
	|	ГДЕ
	|		Организации.Ссылка В (&Организации)) КАК ВложенныйЗапрос
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	НалоговыйОрган
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.Организация КАК Организация,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.УплачиваютсяАвансы КАК УплачиваютсяАвансы
	|ПОМЕСТИТЬ ВТ_УплачиваютсяАвансыПоОрганизации
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах.СрезПоследних(
	|			&Период,
	|			Организация В (&Организации)
	|				И РегистрацияВНалоговомОргане = ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|				И Налог = ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ЗемельныйНалог)) КАК ПорядокУплатыНалоговНаМестахСрезПоследних
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.Организация КАК Организация,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.УплачиваютсяАвансы КАК УплачиваютсяАвансы
	|ПОМЕСТИТЬ ВТ_УплачиваютсяАвансыПоИФНС
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах.СрезПоследних(
	|			&Период,
	|			Организация В (&Организации)
	|				И РегистрацияВНалоговомОргане <> ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|				И Налог = ЗНАЧЕНИЕ(Перечисление.ВидыИмущественныхНалогов.ЗемельныйНалог)) КАК ПорядокУплатыНалоговНаМестахСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	РегистрацияВНалоговомОргане
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(ВЫБОР
	|			КОГДА ВТ_УплачиваютсяАвансыПоИФНС.УплачиваютсяАвансы ЕСТЬ NULL 
	|				ТОГДА ВЫБОР
	|						КОГДА ВТ_УплачиваютсяАвансыПоОрганизации.УплачиваютсяАвансы ЕСТЬ NULL 
	|							ТОГДА ЛОЖЬ
	|						ИНАЧЕ ВТ_УплачиваютсяАвансыПоОрганизации.УплачиваютсяАвансы
	|					КОНЕЦ
	|			ИНАЧЕ ВТ_УплачиваютсяАвансыПоИФНС.УплачиваютсяАвансы
	|		КОНЕЦ), ЛОЖЬ) КАК УплачиваютсяАвансы
	|ИЗ
	|	ВТ_НалоговыеОрганы КАК ВТ_НалоговыеОрганы
	|		ЛЕВОЕ СОЕДИНЕНИЕ 
	|			ВТ_УплачиваютсяАвансыПоОрганизации КАК ВТ_УплачиваютсяАвансыПоОрганизации
	|		ПО
	|			ВТ_НалоговыеОрганы.Организация = ВТ_УплачиваютсяАвансыПоОрганизации.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ 
	|			ВТ_УплачиваютсяАвансыПоИФНС КАК ВТ_УплачиваютсяАвансыПоИФНС
	|		ПО 
	|			ВТ_НалоговыеОрганы.Организация = ВТ_УплачиваютсяАвансыПоИФНС.Организация
	|			И ВТ_НалоговыеОрганы.НалоговыйОрган = ВТ_УплачиваютсяАвансыПоИФНС.РегистрацияВНалоговомОргане";
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		Пока Выборка.Следующий() Цикл
			УплачиваютсяАвансы = Выборка.УплачиваютсяАвансы;
		КонецЦикла;
		
	КонецЕсли;
		
	Возврат УплачиваютсяАвансы;
	
КонецФункции

// Возвращает результат расчета земельного налога (или аванса по налогу).
//
// Параметры:
// 	Организация - СправочникСсылка.Организации - Организация, для которой необходимо выполнить расчет.
// 	ПериодРасчета - Дата - Дата закрытия месяца.
//
// Возвращаемое значение:
// 	ТаблицаРасчетНалога - ТаблицаЗначений - Результат расчета земельного налога.
//
Функция ПолучитьРасчетПоЗемельномуНалогу(Организация, ПериодРасчета) Экспорт
	
	РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, "РегистрацияВНалоговомОргане");
	
	ГодовойРасчет = КонецКвартала(ПериодРасчета) = КонецГода(ПериодРасчета);
	
	ДатаНачалаПериодаОтчета = ?(ГодовойРасчет,	НачалоГода(ПериодРасчета),	НачалоКвартала(ПериодРасчета));
	ДатаКонцаПериодаОтчета = КонецКвартала(ПериодРасчета);
	
	ТаблицаРасчетНалога = ПустаяСправкаРасчет("РасчетЗемельногоНалога");
	
	ЭтоНеЮрЛицо = НЕ ОбщегоНазначенияБПВызовСервераПовтИсп.ЭтоЮрЛицо(Организация);
	
	Если Месяц(ПериодРасчета)%3 <> 0 ИЛИ (ЭтоНеЮрЛицо И ПериодРасчета >= '20150101') Тогда
		Возврат ТаблицаРасчетНалога;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДатаКонцаПериодаОтчета", ДатаКонцаПериодаОтчета);
	
	Период = ДатаНачалаПериодаОтчета;
	
	ТекстОбъединяющегоЗапроса = "";
	
	Пока Период <= ДатаКонцаПериодаОтчета Цикл
		
		// Выборка земельных участков и числа полных месяцев владения ими.
		// При этом если возникновение (прекращение) у налогоплательщика в течение налогового периода 
		// права собственности (постоянного (бессрочного) пользования, пожизненного наследуемого владения) 
		// на земельный участок (его долю) произошло до 15-го числа соответствующего месяца включительно, 
		// за полный месяц принимается месяц возникновения указанных прав. Если возникновение (прекращение)
		// указанных прав произошло после 15-го числа соответствующего месяца, за полный месяц принимается 
		// месяц прекращения указанных прав.
		
		Запрос.УстановитьПараметр("КонецПредыдущегоМесяца", НачалоМесяца(Период) - 1);
		Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(Период));
		Запрос.УстановитьПараметр("СерединаМесяца", Дата(Год(Период), Месяц(Период), 15));
		Запрос.УстановитьПараметр("КонецМесяца", КонецМесяца(Период));
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	РегистрацияЗемельныхУчастковСрезПоследних.ОсновноеСредство КАК ОсновноеСредство,
		|	РегистрацияЗемельныхУчастковСрезПоследних.Организация КАК Организация,
		|	РегистрацияЗемельныхУчастковСрезПоследних.Период КАК Период,
		|	NULL КАК НалоговаяЛьготаПоНалоговойБазе,
		|	1 КАК КоличествоМесяцев,
		|	0 КАК КоличествоМесяцевПримененияЛьготы,
		|	РегистрацияЗемельныхУчастковСрезПоследних.ДатаНачалаПроектирования,
		|	РегистрацияЗемельныхУчастковСрезПоследних.ДатаРегистрацииПравНаОбъектНедвижимости,
		|	РегистрацияЗемельныхУчастковСрезПоследних.ЖилищноеСтроительство
		|ПОМЕСТИТЬ ВладениеЗемельнымиУчастками
		|ИЗ
		|	РегистрСведений.РегистрацияЗемельныхУчастков.СрезПоследних(&СерединаМесяца, Организация = &Организация) КАК РегистрацияЗемельныхУчастковСрезПоследних
		|ГДЕ
		|	РегистрацияЗемельныхУчастковСрезПоследних.ВключатьВНалоговуюБазу
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	РегистрацияЗемельныхУчастков.ОсновноеСредство,
		|	РегистрацияЗемельныхУчастков.Организация,
		|	РегистрацияЗемельныхУчастков.Период,
		|	NULL,
		|	1,
		|	0,
		|	РегистрацияЗемельныхУчастков.ДатаНачалаПроектирования,
		|	РегистрацияЗемельныхУчастков.ДатаРегистрацииПравНаОбъектНедвижимости,
		|	РегистрацияЗемельныхУчастков.ЖилищноеСтроительство
		|ИЗ
		|	РегистрСведений.РегистрацияЗемельныхУчастков КАК РегистрацияЗемельныхУчастков
		|ГДЕ
		|	РегистрацияЗемельныхУчастков.Организация = &Организация
		|	И РегистрацияЗемельныхУчастков.Период МЕЖДУ &НачалоМесяца И &СерединаМесяца
		|	И РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	РегистрацияЗемельныхУчастковСрезПоследних.ОсновноеСредство,
		|	РегистрацияЗемельныхУчастковСрезПоследних.Организация,
		|	РегистрацияЗемельныхУчастковСрезПоследних.Период,
		|	РегистрацияЗемельныхУчастковСрезПоследних.НалоговаяЛьготаПоНалоговойБазе,
		|	0,
		|	1,
		|	РегистрацияЗемельныхУчастковСрезПоследних.ДатаНачалаПроектирования,
		|	РегистрацияЗемельныхУчастковСрезПоследних.ДатаРегистрацииПравНаОбъектНедвижимости,
		|	РегистрацияЗемельныхУчастковСрезПоследних.ЖилищноеСтроительство
		|ИЗ
		|	РегистрСведений.РегистрацияЗемельныхУчастков.СрезПоследних(&КонецПредыдущегоМесяца, Организация = &Организация) КАК РегистрацияЗемельныхУчастковСрезПоследних
		|ГДЕ
		|	РегистрацияЗемельныхУчастковСрезПоследних.ВключатьВНалоговуюБазу
		|	И РегистрацияЗемельныхУчастковСрезПоследних.НалоговаяЛьготаПоНалоговойБазе <> ЗНАЧЕНИЕ(Перечисление.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.НеПрименяется)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	РегистрацияЗемельныхУчастков.ОсновноеСредство,
		|	РегистрацияЗемельныхУчастков.Организация,
		|	РегистрацияЗемельныхУчастков.Период,
		|	РегистрацияЗемельныхУчастков.НалоговаяЛьготаПоНалоговойБазе,
		|	0,
		|	1,
		|	РегистрацияЗемельныхУчастков.ДатаНачалаПроектирования,
		|	РегистрацияЗемельныхУчастков.ДатаРегистрацииПравНаОбъектНедвижимости,
		|	РегистрацияЗемельныхУчастков.ЖилищноеСтроительство
		|ИЗ
		|	РегистрСведений.РегистрацияЗемельныхУчастков КАК РегистрацияЗемельныхУчастков
		|ГДЕ
		|	РегистрацияЗемельныхУчастков.Организация = &Организация
		|	И РегистрацияЗемельныхУчастков.Период МЕЖДУ &НачалоМесяца И &КонецМесяца
		|	И РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу
		|	И РегистрацияЗемельныхУчастков.НалоговаяЛьготаПоНалоговойБазе <> ЗНАЧЕНИЕ(Перечисление.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.НеПрименяется)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство,
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВладениеЗемельнымиУчастками.ОсновноеСредство КАК ОсновноеСредство,
		|	ВладениеЗемельнымиУчастками.Организация КАК Организация,
		|	МАКСИМУМ(ЕСТЬNULL(РегистрацияЗемельныхУчастков.Период, ДАТАВРЕМЯ(1, 1, 1))) КАК ДатаПоследнегоСнятияСРегистрационногоУчета
		|ПОМЕСТИТЬ ДатыПоследнегоСнятияСРегистрационногоУчета
		|ИЗ
		|	ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияЗемельныхУчастков КАК РегистрацияЗемельныхУчастков
		|		ПО ВладениеЗемельнымиУчастками.ОсновноеСредство = РегистрацияЗемельныхУчастков.ОсновноеСредство
		|			И ВладениеЗемельнымиУчастками.Организация = РегистрацияЗемельныхУчастков.Организация
		|			И (РегистрацияЗемельныхУчастков.Период < ВладениеЗемельнымиУчастками.Период)
		|			И (НЕ РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВладениеЗемельнымиУчастками.ОсновноеСредство,
		|	ВладениеЗемельнымиУчастками.Организация
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство,
		|	Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДатыПоследнегоСнятияСРегистрационногоУчета.ОсновноеСредство КАК ОсновноеСредство,
		|	МИНИМУМ(РегистрацияЗемельныхУчастков.Период) КАК ДатаРегистрацииЗемли
		|ПОМЕСТИТЬ ДатыРегистрацииЗемли
		|ИЗ
		|	ДатыПоследнегоСнятияСРегистрационногоУчета КАК ДатыПоследнегоСнятияСРегистрационногоУчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияЗемельныхУчастков КАК РегистрацияЗемельныхУчастков
		|		ПО ДатыПоследнегоСнятияСРегистрационногоУчета.ОсновноеСредство = РегистрацияЗемельныхУчастков.ОсновноеСредство
		|			И ДатыПоследнегоСнятияСРегистрационногоУчета.Организация = РегистрацияЗемельныхУчастков.Организация
		|			И (РегистрацияЗемельныхУчастков.Период > ДатыПоследнегоСнятияСРегистрационногоУчета.ДатаПоследнегоСнятияСРегистрационногоУчета)
		|			И (РегистрацияЗемельныхУчастков.ВключатьВНалоговуюБазу)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДатыПоследнегоСнятияСРегистрационногоУчета.ОсновноеСредство
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	МЕСЯЦ(&НачалоМесяца) КАК НомерМесяца,
		|	ВладениеЗемельнымиУчастками.ОсновноеСредство,
		|	ВладениеЗемельнымиУчастками.НалоговаяЛьготаПоНалоговойБазе,
		|	ВладениеЗемельнымиУчастками.КоличествоМесяцев,
		|	ВладениеЗемельнымиУчастками.КоличествоМесяцевПримененияЛьготы,
		|	ВЫБОР
		|		КОГДА ВладениеЗемельнымиУчастками.ЖилищноеСтроительство
		|			ТОГДА ВЫБОР
		|					КОГДА ДатыРегистрацииЗемли.ДатаРегистрацииЗемли >= ДАТАВРЕМЯ(2008, 1, 1)
		|						ТОГДА ВЫБОР
		|								КОГДА ВладениеЗемельнымиУчастками.ДатаРегистрацииПравНаОбъектНедвижимости = ДАТАВРЕМЯ(1, 1, 1)
		|										ИЛИ ВладениеЗемельнымиУчастками.ДатаРегистрацииПравНаОбъектНедвижимости > &НачалоМесяца
		|									ТОГДА ВЫБОР
		|											КОГДА ДОБАВИТЬКДАТЕ(ДатыРегистрацииЗемли.ДатаРегистрацииЗемли, ГОД, 3) <= &СерединаМесяца
		|												ТОГДА ""2""
		|											ИНАЧЕ ""1""
		|										КОНЕЦ
		|								ИНАЧЕ """"
		|							КОНЕЦ
		|					ИНАЧЕ ВЫБОР
		|							КОГДА ВладениеЗемельнымиУчастками.ДатаНачалаПроектирования <> ДАТАВРЕМЯ(1, 1, 1)
		|								ТОГДА ВЫБОР
		|										КОГДА ВладениеЗемельнымиУчастками.ДатаРегистрацииПравНаОбъектНедвижимости = ДАТАВРЕМЯ(1, 1, 1)
		|												ИЛИ ВладениеЗемельнымиУчастками.ДатаРегистрацииПравНаОбъектНедвижимости > &НачалоМесяца
		|											ТОГДА ВЫБОР
		|													КОГДА ДОБАВИТЬКДАТЕ(ВладениеЗемельнымиУчастками.ДатаНачалаПроектирования, ГОД, 3) <= &СерединаМесяца
		|														ТОГДА ""2""
		|													ИНАЧЕ ""1""
		|												КОНЕЦ
		|										ИНАЧЕ """"
		|									КОНЕЦ
		|							ИНАЧЕ """"
		|						КОНЕЦ
		|				КОНЕЦ
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ПериодСтроительства
		|ПОМЕСТИТЬ ВладениеЗемельнымиУчастками" + Формат(Период, "ДФ=MM") + "
		|ИЗ
		|	ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДатыРегистрацииЗемли КАК ДатыРегистрацииЗемли
		|		ПО ВладениеЗемельнымиУчастками.ОсновноеСредство = ДатыРегистрацииЗемли.ОсновноеСредство
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВладениеЗемельнымиУчастками
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДатыПоследнегоСнятияСРегистрационногоУчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДатыРегистрацииЗемли";
		Запрос.Выполнить();
		
		ТекстОбъединяющегоЗапроса = ТекстОбъединяющегоЗапроса
								  + ?(ПустаяСтрока(ТекстОбъединяющегоЗапроса), 
									  "ВЫБРАТЬ РАЗРЕШЕННЫЕ *
		                              |",
									  "ОБЪЕДИНИТЬ ВСЕ
									  |ВЫБРАТЬ *
		                              |")
								  + ?(ПустаяСтрока(ТекстОбъединяющегоЗапроса), 
									  "ПОМЕСТИТЬ ВладениеЗемельнымиУчастками
									  |",
									  "")
								  + "ИЗ ВладениеЗемельнымиУчастками" + Формат(Период, "ДФ=MM") + "
									|";
		
		Период = ДобавитьМесяц(Период, 1);
		
	КонецЦикла;
	
	Запрос.Текст = ТекстОбъединяющегоЗапроса;
	Запрос.Выполнить();
	
	СоздатьНалоговыеОрганыСУстановленнойУплатойАвансов(
		Запрос.МенеджерВременныхТаблиц, Организация, ПериодРасчета, "ЗемельныйНалог");
	
	Запрос.УстановитьПараметр("Период", КонецГода(ПериодРасчета));
	Запрос.УстановитьПараметр("НачалоГода", НачалоГода(ПериодРасчета));
	Запрос.УстановитьПараметр("ОсновнойНалоговыйОрган", РеквизитыОрганизации.РегистрацияВНалоговомОргане);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МАКСИМУМ(ВладениеЗемельнымиУчастками.НомерМесяца) КАК НомерМесяца,
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство
	|ПОМЕСТИТЬ ВТ_НомераПоследнихМесяцев
	|ИЗ
	|	ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
	|ГДЕ
	|	НЕ ВладениеЗемельнымиУчастками.НалоговаяЛьготаПоНалоговойБазе ЕСТЬ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство,
	|	ВладениеЗемельнымиУчастками.НомерМесяца,
	|	ВладениеЗемельнымиУчастками.НалоговаяЛьготаПоНалоговойБазе
	|ПОМЕСТИТЬ ВТ_ВидыЛьгот
	|ИЗ
	|	ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
	|ГДЕ
	|	НЕ ВладениеЗемельнымиУчастками.НалоговаяЛьготаПоНалоговойБазе ЕСТЬ NULL 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство КАК ОсновноеСредство,
	|	СУММА(ВладениеЗемельнымиУчастками.КоличествоМесяцевПримененияЛьготы) КАК КоличествоМесяцевПримененияЛьготы
	|ПОМЕСТИТЬ ВТ_ПрименениеЛьготы
	|ИЗ
	|	ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
	|ГДЕ
	|	НЕ ВладениеЗемельнымиУчастками.НалоговаяЛьготаПоНалоговойБазе ЕСТЬ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство,
	|	ВладениеЗемельнымиУчастками.НалоговаяЛьготаПоНалоговойБазе
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПрименениеЛьготы.ОсновноеСредство,
	|	ПрименениеЛьготы.КоличествоМесяцевПримененияЛьготы
	|ПОМЕСТИТЬ ВТ_Льготы
	|ИЗ
	|	ВТ_НомераПоследнихМесяцев КАК НомераПоследнихМесяцев
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВидыЛьгот КАК ВидыЛьгот
	|		ПО НомераПоследнихМесяцев.ОсновноеСредство = ВидыЛьгот.ОсновноеСредство
	|			И НомераПоследнихМесяцев.НомерМесяца = ВидыЛьгот.НомерМесяца
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПрименениеЛьготы КАК ПрименениеЛьготы
	|		ПО (ВидыЛьгот.ОсновноеСредство = ПрименениеЛьготы.ОсновноеСредство)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПрименениеЛьготы.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство КАК ОсновноеСредство,
	|	ВладениеЗемельнымиУчастками.ПериодСтроительства КАК ПериодСтроительства,
	|	СУММА(ВладениеЗемельнымиУчастками.КоличествоМесяцев) КАК КоличествоМесяцевВладения
	|ПОМЕСТИТЬ ВТ_ВладениеУчастками
	|ИЗ
	|	ВладениеЗемельнымиУчастками КАК ВладениеЗемельнымиУчастками
	|
	|СГРУППИРОВАТЬ ПО
	|	ВладениеЗемельнымиУчастками.ОсновноеСредство,
	|	ВладениеЗемельнымиУчастками.ПериодСтроительства
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВладениеУчастками.ОсновноеСредство,
	|	ЕСТЬNULL(ВладениеУчастками.КоличествоМесяцевВладения, 0) КАК КоличествоМесяцевВладения,
	|	РегистрацияЗемельныхУчастковСрезПоследних.КодКатегорииЗемель,
	|	РегистрацияЗемельныхУчастковСрезПоследних.КадастровыйНомер,
	|	РегистрацияЗемельныхУчастковСрезПоследних.КБК КАК КБК,
	|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.КадастроваяСтоимость, 0) КАК КадастроваяСтоимость,
	|	ВЫБОР
	|		КОГДА РегистрацияЗемельныхУчастковСрезПоследних.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|			ТОГДА РегистрацияЗемельныхУчастковСрезПоследних.НалоговыйОрган
	|		ИНАЧЕ &ОсновнойНалоговыйОрган
	|	КОНЕЦ КАК ИФНС,
	|	ВЫБОР
	|		КОГДА РегистрацияЗемельныхУчастковСрезПоследних.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.ВДругомНалоговомОргане)
	|				ИЛИ РегистрацияЗемельныхУчастковСрезПоследних.ПостановкаНаУчетВНалоговомОргане = ЗНАЧЕНИЕ(Перечисление.ПостановкаНаУчетВНалоговомОргане.СДругимКодомПоОКАТО)
	|			ТОГДА РегистрацияЗемельныхУчастковСрезПоследних.КодПоОКТМО
	|		ИНАЧЕ ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.Организация.РегистрацияВНалоговомОргане.КодПоОКТМО, """")
	|	КОНЕЦ КАК КодПоОКТМО,
	|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.ОбщаяСобственность, ЛОЖЬ) КАК ОбщаяСобственность,
	|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.ДоляВПравеОбщейСобственностиЧислитель, 0) КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.ДоляВПравеОбщейСобственностиЗнаменатель, 0) КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.ЖилищноеСтроительство, ЛОЖЬ) КАК ЖилищноеСтроительство,
	|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.НалоговаяСтавка, 0) КАК НалоговаяСтавка,
	|	РегистрацияЗемельныхУчастковСрезПоследнихПоЛьготам.НалоговаяЛьготаПоНалоговойБазе,
	|	ЕСТЬNULL(Льготы.КоличествоМесяцевПримененияЛьготы, 0) КАК КоличествоМесяцевПримененияЛьготы,
	|	РегистрацияЗемельныхУчастковСрезПоследнихПоЛьготам.КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395,
	|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследнихПоЛьготам.ДоляНеОблагаемойНалогомПлощадиЧислитель, 0) КАК ДоляНеоблагаемойПлощадиЧислитель,
	|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследнихПоЛьготам.ДоляНеОблагаемойНалогомПлощадиЗнаменатель, 0) КАК ДоляНеоблагаемойПлощадиЗнаменатель,
	|	РегистрацияЗемельныхУчастковСрезПоследних.КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391,
	|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.УменьшениеНалоговойБазыПоСтатье391, ЛОЖЬ) КАК УменьшениеНалоговойБазыПоСтатье391,
	|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.УменьшениеНалоговойБазыНаСумму, ЛОЖЬ) КАК УменьшениеНалоговойБазыНаСумму,
	|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.НеОблагаемаяНалогомСумма, 0) КАК НеОблагаемаяНалогомСумма,
	|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.СниженнаяНалоговаяСтавка, 0) КАК СниженнаяНалоговаяСтавка,
	|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.ПроцентУменьшенияСуммыНалога, 0) КАК ПроцентУменьшенияСуммыНалога,
	|	ЕСТЬNULL(РегистрацияЗемельныхУчастковСрезПоследних.СуммаУменьшенияСуммыНалога, 0) КАК СуммаУменьшенияСуммыНалога,
	|	ЕСТЬNULL(ВладениеУчастками.ПериодСтроительства, """") КАК ПериодСтроительства
	|ПОМЕСТИТЬ ВТ_РасчетНалога
	|ИЗ
	|	ВТ_ВладениеУчастками КАК ВладениеУчастками
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Льготы КАК Льготы
	|		ПО ВладениеУчастками.ОсновноеСредство = Льготы.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияЗемельныхУчастков.СрезПоследних(
	|				&Период,
	|				Организация = &Организация
	|					И ВключатьВНалоговуюБазу = ИСТИНА) КАК РегистрацияЗемельныхУчастковСрезПоследних
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацияЗемельныхУчастков.СрезПоследних(
	|					&Период,
	|					Организация = &Организация
	|						И ВключатьВНалоговуюБазу = ИСТИНА
	|						И НалоговаяЛьготаПоНалоговойБазе <> ЗНАЧЕНИЕ(Перечисление.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.НеПрименяется)) КАК РегистрацияЗемельныхУчастковСрезПоследнихПоЛьготам
	|			ПО РегистрацияЗемельныхУчастковСрезПоследних.ОсновноеСредство = РегистрацияЗемельныхУчастковСрезПоследнихПоЛьготам.ОсновноеСредство
	|		ПО ВладениеУчастками.ОсновноеСредство = РегистрацияЗемельныхУчастковСрезПоследних.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ИФНС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_РасчетНалога.ОсновноеСредство,
	|	ВТ_РасчетНалога.КоличествоМесяцевВладения,
	|	ВТ_РасчетНалога.КодКатегорииЗемель,
	|	ВТ_РасчетНалога.КадастровыйНомер,
	|	ВТ_РасчетНалога.КБК,
	|	ВТ_РасчетНалога.КадастроваяСтоимость,
	|	ВТ_РасчетНалога.ИФНС,
	|	ВТ_РасчетНалога.КодПоОКТМО,
	|	ВТ_РасчетНалога.ОбщаяСобственность,
	|	ВТ_РасчетНалога.ДоляВПравеОбщейСобственностиЧислитель,
	|	ВТ_РасчетНалога.ДоляВПравеОбщейСобственностиЗнаменатель,
	|	ВТ_РасчетНалога.ЖилищноеСтроительство,
	|	ВТ_РасчетНалога.ПериодСтроительства,
	|	ВТ_РасчетНалога.НалоговаяСтавка,
	|	ВТ_РасчетНалога.НалоговаяЛьготаПоНалоговойБазе,
	|	ВТ_РасчетНалога.КоличествоМесяцевПримененияЛьготы,
	|	ВТ_РасчетНалога.КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395,
	|	ВТ_РасчетНалога.ДоляНеоблагаемойПлощадиЧислитель,
	|	ВТ_РасчетНалога.ДоляНеоблагаемойПлощадиЗнаменатель,
	|	ВТ_РасчетНалога.КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391,
	|	ВТ_РасчетНалога.УменьшениеНалоговойБазыПоСтатье391,
	|	ВТ_РасчетНалога.УменьшениеНалоговойБазыНаСумму,
	|	ВТ_РасчетНалога.НеОблагаемаяНалогомСумма,
	|	ВТ_РасчетНалога.СниженнаяНалоговаяСтавка,
	|	ВТ_РасчетНалога.ПроцентУменьшенияСуммыНалога,
	|	ВТ_РасчетНалога.СуммаУменьшенияСуммыНалога,
	|	ВЫБОР
	|		КОГДА ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК УплачиваютсяАвансы
	|ИЗ
	|	ВТ_РасчетНалога КАК ВТ_РасчетНалога
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов КАК ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов
	|		ПО ВТ_РасчетНалога.ИФНС = ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов.НалоговыйОрган";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		РасчетЗемельногоНалога = РезультатЗапроса.Выгрузить();
		
		Если ГодовойРасчет Тогда
			
			РасчетНалога1Кв = ТаблицаРасчетНалога.СкопироватьКолонки();
			РасчетНалога2Кв = ТаблицаРасчетНалога.СкопироватьКолонки();
			РасчетНалога3Кв = ТаблицаРасчетНалога.СкопироватьКолонки();
			
			Отбор = Новый Структура("ИФНС, ОсновноеСредство, ПериодСтроительства");
			
			НайденнаяСтрока = РасчетЗемельногоНалога.Найти(Истина, "УплачиваютсяАвансы");
			Если НайденнаяСтрока <> Неопределено Тогда
				РасчетНалога1Кв = ПолучитьРасчетПоЗемельномуНалогу(Организация, Дата(Год(ПериодРасчета), 03, 31));
				РасчетНалога2Кв = ПолучитьРасчетПоЗемельномуНалогу(Организация, Дата(Год(ПериодРасчета), 06, 30));
				РасчетНалога3Кв = ПолучитьРасчетПоЗемельномуНалогу(Организация, Дата(Год(ПериодРасчета), 09, 30));
			КонецЕсли;
			
		КонецЕсли;
		
		Для Каждого СтрокаРасчета Из РасчетЗемельногоНалога Цикл
			
			Если НЕ ГодовойРасчет И НЕ СтрокаРасчета.УплачиваютсяАвансы Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = ТаблицаРасчетНалога.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРасчета);

			Если ПериодРасчета >= '20170101' Тогда
				НоваяСтрока.УменьшениеНалоговойБазыПоСтатье391 = Ложь;
			КонецЕсли;
			
			ЧислоМесяцевВОтчетномПериоде = 12;
			
			// Коэффициент Кл определяется как отношение числа полных месяцев, 
			// в течение которых отсутствует налоговая льгота, к числу календарных 
			// месяцев в налоговом периоде.
			
			Если НоваяСтрока.КоличествоМесяцевПримененияЛьготы = 0 Тогда
				КоэффициентКл = 1;
			Иначе
				ЧислоМесяцевИспользованияЛьготы = НоваяСтрока.КоличествоМесяцевВладения - НоваяСтрока.КоличествоМесяцевПримененияЛьготы;
				КоэффициентКл = Макс(ЧислоМесяцевИспользованияЛьготы / ЧислоМесяцевВОтчетномПериоде, 0);
				КоэффициентКл = Окр(КоэффициентКл, 4);
			КонецЕсли;
			
			Если НоваяСтрока.ДоляВПравеОбщейСобственностиЧислитель <> 0 И НоваяСтрока.ДоляВПравеОбщейСобственностиЗнаменатель <> 0 Тогда
				ДоляВПравеОбщейСобственности = 
					Мин(1, НоваяСтрока.ДоляВПравеОбщейСобственностиЧислитель / НоваяСтрока.ДоляВПравеОбщейСобственностиЗнаменатель);
			Иначе
				ДоляВПравеОбщейСобственности = 1;
			КонецЕсли;
			
			Если НоваяСтрока.ДоляНеоблагаемойПлощадиЧислитель <> 0 И НоваяСтрока.ДоляНеоблагаемойПлощадиЗнаменатель <> 0 Тогда
				ДоляНеоблагаемойПлощади = 
					Мин(1, НоваяСтрока.ДоляНеоблагаемойПлощадиЧислитель / НоваяСтрока.ДоляНеоблагаемойПлощадиЗнаменатель);
			Иначе
				ДоляНеоблагаемойПлощади = 1;
			КонецЕсли;
			
			// Расчет налоговой базы
			
			ОсноваДляРасчетаНалоговойБазы = НоваяСтрока.КадастроваяСтоимость * ДоляВПравеОбщейСобственности;
			Если НоваяСтрока.УменьшениеНалоговойБазыПоСтатье391 Тогда 
				СуммаУменьшенияНалоговойБазыПоСтатье391 = 10000;
			Иначе
				СуммаУменьшенияНалоговойБазыПоСтатье391 = 0;
			КонецЕсли;
			
			Если НоваяСтрока.НеОблагаемаяНалогомСумма <> 0 ИЛИ СуммаУменьшенияНалоговойБазыПоСтатье391 <> 0 Тогда
				НалоговаяБаза = 
					Макс(ОсноваДляРасчетаНалоговойБазы - (НоваяСтрока.НеОблагаемаяНалогомСумма + СуммаУменьшенияНалоговойБазыПоСтатье391), 0);
			ИначеЕсли ДоляНеоблагаемойПлощади > 0 Тогда
				НалоговаяБаза =
					Макс((ОсноваДляРасчетаНалоговойБазы - (ОсноваДляРасчетаНалоговойБазы * ДоляНеоблагаемойПлощади * (1 - КоэффициентКл))), 0);
			Иначе
				НалоговаяБаза = Макс(ОсноваДляРасчетаНалоговойБазы, 0);
			КонецЕсли;
			
			НоваяСтрока.НалоговаяБаза = Окр(НалоговаяБаза);
			
			// Коэффициент Кв определяется как отношение числа полных месяцев, 
			// в течение которых данный земельный участок находился в собственности
			// (постоянном (бессрочном) пользовании, пожизненном наследуемом владении) 
			// налогоплательщика, указанных по строке с кодом 150, к числу календарных 
			// месяцев в налоговом периоде.
			
			КоэффициентКв = Окр(НоваяСтрока.КоличествоМесяцевВладения / ЧислоМесяцевВОтчетномПериоде, 4);
			
			// Определение периода строительства
			КоэффициентПериодаСтроительства = 1;
			Если СтрокаРасчета.ЖилищноеСтроительство Тогда
				
				Если НоваяСтрока.ПериодСтроительства = "2" Тогда
					КоэффициентПериодаСтроительства = 4;
				ИначеЕсли НоваяСтрока.ПериодСтроительства = "1" Тогда
					КоэффициентПериодаСтроительства = 2;
				КонецЕсли;
				
			КонецЕсли;

			Если ПериодРасчета < '20170101' Или СтрокаРасчета.СниженнаяНалоговаяСтавка = 0 Тогда
				НалоговаяСтавка = НоваяСтрока.НалоговаяСтавка;
			Иначе
				НалоговаяСтавка = СтрокаРасчета.СниженнаяНалоговаяСтавка;
			КонецЕсли;
			
			НоваяСтрока.СуммаНалога = 
				Окр(НоваяСтрока.НалоговаяБаза * НалоговаяСтавка * КоэффициентПериодаСтроительства * КоэффициентКв / 100);
			
			// Расчет сумм налоговых льгот
			СуммаНалоговойЛьготыОсвобождениеОтНалогообложенияМестное = 0;
			СуммаНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395 = 0;
			СуммаНалоговойЛьготыВВидеУменьшенияСуммыНалога = 0;
			СуммаНалоговойЛьготыВВидеСниженияНалоговойСтавки = 0;
			
			Если НоваяСтрока.НалоговаяЛьготаПоНалоговойБазе = 
				Перечисления.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.ОсвобождениеОтНалогообложенияМестное Тогда
				
				СуммаНалоговойЛьготыОсвобождениеОтНалогообложенияМестное = Окр(НоваяСтрока.СуммаНалога * (1 - КоэффициентКл));
				
			ИначеЕсли НоваяСтрока.НалоговаяЛьготаПоНалоговойБазе =
				Перечисления.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу.ОсвобождениеОтНалогообложенияПоСтатье395 Тогда
				
				СуммаНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395 = Окр(НоваяСтрока.СуммаНалога * (1 - КоэффициентКл));
				
			КонецЕсли;
			
			Если СтрокаРасчета.ПроцентУменьшенияСуммыНалога > 0 Тогда
				СуммаНалоговойЛьготыВВидеУменьшенияСуммыНалога =
					Окр(НоваяСтрока.СуммаНалога * СтрокаРасчета.ПроцентУменьшенияСуммыНалога / 100);
			КонецЕсли;	
				
			Если СтрокаРасчета.СуммаУменьшенияСуммыНалога > 0 Тогда
				СуммаНалоговойЛьготыВВидеУменьшенияСуммыНалога = СуммаНалоговойЛьготыВВидеУменьшенияСуммыНалога
					+ Окр(СтрокаРасчета.СуммаУменьшенияСуммыНалога);
			КонецЕсли;
			
			Если ПериодРасчета < '20170101' И СтрокаРасчета.СниженнаяНалоговаяСтавка > 0 Тогда
				СуммаНалоговойЛьготыВВидеСниженияНалоговойСтавки = 
					Окр(НоваяСтрока.НалоговаяБаза * (НоваяСтрока.НалоговаяСтавка - СтрокаРасчета.СниженнаяНалоговаяСтавка) / 100);
			Иначе
				СуммаНалоговойЛьготыВВидеСниженияНалоговойСтавки = 0;
			КонецЕсли;
			
			НоваяСтрока.СуммаНалоговойЛьготыВВидеУменьшенияСуммыНалога = СуммаНалоговойЛьготыВВидеУменьшенияСуммыНалога;
			НоваяСтрока.СуммаНалоговойЛьготыВВидеСниженияНалоговойСтавки = СуммаНалоговойЛьготыВВидеСниженияНалоговойСтавки;
			
			ПараметрыЛьготы = Новый Структура(
				"ДоляНеОблагаемойНалогомПлощадиЗнаменатель, 
				|ДоляНеОблагаемойНалогомПлощадиЧислитель, 
				|КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395, 
				|КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391, 
				|НалоговаяЛьготаПоНалоговойБазе, 
				|НеОблагаемаяНалогомСумма, 
				|ПроцентУменьшенияСуммыНалога, 
				|СниженнаяНалоговаяСтавка, 
				|СуммаУменьшенияСуммыНалога, 
				|УменьшениеНалоговойБазыНаСумму, 
				|УменьшениеНалоговойБазыПоСтатье391");
				
			ЗаполнитьЗначенияСвойств(ПараметрыЛьготы, СтрокаРасчета);
			
			ПараметрыЛьготы.ДоляНеОблагаемойНалогомПлощадиЗнаменатель = СтрокаРасчета.ДоляНеоблагаемойПлощадиЗнаменатель;
			ПараметрыЛьготы.ДоляНеОблагаемойНалогомПлощадиЧислитель = СтрокаРасчета.ДоляНеоблагаемойПлощадиЧислитель;
			
			НоваяСтрока.ПредставлениеНалоговойЛьготы =
				УправлениеВнеоборотнымиАктивамиКлиентСервер.ПредставлениеНалоговойЛьготыПоЗемельномуНалогу(ПараметрыЛьготы);
			
			// Расчет суммы налога к уплате
			Если СуммаНалоговойЛьготыВВидеУменьшенияСуммыНалога > 0 Тогда
				НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалога - СуммаНалоговойЛьготыВВидеУменьшенияСуммыНалога;
			ИначеЕсли СуммаНалоговойЛьготыВВидеСниженияНалоговойСтавки > 0 Тогда
				НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалога - СуммаНалоговойЛьготыВВидеСниженияНалоговойСтавки;
			ИначеЕсли СуммаНалоговойЛьготыОсвобождениеОтНалогообложенияМестное > 0 Тогда
				НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалога - СуммаНалоговойЛьготыОсвобождениеОтНалогообложенияМестное;
			ИначеЕсли СуммаНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395 > 0 Тогда
				НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалога - СуммаНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395;
			Иначе
				НоваяСтрока.СуммаНалогаКУплате = НоваяСтрока.СуммаНалога;
			КонецЕсли;
			
			НоваяСтрока.СуммаНалогаКУплате = Макс(0, НоваяСтрока.СуммаНалогаКУплате);
			
			Если ГодовойРасчет И СтрокаРасчета.УплачиваютсяАвансы Тогда
				
				ЗаполнитьЗначенияСвойств(Отбор, НоваяСтрока);
				
				НоваяСтрока.СуммаАвансовыхПлатежей1Кв = 0;
				СтрокиРасчетаАвансовыхПлатежей = РасчетНалога1Кв.НайтиСтроки(Отбор);
				Для Каждого СтрокаРасчетаАвансовыхПлатежей Из СтрокиРасчетаАвансовыхПлатежей Цикл
					НоваяСтрока.СуммаАвансовыхПлатежей1Кв = НоваяСтрока.СуммаАвансовыхПлатежей1Кв
						+ СтрокаРасчетаАвансовыхПлатежей.СуммаНалогаКУплате;
				КонецЦикла;
				
				НоваяСтрока.СуммаАвансовыхПлатежей2Кв = 0;
				СтрокиРасчетаАвансовыхПлатежей = РасчетНалога2Кв.НайтиСтроки(Отбор);
				Для Каждого СтрокаРасчетаАвансовыхПлатежей Из СтрокиРасчетаАвансовыхПлатежей Цикл
					НоваяСтрока.СуммаАвансовыхПлатежей2Кв = НоваяСтрока.СуммаАвансовыхПлатежей2Кв
						+ СтрокаРасчетаАвансовыхПлатежей.СуммаНалогаКУплате;
				КонецЦикла;
				
				НоваяСтрока.СуммаАвансовыхПлатежей3Кв = 0;
				СтрокиРасчетаАвансовыхПлатежей = РасчетНалога3Кв.НайтиСтроки(Отбор);
				Для Каждого СтрокаРасчетаАвансовыхПлатежей Из СтрокиРасчетаАвансовыхПлатежей Цикл
					НоваяСтрока.СуммаАвансовыхПлатежей3Кв = НоваяСтрока.СуммаАвансовыхПлатежей3Кв
					+ СтрокаРасчетаАвансовыхПлатежей.СуммаНалогаКУплате;
				КонецЦикла;
				
				НоваяСтрока.СуммаНалогаКУплате = Макс(0, НоваяСтрока.СуммаНалогаКУплате - НоваяСтрока.СуммаАвансовыхПлатежей1Кв
					- НоваяСтрока.СуммаАвансовыхПлатежей2Кв - НоваяСтрока.СуммаАвансовыхПлатежей3Кв);
				
			КонецЕсли;
			
			Если НЕ ГодовойРасчет Тогда
				НоваяСтрока["СуммаАвансовыхПлатежей" + Формат(ПериодРасчета, "ДФ='q''Кв'''")] = НоваяСтрока.СуммаНалогаКУплате;
			КонецЕсли;
				
		КонецЦикла;
		
	КонецЕсли;
	
	ТаблицаРасчетНалога.ЗаполнитьЗначения(ДатаКонцаПериодаОтчета, "ПериодРасчета");
	ТаблицаРасчетНалога.ЗаполнитьЗначения(Организация, "Организация");

	Возврат ТаблицаРасчетНалога;
	
КонецФункции

#КонецОбласти

// Процедура формирует временную таблицу в менеджере временных таблиц,
// содержащую сведения о регистрации в налоговых органах, в которые 
// уплачивается авансовые платежи организации по указанному виду налога.
//
// Параметры:
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджер, в котором создать таблицы.
//	Организация - СправочникСсылка.Организации - организация, для которой определяются авансовые платежи.
//	Период - Дата - Дата, по состоянию на которую определяются сведения.
//	Налог - Строка - Может содержать одно из значений:
//			- "ЗемельныйНалог";
//			- "ТранспортныйНалог";
//			- "НалогНаИмущество".
// 
Процедура СоздатьНалоговыеОрганыСУстановленнойУплатойАвансов(МенеджерВременныхТаблиц, Организация, Период, Налог) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ВидНалога = Перечисления.ВидыИмущественныхНалогов.НалогНаИмущество;
	
	Если Налог = "ЗемельныйНалог" Тогда
		ВидНалога = Перечисления.ВидыИмущественныхНалогов.ЗемельныйНалог;
	ИначеЕсли Налог = "ТранспортныйНалог" Тогда
		ВидНалога = Перечисления.ВидыИмущественныхНалогов.ТранспортныйНалог;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Период",      Период);
	Запрос.УстановитьПараметр("Налог",       ВидНалога);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РегистрацииВНалоговомОргане.Ссылка КАК НалоговыйОрган
	|ПОМЕСТИТЬ ВТ_ВсеРегистрацииВНалоговомОргане
	|ИЗ
	|	Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
	|ГДЕ
	|	РегистрацииВНалоговомОргане.Владелец = &Организация
	|	И НЕ РегистрацииВНалоговомОргане.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПорядокУплатыНалоговНаМестахСрезПоследних.РегистрацияВНалоговомОргане КАК НалоговыйОрган
	|ПОМЕСТИТЬ ВТ_ПорядокУплатыНалоговНаМестах
	|ИЗ
	|	РегистрСведений.ПорядокУплатыНалоговНаМестах.СрезПоследних(
	|			&Период,
	|			Организация = &Организация
	|				И Налог = &Налог
	|				И УплачиваютсяАвансы) КАК ПорядокУплатыНалоговНаМестахСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР
	|		КОГДА ВТ_ПорядокУплатыНалоговНаМестах.НалоговыйОрган = ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка)
	|			ТОГДА ВТ_ВсеРегистрацииВНалоговомОргане.НалоговыйОрган
	|		ИНАЧЕ ВТ_ПорядокУплатыНалоговНаМестах.НалоговыйОрган
	|	КОНЕЦ КАК НалоговыйОрган
	|ПОМЕСТИТЬ ВТ_НалоговыеОрганыСУстановленнойУплатойАвансов
	|ИЗ
	|	ВТ_ПорядокУплатыНалоговНаМестах КАК ВТ_ПорядокУплатыНалоговНаМестах
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ВсеРегистрацииВНалоговомОргане КАК ВТ_ВсеРегистрацииВНалоговомОргане
	|		ПО (ВТ_ПорядокУплатыНалоговНаМестах.НалоговыйОрган = ЗНАЧЕНИЕ(Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НалоговыйОрган";
	
	Запрос.Выполнить();
	
КонецПроцедуры

// Формирует таблицы значений, по структуре соответствующие регистрам сведений 	
// РасчетТранспортногоНалога и РасчетЗемельногоНалога БП.
//
// Параметры:
//	ИмяРегистраСведений - Строка - Имя регистра.
//
// Возвращаемое значение:
//	ТаблицаЗначений - Таблица с колонками, совпадающими с полями регистра.
//
Функция ПустаяСправкаРасчет(ИмяРегистраСведений) Экспорт

	СправкаРасчет = РегистрыСведений[ИмяРегистраСведений].СоздатьНаборЗаписей().ВыгрузитьКолонки();
	
	СправкаРасчет.Колонки.Удалить("Регистратор");
	СправкаРасчет.Колонки.Удалить("Активность");
	СправкаРасчет.Колонки.Удалить("НомерСтроки");
	
	Возврат СправкаРасчет;
	
КонецФункции

// Возвращает таблицы выполнения регламентных операций расчета имущественных налогов.
//
// Параметры:
// 	ТаблицаРеквизиты 
// 	Отказ - Булево - Флаг отказа проведения.
// 
// Возвращаемое значение:
// 	 СтруктураТаблиц - Структура - Таблицы расчета налога:
// 							Ключи:
// 								* ТаблицаСправкиРасчета - ТаблицаЗначений - Справка-расчет налога.
// 								* ТаблицаРасходыПоНалогу - ТаблицаЗначений - Отражение расходов по налогу.
//
Функция ПодготовитьТаблицыРасчетНалога(ТаблицаРеквизиты, Отказ) Экспорт
	
	Возврат РасчетИмущественныхНалоговПереопределяемый.ПодготовитьТаблицыРасчетНалога(ТаблицаРеквизиты, Отказ);
	
КонецФункции

// Формирует движения регистров по данным расчета налога.
//
Процедура СформироватьДвиженияРасчетНалога(ТаблицыНачислениеНалога, ТаблицаРеквизитов, Движения, Отказ) Экспорт

	РасчетИмущественныхНалоговПереопределяемый.СформироватьДвиженияРасчетНалога(ТаблицыНачислениеНалога, ТаблицаРеквизитов, Движения, Отказ);
	
КонецПроцедуры

// Добавляем в состав параметров таблицу значений со структурой колонок для справки - расчета.
// 
// Параметры:
//	Параметры - Структура - Содержит параметры формирования движений для справки-расчета.
//	Реквизиты - СтрокаТаблицыЗначений - Содержит поля:
//		* ВидНалога - ПеречислениеСсылка.ВидыИмущественныхНалогов - Вид налога
//	ТаблицаСправкиРасчета - ТаблицаЗначений - По структуре соответствует регистру сведений для соответствующего налога
//			(РасчетТранспортногоНалога и др.).
//
Процедура ДополнитьПараметрыДвиженийСправкаРасчет(Параметры, Реквизиты, ТаблицаСправкиРасчета) Экспорт
	
	СписокОбязательныхКолонок = "";
	
	// Подготовка таблицы Параметры.СправкаРасчет
	Если Реквизиты.ВидНалога = Перечисления.ВидыИмущественныхНалогов.НалогНаИмущество Тогда
		
		СписокОбязательныхКолонок = СписокОбязательныхКолонок
		+ "ПериодРасчета,"										// <Дата> - период расчета (конец квартала)
		+ "Организация,"										// <СправочникСсылка.Организации> - организация
		+ "ВидНалоговойБазы,"									// <ПеречислениеСсылка.НалоговаяБазаПоНалогуНаИмущество>
		+ "ИФНС,"												// <СправочникСсылка.РегистрацииВНалоговомОргане>
		+ "КодПоОКТМО,"											// <Строка, 11> 
		+ "КодВидаИмущества,"									// <Строка, 2>
		+ "ОсновноеСредство,"									// <СправочникСсылка.ОсновныеСредства>
		+ "ДоляВПравеОбщейСобственностиЧислитель,"				// <Число, 10, 0>
		+ "ДоляВПравеОбщейСобственностиЗнаменатель,"			// <Число, 10, 0>
		+ "ДоляПлощадиЧислитель,"								// <Число, 10, 0>
		+ "ДоляПлощадиЗнаменатель,"								// <Число, 10, 0>
		+ "ДоляСтоимостиЧислитель,"								// <Число, 10, 0>
		+ "ДоляСтоимостиЗнаменатель,"							// <Число, 10, 0>
		+ "КБК,"												// <Строка, 20>
		+ "КадастровыйНомер,"									// <Строка, 100>
		+ "КадастровыйНомерПомещения,"							// <Строка, 100>
		+ "УсловныйНомер,"										// <Строка, 100>
		+ "КадастроваяСтоимость,"								// <Число, 15, 0>
		+ "НеоблагаемаяКадастроваяСтоимость,"					// <Число, 15, 0>
		+ "ОстаточнаяСтоимостьНа0101,"							// <Число, 15, 0>
		+ "ОстаточнаяСтоимостьНа0102,"							// <Число, 15, 0>
		+ "ОстаточнаяСтоимостьНа0103,"							// <Число, 15, 0>
		+ "ОстаточнаяСтоимостьНа0104,"							// <Число, 15, 0>
		+ "ОстаточнаяСтоимостьНа0105,"							// <Число, 15, 0>
		+ "ОстаточнаяСтоимостьНа0106,"							// <Число, 15, 0>
		+ "ОстаточнаяСтоимостьНа0107,"							// <Число, 15, 0>
		+ "ОстаточнаяСтоимостьНа0108,"							// <Число, 15, 0>
		+ "ОстаточнаяСтоимостьНа0109,"							// <Число, 15, 0>
		+ "ОстаточнаяСтоимостьНа0110,"							// <Число, 15, 0>
		+ "ОстаточнаяСтоимостьНа0111,"							// <Число, 15, 0>
		+ "ОстаточнаяСтоимостьНа0112,"							// <Число, 15, 0>
		+ "ОстаточнаяСтоимостьНа3112,"							// <Число, 15, 0>
		+ "ОстаточнаяСтоимостьНедвижимости,"					// <Число, 15, 0>
		+ "ОстаточнаяСтоимостьОсновныхСредств,"					// <Число, 15, 0>
		+ "СредняяСтоимость,"									// <Число, 15, 0>
		+ "НеоблагаемаяСредняяСтоимость,"						// <Число, 15, 0>
		+ "КодНалоговойЛьготыОсвобождениеОтНалогообложения,"	// <Строка, 7>
		+ "НалоговаяБаза,"										// <Число, 15, 0>
		+ "ПонижениеНалоговойСтавки,"							// <Булево>
		+ "НалоговаяСтавка,"									// <Число, 3, 2>
		+ "СуммаНалога,"										// <Число, 15, 0>
		+ "СуммаУменьшенияСуммыНалога,"							// <Число, 15, 0>
		+ "СуммаНалогаКУплате,"									// <Число, 15, 0>
		+ "СуммаАвансовыхПлатежей,"								// <Число, 15, 0>
		+ "КоличествоМесяцевВладения";							// <Число, 2, 0>
		
	ИначеЕсли Реквизиты.ВидНалога = Перечисления.ВидыИмущественныхНалогов.ТранспортныйНалог Тогда
		
		СписокОбязательныхКолонок = СписокОбязательныхКолонок
		+ "ПериодРасчета,"							// <Дата> - период расчета (конец квартала)
		+ "ДатаРегистрационныхДанных,"				// <Дата>
		+ "Организация,"							// <СправочникСсылка.Организации> - организация
		+ "ИФНС,"									// <СправочникСсылка.РегистрацииВНалоговомОргане>
		+ "КодПоОКТМО,"								// <Строка, 11> 
		+ "ОсновноеСредство,"						// <СправочникСсылка.ОсновныеСредства>
		+ "КодВидаТранспортногоСредства,"			// <Строка, 100>
		+ "ИдентификационныйНомер,"					// <Строка, 50>	
		+ "Марка,"									// <Строка, 25>
		+ "РегистрационныйЗнак,"					// <Строка, 25>
		+ "НалоговаяБаза,"							// <Число, 15, 2>
		+ "ЕдиницаИзмерения,"						// <Строка, 5>
		+ "КоличествоМесяцевВладения,"				// <Число, 2, 0>
		+ "НалоговаяСтавка,"						// <Число, 3, 2>
		+ "СуммаНалога,"							// <Число, 15, 0>
		+ "КоличествоМесяцевЛьготы,"				// <Число, 2, 0>
		+ "КодНалоговойЛьготы,"						// <Строка, 6>
		+ "РегиональныйКодЛьготы,"					// <Строка, 12>
		+ "ПроцентУменьшения,"						// <Число, 4, 2>
		+ "ЛьготнаяСтавка,"							// <Число, 8, 2>
		+ "СуммаНалоговойЛьготы,"					// <Число, 15, 0>
		+ "СуммаНалогаКУплате,"						// <Число, 15, 0>
		+ "ЭкологическийКласс,"						// <Строка, 1>
		+ "ПовышающийКоэффициент,"					// <Число, 3, 1>
		+ "КоличествоЛетПрошедшихСГодаВыпускаТС,"	// <Число, 3, 0>
		+ "ДоляВПравеОбщейСобственностиЧислитель,"	// <Число, 10, 0>
		+ "ДоляВПравеОбщейСобственностиЗнаменатель,"// <Число, 10, 0>
		+ "СуммаАвансовыхПлатежей1Кв,"				// <Число, 15, 0>
		+ "СуммаАвансовыхПлатежей2Кв,"				// <Число, 15, 0>
		+ "СуммаАвансовыхПлатежей3Кв,"				// <Число, 15, 0>
		+ "ДатаРегистрации,"						// <Дата>
		+ "ДатаПрекращенияРегистрации";				// <Дата>
			
	ИначеЕсли Реквизиты.ВидНалога = Перечисления.ВидыИмущественныхНалогов.ЗемельныйНалог Тогда
		
		СписокОбязательныхКолонок = СписокОбязательныхКолонок
		+ "ПериодРасчета,"												// <Дата> - период расчета (конец квартала)
		+ "Организация,"												// <СправочникСсылка.Организации> - организация
		+ "ИФНС,"														// <СправочникСсылка.РегистрацииВНалоговомОргане>
		+ "ОсновноеСредство,"											// <СправочникСсылка.ОсновныеСредства>
		+ "КадастровыйНомер,"											// <Строка, 100>
		+ "КБК,"														// <Строка, 20>	
		+ "КодПоОКТМО,"													// <Строка, 11> 
		+ "КодКатегорииЗемель,"											// <Строка, 12>
		+ "ПериодСтроительства,"										// <Строка, 1>	
		+ "КадастроваяСтоимость,"										// <Число, 15, 0>	
		+ "ДоляВПравеОбщейСобственностиЧислитель,"						// <Число, 10, 0>	
		+ "ДоляВПравеОбщейСобственностиЗнаменатель,"					// <Число, 10, 0>
		+ "УменьшениеНалоговойБазыНаСумму,"								// <Булево>
		+ "НеОблагаемаяНалогомСумма,"									// <Число, 15, 0>
		+ "УменьшениеНалоговойБазыПоСтатье391,"							// <Булево>
		+ "КодНалоговойЛьготыУменьшениеНалоговойБазыПоСтатье391," 		// <Строка, 7>
		+ "НалоговаяЛьготаПоНалоговойБазе," 							// <ПеречислениеСсылка.ВидНалоговойЛьготыПоНалоговойБазеПоЗемельномуНалогу>
		+ "ДоляНеоблагаемойПлощадиЧислитель,"							// <Число, 10, 0>
		+ "ДоляНеоблагаемойПлощадиЗнаменатель,"							// <Число, 10, 0>
		+ "НалоговаяБаза,"												// <Число, 15, 0>
		+ "КоличествоМесяцевВладения,"									// <Число, 2, 0>
		+ "КоличествоМесяцевПримененияЛьготы,"							// <Число, 2, 0>	
		+ "НалоговаяСтавка,"											// <Число, 3, 2>
		+ "СуммаНалога,"												// <Число, 15, 0>
		+ "КодНалоговойЛьготыОсвобождениеОтНалогообложенияПоСтатье395," // <Строка, 7>	
		+ "СуммаНалоговойЛьготыВВидеУменьшенияСуммыНалога,"				// <Число, 15, 0>
		+ "СуммаНалоговойЛьготыВВидеСниженияНалоговойСтавки,"			// <Число, 15, 0>
		+ "ПредставлениеНалоговойЛьготы," 								// <Строка, 250>
		+ "СниженнаяНалоговаяСтавка," 									// <Число, 5, 4>
		+ "ПроцентУменьшенияСуммыНалога," 								// <Число, 7, 4>
		+ "СуммаНалогаКУплате,"											// <Число, 15, 0>
		+ "СуммаАвансовыхПлатежей1Кв,"									// <Число, 15, 0>
		+ "СуммаАвансовыхПлатежей2Кв,"									// <Число, 15, 0>
		+ "СуммаАвансовыхПлатежей3Кв";									// <Число, 15, 0>
	
	КонецЕсли;	
		
	Параметры.Вставить("СправкаРасчет", ОбщегоНазначенияБПВызовСервера.ПолучитьТаблицуПараметровПроведения(
		ТаблицаСправкиРасчета, СписокОбязательныхКолонок));
	
КонецПроцедуры

// Формирует таблицы значений, по структуре соответствующие регистрам сведений 	
// РасчетИмущественногоНалога, РасчетТранспортногоНалога, РасчетЗемельногоНалога БП.
//
// Параметры:
//	Параметры - Структура - Содержит параметры формирования движений для справки-расчета.
//
// Возвращаемое значение:
//	ТаблицаЗначений - Таблица с колонками, совпадающими с полями регистра.
//
Функция СправкаРасчет(Реквизиты) Экспорт
	
	Если Реквизиты.ВидНалога = Перечисления.ВидыИмущественныхНалогов.НалогНаИмущество Тогда
		
		РасчетПоНалогу = ПолучитьРасчетПоНалогуНаИмущество(Реквизиты.Организация, Реквизиты.Период);
		
	ИначеЕсли Реквизиты.ВидНалога = Перечисления.ВидыИмущественныхНалогов.ТранспортныйНалог Тогда
		
		РасчетПоНалогу = ПолучитьРасчетПоТранспортномуНалогу(Реквизиты.Организация, Реквизиты.Период);
		
	ИначеЕсли Реквизиты.ВидНалога = Перечисления.ВидыИмущественныхНалогов.ЗемельныйНалог Тогда
		
		РасчетПоНалогу = ПолучитьРасчетПоЗемельномуНалогу(Реквизиты.Организация, Реквизиты.Период);
		
	КонецЕсли;
	
	СправкаРасчет = ПустаяСправкаРасчет(Реквизиты.ИмяРегистраРасчетНалогов);
	
	Для Каждого СтрокаТаблицы Из РасчетПоНалогу Цикл
		Запись = СправкаРасчет.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицы);
	КонецЦикла;
	
	Возврат СправкаРасчет;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьАвансовыйРасчетПоТранспортномуНалогу(Организация, ПериодРасчета)
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПериодРасчета", ПериодРасчета);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасчетТранспортногоНалога.ПериодРасчета КАК ПериодРасчета,
	|	РасчетТранспортногоНалога.Организация КАК Организация,
	|	РасчетТранспортногоНалога.ИФНС КАК ИФНС,
	|	РасчетТранспортногоНалога.КодПоОКТМО КАК КодПоОКТМО,
	|	РасчетТранспортногоНалога.ОсновноеСредство КАК ОсновноеСредство,
	|	РасчетТранспортногоНалога.ДатаРегистрационныхДанных КАК ДатаРегистрационныхДанных,
	|	РасчетТранспортногоНалога.КодВидаТранспортногоСредства КАК КодВидаТранспортногоСредства,
	|	РасчетТранспортногоНалога.ИдентификационныйНомер КАК ИдентификационныйНомер,
	|	РасчетТранспортногоНалога.Марка КАК Марка,
	|	РасчетТранспортногоНалога.РегистрационныйЗнак КАК РегистрационныйЗнак,
	|	РасчетТранспортногоНалога.НалоговаяБаза КАК НалоговаяБаза,
	|	РасчетТранспортногоНалога.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	РасчетТранспортногоНалога.КоличествоМесяцевВладения КАК КоличествоМесяцевВладения,
	|	РасчетТранспортногоНалога.НалоговаяСтавка КАК НалоговаяСтавка,
	|	РасчетТранспортногоНалога.СуммаНалога КАК СуммаНалога,
	|	РасчетТранспортногоНалога.КоличествоМесяцевЛьготы КАК КоличествоМесяцевЛьготы,
	|	РасчетТранспортногоНалога.КодНалоговойЛьготы КАК КодНалоговойЛьготы,
	|	РасчетТранспортногоНалога.РегиональныйКодЛьготы КАК РегиональныйКодЛьготы,
	|	РасчетТранспортногоНалога.ПроцентУменьшения КАК ПроцентУменьшения,
	|	РасчетТранспортногоНалога.ЛьготнаяСтавка КАК ЛьготнаяСтавка,
	|	РасчетТранспортногоНалога.СуммаНалоговойЛьготы КАК СуммаНалоговойЛьготы,
	|	РасчетТранспортногоНалога.СуммаНалогаКУплате КАК СуммаНалогаКУплате,
	|	РасчетТранспортногоНалога.ЭкологическийКласс КАК ЭкологическийКласс,
	|	РасчетТранспортногоНалога.СуммаАвансовыхПлатежей1Кв КАК СуммаАвансовыхПлатежей1Кв,
	|	РасчетТранспортногоНалога.СуммаАвансовыхПлатежей2Кв КАК СуммаАвансовыхПлатежей2Кв,
	|	РасчетТранспортногоНалога.СуммаАвансовыхПлатежей3Кв КАК СуммаАвансовыхПлатежей3Кв,
	|	РасчетТранспортногоНалога.ПовышающийКоэффициент КАК ПовышающийКоэффициент,
	|	РасчетТранспортногоНалога.КоличествоЛетПрошедшихСГодаВыпускаТС КАК КоличествоЛетПрошедшихСГодаВыпускаТС,
	|	РасчетТранспортногоНалога.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	РасчетТранспортногоНалога.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	РасчетТранспортногоНалога.ДатаРегистрации КАК ДатаРегистрации,
	|	РасчетТранспортногоНалога.ДатаПрекращенияРегистрации КАК ДатаПрекращенияРегистрации
	|ИЗ
	|	РегистрСведений.РасчетТранспортногоНалога КАК РасчетТранспортногоНалога
	|ГДЕ
	|	РасчетТранспортногоНалога.Активность = ИСТИНА
	|	И РасчетТранспортногоНалога.Организация = &Организация
	|	И РасчетТранспортногоНалога.ПериодРасчета = &ПериодРасчета";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти
