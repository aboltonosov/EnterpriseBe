
#Область СлужебныеПроцедурыИФункции

Процедура ПередДобавлениемКомандОтчетов(ИмяФормы, КомандыОтчетов, СтандартнаяОбработка) Экспорт
	
	МассивПолей      = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИмяФормы, ".");
	ПолноеИмя        = МассивПолей[0] + "." + МассивПолей[1];
	ОбъектНазначения = Справочники.ИдентификаторыОбъектовМетаданных.НайтиПоРеквизиту("ПолноеИмя", ПолноеИмя);
	
	ПараметрыОпцииПоИмениФормы = Новый Структура;
	ПараметрыОпцииПоИмениФормы.Вставить("ДополнительныеОтчетыИОбработкиОбъектНазначения", ОбъектНазначения);
	ПараметрыОпцииПоИмениФормы.Вставить("ДополнительныеОтчетыИОбработкиТипФормы",         МассивПолей[3]);
	
	ПараметрыОпцииПоТипуФормы = Новый Структура;
	ПараметрыОпцииПоТипуФормы.Вставить("ДополнительныеОтчетыИОбработкиОбъектНазначения", ОбъектНазначения);
	ПараметрыОпцииПоТипуФормы.Вставить("ДополнительныеОтчетыИОбработкиТипФормы", 
		?(СтрНайти(МассивПолей[3], "ФормаСписка") <> 0, ДополнительныеОтчетыИОбработкиКлиентСервер.ТипФормыСписка(),
		                                                ДополнительныеОтчетыИОбработкиКлиентСервер.ТипФормыОбъекта()));
	
	Если     ПолучитьФункциональнуюОпцию("ИспользоватьДополнительныеОтчеты", ПараметрыОпцииПоИмениФормы)
	     ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьДополнительныеОтчеты", ПараметрыОпцииПоТипуФормы) Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Идентификатор = Метаданные.ОбщиеФормы.ДополнительныеОтчетыИОбработки.ПолноеИмя();
		КомандаОтчет.Обработчик = "МенюОтчетыУТКлиент.ОтчетыОбъектов";
		
		КомандаОтчет.Представление = НСтр("ru = 'Дополнительные отчеты'");
		КомандаОтчет.Порядок       = 100;
		КомандаОтчет.РежимИспользованияПараметра = РежимИспользованияПараметраКоманды.Множественный;
		КомандаОтчет.МестоРазмещенияКоманды = "ПодменюОтчетыСмТакже";
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 
