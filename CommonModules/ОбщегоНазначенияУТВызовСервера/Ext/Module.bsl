
#Область ПрограммныйИнтерфейс

#Область ПроцедурыИФункцииУстановкиСтатусовДляМассиваДокументов

// Устанавливает статус для списка документов
//
// Параметры:
// 		МассивДокументов - Массив - Массив документов
// 		НовыйСтатус - Строка - Имя нового статуса для документов
// 		ДополнительныеПараметры - Структура - Структура дополнительных параметров
//
// Возвращаемое значение:
// 		Число - Количество документов у которых был изменен статус
//
// ВАЖНО. При использования процедуры для каждого типа документа из массива должны быть объявлены функции:
// В модуле менеджера документа:
// 		Функция СформироватьЗапросПроверкиПриСменеСтатуса(МассивДокументов, НовыйСтатус, ДополнительныеПараметры) Экспорт
// 		Функция ПроверкаПередСменойСтатуса(ВыборкаПроверки, НовыйСтатус, ДополнительныеПараметры) Экспорт
// В модуле объекта документа:
// 		Функция УстановитьСтатус(НовыйСтатус, ДополнительныеПараметры) Экспорт
//
Функция УстановитьСтатусДокументов(Знач МассивДокументов, Знач НовыйСтатус, Знач ДополнительныеПараметры=Неопределено) Экспорт
	
	// Получение шаблонов сообщений стандартных ошибок
	ШаблонОшибкиСтатусСовпадает   = НСтр("ru='Документу %Документ% уже присвоен статус ""%Статус%""'");
	ШаблонОшибкиНеПроведен        = НСтр("ru='Документ %Документ% не проведен. Невозможно изменить статус'");
	ШаблонОшибкиПомеченНаУдаление = НСтр("ru='Документ %Документ% помечен на удаление. Невозможно изменить статус'");
	ШаблонОшибкиЗаблокировать     = НСтр("ru='Не удалось заблокировать %Документ%. %ОписаниеОшибки%'");
	ШаблонОшибкиЗаписать          = НСтр("ru='Не удалось записать %Документ%. %ОписаниеОшибки%'");
	
	// Получение исключаемых типов
	МассивИсключаемыхТипов = Новый Массив;
	Если ДополнительныеПараметры <> Неопределено 
		И ДополнительныеПараметры.Свойство("ИсключаемыеТипы") Тогда
		
		МассивИсключаемыхТипов = ДополнительныеПараметры.ИсключаемыеТипы;
		
	КонецЕсли;
	
	// Получение соответствие типов документов из массива документов разных типов
	СоответствиеТипов = ОбщегоНазначенияУТ.РазложитьМассивСсылокПоТипам(МассивДокументов);
	
	КоличествоОбработанных = 0;
	Для Каждого СоставДокументов Из СоответствиеТипов Цикл
		
		// Пропуск документов исключаемого типа
		Если МассивИсключаемыхТипов.Найти(СоставДокументов.Ключ) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		// Получение менеджера документов данного типа
		МенеджерДокументов = Документы[Метаданные.НайтиПоТипу(СоставДокументов.Ключ).Имя];
		
		// Получение массива ссылок документов данного типа
		МассивСсылок = СоставДокументов.Значение;
		
		// Формирование запроса
		Запрос = МенеджерДокументов.СформироватьЗапросПроверкиПриСменеСтатуса(МассивСсылок, НовыйСтатус, ДополнительныеПараметры);
		УстановитьПривилегированныйРежим(Истина);
		Результат = Запрос.Выполнить();
		УстановитьПривилегированныйРежим(Ложь);
		Если Результат.Пустой() Тогда
			Возврат 0;
		КонецЕсли;
		
		// Цикл обхода выборки
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			// Универсальные проверки
			Если Выборка.ПометкаУдаления Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрЗаменить(ШаблонОшибкиПомеченНаУдаление, "%Документ%", Выборка.Представление), Выборка.Ссылка);
				Продолжить;
			КонецЕсли;

			Если Не Выборка.Проведен Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрЗаменить(ШаблонОшибкиНеПроведен, "%Документ%", Выборка.Представление), Выборка.Ссылка);
				Продолжить;
			КонецЕсли;

			Если Выборка.СтатусСовпадает Тогда

				ТекстОшибки = СтрЗаменить(ШаблонОшибкиСтатусСовпадает, "%Документ%", Выборка.Представление);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%Статус%", Выборка.ПредставлениеНовогоСтатуса);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Выборка.Ссылка);
				Продолжить;

			КонецЕсли;
			
			// Проверки уникальные для каждого из типов документов
			Если Не МенеджерДокументов.ПроверкаПередСменойСтатуса(Выборка, НовыйСтатус, ДополнительныеПараметры) Тогда
				Продолжить;
			КонецЕсли;
			
			// Захват объекта для редактирования
			Попытка
				ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
			Исключение
				ТекстОшибки = СтрЗаменить(ШаблонОшибкиЗаблокировать, "%Документ%", Выборка.Представление);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Выборка.Ссылка);
				Продолжить;
			КонецПопытки;
			
			// Получение объекта документа
			Объект = Выборка.Ссылка.ПолучитьОбъект();
			
			// Установка статуса документа
			Если Не Объект.УстановитьСтатус(НовыйСтатус, ДополнительныеПараметры) Тогда
				Продолжить;
			КонецЕсли;
			
			// Запись документа
			Попытка
				Объект.Записать(?(Выборка.ЗаписьПроведением, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись));
				КоличествоОбработанных = КоличествоОбработанных + 1;
			Исключение
				ТекстОшибки = СтрЗаменить(ШаблонОшибкиЗаписать, "%Документ%", Выборка.Представление);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Выборка.Ссылка);
			КонецПопытки
			
		КонецЦикла; // выборки документов данного типа
		
	КонецЦикла; // обхода соответствия типов
	
	Возврат КоличествоОбработанных;
	
КонецФункции // УстановитьСтатусДокументов()

#КонецОбласти

// Возвращает структуру, содержащую массив ссылок на документы, подлежащие пометке к удалению
// или снятию пометки с удаления, а также признак пометки на удаление документов.
//
// Параметры:
// 		ВыделенныеСтроки - Массив - массив документов динамического списка,
// 		ПутьКСсылке - Строка - имя реквизита ссылки документа.
//
// Возвращаемое значение:
// 		Структура - содержит массив ссылок на документы и признак пометки на удаление документов.
//
Функция СсылкиОбъектовПомеченныхНаУдаление(ВыделенныеСтроки, ПутьКСсылке = "") Экспорт
	
	Если Не ПустаяСтрока(ПутьКСсылке) Тогда
		МассивСсылок = Новый Массив;
		Для каждого Строка Из ВыделенныеСтроки Цикл
			МассивСсылок.Добавить(Строка[ПутьКСсылке]);
		КонецЦикла;
	Иначе
		МассивСсылок = ВыделенныеСтроки;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("МассивСсылок", МассивСсылок);
	Результат.Вставить("ЕстьСтрокиПомеченныеНаУдаление", Ложь);
	
	ПометкиНаУдаление = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивСсылок, "ПометкаУдаления");
	
	Для Каждого КлючЗначение Из ПометкиНаУдаление Цикл
		Если КлючЗначение.Значение.ПометкаУдаления Тогда
			Результат.ЕстьСтрокиПомеченныеНаУдаление = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Осуществляет пометку к удалению или снятие пометки с удаления у документов.
//
// Параметры:
// 		МассивСсылок - Массив - массив ссылок на документы,
// 		ПометитьНаУдаление - Булево, Истина - установить пометку на удаление документа.
//
Процедура УстановитьПометкуУдаленияЗавершениеСервер(МассивСсылок, ПометитьНаУдаление) Экспорт
	
	Для Каждого Ссылка Из МассивСсылок Цикл
		
		ДокументОбъект = Ссылка.ПолучитьОбъект();
		
		// Записать только те объекты, значение пометки удаления которых изменяется
		Если ПометитьНаУдаление
			И Не ДокументОбъект.ПометкаУдаления Тогда
			
			ДокументОбъект.ПометкаУдаления = Истина;
			
			Если ДокументОбъект.Проведен Тогда
				ДокументОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
			Иначе
				ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
			КонецЕсли;
			
		ИначеЕсли Не ПометитьНаУдаление
			И ДокументОбъект.ПометкаУдаления Тогда
			
			ДокументОбъект.ПометкаУдаления = Ложь;
			ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает массив ссылок документов динамического списка.
//
// Параметры:
// 		ВыделенныеСтроки - Массив - массив документов динамического списка.
//
// Возвращаемое значение:
// 		Структура - содержит массив ссылок на документы и признак пометки на удаление документов.
//
Функция СсылкиДокументовДинамическогоСписка(ВыделенныеСтроки) Экспорт
	
	МассивСсылок = Новый Массив;
	
	Для Каждого Строка Из ВыделенныеСтроки Цикл
		МассивСсылок.Добавить(Строка.Ссылка);
	КонецЦикла;
	
	Возврат МассивСсылок;
	
КонецФункции

// Возвращает массив ссылок документов, проведение которых отменено.
//
// Параметры:
// 		МассивСсылок - Массив - массив ссылок документов.
//
// Возвращаемое значение:
// 		Массив - массив ссылок на документы, проведение которых отменено.
//
Функция СсылкиРаспроведенныхДокументов(МассивСсылок) Экспорт
	
	РаспроведенныеДокументы = Новый Массив;
	НепроведенныеДокументы = ОбщегоНазначения.ПроверитьПроведенностьДокументов(МассивСсылок);
	РаспроводимыеДокументы = ОбщегоНазначенияКлиентСервер.СократитьМассив(МассивСсылок, НепроведенныеДокументы);
	
	Для Каждого Ссылка Из РаспроводимыеДокументы Цикл
		ДокументОбъект = Ссылка.ПолучитьОбъект();
		ДокументОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
	КонецЦикла;
	
	Возврат РаспроводимыеДокументы;
	
КонецФункции

// Возвращает банковский счет организации, если он один в ИБ.
//
Функция БанковскийСчетОрганизацииПоУмолчанию() Экспорт
	
	Возврат ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетОрганизацииПоУмолчанию();
	
КонецФункции

// Возвращает кассу организации, если она одна в ИБ.
//
Функция КассаОрганизацииПоУмолчанию() Экспорт
	
	Возврат ЗначениеНастроекПовтИсп.ПолучитьКассуОрганизацииПоУмолчанию();
	
КонецФункции

// Возвращает вариант классификации по умолчанию.
//
Функция ВариантКлассификацииЗадолженностиПоУмолчанию(ТолькоЕслиОдин = Ложь) Экспорт
	
	Возврат ЗначениеНастроекПовтИсп.ВариантКлассификацииЗадолженностиПоУмолчанию(ТолькоЕслиОдин);
	
КонецФункции

// Возвращает полное имя основной формы объекта.
//
// Параметры:
//  Ссылка	 - 	ЛюбаяСсылка - ссылка на объект. Например, ДокументСсылка или СправочникСсылка
// 
// Возвращаемое значение:
//  Строка - полное имя основной формы объекта
//
Функция ПолноеИмяФормыОбъекта(Ссылка) Экспорт
	
	ИмяФормы = "";
	ОбъектМетаданных = Ссылка.Метаданные();
	
	Если ОбщегоНазначения.ЭтоДокумент(ОбъектМетаданных) Тогда
		ИмяФормы = ОбъектМетаданных.Формы.ФормаДокумента.ПолноеИмя();
	ИначеЕсли ОбщегоНазначения.ЭтоСправочник(ОбъектМетаданных) Тогда
		ИмяФормы = ОбъектМетаданных.Формы.ФормаЭлемента.ПолноеИмя();
	КонецЕсли;
		
	Возврат ИмяФормы;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РеквизитыОбъекта

// см. ОбщегоНазначения.ЗначенияРеквизитовОбъекта()
//
Функция ЗначенияРеквизитовОбъекта(Ссылка, Реквизиты) Экспорт
	
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, Реквизиты);
	
КонецФункции

// см. ОбщегоНазначения.ЗначениеРеквизитаОбъекта()
//
Функция ЗначениеРеквизитаОбъекта(Ссылка, ИмяРеквизита) Экспорт
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, ИмяРеквизита);
	
КонецФункции

#КонецОбласти

#Область ПраваПользователей

Функция ЭтоПолноправныйПользователь(Пользователь = Неопределено,
                                    ПроверятьПраваАдминистрированияСистемы = Ложь,
                                    УчитыватьПривилегированныйРежим = Истина) Экспорт
	
	Возврат Пользователи.ЭтоПолноправныйПользователь(
		Пользователь,
		ПроверятьПраваАдминистрированияСистемы,
		УчитыватьПривилегированныйРежим);
	
КонецФункции

#КонецОбласти

#Область КонтактнаяИнформация

// Актуализирует значения полей КИ из ее представления.
//
//  Параметры:
//		АдресПредставление - представление КИ
//		АдресЗначенияПолей - служебная информация, значения полей КИ
//		ТипКИ			   - тип контактной информации (Перечисления.ТипыКонтактнойИнформации)
//
Процедура ЗаполнитьЗначенияПолейКИПоПредставлению(Представление, ЗначенияПолей, ТипКИ = Неопределено) Экспорт
	
	ОбщегоНазначенияУТ.ЗаполнитьЗначенияПолейКИПоПредставлению(Представление, ЗначенияПолей, ТипКИ);
	
КонецПроцедуры

#КонецОбласти

#Область ПолнотекствовыйПоиск
// Определяет признак использования полнотекстового поиска, исходя из значения переданной ФО
// и настройки использования для базы данных в целом
//
// Параметры
//	ИмяФОИспользованияППД - Строка - имя учитваемой функциональной опции
//
// Возвращаемое значение:
//	Булево - Истина, если используется, Ложь в обратном случае.
//
Функция ИспользуетсяПолнотекстовыйПоиск(ИмяФОИспользованияППД) Экспорт
	
	Возврат ПолучитьФункциональнуюОпцию(ИмяФОИспользованияППД)
		И ПолнотекстовыйПоиск.ПолучитьРежимПолнотекстовогоПоиска() = РежимПолнотекстовогоПоиска.Разрешить;
	
КонецФункции

#КонецОбласти

#Область СпискиВыбора

Процедура ПолучитьСписокВыбораПеречисления(ИмяПеречисления, ДанныеВыбора, Параметры, ИсключаемыеЗначения) Экспорт
	
	ОбщегоНазначенияУТ.ПолучитьСписокВыбораПеречисления(
			ИмяПеречисления,
			ДанныеВыбора,
			Параметры,
			ИсключаемыеЗначения);
			
КонецПроцедуры

Функция ДатаСеанса() Экспорт
	Возврат ТекущаяДатаСеанса();
КонецФункции

#КонецОбласти

#Область ПолучениеДанныхВыбора

Процедура ОбработкаПолученияДанныхВыбораПВХСтатьиАктивовПассивов(ДанныеВыбора, Параметры, СтандартнаяОбработка) Экспорт
	
	Запрос = Новый Запрос;
	
	СвойстваЗапроса = СвойстваЗапросаПолученияДанныхВыбораСтатейАктивовПассивов(Параметры);
	
	Запрос.Текст = СвойстваЗапроса.ТекстЗапроса;
	Для каждого ПараметрЗапроса Из СвойстваЗапроса.ПараметрыЗапроса Цикл
		Запрос.УстановитьПараметр(ПараметрЗапроса.Ключ, ПараметрЗапроса.Значение);
	КонецЦикла;
		
	Если Параметры.Свойство("ДополнитьСтатьямиРасходов") И Параметры.ДополнитьСтатьямиРасходов Тогда
		
		СвойстваЗапроса = СвойстваЗапросаПолученияДанныхВыбораСтатейРасходов(Параметры);
		Запрос.Текст = Запрос.Текст + "
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|" + СвойстваЗапроса.ТекстЗапроса;
			
		Для каждого ПараметрЗапроса Из СвойстваЗапроса.ПараметрыЗапроса Цикл
			Запрос.УстановитьПараметр(ПараметрЗапроса.Ключ, ПараметрЗапроса.Значение);
		КонецЦикла;
	КонецЕсли;
	
	Если Параметры.Свойство("ДополнитьСтатьямиДоходов") И Параметры.ДополнитьСтатьямиДоходов Тогда
		
		СвойстваЗапроса = СвойстваЗапросаПолученияДанныхВыбораСтатейДоходов(Параметры);
		Запрос.Текст = Запрос.Текст + "
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|" + СвойстваЗапроса.ТекстЗапроса;
			
		Для каждого ПараметрЗапроса Из СвойстваЗапроса.ПараметрыЗапроса Цикл
			Запрос.УстановитьПараметр(ПараметрЗапроса.Ключ, ПараметрЗапроса.Значение);
		КонецЦикла;
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + "
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование
	|";
	
	Запрос.УстановитьПараметр("СтрокаПоиска", Параметры.СтрокаПоиска + "%");

	ДанныеВыбора = Новый СписокЗначений;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ДанныеВыбора.Добавить(
			Выборка.Ссылка, 
			СокрЛП(Выборка.Наименование) + " (" + СокрЛП(Выборка.Код) + ")", 
			Выборка.ПометкаУдаления);
	КонецЦикла;
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

Процедура ОбработкаПолученияДанныхВыбораПВХСтатьиРасходов(ДанныеВыбора, Параметры, СтандартнаяОбработка) Экспорт
	
	//++ НЕ УТ
	ПланыВидовХарактеристик.СтатьиРасходов.ОбработкаПараметровВыбора(Параметры);
	//-- НЕ УТ
	
	Если ЗначениеЗаполнено(Параметры.СтрокаПоиска) Тогда
		
		Запрос = Новый Запрос;
		
		СвойстваЗапроса = СвойстваЗапросаПолученияДанныхВыбораСтатейРасходов(Параметры);
		
		Запрос.Текст = СвойстваЗапроса.ТекстЗапроса;
		Для каждого ПараметрЗапроса Из СвойстваЗапроса.ПараметрыЗапроса Цикл
			Запрос.УстановитьПараметр(ПараметрЗапроса.Ключ, ПараметрЗапроса.Значение);
		КонецЦикла;
		
		Если Параметры.Свойство("ДополнитьСтатьямиАктивовПассивов") И Параметры.ДополнитьСтатьямиАктивовПассивов Тогда
			
			СвойстваЗапроса = СвойстваЗапросаПолученияДанныхВыбораСтатейАктивовПассивов(Параметры);
			Запрос.Текст = Запрос.Текст + "
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|" + СвойстваЗапроса.ТекстЗапроса;
				
			Для каждого ПараметрЗапроса Из СвойстваЗапроса.ПараметрыЗапроса Цикл
				Запрос.УстановитьПараметр(ПараметрЗапроса.Ключ, ПараметрЗапроса.Значение);
			КонецЦикла;
		КонецЕсли;
		
		Если Параметры.Свойство("ДополнитьСтатьямиДоходов") И Параметры.ДополнитьСтатьямиДоходов Тогда
		
			СвойстваЗапроса = СвойстваЗапросаПолученияДанныхВыбораСтатейДоходов(Параметры);
			Запрос.Текст = Запрос.Текст + "
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|" + СвойстваЗапроса.ТекстЗапроса;
				
			Для каждого ПараметрЗапроса Из СвойстваЗапроса.ПараметрыЗапроса Цикл
				Запрос.УстановитьПараметр(ПараметрЗапроса.Ключ, ПараметрЗапроса.Значение);
			КонецЦикла;
			
		КонецЕсли;
		
		Запрос.Текст = Запрос.Текст + "
		|
		|УПОРЯДОЧИТЬ ПО
		|	Наименование
		|";
		
		Запрос.УстановитьПараметр("СтрокаПоиска", Параметры.СтрокаПоиска + "%");
		
		ДанныеВыбора = Новый СписокЗначений;
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ДанныеВыбора.Добавить(
				Выборка.Ссылка, 
				СокрЛП(Выборка.Наименование) + " (" + СокрЛП(Выборка.Код) + ")", 
				Выборка.ПометкаУдаления);
		КонецЦикла;
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПолученияДанныхВыбораПВХСтатьиДоходов(ДанныеВыбора, Параметры, СтандартнаяОбработка) Экспорт
	
	Если ЗначениеЗаполнено(Параметры.СтрокаПоиска) Тогда
		
		Запрос = Новый Запрос;
		
		СвойстваЗапроса = СвойстваЗапросаПолученияДанныхВыбораСтатейДоходов(Параметры);
		
		Запрос.Текст = СвойстваЗапроса.ТекстЗапроса;
		Для каждого ПараметрЗапроса Из СвойстваЗапроса.ПараметрыЗапроса Цикл
			Запрос.УстановитьПараметр(ПараметрЗапроса.Ключ, ПараметрЗапроса.Значение);
		КонецЦикла;
		
		Если Параметры.Свойство("ДополнитьСтатьямиАктивовПассивов") И Параметры.ДополнитьСтатьямиАктивовПассивов Тогда
			
			СвойстваЗапроса = СвойстваЗапросаПолученияДанныхВыбораСтатейАктивовПассивов(Параметры);
			Запрос.Текст = Запрос.Текст + "
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|" + СвойстваЗапроса.ТекстЗапроса;
				
			Для каждого ПараметрЗапроса Из СвойстваЗапроса.ПараметрыЗапроса Цикл
				Запрос.УстановитьПараметр(ПараметрЗапроса.Ключ, ПараметрЗапроса.Значение);
			КонецЦикла;
		КонецЕсли;
		
		Если Параметры.Свойство("ДополнитьСтатьямиРасходов") И Параметры.ДополнитьСтатьямиРасходов Тогда
			
			СвойстваЗапроса = СвойстваЗапросаПолученияДанныхВыбораСтатейРасходов(Параметры);
			Запрос.Текст = Запрос.Текст + "
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|" + СвойстваЗапроса.ТекстЗапроса;
				
			Для каждого ПараметрЗапроса Из СвойстваЗапроса.ПараметрыЗапроса Цикл
				Запрос.УстановитьПараметр(ПараметрЗапроса.Ключ, ПараметрЗапроса.Значение);
			КонецЦикла;
		КонецЕсли;
		
		Запрос.Текст = Запрос.Текст + "
		|
		|УПОРЯДОЧИТЬ ПО
		|	Наименование
		|";
		
		Запрос.УстановитьПараметр("СтрокаПоиска", Параметры.СтрокаПоиска + "%");
		
		ДанныеВыбора = Новый СписокЗначений;
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ДанныеВыбора.Добавить(
				Выборка.Ссылка, 
				СокрЛП(Выборка.Наименование) + " (" + СокрЛП(Выборка.Код) + ")", 
				Выборка.ПометкаУдаления);
		КонецЦикла;
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПолученияДанныхВыбораПартииТМЦВЭксплуатации(ДанныеВыбора, Параметры, СтандартнаяОбработка) Экспорт
	
	Если НЕ Параметры.Свойство("ОграничениеВыбора") 
		ИЛИ НЕ Параметры.ОграничениеВыбора Тогда
		Возврат;
	КонецЕсли;
		
	Если Параметры.Свойство("ТекущийРегистратор") Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ВложенныйЗапрос.Ссылка
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТМЦВЭксплуатацииОбороты.Партия КАК Ссылка,
		|		ТМЦВЭксплуатацииОбороты.КоличествоОборот КАК КоличествоОборот
		|	ИЗ
		|		РегистрНакопления.ТМЦВЭксплуатации.Обороты(
		|				,
		|				,
		|				,
		|				НЕ Партия.ПометкаУдаления
		|					И (НЕ &ОтборДата
		|						ИЛИ Партия.ДатаЗавершенияЭксплуатации > &Дата
		|						ИЛИ Партия.ДатаЗавершенияЭксплуатации = ДАТАВРЕМЯ(1, 1, 1))
		|					И (НЕ &ОтборОрганизация
		|						ИЛИ Организация = &Организация)
		|					И (НЕ &ОтборПодразделение
		|						ИЛИ Подразделение = &Подразделение)
		|					И (НЕ &ОтборФизическоеЛицо
		|						ИЛИ ФизическоеЛицо = &ФизическоеЛицо)
		|					И (НЕ &ОтборНоменклатура
		|						ИЛИ Номенклатура = &Номенклатура)
		|					И (НЕ &ОтборХарактеристика
		|						ИЛИ Характеристика = &Характеристика)) КАК ТМЦВЭксплуатацииОбороты
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТМЦВЭксплуатации.Партия,
		|		ТМЦВЭксплуатации.Количество
		|	ИЗ
		|		РегистрНакопления.ТМЦВЭксплуатации КАК ТМЦВЭксплуатации
		|	ГДЕ
		|		ТМЦВЭксплуатации.Регистратор = &ТекущийРегистратор
		|		И НЕ ТМЦВЭксплуатации.Партия.ПометкаУдаления
		|		И (НЕ &ОтборДата
		|				ИЛИ ТМЦВЭксплуатации.Партия.ДатаЗавершенияЭксплуатации > &Дата
		|				ИЛИ ТМЦВЭксплуатации.Партия.ДатаЗавершенияЭксплуатации = ДАТАВРЕМЯ(1, 1, 1))
		|		И (НЕ &ОтборОрганизация
		|				ИЛИ ТМЦВЭксплуатации.Организация = &Организация)
		|		И (НЕ &ОтборПодразделение
		|				ИЛИ ТМЦВЭксплуатации.Подразделение = &Подразделение)
		|		И (НЕ &ОтборФизическоеЛицо
		|				ИЛИ ТМЦВЭксплуатации.ФизическоеЛицо = &ФизическоеЛицо)
		|		И (НЕ &ОтборНоменклатура
		|				ИЛИ ТМЦВЭксплуатации.Номенклатура = &Номенклатура)
		|		И (НЕ &ОтборХарактеристика
		|				ИЛИ ТМЦВЭксплуатации.Характеристика = &Характеристика)) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Ссылка
		|
		|ИМЕЮЩИЕ
		|	СУММА(ВложенныйЗапрос.КоличествоОборот) > 0";
		
	Иначе
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТМЦВЭксплуатацииОбороты.Партия КАК Ссылка
		|ИЗ
		|	РегистрНакопления.ТМЦВЭксплуатации.Обороты(
		|			,
		|			,
		|			,
		|			НЕ Партия.ПометкаУдаления
		|				И (НЕ &ОтборДата
		|					ИЛИ Партия.ДатаЗавершенияЭксплуатации > &Дата
		|					ИЛИ Партия.ДатаЗавершенияЭксплуатации = ДАТАВРЕМЯ(1, 1, 1))
		|				И (НЕ &ОтборОрганизация
		|					ИЛИ Организация = &Организация)
		|				И (НЕ &ОтборПодразделение
		|					ИЛИ Подразделение = &Подразделение)
		|				И (НЕ &ОтборФизическоеЛицо
		|					ИЛИ ФизическоеЛицо = &ФизическоеЛицо)
		|				И (НЕ &ОтборНоменклатура
		|					ИЛИ Номенклатура = &Номенклатура)
		|				И (НЕ &ОтборХарактеристика
		|					ИЛИ Характеристика = &Характеристика)) КАК ТМЦВЭксплуатацииОбороты
		|ГДЕ
		|	ТМЦВЭксплуатацииОбороты.КоличествоОборот > 0";
		
	КонецЕсли; 
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("Дата", Дата(1, 1, 1, 0, 0, 0));
	ПараметрыЗапроса.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	ПараметрыЗапроса.Вставить("Подразделение", Справочники.СтруктураПредприятия.ПустаяСсылка());
	ПараметрыЗапроса.Вставить("ФизическоеЛицо", Справочники.ФизическиеЛица.ПустаяСсылка());
	ПараметрыЗапроса.Вставить("Номенклатура", Справочники.Номенклатура.ПустаяСсылка());
	ПараметрыЗапроса.Вставить("Характеристика", Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
	ПараметрыЗапроса.Вставить("ТекущийРегистратор", Неопределено);
	
	ЗаполнитьЗначенияСвойств(ПараметрыЗапроса, Параметры);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Для Каждого КлючИЗначение Из ПараметрыЗапроса Цикл
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		Запрос.УстановитьПараметр("Отбор" + КлючИЗначение.Ключ, ЗначениеЗаполнено(КлючИЗначение.Значение));
	КонецЦикла;
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		СтандартнаяОбработка = Ложь;
	Иначе
		Параметры.Отбор.Вставить("Ссылка", Результат.Выгрузить().ВыгрузитьКолонку("Ссылка"));
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПолученияДанныхВыбораХозяйственныеОперации(ДанныеВыбора, Параметры, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ДоступныеФО = Новый Массив;
	Для Каждого ФО ИЗ Метаданные.ФункциональныеОпции Цикл
		ДоступныеФО.Добавить(ФО.Имя);
	КонецЦикла;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	НастройкиХозяйственныхОперацийФункциональныеОпции.ИмяФункциональнойОпции,
	|	НастройкиХозяйственныхОперацийФункциональныеОпции.Ссылка.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|ИЗ
	|	Справочник.НастройкиХозяйственныхОпераций.ФункциональныеОпции КАК НастройкиХозяйственныхОперацийФункциональныеОпции
	|ГДЕ
	|	НастройкиХозяйственныхОперацийФункциональныеОпции.ИмяФункциональнойОпции В(&ДоступныеФункциональныеОпции)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ХозяйственнаяОперация
	|
	|ИТОГИ ПО
	|	ХозяйственнаяОперация";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ДоступныеФункциональныеОпции", ДоступныеФО);
	
	МассивИсключаемыхЗначений = Новый Массив;
	ВыборкаПоОперациям = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоОперациям.Следующий() Цикл
		ФункциональныеОпции = ВыборкаПоОперациям.Выбрать();
		ДоступнаПоФО = Ложь;
		Пока ФункциональныеОпции.Следующий() Цикл
			ДоступнаПоФО = ПолучитьФункциональнуюОпцию(ФункциональныеОпции.ИмяФункциональнойОпции);
			Если ДоступнаПоФО Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если НЕ ДоступнаПоФО Тогда
			МассивИсключаемыхЗначений.Добавить(ВыборкаПоОперациям.ХозяйственнаяОперация);
		КонецЕсли;
	КонецЦикла;
	
	ПолучитьСписокВыбораПеречисления(
		"ХозяйственныеОперации",
		ДанныеВыбора,
		Параметры,
		МассивИсключаемыхЗначений);
	
КонецПроцедуры

//++ НЕ УТ

Процедура ОбработкаПолученияДанныхВыбораПланСчетовХозрасчетный(ДанныеВыбора, Параметры, СтандартнаяОбработка) Экспорт
	
	Если Параметры.Свойство("ВидыСубконто") Тогда
		ИсключаемыеСсылки = Неопределено;
		Параметры.Свойство("ИсключаемыеСсылки", ИсключаемыеСсылки);
		Параметры.Отбор.Вставить("Ссылка", ПланыСчетов.Хозрасчетный.СчетаСНаборомСубконто(Параметры.ВидыСубконто, ИсключаемыеСсылки));
	КонецЕсли;
	
КонецПроцедуры

//-- НЕ УТ

#КонецОбласти

#Область ПолучениеПредставления

// Формирование заголовка документа.
//
// Параметры:
// Документ - ДокументСсылка.ВводОстатков
// НомерДокумента - Строка - Номер документа ВводОстатков
// ДатаДокумента - Дата - Дата документа ВводОстатков
// ТипОперации - ПеречислениеСсылка.ТипыОперацийВводаОстатков - Тип операции документа ВводОстатков
//
// Возвращаемое значение:
//  Строка - строковое значение заголовка документа.S
//
Функция ЗаголовокДокументаВводОстатковПоТипуОперации(Документ, НомерДокумента, ДатаДокумента, ТипОперации) Экспорт
	
	Заголовок = "";
	ИспользуетсяНесколькоКасс = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоКасс");
	ИспользуетсяНесколькоСчетов = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоРасчетныхСчетов");
	
	Если ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиВАвтономныхКассахККМКОформлениюОтчетовОРозничныхПродажах
		ИЛИ ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиВАвтономныхКассахККМПоРозничнойВыручке
		ИЛИ ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиВКассах Тогда
			Если ЗначениеЗаполнено(Документ.Ссылка) Тогда
				Заголовок = ?(ИспользуетсяНесколькоКасс,
								НСтр("ru = 'Ввод начальных остатков в кассах %1 от %2'"),
								НСтр("ru = 'Ввод начальных остатков в кассе %1 от %2'"));
			Иначе
				Заголовок = ?(ИспользуетсяНесколькоКасс,
								НСтр("ru = 'Ввод начальных остатков в кассах (создание)'"),
								НСтр("ru = 'Ввод начальных остатков в кассе (создание)'"));
			КонецЕсли;
	ИначеЕсли ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиЗадолженностиКлиентов Тогда
			Заголовок = ?(ЗначениеЗаполнено(Документ.Ссылка),
							НСтр("ru = 'Ввод начальных остатков задолженности клиентов %1 от %2'"),
							НСтр("ru = 'Ввод начальных остатков задолженности клиентов (создание)'"));
	ИначеЕсли ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиЗадолженностиПередПоставщиками Тогда
			Заголовок = ?(ЗначениеЗаполнено(Документ.Ссылка),
							НСтр("ru = 'Ввод начальных остатков задолженности перед поставщиками %1 от %2'"),
							НСтр("ru = 'Ввод начальных остатков задолженности перед поставщиками (создание)'"));
	ИначеЕсли ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиЗадолженностиПодотчетников Тогда
			Заголовок = ?(ЗначениеЗаполнено(Документ.Ссылка),
							НСтр("ru = 'Ввод начальных остатков задолженности подотчетных лиц %1 от %2'"),
							НСтр("ru = 'Ввод начальных остатков задолженности подотчетных лиц (создание)'"));
	ИначеЕсли ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ПерерасходыПодотчетныхСредств Тогда
			Заголовок = ?(ЗначениеЗаполнено(Документ.Ссылка),
							НСтр("ru = 'Ввод начальных остатков перерасходов подотчетных средств %1 от %2'"),
							НСтр("ru = 'Ввод начальных остатков перерасходов подотчетных средств (создание)'"));
	ИначеЕсли ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиНаБанковскихСчетах Тогда
		Если ЗначениеЗаполнено(Документ.Ссылка) Тогда
			Заголовок = ?(ИспользуетсяНесколькоСчетов,
							НСтр("ru = 'Ввод начальных остатков на банковских счетах %1 от %2'"),
							НСтр("ru = 'Ввод начальных остатков на банковском счете %1 от %2'"));
		Иначе
			Заголовок = ?(ИспользуетсяНесколькоСчетов,
							НСтр("ru = 'Ввод начальных остатков на банковских счетах (создание)'"),
							НСтр("ru = 'Ввод начальных остатков на банковском счете (создание)'"));
		КонецЕсли;
	ИначеЕсли ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиПоАвансамКлиентов Тогда
			Заголовок = ?(ЗначениеЗаполнено(Документ.Ссылка),
							НСтр("ru = 'Ввод начальных остатков авансов, полученных от клиентов %1 от %2'"),
							НСтр("ru = 'Ввод начальных остатков авансов, полученных от клиентов (создание)'"));
	ИначеЕсли ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиПоАвансамПоставщикам Тогда
			Заголовок = ?(ЗначениеЗаполнено(Документ.Ссылка),
							НСтр("ru = 'Ввод начальных остатков авансов, выданных поставщикам %1 от %2'"),
							НСтр("ru = 'Ввод начальных остатков авансов, выданных поставщикам (создание)'"));
	ИначеЕсли ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиРасчетовМеждуОрганизациямиПоАвансам Тогда
			Заголовок = ?(ЗначениеЗаполнено(Документ.Ссылка),
							НСтр("ru = 'Ввод начальных остатков расчетов между организациями по авансам %1 от %2'"),
							НСтр("ru = 'Ввод начальных остатков расчетов между организациями по авансам (создание)'"));
	ИначеЕсли ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиРасчетовМеждуОрганизациямиПоРеализациям Тогда
			Заголовок = ?(ЗначениеЗаполнено(Документ.Ссылка),
							НСтр("ru = 'Ввод начальных остатков расчетов между организациями по реализациям %1 от %2'"),
							НСтр("ru = 'Ввод начальных остатков расчетов между организациями по реализациям (создание)'"));
	ИначеЕсли ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиСобственныхТоваров Тогда
			Заголовок = ?(ЗначениеЗаполнено(Документ.Ссылка),
							НСтр("ru = 'Ввод начальных остатков собственных товаров %1 от %2'"),
							НСтр("ru = 'Ввод начальных остатков собственных товаров (создание)'"));
	ИначеЕсли ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиТоваровПолученныхНаКомиссию Тогда
			Заголовок = ?(ЗначениеЗаполнено(Документ.Ссылка),
							НСтр("ru = 'Ввод начальных остатков полученных на комиссию товаров %1 от %2'"),
							НСтр("ru = 'Ввод начальных остатков полученных на комиссию товаров (создание)'"));
	ИначеЕсли ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиТоваровПереданныхНаКомиссию Тогда
			Заголовок = ?(ЗначениеЗаполнено(Документ.Ссылка),
							НСтр("ru = 'Ввод начальных остатков переданных на комиссию товаров %1 от %2'"),
							НСтр("ru = 'Ввод начальных остатков переданных на комиссию товаров (создание)'"));
	ИначеЕсли ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиВозвратнойТарыПереданнойКлиентам Тогда
			Заголовок = ?(ЗначениеЗаполнено(Документ.Ссылка),
							НСтр("ru = 'Ввод начальных остатков переданной клиентам возвратной тары %1 от %2'"),
							НСтр("ru = 'Ввод начальных остатков переданной клиентам возвратной тары (создание)'"));
	ИначеЕсли ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиВозвратнойТарыПринятойОтПоставщиков Тогда
			Заголовок = ?(ЗначениеЗаполнено(Документ.Ссылка),
							НСтр("ru = 'Ввод начальных остатков принятой от поставщиков возвратной тары %1 от %2'"),
							НСтр("ru = 'Ввод начальных остатков принятой от поставщиков возвратной тары (создание)'"));
	ИначеЕсли ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ПереходНаИспользованиеАдресногоХраненияОстатков Тогда
			Заголовок = ?(ЗначениеЗаполнено(Документ.Ссылка),
							НСтр("ru = 'Переход на использование адресного хранения остатков %1 от %2'"),
							НСтр("ru = 'Переход на использование адресного хранения остатков (создание)'"));
	ИначеЕсли ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ПереходНаИспользованиеСкладскихПомещений Тогда
			Заголовок = ?(ЗначениеЗаполнено(Документ.Ссылка),
							НСтр("ru = 'Переход на использование складских помещений %1 от %2'"),
							НСтр("ru = 'Переход на использование складских помещений (создание)'"));
	ИначеЕсли ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиМатериаловПереданныхПереработчикам Тогда
			Заголовок = ?(ЗначениеЗаполнено(Документ.Ссылка),
							НСтр("ru = 'Ввод начальных остатков переданных переработчику материалов %1 от %2'"),
							НСтр("ru = 'Ввод начальных остатков переданных переработчику материалов (создание)'"));
	ИначеЕсли ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиМатериаловПереданныхВПроизводство Тогда
			Заголовок = ?(ЗначениеЗаполнено(Документ.Ссылка),
							НСтр("ru = 'Ввод начальных остатков переданных в производство материалов %1 от %2'"),
							НСтр("ru = 'Ввод начальных остатков переданных в производство материалов (создание)'"));
	ИначеЕсли ТипОперации = Перечисления.ТипыОперацийВводаОстатков.СторноОтчетовПоЭквайрингу Тогда
			Заголовок = ?(ЗначениеЗаполнено(Документ.Ссылка),
							НСтр("ru = 'Сторно отчетов по эквайрингу %1 от %2'"),
							НСтр("ru = 'Сторно отчетов по эквайрингу (создание)'"));
	ИначеЕсли ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиПрочихАктивовПассивов Тогда
			Заголовок = ?(ЗначениеЗаполнено(Документ.Ссылка),
							НСтр("ru = 'Ввод начальных остатков прочих активов и пассивов %1 от %2'"),
							НСтр("ru = 'Ввод начальных остатков прочих активов и пассивов (создание)'"));
	ИначеЕсли ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ПодарочныеСертификаты Тогда
			Заголовок = ?(ЗначениеЗаполнено(Документ.Ссылка),
							НСтр("ru = 'Ввод начальных остатков подарочных сертификатов %1 от %2'"),
							НСтр("ru = 'Ввод начальных остатков подарочных сертификатов (создание)'"));
	ИначеЕсли ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиПоДоговорамКредитовИДепозитов Тогда
			Заголовок = ?(ЗначениеЗаполнено(Документ.Ссылка),
							НСтр("ru = 'Ввод начальных остатков расчетов по кредитам, депозитам и выданным займам %1 от %2'"),
							НСтр("ru = 'Ввод начальных остатков расчетов по кредитам, депозитам и выданным займам (создание)'"));
	ИначеЕсли ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиТМЦВЭксплуатации Тогда
			Заголовок = ?(ЗначениеЗаполнено(Документ.Ссылка),
							НСтр("ru = 'Ввод начальных остатков ТМЦ в эксплуатации %1 от %2'"),
							НСтр("ru = 'Ввод начальных остатков ТМЦ в эксплуатации (создание)'"));
	ИначеЕсли ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиПрочихРасходов Тогда
			Заголовок = ?(ЗначениеЗаполнено(Документ.Ссылка),
							НСтр("ru = 'Ввод начальных остатков прочих расходов %1 от %2'"),
							НСтр("ru = 'Ввод начальных остатков прочих расходов (создание)'"));
	ИначеЕсли ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ФинансовыйРезультатЗаПрошлыеПериоды Тогда
			Заголовок = ?(ЗначениеЗаполнено(Документ.Ссылка),
							НСтр("ru = 'Финансовый результат за прошлые периоды %1 от %2'"),
							НСтр("ru = 'Финансовый результат за прошлые периоды (создание)'"));
	ИначеЕсли ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОптовыеПродажиЗаПрошлыеПериоды Тогда
			Заголовок = ?(ЗначениеЗаполнено(Документ.Ссылка),
							НСтр("ru = 'Оптовые продажи за прошлые периоды %1 от %2'"),
							НСтр("ru = 'Оптовые продажи за прошлые периоды (создание)'"));
	ИначеЕсли ТипОперации = Перечисления.ТипыОперацийВводаОстатков.РозничныеПродажиЗаПрошлыеПериоды Тогда
			Заголовок = ?(ЗначениеЗаполнено(Документ.Ссылка),
							НСтр("ru = 'Розничные продажи за прошлые периоды %1 от %2'"),
							НСтр("ru = 'Розничные продажи за прошлые периоды (создание)'"));
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Заголовок,
																					  НомерДокумента,
																					  ДатаДокумента);
КонецФункции

#КонецОбласти

#Область ОбновлениеИБ

// Возвращает структуру параметров, необходимых при запуске конфигурации.
//
// Параметры:
//   Параметры - Структура - (возвращаемое значение) структура параметров работы программы при запуске.
//
Процедура ПараметрыРаботыКлиентаПриЗапуске(Параметры) Экспорт
	
	// Для запрета обновления с неподдерживаемых версий
	Параметры.Вставить("ВерсияДанных", ОбновлениеИнформационнойБазыСлужебный.ВерсияИБ(Метаданные.Имя, Истина)); 
	Параметры.Вставить("ВерсияМетаданных", СокрЛП(Метаданные.Версия)); 
	
КонецПроцедуры

Функция СвойстваЗапросаПолученияДанныхВыбораСтатейАктивовПассивов(Параметры)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СтатьиАктивовПассивов.Ссылка,
	|	СтатьиАктивовПассивов.Код,
	|	СтатьиАктивовПассивов.Наименование,
	|	СтатьиАктивовПассивов.ПометкаУдаления
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиАктивовПассивов КАК СтатьиАктивовПассивов
	|ГДЕ
	|	(СтатьиАктивовПассивов.АктивПассив В (&АктивПассив) ИЛИ &БезОграниченийПоАктивуПассиву)
	|	И СтатьиАктивовПассивов.Наименование ПОДОБНО &СтрокаПоиска
	|";
	
	ПараметрыЗапроса = Новый Структура;
	
	АктивПассив = Неопределено;
	Параметры.Отбор.Свойство("АктивПассив", АктивПассив);
	ПараметрыЗапроса.Вставить("АктивПассив", АктивПассив);
	ПараметрыЗапроса.Вставить("БезОграниченийПоАктивуПассиву", НЕ ЗначениеЗаполнено(АктивПассив));
	
	Результат = Новый Структура;
	Результат.Вставить("ТекстЗапроса",     ТекстЗапроса);
	Результат.Вставить("ПараметрыЗапроса", ПараметрыЗапроса);
	
	Возврат Результат;
	
КонецФункции

Функция СвойстваЗапросаПолученияДанныхВыбораСтатейРасходов(Параметры)

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СтатьиРасходов.Ссылка,
	|	СтатьиРасходов.Код,
	|	СтатьиРасходов.Наименование,
	|	СтатьиРасходов.ПометкаУдаления
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|ГДЕ
	|	(СтатьиРасходов.Наименование ПОДОБНО &СтрокаПоиска
	|		ИЛИ СтатьиРасходов.Код ПОДОБНО &СтрокаПоиска)
	|	И НЕ СтатьиРасходов.Ссылка В (&ЗаблокированныеСтатьи)
	|	И (Не СтатьиРасходов.ОграничитьИспользование
	|		ИЛИ СтатьиРасходов.Ссылка В (
	|			ВЫБРАТЬ
	|				ДоступныеОперации.Ссылка
	|			ИЗ
	|				ПланВидовХарактеристик.СтатьиРасходов.ДоступныеХозяйственныеОперации КАК ДоступныеОперации
	|			ГДЕ
	|			ДоступныеОперации.ХозяйственнаяОперация = &ХозяйственнаяОперация
	|			ИЛИ &ПоВсемХозОперациям
	|		)
	|	 )
	|	И СтатьиРасходов.ВариантРаспределенияРасходов <> ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы)
	|	И (СтатьиРасходов.ВариантРаспределенияРасходов В (&ВариантРаспределенияРасходов)
	|		ИЛИ &ПоВсемВариантамРаспределения)
	|	И (СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат В (&ВидДеятельностиДляНалоговогоУчетаЗатрат)
	|		ИЛИ &ПоВсемВидамДеятельностиДляНалоговогоУчетаЗатрат)
	|	И (СтатьиРасходов.ДоговорыКредитовИДепозитов ИЛИ &БезУчетаДоговоровКредитовИДепозитов)
	|";
	
	ПараметрыЗапроса = Новый Структура;
	
	ХозяйственнаяОперация = Неопределено;
	Если Параметры.Отбор.Свойство("ХозяйственнаяОперация") Тогда
		ХозяйственнаяОперация = Параметры.Отбор.ХозяйственнаяОперация;
	КонецЕсли;
	ПараметрыЗапроса.Вставить("ХозяйственнаяОперация", ХозяйственнаяОперация);
	ПараметрыЗапроса.Вставить("ПоВсемХозОперациям",    ХозяйственнаяОперация = Неопределено);
	
	ВариантРаспределенияРасходов = Неопределено;
	Если Параметры.Отбор.Свойство("ВариантРаспределенияРасходов") Тогда
		ВариантРаспределенияРасходов = Параметры.Отбор.ВариантРаспределенияРасходов;
	КонецЕсли;
	ПараметрыЗапроса.Вставить("ВариантРаспределенияРасходов", ВариантРаспределенияРасходов);
	ПараметрыЗапроса.Вставить("ПоВсемВариантамРаспределения", ВариантРаспределенияРасходов = Неопределено);
	
	ВидДеятельностиДляНалоговогоУчетаЗатрат = Неопределено;
	Если Параметры.Отбор.Свойство("ВидДеятельностиДляНалоговогоУчетаЗатрат") Тогда
		ВидДеятельностиДляНалоговогоУчетаЗатрат = Параметры.Отбор.ВидДеятельностиДляНалоговогоУчетаЗатрат;
	КонецЕсли;
	ПараметрыЗапроса.Вставить("ВидДеятельностиДляНалоговогоУчетаЗатрат", ВидДеятельностиДляНалоговогоУчетаЗатрат);
	ПараметрыЗапроса.Вставить("ПоВсемВидамДеятельностиДляНалоговогоУчетаЗатрат", ВидДеятельностиДляНалоговогоУчетаЗатрат = Неопределено);
	
	ДоговорыКредитовИДепозитов = Неопределено;
	Если Параметры.Отбор.Свойство("ДоговорыКредитовИДепозитов") Тогда
		ДоговорыКредитовИДепозитов = Параметры.Отбор.ДоговорыКредитовИДепозитов;
	КонецЕсли;
	ПараметрыЗапроса.Вставить("БезУчетаДоговоровКредитовИДепозитов", ДоговорыКредитовИДепозитов = Неопределено);
	
	ДоступнаСебестоимостьПродаж = Параметры.Свойство("ДоступнаСебестоимостьПродаж");
	ПараметрыЗапроса.Вставить("ЗаблокированныеСтатьи", ПланыВидовХарактеристик.СтатьиРасходов.ЗаблокированныеСтатьиРасходов(ДоступнаСебестоимостьПродаж));
	
	Результат = Новый Структура;
	Результат.Вставить("ТекстЗапроса",     ТекстЗапроса);
	Результат.Вставить("ПараметрыЗапроса", ПараметрыЗапроса);
	
	Возврат Результат;
	
КонецФункции

Функция СвойстваЗапросаПолученияДанныхВыбораСтатейДоходов(Параметры)
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СтатьиДоходов.Ссылка КАК Ссылка,
	|	СтатьиДоходов.Код КАК Код,
	|	СтатьиДоходов.Наименование КАК Наименование,
	|	СтатьиДоходов.ПометкаУдаления КАК ПометкаУдаления
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиДоходов КАК СтатьиДоходов
	|ГДЕ
	|	СтатьиДоходов.Наименование ПОДОБНО &СтрокаПоиска
	|	И НЕ СтатьиДоходов.ЭтоГруппа
	|	И НЕ СтатьиДоходов.Ссылка В (&ЗаблокированныеСтатьи)
	|	И (СтатьиДоходов.ДоговорыКредитовИДепозитов ИЛИ &БезУчетаДоговоровКредитовИДепозитов)
	|";
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("ЗаблокированныеСтатьи", ПланыВидовХарактеристик.СтатьиДоходов.ЗаблокированныеСтатьиДоходов());
	
	ДоговорыКредитовИДепозитов = Неопределено;
	Если Параметры.Отбор.Свойство("ДоговорыКредитовИДепозитов") Тогда
		ДоговорыКредитовИДепозитов = Параметры.Отбор.ДоговорыКредитовИДепозитов;
	КонецЕсли;
	ПараметрыЗапроса.Вставить("БезУчетаДоговоровКредитовИДепозитов", ДоговорыКредитовИДепозитов = Неопределено);
	
	Результат = Новый Структура;
	Результат.Вставить("ТекстЗапроса",     ТекстЗапроса);
	Результат.Вставить("ПараметрыЗапроса", ПараметрыЗапроса);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти
