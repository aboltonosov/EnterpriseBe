////////////////////////////////////////////////////////////////////////////////
// Содержит процедуры для заполнения форм отчетности по НДС:
// - налоговой декларации по НДС с 1 квартала 2015 года
// - реестров для подтверждения ставки 0% (Приложение 5)
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

#Область СлужебныйПрограммныйИнтерфейс

#Область ДекларацияНДС

// Заполняет контейнер с показателями декларации по НДС.
// Для версии отчета ФормаОтчета2017Кв1.
Процедура ЗаполнитьОтчетНДСФормаОтчета2017Кв1(ПараметрыОтчета, Контейнер) Экспорт
	
	ПараметрыОтчета.Вставить("ПравилаЗаполнения", "2017Кв1");
	ЗаполнитьДекларациюНДС(ПараметрыОтчета, Контейнер);
	
КонецПроцедуры

// Заполняет контейнер с показателями декларации по НДС.
// Для версии отчета ФормаОтчета2015Кв1.
Процедура ЗаполнитьОтчетНДСФормаОтчета2015Кв1(ПараметрыОтчета, Контейнер) Экспорт
	
	ПараметрыОтчета.Вставить("ПравилаЗаполнения", "2015Кв1");
	ЗаполнитьДекларациюНДС(ПараметрыОтчета, Контейнер);
	
КонецПроцедуры

// Создает список показателей декларации по НДС, которые будут автоматически заполняться.
// Для версии отчета ФормаОтчета2017Кв1.
Процедура ПолучитьСведенияОПоказателяхОтчетаНДСФормаОтчета2017Кв1(ПоказателиОтчета) Экспорт
	
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000100003003", Истина, Истина);
	
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000200001003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000200002003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000200003003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000200006003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000200007003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000200008003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000200009003", Истина, Истина);

	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300001003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300002003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300003003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300004003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300006003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300007003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300007005", Истина, Истина);

	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300008005", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300009005", Истина, Истина);
	
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300012003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300013003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300014003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300015003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300016003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300017003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300018003", Истина, Истина);

	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000310001001", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000310002001", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000310003001", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000310004001", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000310005001", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000310006001", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000310007001", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000310007002", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000310007003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000310007004", Истина, Истина);
	
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П0004М1001003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П0004М1002003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П0004М1003003", Истина, Истина);
	
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П0006М1001003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П0006М1002003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П0006М1003003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П0006М1004003", Истина, Истина);
	
КонецПроцедуры

// Создает список показателей декларации по НДС, которые будут автоматически заполняться.
// Для версии отчета ФормаОтчета2015Кв1.
Процедура ПолучитьСведенияОПоказателяхОтчетаНДСФормаОтчета2015Кв1(ПоказателиОтчета) Экспорт
	
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000100003003", Истина, Истина);
	
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000200001003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000200002003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000200003003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000200006003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000200007003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000200008003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000200009003", Истина, Истина);

	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300001003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300002003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300003003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300004003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300006003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300007003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300007005", Истина, Истина);

	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300008005", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300009005", Истина, Истина);
	
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300012003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300013003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300014003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300015003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300016003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300017003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000300018003", Истина, Истина);

	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000310001000", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000310002001", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000310002002", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000310002003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000310002004", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000310002005", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000310002006", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000310002007", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000310002008", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000310002009", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000310003001", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000310004001", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000310005001", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000310006001", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000310007001", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000310008001", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000310008002", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000310008003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000310008004", Истина, Истина);
	
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000400001003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000400002003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000400003003", Истина, Истина);
	
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000600001003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000600002003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000600003003", Истина, Истина);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П000600004003", Истина, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ЭлектронноеПредставлениеКнигЖурнала

Функция ЭлектронноеПредставлениеКнигиПокупок_501(ВыгружаемыйДокумент) Экспорт
	
	РезультатВыгрузки = Неопределено;
	
	ПараметрыВыгрузки = ОсновныеСведенияЭлектронногоПредставления(ВыгружаемыйДокумент, "1115102");
	
	КомпоновщикXML = КомпоновщикXML_Новый();
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Файл");
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance");
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИдФайл", ПараметрыВыгрузки.ИдФайл);
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ВерсПрог", ПараметрыВыгрузки.ВерсПрог, "Т,40");
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ВерсФорм", "5.01");
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Документ");
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "КНД", ПараметрыВыгрузки.КНД);
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвПокуп");
	
	ВыгрузитьСведенияОбОрганизации_501(КомпоновщикXML, ПараметрыВыгрузки);
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвКнПок");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Период", ПараметрыВыгрузки.Период);
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ОтчетГод", ПараметрыВыгрузки.ОтчетГод);
	
	ВывестиУзелКнигиПокупок501_Всего(КомпоновщикXML, ВыгружаемыйДокумент);
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
	Для Каждого СтрокаСведений Из ВыгружаемыйДокумент.ТабличнаяЧасть Цикл
		ВывестиУзелКнигиПокупок501_СвПокупка(КомпоновщикXML, СтрокаСведений);
	КонецЦикла;
	
	ВывестиУзел_Подписант(КомпоновщикXML, ПараметрыВыгрузки);
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
	РезультатВыгрузки = Новый Массив;
	
	СтруктураВыгрузки = Новый Структура;
	СтруктураВыгрузки.Вставить("АдресФайлаВыгрузки", КомпоновщикXML_СсылкаНаВременноеХранилище(КомпоновщикXML));
	СтруктураВыгрузки.Вставить("ИмяФайлаВыгрузки", ПараметрыВыгрузки.ИдФайл + ".xml");
	
	РезультатВыгрузки.Добавить(СтруктураВыгрузки);
	
	Возврат РезультатВыгрузки;
	
КонецФункции

Функция ЭлектронноеПредставлениеКнигиПокупок_502(ВыгружаемыйДокумент) Экспорт
	
	РезультатВыгрузки = Неопределено;
	
	ПараметрыВыгрузки = ОсновныеСведенияЭлектронногоПредставления(ВыгружаемыйДокумент, "1115102");
	
	СведенияДокумента = ВыгружаемыйДокумент.ДанныеОтчета.Получить();
	
	Если СведенияДокумента <> Неопределено Тогда
		
		КомпоновщикXML = КомпоновщикXML_Новый();
		
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Файл");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИдФайл", ПараметрыВыгрузки.ИдФайл);
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ВерсПрог", ПараметрыВыгрузки.ВерсПрог, "Т,40");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ВерсФорм", "5.02");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИдФайлИсх", ПараметрыВыгрузки.ИдФайл);
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИдФайлПерв", ПараметрыВыгрузки.ИдФайл);
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "КолФайл", 1, "Ч,5");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НомФайл", 1, "Ч,5");
		
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Документ");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "КНД", ПараметрыВыгрузки.КНД);
		
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвПокуп");
		
		Если ПараметрыВыгрузки.ЭтоИП Тогда
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СведИП");
			
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИННФЛ", ПараметрыВыгрузки.ИННФЛ, "Т,12");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "СвГосРегИП", ПараметрыВыгрузки.СвГосРегИП, "Т,100");
			
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ФИОИП");
			
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Фамилия", ПараметрыВыгрузки.ФамилияИП, "Т,60");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Имя", ПараметрыВыгрузки.ИмяИП, "Т,60");
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "Отчество", ПараметрыВыгрузки.ОтчествоИП, "Т,60");
			
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
			
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
			
		Иначе
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СведЮЛ");
			
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НаимОрг", ПараметрыВыгрузки.НаимОрг, "Т,1000");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИННЮЛ", ПараметрыВыгрузки.ИННЮЛ, "Т,10");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "КПП", ПараметрыВыгрузки.КПП, "Т,9");
			
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
			
		КонецЕсли;
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвКнПок");
		
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Период", ПараметрыВыгрузки.Период);
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ОтчетГод", ПараметрыВыгрузки.ОтчетГод);
		
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Всего");
		
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "СумНДСВыч", СведенияДокумента.ОбщиеСведения.СумНДСВыч, "Ч,19,2");
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		
		Для Каждого СтрокаСведений Из СведенияДокумента.Записи Цикл
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвПокупка");
			
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НомПП", СтрокаСведений.НомПП, "Ч,12");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НомерСчФ", СтрокаСведений.НомерСчФ, "Т,1000");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ДатаСчФ", СтрокаСведений.ДатаСчФ, "Т,10");
			
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "НомИспрСчФ",  СтрокаСведений.НомИспрСчФ,  "Ч,3");
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаИспрСчФ", СтрокаСведений.ДатаИспрСчФ, "Т,10");
			
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "НомерКСчФ", СтрокаСведений.НомерКСчФ, "Т,1000");
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаКСчФ",  СтрокаСведений.ДатаКСчФ,  "Т,10");
			
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "НомИспрКСчФ",  СтрокаСведений.НомИспрКСчФ,  "Ч,3");
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаИспрКСчФ", СтрокаСведений.ДатаИспрКСчФ, "Т,10");
			
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НаимПрод", СтрокаСведений.НаимПрод, "Т,1000");
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ИННЮЛ", СтрокаСведений.ИННЮЛ, "Т,10");
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "КПП",   СтрокаСведений.КПП,   "Т,9");
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ИННФЛ", СтрокаСведений.ИННФЛ, "Т,12");
			
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "КодОКВ", СтрокаСведений.КодОКВ, "Ч,3");
			
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "СтТовУчНалВсего", СтрокаСведений.СтТовУчНалВсего, "Ч,19,2");
			
			Для Каждого КодВидаОперации Из СтрокаСведений.КодВидОпер Цикл
				КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "КодВидОпер");
				
				КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, КодВидаОперации);
			КонецЦикла;
			
			Для Каждого ДокументПодтверждения Из СтрокаСведений.СвДокУплНал Цикл
				КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвДокУплНал");
				
				КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НомерДокУплНал", ДокументПодтверждения.НомерДокУплНал, "Т,256");
				КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ДатаДокУплНал", ДокументПодтверждения.ДатаДокУплНал, "Т,10");
				
				КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
			КонецЦикла;
			
			Для Каждого ДатаПринятияНаУчет Из СтрокаСведений.ДатаПринУчет Цикл
				ДатаПринятияНаУчет = Лев(СокрЛП(ДатаПринятияНаУчет), 10);
				
				КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ДатаПринУчет");
				
				КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, ДатаПринятияНаУчет);
			КонецЦикла;
			
			Если СтрокаСведений.СвПосредн <> Неопределено Тогда
				КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвПосредн");
				
				Если СтрокаСведений.СвПосредн.Свойство("ИННЮЛ") Тогда
					КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СведЮЛ");
					
					КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НаимОрг", СтрокаСведений.СвПосредн.НаимОрг, "Т,1000");
					КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИННЮЛ", СтрокаСведений.СвПосредн.ИННЮЛ, "Т,10");
					КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "КПП", СтрокаСведений.СвПосредн.КПП, "Т,9");
					
					КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
				Иначе
					КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СведИП");
					
					КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИННФЛ", СтрокаСведений.СвПосредн.ИННФЛ, "Т,12");
					
					КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ФИОИП");
					
					КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Фамилия", СтрокаСведений.СвПосредн.Фамилия, "Т,60");
					КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Имя", СтрокаСведений.СвПосредн.Имя, "Т,60");
					КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "Отчество", СтрокаСведений.СвПосредн.Отчество, "Т,60");
					
					КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
					
					КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
				КонецЕсли;
				
				КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
			КонецЕсли;
			
			Для Каждого НомерТД Из СтрокаСведений.НомерТД Цикл
				НомерТД = Лев(СокрЛП(НомерТД), 29);
				
				КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "НомерТД");
				
				КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, НомерТД);
			КонецЦикла;
			
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СумНДСВыч");
			
			Если ТипЗнч(СтрокаСведений.СумНДСВыч) = Тип("Число") Тогда
				СумНДСВыч = Формат(СтрокаСведений.СумНДСВыч,"ЧЦ=19; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0");
				КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СумНДС");
				КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, СумНДСВыч);
			Иначе
				СумНДСВыч = Лев(СокрЛП(СтрокаСведений.СумНДСВыч), 7);
				КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "БезНДС");
				КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, СумНДСВыч);
			КонецЕсли;
			
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
			
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		КонецЦикла;
		
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Подписант");
		
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ПрПодп", ПараметрыВыгрузки.ПрПодп);
		
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ФИО");
		
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Фамилия", ПараметрыВыгрузки.ПодпФамилия, "Т,60");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Имя", ПараметрыВыгрузки.ПодпИмя, "Т,60");
		КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "Отчество", ПараметрыВыгрузки.ПодпОтчество, "Т,60");
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		
		Если ПараметрыВыгрузки.Свойство("НаимДокПодп") И ЗначениеЗаполнено(ПараметрыВыгрузки.НаимДокПодп) Тогда
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвПред");
			
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НаимДок", ПараметрыВыгрузки.НаимДокПодп, "Т,120");
			
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		КонецЕсли;
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		
		РезультатВыгрузки = Новый Массив;
		
		СтруктураВыгрузки = Новый Структура;
		СтруктураВыгрузки.Вставить("АдресФайлаВыгрузки", КомпоновщикXML_СсылкаНаВременноеХранилище(КомпоновщикXML));
		СтруктураВыгрузки.Вставить("ИмяФайлаВыгрузки", ПараметрыВыгрузки.ИдФайл + ".xml");
		
		РезультатВыгрузки.Добавить(СтруктураВыгрузки);
		
	КонецЕсли;
	
	Возврат РезультатВыгрузки;
	
КонецФункции

Функция ЭлектронноеПредставлениеДопЛистовКнигиПокупок_502(ВыгружаемыйДокумент) Экспорт
	
	РезультатВыгрузки = Неопределено;
	
	ПараметрыВыгрузки = ОсновныеСведенияЭлектронногоПредставления(ВыгружаемыйДокумент, "1115105");
	
	СведенияДокумента = ВыгружаемыйДокумент.ДанныеОтчета.Получить();
	
	Если СведенияДокумента <> Неопределено Тогда
		
		КомпоновщикXML = КомпоновщикXML_Новый();
		
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Файл");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИдФайл", ПараметрыВыгрузки.ИдФайл);
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ВерсПрог", ПараметрыВыгрузки.ВерсПрог, "Т,40");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ВерсФорм", "5.02");
		
		Для Каждого ЛистКниги Из СведенияДокумента Цикл
			ВывестиДокументДопЛистаКнигиПокупок_502(КомпоновщикXML, ПараметрыВыгрузки, ЛистКниги);
		КонецЦикла;
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		
		РезультатВыгрузки = Новый Массив;
		
		СтруктураВыгрузки = Новый Структура;
		СтруктураВыгрузки.Вставить("АдресФайлаВыгрузки", КомпоновщикXML_СсылкаНаВременноеХранилище(КомпоновщикXML));
		СтруктураВыгрузки.Вставить("ИмяФайлаВыгрузки", ПараметрыВыгрузки.ИдФайл + ".xml");
		
		РезультатВыгрузки.Добавить(СтруктураВыгрузки);
		
	КонецЕсли;
	
	Возврат РезультатВыгрузки;
	
КонецФункции

Функция ЭлектронноеПредставлениеКнигиПродаж_501(ВыгружаемыйДокумент) Экспорт
	
	РезультатВыгрузки = Неопределено;
	
	ПараметрыВыгрузки = ОсновныеСведенияЭлектронногоПредставления(ВыгружаемыйДокумент, "1115103");
	
	КомпоновщикXML = КомпоновщикXML_Новый();
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Файл");
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance");
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИдФайл", ПараметрыВыгрузки.ИдФайл);
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ВерсПрог", ПараметрыВыгрузки.ВерсПрог, "Т,40");
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ВерсФорм", "5.01");
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Документ");
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "КНД", ПараметрыВыгрузки.КНД);
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвПродав");
	
	ВыгрузитьСведенияОбОрганизации_501(КомпоновщикXML, ПараметрыВыгрузки);
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвКнПрод");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Период", ПараметрыВыгрузки.Период);
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ОтчетГод", ПараметрыВыгрузки.ОтчетГод);
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ВсегоВсПродВклНДС", ВыгружаемыйДокумент.ВсегоПродаж, "Ч,17,2");
	
	ВывестиУзелКнигиПродаж501_Всего(КомпоновщикXML, ВыгружаемыйДокумент);
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
	Для Каждого СтрокаСведений Из ВыгружаемыйДокумент.ТабличнаяЧасть Цикл
		ВывестиУзелКнигиПродаж501_СвПродаж(КомпоновщикXML, СтрокаСведений);
	КонецЦикла;
	
	ВывестиУзел_Подписант(КомпоновщикXML, ПараметрыВыгрузки);
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
	РезультатВыгрузки = Новый Массив;
	
	СтруктураВыгрузки = Новый Структура;
	СтруктураВыгрузки.Вставить("АдресФайлаВыгрузки", КомпоновщикXML_СсылкаНаВременноеХранилище(КомпоновщикXML));
	СтруктураВыгрузки.Вставить("ИмяФайлаВыгрузки", ПараметрыВыгрузки.ИдФайл + ".xml");
	
	РезультатВыгрузки.Добавить(СтруктураВыгрузки);
	
	Возврат РезультатВыгрузки;
	
КонецФункции

Функция ЭлектронноеПредставлениеКнигиПродаж_502(ВыгружаемыйДокумент) Экспорт
	
	РезультатВыгрузки = Неопределено;
	
	ПараметрыВыгрузки = ОсновныеСведенияЭлектронногоПредставления(ВыгружаемыйДокумент, "1115103");
	
	СведенияДокумента = ВыгружаемыйДокумент.ДанныеОтчета.Получить();
	
	Если СведенияДокумента <> Неопределено Тогда
		
		КомпоновщикXML = КомпоновщикXML_Новый();
		
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Файл");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИдФайл", ПараметрыВыгрузки.ИдФайл);
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ВерсПрог", ПараметрыВыгрузки.ВерсПрог, "Т,40");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ВерсФорм", "5.02");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИдФайлИсх", ПараметрыВыгрузки.ИдФайл);
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИдФайлПерв", ПараметрыВыгрузки.ИдФайл);
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "КолФайл", 1, "Ч,5");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НомФайл", 1, "Ч,5");
		
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Документ");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "КНД", ПараметрыВыгрузки.КНД);
		
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвПродав");
		
		Если ПараметрыВыгрузки.ЭтоИП Тогда
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СведИП");
			
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИННФЛ", ПараметрыВыгрузки.ИННФЛ, "Т,12");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "СвГосРегИП", ПараметрыВыгрузки.СвГосРегИП, "Т,100");
			
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ФИОИП");
			
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Фамилия", ПараметрыВыгрузки.ФамилияИП, "Т,60");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Имя", ПараметрыВыгрузки.ИмяИП, "Т,60");
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "Отчество", ПараметрыВыгрузки.ОтчествоИП, "Т,60");
			
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
			
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
			
		Иначе
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СведЮЛ");
			
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НаимОрг", ПараметрыВыгрузки.НаимОрг, "Т,1000");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИННЮЛ", ПараметрыВыгрузки.ИННЮЛ, "Т,10");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "КПП", ПараметрыВыгрузки.КПП, "Т,9");
			
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
			
		КонецЕсли;
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвКнПрод");
		
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Период", ПараметрыВыгрузки.Период);
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ОтчетГод", ПараметрыВыгрузки.ОтчетГод);
		
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Всего");
		
		КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СтТовРубКоп18", СведенияДокумента.ОбщиеСведения.СтТовРубКоп18, "Ч,19,2");
		КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СтТовРубКоп10", СведенияДокумента.ОбщиеСведения.СтТовРубКоп10, "Ч,19,2");
		КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СтТовРубКоп0", СведенияДокумента.ОбщиеСведения.СтТовРубКоп0, "Ч,19,2");
		КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СумНДСРубКоп18", СведенияДокумента.ОбщиеСведения.СумНДСРубКоп18, "Ч,19,2");
		КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СумНДСРубКоп10", СведенияДокумента.ОбщиеСведения.СумНДСРубКоп10, "Ч,19,2");
		КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СтПродОсвНДСРубКоп", СведенияДокумента.ОбщиеСведения.СтПродОсвНДСРубКоп, "Ч,19,2");
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		
		Для Каждого СтрокаСведений Из СведенияДокумента.Записи Цикл
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвПродаж");
			
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НомПП", СтрокаСведений.НомПП, "Ч,12");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НомерСчФ", СтрокаСведений.НомерСчФ, "Т,1000");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ДатаСчФ", СтрокаСведений.ДатаСчФ, "Т,10");
			
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "НомИспрСчФ",  СтрокаСведений.НомИспрСчФ,  "Ч,3");
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаИспрСчФ", СтрокаСведений.ДатаИспрСчФ, "Т,10");
			
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "НомерКСчФ", СтрокаСведений.НомерКСчФ, "Т,1000");
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаКСчФ",  СтрокаСведений.ДатаКСчФ,  "Т,10");
			
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "НомИспрКСчФ",  СтрокаСведений.НомИспрКСчФ,  "Ч,3");
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаИспрКСчФ", СтрокаСведений.ДатаИспрКСчФ, "Т,10");
			
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НаимПок", СтрокаСведений.НаимПок, "Т,1000");
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ИННЮЛ", СтрокаСведений.ИННЮЛ, "Т,10");
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "КПП",   СтрокаСведений.КПП,   "Т,9");
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ИННФЛ", СтрокаСведений.ИННФЛ, "Т,12");
			
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "КодОКВ", СтрокаСведений.КодОКВ, "Ч,3");
			
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СтТовУчНалВсего",    СтрокаСведений.СтТовУчНалВсего,    "Ч,19,2");
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СтТовУчНалРубКоп",   СтрокаСведений.СтТовУчНалРубКоп,   "Ч,19,2");
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СтТовРубКоп18",      СтрокаСведений.СтТовРубКоп18,      "Ч,19,2");
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СтТовРубКоп10",      СтрокаСведений.СтТовРубКоп10,      "Ч,19,2");
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СтТовРубКоп0",       СтрокаСведений.СтТовРубКоп0,       "Ч,19,2");
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СумНДСРубКоп18",     СтрокаСведений.СумНДСРубКоп18,     "Ч,19,2");
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СумНДСРубКоп10",     СтрокаСведений.СумНДСРубКоп10,     "Ч,19,2");
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СтПродОсвНДСРубКоп", СтрокаСведений.СтПродОсвНДСРубКоп, "Ч,19,2");
			
			Для Каждого КодВидаОперации Из СтрокаСведений.КодВидОпер Цикл
				КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "КодВидОпер");
				
				КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, КодВидаОперации);
			КонецЦикла;
			
			Если СтрокаСведений.СвПосредн <> Неопределено Тогда
				КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвПосредн");
				
				Если СтрокаСведений.СвПосредн.Свойство("ИННЮЛ") Тогда
					КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СведЮЛ");
					
					КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НаимОрг", СтрокаСведений.СвПосредн.НаимОрг, "Т,1000");
					КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИННЮЛ", СтрокаСведений.СвПосредн.ИННЮЛ, "Т,10");
					КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "КПП", СтрокаСведений.СвПосредн.КПП, "Т,9");
					
					КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
				Иначе
					КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СведИП");
					
					КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИННФЛ", СтрокаСведений.СвПосредн.ИННФЛ, "Т,12");
					
					КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ФИОИП");
					
					КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Фамилия", СтрокаСведений.СвПосредн.Фамилия, "Т,60");
					КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Имя", СтрокаСведений.СвПосредн.Имя, "Т,60");
					КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "Отчество", СтрокаСведений.СвПосредн.Отчество, "Т,60");
					
					КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
					
					КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
				КонецЕсли;
				
				КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
			КонецЕсли;
			
			Для Каждого ДокументПодтверждения Из СтрокаСведений.СвДокПдтвОпл Цикл
				КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвДокПдтвОпл");
				
				КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НомерДокПдтвОпл", ДокументПодтверждения.НомерДокПдтвОпл, "Т,256");
				КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ДатаДокПдтвОпл", ДокументПодтверждения.ДатаДокПдтвОпл, "Т,10");
				
				КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
			КонецЦикла;
			
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		КонецЦикла;
		
		ВывестиУзел_Подписант(КомпоновщикXML, ПараметрыВыгрузки);
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		
		РезультатВыгрузки = Новый Массив;
		
		СтруктураВыгрузки = Новый Структура;
		СтруктураВыгрузки.Вставить("АдресФайлаВыгрузки", КомпоновщикXML_СсылкаНаВременноеХранилище(КомпоновщикXML));
		СтруктураВыгрузки.Вставить("ИмяФайлаВыгрузки", ПараметрыВыгрузки.ИдФайл + ".xml");
		
		РезультатВыгрузки.Добавить(СтруктураВыгрузки);
		
	КонецЕсли;
	
	Возврат РезультатВыгрузки;
	
КонецФункции

Функция ЭлектронноеПредставлениеДопЛистовКнигиПродаж_502(ВыгружаемыйДокумент) Экспорт
	
	РезультатВыгрузки = Неопределено;
	
	ПараметрыВыгрузки = ОсновныеСведенияЭлектронногоПредставления(ВыгружаемыйДокумент, "1115106");
	
	СведенияДокумента = ВыгружаемыйДокумент.ДанныеОтчета.Получить();
	
	Если СведенияДокумента <> Неопределено Тогда
		
		КомпоновщикXML = КомпоновщикXML_Новый();
		
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Файл");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИдФайл", ПараметрыВыгрузки.ИдФайл);
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ВерсПрог", ПараметрыВыгрузки.ВерсПрог, "Т,40");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ВерсФорм", "5.02");
		
		Для Каждого ЛистКниги Из СведенияДокумента Цикл
			ВывестиДокументДопЛистаКнигиПродаж_502(КомпоновщикXML, ПараметрыВыгрузки, ЛистКниги);
		КонецЦикла;
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		
		РезультатВыгрузки = Новый Массив;
		
		СтруктураВыгрузки = Новый Структура;
		СтруктураВыгрузки.Вставить("АдресФайлаВыгрузки", КомпоновщикXML_СсылкаНаВременноеХранилище(КомпоновщикXML));
		СтруктураВыгрузки.Вставить("ИмяФайлаВыгрузки", ПараметрыВыгрузки.ИдФайл + ".xml");
		
		РезультатВыгрузки.Добавить(СтруктураВыгрузки);
		
	КонецЕсли;
	
	Возврат РезультатВыгрузки;
	
КонецФункции

Функция ЭлектронноеПредставлениеЖурналаСчетовФактур_501(ВыгружаемыйДокумент) Экспорт
	
	РезультатВыгрузки = Неопределено;
	
	ПараметрыВыгрузки = ОсновныеСведенияЭлектронногоПредставления(ВыгружаемыйДокумент, "1115104");
	
	КомпоновщикXML = КомпоновщикXML_Новый();
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Файл");
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance");
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИдФайл", ПараметрыВыгрузки.ИдФайл);
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ВерсПрог", ПараметрыВыгрузки.ВерсПрог, "Т,40");
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ВерсФорм", "5.01");
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Документ");
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "КНД", ПараметрыВыгрузки.КНД);
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Период", ПараметрыВыгрузки.Период);
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ОтчетГод", ПараметрыВыгрузки.ОтчетГод);
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвНП");
	
	ВыгрузитьСведенияОбОрганизации_501(КомпоновщикXML, ПараметрыВыгрузки);
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
	Для Каждого СтрокаСведений Из ВыгружаемыйДокумент.ВыставленныеСчетаФактуры Цикл
		ВывестиУзелЖурналаСчетовФактур501_СвЧ1(КомпоновщикXML, СтрокаСведений);
	КонецЦикла;
	
	Для Каждого СтрокаСведений Из ВыгружаемыйДокумент.ПолученныеСчетаФактуры Цикл
		ВывестиУзелЖурналаСчетовФактур501_СвЧ2(КомпоновщикXML, СтрокаСведений);
	КонецЦикла;
	
	ВывестиУзел_Подписант(КомпоновщикXML, ПараметрыВыгрузки);
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
	РезультатВыгрузки = Новый Массив;
	
	СтруктураВыгрузки = Новый Структура;
	СтруктураВыгрузки.Вставить("АдресФайлаВыгрузки", КомпоновщикXML_СсылкаНаВременноеХранилище(КомпоновщикXML));
	СтруктураВыгрузки.Вставить("ИмяФайлаВыгрузки", ПараметрыВыгрузки.ИдФайл + ".xml");
	
	РезультатВыгрузки.Добавить(СтруктураВыгрузки);
	
	Возврат РезультатВыгрузки;
	
КонецФункции

Функция ЭлектронноеПредставлениеЖурналаСчетовФактур_502(ВыгружаемыйДокумент) Экспорт
	
	РезультатВыгрузки = Неопределено;
	
	ПараметрыВыгрузки = ОсновныеСведенияЭлектронногоПредставления(ВыгружаемыйДокумент, "1115104");
	
	СведенияДокумента = ВыгружаемыйДокумент.ДанныеОтчета.Получить();
	
	Если СведенияДокумента <> Неопределено Тогда
		
		КомпоновщикXML = КомпоновщикXML_Новый();
		
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Файл");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "xmlns:xsi", "http://www.w3.org/2001/XMLSchema-instance");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИдФайл", ПараметрыВыгрузки.ИдФайл);
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ВерсПрог", ПараметрыВыгрузки.ВерсПрог, "Т,40");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ВерсФорм", "5.02");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИдФайлИсх", ПараметрыВыгрузки.ИдФайл);
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИдФайлПерв", ПараметрыВыгрузки.ИдФайл);
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "КолФайл", 1, "Ч,5");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НомФайл", 1, "Ч,5");
		
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Документ");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "КНД", ПараметрыВыгрузки.КНД);
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Период", ПараметрыВыгрузки.Период);
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ОтчетГод", ПараметрыВыгрузки.ОтчетГод);
		
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвНП");
		
		Если ПараметрыВыгрузки.ЭтоИП Тогда
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СведИП");
			
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИННФЛ", ПараметрыВыгрузки.ИННФЛ, "Т,12");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "СвГосРегИП", ПараметрыВыгрузки.СвГосРегИП, "Т,100");
			
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ФИОИП");
			
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Фамилия", ПараметрыВыгрузки.ФамилияИП, "Т,60");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Имя", ПараметрыВыгрузки.ИмяИП, "Т,60");
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "Отчество", ПараметрыВыгрузки.ОтчествоИП, "Т,60");
			
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
			
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
			
		Иначе
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СведЮЛ");
			
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НаимОрг", ПараметрыВыгрузки.НаимОрг, "Т,1000");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИННЮЛ", ПараметрыВыгрузки.ИННЮЛ, "Т,10");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "КПП", ПараметрыВыгрузки.КПП, "Т,9");
			
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
			
		КонецЕсли;
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		
		Для Каждого СтрокаСведений Из СведенияДокумента.ЗаписиВыставленных Цикл
			ВывестиСвЧ1ЖурналаСчетовФактур_502(КомпоновщикXML, СтрокаСведений);
		КонецЦикла;
		
		Для Каждого СтрокаСведений Из СведенияДокумента.ЗаписиПолученных Цикл
			ВывестиСвЧ2ЖурналаСчетовФактур_502(КомпоновщикXML, СтрокаСведений);
		КонецЦикла;
		
		ВывестиУзел_Подписант(КомпоновщикXML, ПараметрыВыгрузки);
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		
		РезультатВыгрузки = Новый Массив;
		
		СтруктураВыгрузки = Новый Структура;
		СтруктураВыгрузки.Вставить("АдресФайлаВыгрузки", КомпоновщикXML_СсылкаНаВременноеХранилище(КомпоновщикXML));
		СтруктураВыгрузки.Вставить("ИмяФайлаВыгрузки", ПараметрыВыгрузки.ИдФайл + ".xml");
		
		РезультатВыгрузки.Добавить(СтруктураВыгрузки);
		
	КонецЕсли;
	
	Возврат РезультатВыгрузки;
	
КонецФункции

#КонецОбласти

#Область РеестрНДСПриложение5

Процедура ПолучитьСведенияОПоказателяхОтчетаРеестрНДСПриложение5ФормаОтчета2015Кв4(ПоказателиОтчета) Экспорт
	
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "КодОперации", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П1000001", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П1000002", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П1000003", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П1000008", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П1100004", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П1110005", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П1110006", Истина, Ложь);
	РегламентированнаяОтчетность.ВставитьПоказательВСтруктуру(ПоказателиОтчета, "П1110007", Истина, Ложь);
	
КонецПроцедуры

Процедура ЗаполнитьОтчетРеестрНДСПриложение5ФормаОтчета2015Кв4(ПараметрыОтчета, Контейнер) Экспорт

	Если ПараметрыОтчета.Свойство("ДатаНачалаРасширенногоПериодаОтчета") Тогда
		НачалоПериодаОтчета = ПараметрыОтчета.ДатаНачалаРасширенногоПериодаОтчета;
	Иначе
		НачалоПериодаОтчета = ПараметрыОтчета.мДатаНачалаПериодаОтчета;
	КонецЕсли;

	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация", ОбщегоНазначенияБПВызовСервераПовтИсп.ВсяОрганизация(ПараметрыОтчета.Организация));
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериодаОтчета);
	Запрос.УстановитьПараметр("КонецПериода", ПараметрыОтчета.мДатаКонцаПериодаОтчета);
	Запрос.УстановитьПараметр("Событие", Перечисления.СобытияПоНДСПродажи.ПодтвержденаСтавка0);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСЗаписиКнигиПродаж.Организация,
	|	НДСЗаписиКнигиПродаж.СчетФактура КАК ДокументОтгрузки,
	|	СУММА(НДСЗаписиКнигиПродаж.СуммаБезНДС) КАК ПодтвержденнаяНалоговаяБаза
	|ПОМЕСТИТЬ ПодтвержденныеОтгрузки
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж КАК НДСЗаписиКнигиПродаж
	|ГДЕ
	|	НДСЗаписиКнигиПродаж.Организация В(&Организация)
	|	И НДСЗаписиКнигиПродаж.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И НДСЗаписиКнигиПродаж.Событие = &Событие
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСЗаписиКнигиПродаж.СчетФактура,
	|	НДСЗаписиКнигиПродаж.Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОтгрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СведенияТаможенныхДекларацийЭкспорт.Организация,
	|	СведенияТаможенныхДекларацийЭкспорт.ДокументОтгрузки КАК ДокументОтгрузки,
	|	СведенияТаможенныхДекларацийЭкспорт.НомерТаможеннойДекларации
	|ПОМЕСТИТЬ ДокументыОтгрузкиИТД
	|ИЗ
	|	ПодтвержденныеОтгрузки КАК ПодтвержденныеОтгрузки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияТаможенныхДекларацийЭкспорт КАК СведенияТаможенныхДекларацийЭкспорт
	|		ПО ПодтвержденныеОтгрузки.ДокументОтгрузки = СведенияТаможенныхДекларацийЭкспорт.ДокументОтгрузки
	|			И ПодтвержденныеОтгрузки.Организация = СведенияТаможенныхДекларацийЭкспорт.Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОтгрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыОтгрузкиИТД.НомерТаможеннойДекларации КАК НомерТаможеннойДекларации,
	|	СУММА(ПодтвержденныеОтгрузки.ПодтвержденнаяНалоговаяБаза) КАК ПодтвержденнаяНалоговаяБаза
	|ПОМЕСТИТЬ НалоговаяБазаПоДекларациям
	|ИЗ
	|	ПодтвержденныеОтгрузки КАК ПодтвержденныеОтгрузки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыОтгрузкиИТД КАК ДокументыОтгрузкиИТД
	|		ПО ПодтвержденныеОтгрузки.ДокументОтгрузки = ДокументыОтгрузкиИТД.ДокументОтгрузки
	|			И ПодтвержденныеОтгрузки.Организация = ДокументыОтгрузкиИТД.Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыОтгрузкиИТД.НомерТаможеннойДекларации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерТаможеннойДекларации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СведенияТаможенныхДекларацийЭкспорт.НомерТаможеннойДекларации КАК НомерТаможеннойДекларации,
	|	СведенияТаможенныхДекларацийЭкспорт.КодОперации КАК КодОперации,
	|	СведенияТаможенныхДекларацийЭкспорт.Примечание КАК Примечание,
	|	СведенияТаможенныхДекларацийЭкспорт.КодТС КАК КодТС,
	|	СведенияТаможенныхДекларацийЭкспорт.ВидДокумента КАК ВидДокумента,
	|	СведенияТаможенныхДекларацийЭкспорт.НомерТСД КАК НомерТСД,
	|	СведенияТаможенныхДекларацийЭкспорт.ДатаТСД КАК ДатаТСД,
	|	НалоговаяБазаПоДекларациям.ПодтвержденнаяНалоговаяБаза КАК ПодтвержденнаяНалоговаяБаза
	|ИЗ
	|	РегистрСведений.СведенияТаможенныхДекларацийЭкспорт КАК СведенияТаможенныхДекларацийЭкспорт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НалоговаяБазаПоДекларациям КАК НалоговаяБазаПоДекларациям
	|		ПО СведенияТаможенныхДекларацийЭкспорт.НомерТаможеннойДекларации = НалоговаяБазаПоДекларациям.НомерТаможеннойДекларации
	|ИТОГИ
	|	МАКСИМУМ(Примечание),
	|	МАКСИМУМ(ПодтвержденнаяНалоговаяБаза)
	|ПО
	|	КодОперации,
	|	НомерТаможеннойДекларации,
	|	КодТС";
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не Результат.Пустой() Тогда
		
		Реестр = Контейнер.Реестр;
		Реестр.Строки.Очистить();
		
		ВыборкаКодОперации = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаКодОперации.Следующий() Цикл
			
			НоваяСтраница = Реестр.Строки.Добавить();
			НоваяСтраница.Данные = Новый Структура("КодОперации, НалБазаИтого", ВыборкаКодОперации.КодОперации, 0);
			НоваяСтраница.ДанныеМногострочныхЧастей = Новый Структура("П10000", ДеревоЗначенийРеестрНДСПриложение5());
			
			Сч = 1;
			
			ВыборкаДекларация = ВыборкаКодОперации.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаДекларация.Следующий() Цикл
				
				Если Сч > ПараметрыОтчета.МаксКолСтрокНаСтранице Тогда
					
					НоваяСтраница = Реестр.Строки.Добавить();
					НоваяСтраница.Данные = Новый Структура("КодОперации, НалБазаИтого", ВыборкаКодОперации.КодОперации, 0);
					НоваяСтраница.ДанныеМногострочныхЧастей = Новый Структура("П10000", ДеревоЗначенийРеестрНДСПриложение5());
					
					Сч = 1;
					
				КонецЕсли;
				
				НоваяДекларация = НоваяСтраница.ДанныеМногострочныхЧастей.П10000.Строки.Добавить();
				НоваяДекларация.Данные = Новый Структура("П1000001, П1000002, П1000003, П1000008", 
					0, ВыборкаДекларация.НомерТаможеннойДекларации, ВыборкаДекларация.ПодтвержденнаяНалоговаяБаза, ВыборкаДекларация.Примечание);
				НоваяДекларация.ДанныеМногострочныхЧастей = Новый Структура("П11000", ДеревоЗначенийРеестрНДСПриложение5());
				
				ВыборкаКодТС = ВыборкаДекларация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаКодТС.Следующий() Цикл
					
					НовыйКодТС = НоваяДекларация.ДанныеМногострочныхЧастей.П11000.Строки.Добавить();
					НовыйКодТС.Данные = Новый Структура("П1100004", ВыборкаКодТС.КодТС);
					НовыйКодТС.ДанныеМногострочныхЧастей = Новый Структура("П11100", ДеревоЗначенийРеестрНДСПриложение5());
					
					ВыборкаДокумент = ВыборкаКодТС.Выбрать();
					Пока ВыборкаДокумент.Следующий() Цикл
						
						НовыйДокумент = НовыйКодТС.ДанныеМногострочныхЧастей.П11100.Строки.Добавить();
						НовыйДокумент.Данные = Новый Структура("П1110005, П1110006, П1110007", 
							ВыборкаДокумент.ВидДокумента, ВыборкаДокумент.НомерТСД, ВыборкаДокумент.ДатаТСД);
						
					КонецЦикла;
					
				КонецЦикла;
				
				Сч = Сч + 1;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ДекларацияНДС

// Заполняет контейнер с показателями декларации по НДС.
//
Процедура ЗаполнитьДекларациюНДС(ПараметрыОтчета, Контейнер)

#Область ПодготовкаОбщихДанных

	Перем ТаблицаРасшифровки;

	Если ПараметрыОтчета.Свойство("Декларация") И ЗначениеЗаполнено(ПараметрыОтчета.Декларация) Тогда
		СсылкаНаДекларацию = ПараметрыОтчета.Декларация;
		РегламентированнаяОтчетность.УдалитьДополнительныеФайлыДекларацииНДС(СсылкаНаДекларацию);
	Иначе
		Возврат;
	КонецЕсли;

	Если ПараметрыОтчета.Свойство("ДатаНачалаРасширенногоПериодаОтчета") Тогда
		НачалоПериодаОтчета = ПараметрыОтчета.ДатаНачалаРасширенногоПериодаОтчета;
	Иначе
		НачалоПериодаОтчета = ПараметрыОтчета.мДатаНачалаПериодаОтчета;
	КонецЕсли;

	ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации(),
		УровеньЖурналаРегистрации.Информация,,, НСтр("ru = 'Формирование разделов 1-7'"));
	
	ОрганизацияНеПлательшикНДС = УчетнаяПолитика.СистемаНалогообложения(
		ПараметрыОтчета.Организация, ПараметрыОтчета.мДатаКонцаПериодаОтчета) 
		<> Перечисления.СистемыНалогообложения.Общая;
	ОрганизацияОсвобожденаОтУплатыНДС = УчетнаяПолитика.ПрименяетсяОсвобождениеОтУплатыНДС(
		ПараметрыОтчета.Организация, ПараметрыОтчета.мДатаКонцаПериодаОтчета);
	ОрганизацияВедетРаздельныйУчетНДС = Истина;
		
	Если ПараметрыОтчета.Свойство("ГруппаОрганизаций") Тогда
		СписокОрганизаций = ПараметрыОтчета.ГруппаОрганизаций;
	Иначе
		СписокОрганизаций = Новый СписокЗначений();
	КонецЕсли;
		
	СписокОрганизаций.Добавить(ПараметрыОтчета.Организация);

	РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ПараметрыОтчета.Организация, "ИностраннаяОрганизация,КПП");
	
	ОбщиеПараметрыЗапросов = Новый Структура;
	ОбщиеПараметрыЗапросов.Вставить("Организация", СписокОрганизаций);
	ОбщиеПараметрыЗапросов.Вставить("НачалоПериода", НачалоДня(НачалоПериодаОтчета));
	ОбщиеПараметрыЗапросов.Вставить("КонецПериода",  КонецДня(ПараметрыОтчета.мДатаКонцаПериодаОтчета));
	ОбщиеПараметрыЗапросов.Вставить("КонецПериодаУчитываемыхКорректировок", 
		?(НЕ ЗначениеЗаполнено(ПараметрыОтчета.ДатаПодписи),
		ПараметрыОтчета.ДатаПодписи, КонецДня(ПараметрыОтчета.ДатаПодписи)));
	ОбщиеПараметрыЗапросов.Вставить("СУчетомКорректировок", НЕ ПараметрыОтчета.ВидДокумента = 0);
	
	Запрос = Новый Запрос;
	УстановитьОбщиеПараметрыЗапроса(Запрос, ОбщиеПараметрыЗапросов);
	
	Запрос.УстановитьПараметр("ВесьОборотЗаПериод", 
		?(ПараметрыОтчета.Свойство("ВесьОборотЗаПериод"),ПараметрыОтчета.ВесьОборотЗаПериод, Ложь));
	Запрос.УстановитьПараметр("КППИностраннойОрганизации", 
		?(РеквизитыОрганизации.ИностраннаяОрганизация, РеквизитыОрганизации.КПП, ""));
	
	// События
	СобытиеРеализация = Новый СписокЗначений;
	СобытиеРеализация.Добавить(Перечисления.СобытияПоНДСПродажи.Реализация);
	СобытиеРеализация.Добавить(Перечисления.СобытияПоНДСПродажи.Оплата);
	// Суммы связанные с расчетами по оплате включаются в общий объем реализации.
	СобытиеРеализация.Добавить(Перечисления.СобытияПоНДСПродажи.НДСНачисленКУплате);
	СобытиеРеализация.Добавить(Перечисления.СобытияПоНДСПродажи.НДССкорректирован);
	Запрос.УстановитьПараметр("СобытиеРеализация", СобытиеРеализация);
	
	СобытиеРеализацияРаздел2 = Новый СписокЗначений;
	СобытиеРеализацияРаздел2.Добавить(Перечисления.СобытияПоНДСПродажи.Реализация);
	СобытиеРеализацияРаздел2.Добавить(Перечисления.СобытияПоНДСПродажи.Оплата);
	Запрос.УстановитьПараметр("СобытиеРеализацияРаздел2", СобытиеРеализацияРаздел2);

	СобытиеПолученАванс = Новый СписокЗначений;
	СобытиеПолученАванс.Добавить(Перечисления.СобытияПоНДСПродажи.ПолученАванс);
	Запрос.УстановитьПараметр("СобытиеПолученАванс", СобытиеПолученАванс);
	
	СобытиеНачисленНДС = Новый СписокЗначений;
	СобытиеНачисленНДС.Добавить(Перечисления.СобытияПоНДСПродажи.НДСНачисленКУплате);
	СобытиеНачисленНДС.Добавить(Перечисления.СобытияПоНДСПродажи.НДССкорректирован);
	Запрос.УстановитьПараметр("СобытиеНачисленНДС", СобытиеНачисленНДС);
	
	СобытиеВосстановлениеНДС = Новый СписокЗначений;
	СобытиеВосстановлениеНДС.Добавить(Перечисления.СобытияПоНДСПродажи.ВосстановлениеНДС);
	Запрос.УстановитьПараметр("СобытиеВосстановлениеНДС", СобытиеВосстановлениеНДС);
	
	СобытиеПредъявленКВычетуНДС = Новый СписокЗначений;
	СобытиеПредъявленКВычетуНДС.Добавить(Перечисления.СобытияПоНДСПокупки.ПредъявленНДСКВычету);
	СобытиеПредъявленКВычетуНДС.Добавить(Перечисления.СобытияПоНДСПокупки.ВосстановленНДС);
	Запрос.УстановитьПараметр("СобытиеПредъявленКВычетуНДС", СобытиеПредъявленКВычетуНДС);
	
	СобытиеПодтвержденаСтавка0 = Новый СписокЗначений;
	СобытиеПодтвержденаСтавка0.Добавить(Перечисления.СобытияПоНДСПродажи.ПодтвержденаСтавка0);
	Запрос.УстановитьПараметр("СобытиеПодтвержденаСтавка0", СобытиеПодтвержденаСтавка0);
	
	СобытиеПредъявленКВычетуНДС0 = Новый СписокЗначений;
	СобытиеПредъявленКВычетуНДС0.Добавить(Перечисления.СобытияПоНДСПокупки.ПредъявленНДСКВычету0);
	Запрос.УстановитьПараметр("СобытиеПредъявленКВычетуНДС0", СобытиеПредъявленКВычетуНДС0);
	
	СобытиеНеПодтвержденаСтавка0 = Новый СписокЗначений;
	СобытиеНеПодтвержденаСтавка0.Добавить(Перечисления.СобытияПоНДСПродажи.НеПодтвержденаСтавка0);
	Запрос.УстановитьПараметр("СобытиеНеПодтвержденаСтавка0", СобытиеНеПодтвержденаСтавка0);

	СобытиеПредъявленКВычетуНеПодтвержденНДС0 = Новый СписокЗначений;
	СобытиеПредъявленКВычетуНеПодтвержденНДС0.Добавить(Перечисления.СобытияПоНДСПокупки.НеПодтвержденаСтавка0);
	Запрос.УстановитьПараметр("СобытиеПредъявленКВычетуНеПодтвержденНДС0", СобытиеПредъявленКВычетуНеПодтвержденНДС0);
	
	// Виды ценностей
	ВидыЦенностейНеРеализация = Новый СписокЗначений;
	ВидыЦенностейНеРеализация.Добавить(Перечисления.ВидыЦенностей.СМРСобственнымиСилами);
	ВидыЦенностейНеРеализация.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные);
	ВидыЦенностейНеРеализация.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентАренда);
	ВидыЦенностейНеРеализация.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентРеализацияИмущества);
	ВидыЦенностейНеРеализация.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентИностранцы);
	ВидыЦенностейНеРеализация.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентКомитент);
	
	ВидыЦенностейСтр010_040 = Новый СписокЗначений;
	Для Каждого ВидЦенности Из Перечисления.ВидыЦенностей Цикл
		Если ВидыЦенностейНеРеализация.НайтиПоЗначению(ВидЦенности) = Неопределено Тогда
			ВидыЦенностейСтр010_040.Добавить(ВидЦенности);
		КонецЕсли;
	КонецЦикла;
	Запрос.УстановитьПараметр("ВидыЦенностейСтр010_040", ВидыЦенностейСтр010_040);
	
	ВидыЦенностейСтр060 = Новый СписокЗначений;
	ВидыЦенностейСтр060.Добавить(Перечисления.ВидыЦенностей.СМРСобственнымиСилами);
	Запрос.УстановитьПараметр("ВидыЦенностейСтр060", ВидыЦенностейСтр060);
	
	ВидыЦенностейСтр070 = Новый СписокЗначений;
	ВидыЦенностейСтр070.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные);
	Запрос.УстановитьПараметр("ВидыЦенностейСтр070", ВидыЦенностейСтр070);
	
	ВидыЦенностейСтр090 = Новый СписокЗначений;
	ВидыЦенностейСтр090.Добавить(Перечисления.ВидыЦенностей.АвансыВыданные);
	Запрос.УстановитьПараметр("ВидыЦенностейСтр090", ВидыЦенностейСтр090);
	
	ВидыЦенностейСтр130 =  Новый СписокЗначений;
	ВидыЦенностейСтр130.Добавить(Перечисления.ВидыЦенностей.АвансыВыданные);
	Запрос.УстановитьПараметр("ВидыЦенностейСтр130", ВидыЦенностейСтр130);
	
	ВидыЦенностейСтр140 =  Новый СписокЗначений;
	ВидыЦенностейСтр140.Добавить(Перечисления.ВидыЦенностей.СМРСобственнымиСилами);
	Запрос.УстановитьПараметр("ВидыЦенностейСтр140", ВидыЦенностейСтр140);
	
	ВидыЦенностейСтр150 =  Новый СписокЗначений;
	ВидыЦенностейСтр150.Добавить(Перечисления.ВидыЦенностей.ТаможенныеПлатежи);
	ВидыЦенностейСтр150.Добавить(Перечисления.ВидыЦенностей.ТаможенныеПлатежиОС);
	Запрос.УстановитьПараметр("ВидыЦенностейСтр150", ВидыЦенностейСтр150);
	
	ВидыЦенностейСтр160 =  Новый СписокЗначений;
	ВидыЦенностейСтр160.Добавить(Перечисления.ВидыЦенностей.ТаможенныйСоюз);
	Запрос.УстановитьПараметр("ВидыЦенностейСтр160", ВидыЦенностейСтр160);
	
	ВидыЦенностейСтр170 =  Новый СписокЗначений;
	ВидыЦенностейСтр170.Добавить(Перечисления.ВидыЦенностей.АвансыПолученные);
	Запрос.УстановитьПараметр("ВидыЦенностейСтр170", ВидыЦенностейСтр170);
	
	ВидыЦенностейСтр180 =  Новый СписокЗначений;
	ВидыЦенностейСтр180.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентАренда);
	ВидыЦенностейСтр180.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентИностранцы);
	ВидыЦенностейСтр180.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентРеализацияИмущества);
	Запрос.УстановитьПараметр("ВидыЦенностейСтр180", ВидыЦенностейСтр180);
	
	ВидыЦенностейСтр120 = Новый СписокЗначений;
	Для Каждого ВидЦенности Из Перечисления.ВидыЦенностей Цикл
		Если ВидыЦенностейСтр130.НайтиПоЗначению(ВидЦенности) = Неопределено 
			И ВидыЦенностейСтр140.НайтиПоЗначению(ВидЦенности) = Неопределено 
			И ВидыЦенностейСтр150.НайтиПоЗначению(ВидЦенности) = Неопределено 
			И ВидыЦенностейСтр160.НайтиПоЗначению(ВидЦенности) = Неопределено
			И ВидыЦенностейСтр170.НайтиПоЗначению(ВидЦенности) = Неопределено 
			И ВидыЦенностейСтр180.НайтиПоЗначению(ВидЦенности) = Неопределено Тогда
			ВидыЦенностейСтр120.Добавить(ВидЦенности);
		КонецЕсли;
	КонецЦикла;
	Запрос.УстановитьПараметр("ВидыЦенностейСтр120", ВидыЦенностейСтр120);
	
	ВидыЦенностей_1011703 =  Новый СписокЗначений;
	ВидыЦенностей_1011703.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентАренда);
	ВидыЦенностей_1011703.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентРеализацияИмущества);
	
	Запрос.УстановитьПараметр("ВидыЦенностей_1011712", Перечисления.ВидыЦенностей.НалоговыйАгентИностранцы);
	Запрос.УстановитьПараметр("ВидыЦенностей_1011703", ВидыЦенностей_1011703);
	Запрос.УстановитьПараметр("ВидыЦенностей_1011707", Перечисления.ВидыЦенностей.НалоговыйАгентКомитент);
	
	ВидыЦенностиНА = Новый СписокЗначений;
	ВидыЦенностиНА.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентАренда);
	ВидыЦенностиНА.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентРеализацияИмущества);
	ВидыЦенностиНА.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентИностранцы);
	ВидыЦенностиНА.Добавить(Перечисления.ВидыЦенностей.НалоговыйАгентКомитент);
	Запрос.УстановитьПараметр("ВидыЦенностейНА", ВидыЦенностиНА);
	
	// Ставки НДС
	Запрос.УстановитьПараметр("СтавкаНДС10",     Перечисления.СтавкиНДС.НДС10);
	Запрос.УстановитьПараметр("СтавкаНДС10_110", Перечисления.СтавкиНДС.НДС10_110);
	Запрос.УстановитьПараметр("СтавкаНДС18",     Перечисления.СтавкиНДС.НДС18);
	Запрос.УстановитьПараметр("СтавкаНДС18_118", Перечисления.СтавкиНДС.НДС18_118);
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапросаОбщиеДанные();
	Запрос.Выполнить();
	
	ПолеКомпоновкиДанныхСобытие         = Новый ПолеКомпоновкиДанных("Событие");
	ПолеКомпоновкиДанныхВидЦенности     = Новый ПолеКомпоновкиДанных("ВидЦенности");
	ПолеКомпоновкиДанныхСтавкаНДС       = Новый ПолеКомпоновкиДанных("СтавкаНДС");
	ПолеКомпоновкиДанныхКодВидаОперации = Новый ПолеКомпоновкиДанных("КодВидаОперации");
	
	Если ПараметрыОтчета.ПравилаЗаполнения = "2015Кв1" Тогда
		МакетыРасшифровкиПолей = МакетыРасшифровкиПолей2015Кв1(
			ПараметрыОтчета.Организация, ПараметрыОтчета.мДатаКонцаПериодаОтчета);
	ИначеЕсли ПараметрыОтчета.ПравилаЗаполнения = "2017Кв1" Тогда
		МакетыРасшифровкиПолей = МакетыРасшифровкиПолей2017Кв1(
			ПараметрыОтчета.Организация, ПараметрыОтчета.мДатаКонцаПериодаОтчета);
	КонецЕсли;
	
#КонецОбласти

#Область Раздел2

	Если Контейнер.Свойство("Раздел2") Тогда
		
		Раздел2 = Контейнер.Раздел2;
		
		Запрос.Текст = ТекстЗапросаРаздел2();
		
		РезультатЗапроса = Запрос.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();
		
		СтраницаРаздел2 = Раздел2[0].Данные;
		
		Если СтраницаРаздел2.П000200005003 = Неопределено Тогда
			СтраницаРаздел2.П000200005003 = "";
			ОКАТО = "";
		Иначе
			ОКАТО = СтраницаРаздел2.П000200005003;
		КонецЕсли;
		
		Для Каждого Показатель Из СтраницаРаздел2 Цикл
			СтраницаРаздел2[Показатель.Ключ] = ОбщегоНазначенияБПКлиентСервер.ПустоеЗначениеТипа(ТипЗнч(Показатель.Значение));
		КонецЦикла;
		
		Пока Раздел2.Количество() > 1 Цикл
			Раздел2.Удалить(1);
		КонецЦикла;
		
		ПерваяСтраница = Истина;
		
		Пока Выборка.Следующий() Цикл
			
			Если НЕ ПерваяСтраница Тогда
				// Создаем дополнительную страницу в декларации
				СтруктураНовойТаблицы = Новый Структура();
				Для Каждого Показатель Из СтраницаРаздел2 Цикл
					СтруктураНовойТаблицы.Вставить(
						Показатель.Ключ, ОбщегоНазначенияБПКлиентСервер.ПустоеЗначениеТипа(ТипЗнч(Показатель.Значение)));
				КонецЦикла;
				
				НоваяСтраницаРаздела2 = Раздел2.Добавить();
				НоваяСтраницаРаздела2.Данные = СтруктураНовойТаблицы;
				
			Иначе
				// На последующих выполнениях цикла нужно будет создавать дополнительные страницы
				ПерваяСтраница = Ложь;
			КонецЕсли;
			
			СтраницаРаздел2 = Раздел2[Раздел2.Количество()-1].Данные;
			
			ДанныеЛистаРаздел2ФИО = (Выборка.ЮрФизЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо);
			
			СтраницаРаздел2.П000200001003 = Выборка.П000200001003;
			СтраницаРаздел2.П000200002003 = Выборка.П000200002003;
			СтраницаРаздел2.П000200003003 = Выборка.П000200003003;
			СтраницаРаздел2.П000200005003 = ОКАТО;
			СтраницаРаздел2.П000200006003 = Выборка.П000200006003;
			
			#Область Расшифровка_П000200006003
			
			ДополнительныеПараметрыИОтбор = НовыйДополнительныеПараметрыРасшифровкиИОтбор(
				МакетыРасшифровкиПолей.Получить("П000200006003"));
			
			ПолеКомпоновкиДанныхКонтрагент = Новый ПолеКомпоновкиДанных("Контрагент");
			
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхКонтрагент,
				ВидСравненияКомпоновкиДанных.Равно,
				Выборка.П000200002003_Расшифровка);
			
			СписокСобытий = Новый СписокЗначений;
			
			Для Каждого Событие Из СобытиеНачисленНДС Цикл
				СписокСобытий.Добавить(Событие.Значение);
			КонецЦикла;
			
			Для Каждого Событие Из СобытиеПолученАванс Цикл
				СписокСобытий.Добавить(Событие.Значение);
			КонецЦикла;
			
			Для Каждого Событие Из СобытиеРеализация Цикл
				СписокСобытий.Добавить(Событие.Значение);
			КонецЦикла;
			
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхСобытие,
				ВидСравненияКомпоновкиДанных.ВСписке,
				СписокСобытий);
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхВидЦенности,
				ВидСравненияКомпоновкиДанных.ВСписке,
				ВидыЦенностиНА);
			
			ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
				ТаблицаРасшифровки,
				"П000200006003_" + Раздел2.Количество(),
				"Сумма налога, подлежащая уплате в бюджет",
				"",
				"",
				СтраницаРаздел2.П000200006003,
				"Раздел2",
				ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
			
			#КонецОбласти
			
			СтраницаРаздел2.П000200007003 = Выборка.П000200007003;
			СтраницаРаздел2.П000200008003 = Выборка.П000200008003;
			
			#Область Расшифровка_П000200008003
			
			ДополнительныеПараметрыИОтбор = НовыйДополнительныеПараметрыРасшифровкиИОтбор(
				МакетыРасшифровкиПолей.Получить("П000200008003"));
			
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхКонтрагент,
				ВидСравненияКомпоновкиДанных.Равно,
				Выборка.П000200002003_Расшифровка);
			
			ГруппаИЛИ = ДополнительныеПараметрыИОтбор.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
			ГруппаИЛИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
			
			ДобавитьЗначениеВОтбор(
				ГруппаИЛИ, ПолеКомпоновкиДанныхСобытие, ВидСравненияКомпоновкиДанных.ВСписке, СобытиеРеализация);
			
			//ИЛИ
			ГруппаИ = ГруппаИЛИ.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
			ГруппаИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
			
			ДобавитьЗначениеВОтбор(
				ГруппаИ,
				ПолеКомпоновкиДанныхСобытие,
				ВидСравненияКомпоновкиДанных.ВСписке,
				СобытиеНачисленНДС);
			//И
			ДобавитьЗначениеВОтбор(ГруппаИ,
				ПолеКомпоновкиДанныхВидЦенности,
				ВидСравненияКомпоновкиДанных.Равно,
				Перечисления.ВидыЦенностей.НалоговыйАгентКомитент);
			
			НаимПоказателя = "Сумма налога, исчисленная при отгрузке товаров 
				|(выполнении работ, оказании услуг, передаче имущественных прав)";
			
			ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
				ТаблицаРасшифровки,
				"П000200008003_" + Раздел2.Количество(),
				НаимПоказателя,
				"",
				"",
				СтраницаРаздел2.П000200008003,
				"Раздел2",
				ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
			
			#КонецОбласти
			
			СтраницаРаздел2.П000200009003 = Выборка.П000200009003;
			
			#Область Расшифровка_П000200009003
			
			ДополнительныеПараметрыИОтбор = НовыйДополнительныеПараметрыРасшифровкиИОтбор(
				МакетыРасшифровкиПолей.Получить("П000200009003"));
			
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхКонтрагент,
				ВидСравненияКомпоновкиДанных.Равно,
				Выборка.П000200002003_Расшифровка);
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхСобытие,
				ВидСравненияКомпоновкиДанных.ВСписке,
				СобытиеПолученАванс);
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхВидЦенности,
				ВидСравненияКомпоновкиДанных.ВСписке,
				ВидыЦенностиНА);
			
			НаимПоказателя = "Сумма налога, исчисленная при получении оплаты, частичной оплаты в счет предстоящей
				|отгрузки товаров (выполнения работ, оказания услуг, передачи имущественных прав)";
			ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
				ТаблицаРасшифровки,
				"П000200009003_" + Раздел2.Количество(),
				НаимПоказателя,
				"",
				"",
				СтраницаРаздел2.П000200009003,
				"Раздел2",
				ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
				
			#КонецОбласти
			
			Если ДанныеЛистаРаздел2ФИО Тогда
				СтраницаРаздел2.П000200001003 = "";
				// Если в строке есть запятые, то удалим их, т.к. они являются разделителями.
				П000200002003 = СтрЗаменить(Выборка.П000200002003, ",", "");
				ФИО = ФизическиеЛицаКлиентСервер.ЧастиИмени(П000200002003);
				ФамилияРаздел2  = СокрЛП(ФИО.Фамилия);
				ИмяРаздел2      = СокрЛП(ФИО.Имя);
				ОтчествоРаздел2 = СокрЛП(ФИО.Отчество);
				
				// Покажем в разделе, разбитое значение по частям, в случае, если что-то ошибочное, необходимо поправить в отчете.
				П000200002003 = СокрЛП(ФамилияРаздел2 + " " + ИмяРаздел2 + " " + ОтчествоРаздел2);
			Иначе
				ФамилияРаздел2 = "";
				ИмяРаздел2 = "";
				ОтчествоРаздел2 = "";
			КонецЕсли;
			
		КонецЦикла; 
	КонецЕсли;
	
#КонецОбласти

#Область Раздел3

	Если Контейнер.Свойство("Раздел3")
		И НЕ ОрганизацияНеПлательшикНДС
		И НЕ ОрганизацияОсвобожденаОтУплатыНДС Тогда
		
		Раздел3 = Контейнер.Раздел3;
		
		Запрос.Текст = ТекстЗапросаРаздел3Начисления();
		
		ЗначенияДляЗаполнения = Запрос.Выполнить().Выбрать();
		
		Если ЗначенияДляЗаполнения.Следующий() Тогда
			
			ЗаполнитьЗначенияСвойств(Раздел3, ЗначенияДляЗаполнения);
			
			#Область НаименованиеПоказателейРасшифровки
			
			НаимПоказателя010_040 = НСтр("ru='1. Реализация (передача на территории Российской Федерации для собственных нужд)
					|товаров (работ, услуг), передача имущественных прав по соответствующим ставкам налога,
					|а также суммы, связанные с расчетами по оплате налогооблагаемых товаров (работ, услуг), всего'");
			
			Если ПараметрыОтчета.ПравилаЗаполнения = "2017Кв1" Тогда
				НаимПоказателя060 = НСтр("ru='4. Выполнение строительно-монтажных работ для собственного потребления'");
				НаимПоказателя070 = НСтр("ru='5. Суммы полученной оплаты, частичной оплаты в счет предстоящих
					|поставок товаров (выполнения работ, оказания услуг), передачи имущественных прав'");
				НаимПоказателя080 = НСтр("ru='6. Суммы налога, подлежащие восстановлению, всего'");
				НаимПоказателя090 = НСтр("ru='6.1. суммы налога, подлежащие восстановлению в соответствии
					|с подпунктом 3 пункта 3 статьи 170 Налогового кодекса Российской Федерации'");
				НаимПоказателя120 = НСтр("ru='12. Сумма налога, предъявленная налогоплательщику при приобретении товаров 
					|(работ, услуг), имущественных прав на территории Российской Федерации, подлежащая вычету в соответствии с
					|пунктами 2, 4, 13 статьи 171 Налогового кодекса Российской Федерации, а также сумма налога,
					|подлежащая вычету в соответствии с пунктом 5 статьи 171 Налогового кодекса Российской Федерации'");
				НаимПоказателя130 = НСтр("ru='13. Сумма налога, предъявленная налогоплательщику - покупателю 
					|при перечислении суммы оплаты, частичной оплаты в счет предстоящих поставок товаров (выполнения работ, 
					|оказания услуг), передачи имущественных прав, подлежащая вычету у покупателя'");
				НаимПоказателя140 = НСтр("ru='14. Сумма налога, исчисленная при выполнении строительно-монтажных работ для
					|собственного потребления, подлежащая вычету'");
				НаимПоказателя150 = НСтр("ru='15. Сумма налога, уплаченная налогоплательщиком таможенным органам при ввозе товаров
					|на территорию Российской Федерации и иные территории, находящиеся под её юрисдикцией,
					|в таможенных процедурах выпуска для внутреннего потребления, переработки для внутреннего потребления,
					|временного ввоза и переработки вне таможенной территории'");
				НаимПоказателя160 = НСтр("ru='16. Сумма налога, уплаченная налогоплательщиком налоговым органам при ввозе товаров
					|на территорию Российской Федерации и иные территории, находящиеся под её юрисдикцией,
					|с территории государств - членов Евразийского экономического союза'");
				НаимПоказателя170 = НСтр("ru='17. Сумма налога, исчисленная продавцом с сумм оплаты, частичной оплаты, 
					|подлежащая вычету у продавца с даты отгрузки соответствующих товаров (выполнения работ, оказания услуг)'");
				НаимПоказателя180 = НСтр("ru='18. Сумма налога, уплаченная в бюджет налогоплательщиком 
					|в качестве покупателя - налогового агента, подлежащая вычету'");
			ИначеЕсли ПараметрыОтчета.ПравилаЗаполнения = "2015Кв1" Тогда
				НаимПоказателя060 = НСтр("ru='3. Выполнение строительно-монтажных работ для собственного потребления'");
				НаимПоказателя070 = НСтр("ru='4. Суммы полученной оплаты, частичной оплаты в счет предстоящих
					|поставок товаров (выполнения работ, оказания услуг), передачи имущественных прав'");
				НаимПоказателя080 = НСтр("ru='5. Суммы налога, подлежащие восстановлению, всего'");
				НаимПоказателя090 = НСтр("ru='5.1. суммы налога, подлежащие восстановлению в соответствии
					|с подпунктом 3 пункта 3 статьи 170 Налогового кодекса Российской Федерации'");
				НаимПоказателя120 = НСтр("ru='9. Сумма налога, предъявленная налогоплательщику при приобретении товаров 
					|(работ, услуг), имущественных прав на территории Российской Федерации, подлежащая вычету в соответствии с
					|пунктами 2, 4, 13 статьи 171 Налогового кодекса Российской Федерации, а также сумма налога,
					|подлежащая вычету в соответствии с пунктом 5 статьи 171 Налогового кодекса Российской Федерации'");
				НаимПоказателя130 = НСтр("ru='10. Сумма налога, предъявленная налогоплательщику - покупателю 
					|при перечислении суммы оплаты, частичной оплаты в счет предстоящих поставок товаров (выполнения работ, 
					|оказания услуг), передачи имущественных прав, подлежащая вычету у покупателя'");
				НаимПоказателя140 = НСтр("ru='11. Сумма налога, исчисленная при выполнении строительно-монтажных работ для
					|собственного потребления, подлежащая вычету'");
				НаимПоказателя150 = НСтр("ru='12. Сумма налога, уплаченная налогоплательщиком таможенным органам при ввозе товаров
					|на территорию Российской Федерации и иные территории, находящиеся под её юрисдикцией,
					|в таможенных процедурах выпуска для внутреннего потребления, переработки для внутреннего потребления,
					|временного ввоза и переработки вне таможенной территории'");
				НаимПоказателя160 = НСтр("ru='13. Сумма налога, уплаченная налогоплательщиком налоговым органам при ввозе товаров
					|на территорию Российской Федерации и иные территории, находящиеся под её юрисдикцией,
					|с территории государств - членов Таможенного союза'");
				НаимПоказателя170 = НСтр("ru='14. Сумма налога, исчисленная продавцом с сумм оплаты, частичной оплаты, 
					|подлежащая вычету у продавца с даты отгрузки соответствующих товаров (выполнения работ, оказания услуг)'");
				НаимПоказателя180 = НСтр("ru='15. Сумма налога, уплаченная в бюджет налогоплательщиком 
					|в качестве покупателя - налогового агента, подлежащая вычету'");
			КонецЕсли;

			#КонецОбласти
			
			#Область Расшифровка_П000300001003_и_П000300001005
			
			ДополнительныеПараметрыИОтбор = НовыйДополнительныеПараметрыРасшифровкиИОтбор(
				МакетыРасшифровкиПолей.Получить("П000300001003"));
			
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхСобытие,
				ВидСравненияКомпоновкиДанных.ВСписке,
				СобытиеРеализация);
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхВидЦенности,
				ВидСравненияКомпоновкиДанных.ВСписке,
				ВидыЦенностейСтр010_040);
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхСтавкаНДС,
				ВидСравненияКомпоновкиДанных.Равно,
				Перечисления.СтавкиНДС.НДС18);
			
			ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
				ТаблицаРасшифровки,
				"П000300001003",
				НаимПоказателя010_040,
				"",
				"",
				Раздел3.П000300001003,
				"Раздел3",
				ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
			ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
				ТаблицаРасшифровки,
				"П000300001005",
				НаимПоказателя010_040,
				"",
				"",
				Раздел3.П000300001005,
				"Раздел3",
				ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
			
			#КонецОбласти
			
			#Область Расшифровка_П000300002003_и_П000300002005
			
			ДополнительныеПараметрыИОтбор = НовыйДополнительныеПараметрыРасшифровкиИОтбор(
				МакетыРасшифровкиПолей.Получить("П000300002003"));
			
			ДобавитьЗначениеВОтбор(ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхСобытие,
				ВидСравненияКомпоновкиДанных.ВСписке,
				СобытиеРеализация);
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхВидЦенности, ВидСравненияКомпоновкиДанных.ВСписке,
				ВидыЦенностейСтр010_040);
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхСтавкаНДС, ВидСравненияКомпоновкиДанных.Равно,
				Перечисления.СтавкиНДС.НДС10);
			
			ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
				ТаблицаРасшифровки,
				"П000300002003",
				НаимПоказателя010_040,
				"",
				"",
				Раздел3.П000300002003,
				"Раздел3",
				ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
			ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
				ТаблицаРасшифровки,
				"П000300002005",
				НаимПоказателя010_040,
				"",
				"",
				Раздел3.П000300002005,
				"Раздел3",
				ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
			
			#КонецОбласти
			
			#Область Расшифровка_П000300003003_и_П000300003005
			
			ДополнительныеПараметрыИОтбор = НовыйДополнительныеПараметрыРасшифровкиИОтбор(
				МакетыРасшифровкиПолей.Получить("П000300003003"));
			
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхСобытие,
				ВидСравненияКомпоновкиДанных.ВСписке,
				СобытиеРеализация);
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхВидЦенности,
				ВидСравненияКомпоновкиДанных.ВСписке,
				ВидыЦенностейСтр010_040);
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхСтавкаНДС,
				ВидСравненияКомпоновкиДанных.Равно,
				Перечисления.СтавкиНДС.НДС18_118);
			
			ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
				ТаблицаРасшифровки,
				"П000300003003",
				НаимПоказателя010_040,
				"",
				"",
				Раздел3.П000300003003,
				"Раздел3",
				ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
			ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
				ТаблицаРасшифровки,
				"П000300003005",
				НаимПоказателя010_040,
				"",
				"",
				Раздел3.П000300003005,
				"Раздел3",
				ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
			
			#КонецОбласти

			#Область Расшифровка_П000300004003_и_П000300004005
			
			ДополнительныеПараметрыИОтбор = НовыйДополнительныеПараметрыРасшифровкиИОтбор(
				МакетыРасшифровкиПолей.Получить("П000300004003"));
			
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхСобытие,
				ВидСравненияКомпоновкиДанных.ВСписке,
				СобытиеРеализация);
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхВидЦенности,
				ВидСравненияКомпоновкиДанных.ВСписке,
				ВидыЦенностейСтр010_040);
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхСтавкаНДС,
				ВидСравненияКомпоновкиДанных.Равно,
				Перечисления.СтавкиНДС.НДС10_110);
			
			ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
				ТаблицаРасшифровки,
				"П000300004003",
				НаимПоказателя010_040,
				"",
				"",
				Раздел3.П000300004003,
				"Раздел3",
				ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
			ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
				ТаблицаРасшифровки,
				"П000300004005",
				НаимПоказателя010_040,
				"",
				"",
				Раздел3.П000300004005,
				"Раздел3",
				ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
			
			#КонецОбласти

			#Область Расшифровка_П000300006003_и_П000300006005
			
			ДополнительныеПараметрыИОтбор = НовыйДополнительныеПараметрыРасшифровкиИОтбор(
				МакетыРасшифровкиПолей.Получить("П000300006003"));
			
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхСобытие,
				ВидСравненияКомпоновкиДанных.ВСписке,
				СобытиеНачисленНДС);
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхВидЦенности,
				ВидСравненияКомпоновкиДанных.ВСписке,
				ВидыЦенностейСтр060);
			
			ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
				ТаблицаРасшифровки,
				"П000300006003",
				НаимПоказателя060,
				"",
				"",
				Раздел3.П000300006003,
				"Раздел3",
				ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
			ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
				ТаблицаРасшифровки,
				"П000300006005",
				НаимПоказателя060,
				"",
				"",
				Раздел3.П000300006005,
				"Раздел3",
				ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
			
			#КонецОбласти
			
			#Область Расшифровка_П000300007003_и_П000300007005
			
			ДополнительныеПараметрыИОтбор = НовыйДополнительныеПараметрыРасшифровкиИОтбор(
				МакетыРасшифровкиПолей.Получить("П000300007003"));
			
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхСобытие,
				ВидСравненияКомпоновкиДанных.Равно,
				СобытиеПолученАванс);
			ДобавитьЗначениеВОтбор(ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхВидЦенности,
				ВидСравненияКомпоновкиДанных.ВСписке,
				ВидыЦенностейСтр070);
			
			ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
				ТаблицаРасшифровки,
				"П000300007003",
				НаимПоказателя070,
				"",
				"",
				Раздел3.П000300007003,
				"Раздел3",
				ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
			ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
				ТаблицаРасшифровки,
				"П000300007005",
				НаимПоказателя070,
				"",
				"",
				Раздел3.П000300007005,
				"Раздел3",
				ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
			
			#КонецОбласти
			
			#Область Расшифровка_П000300008005
			
			ДополнительныеПараметрыИОтбор = НовыйДополнительныеПараметрыРасшифровкиИОтбор(
				МакетыРасшифровкиПолей.Получить("П000300008005"));
				
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхСобытие,
				ВидСравненияКомпоновкиДанных.ВСписке,
				СобытиеВосстановлениеНДС);
			
			ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
				ТаблицаРасшифровки,
				"П000300008005",
				НаимПоказателя080,
				"",
				"",
				Раздел3.П000300008005,
				"Раздел3",
				ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
				
			#КонецОбласти
			
			#Область Расшифровка_П000300009005
			
			ДополнительныеПараметрыИОтбор = НовыйДополнительныеПараметрыРасшифровкиИОтбор(
				МакетыРасшифровкиПолей.Получить("П000300009005"));
			
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхСобытие,
				ВидСравненияКомпоновкиДанных.ВСписке,
				СобытиеВосстановлениеНДС);
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхВидЦенности,
				ВидСравненияКомпоновкиДанных.ВСписке,
				ВидыЦенностейСтр090);
			
			ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
				ТаблицаРасшифровки,
				"П000300009005",
				НаимПоказателя090,
				"",
				"",
				Раздел3.П000300009005,
				"Раздел3",
				ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
			
			#КонецОбласти
			
		КонецЕсли;
		
		Запрос.Текст = ТекстЗапросаРаздел3Вычеты();
		ЗначенияДляЗаполнения = Запрос.Выполнить().Выбрать();
		
		Если ЗначенияДляЗаполнения.Следующий() Тогда
			
			ЗаполнитьЗначенияСвойств(Раздел3, ЗначенияДляЗаполнения);
			
			#Область Расшифровка_П000300012003
			
			ДополнительныеПараметрыИОтбор = НовыйДополнительныеПараметрыРасшифровкиИОтбор(
				МакетыРасшифровкиПолей.Получить("П000300012003"));
			
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхСобытие,
				ВидСравненияКомпоновкиДанных.ВСписке,
				СобытиеПредъявленКВычетуНДС);
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхВидЦенности,
				ВидСравненияКомпоновкиДанных.ВСписке,
				ВидыЦенностейСтр120);
			
			ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
				ТаблицаРасшифровки,
				"П000300012003",
				НаимПоказателя120,
				"",
				"",
				Раздел3.П000300012003,
				"Раздел3",
				ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
			
			#КонецОбласти
			
			#Область Расшифровка_П000300013003
			
			ДополнительныеПараметрыИОтбор = НовыйДополнительныеПараметрыРасшифровкиИОтбор(
				МакетыРасшифровкиПолей.Получить("П000300013003"));
			
			ДобавитьЗначениеВОтбор(ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхСобытие,
				ВидСравненияКомпоновкиДанных.ВСписке,
				СобытиеПредъявленКВычетуНДС);
			ДобавитьЗначениеВОтбор(ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхВидЦенности,
				ВидСравненияКомпоновкиДанных.ВСписке,
				ВидыЦенностейСтр130);
			
			ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
				ТаблицаРасшифровки,
				"П000300013003",
				НаимПоказателя130,
				"",
				"",
				Раздел3.П000300013003,
				"Раздел3",
				ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
			
			#КонецОбласти

			#Область Расшифровка_ПП000300014003
			
			ДополнительныеПараметрыИОтбор = НовыйДополнительныеПараметрыРасшифровкиИОтбор(
				МакетыРасшифровкиПолей.Получить("П000300014003"));
			
			ДобавитьЗначениеВОтбор(
			ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхСобытие,
				ВидСравненияКомпоновкиДанных.ВСписке,
				СобытиеПредъявленКВычетуНДС);
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхВидЦенности,
				ВидСравненияКомпоновкиДанных.ВСписке,
				ВидыЦенностейСтр140);
			
			ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
				ТаблицаРасшифровки,
				"П000300014003",
				НаимПоказателя140,
				"",
				"",
				Раздел3.П000300014003,
				"Раздел3",
				ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
				
			#КонецОбласти
			
			#Область Расшифровка_П000300015003
			
			ДополнительныеПараметрыИОтбор = НовыйДополнительныеПараметрыРасшифровкиИОтбор(
				МакетыРасшифровкиПолей.Получить("П000300015003"));
			
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхСобытие,
				ВидСравненияКомпоновкиДанных.ВСписке,
				СобытиеПредъявленКВычетуНДС);
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхВидЦенности,
				ВидСравненияКомпоновкиДанных.ВСписке,
				ВидыЦенностейСтр150);
			
			ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
				ТаблицаРасшифровки,
				"П000300015003",
				НаимПоказателя150,
				"",
				"",
				Раздел3.П000300015003,
				"Раздел3",
				ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
			
			#КонецОбласти
			
			#Область Расшифровка_П000300016003
			
			ДополнительныеПараметрыИОтбор = НовыйДополнительныеПараметрыРасшифровкиИОтбор(
				МакетыРасшифровкиПолей.Получить("П000300016003"));
			
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхСобытие,
				ВидСравненияКомпоновкиДанных.ВСписке,
				СобытиеПредъявленКВычетуНДС);
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхВидЦенности,
				ВидСравненияКомпоновкиДанных.ВСписке,
				ВидыЦенностейСтр160);
			
			ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
				ТаблицаРасшифровки,
				"П000300016003",
				НаимПоказателя160,
				"",
				"",
				Раздел3.П000300016003,
				"Раздел3",
				ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
			
			#КонецОбласти
			
			#Область Расшифровка_П000300017003
			
			ДополнительныеПараметрыИОтбор = НовыйДополнительныеПараметрыРасшифровкиИОтбор(
				МакетыРасшифровкиПолей.Получить("П000300017003"));
			
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхСобытие,
				ВидСравненияКомпоновкиДанных.ВСписке,
				СобытиеПредъявленКВычетуНДС);
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхВидЦенности,
				ВидСравненияКомпоновкиДанных.ВСписке,
				ВидыЦенностейСтр170);
			
			ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
				ТаблицаРасшифровки,
				"П000300017003",
				НаимПоказателя170,
				"",
				"",
				Раздел3.П000300017003,
				"Раздел3",
				ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
				
			#КонецОбласти
			
			#Область Расшифровка_П000300018003
			
			ДополнительныеПараметрыИОтбор = НовыйДополнительныеПараметрыРасшифровкиИОтбор(
				МакетыРасшифровкиПолей.Получить("П000300018003"));
			
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхСобытие,
				ВидСравненияКомпоновкиДанных.ВСписке,
				СобытиеПредъявленКВычетуНДС);
			ДобавитьЗначениеВОтбор(
				ДополнительныеПараметрыИОтбор.Отбор,
				ПолеКомпоновкиДанныхВидЦенности,
				ВидСравненияКомпоновкиДанных.ВСписке,
				ВидыЦенностейСтр180);
			
			ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
				ТаблицаРасшифровки,
				"П000300018003",
				НаимПоказателя180,
				"",
				"",
				Раздел3.П000300018003,
				"Раздел3",
				ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
			
			#КонецОбласти
			
		КонецЕсли;
		
	КонецЕсли;
	
#КонецОбласти 

#Область Раздел3Приложение1

	Если Контейнер.Свойство("Раздел3_Прил1") Тогда
		
		Если КонецДня(ПараметрыОтчета.мДатаКонцаПериодаОтчета) = КонецГода(ПараметрыОтчета.мДатаКонцаПериодаОтчета) Тогда
			
			Раздел3_Прил1         = Контейнер.Раздел3_Прил1;
			СтраницаРаздел3_Прил1 = Раздел3_Прил1[0].Данные;
			ПерваяСтраница        = Истина;
			
			Для Каждого Показатель Из СтраницаРаздел3_Прил1 Цикл
				СтраницаРаздел3_Прил1[Показатель.Ключ] = ОбщегоНазначенияБПКлиентСервер.ПустоеЗначениеТипа(
					ТипЗнч(Показатель.Значение));
			КонецЦикла;
			
			Пока Раздел3_Прил1.Количество() > 1 Цикл
				Раздел3_Прил1.Удалить(1);
			КонецЦикла;
			
			Запрос = Новый Запрос();
			УстановитьОбщиеПараметрыЗапроса(Запрос, ОбщиеПараметрыЗапросов);
			Запрос.Текст = ТекстЗапросаРаздел3Приложение1();
			РезультатЗапроса = Запрос.Выполнить();
			ОбъектыНедвижимости = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
			
			Пока ОбъектыНедвижимости.Следующий() Цикл
				
				КодыОпераций = ОбъектыНедвижимости.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
				
				Пока КодыОпераций.Следующий() Цикл
					
					Если НЕ ПерваяСтраница Тогда
						
						СтруктураНовойТаблицы = Новый Структура();
						Для Каждого Показатель Из СтраницаРаздел3_Прил1 Цикл
							СтруктураНовойТаблицы.Вставить(Показатель.Ключ, 
								ОбщегоНазначенияБПКлиентСервер.ПустоеЗначениеТипа(ТипЗнч(Показатель.Значение)));
						КонецЦикла;
						
						НоваяСтраницаРаздела3 = Раздел3_Прил1.Добавить();
						НоваяСтраницаРаздела3.Данные = СтруктураНовойТаблицы;
						
					Иначе
						// На последующих выполнениях цикла нужно будет создавать дополнительные страницы
						ПерваяСтраница = Ложь;
					КонецЕсли;
					
					СтраницаРаздел3_Прил1 = Раздел3_Прил1[Раздел3_Прил1.Количество() - 1].Данные;
					
					Если ПараметрыОтчета.ПравилаЗаполнения = "2017Кв1" Тогда
						СтраницаРаздел3_Прил1.П000310001001 = ОбъектыНедвижимости.ОбъектНаименованиеПолное;
					ИначеЕсли ПараметрыОтчета.ПравилаЗаполнения = "2015Кв1" Тогда
						СтраницаРаздел3_Прил1.П000310001000 = ОбъектыНедвижимости.ОбъектНаименованиеПолное;
					КонецЕсли;
					
					#Область РасшифровкаСтроки010_060
					
					ДополнительныеПараметрыИОтбор = НовыйДополнительныеПараметрыРасшифровкиИОтбор("ОткрытьОбъект");
					ДополнительныеПараметрыИОтбор.ДополнительныеПараметры.Вставить("Объект", ОбъектыНедвижимости.ОбъектНедвижимости);
					
					Если ПараметрыОтчета.ПравилаЗаполнения = "2017Кв1" Тогда
						
						НаимПоказателя = НСтр("ru='Наименование объекта основного средства (код строки 010)'");
						ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
							ТаблицаРасшифровки,
							"П000310001001_" + Раздел3_Прил1.Количество(),
							НаимПоказателя,
							"",
							"",
							СтраницаРаздел3_Прил1.П000310001001,
							"Раздел3_Прил1",
							ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
						
						НаимПоказателя = НСтр("ru='Дата ввода объекта основного средства в эксплуатацию 
							|для исчисления амортизации (код строки 030)'");
						ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
							ТаблицаРасшифровки,
							"П000310003001_" + Раздел3_Прил1.Количество(),
							НаимПоказателя,
							"",
							"",
							СтраницаРаздел3_Прил1.П000310003001,
							"Раздел3_Прил1",
							ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
						
						НаимПоказателя = НСтр("ru='Дата начала начисления амортизационных отчислений по объекту 
							|основного средства (код строки 040)'");
						ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
							ТаблицаРасшифровки,
							"П000310004001_" + Раздел3_Прил1.Количество(),
							НаимПоказателя,
							"",
							"",
							СтраницаРаздел3_Прил1.П000310004001,
							"Раздел3_Прил1",
							ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
						
						НаимПоказателя = НСтр("ru='Стоимость объекта основного средства на дату ввода объекта в эксплуатацию
							|без учета налога (код строки 050)'");
						ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
							ТаблицаРасшифровки,
							"П000310005001_" + Раздел3_Прил1.Количество(),
							НаимПоказателя,
							"",
							"",
							СтраницаРаздел3_Прил1.П000310005001,
							"Раздел3_Прил1",
							ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
						
					ИначеЕсли ПараметрыОтчета.ПравилаЗаполнения = "2015Кв1" Тогда
					
						НаимПоказателя = НСтр("ru='Наименование объекта недвижимости (код строки 010)'");
						ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
							ТаблицаРасшифровки,
							"П000310001000_" + Раздел3_Прил1.Количество(),
							НаимПоказателя,
							"",
							"",
							СтраницаРаздел3_Прил1.П000310001000,
							"Раздел3_Прил1",
							ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
						
						НаимПоказателя = НСтр("ru='Дата ввода объекта недвижимости в эксплуатацию 
							|для исчисления амортизации (код строки 040)'");
						ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
							ТаблицаРасшифровки,
							"П000310004001_" + Раздел3_Прил1.Количество(),
							НаимПоказателя,
							"",
							"",
							СтраницаРаздел3_Прил1.П000310004001,
							"Раздел3_Прил1",
							ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
						
						НаимПоказателя = НСтр("ru='Дата начала начисления амортизационных отчислений 
							|по объекту недвижимости (код строки 050)'");
						ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
							ТаблицаРасшифровки,
							"П000310005001_" + Раздел3_Прил1.Количество(),
							НаимПоказателя,
							"",
							"",
							СтраницаРаздел3_Прил1.П000310005001,
							"Раздел3_Прил1",
							ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
						
						НаимПоказателя = НСтр("ru='Стоимость объекта недвижимости на дату ввода объекта в эксплуатацию
							|без учета НДС (руб.) (код строки 060)'");
						ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
							ТаблицаРасшифровки,
							"П000310006001_" + Раздел3_Прил1.Количество(),
							НаимПоказателя,
							"",
							"",
							СтраницаРаздел3_Прил1.П000310006001,
							"Раздел3_Прил1",
							ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
							
					КонецЕсли;
					
					#КонецОбласти
					
					ГодВосстановления = КодыОпераций.Выбрать();
					НомерСтроки = 0;
					
					Пока ГодВосстановления.Следующий() Цикл
						
						Если НомерСтроки = 0 Тогда
							
							Если ПараметрыОтчета.ПравилаЗаполнения = "2017Кв1" Тогда
								
								СтраницаРаздел3_Прил1.П000310002001 = ГодВосстановления.КодОперацииСтрокой;
								СтраницаРаздел3_Прил1.П000310003001 = ГодВосстановления.ДатаВводаВЭксплуатациюБУ;
								СтраницаРаздел3_Прил1.П000310004001 = ГодВосстановления.ДатаНачисленияАмортизацииНУ;
								СтраницаРаздел3_Прил1.П000310005001 = ГодВосстановления.СтоимостьОбъектаНедвижимости;
								СтраницаРаздел3_Прил1.П000310006001 = ГодВосстановления.НДСПоОбъектуНедвижимости;
							
							ИначеЕсли ПараметрыОтчета.ПравилаЗаполнения = "2015Кв1" Тогда
								
								Если НЕ ПустаяСтрока(ГодВосстановления.АдресМестонахождения) Тогда
									
									РоссийскийАдрес = Новый Соответствие;
									РоссийскийАдрес.Вставить("Индекс",          );
									РоссийскийАдрес.Вставить("Регион",          );
									РоссийскийАдрес.Вставить("КодРегиона",      );
									РоссийскийАдрес.Вставить("Район",           );
									РоссийскийАдрес.Вставить("Город",           );
									РоссийскийАдрес.Вставить("НаселенныйПункт", );
									РоссийскийАдрес.Вставить("Улица",           );
									РоссийскийАдрес.Вставить("Дом",             );
									РоссийскийАдрес.Вставить("Корпус",          );
									РоссийскийАдрес.Вставить("Квартира",        );
									
									РегламентированнаяОтчетностьВызовСервера.СформироватьАдрес(
										ГодВосстановления.АдресМестонахождения, РоссийскийАдрес);
										
									СтраницаРаздел3_Прил1.П000310002001 = РоссийскийАдрес["Индекс"];
									СтраницаРаздел3_Прил1.П000310002002 = ГодВосстановления.КодРегиона;
									СтраницаРаздел3_Прил1.П000310002003 = РоссийскийАдрес["Район"];
									СтраницаРаздел3_Прил1.П000310002004 = РоссийскийАдрес["Город"];
									СтраницаРаздел3_Прил1.П000310002005 = РоссийскийАдрес["НаселенныйПункт"];
									СтраницаРаздел3_Прил1.П000310002006 = РоссийскийАдрес["Улица"];
									СтраницаРаздел3_Прил1.П000310002007 = РоссийскийАдрес["Дом"];
									СтраницаРаздел3_Прил1.П000310002008 = РоссийскийАдрес["Корпус"];
									СтраницаРаздел3_Прил1.П000310002009 = РоссийскийАдрес["Квартира"];
									
								КонецЕсли;
								
								СтраницаРаздел3_Прил1.П000310003001 = ГодВосстановления.КодОперацииСтрокой;
								СтраницаРаздел3_Прил1.П000310004001 = ГодВосстановления.ДатаВводаВЭксплуатациюБУ;
								СтраницаРаздел3_Прил1.П000310005001 = ГодВосстановления.ДатаНачисленияАмортизацииНУ;
								СтраницаРаздел3_Прил1.П000310006001 = ГодВосстановления.СтоимостьОбъектаНедвижимости;
								СтраницаРаздел3_Прил1.П000310007001 = ГодВосстановления.НДСПоОбъектуНедвижимости;
								
							КонецЕсли;
							
						КонецЕсли;
						
						НомерСтроки = НомерСтроки + 1;
						
						Если ПараметрыОтчета.ПравилаЗаполнения = "2017Кв1" Тогда
							СтраницаРаздел3_Прил1["П000310007001_" + НомерСтроки] = Формат(ГодВосстановления.ГодВосстановления, "ЧГ=0");
							СтраницаРаздел3_Прил1["П000310007002_" + НомерСтроки] = ГодВосстановления.ДатаНачалаИспДляНеоблОпераций;
							СтраницаРаздел3_Прил1["П000310007003_" + НомерСтроки] = ГодВосстановления.ДоляНеоблагаемойВыручки;
							СтраницаРаздел3_Прил1["П000310007004_" + НомерСтроки] = ГодВосстановления.СуммаВосстановленногоНДС;
						ИначеЕсли ПараметрыОтчета.ПравилаЗаполнения = "2015Кв1" Тогда
							СтраницаРаздел3_Прил1["П000310008001_" + НомерСтроки] = Формат(ГодВосстановления.ГодВосстановления, "ЧГ=0");
							СтраницаРаздел3_Прил1["П000310008002_" + НомерСтроки] = ГодВосстановления.ДатаНачалаИспДляНеоблОпераций;
							СтраницаРаздел3_Прил1["П000310008003_" + НомерСтроки] = ГодВосстановления.ДоляНеоблагаемойВыручки;
							СтраницаРаздел3_Прил1["П000310008004_" + НомерСтроки] = ГодВосстановления.СуммаВосстановленногоНДС;
						КонецЕсли;
						
						#Область РасшифровкаСтрока070
						
						Если ПараметрыОтчета.ПравилаЗаполнения = "2017Кв1" Тогда
							ДополнительныеПараметрыИОтбор = НовыйДополнительныеПараметрыРасшифровкиИОтбор(
								МакетыРасшифровкиПолей.Получить("П000310007001"));
						ИначеЕсли ПараметрыОтчета.ПравилаЗаполнения = "2015Кв1" Тогда
							ДополнительныеПараметрыИОтбор = НовыйДополнительныеПараметрыРасшифровкиИОтбор(
								МакетыРасшифровкиПолей.Получить("П000310008001"));
						КонецЕсли;
						
						ПолеКомпоновкиДанныхОбъектНедвижимости       = Новый ПолеКомпоновкиДанных("ОбъектНедвижимости");
						ПолеКомпоновкиДанныхКодОперацииДляДекларации = Новый ПолеКомпоновкиДанных("КодОперацииДляДекларации");
						ПолеКомпоновкиДанныхГодВосстановленияНДС     = Новый ПолеКомпоновкиДанных("ГодВосстановленияНДС");
						
						ДобавитьЗначениеВОтбор(
							ДополнительныеПараметрыИОтбор.Отбор,
							ПолеКомпоновкиДанныхОбъектНедвижимости,
							ВидСравненияКомпоновкиДанных.Равно,
							ГодВосстановления.ОбъектНедвижимости);
						ДобавитьЗначениеВОтбор(
							ДополнительныеПараметрыИОтбор.Отбор,
							ПолеКомпоновкиДанныхКодОперацииДляДекларации,
							ВидСравненияКомпоновкиДанных.Равно,
							ГодВосстановления.КодОперацииДляДекларации);
						ДобавитьЗначениеВОтбор(
							ДополнительныеПараметрыИОтбор.Отбор,
							ПолеКомпоновкиДанныхГодВосстановленияНДС,
							ВидСравненияКомпоновкиДанных.Равно,
							ГодВосстановления.ГодВосстановления);
							
						Если ПараметрыОтчета.ПравилаЗаполнения = "2017Кв1" Тогда
							
							НаимПоказателя = НСтр("ru='(код строки 070) За истекший календарный год (предыдущие календарные годы)'");
							ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
								ТаблицаРасшифровки,
								"П000310007001_" + НомерСтроки + "_" + Раздел3_Прил1.Количество(),
								НаимПоказателя,
								"",
								"",
								СтраницаРаздел3_Прил1["П000310007001_" + НомерСтроки],
								"Раздел3_Прил1",
								ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
							
							НаимПоказателя = НСтр("ru='Дата начала использования объекта основного средства для операций, указанных
								|в пункте 2 статьи 170 Налогового кодекса Российской Федерации'");
							ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
								ТаблицаРасшифровки,
								"П000310007002_" + НомерСтроки + "_" + Раздел3_Прил1.Количество(),
								НаимПоказателя,
								"",
								"",
								СтраницаРаздел3_Прил1["П000310007002_" + НомерСтроки],
								"Раздел3_Прил1",
								ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
							
							НаимПоказателя = НСтр("ru='Доля отгруженных товаров (работ, услуг), имущественных прав,
								|не облагаемых налогом, в общей стоимости отгрузки (%)'");
							ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
								ТаблицаРасшифровки,
								"П000310007003_" + НомерСтроки + "_" + Раздел3_Прил1.Количество(),
								НаимПоказателя,
								"",
								"",
								СтраницаРаздел3_Прил1["П000310007003_" + НомерСтроки],
								"Раздел3_Прил1",
								ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
							
							НаимПоказателя = НСтр("ru='Сумма налога, подлежащая восстановлению и уплате в бюджет
								|за календарный год (истекшие календарные годы) в рублях'");
							ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
								ТаблицаРасшифровки,
								"П000310007004_" + НомерСтроки + "_" + Раздел3_Прил1.Количество(),
								НаимПоказателя,
								"",
								"",
								СтраницаРаздел3_Прил1["П000310007004_" + НомерСтроки],
								"Раздел3_Прил1",
								ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
								
						ИначеЕсли ПараметрыОтчета.ПравилаЗаполнения = "2015Кв1" Тогда
								
							НаимПоказателя = НСтр("ru='(код строки 080) За истекший календарный год (предыдущие календарные годы)'");
							ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
								ТаблицаРасшифровки,
								"П000310008001_" + НомерСтроки + "_" + Раздел3_Прил1.Количество(),
								НаимПоказателя,
								"",
								"",
								СтраницаРаздел3_Прил1["П000310008001_" + НомерСтроки],
								"Раздел3_Прил1",
								ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
							
							НаимПоказателя = НСтр("ru='Дата начала использования объекта недвижимости для операций,
								|указанных в пункте 2 статьи 170 Налогового кодекса Российской Федерации'");
							ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
								ТаблицаРасшифровки,
								"П000310008002_" + НомерСтроки + "_" + Раздел3_Прил1.Количество(),
								НаимПоказателя,
								"",
								"",
								СтраницаРаздел3_Прил1["П000310008002_" + НомерСтроки],
								"Раздел3_Прил1",
								ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
							
							НаимПоказателя = НСтр("ru='Доля отгруженных товаров (работ, услуг), имущественных прав,
								|не облагаемых налогом, в общей стоимости отгрузки (%)'");
							ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
								ТаблицаРасшифровки,
								"П000310008003_" + НомерСтроки + "_" + Раздел3_Прил1.Количество(),
								НаимПоказателя,
								"",
								"",
								СтраницаРаздел3_Прил1["П000310008003_" + НомерСтроки],
								"Раздел3_Прил1",
								ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
							
							НаимПоказателя = НСтр("ru='Сумма налога, подлежащая восстановлению и уплате в бюджет за
								|календарный год (истекшие календарные годы) в рублях'");
							ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
								ТаблицаРасшифровки,
								"П000310008004_" + НомерСтроки + "_" + Раздел3_Прил1.Количество(),
								НаимПоказателя,
								"",
								"",
								СтраницаРаздел3_Прил1["П000310008004_" + НомерСтроки],
								"Раздел3_Прил1",
								ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
								
						КонецЕсли;
							
						#КонецОбласти
						
						// Восстановление должно быть отражено в течении 10 лет.
						Если НомерСтроки = 10 Тогда
							Прервать;
						КонецЕсли;
						
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
#КонецОбласти 

#Область Раздел4

	Если Контейнер.Свойство("Раздел4")
	   И ОрганизацияВедетРаздельныйУчетНДС Тогда
		
		ОчиститьРаздел4(Контейнер, ПараметрыОтчета);
		
		Запрос = Новый Запрос;
		УстановитьОбщиеПараметрыЗапроса(Запрос, ОбщиеПараметрыЗапросов);
		Запрос.УстановитьПараметр("КодОперацииПоУмолчанию", "1011410");
		Запрос.УстановитьПараметр("СобытиеВычет",           Перечисления.СобытияПоНДСПокупки.ПредъявленНДСКВычету0);
		Запрос.УстановитьПараметр("СобытиеНачисление",      Перечисления.СобытияПоНДСПродажи.ПодтвержденаСтавка0);
		Запрос.Текст = ТекстЗапросаРаздел4_6();
		Результат = Запрос.Выполнить();
		
		Если НЕ Результат.Пустой() Тогда
			
			Выборка = Результат.Выбрать();
			
			НомерСтроки = 0;
			
			Пока Выборка.Следующий() Цикл
				
				НомерСтроки = НомерСтроки + 1;
				НомерСтрокиБезГруппировки = "_" + Формат(НомерСтроки, "ЧГ=0");
				
				ЗаполнитьСведенияРаздел4(
					Контейнер,
					НомерСтрокиБезГруппировки,
					ПараметрыОтчета,
					Выборка);
				
				#Область РасшифровкаРаздел4
				
				Если ПараметрыОтчета.ПравилаЗаполнения = "2017Кв1" Тогда
					Строка020 = "П0004М1002003";
					Строка030 = "П0004М1003003";
				ИначеЕсли ПараметрыОтчета.ПравилаЗаполнения = "2015Кв1" Тогда
					Строка020 = "П000400002003";
					Строка030 = "П000400003003";
				КонецЕсли;
				
				// Строка 020
				
				ИдентификаторПоказателя = Строка020 + НомерСтрокиБезГруппировки;
				
				ДополнительныеПараметрыИОтбор = НовыйДополнительныеПараметрыРасшифровкиИОтбор(
					МакетыРасшифровкиПолей.Получить(Строка020));
				
				ДобавитьЗначениеВОтбор(
					ДополнительныеПараметрыИОтбор.Отбор,
					ПолеКомпоновкиДанныхСобытие,
					ВидСравненияКомпоновкиДанных.Равно,
					СобытиеПодтвержденаСтавка0);
					
				ДобавитьЗначениеВОтбор(
					ДополнительныеПараметрыИОтбор.Отбор,
					ПолеКомпоновкиДанныхКодВидаОперации,
					ВидСравненияКомпоновкиДанных.Равно,
					Выборка.КодОперации);
					
				НаименованиеПоказателя = НСтр("ru='Налоговая база'");
				
				ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
					ТаблицаРасшифровки,
					ИдентификаторПоказателя,
					НаименованиеПоказателя,
					"",
					"",
					Выборка.ПодтвержденнаяНалоговаяБаза,
					"Раздел4",
					ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
					
				// Строка 030
					
				ИдентификаторПоказателя = Строка030 + НомерСтрокиБезГруппировки;
					
				ДополнительныеПараметрыИОтбор = НовыйДополнительныеПараметрыРасшифровкиИОтбор(
					МакетыРасшифровкиПолей.Получить(Строка030));
			
				ДобавитьЗначениеВОтбор(
					ДополнительныеПараметрыИОтбор.Отбор,
					ПолеКомпоновкиДанныхСобытие,
					ВидСравненияКомпоновкиДанных.Равно,
					СобытиеПредъявленКВычетуНДС0);
					
				ДобавитьЗначениеВОтбор(
					ДополнительныеПараметрыИОтбор.Отбор,
					ПолеКомпоновкиДанныхКодВидаОперации,
					ВидСравненияКомпоновкиДанных.Равно,
					Выборка.КодОперации);
					
				НаимПоказателя = НСтр(
					"ru='Налоговые вычеты по операциям, обоснованность применения налоговой ставки
					|0 процентов по которым документально подтверждена'");
				
				ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
					ТаблицаРасшифровки,
					ИдентификаторПоказателя,
					НаимПоказателя,
					"",
					"",
					Выборка.ВычетНДС,
					"Раздел4",
					ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
					
				#КонецОбласти
				
			КонецЦикла;
		Иначе
			ПараметрыЗаполнения = ПустаяСтруктураПараметровЗаполнения();
			ЗаполнитьСведенияРаздел4(
				Контейнер,
				"_1",
				ПараметрыОтчета,
				ПараметрыЗаполнения);
		КонецЕсли;
		
	КонецЕсли;


#КонецОбласти

#Область Раздел6

	Если Контейнер.Свойство("Раздел6")
	   И ОрганизацияВедетРаздельныйУчетНДС Тогда
		
		ОчиститьРаздел6(Контейнер, ПараметрыОтчета);
		
		Запрос = Новый Запрос;
		УстановитьОбщиеПараметрыЗапроса(Запрос, ОбщиеПараметрыЗапросов);
		Запрос.УстановитьПараметр("КодОперацииПоУмолчанию", "1011410");
		Запрос.УстановитьПараметр("СобытиеВычет",           Перечисления.СобытияПоНДСПокупки.НеПодтвержденаСтавка0);
		Запрос.УстановитьПараметр("СобытиеНачисление",      Перечисления.СобытияПоНДСПродажи.НеПодтвержденаСтавка0);
		Запрос.Текст = ТекстЗапросаРаздел4_6();
		Результат = Запрос.Выполнить();
		
		Если НЕ Результат.Пустой() Тогда
			
			Выборка = Результат.Выбрать();
			
			НомерСтроки = 0;
			
			Пока Выборка.Следующий() Цикл
				
				НомерСтроки = НомерСтроки + 1;
				НомерСтрокиБезГруппировки = "_" + Формат(НомерСтроки, "ЧГ=0");
				
				ЗаполнитьСведенияРаздел6(
					Контейнер,
					НомерСтрокиБезГруппировки,
					ПараметрыОтчета,
					Выборка);
				
				#Область РасшифровкаРаздел6
				
				Если ПараметрыОтчета.ПравилаЗаполнения = "2017Кв1" Тогда
					Строка020 = "П0006М1002003";
					Строка030 = "П0006М1003003";
					Строка040 = "П0006М1004003";
				ИначеЕсли ПараметрыОтчета.ПравилаЗаполнения = "2015Кв1" Тогда
					Строка020 = "П000600002003";
					Строка030 = "П000600003003";
					Строка040 = "П000600004003";
				КонецЕсли;
	
				// Строка 020
				
				ИдентификаторПоказателя = Строка020 + НомерСтрокиБезГруппировки;
				
				ДополнительныеПараметрыИОтбор = НовыйДополнительныеПараметрыРасшифровкиИОтбор(
					МакетыРасшифровкиПолей.Получить(Строка020));
				
				ДобавитьЗначениеВОтбор(
					ДополнительныеПараметрыИОтбор.Отбор,
					ПолеКомпоновкиДанныхСобытие,
					ВидСравненияКомпоновкиДанных.Равно,
					СобытиеНеПодтвержденаСтавка0);
					
				ДобавитьЗначениеВОтбор(
					ДополнительныеПараметрыИОтбор.Отбор,
					ПолеКомпоновкиДанныхКодВидаОперации,
					ВидСравненияКомпоновкиДанных.Равно,
					Выборка.КодОперации);
					
				НаименованиеПоказателя = НСтр("ru='Налоговая база'");
				
				ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
					ТаблицаРасшифровки,
					ИдентификаторПоказателя,
					НаименованиеПоказателя,
					"",
					"",
					Выборка.ПодтвержденнаяНалоговаяБаза,
					"Раздел6",
					ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
				
				// Строка 030
				
				ИдентификаторПоказателя = Строка030 + НомерСтрокиБезГруппировки;
				
				НаименованиеПоказателя = НСтр(
					"ru='Суммы налога, исчисленные по налоговым ставкам, предусмотренным пунктами 2, 3
					|статьи 164 Налогового кодекса Российской Федерации'");
				
				ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
					ТаблицаРасшифровки,
					ИдентификаторПоказателя,
					НаименованиеПоказателя,
					"",
					"",
					Выборка.НачислениеНДС,
					"Раздел6",
					ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
				
				// Строка 040
				
				ИдентификаторПоказателя = Строка040 + НомерСтрокиБезГруппировки;
					
				ДополнительныеПараметрыИОтбор = НовыйДополнительныеПараметрыРасшифровкиИОтбор(
					МакетыРасшифровкиПолей.Получить(Строка040));
			
				ДобавитьЗначениеВОтбор(
					ДополнительныеПараметрыИОтбор.Отбор,
					ПолеКомпоновкиДанныхСобытие,
					ВидСравненияКомпоновкиДанных.Равно,
					СобытиеПредъявленКВычетуНеПодтвержденНДС0);
					
				ДобавитьЗначениеВОтбор(
					ДополнительныеПараметрыИОтбор.Отбор,
					ПолеКомпоновкиДанныхКодВидаОперации,
					ВидСравненияКомпоновкиДанных.Равно,
					Выборка.КодОперации);
					
				НаименованиеПоказателя = НСтр(
					"ru='Сумма налоговых вычетов по операциям по реализации товаров (работ, услуг), 
					|обоснованность применения налоговой ставки 0 процентов по которым документально не подтверждена'");
				
				ЗаполнениеРасшифровкаРегламентированнойОтчетности.ДобавитьСтрокуРасшифровки(
					ТаблицаРасшифровки,
					ИдентификаторПоказателя,
					НаименованиеПоказателя,
					"",
					"",
					Выборка.ВычетНДС,
					"Раздел6",
					ДополнительныеПараметрыИОтбор.ДополнительныеПараметры);
				
				#КонецОбласти
				
			КонецЦикла;
		Иначе
			ПараметрыЗаполнения = ПустаяСтруктураПараметровЗаполнения();
			ЗаполнитьСведенияРаздел6(
				Контейнер,
				"_1",
				ПараметрыОтчета,
				ПараметрыЗаполнения);
		КонецЕсли;
	
	КонецЕсли;
	
#КонецОбласти

#Область ОбщиеПараметрыМногостраничныхРазделов

	ОбщиеПараметры = Новый Структура();
	ОбщиеПараметры.Вставить("Организация",                         ПараметрыОтчета.Организация);
	ОбщиеПараметры.Вставить("НачалоПериода",                       НачалоПериодаОтчета);
	ОбщиеПараметры.Вставить("КонецПериода",                        ПараметрыОтчета.мДатаКонцаПериодаОтчета);
	ОбщиеПараметры.Вставить("ГруппироватьПоКонтрагентам",          Ложь);
	ОбщиеПараметры.Вставить("КонтрагентДляОтбора",                 Справочники.Контрагенты.ПустаяСсылка());
	ОбщиеПараметры.Вставить("ВыводитьПокупателейПоАвансам",        Ложь);
	ОбщиеПараметры.Вставить("ВыводитьПродавцовПоАвансам",          Ложь);
	ОбщиеПараметры.Вставить("ВключатьОбособленныеПодразделения",   Истина);
	ОбщиеПараметры.Вставить("СформироватьОтчетПоСтандартнойФорме", Истина);
	ОбщиеПараметры.Вставить("ОтбиратьПоКонтрагенту",               Ложь);
	ОбщиеПараметры.Вставить("СписокОрганизаций",                   СписокОрганизаций);
	ОбщиеПараметры.Вставить("СсылкаНаДекларацию",                  СсылкаНаДекларацию);
	ОбщиеПараметры.Вставить("ЗаполнениеДокумента",                 Ложь);
	ОбщиеПараметры.Вставить("ЗаполнениеДекларации",                Истина);
	ОбщиеПараметры.Вставить("ПредельноеКоличествоЗаписейВРазделеДекларацииПоНДС", 
		УчетНДС.ПредельноеКоличествоЗаписейВРазделеДекларацииПоНДС());
	ОбщиеПараметры.Вставить("ДатаФормированияДопЛиста");
	ОбщиеПараметры.Вставить("ПравилаЗаполнения",                   ПараметрыОтчета.ПравилаЗаполнения);

	ПроверкаКонтрагентов.ДобавитьОбщиеПараметрыДляПроверкиКонтрагентовВОтчете(ОбщиеПараметры);

#КонецОбласти

#Область РазделыС8По9

	ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации(),
		УровеньЖурналаРегистрации.Информация,,, НСтр("ru = 'Заполнены разделы 1-7'"));

	Если Контейнер.Свойство("Раздел8") Тогда
		
		// Сведения из книги покупок об операциях, отражаемых за истекший налоговый период
		
		ПараметрыРаздел8 = ОбщиеПараметры;
		ПараметрыРаздел8.Вставить("НачалоПериода",                      НачалоПериодаОтчета);
		ПараметрыРаздел8.Вставить("КонецПериода",                       ПараметрыОтчета.мДатаКонцаПериодаОтчета);
		ПараметрыРаздел8.Вставить("ФормироватьДополнительныеЛисты",     Ложь);
		ПараметрыРаздел8.Вставить("ДополнительныеЛистыЗаТекущийПериод", Ложь);
		ПараметрыРаздел8.Вставить("ВыводитьТолькоДопЛисты",             Ложь);
		
		АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, Новый УникальныйИдентификатор());
		Отчеты.КнигаПокупок.СформироватьОтчет(ПараметрыРаздел8, АдресХранилища);
		
	КонецЕсли;
	
	ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации(),
		УровеньЖурналаРегистрации.Информация,,, НСтр("ru = 'Заполнен раздел 8'"));

	Если Контейнер.Свойство("Раздел8_Прил1")
		И НЕ ПараметрыОтчета.ВидДокумента = 0 Тогда
		
		// Сведения из дополнительных листов книги покупок
		
		ПараметрыРаздел81 = ОбщиеПараметры;
		ПараметрыРаздел81.Вставить("КонецПериода",
			?(НЕ ЗначениеЗаполнено(ПараметрыОтчета.ДатаПодписи), 
			ПараметрыОтчета.ДатаПодписи, 
			КонецДня(ПараметрыОтчета.ДатаПодписи)));
		ПараметрыРаздел81.Вставить("НачалоПериода",                      НачалоКвартала(НачалоПериодаОтчета));
		ПараметрыРаздел81.Вставить("НачалоНалоговогоПериода",            НачалоКвартала(НачалоПериодаОтчета));
		ПараметрыРаздел81.Вставить("КонецНалоговогоПериода",             ПараметрыОтчета.мДатаКонцаПериодаОтчета);
		ПараметрыРаздел81.Вставить("ФормироватьДополнительныеЛисты",     Истина);
		ПараметрыРаздел81.Вставить("ДополнительныеЛистыЗаТекущийПериод", Истина);
		ПараметрыРаздел81.Вставить("ВыводитьТолькоДопЛисты",             Истина);
		
		АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, Новый УникальныйИдентификатор());
		Отчеты.КнигаПокупок.СформироватьОтчет(ПараметрыРаздел81, АдресХранилища);
		
	КонецЕсли;
	
	ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации(),
		УровеньЖурналаРегистрации.Информация,,, НСтр("ru = 'Заполнен раздел 8 прил. 1'"));
		
	Если Контейнер.Свойство("Раздел9") Тогда
		
		// Сведения из книги продаж об операциях, отражаемых за истекший налоговый период
		
		ПараметрыРаздел9 = ОбщиеПараметры;
		ПараметрыРаздел9.Вставить("НачалоПериода",                      НачалоПериодаОтчета);
		ПараметрыРаздел9.Вставить("КонецПериода",                       ПараметрыОтчета.мДатаКонцаПериодаОтчета);
		ПараметрыРаздел9.Вставить("ФормироватьДополнительныеЛисты",     Ложь);
		ПараметрыРаздел9.Вставить("ДополнительныеЛистыЗаТекущийПериод", Ложь);
		ПараметрыРаздел9.Вставить("ВыводитьТолькоДопЛисты",             Ложь);
		
		АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, Новый УникальныйИдентификатор());
		Отчеты.КнигаПродаж.СформироватьОтчет(ПараметрыРаздел9, АдресХранилища);
		
	КонецЕсли;
	
	ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации(),
		УровеньЖурналаРегистрации.Информация,,, НСтр("ru = 'Заполнен раздел 9'"));

	Если Контейнер.Свойство("Раздел9_Прил1")
		И НЕ ПараметрыОтчета.ВидДокумента = 0 Тогда
		
		// Сведения из дополнительных листов книги продаж
		
		ПараметрыРаздел91 = ОбщиеПараметры;
		ПараметрыРаздел91.Вставить("КонецПериода",
			?(НЕ ЗначениеЗаполнено(ПараметрыОтчета.ДатаПодписи), 
			ПараметрыОтчета.ДатаПодписи, 
			КонецДня(ПараметрыОтчета.ДатаПодписи)));
		ПараметрыРаздел91.Вставить("НачалоПериода",                      НачалоКвартала(НачалоПериодаОтчета));
		ПараметрыРаздел91.Вставить("НачалоНалоговогоПериода",            НачалоКвартала(НачалоПериодаОтчета));
		ПараметрыРаздел91.Вставить("КонецНалоговогоПериода",             ПараметрыОтчета.мДатаКонцаПериодаОтчета);
		ПараметрыРаздел91.Вставить("ФормироватьДополнительныеЛисты",     Истина);
		ПараметрыРаздел91.Вставить("ДополнительныеЛистыЗаТекущийПериод", Истина);
		ПараметрыРаздел91.Вставить("ВыводитьТолькоДопЛисты",             Истина);
		
		АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, Новый УникальныйИдентификатор());
		Отчеты.КнигаПродаж.СформироватьОтчет(ПараметрыРаздел91, АдресХранилища);
		
	КонецЕсли;
	
	ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации(),
		УровеньЖурналаРегистрации.Информация,,, НСтр("ru = 'Заполнен раздел 9 прил. 1'"));
	
#КонецОбласти

#Область РазделыС10По11
	
	Если Контейнер.Свойство("Раздел10")
		И Контейнер.Свойство("Раздел11") Тогда
	
		ПараметрыРаздела = ОбщиеПараметры;
		ПараметрыРаздела.Вставить("НалоговыйПериод", НачалоКвартала(ПараметрыОтчета.мДатаКонцаПериодаОтчета));
		ПараметрыРаздела.Вставить("ЭтоЖурналУчетаСчетовФактур", Истина);
		
		АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, Новый УникальныйИдентификатор());
		Отчеты.ЖурналУчетаСчетовФактур.СформироватьОтчет(ПараметрыРаздела, АдресХранилища);
		
	КонецЕсли;
	
	ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации(),
		УровеньЖурналаРегистрации.Информация,,, НСтр("ru = 'Заполнены разделы 10 и 11'"));
	
#КонецОбласти

#Область Раздел12
	
	Если Контейнер.Свойство("Раздел12") Тогда
		
		Раздел12 = Контейнер.Раздел12;
		ТаблицаРаздел12 = ПустаяТаблицаРаздел12();

		Запрос = Новый Запрос;
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
		Запрос.УстановитьПараметр("ОрганизацияНеПлательшикНДС",        ОрганизацияНеПлательшикНДС);
		Запрос.УстановитьПараметр("ОрганизацияОсвобожденаОтУплатыНДС", ОрганизацияОсвобожденаОтУплатыНДС);
		Запрос.УстановитьПараметр("Организация",                       СписокОрганизаций);
		Запрос.УстановитьПараметр("НачалоПериода",                     НачалоДня(НачалоПериодаОтчета));
		Запрос.УстановитьПараметр("КонецПериода",                      КонецДня(ПараметрыОтчета.мДатаКонцаПериодаОтчета));

		СчетаФактурыВыставленныеНеплательщикамиНДС = УчетНДСПереопределяемый.ТекстЗапросаСчетаФактурыВыставленныеНеплательщикамиНДС(
			Запрос, СписокОрганизаций, НачалоДня(ПараметрыОтчета.мДатаНачалаПериодаОтчета), КонецДня(ПараметрыОтчета.мДатаКонцаПериодаОтчета));
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СчетФактураВыданный.Номер,
		|	НАЧАЛОПЕРИОДА(СчетФактураВыданный.Дата, ДЕНЬ) КАК ДатаСведений,
		|	СчетФактураВыданный.Контрагент КАК Ссылка,
		|	СчетФактураВыданный.Контрагент,
		|	СчетФактураВыданный.СуммаНДСДокумента,
		|	СчетФактураВыданный.СуммаНДСДокументаКомиссия,
		|	СчетФактураВыданный.СуммаДокумента,
		|	СчетФактураВыданный.ВалютаДокумента КАК ВалютаДокумента,
		|	СчетФактураВыданный.Ссылка КАК СчетФактураРасшифровка,
		|	СчетФактураВыданный.КППКонтрагента,
		|	СчетФактураВыданный.Ссылка КАК СчетФактура
		|ПОМЕСТИТЬ СсылкиНаДаты
		|ИЗ
		|	(" + СчетаФактурыВыставленныеНеплательщикамиНДС + ") КАК СчетФактураВыданный";
		
		Запрос.Выполнить();
		
		УчетНДСПереопределяемый.РегистрационныеСведенияНаДаты(МенеджерВременныхТаблиц);
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СчетФактураВыданный.Номер КАК НомерСчетаФактуры,
		|	СчетФактураВыданный.ДатаСведений КАК ДатаСчетаФактуры,
		|	ЕСТЬNULL(СведенияОСсылкахНаДаты.ИНН, """") КАК ИННпокупателя,
		|	ВЫБОР
		|		КОГДА СчетФактураВыданный.КППКонтрагента ПОДОБНО """"
		|			ТОГДА ЕСТЬNULL(СведенияОСсылкахНаДаты.КПП, """")
		|		ИНАЧЕ СчетФактураВыданный.КППКонтрагента
		|	КОНЕЦ КАК КППпокупателя,
		|	ЕСТЬNULL(СведенияОСсылкахНаДаты.Наименование, """") КАК КонтрагентНаименование,
		|	СчетФактураВыданный.СуммаНДСДокумента - СчетФактураВыданный.СуммаНДСДокументаКомиссия КАК СумНалПокуп,
		|	СчетФактураВыданный.СуммаДокумента КАК СтоимТовСНалВс,
		|	СчетФактураВыданный.СуммаДокумента - (СчетФактураВыданный.СуммаНДСДокумента - СчетФактураВыданный.СуммаНДСДокументаКомиссия) КАК СтоимТовБНалВс,
		|	СчетФактураВыданный.ВалютаДокумента.Код КАК ОКВ,
		|	СчетФактураВыданный.СчетФактураРасшифровка КАК СчетФактураРасшифровка,
		|	Валюта.НаименованиеПолное КАК НаименованиеВалюты,
		|	Валюта.Наименование КАК НаименованиеВалютыСокращенное,
		|	СчетФактураВыданный.СчетФактура КАК СчетФактураСсылка,
		|	СчетФактураВыданный.Контрагент КАК Контрагент
		|ИЗ
		|	СсылкиНаДаты КАК СчетФактураВыданный
		|		ЛЕВОЕ СОЕДИНЕНИЕ СведенияОСсылкахНаДаты КАК СведенияОСсылкахНаДаты
		|		ПО СчетФактураВыданный.Контрагент = СведенияОСсылкахНаДаты.Ссылка
		|			И СчетФактураВыданный.ДатаСведений = СведенияОСсылкахНаДаты.ДатаСведений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Валюты КАК Валюта
		|		ПО СчетФактураВыданный.ВалютаДокумента = Валюта.Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДатаСчетаФактуры,
		|	НомерСчетаФактуры";
		
		Результат = Запрос.Выполнить();
		
		Если НЕ Результат.Пустой() Тогда
			
			ТаблицаСчетовФактур = Результат.Выгрузить();
			
			МакетРаздел12 = ПолучитьОбщийМакет("СведенияИзСчетовФактур");
			ТабличныйДокументРаздел12 = Новый ТабличныйДокумент;
			
			ОбластьШапка = МакетРаздел12.ПолучитьОбласть("Шапка");
			ТабличныйДокументРаздел12.Вывести(ОбластьШапка);
			
			ОбластьСтрока = МакетРаздел12.ПолучитьОбласть("Строка");
			
			НомерСтроки = 0;
			СчетчикСтрокТекущегоСегмента = 0;
			ИтогПоРазделу12 = 0;
			
			Для Каждого СтрокаВыборки Из ТаблицаСчетовФактур Цикл
				
				НомерСтроки = НомерСтроки + 1;
				СчетчикСтрокТекущегоСегмента = СчетчикСтрокТекущегоСегмента + 1;
				
				СтрокаРаздела12 = ТаблицаРаздел12.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаРаздела12, СтрокаВыборки);
				
				ДополнительныеСведения = Новый Структура("Контрагент,ПредставлениеКонтрагента,Документ,ПредставлениеДокумента");
				
				Если НЕ ПустаяСтрока(СтрокаВыборки.ИННпокупателя) Тогда
					
					СтруктураСвПок = Новый Структура();
					Если СтрДлина(СокрЛП(СтрокаВыборки.ИННпокупателя)) > 10 Тогда
						СтруктураСвПок.Вставить("ИННФЛ", СтрокаВыборки.ИННпокупателя);
					Иначе
						СтруктураСвПок.Вставить("ИННЮЛ", СтрокаВыборки.ИННпокупателя);
						СтруктураСвПок.Вставить("КПП", СтрокаВыборки.КППпокупателя);
					КонецЕсли;
					СтруктураСвПок.Вставить("Контрагент", СтрокаВыборки.Контрагент);
					СтруктураСвПок.Вставить("КонтрагентНаименование", СтрокаВыборки.КонтрагентНаименование);
					СтрокаРаздела12.СвПокуп = СтруктураСвПок;
					
					ДополнительныеСведения.Контрагент = СтрокаВыборки.Контрагент;
					ДополнительныеСведения.ПредставлениеКонтрагента = СтрокаВыборки.КонтрагентНаименование;
					
				КонецЕсли;
				
				СтрокаРаздела12.НомСчФ = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(
					СтрокаВыборки.НомерСчетаФактуры, Истина, Истина);
				СтрокаРаздела12.ДатаСчФ  = Формат(СтрокаВыборки.ДатаСчетаФактуры, "ДФ=dd.MM.yyyy");
				СтрокаРаздела12.НомерПор = НомерСтроки;
				ДополнительныеСведения.ПредставлениеДокумента = СтрШаблон(
					НСтр("ru='Счет-фактура выданный № %1 от %2 на сумму %3 %4'"), 
					СтрокаРаздела12.НомСчФ, 
					СтрокаРаздела12.ДатаСчФ, 
					Формат(СтрокаВыборки.СтоимТовСНалВс, "ЧДЦ=2; ЧН="),
					СтрокаВыборки.НаименованиеВалютыСокращенное);
					
				ДополнительныеСведения.Документ = СтрокаВыборки.СчетФактураСсылка;
				СтрокаРаздела12.ДополнительныеСведения = ДополнительныеСведения;
				
				ОбластьСтрока.Параметры.Ном                    = НомерСтроки;
				ОбластьСтрока.Параметры.НомерДата              = "" + СтрокаРаздела12.НомСчФ + ";" +  СтрокаРаздела12.ДатаСчФ;
				ОбластьСтрока.Параметры.Валюта                 = СокрЛП(СтрокаВыборки.НаименованиеВалюты) + ", " + СтрокаВыборки.ОКВ;
				ОбластьСтрока.Параметры.СуммаДокумента         = СтрокаВыборки.СтоимТовСНалВс;
				ОбластьСтрока.Параметры.СуммаНДС               = СтрокаВыборки.СумНалПокуп;
				ОбластьСтрока.Параметры.СуммаБезНДС            = СтрокаВыборки.СтоимТовБНалВс;
				ОбластьСтрока.Параметры.СчетФактураРасшифровка = СтрокаВыборки.СчетФактураРасшифровка;
				ОбластьСтрока.Параметры.КонтрагентНаименование = СтрокаВыборки.КонтрагентНаименование;
				ОбластьСтрока.Параметры.КонтрагентИННКПП       = "" + СтрокаВыборки.ИННпокупателя 
					+ ?(ЗначениеЗаполнено(СтрокаВыборки.КППпокупателя), "/" + Строка(СтрокаВыборки.КППпокупателя), "");
					
				ОбластьСтрока.Область(1, , ОбластьСтрока.ВысотаТаблицы).Имя = "Строка_" + Формат(НомерСтроки, "ЧГ=");
				ТабличныйДокументРаздел12.Вывести(ОбластьСтрока);
				
				ИтогПоРазделу12 = ИтогПоРазделу12 + СтрокаВыборки.СумНалПокуп;
				
				Если СчетчикСтрокТекущегоСегмента = ОбщиеПараметры.ПредельноеКоличествоЗаписейВРазделеДекларацииПоНДС Тогда
					
					// Запишем сегмент
					ПерваяСтрокаСегмента = НомерСтроки - СчетчикСтрокТекущегоСегмента + 1;
					
					РегламентированнаяОтчетность.ЗаписатьСегментДанныхРазделаДекларацииНДС(
						ОбщиеПараметры.СсылкаНаДекларацию,
						"Раздел12",
						ПерваяСтрокаСегмента,
						ТаблицаРаздел12);
					
					РегламентированнаяОтчетность.ЗаписатьСегментТабличногоДокументаРазделаДекларацииНДС(
						ОбщиеПараметры.СсылкаНаДекларацию,
						"Раздел12",
						ПерваяСтрокаСегмента,
						ТабличныйДокументРаздел12);
					
					// Очистим табличный документ и таблицу
					ТабличныйДокументРаздел12.Очистить();
					ТабличныйДокументРаздел12.Вывести(ОбластьШапка);
					ТаблицаРаздел12.Очистить();
					
					// Обнулим счетчик текущего сегмента
					СчетчикСтрокТекущегоСегмента = 0;
					
				КонецЕсли;
				
			КонецЦикла;
			
			Если СчетчикСтрокТекущегоСегмента <> 0
				И СчетчикСтрокТекущегоСегмента <> ОбщиеПараметры.ПредельноеКоличествоЗаписейВРазделеДекларацииПоНДС Тогда
				
				ПерваяСтрокаСегмента = НомерСтроки - СчетчикСтрокТекущегоСегмента + 1;
					
				РегламентированнаяОтчетность.ЗаписатьСегментДанныхРазделаДекларацииНДС(
					ОбщиеПараметры.СсылкаНаДекларацию,
					"Раздел12",
					ПерваяСтрокаСегмента,
					ТаблицаРаздел12);
				
				РегламентированнаяОтчетность.ЗаписатьСегментТабличногоДокументаРазделаДекларацииНДС(
					ОбщиеПараметры.СсылкаНаДекларацию,
					"Раздел12",
					ПерваяСтрокаСегмента,
					ТабличныйДокументРаздел12);
					
				КонецЕсли;
				
			Если Контейнер.Свойство("Раздел1") Тогда
				
				Раздел1 = Контейнер.Раздел1;
				
				ЗначенияДляЗаполнения = Новый Структура("П000100003003");
				ЗначенияДляЗаполнения.П000100003003 = ИтогПоРазделу12;
				ЗаполнитьЗначенияСвойств(Раздел1, ЗначенияДляЗаполнения);
				
			КонецЕсли;
				
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации(),
		УровеньЖурналаРегистрации.Информация,,, НСтр("ru = 'Заполнен раздел 12'"));
		
#КонецОбласти

	ПоместитьВоВременноеХранилище(Контейнер, ПараметрыОтчета.АдресВоВременномХранилище);
	ПоместитьВоВременноеХранилище(ТаблицаРасшифровки, ПараметрыОтчета.АдресВременногоХранилищаРасшифровки);

КонецПроцедуры

Процедура УстановитьОбщиеПараметрыЗапроса(Запрос, ОбщиеПараметры)
	
	Для Каждого Параметр Из ОбщиеПараметры Цикл
		Запрос.Параметры.Вставить(Параметр.Ключ, Параметр.Значение);
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(Запрос.Параметры, ОбщиеПараметры);
	
КонецПроцедуры
 

Функция ТекстЗапросаОбщиеДанные()
	
	Возврат 
	"ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура КАК СчетФактура,
	|	НДСЗаписиКнигиПродажОбороты.Событие КАК Событие,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС КАК СтавкаНДС,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности КАК ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.Покупатель КАК Покупатель,
	|	НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента
	|ПОМЕСТИТЬ НДСЗаписиКнигиПродажОборотыДетальные
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			,
	|			Организация В (&Организация)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК НДСЗаписиКнигиПродажОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОбороты.СчетФактура,
	|	НДСЗаписиКнигиПродажОбороты.Событие,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот,
	|	НДСЗаписиКнигиПродажОбороты.Покупатель,
	|	НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаУчитываемыхКорректировок,
	|			,
	|			Организация В (&Организация)
	|				И ВЫБОР
	|					КОГДА &ВесьОборотЗаПериод
	|						ТОГДА ЗаписьДополнительногоЛиста
	|					КОГДА &СУчетомКорректировок
	|						ТОГДА ЗаписьДополнительногоЛиста
	|								И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода)
	|					ИНАЧЕ ЛОЖЬ
	|				КОНЕЦ) КАК НДСЗаписиКнигиПродажОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Событие,
	|	ВидЦенности
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродажОборотыДетальные.Событие КАК Событие,
	|	НДСЗаписиКнигиПродажОборотыДетальные.СтавкаНДС КАК СтавкаНДС,
	|	НДСЗаписиКнигиПродажОборотыДетальные.ВидЦенности КАК ВидЦенности,
	|	НДСЗаписиКнигиПродажОборотыДетальные.Покупатель КАК Покупатель,
	|	НДСЗаписиКнигиПродажОборотыДетальные.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	СУММА(НДСЗаписиКнигиПродажОборотыДетальные.СуммаБезНДСОборот) КАК СуммаБезНДСОборот,
	|	СУММА(НДСЗаписиКнигиПродажОборотыДетальные.НДСОборот) КАК НДСОборот
	|ПОМЕСТИТЬ НДСЗаписиКнигиПродажОбороты
	|ИЗ
	|	НДСЗаписиКнигиПродажОборотыДетальные КАК НДСЗаписиКнигиПродажОборотыДетальные
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСЗаписиКнигиПродажОборотыДетальные.СтавкаНДС,
	|	НДСЗаписиКнигиПродажОборотыДетальные.Событие,
	|	НДСЗаписиКнигиПродажОборотыДетальные.ВидЦенности,
	|	НДСЗаписиКнигиПродажОборотыДетальные.Покупатель,
	|	НДСЗаписиКнигиПродажОборотыДетальные.ДоговорКонтрагента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупокОбороты.Событие,
	|	НДСЗаписиКнигиПокупокОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.НДСОборот
	|ПОМЕСТИТЬ НДСЗаписиКнигиПокупокОбороты
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			,
	|			Организация В (&Организация)
	|				И НЕ ЗаписьДополнительногоЛиста) КАК НДСЗаписиКнигиПокупокОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупокОбороты.Событие,
	|	НДСЗаписиКнигиПокупокОбороты.СтавкаНДС,
	|	НДСЗаписиКнигиПокупокОбороты.ВидЦенности,
	|	НДСЗаписиКнигиПокупокОбороты.СуммаБезНДСОборот,
	|	НДСЗаписиКнигиПокупокОбороты.НДСОборот
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок.Обороты(
	|			&НачалоПериода,
	|			&КонецПериодаУчитываемыхКорректировок,
	|			,
	|			Организация В (&Организация)
	|				И ВЫБОР
	|					КОГДА &ВесьОборотЗаПериод
	|						ТОГДА ЗаписьДополнительногоЛиста
	|					КОГДА &СУчетомКорректировок
	|						ТОГДА ЗаписьДополнительногоЛиста
	|								И (КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода)
	|					ИНАЧЕ ЛОЖЬ
	|				КОНЕЦ) КАК НДСЗаписиКнигиПокупокОбороты";

КонецФункции

Функция ТекстЗапросаРаздел2()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА НДСЗаписиКнигиПродажОбороты.ВидЦенности В (&ВидыЦенностей_1011707)
	|				И НДСЗаписиКнигиПродажОбороты.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.НалоговыйАгентКомитент)
	|				И НЕ НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента = ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|			ТОГДА НДСЗаписиКнигиПродажОбороты.ДоговорКонтрагента.Владелец
	|		ИНАЧЕ НДСЗаписиКнигиПродажОбороты.Покупатель
	|	КОНЕЦ КАК Покупатель,
	|	НДСЗаписиКнигиПродажОбороты.ВидЦенности КАК ВидЦенности,
	|	НДСЗаписиКнигиПродажОбороты.СтавкаНДС КАК СтавкаНДС,
	|	НДСЗаписиКнигиПродажОбороты.Событие КАК Событие,
	|	НДСЗаписиКнигиПродажОбороты.НДСОборот КАК НДСОборот
	|ПОМЕСТИТЬ НДСЗаписиКнигиПродажРаздел2
	|ИЗ
	|	НДСЗаписиКнигиПродажОбороты КАК НДСЗаписиКнигиПродажОбороты
	|ГДЕ
	|	(НДСЗаписиКнигиПродажОбороты.Событие В (&СобытиеНачисленНДС)
	|			ИЛИ НДСЗаписиКнигиПродажОбороты.Событие В (&СобытиеПолученАванс)
	|			ИЛИ НДСЗаписиКнигиПродажОбороты.Событие В (&СобытиеРеализацияРаздел2))
	|	И НДСЗаписиКнигиПродажОбороты.ВидЦенности В(&ВидыЦенностейНА)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&КППИностраннойОрганизации КАК П000200001003,
	|	ВЫБОР
	|		КОГДА ПОДСТРОКА(Контрагент.НаименованиеПолное, 1, 250) = """"
	|			ТОГДА Контрагент.Наименование
	|		ИНАЧЕ ПОДСТРОКА(Контрагент.НаименованиеПолное, 1, 250)
	|	КОНЕЦ КАК П000200002003,
	|	ЕСТЬNULL(Контрагент.ИНН, """") КАК П000200003003,
	|	ЕСТЬNULL(Контрагент.ЮридическоеФизическоеЛицо, ЗНАЧЕНИЕ(Перечисление.ЮридическоеФизическоеЛицо.ПустаяСсылка)) КАК ЮрФизЛицо,
	|	ВЫБОР
	|		КОГДА НДСЗаписиКнигиПродажРаздел2.ВидЦенности В (&ВидыЦенностей_1011712)
	|			ТОГДА ""1011712""
	|		КОГДА НДСЗаписиКнигиПродажРаздел2.ВидЦенности В (&ВидыЦенностей_1011703)
	|			ТОГДА ""1011703""
	|		КОГДА НДСЗаписиКнигиПродажРаздел2.ВидЦенности В (&ВидыЦенностей_1011707)
	|			ТОГДА ""1011707""
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК П000200007003,
	|	ЕСТЬNULL(СУММА(НДСЗаписиКнигиПродажРаздел2.НДСОборот), 0) КАК П000200006003,
	|	ЕСТЬNULL(СУММА(ВЫБОР
	|				КОГДА НДСЗаписиКнигиПродажРаздел2.ВидЦенности В (&ВидыЦенностей_1011707)
	|							И НДСЗаписиКнигиПродажРаздел2.Событие В (&СобытиеНачисленНДС)
	|						ИЛИ НДСЗаписиКнигиПродажРаздел2.Событие В (&СобытиеРеализацияРаздел2)
	|					ТОГДА НДСЗаписиКнигиПродажРаздел2.НДСОборот
	|				ИНАЧЕ 0
	|			КОНЕЦ), 0) КАК П000200008003,
	|	ЕСТЬNULL(СУММА(ВЫБОР
	|				КОГДА НДСЗаписиКнигиПродажРаздел2.Событие В (&СобытиеПолученАванс)
	|					ТОГДА НДСЗаписиКнигиПродажРаздел2.НДСОборот
	|				ИНАЧЕ 0
	|			КОНЕЦ), 0) КАК П000200009003,
	|	ЕСТЬNULL(Контрагент.Ссылка, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) КАК П000200002003_Расшифровка
	|ИЗ
	|	НДСЗаписиКнигиПродажРаздел2 КАК НДСЗаписиКнигиПродажРаздел2
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагент
	|		ПО НДСЗаписиКнигиПродажРаздел2.Покупатель = Контрагент.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСЗаписиКнигиПродажРаздел2.Покупатель,
	|	Контрагент.ИНН,
	|	Контрагент.ЮридическоеФизическоеЛицо,
	|	ВЫБОР
	|		КОГДА НДСЗаписиКнигиПродажРаздел2.ВидЦенности В (&ВидыЦенностей_1011712)
	|			ТОГДА ""1011712""
	|		КОГДА НДСЗаписиКнигиПродажРаздел2.ВидЦенности В (&ВидыЦенностей_1011703)
	|			ТОГДА ""1011703""
	|		КОГДА НДСЗаписиКнигиПродажРаздел2.ВидЦенности В (&ВидыЦенностей_1011707)
	|			ТОГДА ""1011707""
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ПОДСТРОКА(Контрагент.НаименованиеПолное, 1, 250) = """"
	|			ТОГДА Контрагент.Наименование
	|		ИНАЧЕ ПОДСТРОКА(Контрагент.НаименованиеПолное, 1, 250)
	|	КОНЕЦ,
	|	Контрагент.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	П000200002003";

	// Заменим обращение к реквизиту контрагент договора.
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
		"ДоговорКонтрагента.Владелец", 
		"ДоговорКонтрагента." + БухгалтерскийУчетКлиентСерверПереопределяемый.ПолучитьИмяРеквизитаКонтрагентДоговора());

	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаРаздел3Начисления()
	
	Возврат 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(ВЫРАЗИТЬ(ВЫБОР
	|					КОГДА НДСЗаписиКнигиПродажОбороты.Событие В (&СобытиеРеализация)
	|							И НДСЗаписиКнигиПродажОбороты.СтавкаНДС В (&СтавкаНДС18)
	|							И НДСЗаписиКнигиПродажОбороты.ВидЦенности В (&ВидыЦенностейСтр010_040)
	|						ТОГДА НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот
	|					ИНАЧЕ 0
	|				КОНЕЦ КАК ЧИСЛО(15, 2))), 0) КАК П000300001003,
	|	ЕСТЬNULL(СУММА(ВЫРАЗИТЬ(ВЫБОР
	|					КОГДА НДСЗаписиКнигиПродажОбороты.Событие В (&СобытиеРеализация)
	|							И НДСЗаписиКнигиПродажОбороты.СтавкаНДС В (&СтавкаНДС10)
	|							И НДСЗаписиКнигиПродажОбороты.ВидЦенности В (&ВидыЦенностейСтр010_040)
	|						ТОГДА НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот
	|					ИНАЧЕ 0
	|				КОНЕЦ КАК ЧИСЛО(15, 2))), 0) КАК П000300002003,
	|	ЕСТЬNULL(СУММА(ВЫРАЗИТЬ(ВЫБОР
	|					КОГДА НДСЗаписиКнигиПродажОбороты.Событие В (&СобытиеРеализация)
	|							И НДСЗаписиКнигиПродажОбороты.СтавкаНДС В (&СтавкаНДС18_118)
	|							И НДСЗаписиКнигиПродажОбороты.ВидЦенности В (&ВидыЦенностейСтр010_040)
	|						ТОГДА НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот + НДСЗаписиКнигиПродажОбороты.НДСОборот
	|					ИНАЧЕ 0
	|				КОНЕЦ КАК ЧИСЛО(15, 2))), 0) КАК П000300003003,
	|	ЕСТЬNULL(СУММА(ВЫРАЗИТЬ(ВЫБОР
	|					КОГДА НДСЗаписиКнигиПродажОбороты.Событие В (&СобытиеРеализация)
	|							И НДСЗаписиКнигиПродажОбороты.СтавкаНДС В (&СтавкаНДС10_110)
	|							И НДСЗаписиКнигиПродажОбороты.ВидЦенности В (&ВидыЦенностейСтр010_040)
	|						ТОГДА НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот + НДСЗаписиКнигиПродажОбороты.НДСОборот
	|					ИНАЧЕ 0
	|				КОНЕЦ КАК ЧИСЛО(15, 2))), 0) КАК П000300004003,
	|	ЕСТЬNULL(СУММА(ВЫРАЗИТЬ(ВЫБОР
	|					КОГДА НДСЗаписиКнигиПродажОбороты.Событие В (&СобытиеНачисленНДС)
	|							И НДСЗаписиКнигиПродажОбороты.ВидЦенности В (&ВидыЦенностейСтр060)
	|						ТОГДА НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот
	|					ИНАЧЕ 0
	|				КОНЕЦ КАК ЧИСЛО(15, 2))), 0) КАК П000300006003,
	|	ЕСТЬNULL(СУММА(ВЫРАЗИТЬ(ВЫБОР
	|					КОГДА НДСЗаписиКнигиПродажОбороты.Событие В (&СобытиеПолученАванс)
	|							И НДСЗаписиКнигиПродажОбороты.ВидЦенности В (&ВидыЦенностейСтр070)
	|						ТОГДА НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот + НДСЗаписиКнигиПродажОбороты.НДСОборот
	|					ИНАЧЕ 0
	|				КОНЕЦ КАК ЧИСЛО(15, 2))), 0) КАК П000300007003,
	|	ЕСТЬNULL(СУММА(ВЫРАЗИТЬ(ВЫБОР
	|					КОГДА НДСЗаписиКнигиПродажОбороты.Событие В (&СобытиеПолученАванс)
	|							И НДСЗаписиКнигиПродажОбороты.ВидЦенности В (&ВидыЦенностейСтр070)
	|						ТОГДА НДСЗаписиКнигиПродажОбороты.НДСОборот
	|					ИНАЧЕ 0
	|				КОНЕЦ КАК ЧИСЛО(15, 2))), 0) КАК П000300007005,
	|	ЕСТЬNULL(СУММА(ВЫРАЗИТЬ(ВЫБОР
	|					КОГДА НДСЗаписиКнигиПродажОбороты.Событие В (&СобытиеВосстановлениеНДС)
	|						ТОГДА НДСЗаписиКнигиПродажОбороты.НДСОборот
	|					ИНАЧЕ 0
	|				КОНЕЦ КАК ЧИСЛО(15, 2))), 0) КАК П000300008005,
	|	ЕСТЬNULL(СУММА(ВЫРАЗИТЬ(ВЫБОР
	|					КОГДА НДСЗаписиКнигиПродажОбороты.Событие В (&СобытиеВосстановлениеНДС)
	|							И НДСЗаписиКнигиПродажОбороты.ВидЦенности В (&ВидыЦенностейСтр090)
	|						ТОГДА НДСЗаписиКнигиПродажОбороты.НДСОборот
	|					ИНАЧЕ 0
	|				КОНЕЦ КАК ЧИСЛО(15, 2))), 0) КАК П000300009005
	|ИЗ
	|	НДСЗаписиКнигиПродажОбороты КАК НДСЗаписиКнигиПродажОбороты";

КонецФункции

Функция ТекстЗапросаРаздел3Вычеты()
	
	Возврат 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(ВЫРАЗИТЬ(ВЫБОР
	|					КОГДА НДСЗаписиКнигиПокупокОбороты.Событие В (&СобытиеПредъявленКВычетуНДС)
	|							И НДСЗаписиКнигиПокупокОбороты.ВидЦенности В (&ВидыЦенностейСтр120)
	|						ТОГДА НДСЗаписиКнигиПокупокОбороты.НДСОборот
	|					ИНАЧЕ 0
	|				КОНЕЦ КАК ЧИСЛО(15, 2))), 0) КАК П000300012003,
	|	ЕСТЬNULL(СУММА(ВЫРАЗИТЬ(ВЫБОР
	|					КОГДА НДСЗаписиКнигиПокупокОбороты.Событие В (&СобытиеПредъявленКВычетуНДС)
	|							И НДСЗаписиКнигиПокупокОбороты.ВидЦенности В (&ВидыЦенностейСтр130)
	|						ТОГДА НДСЗаписиКнигиПокупокОбороты.НДСОборот
	|					ИНАЧЕ 0
	|				КОНЕЦ КАК ЧИСЛО(15, 2))), 0) КАК П000300013003,
	|	ЕСТЬNULL(СУММА(ВЫРАЗИТЬ(ВЫБОР
	|					КОГДА НДСЗаписиКнигиПокупокОбороты.Событие В (&СобытиеПредъявленКВычетуНДС)
	|							И НДСЗаписиКнигиПокупокОбороты.ВидЦенности В (&ВидыЦенностейСтр140)
	|						ТОГДА НДСЗаписиКнигиПокупокОбороты.НДСОборот
	|					ИНАЧЕ 0
	|				КОНЕЦ КАК ЧИСЛО(15, 2))), 0) КАК П000300014003,
	|	ЕСТЬNULL(СУММА(ВЫРАЗИТЬ(ВЫБОР
	|					КОГДА НДСЗаписиКнигиПокупокОбороты.Событие В (&СобытиеПредъявленКВычетуНДС)
	|							И НДСЗаписиКнигиПокупокОбороты.ВидЦенности В (&ВидыЦенностейСтр150)
	|						ТОГДА НДСЗаписиКнигиПокупокОбороты.НДСОборот
	|					ИНАЧЕ 0
	|				КОНЕЦ КАК ЧИСЛО(15, 2))), 0) КАК П000300015003,
	|	ЕСТЬNULL(СУММА(ВЫРАЗИТЬ(ВЫБОР
	|					КОГДА НДСЗаписиКнигиПокупокОбороты.Событие В (&СобытиеПредъявленКВычетуНДС)
	|							И НДСЗаписиКнигиПокупокОбороты.ВидЦенности В (&ВидыЦенностейСтр160)
	|						ТОГДА НДСЗаписиКнигиПокупокОбороты.НДСОборот
	|					ИНАЧЕ 0
	|				КОНЕЦ КАК ЧИСЛО(15, 2))), 0) КАК П000300016003,
	|	ЕСТЬNULL(СУММА(ВЫРАЗИТЬ(ВЫБОР
	|					КОГДА НДСЗаписиКнигиПокупокОбороты.Событие В (&СобытиеПредъявленКВычетуНДС)
	|							И НДСЗаписиКнигиПокупокОбороты.ВидЦенности В (&ВидыЦенностейСтр170)
	|						ТОГДА НДСЗаписиКнигиПокупокОбороты.НДСОборот
	|					ИНАЧЕ 0
	|				КОНЕЦ КАК ЧИСЛО(15, 2))), 0) КАК П000300017003,
	|	ЕСТЬNULL(СУММА(ВЫРАЗИТЬ(ВЫБОР
	|					КОГДА НДСЗаписиКнигиПокупокОбороты.Событие В (&СобытиеПредъявленКВычетуНДС)
	|							И НДСЗаписиКнигиПокупокОбороты.ВидЦенности В (&ВидыЦенностейСтр180)
	|						ТОГДА НДСЗаписиКнигиПокупокОбороты.НДСОборот
	|					ИНАЧЕ 0
	|				КОНЕЦ КАК ЧИСЛО(15, 2))), 0) КАК П000300018003
	|ИЗ
	|	НДСЗаписиКнигиПокупокОбороты КАК НДСЗаписиКнигиПокупокОбороты";

КонецФункции

Функция ТекстЗапросаРаздел3Приложение1()
	
	Возврат 
	"ВЫБРАТЬ
	|	Реквизиты.ОбъектНедвижимости КАК ОбъектНедвижимости,
	|	ВЫБОР
	|		КОГДА ПОДСТРОКА(Реквизиты.ОбъектНедвижимости.НаименованиеПолное, 1, 250) = """"
	|			ТОГДА Реквизиты.ОбъектНедвижимости.Наименование
	|		ИНАЧЕ ПОДСТРОКА(Реквизиты.ОбъектНедвижимости.НаименованиеПолное, 1, 250)
	|	КОНЕЦ КАК ОбъектНаименованиеПолное,
	|	МестонахождениеОС.АдресМестонахождения КАК АдресМестонахождения,
	|	"""" КАК КодРегиона,
	|	ВЫБОР
	|		КОГДА СчетаФактуры.КодОперацииДляДекларации = ЗНАЧЕНИЕ(Перечисление.НДСКодыОперацийПоОбъектамНедвижимости.Приобретение)
	|			ТОГДА ""1011803""
	|		КОГДА СчетаФактуры.КодОперацииДляДекларации = ЗНАЧЕНИЕ(Перечисление.НДСКодыОперацийПоОбъектамНедвижимости.СМРПодрядные)
	|			ТОГДА ""1011801""
	|		КОГДА СчетаФактуры.КодОперацииДляДекларации = ЗНАЧЕНИЕ(Перечисление.НДСКодыОперацийПоОбъектамНедвижимости.СМРСобственные)
	|			ТОГДА ""1011802""
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК КодОперацииСтрокой,
	|	Реквизиты.ИспользуетсяДляОперацийНеОблагаемыхНДС,
	|	Реквизиты.ДатаВводаВЭксплуатациюБУ КАК ДатаВводаВЭксплуатациюБУ,
	|	Реквизиты.ДатаНачисленияАмортизацииНУ КАК ДатаНачисленияАмортизацииНУ,
	|	Реквизиты.СтоимостьОбъектаНедвижимости КАК СтоимостьОбъектаНедвижимости,
	|	ЕСТЬNULL(СУММА(СчетаФактуры.НДС), 0) КАК НДСПоОбъектуНедвижимости,
	|	ГОД(Реквизиты.Ссылка.Дата) КАК ГодВосстановления,
	|	Реквизиты.ДатаНачалаИспользованияДляОпераций КАК ДатаНачалаИспДляНеоблОпераций,
	|	Реквизиты.ДоляВыручкиНеОблагаемаяНДС КАК ДоляНеоблагаемойВыручки,
	|	ЕСТЬNULL(СУММА(СчетаФактуры.НДСВосстановлен), 0) КАК СуммаВосстановленногоНДС,
	|	СчетаФактуры.КодОперацииДляДекларации
	|ИЗ
	|	Документ.ВосстановлениеНДСПоОбъектамНедвижимости.ОбъектыНедвижимости КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВосстановлениеНДСПоОбъектамНедвижимости.СчетаФактуры КАК СчетаФактуры
	|		ПО Реквизиты.Ссылка = СчетаФактуры.Ссылка
	|			И Реквизиты.КлючСтроки = СчетаФактуры.КлючСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОСБухгалтерскийУчет.СрезПоследних(&КонецПериода, Организация В (&Организация)) КАК МестонахождениеОС
	|		ПО Реквизиты.ОбъектНедвижимости = МестонахождениеОС.ОсновноеСредство
	|ГДЕ
	|	Реквизиты.Ссылка.Организация В(&Организация)
	|	И Реквизиты.Ссылка.Дата <= &КонецПериода
	|	И Реквизиты.Ссылка.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	Реквизиты.ОбъектНедвижимости,
	|	ВЫБОР
	|		КОГДА СчетаФактуры.КодОперацииДляДекларации = ЗНАЧЕНИЕ(Перечисление.НДСКодыОперацийПоОбъектамНедвижимости.Приобретение)
	|			ТОГДА ""1011803""
	|		КОГДА СчетаФактуры.КодОперацииДляДекларации = ЗНАЧЕНИЕ(Перечисление.НДСКодыОперацийПоОбъектамНедвижимости.СМРПодрядные)
	|			ТОГДА ""1011801""
	|		КОГДА СчетаФактуры.КодОперацииДляДекларации = ЗНАЧЕНИЕ(Перечисление.НДСКодыОперацийПоОбъектамНедвижимости.СМРСобственные)
	|			ТОГДА ""1011802""
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	Реквизиты.ИспользуетсяДляОперацийНеОблагаемыхНДС,
	|	Реквизиты.ДатаВводаВЭксплуатациюБУ,
	|	Реквизиты.ДатаНачисленияАмортизацииНУ,
	|	Реквизиты.ДатаНачалаИспользованияДляОпераций,
	|	ГОД(Реквизиты.Ссылка.Дата),
	|	Реквизиты.СтоимостьОбъектаНедвижимости,
	|	Реквизиты.ДоляВыручкиНеОблагаемаяНДС,
	|	ВЫБОР
	|		КОГДА ПОДСТРОКА(Реквизиты.ОбъектНедвижимости.НаименованиеПолное, 1, 250) = """"
	|			ТОГДА Реквизиты.ОбъектНедвижимости.Наименование
	|		ИНАЧЕ ПОДСТРОКА(Реквизиты.ОбъектНедвижимости.НаименованиеПолное, 1, 250)
	|	КОНЕЦ,
	|	СчетаФактуры.КодОперацииДляДекларации,
	|	МестонахождениеОС.АдресМестонахождения
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГодВосстановления
	|ИТОГИ
	|	СУММА(СтоимостьОбъектаНедвижимости),
	|	СУММА(НДСПоОбъектуНедвижимости),
	|	СУММА(ГодВосстановления),
	|	СУММА(ДоляНеоблагаемойВыручки),
	|	СУММА(СуммаВосстановленногоНДС)
	|ПО
	|	ОбъектНедвижимости,
	|	КодОперацииСтрокой";

КонецФункции

Функция ТекстЗапросаРаздел4_6()
	
	ТекстЗапросаНеподтвержденныеРеализации0 = УчетНДСПереопределяемый.ТекстЗапросаНеподтвержденныеРеализации0();
	
	Возврат 
	"ВЫБРАТЬ
	|	НДСЗаписиКнигиПродаж.Организация,
	|	НДСЗаписиКнигиПродаж.СчетФактура КАК СчетФактура,
	|	СУММА(НДСЗаписиКнигиПродаж.СуммаБезНДС) КАК ПодтвержденнаяНалоговаяБаза,
	|	СУММА(НДСЗаписиКнигиПродаж.НДС) КАК НДС
	|ПОМЕСТИТЬ НалоговаяБазаПоОтгрузкамПредварительная
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж КАК НДСЗаписиКнигиПродаж
	|ГДЕ
	|	НДСЗаписиКнигиПродаж.Организация В(&Организация)
	|	И НДСЗаписиКнигиПродаж.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И НЕ НДСЗаписиКнигиПродаж.ЗаписьДополнительногоЛиста
	|	И НДСЗаписиКнигиПродаж.Событие = &СобытиеНачисление
	|	И НДСЗаписиКнигиПродаж.Активность
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСЗаписиКнигиПродаж.Организация,
	|	НДСЗаписиКнигиПродаж.СчетФактура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродаж.Организация,
	|	НДСЗаписиКнигиПродаж.СчетФактура,
	|	СУММА(НДСЗаписиКнигиПродаж.СуммаБезНДС),
	|	СУММА(НДСЗаписиКнигиПродаж.НДС)
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж КАК НДСЗаписиКнигиПродаж
	|ГДЕ
	|	&СУчетомКорректировок
	|	И НДСЗаписиКнигиПродаж.Организация В(&Организация)
	|	И НДСЗаписиКнигиПродаж.Период МЕЖДУ &НачалоПериода И &КонецПериодаУчитываемыхКорректировок
	|	И НДСЗаписиКнигиПродаж.Событие = &СобытиеНачисление
	|	И НДСЗаписиКнигиПродаж.Активность
	|	И НДСЗаписиКнигиПродаж.ЗаписьДополнительногоЛиста
	|	И НДСЗаписиКнигиПродаж.КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСЗаписиКнигиПродаж.Организация,
	|	НДСЗаписиКнигиПродаж.СчетФактура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НДСЗаписиКнигиПродаж.Организация,
	|	НДСЗаписиКнигиПродаж.СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НалоговаяБазаПоОтгрузкам.СчетФактура,
	|	НалоговаяБазаПоОтгрузкам.ДокументОтгрузки
	|ПОМЕСТИТЬ ДокументыОтгрузки
	|ИЗ
	|	(" + ТекстЗапросаНеподтвержденныеРеализации0 + ") КАК НалоговаяБазаПоОтгрузкам
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НалоговаяБазаПоОтгрузкам.СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НалоговаяБазаПоОтгрузкам.Организация,
	|	ЕСТЬNULL(ДокументыОтгрузки.ДокументОтгрузки, НалоговаяБазаПоОтгрузкам.СчетФактура) КАК ДокументОтгрузки,
	|	НалоговаяБазаПоОтгрузкам.ПодтвержденнаяНалоговаяБаза,
	|	НалоговаяБазаПоОтгрузкам.НДС
	|ПОМЕСТИТЬ НалоговаяБазаПоОтгрузкам
	|ИЗ
	|	НалоговаяБазаПоОтгрузкамПредварительная КАК НалоговаяБазаПоОтгрузкам
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДокументыОтгрузки КАК ДокументыОтгрузки
	|		ПО НалоговаяБазаПоОтгрузкам.СчетФактура = ДокументыОтгрузки.СчетФактура
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НалоговаяБазаПоОтгрузкам.Организация,
	|	ЕСТЬNULL(ДокументыОтгрузки.ДокументОтгрузки, НалоговаяБазаПоОтгрузкам.СчетФактура)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НалоговаяБаза.Организация КАК Организация,
	|	НалоговаяБаза.ДокументОтгрузки КАК ДокументОтгрузки,
	|	НалоговаяБаза.ПодтвержденнаяНалоговаяБаза,
	|	НалоговаяБаза.НДС КАК НачислениеНДС,
	|	0 КАК ВычетНДС
	|ПОМЕСТИТЬ НалоговаяБазаИВычетПоОтгрузкам
	|ИЗ
	|	НалоговаяБазаПоОтгрузкам КАК НалоговаяБаза
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупок.Организация,
	|	НДСЗаписиКнигиПокупок.ДокументОтгрузки,
	|	0,
	|	0,
	|	НДСЗаписиКнигиПокупок.НДС
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок КАК НДСЗаписиКнигиПокупок
	|ГДЕ
	|	НДСЗаписиКнигиПокупок.Организация В(&Организация)
	|	И НЕ НДСЗаписиКнигиПокупок.ЗаписьДополнительногоЛиста
	|	И НДСЗаписиКнигиПокупок.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И НДСЗаписиКнигиПокупок.Событие = &СобытиеВычет
	|	И НДСЗаписиКнигиПокупок.Активность
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупок.Организация,
	|	НДСЗаписиКнигиПокупок.ДокументОтгрузки,
	|	0,
	|	0,
	|	НДСЗаписиКнигиПокупок.НДС
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок КАК НДСЗаписиКнигиПокупок
	|ГДЕ
	|	&СУчетомКорректировок
	|	И НДСЗаписиКнигиПокупок.Организация В(&Организация)
	|	И НДСЗаписиКнигиПокупок.ЗаписьДополнительногоЛиста
	|	И НДСЗаписиКнигиПокупок.Период МЕЖДУ &НачалоПериода И &КонецПериодаУчитываемыхКорректировок
	|	И НДСЗаписиКнигиПокупок.КорректируемыйПериод МЕЖДУ &НачалоПериода И &КонецПериода
	|	И НДСЗаписиКнигиПокупок.Событие = &СобытиеВычет
	|	И НДСЗаписиКнигиПокупок.Активность
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ДокументОтгрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаможенныеДекларацииПоЭкспорту.ДокументОтгрузки,
	|	ТаможенныеДекларацииПоЭкспорту.Организация,
	|	ТаможенныеДекларацииПоЭкспорту.КодОперации
	|ПОМЕСТИТЬ СведенияТаможенныхДекларацийЭкспорт
	|ИЗ
	|	РегистрСведений.СведенияТаможенныхДекларацийЭкспорт КАК ТаможенныеДекларацииПоЭкспорту
	|ГДЕ
	|	ТаможенныеДекларацииПоЭкспорту.Организация В(&Организация)
	|	И ТаможенныеДекларацииПоЭкспорту.ДокументОтгрузки В
	|			(ВЫБРАТЬ
	|				НалоговаяБазаИВычетПоОтгрузкам.ДокументОтгрузки
	|			ИЗ
	|				НалоговаяБазаИВычетПоОтгрузкам)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТаможенныеДекларацииПоЭкспорту.Организация,
	|	ТаможенныеДекларацииПоЭкспорту.ДокументОтгрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НалоговаяБазаИВычетПоОтгрузкам.ПодтвержденнаяНалоговаяБаза КАК ПодтвержденнаяНалоговаяБаза,
	|	НалоговаяБазаИВычетПоОтгрузкам.НачислениеНДС КАК НачислениеНДС,
	|	НалоговаяБазаИВычетПоОтгрузкам.ВычетНДС КАК ВычетНДС,
	|	ЕСТЬNULL(СведенияТаможенныхДекларацийЭкспорт.КодОперации, &КодОперацииПоУмолчанию) КАК КодОперации
	|ПОМЕСТИТЬ НалоговаяБазаИВычетПоОтгрузкамПоКодамОпераций
	|ИЗ
	|	НалоговаяБазаИВычетПоОтгрузкам КАК НалоговаяБазаИВычетПоОтгрузкам
	|		ЛЕВОЕ СОЕДИНЕНИЕ СведенияТаможенныхДекларацийЭкспорт КАК СведенияТаможенныхДекларацийЭкспорт
	|		ПО НалоговаяБазаИВычетПоОтгрузкам.ДокументОтгрузки = СведенияТаможенныхДекларацийЭкспорт.ДокументОтгрузки
	|			И НалоговаяБазаИВычетПоОтгрузкам.Организация = СведенияТаможенныхДекларацийЭкспорт.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(НалоговаяБазаИВычетПоОтгрузкамПоКодамОпераций.ПодтвержденнаяНалоговаяБаза) КАК ПодтвержденнаяНалоговаяБаза,
	|	СУММА(НалоговаяБазаИВычетПоОтгрузкамПоКодамОпераций.НачислениеНДС) КАК НачислениеНДС,
	|	СУММА(НалоговаяБазаИВычетПоОтгрузкамПоКодамОпераций.ВычетНДС) КАК ВычетНДС,
	|	НалоговаяБазаИВычетПоОтгрузкамПоКодамОпераций.КодОперации
	|ИЗ
	|	НалоговаяБазаИВычетПоОтгрузкамПоКодамОпераций КАК НалоговаяБазаИВычетПоОтгрузкамПоКодамОпераций
	|
	|СГРУППИРОВАТЬ ПО
	|	НалоговаяБазаИВычетПоОтгрузкамПоКодамОпераций.КодОперации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НалоговаяБазаПоОтгрузкамПредварительная
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДокументыОтгрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НалоговаяБазаПоОтгрузкам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НалоговаяБазаИВычетПоОтгрузкам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СведенияТаможенныхДекларацийЭкспорт
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ НалоговаяБазаИВычетПоОтгрузкамПоКодамОпераций";

КонецФункции

Процедура ОчиститьРаздел(Раздел)
	
	Если ТипЗнч(Раздел) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	КлючиДляУдаления = Новый СписокЗначений;
	
	Для Каждого ЭлементРаздела Из Раздел Цикл
		Если СтрНайти(ЭлементРаздела.Ключ, "М2")
		 ИЛИ СтрНайти(ЭлементРаздела.Ключ, "М3") Тогда
			Продолжить;
		ИначеЕсли СтрНайти(ЭлементРаздела.Ключ, "_") Тогда
			// Удаление ключа
			КлючиДляУдаления.Добавить(ЭлементРаздела.Ключ);
		Иначе
			// Очистка значения
			Если ТипЗнч(ЭлементРаздела.Значение) = Тип("Строка") Тогда
				Раздел.Вставить(ЭлементРаздела.Ключ, "");
			ИначеЕсли ТипЗнч(ЭлементРаздела.Значение) = Тип("Число") Тогда
				Раздел.Вставить(ЭлементРаздела.Ключ, 0);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого КлючДляУдаления Из КлючиДляУдаления Цикл
		Раздел.Удалить(КлючДляУдаления);
	КонецЦикла;
	
КонецПроцедуры

Процедура ОчиститьРаздел4(Контейнер, ПараметрыОтчета)
	
	ОчиститьРаздел(Контейнер.Раздел4);
	
	Если ПараметрыОтчета.ПравилаЗаполнения = "2017Кв1" Тогда
		Контейнер.Раздел4_ДанныеДопСтрок010_050.Очистить();
	ИначеЕсли ПараметрыОтчета.ПравилаЗаполнения = "2015Кв1" Тогда
		Контейнер.Раздел4_ДанныеДопСтрок.Очистить();
	КонецЕсли;

КонецПроцедуры

Процедура ОчиститьРаздел6(Контейнер, ПараметрыОтчета)
	
	ОчиститьРаздел(Контейнер.Раздел6);
	
	Если ПараметрыОтчета.ПравилаЗаполнения = "2017Кв1" Тогда
		Контейнер.Раздел6_ДанныеДопСтрок010_040.Очистить();
	ИначеЕсли ПараметрыОтчета.ПравилаЗаполнения = "2015Кв1" Тогда
		Контейнер.Раздел6_ДанныеДопСтрок.Очистить();
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьСведенияРаздел4(Контейнер, НомерСтрокиБезГруппировки, ПараметрыОтчета, ДанныеЗаполнения)
	
	Если ПараметрыОтчета.ПравилаЗаполнения = "2017Кв1" Тогда
		Строка010 = "П0004М1001003";
		Строка020 = "П0004М1002003";
		Строка030 = "П0004М1003003";
		Строка040 = "П0004М1004003";
		Строка050 = "П0004М1005003";
		СтрокаРаздел4 = Контейнер.Раздел4_ДанныеДопСтрок010_050.Добавить();
	ИначеЕсли ПараметрыОтчета.ПравилаЗаполнения = "2015Кв1" Тогда
		Строка010 = "П000400001003";
		Строка020 = "П000400002003";
		Строка030 = "П000400003003";
		Строка040 = "П000400004003";
		Строка050 = "П000400005003";
		СтрокаРаздел4 = Контейнер.Раздел4_ДанныеДопСтрок.Добавить();
	КонецЕсли;
	
	ИмяКлюча = Строка010 + НомерСтрокиБезГруппировки;
	Контейнер.Раздел4.Вставить(ИмяКлюча, ДанныеЗаполнения.КодОперации);
	
	ИмяКлюча = Строка020 + НомерСтрокиБезГруппировки;
	Контейнер.Раздел4.Вставить(ИмяКлюча, ДанныеЗаполнения.ПодтвержденнаяНалоговаяБаза);
	
	ИмяКлюча = Строка030 + НомерСтрокиБезГруппировки;
	Контейнер.Раздел4.Вставить(ИмяКлюча, ДанныеЗаполнения.ВычетНДС);
	
	ИмяКлюча = Строка040 + НомерСтрокиБезГруппировки;
	Контейнер.Раздел4.Вставить(ИмяКлюча, 0);
	
	ИмяКлюча = Строка050 + НомерСтрокиБезГруппировки;
	Контейнер.Раздел4.Вставить(ИмяКлюча, 0);
	
	СтрокаРаздел4[Строка010] = ДанныеЗаполнения.КодОперации;
	СтрокаРаздел4[Строка020] = ДанныеЗаполнения.ПодтвержденнаяНалоговаяБаза;
	СтрокаРаздел4[Строка030] = ДанныеЗаполнения.ВычетНДС;
	
КонецПроцедуры

Процедура ЗаполнитьСведенияРаздел6(Контейнер, НомерСтрокиБезГруппировки, ПараметрыОтчета, ДанныеЗаполнения)
	
	Если ПараметрыОтчета.ПравилаЗаполнения = "2017Кв1" Тогда
		Строка010 = "П0006М1001003";
		Строка020 = "П0006М1002003";
		Строка030 = "П0006М1003003";
		Строка040 = "П0006М1004003";
		СтрокаРаздел6 = Контейнер.Раздел6_ДанныеДопСтрок010_040.Добавить();
	ИначеЕсли ПараметрыОтчета.ПравилаЗаполнения = "2015Кв1" Тогда
		Строка010 = "П000600001003";
		Строка020 = "П000600002003";
		Строка030 = "П000600003003";
		Строка040 = "П000600004003";
		СтрокаРаздел6 = Контейнер.Раздел6_ДанныеДопСтрок.Добавить();
	КонецЕсли;
	
	ИмяКлюча = Строка010 + НомерСтрокиБезГруппировки;
	Контейнер.Раздел6.Вставить(ИмяКлюча, ДанныеЗаполнения.КодОперации);
	
	ИмяКлюча = Строка020 + НомерСтрокиБезГруппировки;
	Контейнер.Раздел6.Вставить(ИмяКлюча, ДанныеЗаполнения.ПодтвержденнаяНалоговаяБаза);
	
	ИмяКлюча = Строка030 + НомерСтрокиБезГруппировки;
	Контейнер.Раздел6.Вставить(ИмяКлюча, ДанныеЗаполнения.НачислениеНДС);
	
	ИмяКлюча = Строка040 + НомерСтрокиБезГруппировки;
	Контейнер.Раздел6.Вставить(ИмяКлюча, ДанныеЗаполнения.ВычетНДС);
	
	СтрокаРаздел6[Строка010] = ДанныеЗаполнения.КодОперации;
	СтрокаРаздел6[Строка020] = ДанныеЗаполнения.ПодтвержденнаяНалоговаяБаза;
	СтрокаРаздел6[Строка030] = ДанныеЗаполнения.НачислениеНДС;
	СтрокаРаздел6[Строка040] = ДанныеЗаполнения.ВычетНДС;
	
КонецПроцедуры

Функция ПустаяТаблицаРаздел12()
	
	ТаблицаРаздел12 = Новый ТаблицаЗначений;
	ТаблицаРаздел12.Колонки.Добавить("НомерПор",       ОбщегоНазначения.ОписаниеТипаЧисло(12, 0));
	ТаблицаРаздел12.Колонки.Добавить("НомСчФ",         ОбщегоНазначения.ОписаниеТипаСтрока(0));
	ТаблицаРаздел12.Колонки.Добавить("ДатаСчФ",        ОбщегоНазначения.ОписаниеТипаСтрока(10));
	ТаблицаРаздел12.Колонки.Добавить("ОКВ",            ОбщегоНазначения.ОписаниеТипаСтрока(3));
	ТаблицаРаздел12.Колонки.Добавить("СтоимТовБНалВс", ОбщегоНазначения.ОписаниеТипаЧисло(19, 2));
	ТаблицаРаздел12.Колонки.Добавить("СумНалПокуп",    ОбщегоНазначения.ОписаниеТипаЧисло(19, 2));
	ТаблицаРаздел12.Колонки.Добавить("СтоимТовСНалВс", ОбщегоНазначения.ОписаниеТипаЧисло(19, 2));
	ТаблицаРаздел12.Колонки.Добавить("СвПокуп");
	ТаблицаРаздел12.Колонки.Добавить("ДополнительныеСведения");
	
	Возврат ТаблицаРаздел12;

КонецФункции

Функция ИмяСобытияЖурналаРегистрации()

	Возврат НСтр("ru = 'Заполнение декларации по НДС'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());

КонецФункции

#Область ВспомогательныеПроцедурыИФункцииРасшифровкиДекларацииПоНДС

Функция МакетыРасшифровкиПолей2015Кв1(Организация, Период)
	
	МакетыРасшифровки = Новый Соответствие();
	
	// Раздел 2: начисления при исполнении обязанностей налогового агента.
	МакетыРасшифровки.Вставить("П000200006003", "РасшифровкаНачислениеИсполнениеОНА");
	МакетыРасшифровки.Вставить("П000200008003", "РасшифровкаНачислениеИсполнениеОНА");
	МакетыРасшифровки.Вставить("П000200009003", "РасшифровкаНачислениеИсполнениеОНА");
	
	// Раздел 3
	// Начисления по реализации.
	МакетыРасшифровки.Вставить("П000300001003", "РасшифровкаНачислениеРеализацияСложныйУчетНДС");
	МакетыРасшифровки.Вставить("П000300001005", "РасшифровкаНачислениеРеализацияСложныйУчетНДС");
	МакетыРасшифровки.Вставить("П000300002003", "РасшифровкаНачислениеРеализацияСложныйУчетНДС");
	МакетыРасшифровки.Вставить("П000300002005", "РасшифровкаНачислениеРеализацияСложныйУчетНДС");
	МакетыРасшифровки.Вставить("П000300003003", "РасшифровкаНачислениеРеализацияСложныйУчетНДС");
	МакетыРасшифровки.Вставить("П000300003005", "РасшифровкаНачислениеРеализацияСложныйУчетНДС");
	МакетыРасшифровки.Вставить("П000300004003", "РасшифровкаНачислениеРеализацияСложныйУчетНДС");
	МакетыРасшифровки.Вставить("П000300004005", "РасшифровкаНачислениеРеализацияСложныйУчетНДС");
	
	// Начисления при выполнении СМР хоз. способом.
	МакетыРасшифровки.Вставить("П000300006003", "РасшифровкаНачислениеСМРХозСпособом");
	МакетыРасшифровки.Вставить("П000300006005", "РасшифровкаНачислениеСМРХозСпособом");
	// Начисления по авансам полученным.
	МакетыРасшифровки.Вставить("П000300007003", "РасшифровкаНачислениеАвансыПолученныеУпрощенныйУчетНДС");
	МакетыРасшифровки.Вставить("П000300007005", "РасшифровкаНачислениеАвансыПолученныеУпрощенныйУчетНДС");
	// Восстановление (начисление) по ранее выданным авансам.
	МакетыРасшифровки.Вставить("П000300009005", "РасшифровкаНачислениеВосстановлениеСАвансовВыданныхУпрощенныйУчетНДС");
	// В декларации с 1 кв 2015 года в строке 120 отображаются сведения, аналогичные строке 130
	// декларации до 1 кв 2015 года, поэтому для расшифровки строки 120 используется набор, 
	// который расшифровывает строку 130 прежней формы декларации.
	МакетыРасшифровки.Вставить("П000300012003", "РасшифровкаСтроки130Декларации");
	// Вычет с авансов выданных.
	МакетыРасшифровки.Вставить("П000300013003", "РасшифровкаВычетАвансыВыданныеУпрощенныйУчетНДС");
	// Вычет по СМР хоз. способом.
	МакетыРасшифровки.Вставить("П000300014003", "РасшифровкаВычетПриобретенныеЦенности");
	// Вычет по импорту.
	МакетыРасшифровки.Вставить("П000300015003", "РасшифровкаВычетПриобретенныеЦенности");
	// Вычет НДС при ввозе из Таможенного союза.
	МакетыРасшифровки.Вставить("П000300016003", "РасшифровкаВычетПриобретенныеЦенности");
	// Вычет с авансов полученных (восстановление).
	МакетыРасшифровки.Вставить("П000300017003", "РасшифровкаВычетСАвансовПолученныхУпрощенныйУчетНДС");
	// Вычет при исполнении обязанностей налогового агента.
	МакетыРасшифровки.Вставить("П000300018003", "РасшифровкаВычетИсполнениеОНА");
	
	// Раздел 3 приложение 1: восстановление НДС по объектам недвижимости.
	МакетыРасшифровки.Вставить("П000310008001", "ВосстановлениеНДСпоОбъектамНедвижимости");
	МакетыРасшифровки.Вставить("П000310008002", "ВосстановлениеНДСпоОбъектамНедвижимости");
	МакетыРасшифровки.Вставить("П000310008003", "ВосстановлениеНДСпоОбъектамНедвижимости");
	МакетыРасшифровки.Вставить("П000310008004", "ВосстановлениеНДСпоОбъектамНедвижимости");
	
	// Раздел 4
	МакетыРасшифровки.Вставить("П000400002003", "РасшифровкаНачислениеРеализация0"); 
	МакетыРасшифровки.Вставить("П000400003003", "РасшифровкаВычетПоПриобретеннымЦенностям0");
	// Раздел 6
	МакетыРасшифровки.Вставить("П000600002003", "РасшифровкаНачислениеРеализация0");
	МакетыРасшифровки.Вставить("П000600003003", "РасшифровкаНачислениеРеализация0");
	МакетыРасшифровки.Вставить("П000600004003", "РасшифровкаВычетПоПриобретеннымЦенностям0");
	
	Возврат МакетыРасшифровки;
	
КонецФункции

Функция МакетыРасшифровкиПолей2017Кв1(Организация, Период)
	
	МакетыРасшифровки = Новый Соответствие();
	
	// Раздел 2: начисления при исполнении обязанностей налогового агента.
	МакетыРасшифровки.Вставить("П000200006003", "РасшифровкаНачислениеИсполнениеОНА");
	МакетыРасшифровки.Вставить("П000200008003", "РасшифровкаНачислениеИсполнениеОНА");
	МакетыРасшифровки.Вставить("П000200009003", "РасшифровкаНачислениеИсполнениеОНА");
	
	// Раздел 3
	// Начисления по реализации.
	МакетыРасшифровки.Вставить("П000300001003", "РасшифровкаНачислениеРеализацияСложныйУчетНДС");
	МакетыРасшифровки.Вставить("П000300001005", "РасшифровкаНачислениеРеализацияСложныйУчетНДС");
	МакетыРасшифровки.Вставить("П000300002003", "РасшифровкаНачислениеРеализацияСложныйУчетНДС");
	МакетыРасшифровки.Вставить("П000300002005", "РасшифровкаНачислениеРеализацияСложныйУчетНДС");
	МакетыРасшифровки.Вставить("П000300003003", "РасшифровкаНачислениеРеализацияСложныйУчетНДС");
	МакетыРасшифровки.Вставить("П000300003005", "РасшифровкаНачислениеРеализацияСложныйУчетНДС");
	МакетыРасшифровки.Вставить("П000300004003", "РасшифровкаНачислениеРеализацияСложныйУчетНДС");
	МакетыРасшифровки.Вставить("П000300004005", "РасшифровкаНачислениеРеализацияСложныйУчетНДС");
	
	// Начисления при выполнении СМР хоз. способом.
	МакетыРасшифровки.Вставить("П000300006003", "РасшифровкаНачислениеСМРХозСпособом");
	МакетыРасшифровки.Вставить("П000300006005", "РасшифровкаНачислениеСМРХозСпособом");
	// Начисления по авансам полученным.
	МакетыРасшифровки.Вставить("П000300007003", "РасшифровкаНачислениеАвансыПолученныеУпрощенныйУчетНДС");
	МакетыРасшифровки.Вставить("П000300007005", "РасшифровкаНачислениеАвансыПолученныеУпрощенныйУчетНДС");
	// Восстановление (начисление) по ранее выданным авансам.
	МакетыРасшифровки.Вставить("П000300009005", "РасшифровкаНачислениеВосстановлениеСАвансовВыданныхУпрощенныйУчетНДС");
	
	// В декларации с 1 кв 2015 года в строке 120 отображаются сведения, аналогичные строке 130
	// декларации до 1 кв 2015 года, поэтому для расшифровки строки 120 используется набор, 
	// который расшифровывает строку 130 прежней формы декларации.
	МакетыРасшифровки.Вставить("П000300012003", "РасшифровкаСтроки130Декларации");
	// Вычет с авансов выданных.
	МакетыРасшифровки.Вставить("П000300013003", "РасшифровкаВычетАвансыВыданныеУпрощенныйУчетНДС");
	// Вычет по СМР хоз. способом.
	МакетыРасшифровки.Вставить("П000300014003", "РасшифровкаВычетПриобретенныеЦенности");
	// Вычет по импорту.
	МакетыРасшифровки.Вставить("П000300015003", "РасшифровкаВычетПриобретенныеЦенности");
	// Вычет НДС при ввозе из Таможенного союза.
	МакетыРасшифровки.Вставить("П000300016003", "РасшифровкаВычетПриобретенныеЦенности");
	// Вычет с авансов полученных (восстановление)
	МакетыРасшифровки.Вставить("П000300017003", "РасшифровкаВычетСАвансовПолученныхУпрощенныйУчетНДС");
	// Вычет при исполнении обязанностей налогового агента.
	МакетыРасшифровки.Вставить("П000300018003", "РасшифровкаВычетИсполнениеОНА");
	
	// Раздел 3 приложение 1: восстановление НДС по объектам недвижимости.
	МакетыРасшифровки.Вставить("П000310007001", "ВосстановлениеНДСпоОбъектамНедвижимости");
	МакетыРасшифровки.Вставить("П000310007002", "ВосстановлениеНДСпоОбъектамНедвижимости");
	МакетыРасшифровки.Вставить("П000310007003", "ВосстановлениеНДСпоОбъектамНедвижимости");
	МакетыРасшифровки.Вставить("П000310007004", "ВосстановлениеНДСпоОбъектамНедвижимости");
	
	// Раздел 4
	МакетыРасшифровки.Вставить("П0004М1002003", "РасшифровкаНачислениеРеализация0"); 
	МакетыРасшифровки.Вставить("П0004М1003003", "РасшифровкаВычетПоПриобретеннымЦенностям0");
	// Раздел 6
	МакетыРасшифровки.Вставить("П0006М1002003", "РасшифровкаНачислениеРеализация0");
	МакетыРасшифровки.Вставить("П0006М1003003", "РасшифровкаНачислениеРеализация0");
	МакетыРасшифровки.Вставить("П0006М1004003", "РасшифровкаВычетПоПриобретеннымЦенностям0");
	
	Возврат МакетыРасшифровки;
	
КонецФункции

Функция НовыйДополнительныеПараметрыРасшифровкиИОтбор(ИдентификаторМакета)
	
	ДополнительныеПараметры = Новый Структура;
	
	ДополнительныеПараметры.Вставить("ИдентификаторМакета", ИдентификаторМакета);
	
	ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	
	ДополнительныеПараметры.Вставить("ПользовательскиеНастройки", ПользовательскиеНастройки);
	
	Отбор = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
	Отбор.ИдентификаторПользовательскойНастройки = "Отбор";
	
	Возврат Новый Структура("ДополнительныеПараметры, Отбор", ДополнительныеПараметры, Отбор);
	
КонецФункции

Процедура ДобавитьЗначениеВОтбор(Отбор, ЛевоеЗначение, ВидСравнения, ПравоеЗначение)
	
	ЭлементОтбора = Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	
	ЭлементОтбора.ЛевоеЗначение    = ЛевоеЗначение;
	ЭлементОтбора.ВидСравнения     = ВидСравнения;
	ЭлементОтбора.ПравоеЗначение   = ПравоеЗначение;
	ЭлементОтбора.Использование    = Истина;
	ЭлементОтбора.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ;
	
КонецПроцедуры  

#КонецОбласти

Функция ПустаяСтруктураПараметровЗаполнения()
	
	СтруктураЗаполнения = Новый Структура();
	СтруктураЗаполнения.Вставить("КодОперации", "");
	СтруктураЗаполнения.Вставить("ПодтвержденнаяНалоговаяБаза", 0);
	СтруктураЗаполнения.Вставить("ВычетНДС", 0);
	СтруктураЗаполнения.Вставить("НачислениеНДС", 0);
	Возврат СтруктураЗаполнения;
	
КонецФункции

#КонецОбласти

#Область ЭлектронноеПредставлениеКнигЖурнала

Процедура ВывестиУзелКнигиПокупок501_Всего(КомпоновщикXML, ВыгружаемыйДокумент)
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Всего");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "СтТовУчНалВсего", ВыгружаемыйДокумент.ВсегоПокупок, "Ч,17,2");
	
	// НДС 18%.
	Если ВыгружаемыйДокумент.СуммаБезНДС18 <> 0 ИЛИ ВыгружаемыйДокумент.НДС18 <> 0 Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ВтЧисле");
		ВывестиУзелТипа501_СтНалТип(КомпоновщикXML, "18", ВыгружаемыйДокумент.СуммаБезНДС18, ВыгружаемыйДокумент.НДС18);
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	// НДС 10%.
	Если ВыгружаемыйДокумент.СуммаБезНДС10 <> 0 ИЛИ ВыгружаемыйДокумент.НДС10 <> 0 Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ВтЧисле");
		ВывестиУзелТипа501_СтНалТип(КомпоновщикXML, "10", ВыгружаемыйДокумент.СуммаБезНДС10, ВыгружаемыйДокумент.НДС10);
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	// НДС 0%.
	Если ВыгружаемыйДокумент.НДС0 <> 0 Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ВтЧисле");
		ВывестиУзелТипа501_СтНалТип(КомпоновщикXML, "0", ВыгружаемыйДокумент.НДС0);
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	// НДС 20%.
	Если ВыгружаемыйДокумент.СуммаБезНДС20 <> 0 ИЛИ ВыгружаемыйДокумент.НДС20 <> 0 Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ВтЧисле");
		ВывестиУзелТипа501_СтНалТип(КомпоновщикXML, "20", ВыгружаемыйДокумент.СуммаБезНДС20, ВыгружаемыйДокумент.НДС20);
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	// НДС Без НДС.
	Если ВыгружаемыйДокумент.СуммаСовсемБезНДС <> 0 Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ВтЧисле");
		ВывестиУзелТипа501_СтНалТип(КомпоновщикXML, "без НДС", ВыгружаемыйДокумент.СуммаСовсемБезНДС); // Не локализуется
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
КонецПроцедуры

Процедура ВывестиУзелКнигиПокупок501_СвПокупка(КомпоновщикXML, СтрокаСведений)
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвПокупка");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НомПП", СтрокаСведений.Ном, "Ч,12");
	
	РеквизитыСчетаФактуры = РасщепленнаяСтрока(СтрокаСведений.ДатаНомер, ";");
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ДатаСчФ", СокрЛП(РеквизитыСчетаФактуры[0]), "Т,10");
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НомерСчФ", СокрЛП(РеквизитыСчетаФактуры[1]), "Т,1000");
	
	РеквизитыИсправленияСчетаФактуры = РасщепленнаяСтрока(СтрокаСведений.НомерДатаИсправления, ";");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "НомИспрСчФ", СокрЛП(РеквизитыИсправленияСчетаФактуры[0]), "Ч,3");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаИспрСчФ", СокрЛП(РеквизитыИсправленияСчетаФактуры[1]), "Т,10");
	
	РеквизитыКорректировочногоСчетаФактуры = РасщепленнаяСтрока(СтрокаСведений.НомерДатаКорректировки, ";");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "НомерКСчФ", СокрЛП(РеквизитыКорректировочногоСчетаФактуры[0]), "Т,1000");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаКСчФ", СокрЛП(РеквизитыКорректировочногоСчетаФактуры[1]), "Т,10");
	
	РеквизитыИсправленияКорректировочногоСчетаФактуры = РасщепленнаяСтрока(СтрокаСведений.НомерДатаИсправленияКорректировки, ";");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "НомИспрКСчФ", СокрЛП(РеквизитыИсправленияКорректировочногоСчетаФактуры[0]), "Ч,3");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаИспрКСчФ", СокрЛП(РеквизитыИсправленияКорректировочногоСчетаФактуры[1]), "Т,10");
	
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаПринУчет", СтрокаСведений.ДатаОприходования, "Т,10");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НаимПрод", СтрокаСведений.Продавец, "Т,1000");
	ИНН = СокрЛП(СтрокаСведений.ПродавецИНН);
	Если СтрДлина(ИНН) = 12 Тогда
		КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ИННФЛ", ИНН, "Т,12");
	ИНаче
		КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ИННЮЛ", ИНН, "Т,10");
		КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "КПП", СтрокаСведений.ПродавецКПП, "Т,9");
	КонецЕсли;
	
	ПроисхождениеТовара = СтрокаСведений.НомерГТД;
	РеквизитыПроисхожденияТовара = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПроисхождениеТовара, ";");
	
	Если РеквизитыПроисхожденияТовара.Количество() >= 2 Тогда
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "КодПроисх", СокрЛП(РеквизитыПроисхожденияТовара[0]), "Т,3");
	КонецЕсли;
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "СтТовУчНалВсего", СтрокаСведений.ВсегоПокупок, "Ч,19,2");
	
	ДатыОплаты = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаСведений.ДатаОплаты, ",");
	Для Каждого СтрокаДатыОплаты Из ДатыОплаты Цикл
		Если ЗначениеЗаполнено(СтрокаДатыОплаты) Тогда
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ДатаОплСчФПрод");
			
			КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, СокрЛП(СтрокаДатыОплаты), "Т,10");
		КонецЕсли;
	КонецЦикла;
	
	// НДС 18%.
	Если СтрокаСведений.СуммаБезНДС18 <> 0 ИЛИ СтрокаСведений.НДС18 <> 0 Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ВтчСтоимПок");
		ВывестиУзелТипа501_СтНалТип(КомпоновщикXML, "18", СтрокаСведений.СуммаБезНДС18, СтрокаСведений.НДС18);
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	// НДС 10%.
	Если СтрокаСведений.СуммаБезНДС10 <> 0 ИЛИ СтрокаСведений.НДС10 <> 0 Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ВтчСтоимПок");
		ВывестиУзелТипа501_СтНалТип(КомпоновщикXML, "10", СтрокаСведений.СуммаБезНДС10, СтрокаСведений.НДС10);
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	// НДС 0%.
	Если СтрокаСведений.НДС0 <> 0 Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ВтчСтоимПок");
		ВывестиУзелТипа501_СтНалТип(КомпоновщикXML, "0", СтрокаСведений.НДС0);
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	// НДС 20%.
	Если СтрокаСведений.СуммаБезНДС20 <> 0 ИЛИ СтрокаСведений.НДС20 <> 0 Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ВтчСтоимПок");
		ВывестиУзелТипа501_СтНалТип(КомпоновщикXML, "20", СтрокаСведений.СуммаБезНДС20, СтрокаСведений.НДС20);
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	// НДС Без НДС.
	Если СтрокаСведений.СуммаСовсемБезНДС <> 0 Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ВтчСтоимПок");
		ВывестиУзелТипа501_СтНалТип(КомпоновщикXML, "без НДС", СтрокаСведений.СуммаСовсемБезНДС); // Не локализуется
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	Если РеквизитыПроисхожденияТовара.Количество() >= 2 Тогда
		Для Инд = 1 По РеквизитыПроисхожденияТовара.Количество() - 1 Цикл
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "НомерТД");
			
			КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, СокрЛП(РеквизитыПроисхожденияТовара[Инд]), "Т,29");
		КонецЦикла;
	КонецЕсли;
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
КонецПроцедуры

Процедура ВывестиДокументДопЛистаКнигиПокупок_502(КомпоновщикXML, ПараметрыВыгрузки, ЛистКниги)
	
	ОбщиеСведенияЛиста = ЛистКниги.ОбщиеСведения;
	ЗаписиЛиста = ЛистКниги.Записи;
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Документ");
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "КНД", ПараметрыВыгрузки.КНД);
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвПокуп");
	
	Если ПараметрыВыгрузки.ЭтоИП Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СведИП");
		
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИННФЛ", ПараметрыВыгрузки.ИННФЛ, "Т,12");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "СвГосРегИП", ПараметрыВыгрузки.СвГосРегИП, "Т,100");
		
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ФИОИП");
		
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Фамилия", ПараметрыВыгрузки.ФамилияИП, "Т,60");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Имя", ПараметрыВыгрузки.ИмяИП, "Т,60");
		КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "Отчество", ПараметрыВыгрузки.ОтчествоИП, "Т,60");
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		
	Иначе
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СведЮЛ");
		
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НаимОрг", ПараметрыВыгрузки.НаимОрг, "Т,1000");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИННЮЛ", ПараметрыВыгрузки.ИННЮЛ, "Т,10");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "КПП", ПараметрыВыгрузки.КПП, "Т,9");
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		
	КонецЕсли;
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвДопЛКнПок");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НомерДопЛКнПок", ОбщиеСведенияЛиста.НомерДопЛКнПок);
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Период", ОбщиеСведенияЛиста.Период);
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ОтчетГод", ОбщиеСведенияЛиста.ОтчетГод);
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ДатаДопЛКнПок", ОбщиеСведенияЛиста.ДатаДопЛКнПок);
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Итого");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "СумНДСВыч", ОбщиеСведенияЛиста.Итого.СумНДСВыч, "Ч,19,2");
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Всего");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "СумНДСВыч", ОбщиеСведенияЛиста.Всего.СумНДСВыч, "Ч,19,2");
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
	Для Каждого СтрокаСведений Из ЗаписиЛиста Цикл
		ВывестиСвПокупкаДопЛистаКнигиПокупок_502(КомпоновщикXML, СтрокаСведений);
	КонецЦикла;
	
	ВывестиУзел_Подписант(КомпоновщикXML, ПараметрыВыгрузки);
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
КонецПроцедуры

Процедура ВывестиСвПокупкаДопЛистаКнигиПокупок_502(КомпоновщикXML, СтрокаСведений)
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвПокупка");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НомПП", СтрокаСведений.НомПП, "Ч,12");
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НомерСчФ", СтрокаСведений.НомерСчФ, "Т,1000");
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ДатаСчФ", СтрокаСведений.ДатаСчФ, "Т,10");
	
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "НомИспрСчФ",  СтрокаСведений.НомИспрСчФ,  "Ч,3");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаИспрСчФ", СтрокаСведений.ДатаИспрСчФ, "Т,10");
	
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "НомерКСчФ", СтрокаСведений.НомерКСчФ, "Т,1000");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаКСчФ",  СтрокаСведений.ДатаКСчФ,  "Т,10");
	
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "НомИспрКСчФ",  СтрокаСведений.НомИспрКСчФ,  "Ч,3");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаИспрКСчФ", СтрокаСведений.ДатаИспрКСчФ, "Т,10");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НаимПрод", СтрокаСведений.НаимПрод, "Т,1000");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ИННЮЛ", СтрокаСведений.ИННЮЛ, "Т,10");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "КПП",   СтрокаСведений.КПП,   "Т,9");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ИННФЛ", СтрокаСведений.ИННФЛ, "Т,12");
	
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "КодОКВ", СтрокаСведений.КодОКВ, "Ч,3");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "СтТовУчНалВсего", СтрокаСведений.СтТовУчНалВсего, "Ч,19,2");
	
	Для Каждого КодВидаОперации Из СтрокаСведений.КодВидОпер Цикл
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "КодВидОпер");
		
		КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, КодВидаОперации);
	КонецЦикла;
	
	Для Каждого ДокументПодтверждения Из СтрокаСведений.СвДокУплНал Цикл
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвДокУплНал");
		
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НомерДокУплНал", ДокументПодтверждения.НомерДокУплНал, "Т,256");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ДатаДокУплНал", ДокументПодтверждения.ДатаДокУплНал, "Т,10");
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЦикла;
	
	Для Каждого ДатаПринятияНаУчет Из СтрокаСведений.ДатаПринУчет Цикл
		ДатаПринятияНаУчет = Лев(СокрЛП(ДатаПринятияНаУчет), 10);
		
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ДатаПринУчет");
		
		КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, ДатаПринятияНаУчет);
	КонецЦикла;
	
	Если СтрокаСведений.СвПосредн <> Неопределено Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвПосредн");
		
		Если СтрокаСведений.СвПосредн.Свойство("ИННЮЛ") Тогда
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СведЮЛ");
			
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НаимОрг", СтрокаСведений.СвПосредн.НаимОрг, "Т,1000");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИННЮЛ", СтрокаСведений.СвПосредн.ИННЮЛ, "Т,10");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "КПП", СтрокаСведений.СвПосредн.КПП, "Т,9");
			
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		Иначе
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СведИП");
			
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИННФЛ", СтрокаСведений.СвПосредн.ИННФЛ, "Т,12");
			
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ФИОИП");
			
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Фамилия", СтрокаСведений.СвПосредн.Фамилия, "Т,60");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Имя", СтрокаСведений.СвПосредн.Имя, "Т,60");
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "Отчество", СтрокаСведений.СвПосредн.Отчество, "Т,60");
			
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
			
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		КонецЕсли;
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	Для Каждого НомерТД Из СтрокаСведений.НомерТД Цикл
		НомерТД = Лев(СокрЛП(НомерТД), 29);
		
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "НомерТД");
		
		КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, НомерТД);
	КонецЦикла;
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СумНДСВыч");
	
	Если ТипЗнч(СтрокаСведений.СумНДСВыч) = Тип("Число") Тогда
		СумНДСВыч = Формат(СтрокаСведений.СумНДСВыч,"ЧЦ=19; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0");
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СумНДС");
		КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, СумНДСВыч);
	Иначе
		СумНДСВыч = Лев(СокрЛП(СтрокаСведений.СумНДСВыч), 7);
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "БезНДС");
		КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, СумНДСВыч);
	КонецЕсли;
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
КонецПроцедуры

Процедура ВывестиУзелКнигиПродаж501_Всего(КомпоновщикXML, ВыгружаемыйДокумент)
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Всего");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "СтТовУчНалВсего", ВыгружаемыйДокумент.ВсегоПродаж, "Ч,17,2");
	
	// НДС 18%.
	Если ВыгружаемыйДокумент.СуммаБезНДС18 <> 0 ИЛИ ВыгружаемыйДокумент.НДС18 <> 0 Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ВтЧисле");
		ВывестиУзелТипа501_СтНалТип(КомпоновщикXML, "18", ВыгружаемыйДокумент.СуммаБезНДС18, ВыгружаемыйДокумент.НДС18);
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	// НДС 10%.
	Если ВыгружаемыйДокумент.СуммаБезНДС10 <> 0 ИЛИ ВыгружаемыйДокумент.НДС10 <> 0 Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ВтЧисле");
		ВывестиУзелТипа501_СтНалТип(КомпоновщикXML, "10", ВыгружаемыйДокумент.СуммаБезНДС10, ВыгружаемыйДокумент.НДС10);
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	// НДС 0%.
	Если ВыгружаемыйДокумент.НДС0 <> 0 Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ВтЧисле");
		ВывестиУзелТипа501_СтНалТип(КомпоновщикXML, "0", ВыгружаемыйДокумент.НДС0);
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	// НДС 20%.
	Если ВыгружаемыйДокумент.СуммаБезНДС20 <> 0 ИЛИ ВыгружаемыйДокумент.НДС20 <> 0 Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ВтЧисле");
		ВывестиУзелТипа501_СтНалТип(КомпоновщикXML, "20", ВыгружаемыйДокумент.СуммаБезНДС20, ВыгружаемыйДокумент.НДС20);
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	// НДС Без НДС.
	Если ВыгружаемыйДокумент.СуммаСовсемБезНДС <> 0 Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ВтЧисле");
		ВывестиУзелТипа501_СтНалТип(КомпоновщикXML, "без НДС", ВыгружаемыйДокумент.СуммаСовсемБезНДС); // Не локализуется
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
КонецПроцедуры

Процедура ВывестиУзелКнигиПродаж501_СвПродаж(КомпоновщикXML, СтрокаСведений)
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвПродаж");
	
	РеквизитыСчетаФактуры = РасщепленнаяСтрока(СтрокаСведений.ДатаНомер, ";");
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ДатаСчФ", СокрЛП(РеквизитыСчетаФактуры[0]), "Т,10");
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НомерСчФ", СокрЛП(РеквизитыСчетаФактуры[1]), "Т,1000");
	
	РеквизитыИсправленияСчетаФактуры = РасщепленнаяСтрока(СтрокаСведений.НомерДатаИсправления, ";");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "НомИспрСчФ", СокрЛП(РеквизитыИсправленияСчетаФактуры[0]), "Ч,3");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаИспрСчФ", СокрЛП(РеквизитыИсправленияСчетаФактуры[1]), "Т,10");
	
	РеквизитыКорректировочногоСчетаФактуры = РасщепленнаяСтрока(СтрокаСведений.НомерДатаКорректировки, ";");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "НомерКСчФ", СокрЛП(РеквизитыКорректировочногоСчетаФактуры[0]), "Т,1000");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаКСчФ", СокрЛП(РеквизитыКорректировочногоСчетаФактуры[1]), "Т,10");
	
	РеквизитыИсправленияКорректировочногоСчетаФактуры = РасщепленнаяСтрока(СтрокаСведений.НомерДатаИсправленияКорректировки, ";");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "НомИспрКСчФ", СокрЛП(РеквизитыИсправленияКорректировочногоСчетаФактуры[0]), "Ч,3");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаИспрКСчФ", СокрЛП(РеквизитыИсправленияКорректировочногоСчетаФактуры[1]), "Т,10");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НаимПок", СтрокаСведений.Покупатель, "Т,1000");
	ИНН = СокрЛП(СтрокаСведений.ПокупательИНН);
	Если СтрДлина(ИНН) = 12 Тогда
		КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ИННФЛ", ИНН, "Т,12");
	ИНаче
		КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ИННЮЛ", ИНН, "Т,10");
		КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "КПП", СтрокаСведений.ПокупательКПП, "Т,9");
	КонецЕсли;
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "СтТовУчНалВсего", СтрокаСведений.ВсегоПродаж, "Ч,19,2");
	
	ДатыОплаты = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаСведений.ДатаОплаты, ",");
	Для Каждого СтрокаДатыОплаты Из ДатыОплаты Цикл
		Если ЗначениеЗаполнено(СтрокаДатыОплаты) Тогда
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ДатаОплСчФПрод");
			
			КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, СокрЛП(СтрокаДатыОплаты), "Т,10");
		КонецЕсли;
	КонецЦикла;
	
	// НДС 18%.
	Если СтрокаСведений.СуммаБезНДС18 <> 0 ИЛИ СтрокаСведений.НДС18 <> 0 Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ВтЧисле");
		ВывестиУзелТипа501_СтНалТип(КомпоновщикXML, "18", СтрокаСведений.СуммаБезНДС18, СтрокаСведений.НДС18);
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	// НДС 10%.
	Если СтрокаСведений.СуммаБезНДС10 <> 0 ИЛИ СтрокаСведений.НДС10 <> 0 Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ВтЧисле");
		ВывестиУзелТипа501_СтНалТип(КомпоновщикXML, "10", СтрокаСведений.СуммаБезНДС10, СтрокаСведений.НДС10);
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	// НДС 0%.
	Если СтрокаСведений.НДС0 <> 0 Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ВтЧисле");
		ВывестиУзелТипа501_СтНалТип(КомпоновщикXML, "0", СтрокаСведений.НДС0);
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	// НДС 20%.
	Если СтрокаСведений.СуммаБезНДС20 <> 0 ИЛИ СтрокаСведений.НДС20 <> 0 Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ВтЧисле");
		ВывестиУзелТипа501_СтНалТип(КомпоновщикXML, "20", СтрокаСведений.СуммаБезНДС20, СтрокаСведений.НДС20);
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	// НДС Без НДС.
	Если СтрокаСведений.СуммаСовсемБезНДС <> 0 Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ВтЧисле");
		ВывестиУзелТипа501_СтНалТип(КомпоновщикXML, "без НДС", СтрокаСведений.СуммаСовсемБезНДС); // Не локализуется
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
КонецПроцедуры

Процедура ВывестиДокументДопЛистаКнигиПродаж_502(КомпоновщикXML, ПараметрыВыгрузки, ЛистКниги)
	
	ОбщиеСведенияЛиста = ЛистКниги.ОбщиеСведения;
	ЗаписиЛиста = ЛистКниги.Записи;
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Документ");
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "КНД", ПараметрыВыгрузки.КНД);
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвПродав");
	
	Если ПараметрыВыгрузки.ЭтоИП Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СведИП");
		
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИННФЛ", ПараметрыВыгрузки.ИННФЛ, "Т,12");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "СвГосРегИП", ПараметрыВыгрузки.СвГосРегИП, "Т,100");
		
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ФИОИП");
		
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Фамилия", ПараметрыВыгрузки.ФамилияИП, "Т,60");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Имя", ПараметрыВыгрузки.ИмяИП, "Т,60");
		КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "Отчество", ПараметрыВыгрузки.ОтчествоИП, "Т,60");
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		
	Иначе
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СведЮЛ");
		
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НаимОрг", ПараметрыВыгрузки.НаимОрг, "Т,1000");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИННЮЛ", ПараметрыВыгрузки.ИННЮЛ, "Т,10");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "КПП", ПараметрыВыгрузки.КПП, "Т,9");
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		
	КонецЕсли;
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвДопЛКнПрод");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НомерДопЛКнПрод", ОбщиеСведенияЛиста.НомерДопЛКнПрод);
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Период", ОбщиеСведенияЛиста.Период);
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ОтчетГод", ОбщиеСведенияЛиста.ОтчетГод);
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ДатаДопЛКнПрод", ОбщиеСведенияЛиста.ДатаДопЛКнПрод);
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Итого");
	
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СтТовРубКоп18",      ОбщиеСведенияЛиста.Итого.СтТовРубКоп18, "Ч,19,2");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СтТовРубКоп10",      ОбщиеСведенияЛиста.Итого.СтТовРубКоп10, "Ч,19,2");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СтТовРубКоп0",       ОбщиеСведенияЛиста.Итого.СтТовРубКоп0, "Ч,19,2");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СумНДСРубКоп18",     ОбщиеСведенияЛиста.Итого.СумНДСРубКоп18, "Ч,19,2");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СумНДСРубКоп10",     ОбщиеСведенияЛиста.Итого.СумНДСРубКоп10, "Ч,19,2");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СтПродОсвНДСРубКоп", ОбщиеСведенияЛиста.Итого.СтПродОсвНДСРубКоп, "Ч,19,2");
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Всего");
	
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СтТовРубКоп18",      ОбщиеСведенияЛиста.Всего.СтТовРубКоп18, "Ч,19,2");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СтТовРубКоп10",      ОбщиеСведенияЛиста.Всего.СтТовРубКоп10, "Ч,19,2");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СтТовРубКоп0",       ОбщиеСведенияЛиста.Всего.СтТовРубКоп0, "Ч,19,2");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СумНДСРубКоп18",     ОбщиеСведенияЛиста.Всего.СумНДСРубКоп18, "Ч,19,2");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СумНДСРубКоп10",     ОбщиеСведенияЛиста.Всего.СумНДСРубКоп10, "Ч,19,2");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СтПродОсвНДСРубКоп", ОбщиеСведенияЛиста.Всего.СтПродОсвНДСРубКоп, "Ч,19,2");
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
	Для Каждого СтрокаСведений Из ЗаписиЛиста Цикл
		ВывестиСвПродажДопЛистаКнигиПродаж_502(КомпоновщикXML, СтрокаСведений);
	КонецЦикла;
	
	ВывестиУзел_Подписант(КомпоновщикXML, ПараметрыВыгрузки);
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
КонецПроцедуры

Процедура ВывестиСвПродажДопЛистаКнигиПродаж_502(КомпоновщикXML, СтрокаСведений)
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвПродаж");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НомПП", СтрокаСведений.НомПП, "Ч,12");
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НомерСчФ", СтрокаСведений.НомерСчФ, "Т,1000");
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ДатаСчФ", СтрокаСведений.ДатаСчФ, "Т,10");
	
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "НомИспрСчФ",  СтрокаСведений.НомИспрСчФ,  "Ч,3");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаИспрСчФ", СтрокаСведений.ДатаИспрСчФ, "Т,10");
	
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "НомерКСчФ", СтрокаСведений.НомерКСчФ, "Т,1000");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаКСчФ",  СтрокаСведений.ДатаКСчФ,  "Т,10");
	
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "НомИспрКСчФ",  СтрокаСведений.НомИспрКСчФ,  "Ч,3");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаИспрКСчФ", СтрокаСведений.ДатаИспрКСчФ, "Т,10");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НаимПок", СтрокаСведений.НаимПок, "Т,1000");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ИННЮЛ", СтрокаСведений.ИННЮЛ, "Т,10");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "КПП",   СтрокаСведений.КПП,   "Т,9");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ИННФЛ", СтрокаСведений.ИННФЛ, "Т,12");
	
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "КодОКВ", СтрокаСведений.КодОКВ, "Ч,3");
	
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СтТовУчНалВсего",    СтрокаСведений.СтТовУчНалВсего,    "Ч,19,2");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СтТовУчНалРубКоп",   СтрокаСведений.СтТовУчНалРубКоп,   "Ч,19,2");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СтТовРубКоп18",      СтрокаСведений.СтТовРубКоп18,      "Ч,19,2");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СтТовРубКоп10",      СтрокаСведений.СтТовРубКоп10,      "Ч,19,2");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СтТовРубКоп0",       СтрокаСведений.СтТовРубКоп0,       "Ч,19,2");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СумНДСРубКоп18",     СтрокаСведений.СумНДСРубКоп18,     "Ч,19,2");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СумНДСРубКоп10",     СтрокаСведений.СумНДСРубКоп10,     "Ч,19,2");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СтПродОсвНДСРубКоп", СтрокаСведений.СтПродОсвНДСРубКоп, "Ч,19,2");
	
	Для Каждого КодВидаОперации Из СтрокаСведений.КодВидОпер Цикл
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "КодВидОпер");
		
		КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, КодВидаОперации);
	КонецЦикла;
	
	Если СтрокаСведений.СвПосредн <> Неопределено Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвПосредн");
		
		Если СтрокаСведений.СвПосредн.Свойство("ИННЮЛ") Тогда
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СведЮЛ");
			
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НаимОрг", СтрокаСведений.СвПосредн.НаимОрг, "Т,1000");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИННЮЛ", СтрокаСведений.СвПосредн.ИННЮЛ, "Т,10");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "КПП", СтрокаСведений.СвПосредн.КПП, "Т,9");
			
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		Иначе
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СведИП");
			
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИННФЛ", СтрокаСведений.СвПосредн.ИННФЛ, "Т,12");
			
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ФИОИП");
			
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Фамилия", СтрокаСведений.СвПосредн.Фамилия, "Т,60");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Имя", СтрокаСведений.СвПосредн.Имя, "Т,60");
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "Отчество", СтрокаСведений.СвПосредн.Отчество, "Т,60");
			
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
			
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		КонецЕсли;
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	Для Каждого ДокументПодтверждения Из СтрокаСведений.СвДокПдтвОпл Цикл
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвДокПдтвОпл");
		
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НомерДокПдтвОпл", ДокументПодтверждения.НомерДокПдтвОпл, "Т,256");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ДатаДокПдтвОпл", ДокументПодтверждения.ДатаДокПдтвОпл, "Т,10");
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЦикла;
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
КонецПроцедуры

Процедура ВывестиУзелЖурналаСчетовФактур501_СвЧ1(КомпоновщикXML, СтрокаСведений)
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвЧ1");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НомПП", СтрокаСведений.Ном, "Ч,6");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаВыстСчФ", СтрокаСведений.ДатаПередачиПолучения, "Т,10");
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "КодСпосВыст", СтрокаСведений.КодСпособаВыставления, "Т,1");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НомерСчФ", СтрокаСведений.НомерСчетаФактуры, "Т,256");
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ДатаСчФ", СтрокаСведений.ДатаСчетаФактуры, "Т,10");
	
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "НомерКСчФ", СтрокаСведений.НомерКорректировочногоСчетаФактуры, "Т,256");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаКСчФ", СтрокаСведений.ДатаКорректировочногоСчетаФактуры, "Т,10");
	
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "НомИспрСчФ", СтрокаСведений.НомерИсправления, "Т,3");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаИспрСчФ", СтрокаСведений.ДатаИсправления, "Т,10");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НаимПок", СтрокаСведений.КонтрагентНаименование, "Т,1000");
	РеквизитыКонтрагента = РасщепленнаяСтрока(СтрокаСведений.КонтрагентИННКПП, "/");
	ИНН = СокрЛП(РеквизитыКонтрагента[0]);
	КПП = СокрЛП(РеквизитыКонтрагента[1]);
	Если СтрДлина(ИНН) = 12 Тогда
		КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ИННФЛ", ИНН, "Т,12");
	ИНаче
		КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ИННЮЛ", ИНН, "Т,10");
		КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "КПП", КПП, "Т,9");
	КонецЕсли;
	
	РеквизитыВалюты = РасщепленнаяСтрока(СтрокаСведений.Валюта, ",");
	КодОКВ = СокрЛП(РеквизитыВалюты[1]);
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "КодОКВ", КодОКВ, "Т,3");
	
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СтТовУчНалВсего", СтрокаСведений.СуммаДокумента, "Ч,17,2");
	
	КодыОпераций = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаСведений.КодВидаОперации, ";");
	Для Каждого КодОперации Из КодыОпераций Цикл
		Если ЗначениеЗаполнено(КодыОпераций) Тогда
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "КодВидОпер");
			КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, СокрЛП(КодОперации), "Т,2");
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(СтрокаСведений.СуммаНДС) Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ВтчСумНДС");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "СумНДС", СтрокаСведений.СуммаНДС, "Ч,17,2");
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаСведений.СуммаДокументаРазницаУменьшение)
	 ИЛИ ЗначениеЗаполнено(СтрокаСведений.СуммаДокументаРазницаУвеличение) Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "РазнСтоимКСчФ");
		
		Если ЗначениеЗаполнено(СтрокаСведений.СуммаДокументаРазницаУменьшение) Тогда
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Ум");
			КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, СтрокаСведений.СуммаДокументаРазницаУменьшение, "Ч,17,2");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаСведений.СуммаДокументаРазницаУвеличение) Тогда
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Увел");
			КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, СтрокаСведений.СуммаДокументаРазницаУменьшение, "Ч,17,2");
		КонецЕсли;
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаСведений.СуммаНДСРазницаУменьшение)
	 ИЛИ ЗначениеЗаполнено(СтрокаСведений.СуммаНДСРазницаУвеличение) Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "РазнНДСКСчФ");
		
		Если ЗначениеЗаполнено(СтрокаСведений.СуммаНДСРазницаУменьшение) Тогда
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Ум");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "СумНДС", СтрокаСведений.СуммаНДСРазницаУменьшение, "Ч,17,2");
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаСведений.СуммаНДСРазницаУвеличение) Тогда
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Увел");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "СумНДС", СтрокаСведений.СуммаНДСРазницаУвеличение, "Ч,17,2");
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		КонецЕсли;
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
КонецПроцедуры

Процедура ВывестиУзелЖурналаСчетовФактур501_СвЧ2(КомпоновщикXML, СтрокаСведений)
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвЧ2");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НомПП", СтрокаСведений.Ном, "Ч,6");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаПолСчФ", СтрокаСведений.ДатаПередачиПолучения, "Т,10");
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "КодСпосВыст", СтрокаСведений.КодСпособаВыставления, "Т,1");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НомерСчФ", СтрокаСведений.НомерСчетаФактуры, "Т,256");
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ДатаСчФ", СтрокаСведений.ДатаСчетаФактуры, "Т,10");
	
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "НомерКСчФ", СтрокаСведений.НомерКорректировочногоСчетаФактуры, "Т,256");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаКСчФ", СтрокаСведений.ДатаКорректировочногоСчетаФактуры, "Т,10");
	
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "НомИспрСчФ", СтрокаСведений.НомерИсправления, "Т,3");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаИспрСчФ", СтрокаСведений.ДатаИсправления, "Т,10");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НаимПрод", СтрокаСведений.КонтрагентНаименование, "Т,1000");
	РеквизитыКонтрагента = РасщепленнаяСтрока(СтрокаСведений.КонтрагентИННКПП, "/");
	ИНН = СокрЛП(РеквизитыКонтрагента[0]);
	КПП = СокрЛП(РеквизитыКонтрагента[1]);
	Если СтрДлина(ИНН) = 12 Тогда
		КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ИННФЛ", ИНН, "Т,12");
	ИНаче
		КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ИННЮЛ", ИНН, "Т,10");
		КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "КПП", КПП, "Т,9");
	КонецЕсли;
	
	РеквизитыВалюты = РасщепленнаяСтрока(СтрокаСведений.Валюта, ",");
	КодОКВ = СокрЛП(РеквизитыВалюты[1]);
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "КодОКВ", КодОКВ, "Т,3");
	
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СтТовУчНалВсего", СтрокаСведений.СуммаДокумента, "Ч,17,2");
	
	КодыОпераций = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаСведений.КодВидаОперации, ";");
	Для Каждого КодОперации Из КодыОпераций Цикл
		Если ЗначениеЗаполнено(КодыОпераций) Тогда
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "КодВидОпер");
			КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, СокрЛП(КодОперации), "Т,2");
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(СтрокаСведений.СуммаНДС) Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ВтчСумНДС");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "СумНДС", СтрокаСведений.СуммаНДС, "Ч,17,2");
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаСведений.СуммаДокументаРазницаУменьшение)
	 ИЛИ ЗначениеЗаполнено(СтрокаСведений.СуммаДокументаРазницаУвеличение) Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "РазнСтоимКСчФ");
		
		Если ЗначениеЗаполнено(СтрокаСведений.СуммаДокументаРазницаУменьшение) Тогда
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Ум");
			КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, СтрокаСведений.СуммаДокументаРазницаУменьшение, "Ч,17,2");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаСведений.СуммаДокументаРазницаУвеличение) Тогда
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Увел");
			КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, СтрокаСведений.СуммаДокументаРазницаУменьшение, "Ч,17,2");
		КонецЕсли;
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаСведений.СуммаНДСРазницаУменьшение)
	 ИЛИ ЗначениеЗаполнено(СтрокаСведений.СуммаНДСРазницаУвеличение) Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "РазнНДСКСчФ");
		
		Если ЗначениеЗаполнено(СтрокаСведений.СуммаНДСРазницаУменьшение) Тогда
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Ум");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "СумНДС", СтрокаСведений.СуммаНДСРазницаУменьшение, "Ч,17,2");
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаСведений.СуммаНДСРазницаУвеличение) Тогда
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Увел");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "СумНДС", СтрокаСведений.СуммаНДСРазницаУвеличение, "Ч,17,2");
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		КонецЕсли;
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
КонецПроцедуры

Процедура ВывестиСвЧ1ЖурналаСчетовФактур_502(КомпоновщикXML, СтрокаСведений)
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвЧ1");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НомПП", СтрокаСведений.НомПП, "Ч,12");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаВыстСчФ", СтрокаСведений.ДатаВыстСчФ, "Т,10");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НомерСчФ", СтрокаСведений.НомерСчФ, "Т,1000");
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ДатаСчФ", СтрокаСведений.ДатаСчФ, "Т,10");
	
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "НомерИспрСчФ", СтрокаСведений.НомерИспрСчФ, "Ч,3");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаИспрСчФ",  СтрокаСведений.ДатаИспрСчФ,  "Т,10");
	
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "НомерКСчФ", СтрокаСведений.НомерКСчФ, "Т,1000");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаКСчФ",  СтрокаСведений.ДатаКСчФ,  "Т,10");
	
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "НомерИспрКСчФ", СтрокаСведений.НомерИспрКСчФ, "Ч,3");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаИспрКСчФ",  СтрокаСведений.ДатаИспрКСчФ,  "Т,10");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НаимПок", СтрокаСведений.НаимПок, "Т,1000");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ИННЮЛ", СтрокаСведений.ИННЮЛ, "Т,10");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "КПП",   СтрокаСведений.КПП,   "Т,9");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ИННФЛ", СтрокаСведений.ИННФЛ, "Т,12");
	
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "КодОКВ", СтрокаСведений.КодОКВ, "Ч,3");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "СтТовУчНалВсего", СтрокаСведений.СтТовУчНалВсего,    "Ч,19,2");
	
	Для Каждого КодВидаОперации Из СтрокаСведений.КодВидОпер Цикл
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "КодВидОпер");
		
		КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, КодВидаОперации);
	КонецЦикла;
	
	Если СтрокаСведений.СвПосредн <> Неопределено Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвПосредн");
		
		Если СтрокаСведений.СвПосредн.Свойство("ИННЮЛ") Тогда
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СведЮЛ");
			
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НаимОрг", СтрокаСведений.СвПосредн.НаимОрг, "Т,1000");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИННЮЛ", СтрокаСведений.СвПосредн.ИННЮЛ, "Т,10");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "КПП", СтрокаСведений.СвПосредн.КПП, "Т,9");
			
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НомерСчФпрод", СтрокаСведений.СвПосредн.НомерСчФпрод, "Т,256");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ДатаСчФпрод", СтрокаСведений.СвПосредн.ДатаСчФпрод, "Т,10");
			
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		Иначе
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СведИП");
			
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИННФЛ", СтрокаСведений.СвПосредн.ИННФЛ, "Т,12");
			
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НомерСчФпрод", СтрокаСведений.СвПосредн.НомерСчФпрод, "Т,256");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ДатаСчФпрод", СтрокаСведений.СвПосредн.ДатаСчФпрод, "Т,10");
			
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ФИОИП");
			
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Фамилия", СтрокаСведений.СвПосредн.Фамилия, "Т,60");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Имя", СтрокаСведений.СвПосредн.Имя, "Т,60");
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "Отчество", СтрокаСведений.СвПосредн.Отчество, "Т,60");
			
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
			
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		КонецЕсли;
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ВтчСумНДС");
	
	Если ТипЗнч(СтрокаСведений.ВтчСумНДС) = Тип("Число") Тогда
		ЗначениеУзла = Формат(СтрокаСведений.ВтчСумНДС,"ЧЦ=19; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0");
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СумНДС");
		КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, ЗначениеУзла);
	Иначе
		ЗначениеУзла = Лев(СокрЛП(СтрокаСведений.ВтчСумНДС), 7);
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "БезНДС");
		КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, ЗначениеУзла);
	КонецЕсли;
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
	Если СтрокаСведений.РазнСтоимКСчФ <> Неопределено Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "РазнСтоимКСчФ");
		
		Если СтрокаСведений.РазнСтоимКСчФ.Свойство("Ум")
		   И ЗначениеЗаполнено(СтрокаСведений.РазнСтоимКСчФ.Ум) Тогда
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Ум");
			
			ЗначениеУзла = Формат(СтрокаСведений.РазнСтоимКСчФ.Ум,"ЧЦ=19; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0");
			
			КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, ЗначениеУзла);
		КонецЕсли;
		
		Если СтрокаСведений.РазнСтоимКСчФ.Свойство("Увел")
		   И ЗначениеЗаполнено(СтрокаСведений.РазнСтоимКСчФ.Увел) Тогда
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Увел");
			
			ЗначениеУзла = Формат(СтрокаСведений.РазнСтоимКСчФ.Увел,"ЧЦ=19; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0");
			
			КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, ЗначениеУзла);
		КонецЕсли;
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	Если СтрокаСведений.РазнНДСКСчФ <> Неопределено Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "РазнНДСКСчФ");
		
		Если СтрокаСведений.РазнНДСКСчФ.Свойство("Ум")
		   И ЗначениеЗаполнено(СтрокаСведений.РазнНДСКСчФ.Ум) Тогда
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Ум");
			
			Если ТипЗнч(СтрокаСведений.РазнНДСКСчФ.Ум) = Тип("Число") Тогда
				ЗначениеУзла = Формат(СтрокаСведений.РазнНДСКСчФ.Ум,"ЧЦ=19; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0");
				КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СумНДС");
				КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, ЗначениеУзла);
			Иначе
				ЗначениеУзла = Лев(СокрЛП(СтрокаСведений.РазнНДСКСчФ.Ум), 7);
				КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "БезНДС");
				КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, ЗначениеУзла);
			КонецЕсли;
			
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		КонецЕсли;
		
		Если СтрокаСведений.РазнНДСКСчФ.Свойство("Увел")
		   И ЗначениеЗаполнено(СтрокаСведений.РазнНДСКСчФ.Увел) Тогда
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Увел");
			
			Если ТипЗнч(СтрокаСведений.РазнНДСКСчФ.Увел) = Тип("Число") Тогда
				ЗначениеУзла = Формат(СтрокаСведений.РазнНДСКСчФ.Увел,"ЧЦ=19; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0");
				КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СумНДС");
				КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, ЗначениеУзла);
			Иначе
				ЗначениеУзла = Лев(СокрЛП(СтрокаСведений.РазнНДСКСчФ.Увел), 7);
				КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "БезНДС");
				КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, ЗначениеУзла);
			КонецЕсли;
			
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		КонецЕсли;
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
КонецПроцедуры

Процедура ВывестиСвЧ2ЖурналаСчетовФактур_502(КомпоновщикXML, СтрокаСведений)
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвЧ2");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НомПП", СтрокаСведений.НомПП, "Ч,12");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаПолСчФ", СтрокаСведений.ДатаПолСчФ, "Т,10");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НомерСчФ", СтрокаСведений.НомерСчФ, "Т,1000");
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ДатаСчФ", СтрокаСведений.ДатаСчФ, "Т,10");
	
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "НомерИспрСчФ", СтрокаСведений.НомерИспрСчФ, "Ч,3");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаИспрСчФ",  СтрокаСведений.ДатаИспрСчФ,  "Т,10");
	
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "НомерКСчФ", СтрокаСведений.НомерКСчФ, "Т,1000");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаКСчФ",  СтрокаСведений.ДатаКСчФ,  "Т,10");
	
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "НомерИспрКСчФ", СтрокаСведений.НомерИспрКСчФ, "Ч,3");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ДатаИспрКСчФ",  СтрокаСведений.ДатаИспрКСчФ,  "Т,10");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НаимПрод", СтрокаСведений.НаимПрод, "Т,1000");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ИННЮЛ", СтрокаСведений.ИННЮЛ, "Т,10");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "КПП",   СтрокаСведений.КПП,   "Т,9");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ИННФЛ", СтрокаСведений.ИННФЛ, "Т,12");
	
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "КодОКВ", СтрокаСведений.КодОКВ, "Ч,3");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "СтТовУчНалВсего", СтрокаСведений.СтТовУчНалВсего, "Ч,19,2");
	
	Для Каждого КодВидаОперации Из СтрокаСведений.КодВидОпер Цикл
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "КодВидОпер");
		
		КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, КодВидаОперации);
	КонецЦикла;
	
	Если СтрокаСведений.СвПосредн <> Неопределено Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвПосредн");
		
		Если СтрокаСведений.СвПосредн.Свойство("ИННЮЛСубКом") Тогда
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СведЮЛ");
			
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НаимСубКом", СтрокаСведений.СвПосредн.НаимСубКом, "Т,1000");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИННЮЛСубКом", СтрокаСведений.СвПосредн.ИННЮЛСубКом, "Т,10");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "КППСубКом", СтрокаСведений.СвПосредн.КППСубКом, "Т,9");
			
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "КодВидСдел", СтрокаСведений.СвПосредн.КодВидСдел, "Т,1");
			
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		ИначеЕсли СтрокаСведений.СвПосредн.Свойство("ИННФЛСубКом") Тогда
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СведИП");
			
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "ИННФЛСубКом", СтрокаСведений.СвПосредн.ИННФЛСубКом, "Т,12");
			
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "КодВидСдел", СтрокаСведений.СвПосредн.КодВидСдел, "Т,1");
			
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ФИОИП");
			
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Фамилия", СтрокаСведений.СвПосредн.Фамилия, "Т,60");
			КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Имя", СтрокаСведений.СвПосредн.Имя, "Т,60");
			КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "Отчество", СтрокаСведений.СвПосредн.Отчество, "Т,60");
			
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
			
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		КонецЕсли;
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ВтчСумНДС");
	
	Если ТипЗнч(СтрокаСведений.ВтчСумНДС) = Тип("Число") Тогда
		ЗначениеУзла = Формат(СтрокаСведений.ВтчСумНДС,"ЧЦ=19; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0");
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СумНДС");
		КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, ЗначениеУзла);
	Иначе
		ЗначениеУзла = Лев(СокрЛП(СтрокаСведений.ВтчСумНДС), 7);
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "БезНДС");
		КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, ЗначениеУзла);
	КонецЕсли;
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
	Если СтрокаСведений.РазнСтоимКСчФ <> Неопределено Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "РазнСтоимКСчФ");
		
		Если СтрокаСведений.РазнСтоимКСчФ.Свойство("Ум")
		   И ЗначениеЗаполнено(СтрокаСведений.РазнСтоимКСчФ.Ум) Тогда
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Ум");
			
			ЗначениеУзла = Формат(СтрокаСведений.РазнСтоимКСчФ.Ум,"ЧЦ=19; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0");
			
			КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, ЗначениеУзла);
		КонецЕсли;
		
		Если СтрокаСведений.РазнСтоимКСчФ.Свойство("Увел")
		   И ЗначениеЗаполнено(СтрокаСведений.РазнСтоимКСчФ.Увел) Тогда
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Увел");
			
			ЗначениеУзла = Формат(СтрокаСведений.РазнСтоимКСчФ.Увел,"ЧЦ=19; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0");
			
			КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, ЗначениеУзла);
		КонецЕсли;
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	Если СтрокаСведений.РазнНДСКСчФ <> Неопределено Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "РазнНДСКСчФ");
		
		Если СтрокаСведений.РазнНДСКСчФ.Свойство("Ум")
		   И ЗначениеЗаполнено(СтрокаСведений.РазнНДСКСчФ.Ум) Тогда
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Ум");
			
			Если ТипЗнч(СтрокаСведений.РазнНДСКСчФ.Ум) = Тип("Число") Тогда
				ЗначениеУзла = Формат(СтрокаСведений.РазнНДСКСчФ.Ум,"ЧЦ=19; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0");
				КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СумНДС");
				КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, ЗначениеУзла);
			Иначе
				ЗначениеУзла = Лев(СокрЛП(СтрокаСведений.РазнНДСКСчФ.Ум), 7);
				КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "БезНДС");
				КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, ЗначениеУзла);
			КонецЕсли;
			
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		КонецЕсли;
		
		Если СтрокаСведений.РазнНДСКСчФ.Свойство("Увел")
		   И ЗначениеЗаполнено(СтрокаСведений.РазнНДСКСчФ.Увел) Тогда
			КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Увел");
			
			Если ТипЗнч(СтрокаСведений.РазнНДСКСчФ.Увел) = Тип("Число") Тогда
				ЗначениеУзла = Формат(СтрокаСведений.РазнНДСКСчФ.Увел,"ЧЦ=19; ЧДЦ=2; ЧРД=.; ЧН=; ЧГ=0");
				КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СумНДС");
				КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, ЗначениеУзла);
			Иначе
				ЗначениеУзла = Лев(СокрЛП(СтрокаСведений.РазнНДСКСчФ.Увел), 7);
				КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "БезНДС");
				КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, ЗначениеУзла);
			КонецЕсли;
			
			КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		КонецЕсли;
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
КонецПроцедуры

Функция ИдентификаторФайлаЭлектронногоПредставления(СведенияОтправки)
	
	Префикс = СведенияОтправки.КНД;
	Если СведенияОтправки.ЭтоПБОЮЛ Тогда
		ИдентификаторОтправителя = СокрЛП(СведенияОтправки.ИННФЛ);
	Иначе
		ИдентификаторОтправителя = СокрЛП(СведенияОтправки.ИННЮЛ) + СокрЛП(СведенияОтправки.КПП);
	КонецЕсли;
	ИдентификаторПолучателя = СведенияОтправки.КодНО;
	ИдентификационныйНомер1 = Строка(Новый УникальныйИдентификатор);
	ДатаФормированияФайла = Формат(СведенияОтправки.ДатаФормированияФайла, "ДФ=yyyyMMdd");
	ИдентификационныйНомер2 = Строка(Новый УникальныйИдентификатор);
	
	ИдентификаторФайла = Префикс
	                   + "_" + ИдентификаторОтправителя
	                   + "_" + ИдентификаторПолучателя
	                   + "_" + ИдентификационныйНомер1
	                   + "_" + ДатаФормированияФайла
	                   + "_" + ИдентификационныйНомер2;
	
	Возврат ИдентификаторФайла;
	
КонецФункции

Функция ОсновныеСведенияЭлектронногоПредставления(ВыгружаемыйДокумент, КНД)
	
	ОсновныеСведения = Новый Структура;
	
	ОсновныеСведения.Вставить("КНД", КНД);
	ОсновныеСведения.Вставить("ВерсПрог", РегламентированнаяОтчетность.НазваниеИВерсияПрограммы());
	ОсновныеСведения.Вставить("ДатаФормированияФайла", ТекущаяДатаСеанса());
	
	ЭтоПБОЮЛ = НЕ РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(ВыгружаемыйДокумент.Организация);
	ОсновныеСведения.Вставить("ЭтоПБОЮЛ", ЭтоПБОЮЛ);
	ОсновныеСведения.Вставить("ЭтоЮЛ", НЕ ЭтоПБОЮЛ);
	ОсновныеСведения.Вставить("ЭтоИП", ЭтоПБОЮЛ);
	
	НалоговыйПериод = КонецКвартала(ВыгружаемыйДокумент.НалоговыйПериод);
	СведенияОбОрганизации = СведенияОбОрганизации(ВыгружаемыйДокумент.Организация, НалоговыйПериод);
	
	Если ЭтоПБОЮЛ Тогда
		ОсновныеСведения.Вставить("ИННФЛ", СведенияОбОрганизации.ИННЮЛ);
		СведенияОЮрФизЛице  = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(
			ВыгружаемыйДокумент.Организация,
			НалоговыйПериод);
		СвидетельствоОРегистрации = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОЮрФизЛице, "Свидетельство");
		ОсновныеСведения.Вставить("СвГосРегИП", СвидетельствоОРегистрации);
		
		СведенияОбИП = СведенияОбИП(ВыгружаемыйДокумент);
		ОсновныеСведения.Вставить("ФамилияИП",  СведенияОбИП.Фамилия);
		ОсновныеСведения.Вставить("ИмяИП",      СведенияОбИП.Имя);
		ОсновныеСведения.Вставить("ОтчествоИП", СведенияОбИП.Отчество);
	Иначе
		ОсновныеСведения.Вставить("НаимОрг", СведенияОбОрганизации.НаимЮЛПол);
		ОсновныеСведения.Вставить("ИННЮЛ", СведенияОбОрганизации.ИННЮЛ);
		ОсновныеСведения.Вставить("КПП", СведенияОбОрганизации.КППЮЛ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыгружаемыйДокумент.ИФНС) Тогда
		ОсновныеСведения.Вставить("КодНО", ВыгружаемыйДокумент.ИФНС.Код);
	Иначе
		ОсновныеСведения.Вставить("КодНО", СведенияОбОрганизации.КодНО);
	КонецЕсли;
	
	ДобавитьСведенияОПодписанте(ОсновныеСведения, ВыгружаемыйДокумент);
	
	ОсновныеСведения.Вставить("Период", ВыгружаемыйДокумент.ПериодПоСКНП);
	
	ОтчетныйГод = Формат(ВыгружаемыйДокумент.НалоговыйПериод, "ДФ=yyyy");
	ОсновныеСведения.Вставить("ОтчетГод", ОтчетныйГод);
	
	ИдентификаторФайла = ИдентификаторФайлаЭлектронногоПредставления(ОсновныеСведения);
	ОсновныеСведения.Вставить("ИдФайл", ИдентификаторФайла);
	
	Возврат ОсновныеСведения;
	
КонецФункции

Функция СведенияОбОрганизации(Организация, НалоговыйПериод)
	
	СтрокаСведений = "ИННЮЛ, КППЮЛ, НаимЮЛПол, КодНО, ТелОрганизации, ФИО, ОКВЭД, ОКАТО";
	СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(
		Организация,
		НалоговыйПериод,
		СтрокаСведений);
	
	Возврат СведенияОбОрганизации;
	
КонецФункции

Процедура ДобавитьСведенияОПодписанте(ОсновныеСведения, ВыгружаемыйДокумент)
	
	ИФНС = ВыгружаемыйДокумент.ИФНС;
	
	Если ЗначениеЗаполнено(ИФНС) И ЗначениеЗаполнено(ИФНС.Представитель) Тогда
		Если ТипЗнч(ИФНС.Представитель) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			ФИОПодписанта = СведенияОФизЛице(ИФНС.Представитель, ВыгружаемыйДокумент.Дата);
			ОсновныеСведения.Вставить("ПрПодп", "2");
			ОсновныеСведения.Вставить("ПодпФамилия",  ФИОПодписанта.Фамилия);
			ОсновныеСведения.Вставить("ПодпИмя",      ФИОПодписанта.Имя);
			ОсновныеСведения.Вставить("ПодпОтчество", ФИОПодписанта.Отчество);
			ОсновныеСведения.Вставить("НаимДокПодп",  ИФНС.ДокументПредставителя);
			
		ИначеЕсли НЕ ПустаяСтрока(ИФНС.УполномоченноеЛицоПредставителя) Тогда
			ОсновныеСведения.Вставить("ПрПодп", "2");
			СоставляющиеФИО = СоставляющиеФИО(ИФНС.УполномоченноеЛицоПредставителя);
			ОсновныеСведения.Вставить("ПодпФамилия",  СоставляющиеФИО.Фамилия);
			ОсновныеСведения.Вставить("ПодпИмя",      СоставляющиеФИО.Имя);
			ОсновныеСведения.Вставить("ПодпОтчество", СоставляющиеФИО.Отчество);
			ОсновныеСведения.Вставить("НаимДокПодп",  ИФНС.ДокументПредставителя);
			
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ОсновныеСведения.Свойство("ПрПодп") ИЛИ (ОсновныеСведения.Свойство("ПрПодп") И ОсновныеСведения.ПрПодп <> "2") Тогда
		Если ЗначениеЗаполнено(ВыгружаемыйДокумент.Подписант) Тогда
			ФИОПодписанта = СведенияОФизЛице(ВыгружаемыйДокумент.Подписант, ВыгружаемыйДокумент.Дата);
			ОсновныеСведения.Вставить("ПрПодп", "1");
			ОсновныеСведения.Вставить("ПодпФамилия",  ФИОПодписанта.Фамилия);
			ОсновныеСведения.Вставить("ПодпИмя",      ФИОПодписанта.Имя);
			ОсновныеСведения.Вставить("ПодпОтчество", ФИОПодписанта.Отчество);
			
		Иначе
			ФИОПодписанта = СведенияОРуководителе(ВыгружаемыйДокумент);
			ОсновныеСведения.Вставить("ПрПодп", "1");
			ОсновныеСведения.Вставить("ПодпФамилия",  ФИОПодписанта.Фамилия);
			ОсновныеСведения.Вставить("ПодпИмя",      ФИОПодписанта.Имя);
			ОсновныеСведения.Вставить("ПодпОтчество", ФИОПодписанта.Отчество);
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция СведенияОРуководителе(ВыгружаемыйДокумент)
	
	Результат = Новый Структура("Фамилия, Имя, Отчество", "", "", "");
	
	РеквизитыОтветственныхЛиц = ОтветственныеЛицаБП.ОтветственныеЛица(ВыгружаемыйДокумент.Организация, ВыгружаемыйДокумент.Дата);
	Если ЗначениеЗаполнено(РеквизитыОтветственныхЛиц.Руководитель) Тогда
		Результат.Фамилия 	= СокрЛП(РеквизитыОтветственныхЛиц.РуководительФИО.Фамилия);
		Результат.Имя 		= СокрЛП(РеквизитыОтветственныхЛиц.РуководительФИО.Имя);
		Результат.Отчество	= СокрЛП(РеквизитыОтветственныхЛиц.РуководительФИО.Отчество);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция СведенияОФизЛице(ФизЛицо, Дата)
	
	Результат = Новый Структура("Фамилия, Имя, Отчество", "", "", "");
	
	Если ЗначениеЗаполнено(ФизЛицо) Тогда
		ДанныеФЛ = РегистрыСведений.ФИОФизическихЛиц.СрезПоследних(Дата, Новый Структура("ФизическоеЛицо", ФизЛицо));
		Если ДанныеФЛ.Количество() > 0 Тогда
			Результат.Фамилия  = СокрЛП(ДанныеФЛ[0].Фамилия);
			Результат.Имя      = СокрЛП(ДанныеФЛ[0].Имя);
			Результат.Отчество = СокрЛП(ДанныеФЛ[0].Отчество);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция СоставляющиеФИО(Знач ФИОСтр)
	
	ФИОСтр = СокрЛП(ФИОСтр);
	ФИО = Новый Структура("Фамилия, Имя, Отчество", "", "", "");
	
	ПервыйПробел = СтрНайти(ФИОСтр, " ");
	Если ПервыйПробел = 0 Тогда
		ФИО.Фамилия = ФИОСтр;
		Возврат ФИО;
	КонецЕсли;
	ФИО.Фамилия = СокрЛП(Лев(ФИОСтр, ПервыйПробел - 1));
	ФИОСтр = СокрЛП(Сред(ФИОСтр, ПервыйПробел + 1));
	
	ВторойПробел = СтрНайти(ФИОСтр, " ");
	Если ВторойПробел = 0 Тогда
		ФИО.Имя = ФИОСтр;
		Возврат ФИО;
	КонецЕсли;
	ФИО.Имя = СокрЛП(Лев(ФИОСтр, ВторойПробел - 1));
	
	ФИО.Отчество = СокрЛП(Сред(ФиоСтр, ВторойПробел + 1));
	
	Возврат ФИО;
	
КонецФункции

Функция СведенияОбИП(ВыгружаемыйДокумент)
	
	Результат = Новый Структура("Фамилия, Имя, Отчество", "", "", "");
	
	ФЛ = ВыгружаемыйДокумент.Организация.ИндивидуальныйПредприниматель;
	Если ФЛ = Справочники.ФизическиеЛица.ПустаяСсылка() Тогда
		ФИОИП = ВыгружаемыйДокумент.Организация.НаименованиеПолное;
	Иначе
		ДанныеФЛ = РегистрыСведений.ФИОФизическихЛиц.СрезПоследних(ВыгружаемыйДокумент.Дата, Новый Структура("ФизическоеЛицо", ФЛ));
		Если ДанныеФЛ.Количество() > 0 Тогда
			Результат.Фамилия = СокрЛП(ДанныеФЛ[0].Фамилия);
			Результат.Имя = СокрЛП(ДанныеФЛ[0].Имя);
			Результат.Отчество = СокрЛП(ДанныеФЛ[0].Отчество);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция РасщепленнаяСтрока(СтрокаЗначения, Разделитель)
	
	Результат = Новый Массив;
	
	ПозицияРазделителя = СтрНайти(СтрокаЗначения, Разделитель);
	Если ПозицияРазделителя = 0 Тогда
		Результат.Добавить(СтрокаЗначения);
		Результат.Добавить("");
	Иначе
		Результат.Добавить(Лев(СтрокаЗначения, ПозицияРазделителя - 1));
		Результат.Добавить(Сред(СтрокаЗначения, ПозицияРазделителя + 1));
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#Область УниверсальныеСтруктурыЭлектронныхПредставлений

Процедура ВыгрузитьСведенияОбОрганизации_501(КомпоновщикXML, ПараметрыВыгрузки)
	
	Если ПараметрыВыгрузки.ЭтоИП Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СведИП");
		
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИННФЛ", ПараметрыВыгрузки.ИННФЛ, "Т,12");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "СвГосРегИП", ПараметрыВыгрузки.СвГосРегИП, "Т,100");
		
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ФИОИП");
		
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Фамилия", ПараметрыВыгрузки.ФамилияИП, "Т,60");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Имя", ПараметрыВыгрузки.ИмяИП, "Т,60");
		КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "Отчество", ПараметрыВыгрузки.ОтчествоИП, "Т,60");
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		
	Иначе
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СведЮЛ");
		
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НаимОрг", ПараметрыВыгрузки.НаимОрг, "Т,1000");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ИННЮЛ", ПараметрыВыгрузки.ИННЮЛ, "Т,10");
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "КПП", ПараметрыВыгрузки.КПП, "Т,9");
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ВывестиУзелТипа501_СтНалТип(КомпоновщикXML, Знач СтавкаНДС, СтоимостьБезНДС, СуммаНДС = 0)
	
	Если ТипЗнч(СтавкаНДС) <> Тип("Строка") Тогда
		СтавкаНДС = Строка(СтавкаНДС);
	КонецЕсли;
	
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "СтТовБезНДС", СтоимостьБезНДС, "Ч,17,2");
	
	Если СтавкаНДС = "без НДС" Тогда // Не локализуется
		ТипСтавки = "текст";
		ЗначениеСуммы = "без НДС"; // Не локализуется
	Иначе
		ТипСтавки = "процент";
		ЗначениеСуммы = Формат(СуммаНДС, "ЧДЦ=2; ЧРД=.; ЧН=-; ЧГ=0");
	КонецЕсли;
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "НалСт");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НалСтВел", СтавкаНДС, "Т,7");
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НалСтТип", ТипСтавки, "Т,7");
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СумНал");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "СумНДС", ЗначениеСуммы, "Т,18");
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
КонецПроцедуры

Процедура ВывестиУзел_Подписант(КомпоновщикXML,ПараметрыВыгрузки)
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "Подписант");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "ПрПодп", ПараметрыВыгрузки.ПрПодп);
	
	КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "ФИО");
	
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Фамилия", ПараметрыВыгрузки.ПодпФамилия, "Т,60");
	КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "Имя", ПараметрыВыгрузки.ПодпИмя, "Т,60");
	КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, "Отчество", ПараметрыВыгрузки.ПодпОтчество, "Т,60");
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
	Если ПараметрыВыгрузки.Свойство("НаимДокПодп") И ЗначениеЗаполнено(ПараметрыВыгрузки.НаимДокПодп) Тогда
		КомпоновщикXML_СоздатьУзел(КомпоновщикXML, "СвПред");
		
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, "НаимДок", ПараметрыВыгрузки.НаимДокПодп, "Т,120");
		
		КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	КонецЕсли;
	
	КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML);
	
КонецПроцедуры

#КонецОбласти

#Область КомпоновщикXML

Функция КомпоновщикXML_Новый()
	
	КомпоновщикXML = Новый Структура;
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("xml");
	ЗаписьТекста = Новый ЗаписьТекста(ИмяВременногоФайла, "windows-1251");
	ЗаписьТекста.Записать("<?xml version=""1.0"" encoding=""windows-1251""?>");
	
	КомпоновщикXML.Вставить("ИмяФайла", ИмяВременногоФайла);
	КомпоновщикXML.Вставить("ЗаписьТекста", ЗаписьТекста);
	КомпоновщикXML.Вставить("СтэкУзлов", Новый Массив);
	КомпоновщикXML.Вставить("НовыйУзелЗакрыт", Истина);
	КомпоновщикXML.Вставить("Табуляция", "");
	
	Возврат КомпоновщикXML;
	
КонецФункции

Процедура КомпоновщикXML_СоздатьУзел(КомпоновщикXML, ИмяУзла)
	
	Если НЕ КомпоновщикXML.НовыйУзелЗакрыт Тогда
		КомпоновщикXML.ЗаписьТекста.Записать(">");
		КомпоновщикXML.Табуляция = КомпоновщикXML.Табуляция + Символы.Таб;
	КонецЕсли;
	
	КомпоновщикXML.СтэкУзлов.Вставить(0, ИмяУзла);
	
	КомпоновщикXML.ЗаписьТекста.Записать(Символы.ПС + КомпоновщикXML.Табуляция + "<" + ИмяУзла);
	
	КомпоновщикXML.НовыйУзелЗакрыт = Ложь;
	
КонецПроцедуры

Процедура КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, ИмяАтрибута, ЗначениеАтрибута, ТипАтрибута = "Т")
	
	ПредставлениеЗначения = ПредставлениеЗначенияXML(ЗначениеАтрибута, ТипАтрибута);
	
	КомпоновщикXML.ЗаписьТекста.Записать(" " + ИмяАтрибута + "=""" + ПредставлениеЗначения + """");
	
КонецПроцедуры

Процедура КомпоновщикXML_ВывестиНеобязательныйАтрибут(КомпоновщикXML, ИмяАтрибута, ЗначениеАтрибута, ТипАтрибута = "Т")
	
	Если ЗначениеЗаполнено(ЗначениеАтрибута) Тогда
		КомпоновщикXML_ВывестиАтрибут(КомпоновщикXML, ИмяАтрибута, ЗначениеАтрибута, ТипАтрибута);
	КонецЕсли;
	
КонецПроцедуры

Процедура КомпоновщикXML_ВывестиЗначениеИЗакрытьУзел(КомпоновщикXML, Значение, ТипЗначения = "Т")
	
	Если ЗначениеЗаполнено(Значение) Тогда
		ПредставлениеЗначения = ПредставлениеЗначенияXML(Значение, ТипЗначения);
		КомпоновщикXML.ЗаписьТекста.Записать(">" + ПредставлениеЗначения + "</" + КомпоновщикXML.СтэкУзлов[0] + ">");
	Иначе
		КомпоновщикXML.ЗаписьТекста.Записать(" />");
	КонецЕсли;
	
	КомпоновщикXML.СтэкУзлов.Удалить(0);
	КомпоновщикXML.НовыйУзелЗакрыт = Истина;
	
КонецПроцедуры

Процедура КомпоновщикXML_ЗакрытьУзел(КомпоновщикXML)
	
	Если КомпоновщикXML.НовыйУзелЗакрыт Тогда
		КомпоновщикXML.Табуляция = Сред(КомпоновщикXML.Табуляция, 2);
		КомпоновщикXML.ЗаписьТекста.Записать(Символы.ПС + КомпоновщикXML.Табуляция + "</" + КомпоновщикXML.СтэкУзлов[0] + ">");
		
	Иначе
		КомпоновщикXML.ЗаписьТекста.Записать("/>");
		КомпоновщикXML.НовыйУзелЗакрыт = Истина;
		
	КонецЕсли;
	
	КомпоновщикXML.СтэкУзлов.Удалить(0);
	
КонецПроцедуры

Функция КомпоновщикXML_СсылкаНаВременноеХранилище(КомпоновщикXML)
	
	КомпоновщикXML.ЗаписьТекста.Закрыть();
	КомпоновщикXML.ЗаписьТекста = Неопределено;
	
	ФайлВыгрузки = Новый ДвоичныеДанные(КомпоновщикXML.ИмяФайла);
	
	АдресВХранилище = ПоместитьВоВременноеХранилище(ФайлВыгрузки, Новый УникальныйИдентификатор);
	
	УдалитьФайлы(КомпоновщикXML.ИмяФайла);
	
	Возврат АдресВХранилище;
	
КонецФункции

Функция ПредставлениеЗначенияXML(Значение, ТипЗначения)
	
	ОписаниеТипаАтрибута = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ТипЗначения);
	
	Если ОписаниеТипаАтрибута[0] = "Д" Тогда
		ПредставлениеЗначения = Формат(Значение, "ДФ=dd.MM.yyyy");
		
	ИначеЕсли ОписаниеТипаАтрибута[0] = "Т" Тогда
		ПредставлениеЗначения = СокрЛП(Значение);
		ПредставлениеЗначения = ЗаменитьСимволыРазметки(ПредставлениеЗначения);
		
		Если ОписаниеТипаАтрибута.Количество() >= 2 Тогда
			МаксимальнаяДлина = Число(ОписаниеТипаАтрибута[1]);
			ПредставлениеЗначения = Лев(ПредставлениеЗначения, МаксимальнаяДлина);
		КонецЕсли;
		
	ИначеЕсли ОписаниеТипаАтрибута[0] = "Ч" Тогда
		СтрокаФормата = "ЧРД=.; ЧН=; ЧГ=0";
		
		Если ОписаниеТипаАтрибута.Количество() >= 2 Тогда
			РазмерностьПолная = Число(ОписаниеТипаАтрибута[1]);
			СтрокаФормата = СтрокаФормата + "ЧЦ=" + Формат(РазмерностьПолная, "ЧГ=") + ";";
		КонецЕсли;
		
		Если ОписаниеТипаАтрибута.Количество() >= 3 Тогда
			РазмерностьДробнойЧасти = Число(ОписаниеТипаАтрибута[2]);
			СтрокаФормата = СтрокаФормата + "ЧДЦ=" + Формат(РазмерностьДробнойЧасти, "ЧГ=") + ";";
		КонецЕсли;
		
		ПредставлениеЗначения = Формат(Значение, СтрокаФормата);
		
	Иначе
		ПредставлениеЗначения = СокрЛП(Значение);
	КонецЕсли;
	
	Возврат ПредставлениеЗначения;
	
КонецФункции

Функция ЗаменитьСимволыРазметки(Знач ПредставлениеЗначения)
	
	ПредставлениеЗначения = СтрЗаменить(ПредставлениеЗначения, "&", "&amp;");
	ПредставлениеЗначения = СтрЗаменить(ПредставлениеЗначения, "<", "&lt;");
	ПредставлениеЗначения = СтрЗаменить(ПредставлениеЗначения, ">", "&gt;");
	ПредставлениеЗначения = СтрЗаменить(ПредставлениеЗначения, """", "&quot;");
	ПредставлениеЗначения = СтрЗаменить(ПредставлениеЗначения, "'", "&apos;");
	
	Возврат ПредставлениеЗначения;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область РеестрНДСПриложение5

Функция ДеревоЗначенийРеестрНДСПриложение5()
	
	НовоеДеревоЗначений = Новый ДеревоЗначений();
	НовоеДеревоЗначений.Колонки.Добавить("Данные");
	НовоеДеревоЗначений.Колонки.Добавить("ДанныеМногострочныхЧастей");
	НовоеДеревоЗначений.Колонки.Добавить("АдресТабличногоДокумента");
	
	Возврат НовоеДеревоЗначений;
	
КонецФункции

#КонецОбласти

#КонецОбласти
