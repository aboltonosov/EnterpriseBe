
#Область ПрограммныйИнтерфейс

// Проверяет, что считанный ШК является акцизной маркой
//
// Параметры:
//  Форма						 - УправляемаяФорма - Форма
//  ТекущиеДанные				 - СтрокаТЧ - Текущие данные
//  ДанныеШтрихкодов			 - Структура - Данные считанного штрихкода
//  ИмяКолонкиКоличество		 - Строка - Имя колонки "Количество"
//  СопоставленнаяНоменклатура	 - Структура - найденная сопоставленная номенклатура
// 
// Возвращаемое значение:
//   - 
//
Функция СчитанаАкцизнаяМарка(Форма, ТекущиеДанные, ДанныеШтрихкодов, ИмяКолонкиКоличество = "Количество", СопоставленнаяНоменклатура = Неопределено) Экспорт
	
	Результат = Ложь;
	
	Если ДанныеШтрихкодов.Количество() = 1
		И СтрДлина(ДанныеШтрихкодов[0].Штрихкод) = 68 Тогда
		
		СопоставленнаяНоменклатура = Неопределено;
		
		Штрихкод = ДанныеШтрихкодов[0].Штрихкод;
		
		ЭтоШтрихкодАкцизнойМарки = АкцизныеМаркиВызовСервера.ЭтоШтрихкодАкцизнойМарки(
			Штрихкод,
			СопоставленнаяНоменклатура);
			
		Если ЭтоШтрихкодАкцизнойМарки Тогда
			
			Если ТекущиеДанные <> Неопределено Тогда
				ИдентификаторСтроки = ТекущиеДанные.ПолучитьИдентификатор();
			Иначе
				ИдентификаторСтроки = Неопределено;
			КонецЕсли;
			
			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("ИдентификаторСтроки", ИдентификаторСтроки);
			ДополнительныеПараметры.Вставить("Редактирование", Ложь);
			ДополнительныеПараметры.Вставить("Форма", Форма);
			ДополнительныеПараметры.Вставить("ИмяКолонкиКоличество", ИмяКолонкиКоличество);
			ДополнительныеПараметры.Вставить("ЗапрашиватьНоменклатуру", Истина);
			
			ИспользоватьАкцизныеМарки = Ложь;
			Если ИнтеграцияЕГАИСКлиентСервер.ЕстьРеквизитОбъекта(Форма, "ИспользоватьАкцизныеМарки") Тогда
				ИспользоватьАкцизныеМарки = Форма.ИспользоватьАкцизныеМарки;
			КонецЕсли;
			
			Если ИспользоватьАкцизныеМарки Тогда
				РезультатОповещения = Новый Структура;
				РезультатОповещения.Вставить("КодАкцизнойМарки",           Штрихкод);
				РезультатОповещения.Вставить("СопоставленнаяНоменклатура", СопоставленнаяНоменклатура);
				ВыполнитьОбработкуОповещения(
					Новый ОписаниеОповещения("ВводАкцизнойМаркиЗавершение", Форма, ДополнительныеПараметры),
					РезультатОповещения);
			КонецЕсли;
				
			Результат = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Процедура - Открыть форму считывания акцизной марки
//
// Параметры:
//  Результат - Булево - Не используется
//  ДополнительныеПараметры - Структура - Параметры открытия формы считывания марки: (Форма, ИдентификаторСтроки, Редактирование, АдресВоВременномХранилище)
//
Процедура ОткрытьФормуСчитыванияАкцизнойМарки(Результат, ДополнительныеПараметры) Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДополнительныеПараметры.Форма, "Объект") Тогда
		Источник = ДополнительныеПараметры.Форма.Объект;
	Иначе
		Источник = ДополнительныеПараметры.Форма;
	КонецЕсли;
	
	ТекущиеДанные = Источник.Товары.НайтиПоИдентификатору(ДополнительныеПараметры.ИдентификаторСтроки);
	
	Если Не ТекущиеДанные.МаркируемаяАлкогольнаяПродукция Тогда
		ПоказатьПредупреждение(
			Неопределено,
			НСтр("ru = 'Для данной строки не указываются акцизные марки'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("Номенклатура",   ТекущиеДанные.Номенклатура);
	ПараметрыОткрытияФормы.Вставить("Характеристика", ТекущиеДанные.Характеристика);
	ПараметрыОткрытияФормы.Вставить("Редактирование",            ДополнительныеПараметры.Редактирование);
	ПараметрыОткрытияФормы.Вставить("АдресВоВременномХранилище", ДополнительныеПараметры.АдресВоВременномХранилище);
	
	ДополнительныеПараметры.Вставить("ЗапрашиватьНоменклатуру", Ложь);
	
	ОткрытьФорму(
		"Обработка.РаботаСАкцизнымиМаркамиЕГАИС.Форма.ФормаВводаАкцизнойМарки",
		ПараметрыОткрытияФормы,
		ДополнительныеПараметры.Форма,,,,
		Новый ОписаниеОповещения("ВводАкцизнойМаркиЗавершение", ДополнительныеПараметры.Форма, ДополнительныеПараметры))
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВводАкцизнойМаркиЗавершение(ДанныеПоАкцизнымМаркам, ДополнительныеПараметры) Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДополнительныеПараметры.Форма, "Объект") Тогда
		Источник = ДополнительныеПараметры.Форма.Объект;
	Иначе
		Источник = ДополнительныеПараметры.Форма;
	КонецЕсли;
	
	ТекущиеДанные = ДополнительныеПараметры.ТекущиеДанные;
	
	Если Не ДополнительныеПараметры.Редактирование Тогда
		
		Если ДанныеПоАкцизнымМаркам.АкцизнаяМаркаНайдена Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтрШаблон(
					НСтр("ru = 'Акцизная марка %1 уже введена для номенклатуры в строке %2'"),
					ДанныеПоАкцизнымМаркам.КодАкцизнойМарки,
					ДополнительныеПараметры.Форма.Объект.Товары.Индекс(ТекущиеДанные) + 1));
			Возврат;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ТекущиеДанные.ИдентификаторСтроки) Тогда
			ТекущиеДанные.ИдентификаторСтроки = Строка(Новый УникальныйИдентификатор);
		КонецЕсли;
		
		НоваяСтрока = Источник.АкцизныеМарки.Добавить();
		НоваяСтрока.ИдентификаторСтроки = ТекущиеДанные.ИдентификаторСтроки;
		НоваяСтрока.КодАкцизнойМарки = ДанныеПоАкцизнымМаркам.КодАкцизнойМарки;
		
		ДанныеПоАкцизнымМаркам.Количество = ДанныеПоАкцизнымМаркам.Количество + 1;
		
	КонецЕсли;
	
	Если ДополнительныеПараметры.Свойство("ИмяКолонкиКоличество") Тогда
		ИмяКолонкиКоличество = ДополнительныеПараметры.ИмяКолонкиКоличество;
	Иначе
		ИмяКолонкиКоличество = "Количество";
	КонецЕсли;
	
	ТекущиеДанные.КоличествоАкцизныхМарок = ДанныеПоАкцизнымМаркам.Количество;
	
	ДополнительныеПараметры.Форма.Модифицированность = Истина;
	
	АкцизныеМаркиКлиентСервер.ЗаполнитьИндексАкцизнойМарки(ТекущиеДанные, ИмяКолонкиКоличество);
	
	Если ТекущиеДанные[ИмяКолонкиКоличество] < ДанныеПоАкцизнымМаркам.Количество Тогда
		
		ТекущиеДанные[ИмяКолонкиКоличество] = ДанныеПоАкцизнымМаркам.Количество;
		
		ВыполнитьОбработкуОповещения(
			Новый ОписаниеОповещения(
				"ИзменениеАкцизныхМарокЗавершение",
				ДополнительныеПараметры.Форма,
				ДополнительныеПараметры),
			ТекущиеДанные);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти