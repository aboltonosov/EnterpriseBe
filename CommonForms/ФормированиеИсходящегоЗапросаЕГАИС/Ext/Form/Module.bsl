
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ВидДокументаЕГАИС      = Параметры.ВидДокументаЕГАИС;
	ИмяПараметра           = Параметры.ИмяПараметра;
	ЗначениеПараметра      = Параметры.ЗначениеПараметра;
	ПредставлениеПараметра = Параметры.ПредставлениеПараметра;
	
	ТекущийЭлемент = Элементы.ЗначениеПараметра;
	
	Если Не ЗначениеЗаполнено(ВидДокументаЕГАИС) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Заголовок = Строка(ВидДокументаЕГАИС);
	
	Если ПустаяСтрока(ПредставлениеПараметра) Тогда
		
		Если ВидДокументаЕГАИС = Перечисления.ВидыДокументовЕГАИС.ЗапросСправки1
			ИЛИ ВидДокументаЕГАИС = Перечисления.ВидыДокументовЕГАИС.ЗапросСправки2 Тогда
			ПредставлениеПараметра = НСтр("ru='Номер справки'");
			
		ИначеЕсли ВидДокументаЕГАИС = Перечисления.ВидыДокументовЕГАИС.ЗапросАлкогольнойПродукции
			ИЛИ ВидДокументаЕГАИС = Перечисления.ВидыДокументовЕГАИС.ЗапросОрганизаций Тогда
			Если ПустаяСтрока(Параметры.ИмяПараметра) ИЛИ ВРег(Параметры.ИмяПараметра) = "ИНН" Тогда
				ПредставлениеПараметра = НСтр("ru='ИНН'");
			ИначеЕсли ВРег(Параметры.ИмяПараметра) = "СИО" Тогда
				ПредставлениеПараметра = НСтр("ru='Код ФСРАР'");
			Иначе
				ПредставлениеПараметра = ТРег(Параметры.ИмяПараметра);
			КонецЕсли;
			
		ИначеЕсли ВидДокументаЕГАИС = Перечисления.ВидыДокументовЕГАИС.ЗапросТТН Тогда
			ПредставлениеПараметра = НСтр("ru='Идентификатор ТТН'");
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПустаяСтрока(Заголовок) ИЛИ ПустаяСтрока(ПредставлениеПараметра) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Элементы.ЗначениеПараметра.Заголовок = ПредставлениеПараметра;
	
	ВозможныеТранспортныеМодули = ИнтеграцияЕГАИСВызовСервера.ДоступныеТранспортныеМодули();
	ТранспортныеМодули = Новый Соответствие();
	
	Для Каждого ВозможныйТранспортныйМодуль Из ВозможныеТранспортныеМодули Цикл
		Элементы.ТранспортныйМодуль.СписокВыбора.Добавить(ВозможныйТранспортныйМодуль.ИдентификаторФСРАР, ВозможныйТранспортныйМодуль.Представление);
		ТранспортныеМодули.Вставить(ВозможныйТранспортныйМодуль.ИдентификаторФСРАР, ВозможныйТранспортныйМодуль);
	КонецЦикла;
	
	ДоступныеТранспортныеМодули = Новый ФиксированноеСоответствие(ТранспортныеМодули);
	
	Если Элементы.ТранспортныйМодуль.СписокВыбора.Количество() > 0 Тогда
		ТранспортныйМодуль = ВозможныйТранспортныйМодуль.ИдентификаторФСРАР;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Запросить(Команда)
	
	ОчиститьСообщения();
	
	Отказ = Ложь;
	
	Если Не ЗначениеЗаполнено(ТранспортныйМодуль) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указан транспортный модуль'"),, "ТранспортныйМодуль",, Отказ);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ЗначениеПараметра) Тогда
		ТекстСообщения = НСтр("ru = 'Не указан %1'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ПредставлениеПараметра);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "ЗначениеПараметра",, Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	СформироватьИсходящийЗапрос();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СформироватьИсходящийЗапрос()
	
	ВыбранныйТранспортныйМодуль = ДоступныеТранспортныеМодули.Получить(ТранспортныйМодуль);
	
	Если Не ЗначениеЗаполнено(ВыбранныйТранспортныйМодуль) Тогда
		Возврат;
	КонецЕсли;
	
	ВходныеПараметры = ИнтеграцияЕГАИСКлиентСервер.ПараметрыИсходящегоЗапроса(ВидДокументаЕГАИС);
	
	Если ВидДокументаЕГАИС = ПредопределенноеЗначение("Перечисление.ВидыДокументовЕГАИС.ЗапросСправки1")
		ИЛИ ВидДокументаЕГАИС = ПредопределенноеЗначение("Перечисление.ВидыДокументовЕГАИС.ЗапросСправки2") Тогда
		ВходныеПараметры.РегистрационныйНомер = СокрЛП(ЗначениеПараметра);
	ИначеЕсли ВидДокументаЕГАИС = ПредопределенноеЗначение("Перечисление.ВидыДокументовЕГАИС.ЗапросАлкогольнойПродукции")
		ИЛИ ВидДокументаЕГАИС = ПредопределенноеЗначение("Перечисление.ВидыДокументовЕГАИС.ЗапросОрганизаций") Тогда
		ВходныеПараметры.ИмяПараметра = ?(ПустаяСтрока(ИмяПараметра), "ИНН", ИмяПараметра);
		ВходныеПараметры.ЗначениеПараметра = СокрЛП(ЗначениеПараметра);
	ИначеЕсли ВидДокументаЕГАИС = ПредопределенноеЗначение("Перечисление.ВидыДокументовЕГАИС.ЗапросТТН") Тогда
		ВходныеПараметры.ИдентификаторЕГАИС = СокрЛП(ЗначениеПараметра);
	КонецЕсли;
	
	ИнтеграцияЕГАИСКлиент.НачатьФормированиеИсходящегоЗапроса(
		Новый ОписаниеОповещения("ФормированиеИсходящегоЗапроса_Завершение", ЭтотОбъект),
		ВидДокументаЕГАИС,
		ВходныеПараметры,
		ВыбранныйТранспортныйМодуль);
	
КонецПроцедуры

&НаКлиенте
Процедура ФормированиеИсходящегоЗапроса_Завершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Результат.Вставить("ЗначениеПараметра", ЗначениеПараметра);
	КонецЕсли;
	
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти
