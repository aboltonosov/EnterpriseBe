#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет ставки НДС, операции, отраженные по которым, подлежат регистрации в книге продаж.
//
// Параметры:
//   Параметры - Структура - параметры для определения ставок:
//     * ПрименяетсяОсвобождениеОтУплатыНДС - Булево - признак применения освобождения от уплаты
//               НДС по статье 145 НК. 
//     * ПлательщикНДС - Булево - признак применения общей системы налогообложения.
//     * Параметры.ВерсияПостановленияНДС1137 - Число - см. УчетНДСПереопределяемый.ВерсияПостановленияНДС1137.
//
// Возвращаемое значение:
//   Массив - ставки НДС.
Функция СтавкиПоОперациямОтражаемымВКнигеПродаж(Параметры) Экспорт
	
	МассивСтавок = Новый Массив;
	
	Если Параметры.ПрименяетсяОсвобождениеОтУплатыНДС Тогда
		МассивСтавок.Добавить(Перечисления.СтавкиНДС.БезНДС);
	ИначеЕсли Параметры.ПлательщикНДС Тогда
		МассивСтавок.Добавить(Перечисления.СтавкиНДС.НДС0);
		МассивСтавок.Добавить(Перечисления.СтавкиНДС.НДС10);
		МассивСтавок.Добавить(Перечисления.СтавкиНДС.НДС10_110);
		МассивСтавок.Добавить(Перечисления.СтавкиНДС.НДС18);
		МассивСтавок.Добавить(Перечисления.СтавкиНДС.НДС18_118);
		
		// 4D:ERP для Беларуси, Дмитрий, 12.09.2017 23:36:05 
		// Ставки НДС 20% и 25%
		// {
		МассивСтавок.Добавить(Перечисления.СтавкиНДС.НДС20);
		МассивСтавок.Добавить(Перечисления.СтавкиНДС.НДС20_120);
		МассивСтавок.Добавить(Перечисления.СтавкиНДС.НДС25);
		МассивСтавок.Добавить(Перечисления.СтавкиНДС.НДС25_125);
		// }
		// 4D
		
		Если Параметры.ВерсияПостановленияНДС1137 < 3 Тогда
			МассивСтавок.Добавить(Перечисления.СтавкиНДС.БезНДС);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат МассивСтавок;
	
КонецФункции

// 4D:ERP для Беларуси, ВалентинМ, 31.07.2017 15:39:59 
// Ставки таблицей
// {
Функция ПолучитьТаблицуЗначенийСтавокНДС() Экспорт
	
	тзСтавок = Новый ТаблицаЗначений;
	тзСтавок.Колонки.Добавить("СтавкаНДС", Новый ОписаниеТипов("ПеречислениеСсылка.СтавкиНДС"));
	тзСтавок.Колонки.Добавить("ЗначениеСтавкиНДС", Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(2, 2, ДопустимыйЗнак.Неотрицательный)));
	тзСтавок.Колонки.Добавить("РасчетнаяСтавка", Новый ОписаниеТипов("Булево"));
	
	Для Каждого мдЗначенияПеречисления Из Метаданные.Перечисления.СтавкиНДС.ЗначенияПеречисления Цикл
		СтавкаНДС = Перечисления.СтавкиНДС[мдЗначенияПеречисления.Имя];
		ЗначениеСтавкиНДС = ЦенообразованиеКлиентСервер.ПолучитьСтавкуНДСЧислом(СтавкаНДС);
		Если ЗначениеСтавкиНДС <> 0 Тогда
			стр_тзСтавок = тзСтавок.Добавить();
			стр_тзСтавок.СтавкаНДС = СтавкаНДС;
			стр_тзСтавок.ЗначениеСтавкиНДС = ЗначениеСтавкиНДС;
			стр_тзСтавок.РасчетнаяСтавка = СтрНайти(НРег(мдЗначенияПеречисления.Комментарий), "расчетная ставка") > 0;
		КонецЕсли;
	КонецЦикла;
	
	Возврат тзСтавок;
	
КонецФункции
// }
// 4D 

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	МассивИсключаемыхЗначений = Новый Массив;
	
	НалогообложениеНДС = Неопределено;
	Параметры.Свойство("НалогообложениеНДС", НалогообложениеНДС); 
	Если ЗначениеЗаполнено(НалогообложениеНДС)
		И НалогообложениеНДС <> Перечисления.ТипыНалогообложенияНДС.ПродажаНаЭкспорт Тогда
		МассивИсключаемыхЗначений.Добавить(Перечисления.СтавкиНДС.НДС0);
	КонецЕсли;
	
	Если МассивИсключаемыхЗначений.Количество() > 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ОбщегоНазначенияУТВызовСервера.ПолучитьСписокВыбораПеречисления(
			"СтавкиНДС",
			ДанныеВыбора,
			Параметры,
			МассивИсключаемыхЗначений);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
