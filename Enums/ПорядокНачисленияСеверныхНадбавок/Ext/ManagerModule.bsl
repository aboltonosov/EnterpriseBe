#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает массив описаний шкалы порядка начисления северной надбавки.
//
// Параметры:
//			ПорядокНачисления - ПеречислениеСсылка.ПорядокНачисленияСеверныхНадбавок
//
// ВозвращаемоеЗначение:
//			Массив структур:
//					РазмерПериода - Число, период в месяцах.
//					РазмерПрироста - Число, размер прироста в %.
//					Ограничение - Число, максимально возможный процент.
//
Функция ОписаниеШкалыПоПорядкуНачисления(ПорядокНачисления) Экспорт
	
	ОписаниеШкалы = Новый Массив;
	
	// В общем случае шкала может состоять из двух участков.
	Если ПорядокНачисления = ПредопределенноеЗначение("Перечисление.ПорядокНачисленияСеверныхНадбавок.Группа1Льготная") Тогда
		ОписаниеШкалы.Добавить(Новый Структура("РазмерПериода,РазмерПрироста,Ограничение",6,20,60));
		ОписаниеШкалы.Добавить(Новый Структура("РазмерПериода,РазмерПрироста,Ограничение",6,10,100));
	ИначеЕсли ПорядокНачисления = ПредопределенноеЗначение("Перечисление.ПорядокНачисленияСеверныхНадбавок.Группа1Обычная") Тогда
		ОписаниеШкалы.Добавить(Новый Структура("РазмерПериода,РазмерПрироста,Ограничение",6,10,100));
	ИначеЕсли ПорядокНачисления = ПредопределенноеЗначение("Перечисление.ПорядокНачисленияСеверныхНадбавок.Группа2Льготная") Тогда
		ОписаниеШкалы.Добавить(Новый Структура("РазмерПериода,РазмерПрироста,Ограничение",6,20,60));
		ОписаниеШкалы.Добавить(Новый Структура("РазмерПериода,РазмерПрироста,Ограничение",12,20,80));
	ИначеЕсли ПорядокНачисления = ПредопределенноеЗначение("Перечисление.ПорядокНачисленияСеверныхНадбавок.Группа2Обычная") Тогда
		ОписаниеШкалы.Добавить(Новый Структура("РазмерПериода,РазмерПрироста,Ограничение",6,10,60));
		ОписаниеШкалы.Добавить(Новый Структура("РазмерПериода,РазмерПрироста,Ограничение",12,10,80));
	ИначеЕсли ПорядокНачисления = ПредопределенноеЗначение("Перечисление.ПорядокНачисленияСеверныхНадбавок.Группа3Льготная") Тогда
		ОписаниеШкалы.Добавить(Новый Структура("РазмерПериода,РазмерПрироста,Ограничение",6,10,50));
	ИначеЕсли ПорядокНачисления = ПредопределенноеЗначение("Перечисление.ПорядокНачисленияСеверныхНадбавок.Группа3Обычная") Тогда
		ОписаниеШкалы.Добавить(Новый Структура("РазмерПериода,РазмерПрироста,Ограничение",12,10,50));
	ИначеЕсли ПорядокНачисления = ПредопределенноеЗначение("Перечисление.ПорядокНачисленияСеверныхНадбавок.Группа4Льготная") Тогда
		ОписаниеШкалы.Добавить(Новый Структура("РазмерПериода,РазмерПрироста,Ограничение",6,10,30));
	ИначеЕсли ПорядокНачисления = ПредопределенноеЗначение("Перечисление.ПорядокНачисленияСеверныхНадбавок.Группа4Обычная") Тогда
		ОписаниеШкалы.Добавить(Новый Структура("РазмерПериода,РазмерПрироста,Ограничение",12,10,10));
		ОписаниеШкалы.Добавить(Новый Структура("РазмерПериода,РазмерПрироста,Ограничение",24,10,30));
	КонецЕсли;
	
	Возврат ОписаниеШкалы;
	
КонецФункции

// Возвращает текст запроса, создающего временную таблицу ВТМаксимальныеПроцентыПорядковНачисления
// с полями ПорядокНачисленияСеверныхНадбавок и МаксимальноеЗначение.
//
// Параметры:
//			ИмяВТМаксимальныеПроцентыПорядковНачисления - Строка, желаемое имя временной таблицы.
//
// Возвращаемое значение:
//			Строка - Текст запроса, создающего временную таблицу с именем, переданным в
//						параметре ИмяВТМаксимальныеПроцентыПорядковНачисления. Временная таблица содержит поля:
//              ПорядокНачисленияСеверныхНадбавок - ПеречислениеСсылка.ПорядокНачисленияСеверныхНадбавок
//				МаксимальноеЗначение - Число, максимальный процент северной надбавки.
//
Функция ТекстЗапросаВТМаксимальныеПроцентыПорядковНачисления(ИмяВТМаксимальныеПроцентыПорядковНачисления = "ВТМаксимальныеПроцентыПорядковНачисления") Экспорт
	
	ТекстЗапроса = "";
	ИмяВременнойТаблицы = ИмяВТМаксимальныеПроцентыПорядковНачисления;
	
	Для каждого МетаданныеЗначениеПеречисления Из Метаданные.Перечисления.ПорядокНачисленияСеверныхНадбавок.ЗначенияПеречисления Цикл
		
		ИмяПредопределенногоЗначения = СтрЗаменить(МетаданныеЗначениеПеречисления.ПолноеИмя(), ".ЗначениеПеречисления", "");
		
		ЗначениеПеречисления = ПредопределенноеЗначение(ИмяПредопределенногоЗначения);
		ОписаниеШкалы = ОписаниеШкалыПоПорядкуНачисления(ЗначениеПеречисления);
		Ограничение = ОписаниеШкалы[ОписаниеШкалы.Количество() - 1].Ограничение;
		
		ТекстЗапроса = ?(ПустаяСтрока(ТекстЗапроса), "", ТекстЗапроса + Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС) +
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(" + ИмяПредопределенногоЗначения + ") КАК ПорядокНачисленияСеверныхНадбавок,
		|	" + Формат(Ограничение, "ЧГ=") + " КАК МаксимальноеЗначение";
		
		Если НЕ ПустаяСтрока(ИмяВременнойТаблицы) Тогда
			
			ТекстЗапроса = ТекстЗапроса + "
				|ПОМЕСТИТЬ " + ИмяВременнойТаблицы;
				
			ИмяВременнойТаблицы = "";
			
		КонецЕсли; 
		
	КонецЦикла;
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецЕсли
