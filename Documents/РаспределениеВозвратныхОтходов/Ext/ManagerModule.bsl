#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область СтандартныеПодсистемыКоманды

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Заполняет список команд создания на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании.
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании) Экспорт
	
	ПроизводствоСервер.ДобавитьКомандуСоздатьВыпускПродукцииБезЗаказаНаОсновании(КомандыСоздатьНаОсновании);
	
КонецПроцедуры

// Заполняет список команд создания на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании.
// Возвращаемое значение:
//   КомандаФормы - Добавленная Команда.
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСоздатьНаОсновании) Экспорт
	
	Возврат Неопределено;
	
КонецФункции

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов.
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуДвиженияДокумента(КомандыОтчетов);
	
КонецПроцедуры

// СтандартныеПодсистемы.Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати – ТаблицаЗначений – состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Печать

#КонецОбласти

#Область Серии

// Имена реквизитов, от значений которых зависят параметры указания серий.
//
//	Возвращаемое значение:
//		Строка - имена реквизитов, перечисленные через запятую.
//
Функция ИменаРеквизитовДляЗаполненияПараметровУказанияСерий() Экспорт
	
	Возврат "Получатель, Дата";
	
КонецФункции

// Возвращает параметры указания серий для товаров, указанных в документе.
//
//	Параметры:
//			Объект - Структура - структура значений реквизитов объекта, необходимых для заполнения параметров указания серий.
//	Возвращаемое значение:
//			Структура - Структура параметров указания серий для объекта.
//
Функция ПараметрыУказанияСерий(Объект) Экспорт
	
	ИспользоватьОрдернуюСхемуПриПриемке = СкладыСервер.ИспользоватьОрдернуюСхемуПриПоступлении(Объект.Получатель, Объект.Дата);
	ПараметрыСерийСклада = СкладыСервер.ИспользованиеСерийНаСкладе(Объект.Получатель, Ложь);
	
	ПараметрыУказанияСерий = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.РаспределениеВозвратныхОтходов";
	
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры = ПараметрыСерийСклада.ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ПараметрыСерийСклада.УчитыватьСебестоимостьПоСериям;
	
	ПараметрыУказанияСерий.СкладскиеОперации.Добавить(Перечисления.СкладскиеОперации.ПриемкаПродукцииИзПроизводства);
	
	ПараметрыУказанияСерий.СерииПриПланированииОтгрузкиУказываютсяВТЧСерии = Истина;
	
	ПараметрыУказанияСерий.ПоляСвязи.Добавить("Назначение");
	
	ПараметрыУказанияСерий.ЭтоНакладная = Истина;
	ПараметрыУказанияСерий.ПланированиеОтгрузки = Ложь;
	ПараметрыУказанияСерий.ПланированиеОтбора   = Ложь;
	ПараметрыУказанияСерий.ФактОтбора           = Истина;
	
	ПараметрыУказанияСерий.РегистрироватьСерии = Истина;
	
	ПараметрыУказанияСерий.ТоварВШапке = Истина;
	ПараметрыУказанияСерий.ИмяПоляСклад = "Получатель";
	ПараметрыУказанияСерий.Дата = Объект.Дата;
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Возвращает текст запроса для расчета статусов указания серий.
//	Параметры:
//		ПараметрыУказанияСерий - Структура - состав полей задается в функции НоменклатураКлиентСервер.ПараметрыУказанияСерий.
//	Возвращаемое значение:
//		Строка - текст запроса.
//
Функция ТекстЗапросаЗаполненияСтатусовУказанияСерий(ПараметрыУказанияСерий) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Товары.Серия,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Назначение,
	|	Товары.Количество,
	|	Товары.СтатусУказанияСерий,
	|	Товары.НомерСтроки
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Серия,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Назначение,
	|	СУММА(Товары.Количество) КАК Количество,
	|	ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура).ВидНоменклатуры КАК ВидНоменклатуры
	|ПОМЕСТИТЬ ТоварыДляЗапроса
	|ИЗ
	|	Товары КАК Товары
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.Серия,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Назначение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Серии.Номенклатура,
	|	Серии.Характеристика,
	|	Серии.Назначение,
	|	Серии.Количество
	|ПОМЕСТИТЬ Серии
	|ИЗ
	|	&Серии КАК Серии
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСерий.Номенклатура,
	|	ТаблицаСерий.Характеристика,
	|	ТаблицаСерий.Назначение,
	|	СУММА(ТаблицаСерий.Количество) КАК Количество
	|ПОМЕСТИТЬ СерииДляЗапроса
	|ИЗ
	|	Серии КАК ТаблицаСерий
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСерий.Номенклатура,
	|	ТаблицаСерий.Характеристика,
	|	ТаблицаСерий.Назначение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.СтатусУказанияСерий КАК СтарыйСтатусУказанияСерий,
	|	ВЫБОР
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий ЕСТЬ NULL 
	|			ТОГДА 0
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьСебестоимостьПоСериям
	|			ТОГДА ВЫБОР
	|					КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|						ТОГДА 14
	|					ИНАЧЕ 13
	|				КОНЕЦ
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриПланированииОтгрузки
	|			ТОГДА ВЫБОР
	|					КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|							И ТоварыДляЗапроса.Количество > 0
	|						ТОГДА 10
	|					ИНАЧЕ 9
	|				КОНЕЦ
	|		КОГДА Склады.ИспользоватьОрдернуюСхемуПриОтгрузке
	|				И &Дата >= Склады.ДатаНачалаОрдернойСхемыПриОтгрузке
	|			ТОГДА 0
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриПланированииОтбора
	|			ТОГДА ВЫБОР
	|					КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчетСерийПоFEFO
	|						ТОГДА ВЫБОР
	|								КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|										И ТоварыДляЗапроса.Количество > 0
	|									ТОГДА 6
	|								ИНАЧЕ 5
	|							КОНЕЦ
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|									И ТоварыДляЗапроса.Количество > 0
	|								ТОГДА 8
	|							ИНАЧЕ 7
	|						КОНЕЦ
	|				КОНЕЦ
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПоФактуОтбора
	|				И &ФактОтбора
	|				И ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриОтгрузкеКомплектовДляРазборки
	|			ТОГДА ВЫБОР
	|					КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьОстаткиСерий
	|						ТОГДА ВЫБОР
	|								КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|										И ТоварыДляЗапроса.Количество > 0
	|									ТОГДА 4
	|								ИНАЧЕ 3
	|							КОНЕЦ
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|									И ТоварыДляЗапроса.Количество > 0
	|								ТОГДА 2
	|							ИНАЧЕ 1
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтатусУказанияСерий
	|ПОМЕСТИТЬ Статусы
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыДляЗапроса КАК ТоварыДляЗапроса
	|			ЛЕВОЕ СОЕДИНЕНИЕ СерииДляЗапроса КАК СерииДляЗапроса
	|			ПО ТоварыДляЗапроса.Номенклатура = СерииДляЗапроса.Номенклатура
	|				И ТоварыДляЗапроса.Характеристика = СерииДляЗапроса.Характеристика
	|				И ТоварыДляЗапроса.Назначение = СерииДляЗапроса.Назначение
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ПолитикиУчетаСерий
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|				ПО ПолитикиУчетаСерий.Склад = Склады.Ссылка
	|			ПО (ПолитикиУчетаСерий.Склад = &Склад)
	|				И ТоварыДляЗапроса.ВидНоменклатуры = ПолитикиУчетаСерий.Ссылка
	|		ПО Товары.Номенклатура = ТоварыДляЗапроса.Номенклатура
	|			И Товары.Характеристика = ТоварыДляЗапроса.Характеристика
	|			И Товары.Назначение = ТоварыДляЗапроса.Назначение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Статусы.НомерСтроки КАК НомерСтроки,
	|	Статусы.СтатусУказанияСерий КАК СтатусУказанияСерий
	|ИЗ
	|	Статусы КАК Статусы
	|ГДЕ
	|	Статусы.СтатусУказанияСерий <> Статусы.СтарыйСтатусУказанияСерий
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса;

КонецФункции

#КонецОбласти

#Область Заполнение

// Формирует список документов, на основании переданных параметров.
//	Параметры:
//		Параметры - Структура - параметры для формирования документов.
//			Имеет структуру {ИзделияПоПравилу, ДанныеШапки, ГруппировкаЗатрат}, все ключи являются обязательными.
//			ИзделияПоПравилу - таблица значений, содержащая перечень выпущенной продукции, распределяемой по правилам.
//			ДанныеШапки - Структура - данные заполнения шапки документов.
//		СписокОбъектов - Структура - список сформированных документов.
//
Процедура ДокументыПоПараметрам(Параметры, СписокОбъектов) Экспорт
	
	ТаблицаПродукции = ПолучитьИзВременногоХранилища(Параметры.ИзделияПоПравилу);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаПродукции.Организация,
		|	ТаблицаПродукции.ПравилоРаспределения,
		|	ТаблицаПродукции.Подразделение,
		|	ТаблицаПродукции.Получатель,
		|	ТаблицаПродукции.Номенклатура,
		|	ТаблицаПродукции.Характеристика,
		|	ТаблицаПродукции.Серия,
		|	ТаблицаПродукции.Назначение,
		|	ТаблицаПродукции.Количество
		|ПОМЕСТИТЬ ТаблицаПродукции
		|ИЗ
		|	&ТаблицаПродукции КАК ТаблицаПродукции
		|ГДЕ
		|	НЕ ТаблицаПродукции.Количество = 0
		|	И НЕ ТаблицаПродукции.ОшибкаВНастройкахМодели
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаПродукции.Организация,
		|	ВЫБОР
		|		КОГДА &ЗаполнятьАвтоматически
		|			ТОГДА ТаблицаПродукции.ПравилоРаспределения
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ПравилоРаспределения,
		|	ПравилаРаспределения.БазаРаспределения КАК БазаРаспределения,
		|	ПравилаРаспределения.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	ПлановыйВидЦены.Значение КАК ВидЦены,
		|	ТаблицаПродукции.Подразделение,
		|	ТаблицаПродукции.Получатель,
		|	ТаблицаПродукции.Номенклатура,
		|	ТаблицаПродукции.Характеристика,
		|	ТаблицаПродукции.Серия,
		|	ТаблицаПродукции.Назначение,
		|	СУММА(ТаблицаПродукции.Количество) КАК Количество,
		|	СУММА(ТаблицаПродукции.Количество) КАК КоличествоПоПравилу,
		|	ЦеныНоменклатуры.Цена КАК Цена,
		|	СУММА(ТаблицаПродукции.Количество * ЦеныНоменклатуры.Цена) КАК Сумма
		|ИЗ
		|	ТаблицаПродукции КАК ТаблицаПродукции
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПравилаРаспределенияРасходов КАК ПравилаРаспределения
		|		ПО ТаблицаПродукции.ПравилоРаспределения = ПравилаРаспределения.Ссылка
		|			И (&ЗаполнятьАвтоматически)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Константа.ВидЦеныПлановойСтоимостиМатериаловРабот КАК ПлановыйВидЦены
		|		ПО (ИСТИНА)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&Дата, ) КАК ЦеныНоменклатуры
		|		ПО (ПлановыйВидЦены.Значение = ЦеныНоменклатуры.ВидЦены)
		|			И ТаблицаПродукции.Номенклатура = ЦеныНоменклатуры.Номенклатура
		|			И ТаблицаПродукции.Характеристика = ЦеныНоменклатуры.Характеристика
		|
		|СГРУППИРОВАТЬ ПО
		|	ПлановыйВидЦены.Значение,
		|	ТаблицаПродукции.Организация,
		|	ПравилаРаспределения.СтатьяКалькуляции,
		|	ТаблицаПродукции.Серия,
		|	ТаблицаПродукции.Назначение,
		|	ТаблицаПродукции.Подразделение,
		|	ТаблицаПродукции.Характеристика,
		|	ТаблицаПродукции.Номенклатура,
		|	ТаблицаПродукции.Получатель,
		|	ТаблицаПродукции.ПравилоРаспределения,
		|	ПравилаРаспределения.БазаРаспределения,
		|	ЦеныНоменклатуры.Цена";
	
	Запрос.УстановитьПараметр("ТаблицаПродукции",		ТаблицаПродукции);
	Запрос.УстановитьПараметр("ЗаполнятьАвтоматически",	Параметры.ЗаполнятьАвтоматически);
	Запрос.УстановитьПараметр("Дата",					Параметры.ДанныеШапки.Дата);
	
	ТаблицаПродукции = Запрос.Выполнить().Выгрузить();
	
	КоличествоДокументов		= СписокОбъектов.Количество();
	КоличествоНовыхДокументов	= 0;
	
	Для Каждого ТекПродукция Из ТаблицаПродукции Цикл
		
		ТекДокумент = Документы.РаспределениеВозвратныхОтходов.СоздатьДокумент();
		ТекДокумент.ПолучитьСсылкуНового();
		
		СписокОбъектов.Добавить(ТекДокумент);
		КоличествоНовыхДокументов = КоличествоНовыхДокументов + 1;
		
		ТекДокумент.Заполнить(Новый Структура("Организация", ТекПродукция.Организация));
		
		ЗаполнитьЗначенияСвойств(ТекДокумент, Параметры.ДанныеШапки);
		ЗаполнитьЗначенияСвойств(ТекДокумент, ТекПродукция);
		
		ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ТекДокумент, Документы.РаспределениеВозвратныхОтходов);
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ТекДокумент, ПараметрыУказанияСерий);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

// Возвращает текст запроса для получениях доступных назначений.
//	Параметры:
//		ПараметрыФормированияЗапроса - Структура - параметры для формирования текстов запросов.
//	Возвращаемое значение:
//		Строка - текст запроса.
//
Функция ТекстЗапросаДоступныхНазначений(ПараметрыФормированияЗапроса) Экспорт
	
	Возврат Справочники.Назначения.ТекстЗапросаНазначенийРасширенный();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

#Область Инициализация

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	// Создание запроса инициализации движений и заполенение его параметров.
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	// Формирование текста запроса.
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстЗапросаТаблицаТоварыОрганизаций(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаТоварыНаСкладах(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаМатериалыИРаботыВПроизводстве(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаСебестоимостьТоваров(Запрос, ТекстыЗапроса, Регистры);
	
	ТекстЗапросаТаблицаСвободныеОстатки(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаДвиженияСерийТоваров(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаДатыПоступленияТоваровОрганизаций(Запрос, ТекстыЗапроса, Регистры);
	
	// Исполнение запроса и выгрузка полученных таблиц для движений.
	ПроведениеСерверУТ.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)

	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);

	Запрос.Текст =
		"ВЫБРАТЬ
		|	Реквизиты.Дата,
		|	Реквизиты.Ссылка,
		|	Реквизиты.Организация,
		|	Реквизиты.ВыпускПодДеятельность,
		|	Реквизиты.НаправлениеДеятельности,
		|	Реквизиты.ПравилоРаспределения,
		|	Реквизиты.КоличествоПоПравилу,
		|	Реквизиты.СтатьяКалькуляции,
		|	Реквизиты.ВидЦены,
		|	Реквизиты.Подразделение,
		|	Реквизиты.Получатель,
		|	Реквизиты.Номенклатура,
		|	СпрНоменклатура.ТипНоменклатуры КАК ТипНоменклатуры,
		|	СпрНоменклатура.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
		|	СпрНоменклатура.ГруппаАналитическогоУчета КАК ГруппаАналитическогоУчета,
		|	Реквизиты.Характеристика,
		|	Реквизиты.Серия,
		|	Реквизиты.СтатусУказанияСерий,
		|	Реквизиты.Количество,
		|	Реквизиты.Назначение,
		|	СпрНазначения.ДвиженияПоСкладскимРегистрам КАК ДвиженияПоСкладскимРегистрам,
		|	Реквизиты.Цена,
		|	Реквизиты.Сумма,
		|	Реквизиты.ВидЗапасов,
		|	Реквизиты.ВидЗапасов.ТипЗапасов КАК ТипЗапасов,
		|	Реквизиты.АналитикаУчетаНоменклатуры,
		|	Реквизиты.Валюта
		|ИЗ
		|	Документ.РаспределениеВозвратныхОтходов КАК Реквизиты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО (СпрНоменклатура.Ссылка = Реквизиты.Номенклатура)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК СпрНазначения
		|		ПО (СпрНазначения.Ссылка = Реквизиты.Назначение)
		|ГДЕ
		|	Реквизиты.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("УчитыватьСебестоимостьТоваровПоВидамЗапасов",
								ПолучитьФункциональнуюОпцию("УчитыватьСебестоимостьТоваровПоВидамЗапасов"));
	
	Запрос.УстановитьПараметр("АналитическийУчетПоГруппамПродукции",
								ПолучитьФункциональнуюОпцию("АналитическийУчетПоГруппамПродукции"));
	
	УниверсальныеМеханизмыПартийИСебестоимости.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
	УстановитьПараметрЗапросаВидЗапасовРаботы(Запрос);
	
КонецПроцедуры

Процедура УстановитьПараметрЗапросаВидЗапасовРаботы(Запрос)
	
	Если Запрос.Параметры.Свойство("ВидЗапасовРаботы") Тогда
		Возврат;
	КонецЕсли;
	
	ДопКолонки = Новый Структура("Комитент, Соглашение, Валюта, НалогообложениеНДС, НалогообложениеОрганизации, Поставщик, Подразделение, Менеджер, Сделка");
	ДопКолонки.Вставить("ОбособленныйУчетТоваровПоСделке",	Ложь);
	ДопКолонки.Вставить("ДеятельностьОблагаетсяЕНВД",		Ложь);
	ДопКолонки.Вставить("ВидЗапасов",						Справочники.ВидыЗапасов.ПустаяСсылка());
	ДопКолонки.Вставить("ТипЗапасов",						Перечисления.ТипыЗапасов.Услуга);
	ДопКолонки.Вставить("ГруппаФинансовогоУчета",			Справочники.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка());
	ДопКолонки.Вставить("ВариантОбособленногоУчетаТоваров",	Перечисления.ВариантыОбособленногоУчетаТоваров.НеВедется);
	ДопКолонки.Вставить("Организация",						Запрос.Параметры.Организация);
	ДопКолонки.Вставить("ХозяйственнаяОперация",			Перечисления.ХозяйственныеОперации.ПередачаВПроизводство);
	
	Запрос.УстановитьПараметр("ВидЗапасовРаботы", Справочники.ВидыЗапасов.ВидЗапасовДокумента(ДопКолонки.Организация, ДопКолонки.ХозяйственнаяОперация, ДопКолонки));
	
КонецПроцедуры

Процедура УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос)
	
	Если Запрос.Параметры.Свойство("КоэффициентПересчетаВВалютуУпр")
		И Запрос.Параметры.Свойство("КоэффициентПересчетаВВалютуРегл") Тогда
		Возврат;
	КонецЕсли;
	
	Коэффициенты = РаботаСКурсамивалютУТ.ПолучитьКоэффициентыПересчетаВалюты(Запрос.Параметры.Валюта,
		                                                                         Запрос.Параметры.Валюта,
		                                                                         Запрос.Параметры.Дата);
	
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуУпр",  Коэффициенты.КоэффициентПересчетаВВалютуУпр);
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуРегл", Коэффициенты.КоэффициентПересчетаВВалютуРегл);
	
КонецПроцедуры

Процедура ИнициализироватьКлючиАналитикиНоменклатуры(Запрос)

	Если Запрос = Неопределено ИЛИ Запрос.Параметры.Свойство("КлючиАналитикиНоменклатурыИнициализированы") Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросАналитики = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТовары.Склад КАК Склад,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.Назначение КАК Назначение,
	|	ТаблицаТовары.Серия КАК Серия,
	|	ТаблицаТовары.СтатьяКалькуляции
	|ИЗ
	|	(ВЫБРАТЬ
	|		&Получатель КАК Склад,
	|		&Номенклатура КАК Номенклатура,
	|		&Характеристика КАК Характеристика,
	|		&Назначение КАК Назначение,
	|		&Серия КАК Серия,
	|		ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) КАК СтатьяКалькуляции
	|	ГДЕ
	|		&УчитыватьСебестоимостьТоваровПоВидамЗапасов
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		&Получатель КАК Склад,
	|		&Номенклатура КАК Номенклатура,
	|		&Характеристика КАК Характеристика,
	|		ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
	|		&Серия КАК Серия,
	|		ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) КАК СтатьяКалькуляции
	|	ГДЕ
	|		НЕ &УчитыватьСебестоимостьТоваровПоВидамЗапасов
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		&Подразделение,
	|		&Номенклатура,
	|		&Характеристика,
	|		ЕСТЬNULL(ЕСТЬNULL(КлючиПартийПроизводства.Назначение, ЭтапПроизводства2_2.Назначение), ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)),
	|		ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка),
	|		ТаблицаТовары.СтатьяКалькуляции
	|	ИЗ
	|		Документ.РаспределениеВозвратныхОтходов.ПартииПроизводства КАК ТаблицаТовары
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартийПроизводства КАК КлючиПартийПроизводства
	|			ПО (КлючиПартийПроизводства.Ссылка = ТаблицаТовары.АналитикаУчетаПартийПроизводства)
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства2_2
	|			ПО (ЭтапПроизводства2_2.Ссылка = ТаблицаТовары.Этап)
	|	ГДЕ
	|		&УчитыватьСебестоимостьТоваровПоВидамЗапасов
	|		И ТаблицаТовары.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		&Подразделение,
	|		&Номенклатура,
	|		&Характеристика,
	|		ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка),
	|		ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка),
	|		ТаблицаТовары.СтатьяКалькуляции
	|	ИЗ
	|		Документ.РаспределениеВозвратныхОтходов.ПартииПроизводства КАК ТаблицаТовары
	|	ГДЕ
	|		НЕ &УчитыватьСебестоимостьТоваровПоВидамЗапасов
	|		И ТаблицаТовары.Ссылка = &Ссылка) КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|		ПО ТаблицаТовары.Номенклатура = Аналитика.Номенклатура
	|			И ТаблицаТовары.Характеристика = Аналитика.Характеристика
	|			И ТаблицаТовары.Серия = Аналитика.Серия
	|			И ТаблицаТовары.Склад = Аналитика.Склад
	|			И ТаблицаТовары.Назначение = Аналитика.Назначение
	|			И ТаблицаТовары.СтатьяКалькуляции = Аналитика.СтатьяКалькуляции
	|ГДЕ
	|	Аналитика.Номенклатура ЕСТЬ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Склад,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Назначение,
	|	ТаблицаТовары.Серия,
	|	ТаблицаТовары.СтатьяКалькуляции");
	
	ЗапросАналитики.УстановитьПараметр("Ссылка",					Запрос.Параметры.Ссылка);
	ЗапросАналитики.УстановитьПараметр("Получатель",				Запрос.Параметры.Получатель);
	ЗапросАналитики.УстановитьПараметр("Номенклатура",				Запрос.Параметры.Номенклатура);
	ЗапросАналитики.УстановитьПараметр("Характеристика",			Запрос.Параметры.Характеристика);
	ЗапросАналитики.УстановитьПараметр("Назначение",				Запрос.Параметры.Назначение);
	ЗапросАналитики.УстановитьПараметр("Серия",						Запрос.Параметры.Серия);
	ЗапросАналитики.УстановитьПараметр("Подразделение",	Запрос.Параметры.Подразделение);
	ЗапросАналитики.УстановитьПараметр("УчитыватьСебестоимостьТоваровПоВидамЗапасов",	Запрос.Параметры.УчитыватьСебестоимостьТоваровПоВидамЗапасов);
	
	Выборка = ЗапросАналитики.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РегистрыСведений.АналитикаУчетаНоменклатуры.СоздатьКлючАналитики(Выборка)
	КонецЦикла;

	Запрос.УстановитьПараметр("КлючиАналитикиНоменклатурыИнициализированы", Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ТекстыЗапросовПроведения

Функция ТекстЗапросаТаблицаТоварыОрганизаций(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТоварыОрганизаций";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса =
	// приход возвратных отходов в кладовую
	"ВЫБРАТЬ
	|	&Дата																				КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)												КАК ВидДвижения,
	|	АналитикаПоПолучателю.КлючАналитики													КАК АналитикаУчетаНоменклатуры,
	|	&Организация																		КАК Организация,
	|	&ВидЗапасов																			КАК ВидЗапасов,
	|	&Количество																			КАК Количество,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)	КАК ХозяйственнаяОперация,
	|	&ВыпускПодДеятельность																КАК НалогообложениеНДС,
	|	&Номенклатура																		КАК Номенклатура,
	|	&Характеристика																		КАК Характеристика,
	|	ИСТИНА																				КАК Первичное
	|ИЗ
	|	РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПоПолучателю
	|ГДЕ
	|	НЕ &ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И &Номенклатура = АналитикаПоПолучателю.Номенклатура
	|	И &Характеристика = АналитикаПоПолучателю.Характеристика
	|	И &Получатель = АналитикаПоПолучателю.Склад
	|	И &Назначение = АналитикаПоПолучателю.Назначение
	|	И &Серия = АналитикаПоПолучателю.Серия
	|	И (ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) = АналитикаПоПолучателю.СтатьяКалькуляции)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаТоварыНаСкладах(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТоварыНаСкладах";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|	&Дата									КАК Период,
	|	&Получатель								КАК Склад,
	|	&Номенклатура							КАК Номенклатура,
	|	&Характеристика							КАК Характеристика,
	|	ВЫБОР
	|		КОГДА &ДвиженияПоСкладскимРегистрам
	|			ТОГДА &Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ									КАК Назначение,
	|	&Серия									КАК Серия,
	|	&Количество								КАК ВНаличии,
	|	ВЫБОР
	|		КОГДА &СтатусУказанияСерий = 14
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ									КАК КонтролироватьОстатки
	|ГДЕ
	|	НЕ &ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И НЕ &СтатусУказанияСерий В (4, 6, 8, 10)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|	&Дата									КАК Период,
	|	&Получатель								КАК Склад,
	|	&Номенклатура							КАК Номенклатура,
	|	&Характеристика							КАК Характеристика,
	|	ВЫБОР
	|		КОГДА &ДвиженияПоСкладскимРегистрам
	|			ТОГДА &Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ									КАК Назначение,
	|	ТаблицаТовары.Серия						КАК Серия,
	|	ТаблицаТовары.Количество				КАК ВНаличии,
	|	ВЫБОР
	|		КОГДА &СтатусУказанияСерий В (6, 8, 10)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ									КАК КонтролироватьОстатки
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов.Серии КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И НЕ &ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И &СтатусУказанияСерий В (4, 6, 8, 10)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаСебестоимостьТоваров(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СебестоимостьТоваров";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ИнициализироватьКлючиАналитикиНоменклатуры(Запрос);
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	
	ТекстЗапроса =
	// приход возвратных отходов по получателю с назначением
	"ВЫБРАТЬ
	|	1																					КАК Порядок,
	|	&Дата																				КАК Период,
	|	&Ссылка																				КАК ДокументДвижения,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)												КАК ВидДвижения,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)										КАК ТипЗаписи,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)	КАК ХозяйственнаяОперация,
	|	ВЫБОР
	|		КОГДА &ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца)
	|				ИЛИ &ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПродукцияДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|	КОНЕЦ																				КАК РазделУчета,
	|	&Организация																		КАК Организация,
	|	&ВидЗапасов																			КАК ВидЗапасов,
	|	АналитикаПоПолучателю.КлючАналитики													КАК АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|				И &ФИФОСкользящаяОценка
	|			ТОГДА &Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																				КАК Партия,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаУчетаПартий,
	|	ВЫБОР
	|		КОГДА &АналитическийУчетПоГруппамПродукции
	|			И НЕ &ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка) ТОГДА
	|			&ГруппаАналитическогоУчета
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																				КАК АналитикаФинансовогоУчета,
	|	&ВыпускПодДеятельность																КАК ВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО																		КАК КорРазделУчета,
	|	&Организация																		КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО																		КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО																		КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО																		КАК КорВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО																		КАК КорПартия,
	|	НЕОПРЕДЕЛЕНО																		КАК КорАналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаУчетаПартийПроизводства,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяКалькуляции,
	|	НЕОПРЕДЕЛЕНО																		КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаАктивовПассивов,
	|	&Количество																			КАК Количество,
	|	ВЫРАЗИТЬ(&Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2))					КАК Стоимость,
	|	ВЫРАЗИТЬ(&Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2))					КАК СтоимостьБезНДС,
	|	ВЫРАЗИТЬ(&Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))				КАК СтоимостьРегл
	|ИЗ
	|	РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПоПолучателю
	|ГДЕ
	|		&УчитыватьСебестоимостьТоваровПоВидамЗапасов
	|		И &Номенклатура = АналитикаПоПолучателю.Номенклатура
	|		И &Характеристика = АналитикаПоПолучателю.Характеристика
	|		И &Получатель = АналитикаПоПолучателю.Склад
	|		И &Назначение = АналитикаПоПолучателю.Назначение
	|		И &Серия = АналитикаПоПолучателю.Серия
	|		И (ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) = АналитикаПоПолучателю.СтатьяКалькуляции)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// приход возвратных отходов по получателю без назначения
	|ВЫБРАТЬ
	|	1																					КАК Порядок,
	|	&Дата																				КАК Период,
	|	&Ссылка																				КАК ДокументДвижения,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)												КАК ВидДвижения,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)										КАК ТипЗаписи,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)	КАК ХозяйственнаяОперация,
	|	ВЫБОР
	|		КОГДА &ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца)
	|				ИЛИ &ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПродукцияДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|	КОНЕЦ																				КАК РазделУчета,
	|	&Организация																		КАК Организация,
	|	НЕОПРЕДЕЛЕНО																		КАК ВидЗапасов,
	|	АналитикаПоПолучателю.КлючАналитики													КАК АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|				И &ФИФОСкользящаяОценка
	|			ТОГДА &Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																				КАК Партия,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаУчетаПартий,
	|	ВЫБОР
	|		КОГДА &АналитическийУчетПоГруппамПродукции
	|			И НЕ &ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка) ТОГДА
	|			&ГруппаАналитическогоУчета
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																				КАК АналитикаФинансовогоУчета,
	|	&ВыпускПодДеятельность																КАК ВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО																		КАК КорРазделУчета,
	|	&Организация																		КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО																		КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО																		КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО																		КАК КорВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО																		КАК КорПартия,
	|	НЕОПРЕДЕЛЕНО																		КАК КорАналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаУчетаПартийПроизводства,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяКалькуляции,
	|	НЕОПРЕДЕЛЕНО																		КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаАктивовПассивов,
	|	&Количество																			КАК Количество,
	|	ВЫРАЗИТЬ(&Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2))					КАК Стоимость,
	|	ВЫРАЗИТЬ(&Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2))					КАК СтоимостьБезНДС,
	|	ВЫРАЗИТЬ(&Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))				КАК СтоимостьРегл
	|ИЗ
	|	РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПоПолучателю
	|ГДЕ
	|		НЕ &УчитыватьСебестоимостьТоваровПоВидамЗапасов
	|		И &Номенклатура = АналитикаПоПолучателю.Номенклатура
	|		И &Характеристика = АналитикаПоПолучателю.Характеристика
	|		И &Получатель = АналитикаПоПолучателю.Склад
	|		И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаПоПолучателю.Назначение
	|		И &Серия = АналитикаПоПолучателю.Серия
	|		И (ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) = АналитикаПоПолучателю.СтатьяКалькуляции)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// приход возвратных отходов по партии производства
	|ВЫБРАТЬ
	|	2																					КАК Порядок,
	|	&Дата																				КАК Период,
	|	&Ссылка																				КАК ДокументДвижения,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)												КАК ВидДвижения,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)										КАК ТипЗаписи,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)	КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство)	КАК РазделУчета,
	|	&Организация																		КАК Организация,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов
	|			ТОГДА &ВидЗапасов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																				КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов
	|			ТОГДА АналитикаНоменклатурыПоПартии.КлючАналитики
	|		ИНАЧЕ АналитикаНоменклатурыПоПартииБезНазначения.КлючАналитики
	|	КОНЕЦ																				КАК АналитикаУчетаНоменклатуры,
	|	ПартииПроизводства.ПартияПроизводства												КАК Партия,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаУчетаПартий,
	|	ВЫБОР
	|		КОГДА &АналитическийУчетПоГруппамПродукции
	|			И НЕ ОсновныеИзделия.Номенклатура ЕСТЬ NULL
	|			И НЕ ОсновныеИзделия.Номенклатура.ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА ОсновныеИзделия.Номенклатура.ГруппаАналитическогоУчета
	|		КОГДА &АналитическийУчетПоГруппамПродукции
	|			И НЕ ВыпускающиеЭтапы.ОсновноеИзделиеНоменклатура.ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА ВыпускающиеЭтапы.ОсновноеИзделиеНоменклатура.ГруппаАналитическогоУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																				КАК АналитикаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО																		КАК ВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО																		КАК КорРазделУчета,
	|	&Организация																		КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО																		КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО																		КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО																		КАК КорВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО																		КАК КорПартия,
	|	НЕОПРЕДЕЛЕНО																		КАК КорАналитикаУчетаПартий,
	|	ПартииПроизводства.АналитикаУчетаПартийПроизводства									КАК АналитикаУчетаПартийПроизводства,
	|	ПартииПроизводства.СтатьяКалькуляции												КАК СтатьяКалькуляции,
	|	НЕОПРЕДЕЛЕНО																		КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаАктивовПассивов,
	|	-ПартииПроизводства.Количество														КАК Количество,
	|	ВЫРАЗИТЬ(-&Цена * ПартииПроизводства.Количество * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2))		КАК Стоимость,
	|	ВЫРАЗИТЬ(-&Цена * ПартииПроизводства.Количество * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2))		КАК СтоимостьБезНДС,
	|	ВЫРАЗИТЬ(-&Цена * ПартииПроизводства.Количество * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))	КАК СтоимостьРегл
	|ИЗ
	|	Документ.РаспределениеВозвратныхОтходов.ПартииПроизводства КАК ПартииПроизводства
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартийПроизводства КАК КлючиПартийПроизводства
	|		ПО (КлючиПартийПроизводства.Ссылка = ПартииПроизводства.АналитикаУчетаПартийПроизводства)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства2_2
	|		ПО (ЭтапПроизводства2_2.Ссылка = ПартииПроизводства.Этап)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ВыпускающиеЭтапы
	|		ПО (ВыпускающиеЭтапы.Ссылка = ПартииПроизводства.ПартияПроизводства)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаНоменклатурыПоПартии
	|		ПО &Номенклатура = АналитикаНоменклатурыПоПартии.Номенклатура
	|			И &Характеристика = АналитикаНоменклатурыПоПартии.Характеристика
	|			И АналитикаНоменклатурыПоПартии.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			И &Подразделение = АналитикаНоменклатурыПоПартии.Склад
	|			И ЕСТЬNULL(ЕСТЬNULL(КлючиПартийПроизводства.Назначение, ЭтапПроизводства2_2.Назначение), ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)) = АналитикаНоменклатурыПоПартии.Назначение
	|			И ПартииПроизводства.СтатьяКалькуляции = АналитикаНоменклатурыПоПартии.СтатьяКалькуляции
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаНоменклатурыПоПартииБезНазначения
	|		ПО &Номенклатура = АналитикаНоменклатурыПоПартииБезНазначения.Номенклатура
	|			И &Характеристика = АналитикаНоменклатурыПоПартииБезНазначения.Характеристика
	|			И АналитикаНоменклатурыПоПартииБезНазначения.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			И &Подразделение = АналитикаНоменклатурыПоПартииБезНазначения.Склад
	|			И ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) = АналитикаНоменклатурыПоПартииБезНазначения.Назначение
	|			И ПартииПроизводства.СтатьяКалькуляции = АналитикаНоменклатурыПоПартииБезНазначения.СтатьяКалькуляции
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ОсновныеИзделия
	|		ПО ОсновныеИзделия.ОсновноеИзделиеГруппыЗатрат
	|			И ОсновныеИзделия.Ссылка = ПартииПроизводства.ПартияПроизводства
	|			И ОсновныеИзделия.АналитикаУчетаПартийПроизводства = ПартииПроизводства.АналитикаУчетаПартийПроизводства
	|ГДЕ
	|	ПартииПроизводства.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// приход возвратных отходов по правилам
	|ВЫБРАТЬ
	|	2																					КАК Порядок,
	|	&Дата																				КАК Период,
	|	&Ссылка																				КАК ДокументДвижения,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)												КАК ВидДвижения,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)										КАК ТипЗаписи,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)	КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)		КАК РазделУчета,
	|	&Организация																		КАК Организация,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов
	|			ТОГДА &ВидЗапасов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																				КАК ВидЗапасов,
	|	&АналитикаУчетаНоменклатуры															КАК АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|				И &ФИФОСкользящаяОценка
	|			ТОГДА &Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																				КАК Партия,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО																		КАК ВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО																		КАК КорРазделУчета,
	|	&Организация																		КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО																		КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО																		КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО																		КАК КорВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО																		КАК КорПартия,
	|	НЕОПРЕДЕЛЕНО																		КАК КорАналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаУчетаПартийПроизводства,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяКалькуляции,
	|	НЕОПРЕДЕЛЕНО																		КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаАктивовПассивов,
	|	-&КоличествоПоПравилу																КАК Количество,
	|	ВЫРАЗИТЬ(-&Цена * &КоличествоПоПравилу * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2))	КАК Стоимость,
	|	ВЫРАЗИТЬ(-&Цена * &КоличествоПоПравилу * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2))	КАК СтоимостьБезНДС,
	|	ВЫРАЗИТЬ(-&Цена * &КоличествоПоПравилу * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))	КАК СтоимостьРегл
	|ГДЕ
	| НЕ &КоличествоПоПравилу = 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаМатериалыИРаботыВПроизводстве(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "МатериалыИРаботыВПроизводстве";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	// приход работ по фиксированной стоимости в выпускающее подразделение
	"ВЫБРАТЬ
	|	&Дата									КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|	&Организация							КАК Организация,
	|	&Подразделение							КАК Подразделение,
	|	&Номенклатура							КАК Номенклатура,
	|	&Характеристика							КАК Характеристика,
	|	&Назначение								КАК Назначение,
	|	&Количество								КАК Количество
	|ГДЕ
	|	&ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И НЕ &Подразделение = &Получатель
	|
	|ОБЪЕДИНИТЬ ВСЕ
	// расход работ по фиксированной стоимости из выпускающего подразделения
	|ВЫБРАТЬ
	|	&Дата									КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)	КАК ВидДвижения,
	|	&Организация							КАК Организация,
	|	&Подразделение							КАК Подразделение,
	|	&Номенклатура							КАК Номенклатура,
	|	&Характеристика							КАК Характеристика,
	|	&Назначение								КАК Назначение,
	|	&Количество								КАК Количество
	|ГДЕ
	|	&ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И НЕ &Подразделение = &Получатель
	|
	|ОБЪЕДИНИТЬ ВСЕ
	// приход работ по фиксированной стоимости по получателю
	|ВЫБРАТЬ
	|	&Дата									КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|	&Организация							КАК Организация,
	|	&Получатель								КАК Подразделение,
	|	&Номенклатура							КАК Номенклатура,
	|	&Характеристика							КАК Характеристика,
	|	&Назначение								КАК Назначение,
	|	&Количество								КАК Количество
	|ГДЕ
	|	&ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаСвободныеОстатки(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "СвободныеОстатки";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса = 
	// Поступление побочных изделий в кладовую.
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|	&Дата									КАК Период,
	|	&Получатель								КАК Склад,
	|	&Номенклатура							КАК Номенклатура,
	|	&Характеристика							КАК Характеристика,
	|	0										КАК ВРезервеСоСклада,
	|	ВЫБОР
	|		КОГДА &Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|			ТОГДА &Количество
	|		ИНАЧЕ 0
	|	КОНЕЦ									КАК ВРезервеПодЗаказ,
	|	&Количество								КАК ВНаличии
	|ГДЕ
	|	НЕ &ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаДвиженияСерийТоваров(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ДвиженияСерийТоваров";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	&Дата																	КАК Период,
		|	&Номенклатура															КАК Номенклатура,
		|	&Характеристика															КАК Характеристика,
		|	ВЫБОР
		|		КОГДА &ДвиженияПоСкладскимРегистрам
		|			ТОГДА &Назначение
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|	КОНЕЦ																	КАК Назначение,
		|	&Серия																	КАК Серия,
		|	&Количество																КАК Количество,
		|	&Получатель																КАК Отправитель,
		|	&Получатель																КАК Получатель,
		|	ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПриемкаПродукцииИзПроизводства)	КАК СкладскаяОперация,
		|	&Ссылка																	КАК Документ,
		|	НЕ СпрСклады.ИспользоватьОрдернуюСхемуПриПоступлении
		|		ИЛИ &Дата < СпрСклады.ДатаНачалаОрдернойСхемыПриПоступлении			КАК ЭтоСкладскоеДвижение
		|ИЗ
		|	Справочник.Склады КАК СпрСклады
		|ГДЕ
		|	НЕ &Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
		|	И СпрСклады.Ссылка = &Получатель
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Дата																	КАК Период,
		|	&Номенклатура															КАК Номенклатура,
		|	&Характеристика															КАК Характеристика,
		|	ВЫБОР
		|		КОГДА &ДвиженияПоСкладскимРегистрам
		|			ТОГДА &Назначение
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|	КОНЕЦ																	КАК Назначение,
		|	ТаблицаТовары.Серия														КАК Серия,
		|	ТаблицаТовары.Количество												КАК Количество,
		|	&Получатель																КАК Отправитель,
		|	&Получатель																КАК Получатель,
		|	ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПриемкаПродукцииИзПроизводства)	КАК СкладскаяОперация,
		|	&Ссылка																	КАК Документ,
		|	НЕ СпрСклады.ИспользоватьОрдернуюСхемуПриПоступлении
		|		ИЛИ &Дата < СпрСклады.ДатаНачалаОрдернойСхемыПриПоступлении			КАК ЭтоСкладскоеДвижение
		|ИЗ
		|	Документ.РаспределениеВозвратныхОтходов.Серии КАК ТаблицаТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеВозвратныхОтходов КАК Шапка
		|		ПО ТаблицаТовары.Ссылка = Шапка.Ссылка
		|			И ТаблицаТовары.Номенклатура = Шапка.Номенклатура
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК СпрСклады
		|		ПО (СпрСклады.Ссылка = &Получатель)
		|ГДЕ
		|	ТаблицаТовары.Ссылка = &Ссылка
		|	И НЕ ТаблицаТовары.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаДатыПоступленияТоваровОрганизаций(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДатыПоступленияТоваровОрганизаций";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса =
	// приход возвратных отходов в кладовую
	"ВЫБРАТЬ
	|	&Дата										КАК ДатаПоступления,
	|	&Номенклатура								КАК Номенклатура,
	|	&Характеристика								КАК Характеристика,
	|	&Серия										КАК Серия,
	|	&Назначение									КАК Назначение,
	|	&ВидЗапасов									КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)	КАК НомерГТД
	|ГДЕ
	|	НЕ &ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Дата										КАК ДатаПоступления,
	|	&Номенклатура								КАК Номенклатура,
	|	&Характеристика								КАК Характеристика,
	|	&Серия										КАК Серия,
	|	&Назначение									КАК Назначение,
	|	&ВидЗапасов									КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)	КАК НомерГТД
	|ИЗ
	|	РегистрСведений.ДатыПоступленияТоваровОрганизаций КАК ПоступленияТоваров
	|ГДЕ
	|	&ВидЗапасов = ПоступленияТоваров.ВидЗапасов
	|	И &Номенклатура = ПоступленияТоваров.Номенклатура
	|	И &Характеристика = ПоступленияТоваров.Характеристика
	|	И &Серия = ПоступленияТоваров.Серия
	|	И &Назначение = ПоступленияТоваров.Назначение
	|	И (ПоступленияТоваров.НомерГТД = ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка))
	|	И ПоступленияТоваров.ДатаПоступления < &Дата
	|
	|УПОРЯДОЧИТЬ ПО
	|	&ВидЗапасов,
	|	&Номенклатура,
	|	&Характеристика,
	|	&Серия,
	|	&Назначение";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти

#КонецЕсли