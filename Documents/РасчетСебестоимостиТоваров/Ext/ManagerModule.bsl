#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
//++ НЕ УТ
#Область ТекстыЗапросов

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//                                                                               
Функция ТекстОтраженияВРеглУчете() Экспорт
	
#Область ТекстПогрешностьРасчетаСебестоимостиТовары

	ТекстПогрешностьРасчетаСебестоимостиТовары = "
	|ВЫБРАТЬ //// Погрешность расчета себестоимости (Дт 91.02 :: Кт 41.01)
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Строки.Сумма КАК Сумма,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	ЕСТЬNULL(Строки.Склад.Подразделение, НЕОПРЕДЕЛЕНО) КАК ПодразделениеДт,
	|	ЕСТЬNULL(Строки.НаправленияДеятельности, НЕОПРЕДЕЛЕНО) КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеРасходы) КАК СчетДт,
	|
	|	Строки.СтатьяРасходов КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СуммаНУ КАК СуммаНУДт,
	|	Строки.СуммаПР КАК СуммаПРДт,
	|	Строки.СуммаВР КАК СуммаВРДт,
	|
	|	ВЫБОР КОГДА Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию) ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПередачаНаКомиссию)
	|	КОГДА Строки.Склад ССЫЛКА Справочник.Партнеры ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НаСкладе)
	|	КОНЕЦ КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	ВЫБОР КОГДА Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию) ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	ИНАЧЕ
	|		Строки.Склад
	|	КОНЕЦ КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	ЕСТЬNULL(Строки.Склад.Подразделение, НЕОПРЕДЕЛЕНО) КАК ПодразделениеКт,
	|	ЕСТЬNULL(Строки.НаправленияДеятельности, НЕОПРЕДЕЛЕНО) КАК НаправлениеДеятельностиКт,
	|
	|	ВЫБОР КОГДА Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию) ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	КОГДА Строки.Склад ССЫЛКА Справочник.Партнеры ТОГДА
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыКОформлениюОтчетовКомитенту)
	|	ИНАЧЕ
	|		НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СчетКт,
	|
	|	ВЫБОР КОГДА Строки.Склад ССЫЛКА Справочник.Партнеры ТОГДА
	|		Строки.Контрагент
	|	ИНАЧЕ
	|		Строки.Номенклатура
	|	КОНЕЦ КАК СубконтоКт1,
	|	ВЫБОР КОГДА Строки.Склад ССЫЛКА Справочник.Партнеры ТОГДА
	|		Строки.Номенклатура
	|	ИНАЧЕ
	|		Строки.Склад
	|	КОНЕЦ КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СуммаНУ КАК СуммаНУКт,
	|	Строки.СуммаПР КАК СуммаПРКт,
	|	Строки.СуммаВР КАК СуммаВРКт,
	|	""%1"" КАК Содержание
	|ИЗ
	|	Документ.РасчетСебестоимостиТоваров КАК Операция
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			Движения.Организация КАК Организация,
	|			Аналитика.Номенклатура КАК Номенклатура,
	|			Аналитика.Склад КАК Склад,
	|			ВЫБОР КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета ТОГДА
	|				Движения.ВидЗапасов.ГруппаФинансовогоУчета
	|			ИНАЧЕ
	|				Аналитика.Номенклатура.ГруппаФинансовогоУчета
	|			КОНЕЦ КАК ГруппаФинансовогоУчета,
	|			Назначения.НаправлениеДеятельности КАК НаправленияДеятельности,
	|			Движения.СтатьяРасходовСписания КАК СтатьяРасходов,
	|			Движения.РазделУчета,
	|			ЕСТЬNULL(Комиссия.Контрагент, Движения.ВидЗапасов.Контрагент) КАК Контрагент,
	|			СУММА(Движения.СтоимостьРегл
	|				+ Движения.ДопРасходыРегл
	|				+ Движения.ТрудозатратыРегл
	|				+ Движения.ПостатейныеРегл) КАК Сумма,
	|			СУММА(Движения.СтоимостьРегл
	|				+ Движения.ДопРасходыРегл
	|				+ Движения.ТрудозатратыРегл
	|				+ Движения.ПостатейныеРегл
	|				- Движения.ПостояннаяРазница
	|				- Движения.ВременнаяРазница) КАК СуммаНУ,
	|			СУММА(Движения.ПостояннаяРазница) КАК СуммаПР,
	|			СУММА(Движения.ВременнаяРазница) КАК СуммаВР
	|		ИЗ
	|			РегистрНакопления.СебестоимостьТоваров КАК Движения
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|				РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|			ПО
	|				Движения.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ
	|				Справочник.ВидыЗапасов КАК ВидыЗапасов
	|			ПО
	|				Движения.ВидЗапасов = ВидыЗапасов.Ссылка
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ
	|				Справочник.Назначения КАК Назначения
	|			ПО
	|				Аналитика.Назначение = Назначения.Ссылка
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ Комиссия КАК Комиссия
	|				ПО Комиссия.Организация = Движения.Организация
	|				И Комиссия.АналитикаУчетаНоменклатуры = Движения.АналитикаУчетаНоменклатуры
	|				И Комиссия.ВидЗапасов = Движения.ВидЗапасов
	|				И Комиссия.РазделУчета = Движения.РазделУчета
	|		ГДЕ
	|			Движения.Регистратор = &Ссылка
	|			И Движения.Активность
	|			И Движения.РазделУчета В (
	|				ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах),
	|				ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию))
	|			И Движения.СтатьяРасходовСписания = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПогрешностьРасчетаСебестоимости)
	|			И Движения.ДокументДвижения В (НЕОПРЕДЕЛЕНО, &Ссылка)
	|
	|		СГРУППИРОВАТЬ ПО
	|			Движения.Организация,
	|			Движения.РазделУчета,
	|			Аналитика.Номенклатура,
	|			Аналитика.Склад,
	|			ЕСТЬNULL(Комиссия.Контрагент, Движения.ВидЗапасов.Контрагент),
	|			ВЫБОР КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета ТОГДА
	|				Движения.ВидЗапасов.ГруппаФинансовогоУчета
	|			ИНАЧЕ
	|				Аналитика.Номенклатура.ГруппаФинансовогоУчета
	|			КОНЕЦ,
	|			Назначения.НаправлениеДеятельности,
	|			Движения.СтатьяРасходовСписания
	|		) КАК Строки
	|	ПО
	|		ИСТИНА
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И (Строки.Сумма <> 0 ИЛИ Строки.СуммаНУ <> 0 ИЛИ Строки.СуммаВР <> 0 ИЛИ Строки.СуммаПР <> 0)
	|";
#КонецОбласти

#Область ТекстПогрешностьРасчетаСебестоимостиПереработка

	ТекстПогрешностьРасчетаСебестоимостиПереработка = "
	|ВЫБРАТЬ //// Погрешность расчета себестоимости (Дт 91.02 :: Кт 10.07)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Строки.Сумма КАК Сумма,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеРасходы) КАК СчетДт,
	|
	|	Строки.СтатьяРасходов КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СуммаНУ КАК СуммаНУДт,
	|	Строки.СуммаПР КАК СуммаПРДт,
	|	Строки.СуммаВР КАК СуммаВРДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.МатериалыПереданныеВПереработку) КАК СчетКт,
	|
	|	Строки.Контрагент КАК СубконтоКт1,
	|	Строки.Номенклатура КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СуммаНУ КАК СуммаНУКт,
	|	Строки.СуммаПР КАК СуммаПРКт,
	|	Строки.СуммаВР КАК СуммаВРКт,
	|	""%2"" КАК Содержание
	|ИЗ
	|	Документ.РасчетСебестоимостиТоваров КАК Операция
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			Движения.Организация КАК Организация,
	|			Аналитика.Номенклатура КАК Номенклатура,
	|			Движения.СтатьяРасходовСписания КАК СтатьяРасходов,
	|			ЕСТЬNULL(ПередачиПереработчику.Контрагент, НЕОПРЕДЕЛЕНО) КАК Контрагент,
	|			СУММА(Движения.СтоимостьРегл
	|				+ Движения.ДопРасходыРегл
	|				+ Движения.ТрудозатратыРегл
	|				+ Движения.ПостатейныеРегл) КАК Сумма,
	|			СУММА(Движения.СтоимостьРегл
	|				+ Движения.ДопРасходыРегл
	|				+ Движения.ТрудозатратыРегл
	|				+ Движения.ПостатейныеРегл
	|				- Движения.ПостояннаяРазница
	|				- Движения.ВременнаяРазница) КАК СуммаНУ,
	|			СУММА(Движения.ПостояннаяРазница) КАК СуммаПР,
	|			СУММА(Движения.ВременнаяРазница) КАК СуммаВР
	|		ИЗ
	|			РегистрНакопления.СебестоимостьТоваров КАК Движения
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|				РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|			ПО
	|				Движения.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПередачиПереработчику КАК ПередачиПереработчику
	|				ПО ПередачиПереработчику.Организация = Движения.Организация
	|				И ПередачиПереработчику.АналитикаУчетаНоменклатуры = Движения.АналитикаУчетаНоменклатуры
	|				И ПередачиПереработчику.ВидЗапасов = Движения.ВидЗапасов
	|		ГДЕ
	|			Движения.Регистратор = &Ссылка
	|			И Движения.Активность
	|			И Движения.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеПереработчику)
	|			И Движения.СтатьяРасходовСписания = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПогрешностьРасчетаСебестоимости)
	|			И Движения.ДокументДвижения В (НЕОПРЕДЕЛЕНО, &Ссылка)
	|
	|		СГРУППИРОВАТЬ ПО
	|			Движения.Организация,
	|			Аналитика.Номенклатура,
	|			Движения.СтатьяРасходовСписания,
	|			ЕСТЬNULL(ПередачиПереработчику.Контрагент, НЕОПРЕДЕЛЕНО)
	|		) КАК Строки
	|	ПО
	|		ИСТИНА
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И (Строки.Сумма <> 0 ИЛИ Строки.СуммаНУ <> 0 ИЛИ Строки.СуммаВР <> 0 ИЛИ Строки.СуммаПР <> 0)
	|";
#КонецОбласти
	
#Область ТекстПогрешностьРасчетаСебестоимостиНЗП

	ТекстПогрешностьРасчетаСебестоимостиНЗП = "
	|ВЫБРАТЬ //// Погрешность расчета себестоимости (Дт 91.02 :: Кт 20)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Строки.Сумма КАК Сумма,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеРасходы) КАК СчетДт,
	|
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПогрешностьРасчетаСебестоимости) КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СуммаНУ КАК СуммаНУДт,
	|	Строки.СуммаПР КАК СуммаПРДт,
	|	Строки.СуммаВР КАК СуммаВРДт,
	|
	|	Строки.ВидСчета КАК ВидСчетаКт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаКт,
	|	Строки.МестоУчетаКт КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	Строки.СтатьяРасходов КАК СубконтоКт1,
	|	Строки.ГруппаПродукции КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СуммаНУ КАК СуммаНУКт,
	|	Строки.СуммаПР КАК СуммаПРКт,
	|	Строки.СуммаВР КАК СуммаВРКт,
	|	""%3"" КАК Содержание
	|ИЗ
	|	Документ.РасчетСебестоимостиТоваров КАК Операция
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			Движения.Организация КАК Организация,
	|			ВЫБОР КОГДА НЕ Переработка.МестоУчета ЕСТЬ NULL ТОГДА 
	|				Переработка.МестоУчета
	|			КОГДА Аналитика.Склад ССЫЛКА Справочник.Склады ТОГДА 
	|				Аналитика.Склад.Подразделение
	|			ИНАЧЕ 
	|				Аналитика.Склад 
	|			КОНЕЦ КАК МестоУчетаКт,
	|			ВЫБОР КОГДА НЕ Переработка.Подразделение ЕСТЬ NULL ТОГДА 
	|				Переработка.МестоУчета
	|			КОГДА Аналитика.Склад ССЫЛКА Справочник.Склады ТОГДА 
	|				Аналитика.Склад.Подразделение
	|			ИНАЧЕ 
	|				Аналитика.Склад 
	|			КОНЕЦ КАК Подразделение,
	|			ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство) КАК ВидСчета,
	|			НЕОПРЕДЕЛЕНО КАК СтатьяРасходов,
	|			ВЫБОР КОГДА &ПартионныйУчетВерсии22 ТОГДА
	|				Движения.АналитикаФинансовогоУчета
	|           КОГДА Аналитика.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа) ТОГДА
	|				Аналитика.Номенклатура.ГруппаАналитическогоУчета
	|			ИНАЧЕ
	|				ЕСТЬNULL(Движения.ВидЗапасов.ГруппаПродукции, НЕОПРЕДЕЛЕНО)
	|			КОНЕЦ КАК ГруппаПродукции,
	|			Назначения.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|			СУММА(Движения.СтоимостьРегл
	|				+ Движения.ДопРасходыРегл
	|				+ Движения.ТрудозатратыРегл
	|				+ Движения.ПостатейныеРегл) КАК Сумма,
	|			СУММА(Движения.СтоимостьРегл
	|				+ Движения.ДопРасходыРегл
	|				+ Движения.ТрудозатратыРегл
	|				+ Движения.ПостатейныеРегл
	|				- Движения.ПостояннаяРазница
	|				- Движения.ВременнаяРазница) КАК СуммаНУ,
	|			СУММА(Движения.ПостояннаяРазница) КАК СуммаПР,
	|			СУММА(Движения.ВременнаяРазница) КАК СуммаВР
	|		ИЗ
	|			РегистрНакопления.СебестоимостьТоваров КАК Движения
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|				ПО Движения.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|			ЛЕВОЕ СОЕДИНЕНИЕ Переработка КАК Переработка
	|				ПО Переработка.Организация = Движения.Организация
	|				И Переработка.АналитикаУчетаНоменклатуры = Движения.АналитикаУчетаНоменклатуры
	|				И Переработка.ВидЗапасов = Движения.ВидЗапасов
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК Назначения
	|			ПО Аналитика.Назначение = Назначения.Ссылка
	|		ГДЕ
	|			Движения.Регистратор = &Ссылка
	|			И Движения.Активность
	|			И Движения.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты),
	|										ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство))
	|			И Движения.СтатьяРасходовСписания = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПогрешностьРасчетаСебестоимости)
	|			И Движения.ДокументДвижения В (НЕОПРЕДЕЛЕНО, &Ссылка)
	|
	|		СГРУППИРОВАТЬ ПО
	|			Движения.Организация,
	|			ВЫБОР КОГДА НЕ Переработка.МестоУчета ЕСТЬ NULL ТОГДА 
	|				Переработка.МестоУчета
	|			КОГДА Аналитика.Склад ССЫЛКА Справочник.Склады ТОГДА 
	|				Аналитика.Склад.Подразделение
	|			ИНАЧЕ 
	|				Аналитика.Склад 
	|			КОНЕЦ,
	|			Назначения.НаправлениеДеятельности,
	|			ВЫБОР КОГДА НЕ Переработка.Подразделение ЕСТЬ NULL ТОГДА 
	|				Переработка.МестоУчета
	|			КОГДА Аналитика.Склад ССЫЛКА Справочник.Склады ТОГДА 
	|				Аналитика.Склад.Подразделение
	|			ИНАЧЕ 
	|				Аналитика.Склад 
	|			КОНЕЦ,
	|			ВЫБОР КОГДА &ПартионныйУчетВерсии22 ТОГДА
	|				Движения.АналитикаФинансовогоУчета
	|           КОГДА Аналитика.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа) ТОГДА
	|				Аналитика.Номенклатура.ГруппаАналитическогоУчета
	|			ИНАЧЕ
	|				ЕСТЬNULL(Движения.ВидЗапасов.ГруппаПродукции, НЕОПРЕДЕЛЕНО)
	|			КОНЕЦ
	|
	|		ОБЪЕДИНИТЬ ВСЕ
	|
	|		ВЫБРАТЬ
	|			Движения.Организация КАК Организация,
	|			Движения.Подразделение КАК МестоУчетаКт,
	|			Движения.Подразделение КАК Подразделение,
	|			ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчета,
	|			Движения.СтатьяРасходов КАК СтатьяРасходов,
	|			НЕОПРЕДЕЛЕНО КАК ГруппаПродукции,
	|			Движения.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|			СУММА(Движения.СуммаРегл) КАК Сумма,
	|			СУММА(Движения.СуммаРегл - Движения.ПостояннаяРазница - Движения.ВременнаяРазница) КАК СуммаНУ,
	|			СУММА(Движения.ПостояннаяРазница) КАК СуммаПР,
	|			СУММА(Движения.ВременнаяРазница) КАК СуммаВР
	|		ИЗ
	|			РегистрНакопления.ПрочиеРасходы КАК Движения
	|		ГДЕ
	|			Движения.Регистратор = &Ссылка
	|			И Движения.Активность
	|			И Движения.СтатьяРасходов.ВариантРаспределенияРасходов =
	|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
	|			И Движения.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
	|			И Движения.СтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПогрешностьРасчетаСебестоимости)
	|
	|		СГРУППИРОВАТЬ ПО
	|			Движения.Организация,
	|			Движения.Подразделение,
	|			Движения.СтатьяРасходов,
	|			Движения.НаправлениеДеятельности
	|
	|		ОБЪЕДИНИТЬ ВСЕ
	|
	|		ВЫБРАТЬ
	|			Движения.Организация КАК Организация,
	|			Движения.Подразделение КАК МестоУчетаКт,
	|			Движения.Подразделение КАК Подразделение,
	|			ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство) КАК ВидСчета,
	|			НЕОПРЕДЕЛЕНО КАК СтатьяРасходов,
	|			Движения.ГруппаПродукции КАК ГруппаПродукции,
	|			ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|			СУММА(Движения.СтоимостьРегл) КАК Сумма,
	|			СУММА(Движения.СтоимостьРегл - Движения.ПостояннаяРазница - Движения.ВременнаяРазница) КАК СуммаНУ,
	|			СУММА(Движения.ПостояннаяРазница) КАК СуммаПР,
	|			СУММА(Движения.ВременнаяРазница) КАК СуммаВР
	|		ИЗ
	|			РегистрНакопления.ПрочиеРасходыНезавершенногоПроизводства КАК Движения
	|		ГДЕ
	|			Движения.Регистратор = &Ссылка
	|			И Движения.Активность
	|
	|		СГРУППИРОВАТЬ ПО
	|			Движения.Организация,
	|			Движения.Подразделение,
	|			Движения.ГруппаПродукции,
	|			Движения.СтатьяРасходов
	|
	|		) КАК Строки
	|	ПО
	|		ИСТИНА
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И (Строки.Сумма <> 0 ИЛИ Строки.СуммаНУ <> 0 ИЛИ Строки.СуммаВР <> 0 ИЛИ Строки.СуммаПР <> 0)
	|";
#КонецОбласти
	
	ТекстЗапроса =
		ТекстПогрешностьРасчетаСебестоимостиТовары
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстПогрешностьРасчетаСебестоимостиПереработка
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстПогрешностьРасчетаСебестоимостиНЗП;
	
	ТекстЗапроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ТекстЗапроса,
		НСтр("ru='Погрешность расчета себестоимости (товары)'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
		НСтр("ru='Погрешность расчета себестоимости (переработка)'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),
		НСтр("ru='Погрешность расчета себестоимости (НЗП)'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()));
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц, 
// необходимых для отражения в регламентированном учете.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Погрешность.Организация,
	|	Погрешность.АналитикаУчетаНоменклатуры,
	|	Погрешность.ВидЗапасов,
	|	МАКСИМУМ(Движения.Подразделение) КАК Подразделение,
	|	Аналитика.Склад КАК МестоУчета
	|ПОМЕСТИТЬ
	|	Переработка
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Погрешность
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|		ПО Аналитика.КлючАналитики = Погрешность.АналитикаУчетаНоменклатуры
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК Движения
	|		ПО Движения.Организация = Погрешность.Организация
	|		И Движения.АналитикаУчетаНоменклатуры = Погрешность.АналитикаУчетаНоменклатуры
	|		И Движения.РазделУчета = Погрешность.РазделУчета
	|		И Движения.ВидЗапасов = Погрешность.ВидЗапасов
	|ГДЕ
	|	Погрешность.Регистратор = &Ссылка
	|	И Погрешность.Активность
	|	И Погрешность.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты),
	|										ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство))
	|	И Погрешность.СтатьяРасходовСписания = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПогрешностьРасчетаСебестоимости)
	|	И Аналитика.Склад ССЫЛКА Справочник.Партнеры
	|	И Движения.Период МЕЖДУ НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ) И КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ)
	|	И Движения.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|СГРУППИРОВАТЬ ПО
	|	Погрешность.Организация,
	|	Погрешность.АналитикаУчетаНоменклатуры,
	|	Погрешность.ВидЗапасов,
	|	Аналитика.Склад
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Погрешность.Организация,
	|	Погрешность.АналитикаУчетаНоменклатуры,
	|	Погрешность.ВидЗапасов,
	|	Аналитика.Склад.Подразделение,
	|	Аналитика.Склад.Подразделение
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Погрешность
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|		ПО (Аналитика.КлючАналитики = Погрешность.АналитикаУчетаНоменклатуры)
	|ГДЕ
	|	Погрешность.Регистратор = &Ссылка
	|	И Погрешность.Активность
	|	И Погрешность.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты),
	|										ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство))
	|	И Погрешность.СтатьяРасходовСписания = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПогрешностьРасчетаСебестоимости)
	|	И ВЫРАЗИТЬ(Аналитика.Склад КАК Справочник.Склады).ЦеховаяКладовая
	|;
	|ВЫБРАТЬ
	|	Погрешность.Организация,
	|	Погрешность.АналитикаУчетаНоменклатуры,
	|	Погрешность.ВидЗапасов,
	|	Погрешность.РазделУчета,
	|	МАКСИМУМ(АналитикаКомиссионера.Контрагент) КАК Контрагент
	|ПОМЕСТИТЬ
	|	Комиссия
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Погрешность
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|		ПО Аналитика.КлючАналитики = Погрешность.АналитикаУчетаНоменклатуры
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК Движения
	|		ПО Движения.Организация = Погрешность.Организация
	|		И Движения.АналитикаУчетаНоменклатуры = Погрешность.АналитикаУчетаНоменклатуры
	|		И Движения.РазделУчета = Погрешность.РазделУчета
	|		И Движения.ВидЗапасов = Погрешность.ВидЗапасов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаКомиссионера
	|		ПО АналитикаКомиссионера.КлючАналитики = Движения.АналитикаУчетаНоменклатуры
	|ГДЕ
	|	Погрешность.Регистратор = &Ссылка
	|	И Погрешность.Активность
	|	И Погрешность.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию)
	|	И Погрешность.СтатьяРасходовСписания = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПогрешностьРасчетаСебестоимости)
	|	И Аналитика.Склад ССЫЛКА Справочник.Партнеры
	|	И Движения.Период МЕЖДУ НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ) И КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ)
	|	И Движения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|СГРУППИРОВАТЬ ПО
	|	Погрешность.Организация,
	|	Погрешность.АналитикаУчетаНоменклатуры,
	|	Погрешность.ВидЗапасов,
	|	Погрешность.РазделУчета
	|;
	|ВЫБРАТЬ
	|	Погрешность.Организация,
	|	Погрешность.АналитикаУчетаНоменклатуры,
	|	Погрешность.ВидЗапасов,
	|	МАКСИМУМ(
	|		ЕСТЬNULL(Передача.Контрагент,
	|			ЕСТЬNULL(Возврат.Контрагент,
	|				ЕСТЬNULL(Отчет.Контрагент, НЕОПРЕДЕЛЕНО)))
	|	) КАК Контрагент
	|ПОМЕСТИТЬ
	|	ПередачиПереработчику
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Погрешность
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|		ПО Аналитика.КлючАналитики = Погрешность.АналитикаУчетаНоменклатуры
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК Движения
	|		ПО Движения.Организация = Погрешность.Организация
	|		И Движения.АналитикаУчетаНоменклатуры = Погрешность.АналитикаУчетаНоменклатуры
	|		И Движения.РазделУчета = Погрешность.РазделУчета
	|		И Движения.ВидЗапасов = Погрешность.ВидЗапасов
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаСырьяПереработчику КАК Передача
	|		ПО Передача.Ссылка = Движения.Регистратор
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратСырьяОтПереработчика КАК Возврат
	|		ПО Возврат.Ссылка = Движения.Регистратор
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетПереработчика КАК Отчет
	|		ПО Отчет.Ссылка = Движения.Регистратор
	|ГДЕ
	|	Погрешность.Регистратор = &Ссылка
	|	И Погрешность.Активность
	|	И Погрешность.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеПереработчику)
	|	И Погрешность.СтатьяРасходовСписания = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПогрешностьРасчетаСебестоимости)
	|	И Движения.Период МЕЖДУ НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ) И КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ)
	|	И (НЕ Передача.Контрагент ЕСТЬ NULL
	|		ИЛИ НЕ Возврат.Контрагент ЕСТЬ NULL
	|		ИЛИ НЕ Отчет.Контрагент ЕСТЬ NULL)
	|СГРУППИРОВАТЬ ПО
	|	Погрешность.Организация,
	|	Погрешность.АналитикаУчетаНоменклатуры,
	|	Погрешность.ВидЗапасов
	|;
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти
//-- НЕ УТ
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область Отчеты

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуДвиженияДокумента(КомандыОтчетов);
	
КонецПроцедуры

#КонецОбласти

#Область СозданиеНаОсновании

// Заполняет список команд создания на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании) Экспорт
	
КонецПроцедуры

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСоздатьНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.РасчетСебестоимостиТоваров) Тогда
		КомандаСоздатьНаОсновании = КомандыСоздатьНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Идентификатор = Метаданные.Документы.РасчетСебестоимостиТоваров.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ВводНаОсновании.ПредставлениеОбъекта(Метаданные.Документы.РасчетСебестоимостиТоваров);
		КомандаСоздатьНаОсновании.ПроверкаПроведенияПередСозданиемНаОсновании = Истина;
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

#КонецОбласти

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
