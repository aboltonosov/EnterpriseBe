#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет список команд создания на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции "ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании".
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании) Экспорт



КонецПроцедуры

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции "МенюОтчеты.СоздатьКоллекциюКомандОтчетов".
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов) Экспорт

	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);

	Возврат; //В дальнейшем будет добавлен код команд

КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции "УправлениеПечатью.СоздатьКоллекциюКомандПечати".
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаКонтролируемыхСделокОрганизаций(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеСерверУТ.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = "ВЫБРАТЬ
	               |	ПрочиеКонтролируемыеСделки.Ссылка,
	               |	ПрочиеКонтролируемыеСделки.Дата КАК Период,
	               |	ПрочиеКонтролируемыеСделки.УведомлениеОКонтролируемойСделке КАК Уведомление,
	               |	ПрочиеКонтролируемыеСделки.Организация,
	               |	ПрочиеКонтролируемыеСделки.Контрагент,
	               |	ПрочиеКонтролируемыеСделки.ДоговорКонтрагента КАК Аналитика,
	               |	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперацииКонтролируемыхСделок.ПрочаяОперация) КАК ХозяйственнаяОперация,
	               |	ПрочиеКонтролируемыеСделки.ВалютаДокумента КАК Валюта
	               |ИЗ
	               |	Документ.РегистрацияПрочихКонтролируемыхСделок КАК ПрочиеКонтролируемыеСделки
	               |ГДЕ
	               |	ПрочиеКонтролируемыеСделки.Ссылка = &Ссылка";
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период", Реквизиты.Период);
	Запрос.УстановитьПараметр("Ссылка", Реквизиты.Ссылка);
	Запрос.УстановитьПараметр("Уведомление", Реквизиты.Уведомление);
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("Контрагент", Реквизиты.Контрагент);
	Запрос.УстановитьПараметр("Аналитика", Реквизиты.Аналитика);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация", Реквизиты.ХозяйственнаяОперация);
	Запрос.УстановитьПараметр("Валюта", Реквизиты.Валюта);
	
КонецПроцедуры

Функция ТекстЗапросаКонтролируемыхСделокОрганизаций(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "КонтролируемыеСделкиОрганизаций";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	ВЫБОР
	|		КОГДА НАЧАЛОПЕРИОДА(ПрочиеКонтролируемыеСделкиСделки.ДатаСовершенияСделки, ДЕНЬ) = НАЧАЛОПЕРИОДА(&Период, ДЕНЬ)
	|				ИЛИ ПрочиеКонтролируемыеСделкиСделки.ДатаСовершенияСделки = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА &Период
	|		ИНАЧЕ ПрочиеКонтролируемыеСделкиСделки.ДатаСовершенияСделки
	|	КОНЕЦ КАК Период,
	|	&Уведомление КАК Уведомление,
	|	&Организация КАК Организация,
	|	&Контрагент КАК Контрагент,
	|	&Аналитика КАК Аналитика,
	|	ПрочиеКонтролируемыеСделкиСделки.ПредметСделки,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ПрочиеКонтролируемыеСделкиСделки.Ссылка КАК РасчетныйДокумент,
	|	&Валюта КАК Валюта,
	|	ПрочиеКонтролируемыеСделкиСделки.ЕдиницаИзмерения,
	|	ПрочиеКонтролируемыеСделкиСделки.Количество,
	|	ПрочиеКонтролируемыеСделкиСделки.СуммаБезНДСВРублях,
	|	ПрочиеКонтролируемыеСделкиСделки.СуммаБезНДСВВалютеРасчетов,
	|	ПрочиеКонтролируемыеСделкиСделки.СуммаНДСВРублях,
	|	ПрочиеКонтролируемыеСделкиСделки.СуммаНДСВВалютеРасчетов,
	|	ПрочиеКонтролируемыеСделкиСделки.НаименованиеПредметаСделки,
	|	ПрочиеКонтролируемыеСделкиСделки.ТипПредметаСделки,
	|	ПрочиеКонтролируемыеСделкиСделки.СтранаПроисхожденияПредметаСделки,
	|	ПрочиеКонтролируемыеСделкиСделки.СтавкаНДС,
	|	ПрочиеКонтролируемыеСделкиСделки.ТипКонтролируемойСделки,
	|	ПрочиеКонтролируемыеСделкиСделки.Грузоотправитель,
	|	ПрочиеКонтролируемыеСделкиСделки.Грузополучатель,
	|	ПрочиеКонтролируемыеСделкиСделки.ОперацияОблагаетсяЕНВД
	|ИЗ
	|	Документ.РегистрацияПрочихКонтролируемыхСделок.Сделки КАК ПрочиеКонтролируемыеСделкиСделки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

#КонецОбласти

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСоздатьНаОсновании) Экспорт

	 
	Если ПравоДоступа("Добавление", Метаданные.Документы.РегистрацияПрочихКонтролируемыхСделок) Тогда
		КомандаСоздатьНаОсновании = КомандыСоздатьНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Идентификатор = Метаданные.Документы.РегистрацияПрочихКонтролируемыхСделок.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ВводНаОсновании.ПредставлениеОбъекта(Метаданные.Документы.РегистрацияПрочихКонтролируемыхСделок);
		КомандаСоздатьНаОсновании.ПроверкаПроведенияПередСозданиемНаОсновании = Истина;
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьУведомленияОКонтролируемыхСделках";
	

		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

#КонецОбласти

#КонецЕсли
