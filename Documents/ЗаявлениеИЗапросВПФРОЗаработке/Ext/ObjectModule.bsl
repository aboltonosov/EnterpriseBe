#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Подсистема "Управление доступом".

// Процедура ЗаполнитьНаборыЗначенийДоступа по свойствам объекта заполняет наборы значений доступа
// в таблице с полями:
//    НомерНабора     - Число                                     (необязательно, если набор один),
//    ВидДоступа      - ПланВидовХарактеристикСсылка.ВидыДоступа, (обязательно),
//    ЗначениеДоступа - Неопределено, СправочникСсылка или др.    (обязательно),
//    Чтение          - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Добавление      - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Изменение       - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Удаление        - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//
//  Вызывается из процедуры УправлениеДоступомСлужебный.ЗаписатьНаборыЗначенийДоступа(),
// если объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьНаборыЗначенийДоступа" и
// из таких же процедур объектов, у которых наборы значений доступа зависят от наборов этого
// объекта (в этом случае объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьЗависимыеНаборыЗначенийДоступа").
//
// Параметры:
//  Таблица      - ТабличнаяЧасть,
//                 РегистрСведенийНаборЗаписей.НаборыЗначенийДоступа,
//                 ТаблицаЗначений, возвращаемая УправлениеДоступом.ТаблицаНаборыЗначенийДоступа().
//
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	
	ЗарплатаКадры.ЗаполнитьНаборыПоОрганизацииИФизическимЛицам(ЭтотОбъект, Таблица, "Организация", "ФизическоеЛицо");
	
КонецПроцедуры

// Подсистема "Управление доступом".

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
			
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ЗарплатаКадры.ПроверитьКорректностьДаты(Ссылка, ПериодРаботыС, "Объект.ПериодРаботыС", Отказ, НСтр("ru='Начало периода'"), , , Ложь);
	ЗарплатаКадры.ПроверитьКорректностьДаты(Ссылка, ДатаРождения, "Объект.ДатаРождения", Отказ, НСтр("ru='Начало периода'"), '19000101', , Ложь);
	ЗарплатаКадры.ПроверитьКорректностьДаты(Ссылка, ДатаВыдачиДокумента, "Объект.ДатаВыдачиДокумента", Отказ, НСтр("ru='Дата выдачи'"), , , Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
      
// Функция возвращает признак завершенности работы с объектом.
Функция ОбъектЗафиксирован() Экспорт
	
	Возврат Проведен;
	
КонецФункции 

// Процедура обновляет вторичные данные в документе с учетом фиксации. 
Функция ОбновитьВторичныеДанныеДокумента(ДанныеОрганизации = Истина, ДанныеСотрудника = Истина, ДанныеОЗаработке = Истина, ОбновлятьБезусловно = Истина) Экспорт
	
	Модифицирован = Ложь;
	
	Если ОбъектЗафиксирован() И НЕ ОбновлятьБезусловно Тогда
		Возврат Модифицирован;
	КонецЕсли;
	
	Если ДанныеОрганизации И ОбновитьДанныеСтрахователя() Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ДанныеСотрудника И ОбновитьДанныеСотрудника() Тогда
		Модифицирован = Истина;
	КонецЕсли;
			
	Возврат Модифицирован
	
КонецФункции

Функция ОбновитьДанныеСтрахователя()
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СтрокаРеквизиты = "НаименованиеТерриториальногоОрганаПФР,Руководитель,ДолжностьРуководителя,ТелефонОрганизации,АдресОрганизации";
	
	РеквизитыДокумента = Новый Структура(СтрокаРеквизиты);
	
	СтрокаРеквизитыОрганизации = "НаименованиеТерриториальногоОрганаПФР";									
	РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, СтрокаРеквизитыОрганизации);
	
	ЗаполнитьЗначенияСвойств(РеквизитыДокумента, РеквизитыОрганизации, СтрокаРеквизитыОрганизации);
	
	ЗаполняемыеЗначения = Новый Структура("Организация,Руководитель,ДолжностьРуководителя", Организация);
	ЗарплатаКадры.ПолучитьЗначенияПоУмолчанию(ЗаполняемыеЗначения);
	ЗаполняемыеЗначения.Свойство("Руководитель", РеквизитыДокумента.Руководитель);
	ЗаполняемыеЗначения.Свойство("ДолжностьРуководителя", РеквизитыДокумента.ДолжностьРуководителя);
	
	АдресаОрганизации = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Организация),	
																					Перечисления.ТипыКонтактнойИнформации.Адрес, 
																					Справочники.ВидыКонтактнойИнформации.ЮрАдресОрганизации);
	
	Если АдресаОрганизации.Количество() > 0 Тогда
		РеквизитыДокумента.АдресОрганизации = АдресаОрганизации[0].ЗначенияПолей;	
	Иначе
		РеквизитыДокумента.АдресОрганизации = "";	
	КонецЕсли;
	
	ТелефоныОрганизации = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Организация),	
																					Перечисления.ТипыКонтактнойИнформации.Телефон, 
																					Справочники.ВидыКонтактнойИнформации.ТелефонОрганизации);
	
	Если ТелефоныОрганизации.Количество() > 0 Тогда
		РеквизитыДокумента.ТелефонОрганизации = ТелефоныОрганизации[0].ЗначенияПолей;	
	Иначе
		РеквизитыДокумента.ТелефонОрганизации = "";	
	КонецЕсли;
	
	Запрос = ПрямыеВыплатыПособийСоциальногоСтрахования.ЗапросПоСтрокеПолей(СтрокаРеквизиты);

	ЗаполнитьЗначенияСвойств(Запрос.Параметры, РеквизитыДокумента); 
	
	Запрос.Выполнить();
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьВторичныеДанные(Запрос.МенеджерВременныхТаблиц, ЭтотОбъект);
	
КонецФункции

Функция ОбновитьДанныеСотрудника()
	
	КадровыеДанные = Новый Массив;
	
	КадровыеДанные.Добавить("ФизическоеЛицо");
	КадровыеДанные.Добавить("Фамилия");
	КадровыеДанные.Добавить("Имя");
	КадровыеДанные.Добавить("Отчество");
	КадровыеДанные.Добавить("СтраховойНомерПФР");
	КадровыеДанные.Добавить("ДатаРождения");
	КадровыеДанные.Добавить("ДокументВид");
	КадровыеДанные.Добавить("ДокументСерия");
	КадровыеДанные.Добавить("ДокументНомер");
	КадровыеДанные.Добавить("ДокументКемВыдан");
	КадровыеДанные.Добавить("ДокументДатаВыдачи");
	КадровыеДанные.Добавить("АдресПоПрописке");
	КадровыеДанные.Добавить("АдресПоПропискеПредставление");
	КадровыеДанные.Добавить("ТелефонМобильный");
	КадровыеДанные.Добавить("ТелефонМобильныйПредставление");
		
	КадровыеДанныеФизическогоЛица = КадровыйУчет.КадровыеДанныеСотрудников(Истина, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудник), КадровыеДанные, Дата);
	
	Если КадровыеДанныеФизическогоЛица.Количество() = 0 Тогда
		Возврат Ложь;	
	КонецЕсли;
	
	СтрокаРеквизиты = "ФизическоеЛицо,Фамилия,Имя,Отчество,СтраховойНомерПФР,ДатаРождения,ВидДокумента,СерияДокумента,НомерДокумента,КемВыданДокумент,ДатаВыдачиДокумента,Адрес,Телефон";
	
	РеквизитыДокумента = Новый Структура(СтрокаРеквизиты);
	
	Данные =  КадровыеДанныеФизическогоЛица[0];
	
	ЗаполнитьЗначенияСвойств(РеквизитыДокумента, Данные, "ФизическоеЛицо,Фамилия,Имя,Отчество,СтраховойНомерПФР,ДатаРождения");
	
	РеквизитыДокумента["ВидДокумента"] 			= Данные["ДокументВид"];
	РеквизитыДокумента["СерияДокумента"] 		= Данные["ДокументСерия"];
	РеквизитыДокумента["НомерДокумента"] 		= Данные["ДокументНомер"];
	РеквизитыДокумента["КемВыданДокумент"]		= Данные["ДокументКемВыдан"];
	РеквизитыДокумента["ДатаВыдачиДокумента"] 	= Данные["ДокументДатаВыдачи"];
	РеквизитыДокумента["Адрес"] 				= Данные["АдресПоПрописке"];
	РеквизитыДокумента["Телефон"] 				= Данные["ТелефонМобильный"];
	
	Запрос = ПрямыеВыплатыПособийСоциальногоСтрахования.ЗапросПоСтрокеПолей(СтрокаРеквизиты);
	
	ЗаполнитьЗначенияСвойств(Запрос.Параметры, РеквизитыДокумента); 
	
	Запрос.Выполнить();
	
	ФизическоеЛицо = Данные["ФизическоеЛицо"];
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьВторичныеДанные(Запрос.МенеджерВременныхТаблиц, ЭтотОбъект);
	
КонецФункции

#КонецОбласти

#КонецЕсли

