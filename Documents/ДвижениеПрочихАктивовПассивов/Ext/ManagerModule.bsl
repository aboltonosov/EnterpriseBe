#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет список команд создания на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании) Экспорт



КонецПроцедуры

// Заполняет список команд создания на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании
//
// Возвращаемое значение:
//	 КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСоздатьНаОсновании) Экспорт

	 
	Если ПравоДоступа("Добавление", Метаданные.Документы.ДвижениеПрочихАктивовПассивов) Тогда
		КомандаСоздатьНаОсновании = КомандыСоздатьНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Идентификатор = Метаданные.Документы.ДвижениеПрочихАктивовПассивов.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ВводНаОсновании.ПредставлениеОбъекта(Метаданные.Документы.ДвижениеПрочихАктивовПассивов);
		КомандаСоздатьНаОсновании.ПроверкаПроведенияПередСозданиемНаОсновании = Истина;
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьУчетПрочихАктивовПассивов,ИспользоватьУчетПрочихДоходовРасходов";
	

		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов) Экспорт

	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);

	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуДвиженияДокумента(КомандыОтчетов);

КонецПроцедуры

// Заполняет таблицу прочих расходов по остаткам расходов на строительство ОС
//
// Параметры:
//  ДанныеОтбора - Структура - поля, по которым будут отобраны остатки для заполнения документа
//                     Дата        - Дата - дата среза, на которую будут получены остатки
//                     Валюта      - СправочникСсылка.Валюта - валюта документа
//                     Ссылка      - ДокументСсылка.РаспределениеРасходовНаПрочиеАктивы, Неопределено - документ, который необходимо обновить
//                     Организация - СправочникСсылка.Организации, Неопределено - Организация, по которой будет осуществляться отбор остатков
//                     ТипСтатьи   - ПеречислениеСсылка.ТипыСтатейАктивовПассивов - тип регистра, из которого будут получены остатки
//  Расходы - ДанныеФормыКоллекция - таблица расходов, в которую будут помещены данные
//
Процедура ЗаполнитьПоОстаткамВРегистре(ДанныеОтбора, Расходы) Экспорт
	
	// Данные по остаткам товаров заказа
	ВыборкаРасходы = ПолучитьРезультатЗапросаПоОстаткамВРегистре(ДанныеОтбора).Выбрать();
	
	Пока ВыборкаРасходы.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Расходы.Добавить(), ВыборкаРасходы);
	КонецЦикла;
	
КонецПроцедуры

// Заполняет массивы реквизитов, зависимых от хозяйственной операции документа.
//
// Параметры:
//	Источник - ПеречислениеСсылка.ТипыСтатейАктивовПассивов - Выбранный тип статей
//	Получатель - ПеречислениеСсылка.ТипыСтатейАктивовПассивов - Выбранный тип статей
//	МассивВсехРеквизитов - Массив - Массив всех имен реквизитов, зависимых от хозяйственной операции
//	МассивРеквизитовОперации - Массив - Массив имен реквизитов, используемых в выбранной хозяйственной операции
//
Процедура ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(Источник, Получатель, МассивВсехРеквизитов, МассивРеквизитовОперации) Экспорт
	
	МассивВсехРеквизитов = Новый Массив;
	МассивВсехРеквизитов.Добавить("ДанныеОтражения.АналитикаАктивовПассивовПолучатель");
	МассивВсехРеквизитов.Добавить("ДанныеОтражения.АналитикаДоходовПолучатель");
	МассивВсехРеквизитов.Добавить("ДанныеОтражения.АналитикаРасходовПолучатель");
	
	МассивВсехРеквизитов.Добавить("ДанныеОтражения.АналитикаАктивовПассивовИсточник");
	МассивВсехРеквизитов.Добавить("ДанныеОтражения.АналитикаДоходовИсточник");
	МассивВсехРеквизитов.Добавить("ДанныеОтражения.АналитикаРасходовИсточник");
	
	МассивВсехРеквизитов.Добавить("ДанныеОтражения.СуммаБезНДС");
	
	МассивРеквизитовОперации = Новый Массив;
	Если Получатель = Перечисления.ТипыСтатейАктивовПассивов.СтатьяДоходов Тогда
		МассивРеквизитовОперации.Добавить("ДанныеОтражения.АналитикаДоходовПолучатель");
	ИначеЕсли Получатель = Перечисления.ТипыСтатейАктивовПассивов.СтатьяРасходов Тогда
		МассивРеквизитовОперации.Добавить("ДанныеОтражения.АналитикаРасходовПолучатель");
		МассивРеквизитовОперации.Добавить("ДанныеОтражения.СуммаБезНДС");
	ИначеЕсли Получатель = Перечисления.ТипыСтатейАктивовПассивов.СтатьяПассивов Тогда
		МассивРеквизитовОперации.Добавить("ДанныеОтражения.АналитикаАктивовПассивовПолучатель");
	Иначе
		МассивРеквизитовОперации.Добавить("ДанныеОтражения.АналитикаАктивовПассивовПолучатель");
	КонецЕсли;
	
	Если Источник = Перечисления.ТипыСтатейАктивовПассивов.СтатьяДоходов Тогда
		МассивРеквизитовОперации.Добавить("ДанныеОтражения.АналитикаДоходовИсточник");
	ИначеЕсли Источник = Перечисления.ТипыСтатейАктивовПассивов.СтатьяРасходов Тогда
		МассивРеквизитовОперации.Добавить("ДанныеОтражения.АналитикаРасходовИсточник");
		МассивРеквизитовОперации.Добавить("ДанныеОтражения.СуммаБезНДС");
	ИначеЕсли Источник = Перечисления.ТипыСтатейАктивовПассивов.СтатьяПассивов Тогда
		МассивРеквизитовОперации.Добавить("ДанныеОтражения.АналитикаАктивовПассивовИсточник");
	Иначе
		МассивРеквизитовОперации.Добавить("ДанныеОтражения.АналитикаАктивовПассивовИсточник");
	КонецЕсли;
	
КонецПроцедуры

// Формирует соответствие названий движений возможным сочетаниям статей
//
// Возвращаемое значение:
//  Соответствие - текстовые представления вариантов движений документа
//
Функция ОписаниеДвижений() Экспорт
	
	ТипыСтатей = Перечисления.ТипыСтатейАктивовПассивов;
	Описания = Новый Соответствие;
	// Увеличение активов
	Описания.Вставить(Индетификатор(ТипыСтатей.СтатьяАктивов, ТипыСтатей.СтатьяАктивов),
					НСтр("ru = 'Выполняется увеличение активов и уменьшение других активов'"));
	Описания.Вставить(Индетификатор(ТипыСтатей.СтатьяАктивов, ТипыСтатей.СтатьяПассивов),
					НСтр("ru = 'Выполняется увеличение активов и увеличение пассивов'"));
	Описания.Вставить(Индетификатор(ТипыСтатей.СтатьяАктивов, ТипыСтатей.СтатьяРасходов),
					НСтр("ru = 'Выполняется увеличение активов и уменьшение расходов'"));
	Описания.Вставить(Индетификатор(ТипыСтатей.СтатьяАктивов, ТипыСтатей.СтатьяДоходов),
					НСтр("ru = 'Выполняется увеличение активов и возникновение доходов'"));
	// Увеличение пассивов
	Описания.Вставить(Индетификатор(ТипыСтатей.СтатьяПассивов, ТипыСтатей.СтатьяПассивов),
					НСтр("ru = 'Выполняется увеличение пассивов и уменьшение других пассивов'"));
	Описания.Вставить(Индетификатор(ТипыСтатей.СтатьяПассивов, ТипыСтатей.СтатьяРасходов),
					НСтр("ru = 'Выполняется увеличение пассивов и возникновение расходов'"));
	Описания.Вставить(Индетификатор(ТипыСтатей.СтатьяПассивов, ТипыСтатей.СтатьяДоходов),
					НСтр("ru = 'Выполняется увеличение пассивов и уменьшение доходов'"));
	Возврат Описания;
	
КонецФункции

// Устанавливает параметры динамического списка документов ДвижениеПрочихАктивовПассивов
//
// Параметры:
//	Список - ДинамическийСписок - реквизит формы содержащий динамический список
//
Процедура УстановитьПараметрыДинамическогоСписка(Список) Экспорт
	
	Описание = ОписаниеДвижений();
	ТипыСтатей = Перечисления.ТипыСтатейАктивовПассивов;
	// Активы
	Ид = Индетификатор(ТипыСтатей.СтатьяАктивов, ТипыСтатей.СтатьяАктивов);
	Список.Параметры.УстановитьЗначениеПараметра("АктивыАктивы", Описание[Ид]);
	Ид = Индетификатор(ТипыСтатей.СтатьяАктивов, ТипыСтатей.СтатьяПассивов);
	Список.Параметры.УстановитьЗначениеПараметра("АктивыПассивы", Описание[Ид]);
	Ид = Индетификатор(ТипыСтатей.СтатьяАктивов, ТипыСтатей.СтатьяРасходов);
	Список.Параметры.УстановитьЗначениеПараметра("АктивыРасходы", Описание[Ид]);
	Ид = Индетификатор(ТипыСтатей.СтатьяАктивов, ТипыСтатей.СтатьяДоходов);
	Список.Параметры.УстановитьЗначениеПараметра("АктивыДоходы", Описание[Ид]);
	// Пассивы
	Ид = Индетификатор(ТипыСтатей.СтатьяПассивов, ТипыСтатей.СтатьяПассивов);
	Список.Параметры.УстановитьЗначениеПараметра("ПассивыПассивы", Описание[Ид]);
	Ид = Индетификатор(ТипыСтатей.СтатьяПассивов, ТипыСтатей.СтатьяРасходов);
	Список.Параметры.УстановитьЗначениеПараметра("ПассивыРасходы", Описание[Ид]);
	Ид = Индетификатор(ТипыСтатей.СтатьяПассивов, ТипыСтатей.СтатьяДоходов);
	Список.Параметры.УстановитьЗначениеПараметра("ПассивыДоходы", Описание[Ид]);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаПрочиеДоходы(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры);
	
	////////////////////////////////////////////////////////////////////////////
	// Поместим результаты запроса в дополнительные свойства
	
	ПроведениеСерверУТ.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР КОГДА Документ.Источник В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяАктивов),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяРасходов))
	|	ТОГДА ИСТИНА
	|	ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ					КАК ИсточникАктив,
	|	ВЫБОР КОГДА Документ.Получатель В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяАктивов),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяРасходов))
	|	ТОГДА ИСТИНА
	|	ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ					КАК ПолучательАктив,
	|	Документ.Дата			КАК Дата,
	|	Документ.Валюта			КАК Валюта,
	|	Документ.Источник		КАК Источник,
	|	Документ.Получатель		КАК Получатель,
	|	Документ.Увеличение		КАК Увеличение,
	|	Документ.Организация	КАК Организация
	|ИЗ
	|	Документ.ДвижениеПрочихАктивовПассивов КАК Документ
	|ГДЕ
	|	Документ.Ссылка = &Ссылка";

	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Коэффициенты = РаботаСКурсамивалютУТ.ПолучитьКоэффициентыПересчетаВалюты(
		Реквизиты.Валюта,
		Неопределено,
		Реквизиты.Дата);
	
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуУпр",	Коэффициенты.КоэффициентПересчетаВВалютуУпр);
	Запрос.УстановитьПараметр("Ссылка",					ДокументСсылка);
	Запрос.УстановитьПараметр("Организация",			Реквизиты.Организация);
	Запрос.УстановитьПараметр("ДатаДокумента",			Реквизиты.Дата);
	Запрос.УстановитьПараметр("ВалютаДокумента",		Реквизиты.Валюта);
	Запрос.УстановитьПараметр("ИсточникАктив",			Реквизиты.ИсточникАктив);
	Запрос.УстановитьПараметр("ПолучательАктив",		Реквизиты.ПолучательАктив);
	Запрос.УстановитьПараметр("Источник",				Реквизиты.Источник);
	Запрос.УстановитьПараметр("Получатель",				Реквизиты.Получатель);
	Запрос.УстановитьПараметр("Увеличение",				Реквизиты.Увеличение);
	Запрос.УстановитьПараметр("ВидВиженияСписания",		?(Реквизиты.Увеличение,ВидДвиженияНакопления.Приход,ВидДвиженияНакопления.Расход));
	Если (Реквизиты.Увеличение И Реквизиты.ПолучательАктив = Реквизиты.ИсточникАктив)
		 ИЛИ (НЕ Реквизиты.Увеличение И НЕ Реквизиты.ПолучательАктив = Реквизиты.ИсточникАктив) Тогда
		ВидВиженияПоступления = ВидДвиженияНакопления.Расход;
	Иначе
		ВидВиженияПоступления = ВидДвиженияНакопления.Приход;
	КонецЕсли;
	Запрос.УстановитьПараметр("ВидВиженияПоступления",	ВидВиженияПоступления);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация",	Перечисления.ХозяйственныеОперации.ОтражениеПрочихАктивовПассивов);
	
КонецПроцедуры

Функция ТекстЗапросаВтДанные(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтДанные"; 
	
	ТекстЗапроса = "
	// Получатель
	|ВЫБРАТЬ
	|	ВЫБОР КОГДА РАЗНОСТЬДАТ(Строки.ДатаОтражения, &ДатаДокумента, ДЕНЬ) = 0
	|				ИЛИ Строки.ДатаОтражения = ДАТАВРЕМЯ(1, 1, 1) ТОГДА
	|		&ДатаДокумента
	|	ИНАЧЕ
	|		Строки.ДатаОтражения
	|	КОНЕЦ											КАК Период,
	|	&Организация									КАК Организация,
	|	&ХозяйственнаяОперация							КАК ХозяйственнаяОперация,
	|	Строки.ПодразделениеПолучатель					КАК Подразделение,
	|	Строки.НаправлениеДеятельностиПолучатель		КАК НаправлениеДеятельности,
	|	&ВидВиженияСписания								КАК ВидДвижения,
	|	Строки.НомерСтроки								КАК НомерСтроки,
	|
	|	// Получатель
	|	Строки.СтатьяПолучатель							КАК Статья,
	|	ВЫБОР КОГДА &Получатель = ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяРасходов) ТОГДА
	|		Строки.АналитикаРасходовПолучатель
	|	КОГДА &Получатель = ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяДоходов) ТОГДА
	|		Строки.АналитикаДоходовПолучатель
	|	ИНАЧЕ
	|		Строки.АналитикаАктивовПассивовПолучатель
	|	КОНЕЦ											КАК Аналитика,
	|
	|	// Сумма
	|	Строки.Сумма * &КоэффициентПересчетаВВалютуУпр	КАК СуммаУпр,
	|	Строки.СуммаБезНДС * &КоэффициентПересчетаВВалютуУпр	КАК СуммаБезНДСУпр,
	|	Строки.Сумма									КАК Сумма,
	|	Строки.СуммаБезНДС								КАК СуммаБезНДС,
	|
	|	// Тип регистра
	|	ВЫБОР КОГДА &Получатель = ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяРасходов) ТОГДА
	|		""Расходы""
	|	КОГДА &Получатель = ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяДоходов) ТОГДА
	|		""Доходы""
	|	ИНАЧЕ
	|		""АктивыПассивы""
	|	КОНЕЦ											КАК ТипРегистра,
	|	&Получатель										КАК ТипСтатьи,
	|	ЛОЖЬ											КАК Списание
	|ПОМЕСТИТЬ ВтДанные
	|
	|ИЗ
	|	Документ.ДвижениеПрочихАктивовПассивов.ДанныеОтражения КАК Строки
	|ГДЕ 
	|	Строки.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Источник
	|ВЫБРАТЬ
	|	ВЫБОР КОГДА РАЗНОСТЬДАТ(Строки.ДатаОтражения, &ДатаДокумента, ДЕНЬ) = 0
	|				ИЛИ Строки.ДатаОтражения = ДАТАВРЕМЯ(1, 1, 1) ТОГДА
	|		&ДатаДокумента
	|	ИНАЧЕ
	|		Строки.ДатаОтражения
	|	КОНЕЦ											КАК Период,
	|	&Организация									КАК Организация,
	|	&ХозяйственнаяОперация							КАК ХозяйственнаяОперация,
	|	Строки.ПодразделениеИсточник					КАК Подразделение,
	|	Строки.НаправлениеДеятельностиИсточник			КАК НаправлениеДеятельности,
	|	&ВидВиженияПоступления							КАК ВидДвижения,
	|	Строки.НомерСтроки								КАК НомерСтроки,
	|
	|	// Источник
	|	Строки.СтатьяИсточник							КАК Статья,
	|	ВЫБОР КОГДА &Источник = ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяРасходов) ТОГДА
	|		Строки.АналитикаРасходовИсточник
	|	КОГДА &Источник = ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяДоходов) ТОГДА
	|		Строки.АналитикаДоходовИсточник
	|	ИНАЧЕ
	|		Строки.АналитикаАктивовПассивовИсточник
	|	КОНЕЦ											КАК Аналитика,
	|
	|	// Сумма
	|	Строки.Сумма * &КоэффициентПересчетаВВалютуУпр	КАК СуммаУпр,
	|	Строки.СуммаБезНДС * &КоэффициентПересчетаВВалютуУпр	КАК СуммаБезНДСУпр,
	|	Строки.Сумма									КАК Сумма,
	|	Строки.СуммаБезНДС								КАК СуммаБезНДС,
	|
	|	// Тип регистра
	|	ВЫБОР КОГДА &Источник = ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяРасходов) ТОГДА
	|		""Расходы""
	|	КОГДА &Источник = ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяДоходов) ТОГДА
	|		""Доходы""
	|	ИНАЧЕ
	|		""АктивыПассивы""
	|	КОНЕЦ											КАК ТипРегистра,
	|	&Источник										КАК ТипСтатьи,
	|	ИСТИНА											КАК Списание
	|
	|ИЗ
	|	Документ.ДвижениеПрочихАктивовПассивов.ДанныеОтражения КАК Строки
	|ГДЕ 
	|	Строки.Ссылка = &Ссылка
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры)
		
	ИмяРегистра = "ПрочиеАктивыПассивы";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтДанные", ТекстыЗапроса) Тогда
		ТекстЗапросаВтДанные(Запрос, ТекстыЗапроса);
	КонецЕсли; 

	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Строки.НомерСтроки				КАК НомерСтроки,
	|	ВЫБОР КОГДА Строки.ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяПассивов) ТОГДА
	|		// У статей пассивов знак изменяется на противоположный, т.е. ""увеличение пассивов"" - это ""Расход"" в регистре, и наоборот
	|		ВЫБОР КОГДА Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КОНЕЦ
	|	ИНАЧЕ
	|		Строки.ВидДвижения
	|	КОНЕЦ							КАК ВидДвижения,
	|	Строки.Период					КАК Период,
	|	Строки.Организация				КАК Организация,
	|	Строки.Подразделение			КАК Подразделение,
	|	Строки.НаправлениеДеятельности	КАК НаправлениеДеятельности,
	|	Строки.Статья					КАК Статья,
	|	Строки.Аналитика				КАК Аналитика,
	|	Строки.СуммаУпр					КАК Сумма
	|ИЗ
	|	ВтДанные КАК Строки
	|ГДЕ 
	|	Строки.ТипРегистра = ""АктивыПассивы""
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВидДвижения, НомерСтроки
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеДоходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеДоходы";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтДанные", ТекстыЗапроса) Тогда
		ТекстЗапросаВтДанные(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Строки.НомерСтроки				КАК НомерСтроки,
	|	Строки.Период					КАК Период,
	|	Строки.ВидДвижения				КАК ВидДвижения,
	|	Строки.ХозяйственнаяОперация	КАК ХозяйственнаяОперация,
	|	Строки.Организация				КАК Организация,
	|	Строки.Подразделение			КАК Подразделение,
	|	Строки.НаправлениеДеятельности	КАК НаправлениеДеятельности,
	|	Строки.Статья					КАК СтатьяДоходов,
	|	Строки.Аналитика				КАК АналитикаДоходов,
	|	Строки.СуммаУпр					КАК Сумма
	|ИЗ
	|	ВтДанные КАК Строки
	|ГДЕ 
	|	Строки.ТипРегистра = ""Доходы""
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВидДвижения, НомерСтроки
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтДанные", ТекстыЗапроса) Тогда
		ТекстЗапросаВтДанные(Запрос, ТекстыЗапроса);
	КонецЕсли; 

	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Строки.НомерСтроки				КАК НомерСтроки,
	|	Строки.Период					КАК Период,
	|	Строки.ВидДвижения				КАК ВидДвижения,
	|	Строки.Организация				КАК Организация,
	|	Строки.Подразделение			КАК Подразделение,
	|	Строки.НаправлениеДеятельности	КАК НаправлениеДеятельности,
	|	Строки.ХозяйственнаяОперация	КАК ХозяйственнаяОперация,
	|	Строки.Статья					КАК СтатьяРасходов,
	|	Строки.Аналитика				КАК АналитикаРасходов,
	|	Строки.СуммаУпр					КАК Сумма,
	|	ВЫБОР КОГДА Строки.Статья.ВариантРаспределенияРасходов = 
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
	|		ТОГДА Строки.СуммаБезНДСУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаБезНДС
	|ИЗ
	|	ВтДанные КАК Строки
	|ГДЕ 
	|	Строки.ТипРегистра = ""Расходы""
	|УПОРЯДОЧИТЬ ПО
	|	ВидДвижения, НомерСтроки
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаТаблицаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПартииПрочихРасходов";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтДанные", ТекстыЗапроса) Тогда
		ТекстЗапросаВтДанные(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Строки.Период                  КАК Период,
	|	Строки.ВидДвижения             КАК ВидДвижения,
	|	&Ссылка                        КАК ДокументПоступленияРасходов,
	|	Строки.Организация             КАК Организация,
	|	Строки.Подразделение           КАК Подразделение,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Строки.Статья                  КАК СтатьяРасходов,
	|	Строки.Аналитика               КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО                   КАК АналитикаУчетаПартий,
	|	
	|	Строки.СуммаУпр                КАК Стоимость,
	|	ВЫБОР КОГДА Строки.Статья.ВариантРаспределенияРасходов =
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|		ТОГДА Строки.СуммаБезНДСУпр
	|		ИНАЧЕ 0
	|	КОНЕЦ                          КАК СтоимостьБезНДС,
	|	0                              КАК СтоимостьРегл,
	|	0                              КАК НДСРегл,
	|	0                              КАК ПостояннаяРазница,
	|	0                              КАК ВременнаяРазница
	|	
	|ИЗ
	|	ВтДанные КАК Строки
	|ГДЕ 
	|	Строки.ТипРегистра = ""Расходы""
	|	И Строки.Статья.ВариантРаспределенияРасходов В (ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров))
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДвиженияДоходыРасходыПрочиеАктивыПассивы";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтДанные", ТекстыЗапроса) Тогда
		ТекстЗапросаВтДанные(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Источник.Период						КАК Период,
	|	Источник.Организация				КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ДвижениеАктивовПассивовЗаСчетАктивовПассивов) КАК ХозяйственнаяОперация,
	|
	|	Источник.Подразделение				КАК Подразделение,
	|	Источник.НаправлениеДеятельности	КАК НаправлениеДеятельности,
	|	Источник.Статья						КАК Статья,
	|	НЕОПРЕДЕЛЕНО						КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО						КАК АналитикаРасходов,
	|	Источник.Аналитика					КАК АналитикаАктивовПассивов,
	|
	|	Получатель.Подразделение			КАК КорПодразделение,
	|	Получатель.НаправлениеДеятельности	КАК КорНаправлениеДеятельности,
	|	Получатель.Статья					КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО						КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО						КАК КорАналитикаРасходов,
	|	Получатель.Аналитика				КАК КорАналитикаАктивовПассивов,
	|	
	|	Источник.СуммаУпр					КАК Сумма,
	|	0									КАК СуммаРегл,
	|	
	|	&ВалютаДокумента					КАК Валюта,
	|	Источник.Сумма						КАК СуммаВВалюте
	|	
	|ИЗ
	|	втДанные КАК Источник
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанные КАК Получатель
	|	ПО Источник.НомерСтроки = Получатель.НомерСтроки
	|		И Источник.Списание
	|		И НЕ Получатель.Списание
	|		И Источник.ТипСтатьи В (ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяАктивов),
	|							ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяПассивов))
	|		И Получатель.ТипСтатьи В (ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяАктивов),
	|								ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяПассивов))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Источник.Период						КАК Период,
	|	Источник.Организация				КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеДоходыАктивыПассивы) КАК ХозяйственнаяОперация,
	|
	|	Источник.Подразделение				КАК Подразделение,
	|	Источник.НаправлениеДеятельности	КАК НаправлениеДеятельности,
	|	Источник.Статья						КАК Статья,
	|	Источник.Аналитика					КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО						КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО						КАК АналитикаАктивовПассивов,
	|
	|	Получатель.Подразделение			КАК КорПодразделение,
	|	Получатель.НаправлениеДеятельности	КАК КорНаправлениеДеятельности,
	|	Получатель.Статья					КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО						КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО						КАК КорАналитикаРасходов,
	|	Получатель.Аналитика				КАК КорАналитикаАктивовПассивов,
	|	
	|	Источник.СуммаУпр					КАК Сумма,
	|	0									КАК СуммаРегл,
	|	
	|	&ВалютаДокумента					КАК Валюта,
	|	Источник.Сумма						КАК СуммаВВалюте
	|	
	|ИЗ
	|	втДанные КАК Источник
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанные КАК Получатель
	|	ПО Источник.НомерСтроки = Получатель.НомерСтроки
	|		И Источник.Списание
	|		И НЕ Получатель.Списание
	|		И Источник.ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяДоходов)
	|		И Получатель.ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяАктивов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Источник.Период						КАК Период,
	|	Источник.Организация				КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПрочихРасходов) КАК ХозяйственнаяОперация,
	|
	|	Источник.Подразделение				КАК Подразделение,
	|	Источник.НаправлениеДеятельности	КАК НаправлениеДеятельности,
	|	Источник.Статья						КАК Статья,
	|	НЕОПРЕДЕЛЕНО						КАК АналитикаДоходов,
	|	Источник.Аналитика					КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО						КАК АналитикаАктивовПассивов,
	|
	|	Получатель.Подразделение			КАК КорПодразделение,
	|	Получатель.НаправлениеДеятельности	КАК КорНаправлениеДеятельности,
	|	Получатель.Статья					КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО						КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО						КАК КорАналитикаРасходов,
	|	Получатель.Аналитика				КАК КорАналитикаАктивовПассивов,
	|	
	|	Источник.СуммаУпр					КАК Сумма,
	|	0									КАК СуммаРегл,
	|	
	|	&ВалютаДокумента					КАК Валюта,
	|	Источник.Сумма						КАК СуммаВВалюте
	|	
	|ИЗ
	|	втДанные КАК Источник
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанные КАК Получатель
	|	ПО Источник.НомерСтроки = Получатель.НомерСтроки
	|		И Источник.Списание
	|		И НЕ Получатель.Списание
	|		И Источник.ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяРасходов)
	|		И Получатель.ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяАктивов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Источник.Период						КАК Период,
	|	Источник.Организация				КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПрочихДоходов) КАК ХозяйственнаяОперация,
	|
	|	Источник.Подразделение				КАК Подразделение,
	|	Источник.НаправлениеДеятельности	КАК НаправлениеДеятельности,
	|	Источник.Статья						КАК Статья,
	|	Источник.Аналитика					КАК АналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО						КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО						КАК АналитикаАктивовПассивов,
	|
	|	Получатель.Подразделение			КАК КорПодразделение,
	|	Получатель.НаправлениеДеятельности	КАК КорНаправлениеДеятельности,
	|	Получатель.Статья					КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО						КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО						КАК КорАналитикаРасходов,
	|	Получатель.Аналитика				КАК КорАналитикаАктивовПассивов,
	|	
	|	Источник.СуммаУпр					КАК Сумма,
	|	0									КАК СуммаРегл,
	|	
	|	&ВалютаДокумента					КАК Валюта,
	|	Источник.Сумма						КАК СуммаВВалюте
	|	
	|ИЗ
	|	втДанные КАК Источник
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанные КАК Получатель
	|	ПО Источник.НомерСтроки = Получатель.НомерСтроки
	|		И Источник.Списание
	|		И НЕ Получатель.Списание
	|		И Источник.ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяДоходов)
	|		И Получатель.ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяПассивов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Источник.Период						КАК Период,
	|	Источник.Организация				КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеРасходыАктивыПассивы) КАК ХозяйственнаяОперация,
	|
	|	Источник.Подразделение				КАК Подразделение,
	|	Источник.НаправлениеДеятельности	КАК НаправлениеДеятельности,
	|	Источник.Статья						КАК Статья,
	|	НЕОПРЕДЕЛЕНО						КАК АналитикаДоходов,
	|	Источник.Аналитика					КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО						КАК АналитикаАктивовПассивов,
	|
	|	Получатель.Подразделение				КАК КорПодразделение,
	|	Получатель.НаправлениеДеятельности	КАК КорНаправлениеДеятельности,
	|	Получатель.Статья					КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО						КАК КорАналитикаДоходов,
	|	НЕОПРЕДЕЛЕНО						КАК КорАналитикаРасходов,
	|	Получатель.Аналитика				КАК КорАналитикаАктивовПассивов,
	|	
	|	Источник.СуммаУпр					КАК Сумма,
	|	0									КАК СуммаРегл,
	|	
	|	&ВалютаДокумента					КАК Валюта,
	|	Источник.Сумма						КАК СуммаВВалюте
	|	
	|ИЗ
	|	втДанные КАК Источник
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанные КАК Получатель
	|	ПО Источник.НомерСтроки = Получатель.НомерСтроки
	|		И Источник.Списание
	|		И НЕ Получатель.Списание
	|		И Источник.ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяРасходов)
	|		И Получатель.ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяПассивов)
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область Прочее

Функция Индетификатор(Статья1, Статья2)
	
	Возврат Строка(Статья1) + "\" + Строка(Статья2);
	
КонецФункции

Функция ПолучитьРезультатЗапросаПоОстаткамВРегистре(ДанныеОтбора)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	// Пересчет суммы
	Запрос.УстановитьПараметр("Валюта",			ДанныеОтбора.Валюта);
	Запрос.УстановитьПараметр("ВалютаУпр",		Константы.ВалютаУправленческогоУчета.Получить());
	// Параметры отбора
	Запрос.УстановитьПараметр("ТипСтатьи",		ДанныеОтбора.ТипСтатьи);
	Запрос.УстановитьПараметр("Ссылка",			ДанныеОтбора.Ссылка);
	Запрос.УстановитьПараметр("Организация",	ДанныеОтбора.Организация);
	Запрос.УстановитьПараметр("Граница",		Новый Граница(КонецДня(ДанныеОтбора.Дата), ВидГраницы.Включая));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	""Актив"" КАК АктивПассив,
	|	Т.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВтСтатьи
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиАктивовПассивов КАК Т
	|ГДЕ
	|	Т.АктивПассив = ЗНАЧЕНИЕ(Перечисление.ВидыСтатейУправленческогоБаланса.Актив)
	|	И &ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяАктивов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Пассив"" КАК АктивПассив,
	|	Т.Ссылка
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиАктивовПассивов КАК Т
	|ГДЕ
	|	Т.АктивПассив = ЗНАЧЕНИЕ(Перечисление.ВидыСтатейУправленческогоБаланса.Пассив)
	|	И &ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяПассивов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Организация					КАК Организация,
	|	ВложенныйЗапрос.Подразделение				КАК Подразделение,
	|	ВложенныйЗапрос.Статья						КАК Статья,
	|	ВложенныйЗапрос.АналитикаРасходов			КАК АналитикаРасходов,
	|	ВложенныйЗапрос.АналитикаДоходов			КАК АналитикаДоходов,
	|	ВложенныйЗапрос.АналитикаАктивовПассивов	КАК АналитикаАктивовПассивов,
	|	СУММА(ВложенныйЗапрос.Сумма)				КАК Сумма
	|ПОМЕСТИТЬ ВтРасходы
	|ИЗ
	|	(
	//	Прочие расходы
	|	ВЫБРАТЬ
	|		Регистр.Организация				КАК Организация,
	|		Регистр.Подразделение			КАК Подразделение,
	|		Регистр.СтатьяРасходов			КАК Статья,
	|		Регистр.АналитикаРасходов		КАК АналитикаРасходов,
	|		НЕОПРЕДЕЛЕНО					КАК АналитикаДоходов,
	|		НЕОПРЕДЕЛЕНО					КАК АналитикаАктивовПассивов,
	|		Регистр.СуммаОстаток			КАК Сумма
	|	ИЗ
	|		РегистрНакопления.ПрочиеРасходы.Остатки(
	|			&Граница,
	|			Организация = &Организация
	|			И &ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяРасходов)) КАК Регистр
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Регистр.Организация				КАК Организация,
	|		Регистр.Подразделение			КАК Подразделение,
	|		Регистр.СтатьяРасходов			КАК Статья,
	|		Регистр.АналитикаРасходов		КАК АналитикаРасходов,
	|		НЕОПРЕДЕЛЕНО					КАК АналитикаДоходов,
	|		НЕОПРЕДЕЛЕНО					КАК АналитикаАктивовПассивов,
	|		ВЫБОР КОГДА Регистр.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
	|			Регистр.Сумма
	|		ИНАЧЕ
	|			-Регистр.Сумма
	|		КОНЕЦ							КАК Сумма
	|	ИЗ
	|		РегистрНакопления.ПрочиеРасходы КАК Регистр
	|	ГДЕ
	|		Регистр.Регистратор = &Ссылка
	|		И &ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяРасходов)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	//	Прочие доходы
	|	ВЫБРАТЬ
	|		Регистр.Организация				КАК Организация,
	|		Регистр.Подразделение			КАК Подразделение,
	|		Регистр.СтатьяДоходов			КАК Статья,
	|		НЕОПРЕДЕЛЕНО					КАК АналитикаРасходов,
	|		Регистр.АналитикаДоходов		КАК АналитикаДоходов,
	|		НЕОПРЕДЕЛЕНО					КАК АналитикаАктивовПассивов,
	|		Регистр.СуммаОстаток			КАК Сумма
	|	ИЗ
	|		РегистрНакопления.ПрочиеДоходы.Остатки(
	|			&Граница,
	|			Организация = &Организация
	|			И &ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяДоходов)) КАК Регистр
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Регистр.Организация				КАК Организация,
	|		Регистр.Подразделение			КАК Подразделение,
	|		Регистр.СтатьяДоходов			КАК Статья,
	|		НЕОПРЕДЕЛЕНО					КАК АналитикаРасходов,
	|		Регистр.АналитикаДоходов		КАК АналитикаДоходов,
	|		НЕОПРЕДЕЛЕНО					КАК АналитикаАктивовПассивов,
	|		ВЫБОР КОГДА Регистр.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
	|			Регистр.Сумма
	|		ИНАЧЕ
	|			-Регистр.Сумма
	|		КОНЕЦ							КАК Сумма
	|	ИЗ
	|		РегистрНакопления.ПрочиеДоходы КАК Регистр
	|	ГДЕ
	|		Регистр.Регистратор = &Ссылка
	|		И &ТипСтатьи = ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяДоходов)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	//	Прочие активы и пассивы
	|	ВЫБРАТЬ
	|		Регистр.Организация				КАК Организация,
	|		НЕОПРЕДЕЛЕНО					КАК Подразделение,
	|		Регистр.Статья					КАК Статья,
	|		НЕОПРЕДЕЛЕНО					КАК АналитикаРасходов,
	|		НЕОПРЕДЕЛЕНО					КАК АналитикаДоходов,
	|		Регистр.Аналитика				КАК АналитикаАктивовПассивов,
	|		Регистр.СуммаОстаток			КАК Сумма
	|	ИЗ
	|		РегистрНакопления.ПрочиеАктивыПассивы.Остатки(
	|			&Граница,
	|			Организация = &Организация
	|			И Статья В (ВЫБРАТЬ Т.Ссылка ИЗ ВтСтатьи КАК Т)) КАК Регистр
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Регистр.Организация				КАК Организация,
	|		НЕОПРЕДЕЛЕНО					КАК Подразделение,
	|		Регистр.Статья					КАК Статья,
	|		НЕОПРЕДЕЛЕНО					КАК АналитикаРасходов,
	|		НЕОПРЕДЕЛЕНО					КАК АналитикаДоходов,
	|		Регистр.Аналитика				КАК АналитикаАктивовПассивов,
	|		ВЫБОР КОГДА Регистр.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И Т.АктивПассив = ""Актив"" ТОГДА
	|			Регистр.Сумма // Актив
	|		КОГДА Регистр.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И Т.АктивПассив = ""Актив"" ТОГДА
	|			-Регистр.Сумма // Актив
	|		КОГДА Регистр.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			Регистр.Сумма // Пассив
	|		ИНАЧЕ
	|			-Регистр.Сумма // Пассив
	|		КОНЕЦ							КАК Сумма
	|	ИЗ
	|		РегистрНакопления.ПрочиеАктивыПассивы КАК Регистр
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			ВтСтатьи КАК Т
	|		ПО
	|			Регистр.Статья = Т.Ссылка
	|	ГДЕ
	|		Регистр.Регистратор = &Ссылка
	|
	|	) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.Подразделение,
	|	ВложенныйЗапрос.Статья,
	|	ВложенныйЗапрос.АналитикаРасходов,
	|	ВложенныйЗапрос.АналитикаДоходов,
	|	ВложенныйЗапрос.АналитикаАктивовПассивов
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВложенныйЗапрос.Сумма) <> 0
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтРасходы.Организация				КАК Организация,
	|	ВтРасходы.Подразделение				КАК Подразделение,
	|	ВтРасходы.Статья					КАК Статья,
	|	ВтРасходы.АналитикаРасходов			КАК АналитикаРасходов,
	|	ВтРасходы.АналитикаДоходов			КАК АналитикаДоходов,
	|	ВтРасходы.АналитикаАктивовПассивов	КАК АналитикаАктивовПассивов,
	|
	|	ЕСТЬNULL(СтатьиРасходов.ВариантРаспределенияРасходов, ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.ПустаяСсылка)) КАК ВариантРаспределения,
	|
	|	ВЫБОР КОГДА &ВалютаУпр = &Валюта
	|			ИЛИ КурсыУпр.Валюта ЕСТЬ NULL
	|			ИЛИ Курсы.Валюта ЕСТЬ NULL
	|			ИЛИ Курсы.Курс * КурсыУпр.Кратность = 0 ТОГДА
	|		ВтРасходы.Сумма
	|	ИНАЧЕ
	|		ВЫРАЗИТЬ((ВтРасходы.Сумма * КурсыУпр.Курс * Курсы.Кратность) / (Курсы.Курс * КурсыУпр.Кратность) КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК Сумма
	|ИЗ
	|	ВтРасходы КАК ВтРасходы
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|		ПО СтатьиРасходов.Ссылка = ВтРасходы.Статья
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Граница, НЕ &ВалютаУпр = &Валюта И Валюта = &ВалютаУпр) КАК КурсыУпр
	|		ПО ИСТИНА
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Граница, НЕ &ВалютаУпр = &Валюта И Валюта = &Валюта) КАК Курсы
	|		ПО ИСТИНА
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация,
	|	Подразделение,
	|	Статья,
	|	АналитикаРасходов,
	|	АналитикаДоходов,
	|	АналитикаАктивовПассивов
	|";
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат Запрос.Выполнить();
	
КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Операция.Ссылка
	|ИЗ
	|	Документ.ДвижениеПрочихАктивовПассивов КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДвижениеПрочихАктивовПассивов.ДанныеОтражения КАК ТабличнаяЧасть
	|		ПО Операция.Ссылка = ТабличнаяЧасть.Ссылка
	|ГДЕ
	|	Операция.Источник В (ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяАктивов), ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяПассивов))
	|	И ТабличнаяЧасть.СтатьяИсточник = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЦелевоеФинансирование)
	|	И ТабличнаяЧасть.НаправлениеДеятельностиИсточник = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	И ТабличнаяЧасть.АналитикаАктивовПассивовИсточник ССЫЛКА Справочник.НаправленияДеятельности
	|	И ТабличнаяЧасть.АналитикаАктивовПассивовИсточник <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	И Операция.Проведен
	|	И НЕ Операция.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Операция.Ссылка
	|ИЗ
	|	Документ.ДвижениеПрочихАктивовПассивов КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДвижениеПрочихАктивовПассивов.ДанныеОтражения КАК ТабличнаяЧасть
	|		ПО Операция.Ссылка = ТабличнаяЧасть.Ссылка
	|ГДЕ
	|	Операция.Получатель В (ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяАктивов), ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяПассивов))
	|	И ТабличнаяЧасть.СтатьяПолучатель = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЦелевоеФинансирование)
	|	И ТабличнаяЧасть.НаправлениеДеятельностиПолучатель = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	И ТабличнаяЧасть.АналитикаАктивовПассивовПолучатель ССЫЛКА Справочник.НаправленияДеятельности
	|	И ТабличнаяЧасть.АналитикаАктивовПассивовПолучатель <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	И Операция.Проведен
	|	И НЕ Операция.ПометкаУдаления";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "Документ.ДвижениеПрочихАктивовПассивов";
	МетаданныеОбъекта = Метаданные.Документы.ДвижениеПрочихАктивовПассивов;
	
	МВТ = Новый МенеджерВременныхТаблиц;
	
	ПараметрыВыборкиДляОбработки = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ПараметрыВыборкиДляОбработки.ИмяВременнойТаблицы = "втСсылокДляОбработки";
	Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуСсылокДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта, МВТ, ПараметрыВыборкиДляОбработки);
	Если Не Результат.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Если Не Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТ;
	#Область ТекстЗапроса
	Запрос.Текст=
	"ВЫБРАТЬ
	|	втСсылокДляОбработки.Ссылка
	|ИЗ
	|	втСсылокДляОбработки КАК втСсылокДляОбработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	ТабличнаяЧасть.НомерСтроки - 1 КАК ИндексСтроки,
	|	ВЫБОР
	|		КОГДА Операция.Источник В (ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяАктивов), ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяПассивов))
	|				И ТабличнаяЧасть.СтатьяИсточник = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЦелевоеФинансирование)
	|				И ТабличнаяЧасть.НаправлениеДеятельностиИсточник = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|				И ТабличнаяЧасть.АналитикаАктивовПассивовИсточник ССЫЛКА Справочник.НаправленияДеятельности
	|				И ТабличнаяЧасть.АналитикаАктивовПассивовИсточник <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			ТОГДА ТабличнаяЧасть.АналитикаАктивовПассивовИсточник
	|		ИНАЧЕ ТабличнаяЧасть.НаправлениеДеятельностиИсточник
	|	КОНЕЦ КАК НаправлениеДеятельностиИсточник,
	|	ВЫБОР
	|		КОГДА Операция.Источник В (ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяАктивов), ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяПассивов))
	|				И ТабличнаяЧасть.СтатьяИсточник = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЦелевоеФинансирование)
	|				И ТабличнаяЧасть.НаправлениеДеятельностиИсточник = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|				И ТабличнаяЧасть.АналитикаАктивовПассивовИсточник ССЫЛКА Справочник.НаправленияДеятельности
	|				И ТабличнаяЧасть.АналитикаАктивовПассивовИсточник <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтатьиБезАналитики.ПустаяСсылка)
	|		ИНАЧЕ ТабличнаяЧасть.АналитикаАктивовПассивовИсточник
	|	КОНЕЦ КАК АналитикаАктивовПассивовИсточник,
	|	ВЫБОР
	|		КОГДА Операция.Получатель В (ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяАктивов), ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяПассивов))
	|				И ТабличнаяЧасть.СтатьяПолучатель = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЦелевоеФинансирование)
	|				И ТабличнаяЧасть.НаправлениеДеятельностиПолучатель = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|				И ТабличнаяЧасть.АналитикаАктивовПассивовПолучатель ССЫЛКА Справочник.НаправленияДеятельности
	|				И ТабличнаяЧасть.АналитикаАктивовПассивовПолучатель <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			ТОГДА ТабличнаяЧасть.АналитикаАктивовПассивовПолучатель
	|		ИНАЧЕ ТабличнаяЧасть.НаправлениеДеятельностиПолучатель
	|	КОНЕЦ КАК НаправлениеДеятельностиПолучатель,
	|	ВЫБОР
	|		КОГДА Операция.Получатель В (ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяАктивов), ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяПассивов))
	|				И ТабличнаяЧасть.СтатьяПолучатель = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЦелевоеФинансирование)
	|				И ТабличнаяЧасть.НаправлениеДеятельностиПолучатель = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|				И ТабличнаяЧасть.АналитикаАктивовПассивовПолучатель ССЫЛКА Справочник.НаправленияДеятельности
	|				И ТабличнаяЧасть.АналитикаАктивовПассивовПолучатель <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СтатьиБезАналитики.ПустаяСсылка)
	|		ИНАЧЕ ТабличнаяЧасть.АналитикаАктивовПассивовПолучатель
	|	КОНЕЦ КАК АналитикаАктивовПассивовПолучатель
	|ИЗ
	|	Документ.ДвижениеПрочихАктивовПассивов КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДвижениеПрочихАктивовПассивов.ДанныеОтражения КАК ТабличнаяЧасть
	|		ПО Операция.Ссылка = ТабличнаяЧасть.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСсылокДляОбработки КАК втСсылокДляОбработки
	|		ПО (ТабличнаяЧасть.Ссылка = втСсылокДляОбработки.Ссылка)
	|ГДЕ
	|	(Операция.Источник В (ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяАктивов), ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяПассивов))
	|				И ТабличнаяЧасть.СтатьяИсточник = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЦелевоеФинансирование)
	|				И ТабличнаяЧасть.НаправлениеДеятельностиИсточник = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|				И ТабличнаяЧасть.АналитикаАктивовПассивовИсточник ССЫЛКА Справочник.НаправленияДеятельности
	|				И ТабличнаяЧасть.АналитикаАктивовПассивовИсточник <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			ИЛИ Операция.Получатель В (ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяАктивов), ЗНАЧЕНИЕ(Перечисление.ТипыСтатейАктивовПассивов.СтатьяПассивов))
	|				И ТабличнаяЧасть.СтатьяПолучатель = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЦелевоеФинансирование)
	|				И ТабличнаяЧасть.НаправлениеДеятельностиПолучатель = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|				И ТабличнаяЧасть.АналитикаАктивовПассивовПолучатель ССЫЛКА Справочник.НаправленияДеятельности
	|				И ТабличнаяЧасть.АналитикаАктивовПассивовПолучатель <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))";
	#КонецОбласти
	
	Пакет = Запрос.ВыполнитьПакет();
	
	Выборка = Пакет[Пакет.Количество()-2].Выбрать();
	ВыборкаСтрок = Пакет[Пакет.Количество()-1].Выбрать();
	СтруктураПоискаСтрок = Новый Структура("Ссылка", Неопределено);
	
	ОбработаноОбъектов = 0;
	ПроблемныхОбъектов = 0;
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			Блокировка.Заблокировать();
		Исключение
			ОтменитьТранзакцию();
			ТекстСообщения = НСтр("ru = 'Не удалось заблокировать движение прочих активов и пассивов: %1 по причине: %2'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.Ссылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				МетаданныеОбъекта,
				Выборка.Ссылка,
				ТекстСообщения);
			Продолжить;
		КонецПопытки;
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ВыборкаСтрок.Сбросить();
		ЗаполнитьЗначенияСвойств(СтруктураПоискаСтрок, Выборка);
		Пока ВыборкаСтрок.НайтиСледующий(СтруктураПоискаСтрок) Цикл
			ЗаполнитьЗначенияСвойств(ДокументОбъект.ДанныеОтражения[ВыборкаСтрок.ИндексСтроки], ВыборкаСтрок);;
		КонецЦикла;
		
		ОбработаноОбъектов = ОбработаноОбъектов + 1;
		
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект, Ложь, Ложь, РежимЗаписиДокумента.Запись);
			ЗафиксироватьТранзакцию();
		Исключение
			
			ОтменитьТранзакцию();
			
			ОбработаноОбъектов = ОбработаноОбъектов - 1;
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ТекстСообщения = НСтр("ru = 'Не удалось заполнить направление деятельности в документе ""%1"" по причине: %2'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.Ссылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				МетаданныеОбъекта,
				Выборка.Ссылка,
				ТекстСообщения);
			
			Продолжить;
			
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбработаноОбъектов = 0 И ПроблемныхОбъектов > 0 Тогда
		
		ТекстСообщения = НСтр("ru = 'Не выполнено заполнение направление деятельности в нескольких документах (пропущены): %1'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
		
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли