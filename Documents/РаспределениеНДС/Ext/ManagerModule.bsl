#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет список команд создания на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании) Экспорт


	ВводНаОснованииПереопределяемый.ДобавитьКомандуСоздатьНаОснованииБизнесПроцессЗадание(КомандыСоздатьНаОсновании);


КонецПроцедуры

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСоздатьНаОсновании) Экспорт

	 
	Если ПравоДоступа("Добавление", Метаданные.Документы.РаспределениеНДС) Тогда
		КомандаСоздатьНаОсновании = КомандыСоздатьНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Идентификатор = Метаданные.Документы.РаспределениеНДС.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ВводНаОсновании.ПредставлениеОбъекта(Метаданные.Документы.РаспределениеНДС);
		КомандаСоздатьНаОсновании.ПроверкаПроведенияПередСозданиемНаОсновании = Истина;
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьНДС0,РаспределятьНДС,УправлениеПредприятием,КомплекснаяАвтоматизация";
	

		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов) Экспорт

	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);

	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуДвиженияДокумента(КомандыОтчетов);

КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

КонецПроцедуры

//++ НЕ УТ

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	ТекстЗапроса - Строка - Текст запроса отражения в регл. учете.
//
Функция ТекстОтраженияВРеглУчете() Экспорт

#Область ПередачаНДСВСтоимостьОС                 // (Дт <08.04.2> :: Кт <19.1, 19.3>)
	ПередачаНДСВСтоимостьОС = "
	|ВЫБРАТЬ //// Передача НДС в стоимость ОС (Дт <08.04.2> :: Кт <19.1, 19.3>)
	|	ПартииПрочихРасходов.Регистратор КАК Ссылка,
	|	ПартииПрочихРасходов.Период КАК Период,
	|	ПартииПрочихРасходов.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ПартииПрочихРасходов.НДСРегл КАК Сумма,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	ПартииПрочихРасходов.СтатьяРасходов КАК АналитикаУчетаДт,
	|	ПартииПрочихРасходов.Подразделение КАК МестоУчетаДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	ПартииПрочихРасходов.Подразделение КАК ПодразделениеДт,
	|	ПартииПрочихРасходов.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	ПартииПрочихРасходов.СтатьяРасходов КАК СубконтоДт1,
	|	ПартииПрочихРасходов.АналитикаРасходов КАК СубконтоДт2,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный) КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НДСпоПриобретеннымЦенностям) КАК ВидСчетаКт,
	|	ПартииПрочихРасходов.НалогообложениеФинПартии КАК АналитикаУчетаКт, 
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(Аналитика.Номенклатура.ТипНоменклатуры, НЕОПРЕДЕЛЕНО) В (
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|			ТОГДА ЕСТЬNULL(ГруппыФинансовогоУчетаНоменклатуры.ВидЦенностиНДС, ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары))
	|		ИНАЧЕ Статьи.ВидЦенностиНДС
	|	КОНЕЦ КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	ПартииПрочихРасходов.АналитикаУчетаПартий.Контрагент КАК СубконтоКт1,
	|	ПартииПрочихРасходов.ДокументПоступленияРасходов КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Передача НДС в стоимость ОС"" КАК Содержание
	|ИЗ
	|	ПартииПрочихРасходов КАК ПартииПрочихРасходов
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
	|	ПО
	|		ПартииПрочихРасходов.СтатьяРасходов = Статьи.Ссылка
	|		И Статьи.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|	ПО 
	|		Аналитика.Ссылка = ПартииПрочихРасходов.АналитикаУчетаНоменклатуры
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ГруппыФинансовогоУчетаНоменклатуры КАК ГруппыФинансовогоУчетаНоменклатуры
	|	ПО
	|		Аналитика.Номенклатура.ГруппаФинансовогоУчета = ГруппыФинансовогоУчетаНоменклатуры.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК АналитикаПартий
	|		ПО АналитикаПартий.Ссылка = ПартииПрочихРасходов.АналитикаУчетаПартий
	|	
	|ГДЕ
	|	ПартииПрочихРасходов.НалогообложениеПартии = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|	И ПартииПрочихРасходов.НалогообложениеФинПартии <> ПартииПрочихРасходов.НалогообложениеПартии
	|	И ТИПЗНАЧЕНИЯ(ПартииПрочихРасходов.АналитикаРасходов) В (
	|		ТИП(Справочник.ОбъектыЭксплуатации), 
	|		ТИП(Справочник.НематериальныеАктивы))
	|	И ПартииПрочихРасходов.НДСРегл <> 0
	|";
#КонецОбласти
	
#Область ИсключениеНДСИзСтоимостиОС              // (Дт <19.1, 19.3> :: Кт <08.04.2>)
	ИсключениеНДСИзСтоимостиОС = "
	|ВЫБРАТЬ //// Исключение НДС из стоимости ОС (Дт <19.1, 19.3> :: Кт <08.04.2>)
	|	ПартииПрочихРасходов.Регистратор КАК Ссылка,
	|	ПартииПрочихРасходов.Период КАК Период,
	|	ПартииПрочихРасходов.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ПартииПрочихРасходов.НДСРегл КАК Сумма,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НДСпоПриобретеннымЦенностям) КАК ВидСчетаДт,
	|	ПартииПрочихРасходов.НалогообложениеПартии КАК АналитикаУчетаДт,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(Аналитика.Номенклатура.ТипНоменклатуры, НЕОПРЕДЕЛЕНО) В (
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|			ТОГДА ЕСТЬNULL(ГруппыФинансовогоУчетаНоменклатуры.ВидЦенностиНДС, ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)) 
	|		ИНАЧЕ Статьи.ВидЦенностиНДС
	|	КОНЕЦ КАК МестоУчетаДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	ПартииПрочихРасходов.АналитикаУчетаПартий.Контрагент КАК СубконтоДт1,
	|	ПартииПрочихРасходов.ДокументПоступленияРасходов КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|	ПартииПрочихРасходов.СтатьяРасходов КАК АналитикаУчетаКт,
	|	ПартииПрочихРасходов.Подразделение КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	ПартииПрочихРасходов.Подразделение КАК ПодразделениеКт,
	|	ПартииПрочихРасходов.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	ПартииПрочихРасходов.АналитикаРасходов КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Исключение НДС из стоимости ОС"" КАК Содержание
	|ИЗ
	|	ПартииПрочихРасходов КАК ПартииПрочихРасходов
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
	|	ПО
	|		ПартииПрочихРасходов.СтатьяРасходов = Статьи.Ссылка
	|		И Статьи.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|	ПО 
	|		Аналитика.Ссылка = ПартииПрочихРасходов.АналитикаУчетаНоменклатуры
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ГруппыФинансовогоУчетаНоменклатуры КАК ГруппыФинансовогоУчетаНоменклатуры
	|	ПО
	|		Аналитика.Номенклатура.ГруппаФинансовогоУчета = ГруппыФинансовогоУчетаНоменклатуры.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК АналитикаПартий
	|		ПО АналитикаПартий.Ссылка = ПартииПрочихРасходов.АналитикаУчетаПартий
	|ГДЕ
	|	ПартииПрочихРасходов.НалогообложениеФинПартии = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|	И ПартииПрочихРасходов.НалогообложениеФинПартии <> ПартииПрочихРасходов.НалогообложениеПартии
	|	И ТИПЗНАЧЕНИЯ(ПартииПрочихРасходов.АналитикаРасходов) В (
	|			ТИП(Справочник.ОбъектыЭксплуатации), 
	|			ТИП(Справочник.НематериальныеАктивы))
	|	И ПартииПрочихРасходов.НДСРегл <> 0
	|";
#КонецОбласти
	
#Область СтоимостьОСНаСчетУчета                  // (Дт <СчетУчетаОС/НМА> :: Кт <08.04.2>)
	СтоимостьОСНаСчетУчета = "
	|ВЫБРАТЬ ////Стоимость ОС на счет учета (Дт <СчетУчетаОС/НМА> :: Кт <08.04.2>)
	|	ПартииПрочихРасходов.Регистратор КАК Ссылка,
	|	ПартииПрочихРасходов.Период КАК Период,
	|	ПартииПрочихРасходов.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ПартииПрочихРасходов.НДСРегл КАК Сумма,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	ЕСТЬNULL(МестонахождениеОС.Местонахождение, МестонахождениеНМА.Местонахождение) КАК ПодразделениеДт,
	|	ПартииПрочихРасходов.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЕСТЬNULL(СчетаОтражения.СчетУчета, СчетаОтраженияНМА.СчетУчета) КАК СчетДт,
	|	
	|	ПартииПрочихРасходов.АналитикаРасходов КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|	ПартииПрочихРасходов.СтатьяРасходов КАК АналитикаУчетаКт,
	|	ПартииПрочихРасходов.Подразделение КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	ПартииПрочихРасходов.Подразделение КАК ПодразделениеКт,
	|	ПартииПрочихРасходов.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	ПартииПрочихРасходов.АналитикаРасходов КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Стоимость ОС на счет учета"" КАК Содержание
	|ИЗ
	|	ПартииПрочихРасходов КАК ПартииПрочихРасходов
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО
	|		ПартииПрочихРасходов.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СчетаБухгалтерскогоУчетаОС.СрезПоследних(
	|				КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ),
	|				(Организация, ОсновноеСредство) В
	|						(ВЫБРАТЬ
	|							Т.Организация,
	|							Т.Актив
	|						ИЗ
	|							ПартииПрочихРасходов_ВНА КАК Т)
	|		) КАК СчетаОтражения
	|	ПО
	|		ПартииПрочихРасходов.АналитикаРасходов = СчетаОтражения.ОсновноеСредство
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СчетаБухгалтерскогоУчетаНМА.СрезПоследних(
	|				КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ),
	|				(Организация, НематериальныйАктив) В
	|						(ВЫБРАТЬ
	|							Т.Организация,
	|							Т.Актив
	|						ИЗ
	|							ПартииПрочихРасходов_ВНА КАК Т)
	|		) КАК СчетаОтраженияНМА
	|	ПО
	|		ПартииПрочихРасходов.АналитикаРасходов = СчетаОтраженияНМА.НематериальныйАктив
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.МестонахождениеОСБухгалтерскийУчет.СрезПоследних(
	|				КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ),
	|				(Организация, ОсновноеСредство) В
	|						(ВЫБРАТЬ
	|							Т.Организация,
	|							Т.Актив
	|						ИЗ
	|							ПартииПрочихРасходов_ВНА КАК Т)
	|		) КАК МестонахождениеОС
	|	ПО
	|		ПартииПрочихРасходов.АналитикаРасходов = МестонахождениеОС.ОсновноеСредство
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		(ВЫБРАТЬ
	|			ПУ.НематериальныйАктив,
	|			ПУ.Подразделение КАК Местонахождение
	|		ИЗ
	|			Документ.ПринятиеКУчетуНМА КАК ПУ
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияНМАБухгалтерскийУчет.СрезПоследних(
	|				КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ),
	|				Организация В 
	|					(ВЫБРАТЬ
	|						Т.Организация
	|					ИЗ
	|						ПартииПрочихРасходов_ВНА КАК Т)
	|				И Регистратор ССЫЛКА Документ.ПринятиеКУчетуНМА) КАК ПервоначальныеСведения
	|			ПО ПУ.Ссылка = ПервоначальныеСведения.Регистратор
	|				И ПУ.НематериальныйАктив = ПервоначальныеСведения.НематериальныйАктив
	|		) КАК МестонахождениеНМА
	|	ПО
	|		ПартииПрочихРасходов.АналитикаРасходов = МестонахождениеНМА.НематериальныйАктив
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ПервоначальныеСведенияНМАБухгалтерскийУчет КАК ПервоначальныеСведенияНМА
	|	ПО
	|		ПервоначальныеСведенияНМА.Регистратор = &Ссылка
	|		И ПартииПрочихРасходов.Организация = ПервоначальныеСведенияНМА.Организация
	|		И ПартииПрочихРасходов.АналитикаРасходов = ПервоначальныеСведенияНМА.НематериальныйАктив
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет КАК ПервоначальныеСведенияОС
	|	ПО
	|		ПервоначальныеСведенияОС.Регистратор = &Ссылка
	|		И ПартииПрочихРасходов.Организация = ПервоначальныеСведенияОС.Организация
	|		И ПартииПрочихРасходов.АналитикаРасходов = ПервоначальныеСведенияОС.ОсновноеСредство
	|
	|ГДЕ
	|	НЕ (СчетаОтражения.СчетУчета ЕСТЬ NULL И СчетаОтраженияНМА.СчетУчета ЕСТЬ NULL)
	|	И НЕ (ПервоначальныеСведенияНМА.НематериальныйАктив ЕСТЬ NULL И ПервоначальныеСведенияОС.ОсновноеСредство ЕСТЬ NULL)
	|	И ПартииПрочихРасходов.НалогообложениеПартии = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|	И ПартииПрочихРасходов.НалогообложениеПартии <> ПартииПрочихРасходов.НалогообложениеФинПартии
	|	И ТИПЗНАЧЕНИЯ(ПартииПрочихРасходов.АналитикаРасходов) В (
	|		ТИП(Справочник.ОбъектыЭксплуатации), 
	|		ТИП(Справочник.НематериальныеАктивы))
	|	И ПартииПрочихРасходов.НДСРегл <> 0
	|";
#КонецОбласти

#Область СтоимостьОСВРасходыНУ                  // (Дт <СчетРасходов> :: Кт <01>)
	СтоимостьОСВРасходыНУ = "
	|ВЫБРАТЬ ////Стоимость ОС на счет учета (Дт <СчетУчетаОС/НМА> :: Кт <01>)
	|	ПартииПрочихРасходов.Регистратор КАК Ссылка,
	|	ПартииПрочихРасходов.Период КАК Период,
	|	ПартииПрочихРасходов.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	0 КАК Сумма,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	ЕСТЬNULL(ПринятиеКУчетуОС.СтатьяРасходов, ПринятиеКУчетуНМА.СтатьяРасходов) КАК АналитикаУчетаДт,
	|	ЕСТЬNULL(ПринятиеКУчетуОС.Местонахождение, ПринятиеКУчетуНМА.Подразделение) КАК МестоУчетаДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	ЕСТЬNULL(ПринятиеКУчетуОС.Местонахождение, ПринятиеКУчетуНМА.Подразделение) КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	ЕСТЬNULL(ПринятиеКУчетуОС.СтатьяРасходов, ПринятиеКУчетуНМА.СтатьяРасходов) КАК СубконтоДт1,
	|	ЕСТЬNULL(ПринятиеКУчетуОС.АналитикаРасходов, ПринятиеКУчетуНМА.АналитикаРасходов) КАК СубконтоДт2,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ПартииПрочихРасходов.НДСРегл КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	-ПартииПрочихРасходов.НДСРегл КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	ЕСТЬNULL(ПринятиеКУчетуОС.Местонахождение, ПринятиеКУчетуНМА.Подразделение) КАК ПодразделениеКт,
	|	ПартииПрочихРасходов.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЕСТЬNULL(СчетаОтражения.СчетУчета, СчетаОтраженияНМА.СчетУчета) КАК СчетКт,
	|	
	|	ПартииПрочихРасходов.АналитикаРасходов КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ПартииПрочихРасходов.НДСРегл КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	-ПартииПрочихРасходов.НДСРегл КАК СуммаВРКт,
	|	""Включение в расходы при принятии к учету"" КАК Содержание
	|ИЗ
	|	ПартииПрочихРасходов КАК ПартииПрочихРасходов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СчетаБухгалтерскогоУчетаОС.СрезПоследних(
	|				КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ),
	|				(Организация, ОсновноеСредство) В
	|						(ВЫБРАТЬ
	|							Т.Организация,
	|							Т.Актив
	|						ИЗ
	|							ПартииПрочихРасходов_ВНА КАК Т)
	|			) КАК СчетаОтражения
	|	ПО
	|		ПартииПрочихРасходов.АналитикаРасходов = СчетаОтражения.ОсновноеСредство
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СчетаБухгалтерскогоУчетаНМА.СрезПоследних(
	|				КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ),
	|				(Организация, НематериальныйАктив) В
	|						(ВЫБРАТЬ
	|							Т.Организация,
	|							Т.Актив
	|						ИЗ
	|							ПартииПрочихРасходов_ВНА КАК Т)
	|		) КАК СчетаОтраженияНМА
	|	ПО
	|		ПартииПрочихРасходов.АналитикаРасходов = СчетаОтраженияНМА.НематериальныйАктив
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ПервоначальныеСведенияНМАНалоговыйУчет.СрезПоследних(
	|				КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ),
	|				(НематериальныйАктив, Организация) В 
	|						(ВЫБРАТЬ
	|							Т.Актив,
	|							Т.Организация
	|						ИЗ
	|							ПартииПрочихРасходов_ВНА КАК Т)
	|				) КАК ПервоначальныеСведенияНМА
	|	ПО
	|		ПартииПрочихРасходов.АналитикаРасходов = ПервоначальныеСведенияНМА.НематериальныйАктив
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.ПринятиеКУчетуНМА КАК ПринятиеКУчетуНМА
	|	ПО
	|		ПервоначальныеСведенияНМА.Регистратор = ПринятиеКУчетуНМА.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ПервоначальныеСведенияОСНалоговыйУчет.СрезПоследних(
	|				КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ),
	|				(ОсновноеСредство, Организация) В 
	|					(ВЫБРАТЬ
	|						Т.Актив,
	|						Т.Организация
	|					ИЗ
	|						ПартииПрочихРасходов_ВНА КАК Т)
	|				) КАК ПервоначальныеСведенияОС
	|	ПО
	|		ПартииПрочихРасходов.АналитикаРасходов = ПервоначальныеСведенияОС.ОсновноеСредство
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.ПринятиеКУчетуОС КАК ПринятиеКУчетуОС
	|	ПО
	|		ПервоначальныеСведенияОС.Регистратор = ПринятиеКУчетуОС.Ссылка
	|		
	|ГДЕ
	|	НЕ (СчетаОтражения.СчетУчета ЕСТЬ NULL И СчетаОтраженияНМА.СчетУчета ЕСТЬ NULL)
	|	И НЕ (ПринятиеКУчетуОС.Ссылка ЕСТЬ NULL И ПринятиеКУчетуНМА.Ссылка ЕСТЬ NULL)
	|	И ПартииПрочихРасходов.НалогообложениеПартии = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|	И ПартииПрочихРасходов.НалогообложениеФинПартии <> ПартииПрочихРасходов.НалогообложениеПартии
	|	И ТИПЗНАЧЕНИЯ(ПартииПрочихРасходов.АналитикаРасходов) В (
	|		ТИП(Справочник.ОбъектыЭксплуатации), 
	|		ТИП(Справочник.НематериальныеАктивы))
	|	И ВЫБОР 
	|		КОГДА НЕ ПринятиеКУчетуОС.Ссылка ЕСТЬ NULL
	|				И ПринятиеКУчетуОС.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.ВключениеВРасходыПриПринятииКУчету)
	|				И ПринятиеКУчетуОС.СтатьяРасходов.ПринятиеКналоговомуУчету
	|			ТОГДА ИСТИНА
	|		КОГДА НЕ ПринятиеКУчетуНМА.Ссылка ЕСТЬ NULL
	|				И ПринятиеКУчетуНМА.ПорядокСписанияНИОКРНаРасходыНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокСписанияНИОКРНУ.ПриПринятииКУчету)
	|				И ПринятиеКУчетуНМА.СтатьяРасходов.ПринятиеКналоговомуУчету
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|	И ПартииПрочихРасходов.НДСРегл <> 0
	|";
#КонецОбласти

#Область ИсключениеНДССоСчетаУчетаОС             // (Дт <08.04.2> :: Кт <СчетУчетаОС/НМА>)
	ИсключениеНДССоСчетаУчетаОС = "
	|ВЫБРАТЬ ////Исключение НДС со счета учета ОС (Дт <08.04.2> :: Кт <СчетУчетаОС/НМА>)
	|	ПартииПрочихРасходов.Регистратор КАК Ссылка,
	|	ПартииПрочихРасходов.Период КАК Период,
	|	ПартииПрочихРасходов.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ПартииПрочихРасходов.НДСРегл КАК Сумма,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	ПартииПрочихРасходов.СтатьяРасходов КАК АналитикаУчетаДт,
	|	ПартииПрочихРасходов.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	ПартииПрочихРасходов.Подразделение КАК ПодразделениеДт,
	|	ПартииПрочихРасходов.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	ПартииПрочихРасходов.СтатьяРасходов КАК СубконтоДт1,
	|	ПартииПрочихРасходов.АналитикаРасходов КАК СубконтоДт2,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный) КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	ЕСТЬNULL(МестонахождениеОС.Местонахождение, МестонахождениеНМА.Местонахождение) КАК ПодразделениеКт,
	|	ПартииПрочихРасходов.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЕСТЬNULL(СчетаОтражения.СчетУчета, СчетаОтраженияНМА.СчетУчета) КАК СчетКт,
	|	
	|	ПартииПрочихРасходов.АналитикаРасходов КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Исключение НДС со счета учета ОС"" КАК Содержание
	|ИЗ
	|	ПартииПрочихРасходов КАК ПартииПрочихРасходов
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО
	|		ПартииПрочихРасходов.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СчетаБухгалтерскогоУчетаОС.СрезПоследних(
	|				КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ),
	|				(Организация, ОсновноеСредство) В
	|						(ВЫБРАТЬ
	|							Т.Организация,
	|							Т.Актив
	|						ИЗ
	|							ПартииПрочихРасходов_ВНА КАК Т)
	|		) КАК СчетаОтражения
	|	ПО
	|		ПартииПрочихРасходов.АналитикаРасходов = СчетаОтражения.ОсновноеСредство
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СчетаБухгалтерскогоУчетаНМА.СрезПоследних(
	|				КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ),
	|				(Организация, НематериальныйАктив) В
	|						(ВЫБРАТЬ
	|							Т.Организация,
	|							Т.Актив
	|						ИЗ
	|							ПартииПрочихРасходов_ВНА КАК Т)
	|		) КАК СчетаОтраженияНМА
	|	ПО
	|		ПартииПрочихРасходов.АналитикаРасходов = СчетаОтраженияНМА.НематериальныйАктив
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.МестонахождениеОСБухгалтерскийУчет.СрезПоследних(
	|				КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ),
	|				(Организация, ОсновноеСредство) В
	|						(ВЫБРАТЬ
	|							Т.Организация,
	|							Т.Актив
	|						ИЗ
	|							ПартииПрочихРасходов_ВНА КАК Т)
	|		) КАК МестонахождениеОС
	|	ПО
	|		ПартииПрочихРасходов.АналитикаРасходов = МестонахождениеОС.ОсновноеСредство
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		(ВЫБРАТЬ
	|			ПУ.НематериальныйАктив,
	|			ПУ.Подразделение КАК Местонахождение
	|		ИЗ
	|			Документ.ПринятиеКУчетуНМА КАК ПУ
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияНМАБухгалтерскийУчет.СрезПоследних(
	|				КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ),
	|				Организация В 
	|					(ВЫБРАТЬ
	|						Т.Организация
	|					ИЗ
	|						ПартииПрочихРасходов_ВНА КАК Т)
	|				И Регистратор ССЫЛКА Документ.ПринятиеКУчетуНМА) КАК ПервоначальныеСведения
	|			ПО ПУ.Ссылка = ПервоначальныеСведения.Регистратор
	|				И ПУ.НематериальныйАктив = ПервоначальныеСведения.НематериальныйАктив
	|		) КАК МестонахождениеНМА
	|	ПО
	|		ПартииПрочихРасходов.АналитикаРасходов = МестонахождениеНМА.НематериальныйАктив
	|
	|ГДЕ
	|	НЕ (СчетаОтражения.СчетУчета ЕСТЬ NULL И СчетаОтраженияНМА.СчетУчета ЕСТЬ NULL)
	|	И ПартииПрочихРасходов.НалогообложениеФинПартии = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|	И ПартииПрочихРасходов.НалогообложениеПартии <> ПартииПрочихРасходов.НалогообложениеФинПартии
	|	И ТИПЗНАЧЕНИЯ(ПартииПрочихРасходов.АналитикаРасходов) В (
	|			ТИП(Справочник.ОбъектыЭксплуатации), 
	|			ТИП(Справочник.НематериальныеАктивы))
	|	И ПартииПрочихРасходов.НДСРегл <> 0
	|";
#КонецОбласти
	
#Область КорректировкаАмортизационнойПремии      // (Дт <КВ> :: Кт <ПустаяСсылка>)
	КорректировкаАмортизационнойПремии = "
	|ВЫБРАТЬ ////Корректировка амортизационной премии с учетом изменения стоимости ОС (Дт <КВ> :: Кт <ПустаяСсылка>)
	|	ПартииПрочихРасходов.Регистратор КАК Ссылка,
	|	ПартииПрочихРасходов.Период КАК Период,
	|	ПартииПрочихРасходов.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	0 КАК Сумма,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	МестонахождениеОС.Местонахождение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасходыНаКапитальныеВложения) КАК СчетДт,
	|
	|	ПартииПрочихРасходов.СтатьяРасходов КАК СубконтоДт1,
	|	ПартииПрочихРасходов.АналитикаРасходов КАК СубконтоДт2,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный) КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ВЫБОР КОГДА ПартииПрочихРасходов.НалогообложениеПартии = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|			И ПартииПрочихРасходов.НалогообложениеПартии <> ПартииПрочихРасходов.НалогообложениеФинПартии ТОГДА
	|		// Увеличиваем амортизационную премию
	|		ВЫРАЗИТЬ((ПартииПрочихРасходов.НДСРегл)*(Операция.ПроцентКапитальныхВложенийВключаемыхВРасходыНУ / 100) КАК ЧИСЛО(15, 2))
	|	ИНАЧЕ
	|		// Уменьшаем амортизационную премию
	|		-ВЫРАЗИТЬ((ПартииПрочихРасходов.НДСРегл)*(Операция.ПроцентКапитальныхВложенийВключаемыхВРасходыНУ / 100) КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Корректировка амортизационной премии с учетом изменения стоимости ОС"" КАК Содержание
	|	
	|ИЗ
	|	ПартииПрочихРасходов КАК ПартииПрочихРасходов
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС.ОС КАК Строки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС КАК Операция
	|			ПО Строки.Ссылка = Операция.Ссылка
	|			И Операция.Дата МЕЖДУ НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ) И КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ)
	|			И Операция.Проведен
	|		ПО ПартииПрочихРасходов.АналитикаРасходов = Строки.ОсновноеСредство
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|		ПО ПартииПрочихРасходов.СтатьяРасходов = СтатьиРасходов.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОСБухгалтерскийУчет.СрезПоследних(
	|				КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ),
	|				(Организация, ОсновноеСредство) В
	|						(ВЫБРАТЬ
	|							Т.Организация,
	|							Т.Актив
	|						ИЗ
	|							ПартииПрочихРасходов_ВНА КАК Т)
	|		) КАК МестонахождениеОС
	|	ПО
	|		ПартииПрочихРасходов.АналитикаРасходов = МестонахождениеОС.ОсновноеСредство
	|
	|ГДЕ
	|	Операция.ВключитьАмортизационнуюПремиюВСоставРасходов
	|	И Операция.ПроцентКапитальныхВложенийВключаемыхВРасходыНУ <> 0
	|	И СтатьиРасходов.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|	И ТИПЗНАЧЕНИЯ(ПартииПрочихРасходов.АналитикаРасходов) = ТИП(Справочник.ОбъектыЭксплуатации)
	|	И ПартииПрочихРасходов.НДСРегл <> 0
	|	И ПартииПрочихРасходов.НалогообложениеФинПартии <> ПартииПрочихРасходов.НалогообложениеПартии 
	|	И (ПартииПрочихРасходов.НалогообложениеПартии = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|		ИЛИ ПартииПрочихРасходов.НалогообложениеФинПартии = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС))
	|";
#КонецОбласти

#Область ПередачаНДСВСтоимостьПрочегоАктива               // (Дт <08.04.2>, ХХ :: Кт <19.1, 19.3>)
	ПередачаНДСВСтоимостьПрочегоАктива = "
	|ВЫБРАТЬ //// Передача НДС в стоимость ОС (Дт <08.04.2>, ХХ :: Кт <19.1, 19.3>)
	|	ПартииПрочихРасходов.Регистратор КАК Ссылка,
	|	ПартииПрочихРасходов.Период КАК Период,
	|	ПартииПрочихРасходов.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ПартииПрочихРасходов.НДСРегл КАК Сумма,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	ПартииПрочихРасходов.Подразделение КАК ПодразделениеДт,
	|	ПартииПрочихРасходов.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	СчетаУчетаПрочихАктивов.СчетУчета КАК СчетДт,
	|
	|	СчетаУчетаПрочихАктивов.Субконто1 КАК СубконтоДт1,
	|	СчетаУчетаПрочихАктивов.Субконто2 КАК СубконтоДт2,
	|	СчетаУчетаПрочихАктивов.Субконто3 КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НДСпоПриобретеннымЦенностям) КАК ВидСчетаКт,
	|	ПартииПрочихРасходов.НалогообложениеФинПартии КАК АналитикаУчетаКт,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(Аналитика.Номенклатура.ТипНоменклатуры, НЕОПРЕДЕЛЕНО) В (
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|			ТОГДА ЕСТЬNULL(ГруппыФинансовогоУчетаНоменклатуры.ВидЦенностиНДС, ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)) 
	|		КОГДА ПартииПрочихРасходов.АналитикаАктивовПассивов ССЫЛКА Справочник.ОбъектыЭксплуатации
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ОС)
	|		КОГДА ПартииПрочихРасходов.АналитикаАктивовПассивов ССЫЛКА Справочник.НематериальныеАктивы
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.НМА)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|	КОНЕЦ КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	ПартииПрочихРасходов.АналитикаУчетаПартий.Контрагент КАК СубконтоКт1,
	|	ПартииПрочихРасходов.ДокументПоступленияРасходов КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Передача НДС в стоимость активы"" КАК Содержание
	|ИЗ
	|	ПартииПрочихРасходов КАК ПартииПрочихРасходов
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиАктивовПассивов КАК Статьи
	|	ПО
	|		ПартииПрочихРасходов.СтатьяРасходов = Статьи.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|	ПО 
	|		Аналитика.Ссылка = ПартииПрочихРасходов.АналитикаУчетаНоменклатуры
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ГруппыФинансовогоУчетаНоменклатуры КАК ГруппыФинансовогоУчетаНоменклатуры
	|	ПО
	|		Аналитика.Номенклатура.ГруппаФинансовогоУчета = ГруппыФинансовогоУчетаНоменклатуры.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК АналитикаПартий
	|		ПО АналитикаПартий.Ссылка = ПартииПрочихРасходов.АналитикаУчетаПартий
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		СчетаУчетаПрочихАктивов КАК СчетаУчетаПрочихАктивов
	|	ПО
	|		ПартииПрочихРасходов.Организация = СчетаУчетаПрочихАктивов.Организация
	|		И ПартииПрочихРасходов.Подразделение = СчетаУчетаПрочихАктивов.Подразделение
	|		И ПартииПрочихРасходов.СтатьяРасходов = СчетаУчетаПрочихАктивов.Статья
	|		И ПартииПрочихРасходов.АналитикаАктивовПассивов = СчетаУчетаПрочихАктивов.АналитикаАктивовПассивов
	|		И ПартииПрочихРасходов.ДокументПоступленияРасходов = СчетаУчетаПрочихАктивов.ДокументПоступленияРасходов
	|		И ПартииПрочихРасходов.АналитикаУчетаПартий = СчетаУчетаПрочихАктивов.АналитикаУчетаПартий
	|		И ПартииПрочихРасходов.НаправлениеДеятельности = СчетаУчетаПрочихАктивов.НаправлениеДеятельности
	|		И ПартииПрочихРасходов.АналитикаУчетаНоменклатуры = СчетаУчетаПрочихАктивов.АналитикаУчетаНоменклатуры
	|
	|ГДЕ
	|	ПартииПрочихРасходов.НалогообложениеПартии = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС) 
	|	И ПартииПрочихРасходов.НалогообложениеФинПартии <> ПартииПрочихРасходов.НалогообложениеПартии
	|	И ПартииПрочихРасходов.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|	И ПартииПрочихРасходов.НДСРегл <> 0
	|";
#КонецОбласти

#Область СтоимостьПрочегоАктиваНаОСиНМА              // (Дт <СчетУчетаОС/НМА> :: Кт <08.04.2>)
	СтоимостьПрочегоАктиваНаОСиНМА = "
	|ВЫБРАТЬ ////Стоимость ОС на счет учета (Дт <СчетУчетаОС/НМА> :: Кт <08.04.2>)
	|	ПартииПрочихРасходов.Регистратор КАК Ссылка,
	|	ПартииПрочихРасходов.Период КАК Период,
	|	ПартииПрочихРасходов.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ПартииПрочихРасходов.НДСРегл КАК Сумма,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	ЕСТЬNULL(МестонахождениеОС.Местонахождение, МестонахождениеНМА.Местонахождение) КАК ПодразделениеДт,
	|	ПартииПрочихРасходов.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЕСТЬNULL(СчетаОтражения.СчетУчета, СчетаОтраженияНМА.СчетУчета) КАК СчетДт,
	|	
	|	ПартииПрочихРасходов.АналитикаАктивовПассивов КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	ПартииПрочихРасходов.Подразделение КАК ПодразделениеКт,
	|	ПартииПрочихРасходов.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	СчетаУчетаПрочихАктивов.СчетУчета КАК СчетКт,
	|
	|	СчетаУчетаПрочихАктивов.Субконто1 КАК СубконтоКт1,
	|	СчетаУчетаПрочихАктивов.Субконто2 КАК СубконтоКт2,
	|	СчетаУчетаПрочихАктивов.Субконто3 КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""НДС в стоимость актива"" КАК Содержание
	|ИЗ
	|	ПартииПрочихРасходов КАК ПартииПрочихРасходов
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиАктивовПассивов КАК СтатьиАктивовПассивов
	|	ПО
	|		ПартииПрочихРасходов.СтатьяРасходов = СтатьиАктивовПассивов.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СчетаБухгалтерскогоУчетаОС.СрезПоследних(
	|				КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ),
	|				(Организация, ОсновноеСредство) В
	|						(ВЫБРАТЬ
	|							Т.Организация,
	|							Т.Актив
	|						ИЗ
	|							ПартииПрочихРасходов_ВНА КАК Т)
	|		) КАК СчетаОтражения
	|	ПО
	|		ПартииПрочихРасходов.АналитикаАктивовПассивов = СчетаОтражения.ОсновноеСредство
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СчетаБухгалтерскогоУчетаНМА.СрезПоследних(
	|				КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ),
	|				(Организация, НематериальныйАктив) В
	|						(ВЫБРАТЬ
	|							Т.Организация,
	|							Т.Актив
	|						ИЗ
	|							ПартииПрочихРасходов_ВНА КАК Т)
	|		) КАК СчетаОтраженияНМА
	|	ПО
	|		ПартииПрочихРасходов.АналитикаАктивовПассивов = СчетаОтраженияНМА.НематериальныйАктив
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.МестонахождениеОСБухгалтерскийУчет.СрезПоследних(
	|				КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ),
	|				(Организация, ОсновноеСредство) В
	|						(ВЫБРАТЬ
	|							Т.Организация,
	|							Т.Актив
	|						ИЗ
	|							ПартииПрочихРасходов_ВНА КАК Т)
	|		) КАК МестонахождениеОС
	|	ПО
	|		ПартииПрочихРасходов.АналитикаАктивовПассивов = МестонахождениеОС.ОсновноеСредство
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		(ВЫБРАТЬ
	|			ПУ.НематериальныйАктив,
	|			ПУ.Подразделение КАК Местонахождение
	|		ИЗ
	|			Документ.ПринятиеКУчетуНМА КАК ПУ
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияНМАБухгалтерскийУчет.СрезПоследних(
	|				КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ),
	|				Организация В 
	|					(ВЫБРАТЬ
	|						Т.Организация
	|					ИЗ
	|						ПартииПрочихРасходов_ВНА КАК Т)
	|				И Регистратор ССЫЛКА Документ.ПринятиеКУчетуНМА) КАК ПервоначальныеСведения
	|			ПО ПУ.Ссылка = ПервоначальныеСведения.Регистратор
	|				И ПУ.НематериальныйАктив = ПервоначальныеСведения.НематериальныйАктив
	|		) КАК МестонахождениеНМА
	|	ПО
	|		ПартииПрочихРасходов.АналитикаАктивовПассивов = МестонахождениеНМА.НематериальныйАктив
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		СчетаУчетаПрочихАктивов КАК СчетаУчетаПрочихАктивов
	|	ПО
	|		ПартииПрочихРасходов.Организация = СчетаУчетаПрочихАктивов.Организация
	|		И ПартииПрочихРасходов.Подразделение = СчетаУчетаПрочихАктивов.Подразделение
	|		И ПартииПрочихРасходов.СтатьяРасходов = СчетаУчетаПрочихАктивов.Статья
	|		И ПартииПрочихРасходов.АналитикаАктивовПассивов = СчетаУчетаПрочихАктивов.АналитикаАктивовПассивов
	|		И ПартииПрочихРасходов.ДокументПоступленияРасходов = СчетаУчетаПрочихАктивов.ДокументПоступленияРасходов
	|		И ПартииПрочихРасходов.АналитикаУчетаПартий = СчетаУчетаПрочихАктивов.АналитикаУчетаПартий
	|		И ПартииПрочихРасходов.НаправлениеДеятельности = СчетаУчетаПрочихАктивов.НаправлениеДеятельности
	|		И ПартииПрочихРасходов.АналитикаУчетаНоменклатуры = СчетаУчетаПрочихАктивов.АналитикаУчетаНоменклатуры
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ПервоначальныеСведенияНМАБухгалтерскийУчет КАК ПервоначальныеСведенияНМА
	|	ПО
	|		ПервоначальныеСведенияНМА.Регистратор = &Ссылка
	|		И ПартииПрочихРасходов.Организация = ПервоначальныеСведенияНМА.Организация
	|		И ПартииПрочихРасходов.АналитикаАктивовПассивов = ПервоначальныеСведенияНМА.НематериальныйАктив
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет КАК ПервоначальныеСведенияОС
	|	ПО
	|		ПервоначальныеСведенияОС.Регистратор = &Ссылка
	|		И ПартииПрочихРасходов.Организация = ПервоначальныеСведенияОС.Организация
	|		И ПартииПрочихРасходов.АналитикаАктивовПассивов = ПервоначальныеСведенияОС.ОсновноеСредство
	|	
	|ГДЕ
	|	НЕ (СчетаОтражения.СчетУчета ЕСТЬ NULL И СчетаОтраженияНМА.СчетУчета ЕСТЬ NULL)
	|	И НЕ (ПервоначальныеСведенияНМА.НематериальныйАктив ЕСТЬ NULL И ПервоначальныеСведенияОС.ОсновноеСредство ЕСТЬ NULL)
	|	И ПартииПрочихРасходов.НалогообложениеПартии = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС) 
	|	И ПартииПрочихРасходов.НалогообложениеФинПартии <> ПартииПрочихРасходов.НалогообложениеПартии
	|	И ПартииПрочихРасходов.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|	И ТИПЗНАЧЕНИЯ(ПартииПрочихРасходов.АналитикаАктивовПассивов) В (
	|				ТИП(Справочник.ОбъектыЭксплуатации), 
	|				ТИП(Справочник.НематериальныеАктивы))
	|	И ПартииПрочихРасходов.НДСРегл <> 0
	|";
#КонецОбласти

	Возврат ПередачаНДСВСтоимостьОС
		+ " ОБЪЕДИНИТЬ ВСЕ " + ИсключениеНДСИзСтоимостиОС
		+ " ОБЪЕДИНИТЬ ВСЕ " + СтоимостьОСНаСчетУчета
		+ " ОБЪЕДИНИТЬ ВСЕ " + ИсключениеНДССоСчетаУчетаОС
		+ " ОБЪЕДИНИТЬ ВСЕ " + КорректировкаАмортизационнойПремии
		+ " ОБЪЕДИНИТЬ ВСЕ " + ПередачаНДСВСтоимостьПрочегоАктива
		+ " ОБЪЕДИНИТЬ ВСЕ " + СтоимостьПрочегоАктиваНаОСиНМА
		+ " ОБЪЕДИНИТЬ ВСЕ " + СтоимостьОСВРасходыНУ
		+ "";
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц, 
// необходимых для отражения в регламентированном учете
//
// Возвращаемое значение:
//	ТекстЗапроса - Строка - Текст запроса cоздания временных таблиц.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	Возврат
		"ВЫБРАТЬ
		|	Партии.Регистратор                 КАК Регистратор,
		|	Партии.Период                      КАК Период,
		|	Партии.Организация                 КАК Организация,
		|	Партии.Подразделение               КАК Подразделение,
		|	Партии.НаправлениеДеятельности     КАК НаправлениеДеятельности,
		|	Партии.СтатьяРасходов              КАК СтатьяРасходов,
		|	Партии.АналитикаРасходов           КАК АналитикаРасходов,
		|	Партии.АналитикаАктивовПассивов    КАК АналитикаАктивовПассивов,
		|	Партии.ДокументПоступленияРасходов КАК ДокументПоступленияРасходов,
		|	ВЫБОР
		|		КОГДА Партии.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров)
		|				И ЕСТЬNULL(ДанныеПервичныхДокументов.ДатаРегистратора, ДАТАВРЕМЯ(1,1,1)) >= &ДатаНачалаДействия150ФЗ
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
		|		КОГДА Партии.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
		|		ИНАЧЕ Партии.НалогообложениеНДС
		|	КОНЕЦ КАК НалогообложениеПартии,
		|	
		|	ВЫБОР 
		|		КОГДА Партии.ВидДеятельностиНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
		|			ТОГДА // Партионный учет версия 2.2
		|				ВЫБОР
		|					КОГДА Партии.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров)
		|							И ЕСТЬNULL(ДанныеПервичныхДокументов.ДатаРегистратора, ДАТАВРЕМЯ(1,1,1)) >= &ДатаНачалаДействия150ФЗ
		|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
		|					КОГДА Партии.ВидДеятельностиНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
		|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
		|					ИНАЧЕ Партии.ВидДеятельностиНДС
		|				КОНЕЦ
		|			ИНАЧЕ  // Партионный учет версия 2.1
		|				ВЫБОР
		|					КОГДА АналитикаПартий.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров)
		|							И ЕСТЬNULL(ДанныеПервичныхДокументов.ДатаРегистратора, ДАТАВРЕМЯ(1,1,1)) >= &ДатаНачалаДействия150ФЗ
		|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
		|					КОГДА АналитикаПартий.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
		|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
		|					ИНАЧЕ АналитикаПартий.НалогообложениеНДС
		|				КОНЕЦ
		|	КОНЕЦ                              КАК НалогообложениеФинПартии,
		|	Партии.АналитикаУчетаПартий        КАК АналитикаУчетаПартий,
		|	Партии.АналитикаУчетаНоменклатуры  КАК АналитикаУчетаНоменклатуры,
		|	Партии.СтатьяОтраженияРасходов     КАК СтатьяОтраженияРасходов,
		|	Партии.НДСРегл                     КАК НДСРегл
		|ПОМЕСТИТЬ ПартииПрочихРасходов
		|ИЗ
		|	РегистрНакопления.ПартииПрочихРасходов КАК Партии
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
		|		Справочник.КлючиАналитикиУчетаПартий КАК АналитикаПартий
		|	ПО 
		|		АналитикаПартий.Ссылка = Партии.АналитикаУчетаПартий
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ 
		|		РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
		|	ПО
		|		ДанныеПервичныхДокументов.Организация = Партии.Организация
		|		И ДанныеПервичныхДокументов.Документ = Партии.ДокументПоступленияРасходов
		|
		|ГДЕ
		|	Партии.Регистратор = &Ссылка
		|;
		|
		|///////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПартииПрочихРасходов.Организация КАК Организация,
		|	ПартииПрочихРасходов.АналитикаРасходов КАК Актив
		|ПОМЕСТИТЬ ПартииПрочихРасходов_ВНА
		|ИЗ
		|	ПартииПрочихРасходов КАК ПартииПрочихРасходов
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(ПартииПрочихРасходов.АналитикаРасходов) В (
		|			ТИП(Справочник.ОбъектыЭксплуатации),
		|			ТИП(Справочник.НематериальныеАктивы))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПартииПрочихРасходов.Организация КАК Организация,
		|	ПартииПрочихРасходов.АналитикаАктивовПассивов КАК Актив
		|ИЗ
		|	ПартииПрочихРасходов КАК ПартииПрочихРасходов
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(ПартииПрочихРасходов.АналитикаАктивовПассивов) В (
		|			ТИП(Справочник.ОбъектыЭксплуатации),
		|			ТИП(Справочник.НематериальныеАктивы))
		|;
		|
		|ВЫБРАТЬ
		|	ПартииПрочихРасходов.Организация КАК Организация,
		|	ПартииПрочихРасходов.Подразделение КАК Подразделение,
		|	ПартииПрочихРасходов.СтатьяРасходов КАК Статья,
		|	ПартииПрочихРасходов.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
		|	ПартииПрочихРасходов.ДокументПоступленияРасходов КАК ДокументПоступленияРасходов,
		|	ПартииПрочихРасходов.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
		|	ПартииПрочихРасходов.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	ПартииПрочихРасходов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	МАКСИМУМ(ЕСТЬNULL(ПоступлениеУслугПрочихАктивов.СчетУчета, ЕСТЬNULL(КорректировкаПоступления.СчетУчета ,ВнутреннееПотреблениеТоваров.СчетУчета))) КАК СчетУчета,
		|	МАКСИМУМ(ЕСТЬNULL(ПоступлениеУслугПрочихАктивов.Субконто1, ЕСТЬNULL(КорректировкаПоступления.Субконто1 ,ВнутреннееПотреблениеТоваров.Субконто1))) КАК Субконто1,
		|	МАКСИМУМ(ЕСТЬNULL(ПоступлениеУслугПрочихАктивов.Субконто2, ЕСТЬNULL(КорректировкаПоступления.Субконто2 ,ВнутреннееПотреблениеТоваров.Субконто2))) КАК Субконто2,
		|	МАКСИМУМ(ЕСТЬNULL(ПоступлениеУслугПрочихАктивов.Субконто3, ЕСТЬNULL(КорректировкаПоступления.Субконто3 ,ВнутреннееПотреблениеТоваров.Субконто3))) КАК Субконто3
		|ПОМЕСТИТЬ СчетаУчетаПрочихАктивов
		|ИЗ
		|	ПартииПрочихРасходов КАК ПартииПрочихРасходов
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		РегистрНакопления.ПартииПрочихРасходов КАК ПартииПрочихРасходовПриход
		|	ПО
		|		ПартииПрочихРасходовПриход.Организация = ПартииПрочихРасходов.Организация
		|		И ПартииПрочихРасходовПриход.Подразделение = ПартииПрочихРасходов.Подразделение
		|		И ПартииПрочихРасходовПриход.СтатьяРасходов = ПартииПрочихРасходов.СтатьяРасходов
		|		И ПартииПрочихРасходовПриход.АналитикаАктивовПассивов = ПартииПрочихРасходов.АналитикаАктивовПассивов
		|		И ПартииПрочихРасходовПриход.ДокументПоступленияРасходов = ПартииПрочихРасходов.ДокументПоступленияРасходов
		|		И ПартииПрочихРасходовПриход.АналитикаУчетаПартий = ПартииПрочихРасходов.АналитикаУчетаПартий
		|		И ПартииПрочихРасходовПриход.НаправлениеДеятельности = ПартииПрочихРасходов.НаправлениеДеятельности
		|		И ПартииПрочихРасходовПриход.АналитикаУчетаНоменклатуры = ПартииПрочихРасходов.АналитикаУчетаНоменклатуры
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		Документ.ПоступлениеУслугПрочихАктивов.Расходы КАК ПоступлениеУслугПрочихАктивов
		|	ПО
		|		ПартииПрочихРасходовПриход.Регистратор = ПоступлениеУслугПрочихАктивов.Ссылка
		|		И ПартииПрочихРасходовПриход.СтатьяРасходов = ПоступлениеУслугПрочихАктивов.СтатьяРасходов
		|		И ПартииПрочихРасходовПриход.АналитикаАктивовПассивов = ПоступлениеУслугПрочихАктивов.АналитикаАктивовПассивов
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		Документ.КорректировкаПоступления.Расхождения КАК КорректировкаПоступления
		|	ПО
		|		ПартииПрочихРасходовПриход.Регистратор = КорректировкаПоступления.Ссылка
		|		И ПартииПрочихРасходовПриход.СтатьяРасходов = КорректировкаПоступления.СтатьяРасходов
		|		И ПартииПрочихРасходовПриход.АналитикаАктивовПассивов = КорректировкаПоступления.АналитикаАктивовПассивов
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		Документ.ВнутреннееПотреблениеТоваров.Товары КАК ВнутреннееПотреблениеТоваров
		|	ПО
		|		ПартииПрочихРасходовПриход.Регистратор = ВнутреннееПотреблениеТоваров.Ссылка
		|		И ПартииПрочихРасходовПриход.СтатьяРасходов = ВнутреннееПотреблениеТоваров.СтатьяРасходов
		|		И ПартииПрочихРасходовПриход.АналитикаАктивовПассивов = ВнутреннееПотреблениеТоваров.АналитикаАктивовПассивов
		|	
		|ГДЕ
		|	ПартииПрочихРасходов.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
		|	И ПартииПрочихРасходов.СтатьяОтраженияРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
		|	И ПартииПрочихРасходов.НалогообложениеПартии = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПартииПрочихРасходов.Организация,
		|	ПартииПрочихРасходов.Подразделение,
		|	ПартииПрочихРасходов.СтатьяРасходов,
		|	ПартииПрочихРасходов.АналитикаАктивовПассивов,
		|	ПартииПрочихРасходов.ДокументПоступленияРасходов,
		|	ПартииПрочихРасходов.АналитикаУчетаПартий,
		|	ПартииПрочихРасходов.НаправлениеДеятельности,
		|	ПартииПрочихРасходов.АналитикаУчетаНоменклатуры
		|;
		|";
	
КонецФункции

//-- НЕ УТ

// Возвращает признак необходимости распределения НДС по товарам
//
// Параметры:
// 	Организация -  СправочникСсылка.Организации - Организация, в которой выполняется распределение НДС.
// 	Период -  Дата - Период выполнения распределения НДС.
//
// Возвращаемое значение:
// 	Результат - Булево - Признак необходимости распределения НДС по товарам
//
Функция НеобходимоРаспределениеНДСПоТоварам(Организация, Знач Период) Экспорт
	
	Если Не ЗначениеЗаполнено(Период) Тогда
		Период = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	1
	|ИЗ
	|	РегистрНакопления.ПартииПрочихРасходов КАК ПартииПрочихРасходов
	|ГДЕ
	|	ПартииПрочихРасходов.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ПартииПрочихРасходов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И ПартииПрочихРасходов.Организация = &Организация
	|	И ПартииПрочихРасходов.СтатьяРасходов = НЕОПРЕДЕЛЕНО
	|	И ПартииПрочихРасходов.НДСРегл <> 0
	|";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоКвартала(Период));
	Запрос.УстановитьПараметр("КонецПериода", КонецКвартала(Период));
	
	Результат = НЕ Запрос.Выполнить().Пустой();
	
	Возврат Результат;
	
КонецФункции

// Возвращают данные для оценки возможности применения "правила 5%"
//
// Параметры:
// 	 Организация - СправочникСсылка.Организация - Организация
// 	 Период - Дата - Квартал для оценки
//
// Возвращаемое значение:
// 	 Оценка - Структура - Поля:
// 	 						* РасходыПоДеятельностиНеОблагаемойНДС - Число - Расходы по реализации не облагаемой НДС.
// 							* РасходыВсего - Число - Обшая сумма расходов.
// 							* Доля - Число - Процент расходов, относящихся к реализации не облагаемой НДС.
//
Функция ОценкаПримененияПравила5Процентов(Организация, Период) Экспорт
	
	Оценка = Новый Структура;
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА СебестоимостьПродаж.НалогообложениеНДС В (&НеОблагаетсяНДС)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НеОблагаетсяНДС,
	|	СУММА(СебестоимостьПродаж.СебестоимостьРегл) КАК Себестоимость
	|ПОМЕСТИТЬ СебестоимостьПродаж
	|ИЗ
	|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК СебестоимостьПродаж
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК АналитикаУчета
	|		ПО СебестоимостьПродаж.АналитикаУчетаПоПартнерам = АналитикаУчета.Ссылка
	|ГДЕ
	|	СебестоимостьПродаж.Период МЕЖДУ НАЧАЛОПЕРИОДА(&Период, КВАРТАЛ) И КОНЕЦПЕРИОДА(&Период, КВАРТАЛ)
	|	И АналитикаУчета.Организация = &Организация
	|	И СебестоимостьПродаж.ТипЗапасов В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга))
	|	И СебестоимостьПродаж.СебестоимостьРегл <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА СебестоимостьПродаж.НалогообложениеНДС В (&НеОблагаетсяНДС)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(ВложенныйЗапрос.РасходыПоДеятельностиНеОблагаемойНДС), 0) КАК РасходыПоДеятельностиНеОблагаемойНДС,
	|	ЕСТЬNULL(СУММА(ВложенныйЗапрос.РасходыВсего), 0) КАК РасходыВсего,
	|	ВЫБОР 
	|		КОГДА СУММА(ВложенныйЗапрос.РасходыВсего) = 0 
	|			ТОГДА 0  
	|		ИНАЧЕ ЕСТЬNULL(СУММА(ВложенныйЗапрос.РасходыПоДеятельностиНеОблагаемойНДС) / СУММА(ВложенныйЗапрос.РасходыВсего), 0) * 100 
	|	КОНЕЦ КАК Доля
	|ИЗ
	|	(ВЫБРАТЬ
	|		СебестоимостьПродаж.Себестоимость КАК РасходыПоДеятельностиНеОблагаемойНДС,
	|		0 КАК РасходыВсего
	|	ИЗ
	|		СебестоимостьПродаж КАК СебестоимостьПродаж
	|	ГДЕ
	|		СебестоимостьПродаж.НеОблагаетсяНДС
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		0,
	|		СебестоимостьПродаж.Себестоимость
	|	ИЗ
	|		СебестоимостьПродаж КАК СебестоимостьПродаж) КАК ВложенныйЗапрос";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Период", Период);
	
	НеОблагаетсяНДС = Новый Массив;
	НеОблагаетсяНДС.Добавить(Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС);
	НеОблагаетсяНДС.Добавить(Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД);
	Запрос.УстановитьПараметр("НеОблагаетсяНДС", НеОблагаетсяНДС);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Для каждого Колонка Из РезультатЗапроса.Колонки Цикл
		Оценка.Вставить(Колонка.Имя, 0);
	КонецЦикла;
	
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Оценка, Выборка);
	КонецЕсли;
	
	Возврат Оценка;
	
КонецФункции

// Выполняет распределение НДС за указанный период в указанных организациях. Предназначена для вызова в рамках
//
// Параметры:
//	Период - Дата - месяц, за который необходимо создать документы РаспределениеНДС.
//	Организация - Массив, СправочникСсылка.Организации, Неопределено - отбор организаций, по которым создаются документы.
//		Если передан пустой параметр, то документы будут созданы по всем организациям.
//	ИмяСобытияЖР - Строка - имя события об ошибке для записи журнала регистрации
//	МенеджерВТКэшейРегистров - МенеджерВременныхТаблиц - содержит кэш оборотов/остатков регистров.
//		Используется только при вызове этого метода в ходе выполнения расчета механизмом партионного учета версии 2.2.
//	ТолькоЕслиЕстьЗадания - Булево - признак проверки заданий к распределению. 
//		Если Ложь, то распределение будет выполнено даже при отсутствии заданий.
//
// Возвращаемое значение:
//		Структура - результат распределения НДС; ключи структуры
//		  - МассивДокументов - Массив - ссылки на все обработанные документы распределения НДС (независимо от наличия в них ошибок)
//		  - РассчитанныеОрганизации - Массив - ссылки на организации, по которым распределение НДС было выполнено без ошибок
//		  - ТекстОшибки - Строка - текст всех ошибок, зафиксированных при распределении НДС
//
Функция РаспределитьНДС(Период, Организация, ИмяСобытияЖР, МенеджерВТКэшейРегистров = Неопределено, ТолькоЕслиЕстьЗадания = Истина) Экспорт
	
	РезультатРаспределенияНДС = Новый Структура;
	РезультатРаспределенияНДС.Вставить("МассивДокументов", 		  Новый Массив);
	РезультатРаспределенияНДС.Вставить("РассчитанныеОрганизации", Новый Массив);
	РезультатРаспределенияНДС.Вставить("ТекстОшибки", 	   		  "");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДанныеСправочника.Ссылка КАК Организация
	|ПОМЕСТИТЬ ВТОрганизации
	|ИЗ
	|	Справочник.Организации КАК ДанныеСправочника
	|ГДЕ
	|	(ДанныеСправочника.Ссылка В (&МассивОрганизаций)
	|		ИЛИ &ПоВсемОрганизациям)
	|	И (ДанныеСправочника.Ссылка <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|		ИЛИ &УчитыватьУпрОрганизацию)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТОрганизации.Организация	 	 КАК Организация,
	|	МАКСИМУМ(ЕСТЬNULL(РаспределениеНДСПроведен.Ссылка, РаспределениеНДССоздан.Ссылка)) КАК Документ
	|ИЗ
	|	ВТОрганизации КАК ВТОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеНДС КАК РаспределениеНДСПроведен
	|		ПО ВТОрганизации.Организация = РаспределениеНДСПроведен.Организация
	|			И РаспределениеНДСПроведен.Проведен
	|			И РаспределениеНДСПроведен.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеНДС КАК РаспределениеНДССоздан
	|		ПО ВТОрганизации.Организация = РаспределениеНДССоздан.Организация
	|			И НЕ РаспределениеНДССоздан.ПометкаУдаления
	|			И РаспределениеНДССоздан.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	
	|СГРУППИРОВАТЬ ПО
	|	ВТОрганизации.Организация
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация";
	
	Запрос.УстановитьПараметр("ДатаНачала",    			 НачалоМесяца(Период));
	Запрос.УстановитьПараметр("ДатаОкончания", 			 КонецМесяца(Период));
	Запрос.УстановитьПараметр("УчитыватьУпрОрганизацию", ПолучитьФункциональнуюОпцию("ИспользоватьУправленческуюОрганизацию"));
	Запрос.УстановитьПараметр("МассивОрганизаций",  	 ОбщегоНазначенияУТКлиентСервер.Массив(Организация));
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", 	 НЕ ЗначениеЗаполнено(Организация));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОшибкиДатЗапретаИзменения = "";
	ОшибкиПроверкиЗаполнения  = "";
	ОшибкиПроведения		  = "";
	
	Пока Выборка.Следующий() Цикл 
		
		Если ТолькоЕслиЕстьЗадания Тогда
			Состояние = ЗакрытиеМесяцаУТВызовСервера.СостояниеРаспределениеНДС(Выборка.Организация, Период);
			Если Состояние <> Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		
		НачатьТранзакцию();
		
		Попытка
		
			Если НЕ ЗначениеЗаполнено(Выборка.Документ) Тогда
				
				ДокументОбъект = Документы.РаспределениеНДС.СоздатьДокумент();
				ДокументОбъект.Дата 		 = КонецМесяца(Период);
				ДокументОбъект.Организация 	 = Выборка.Организация;
				ДокументОбъект.Ответственный = Пользователи.ТекущийПользователь();
				
				ПравилаСписанияНДС = Документы.РаспределениеНДС.ЗаполнитьПравилаСписанияНДС(Выборка.Организация);
				ЗаполнитьЗначенияСвойств(ДокументОбъект, ПравилаСписанияНДС);
				
			Иначе
				
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить("Документ.РаспределениеНДС");
				ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Документ);
				Блокировка.Заблокировать();
				
				ДокументОбъект = Выборка.Документ.ПолучитьОбъект();
				
			КонецЕсли;
		
			// Проверим, что документ находится в доступном для изменения периоде.
			ОписаниеОшибкиДаты = "";
			
			Если ДатыЗапретаИзменения.ИзменениеЗапрещено(ДокументОбъект, , ОписаниеОшибкиДаты) Тогда
				
				ОшибкиДатЗапретаИзменения = ОшибкиДатЗапретаИзменения + ?(ОшибкиДатЗапретаИзменения = "", "", "
					|") + ПротоколРасчетаПартийИСебестоимости.ПредставлениеМногострочногоТекста(ОписаниеОшибкиДаты);
					
				ОтменитьТранзакцию();
				
				Продолжить;
				
			КонецЕсли;
			
			// Заполним дополнительные свойства документа.
			ДокументОбъект.ДополнительныеСвойства.Вставить("РегламентныеОперации");
			
			УниверсальныеМеханизмыПартийИСебестоимости.УстановитьПризнакВыполненияРасчетаПартийИСебестоимости(ДокументОбъект);
			
			Если МенеджерВТКэшейРегистров <> Неопределено Тогда
				// Вызвано из партионного учета версии 2.2.
				ДокументОбъект.ДополнительныеСвойства.Вставить("МенеджерВТКэшейРегистров", МенеджерВТКэшейРегистров);
				ДокументОбъект.ДополнительныеСвойства.Вставить("ПериодРасчетаПартий", 	   Период);
			КонецЕсли;
			
			Если НЕ ДокументОбъект.БазаРаспределенияУстанавливаетсяВручную Тогда
				
				Результат = Документы.РаспределениеНДС.ПолучитьБазуРаспределения(ДокументОбъект.Дата, ДокументОбъект.Организация);
				
				ЗаполнитьЗначенияСвойств(
					ДокументОбъект, 
					Результат, 
					"ВыручкаНДС, ВыручкаНеНДС, ВыручкаЕНВД, ВыручкаНДС0, ВыручкаНДС0НесырьевыеТовары, ВыручкаНДС0СырьевыеТоварыУслуги");
					
				ДокументОбъект.ДокументыЭкспорт.Загрузить(Результат.ДокументыЭкспорт);
				
			КонецЕсли;
			
			Если ДокументОбъект.ЭтоНовый() Тогда
				// Регламентный документ должен остаться в ИБ независимо от того, удастся его провести или нет.
				ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
			КонецЕсли;
			
			// Проверим корректность заполнения документа.
			СообщенияПроверки = УниверсальныеМеханизмыПартийИСебестоимости.ПроверитьЗаполнениеОбъектаСПерехватомСообщений(ДокументОбъект);
			
			Если ЗначениеЗаполнено(СообщенияПроверки) Тогда
				// Проверка заполнения документа обнаружила ошибки.
				ОшибкиПроверкиЗаполнения = ОшибкиПроверкиЗаполнения	+ ?(ОшибкиПроверкиЗаполнения = "", "", "
					|") + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'При проверке документа ""%1"" были обнаружены ошибки:
								|%2'"),
							СокрЛП(ДокументОбъект),
							ПротоколРасчетаПартийИСебестоимости.ПредставлениеМногострочногоТекста(СообщенияПроверки, " - "));
			Иначе
				ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
				РезультатРаспределенияНДС.РассчитанныеОрганизации.Добавить(Выборка.Организация); // распределение выполнено успешно
			КонецЕсли;
			
			РезультатРаспределенияНДС.МассивДокументов.Добавить(ДокументОбъект.Ссылка);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ОшибкиПроведения = ОшибкиПроведения + ?(ОшибкиПроведения = "", "", "
				|") + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			
		КонецПопытки;
		
	КонецЦикла;
	
	// Обработаем информацию об ошибках.
	Если ЗначениеЗаполнено(ОшибкиДатЗапретаИзменения)
	 ИЛИ ЗначениеЗаполнено(ОшибкиПроверкиЗаполнения)
	 ИЛИ ЗначениеЗаполнено(ОшибкиПроведения) Тогда
		
		РезультатРаспределенияНДС.ТекстОшибки = ОшибкиДатЗапретаИзменения;
		
		РезультатРаспределенияНДС.ТекстОшибки =
			?(РезультатРаспределенияНДС.ТекстОшибки = "", "", РезультатРаспределенияНДС.ТекстОшибки + "
				|") + ОшибкиПроверкиЗаполнения;
		
		РезультатРаспределенияНДС.ТекстОшибки =
			?(РезультатРаспределенияНДС.ТекстОшибки = "", "", РезультатРаспределенияНДС.ТекстОшибки + "
				|") + ОшибкиПроведения;
		
		ТекстДляЖурнала = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось выполнить распределение НДС по причине:
				|%1'"),
			РезультатРаспределенияНДС.ТекстОшибки);
		
		ЗаписьЖурналаРегистрации(
			ИмяСобытияЖР,
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Документы.РаспределениеНДС,
			,
			ТекстДляЖурнала);
		
 	КонецЕсли;
	
	Возврат РезультатРаспределенияНДС;
	
КонецФункции

Процедура СформироватьЗадание(ПараметрыЗадания) Экспорт
	
	Запись = РегистрыСведений.ЗаданияКЗакрытиюМесяца.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(Запись, ПараметрыЗадания);
	Запись.Операция = Перечисления.ОперацииЗакрытияМесяца.РаспределениеНДС;
	Запись.НомерЗадания = Константы.НомерЗаданияКЗакрытиюМесяца.Получить();
	Запись.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	Если Регистры <> Неопределено Тогда
		ПроведениеСерверУТ.ЗапретВыборочногоПроведенияПоРегистрам(ДокументСсылка);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка							КАК Ссылка,
	|	ДанныеДокумента.Дата							КАК Период,
	|	КОНЕЦПЕРИОДА(ДанныеДокумента.Дата, МЕСЯЦ)		КАК ПериодБазы,
	|	ДанныеДокумента.Организация						КАК Организация,
	|	ДанныеДокумента.ВыручкаНДС						КАК ВыручкаНДС,
	|	ДанныеДокумента.ВыручкаНеНДС					КАК ВыручкаНеНДС,
	|	ДанныеДокумента.ВыручкаЕНВД						КАК ВыручкаЕНВД,
	|	ДанныеДокумента.ВыручкаНДС0						КАК ВыручкаНДС0,
	|	ДанныеДокумента.ВыручкаНДС0СырьевыеТоварыУслуги	КАК ВыручкаНДС0СырьевыеТоварыУслуги,
	|	ДанныеДокумента.ВыручкаНДС0НесырьевыеТовары		КАК ВыручкаНДС0НесырьевыеТовары,
	|	ДанныеДокумента.СтатьяРасходовНеНДС				КАК СтатьяРасходовНеНДС,
	|	ДанныеДокумента.АналитикаРасходовНеНДС			КАК АналитикаРасходовНеНДС,
	|	ДанныеДокумента.СтатьяРасходовЕНВД				КАК СтатьяРасходовЕНВД,
	|	ДанныеДокумента.АналитикаРасходовЕНВД			КАК АналитикаРасходовЕНВД,
	|	ДанныеДокумента.СписатьНДСПоРасходамАктивамНаСтатьиОтражения КАК СписатьНДСПоРасходамАктивамНаСтатьиОтражения,
	|	ДанныеДокумента.ПрименитьПравило5Процентов		КАК ПрименитьПравило5Процентов
	|ИЗ
	|	Документ.РаспределениеНДС КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	СформироватьДвижения(Реквизиты, ДополнительныеСвойства);
	
КонецПроцедуры

Функция ДанныеДляРаспределения(Реквизиты, ДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	
	ПериодНачало    = НачалоКвартала(Реквизиты.Период);
	ПериодОкончание = КонецКвартала(Реквизиты.Период);
	
	//++ НЕ УТ
	Если Месяц(Реквизиты.Период)%3 <> 0 Тогда
		ПериодНачало    = НачалоМесяца(Реквизиты.Период);
		ПериодОкончание = КонецМесяца(Реквизиты.Период);
	КонецЕсли;
	Запрос.УстановитьПараметр("ВыборкаПоКварталу", (Месяц(Реквизиты.Период)%3=0));
	//-- НЕ УТ
	Запрос.УстановитьПараметр("Период",             Реквизиты.Период);
	Запрос.УстановитьПараметр("ПериодБазы",         Реквизиты.ПериодБазы);
	Запрос.УстановитьПараметр("ПериодНачало",       ПериодНачало);
	Запрос.УстановитьПараметр("ПериодОкончание",    ПериодОкончание);
	Запрос.УстановитьПараметр("Организация",        Реквизиты.Организация);
	Запрос.УстановитьПараметр("Граница",            Новый Граница(ПериодОкончание, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ДокументСсылка",     Реквизиты.Ссылка);
	
	Если ДополнительныеСвойства.Свойство("МенеджерВТКэшейРегистров") Тогда
		
		// Вызвано из партионного учета версии 2.2.
		// Необходимо скорректировать остатки на конец периода:
		// вычесть из них "расчетные" движения, хранящиеся в ИБ (результаты прошлого расчета партий)
		// и добавить "расчетные" движения, хранящиеся во временной таблице (результаты выполняющего сейчас расчета партий).
		
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВТКэшейРегистров; // содержит ВТКэшПартииПрочихРасходов
		
		// Период расчета партий (месяц) может отличаться от периода выборки данных для распределения (месяц или квартал).
		Запрос.УстановитьПараметр("НачалоПериодаРасчетаПартий", НачалоМесяца(ДополнительныеСвойства.ПериодРасчетаПартий));
		Запрос.УстановитьПараметр("КонецПериодаРасчетаПартий",  КонецМесяца(ДополнительныеСвойства.ПериодРасчетаПартий));
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВЫБОР КОГДА Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		ТОГДА -1
		|		ИНАЧЕ 1
		|	КОНЕЦ 						  КАК Знак,
		|	Т.Организация                 КАК Организация,
		|	Т.Подразделение               КАК Подразделение,
		|	Т.СтатьяРасходов              КАК СтатьяРасходов,
		|	Т.АналитикаРасходов           КАК АналитикаРасходов,
		|	Т.АналитикаАктивовПассивов    КАК АналитикаАктивовПассивов,
		|	Т.ДокументПоступленияРасходов КАК ДокументПоступленияРасходов,
		|	Т.АналитикаУчетаПартий        КАК АналитикаУчетаПартий,
		|	Т.НаправлениеДеятельности     КАК НаправлениеДеятельности,
		|	Т.АналитикаУчетаНоменклатуры  КАК АналитикаУчетаНоменклатуры,
		|	Т.ВидДеятельностиНДС          КАК ВидДеятельностиНДС,
		|	Т.СтоимостьРегл			  	  КАК СтоимостьРегл,
		|	Т.НДСРегл			  	  	  КАК НДСРегл
		|ПОМЕСТИТЬ ВТКорректировкаОстатковПартииПрочихРасходов
		|ИЗ
		|	РегистрНакопления.ПартииПрочихРасходов КАК Т
		|ГДЕ
		|	Т.Период МЕЖДУ &НачалоПериодаРасчетаПартий И &КонецПериодаРасчетаПартий
		|	И Т.Организация = &Организация
		|	И Т.Активность
		|	И Т.РасчетПартий
		|	И Т.Регистратор <> &ДокументСсылка
		|	И (Т.СтоимостьРегл <> 0 
		|		ИЛИ Т.НДСРегл <> 0)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВЫБОР КОГДА Т.СлужебноеВидДвиженияПриход
		|		ТОГДА 1
		|		ИНАЧЕ -1
		|	КОНЕЦ 						  КАК Знак,
		|	Т.Организация                 КАК Организация,
		|	Т.Подразделение               КАК Подразделение,
		|	Т.СтатьяРасходов              КАК СтатьяРасходов,
		|	Т.АналитикаРасходов           КАК АналитикаРасходов,
		|	Т.АналитикаАктивовПассивов    КАК АналитикаАктивовПассивов,
		|	Т.ДокументПоступленияРасходов КАК ДокументПоступленияРасходов,
		|	Т.АналитикаУчетаПартий        КАК АналитикаУчетаПартий,
		|	Т.НаправлениеДеятельности     КАК НаправлениеДеятельности,
		|	Т.АналитикаУчетаНоменклатуры  КАК АналитикаУчетаНоменклатуры,
		|	Т.ВидДеятельностиНДС          КАК ВидДеятельностиНДС,
		|	Т.СтоимостьРегл			  	  КАК СтоимостьРегл,
		|	Т.НДСРегл			  	  	  КАК НДСРегл
		|ИЗ
		|	ВТКэшПартииПрочихРасходов КАК Т
		|ГДЕ
		|	Т.Период МЕЖДУ &НачалоПериодаРасчетаПартий И &КонецПериодаРасчетаПартий
		|	И Т.Организация = &Организация
		|	И Т.РасчетПартий
		|	И Т.Регистратор <> &ДокументСсылка
		|	И (Т.СтоимостьРегл <> 0 
		|		ИЛИ Т.НДСРегл <> 0)";
		
	Иначе
		
		// Вызвано не из партионного учета версии 2.2.
		// Корректировать остатки на конец периода не надо - "расчетные" движения хранятся только в ИБ.
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 0
		|	0							  КАК Знак,
		|	Т.Организация                 КАК Организация,
		|	Т.Подразделение               КАК Подразделение,
		|	Т.СтатьяРасходов              КАК СтатьяРасходов,
		|	Т.АналитикаРасходов           КАК АналитикаРасходов,
		|	Т.АналитикаАктивовПассивов    КАК АналитикаАктивовПассивов,
		|	Т.ДокументПоступленияРасходов КАК ДокументПоступленияРасходов,
		|	Т.АналитикаУчетаПартий        КАК АналитикаУчетаПартий,
		|	Т.НаправлениеДеятельности     КАК НаправлениеДеятельности,
		|	Т.АналитикаУчетаНоменклатуры  КАК АналитикаУчетаНоменклатуры,
		|	Т.ВидДеятельностиНДС          КАК ВидДеятельностиНДС,
		|	Т.СтоимостьРегл			  	  КАК СтоимостьРегл,
		|	Т.НДСРегл			  	  	  КАК НДСРегл
		|ПОМЕСТИТЬ ВТКорректировкаОстатковПартииПрочихРасходов
		|ИЗ
		|	РегистрНакопления.ПартииПрочихРасходов КАК Т";
		
	КонецЕсли;
	
	Запрос.Выполнить(); // формируется ВТКорректировкаОстатковПартииПрочихРасходов
	
	Запрос.Текст = 
	"
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КОНЕЦПЕРИОДА(ЕСТЬNULL(ДанныеОснованияКорректировки.ДатаРегистратора, &ПериодБазы), КВАРТАЛ)  КАК ПериодБазы,
	|	КорректировкаПоступления.ДокументОснование  КАК ОснованиеКорректировки,
	|	ПартииПрочихРасходов.Организация  КАК Организация,
	|	ПартииПрочихРасходов.Подразделение  КАК Подразделение,
	|	ПартииПрочихРасходов.СтатьяРасходов  КАК СтатьяРасходов,
	|	ПартииПрочихРасходов.АналитикаРасходов  КАК АналитикаРасходов,
	|	ПартииПрочихРасходов.АналитикаАктивовПассивов  КАК АналитикаАктивовПассивов,
	|	ПартииПрочихРасходов.ДокументПоступленияРасходов  КАК ДокументПоступленияРасходов,
	|	ПартииПрочихРасходов.АналитикаУчетаПартий  КАК АналитикаУчетаПартий,
	|	ПартииПрочихРасходов.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	ПартииПрочихРасходов.АналитикаУчетаНоменклатуры  КАК АналитикаУчетаНоменклатуры,
	|	ПартииПрочихРасходов.ВидДеятельностиНДС  КАК ВидДеятельностиНДС
	|ПОМЕСТИТЬ ПартииПрочихРасходовКорректировка
	|ИЗ
	|	РегистрНакопления.ПартииПрочихРасходов КАК ПартииПрочихРасходов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.КорректировкаПоступления КАК КорректировкаПоступления
	|	ПО
	|		ПартииПрочихРасходов.Регистратор = КорректировкаПоступления.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеОснованияКорректировки
	|	ПО
	|		КорректировкаПоступления.Организация = ДанныеОснованияКорректировки.Организация
	|		И КорректировкаПоступления.ДокументОснование = ДанныеОснованияКорректировки.Документ
	|ГДЕ
	|	ПартииПрочихРасходов.Период МЕЖДУ &ПериодНачало И &ПериодОкончание 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументПоступленияРасходов
	|;
	|
	//++ НЕ УТ
	|ВЫБРАТЬ
	|	СостоянияОС.ОсновноеСредство КАК ОсновноеСредство,
	|	СостоянияОС.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВтРегистраторыПринятияКУчетуОС
	|ИЗ
	|	РегистрСведений.СостоянияОСОрганизаций.СрезПоследних(
	|			&Граница,
	|			Организация = &Организация
	|				И Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|				И (Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС
	|					ИЛИ Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов)) КАК СостоянияОС
	|ГДЕ
	|	СостоянияОС.ДатаСостояния >= НАЧАЛОПЕРИОДА(&ПериодОкончание, МЕСЯЦ)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	СобытияОС.ОсновноеСредство,
	|	СостоянияОС.Регистратор
	|ИЗ
	|	РегистрСведений.СобытияОСОрганизаций.СрезПоследних(
	|			&Граница,
	|			Организация = &Организация
	|				И Событие.ВидСобытияОС В (ЗНАЧЕНИЕ(Перечисление.ВидыСобытийОС.Модернизация), 
	|										  ЗНАЧЕНИЕ(Перечисление.ВидыСобытийОС.Достройка), 
	|										  ЗНАЧЕНИЕ(Перечисление.ВидыСобытийОС.Дооборудование))) КАК СобытияОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияОСОрганизаций.СрезПоследних(
	|				&Граница,
	|				Организация = &Организация
	|					И Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|					И (Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС
	|						ИЛИ Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов)) КАК СостоянияОС
	|		ПО СобытияОС.ОсновноеСредство = СостоянияОС.ОсновноеСредство
	|ГДЕ
	|	СобытияОС.Период >= НАЧАЛОПЕРИОДА(&ПериодОкончание, МЕСЯЦ)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|/////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВводОстатковОС.ДатаПринятияКУчету КАК ДатаПринятияКУчету,
	|	""ОС"" КАК ВнеоборотныйАктив,
	|	ОСПринятыеКУчету.ОсновноеСредство КАК Объект,
	|	ВводОстатковОС.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ВЫБОР
	|		КОГДА ВводОстатковОС.ВариантРаздельногоУчетаНДС = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.Распределение)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК РаспределятьНДС,
	|	ЛОЖЬ КАК ВключаетсяВРасходыНУПриПринятииКУчету,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеРасходовНУ,
	|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходовНУ,
	|	ЛОЖЬ КАК ЭтоПринимаемаяСтатьяРасходовНУ,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходовНУ
	|ПОМЕСТИТЬ ВтВнеобАктивы
	|ИЗ
	|	ВтРегистраторыПринятияКУчетуОС КАК ОСПринятыеКУчету
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ВводОстатковВнеоборотныхАктивов.ОС КАК ВводОстатковОС
	|	ПО
	|		ОСПринятыеКУчету.Регистратор = ВводОстатковОС.Ссылка
	|		И ОСПринятыеКУчету.ОсновноеСредство = ВводОстатковОС.ОсновноеСредство
	|ГДЕ
	|	ВводОстатковОС.ВариантРаздельногоУчетаНДС <> ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПринятиеКУчетуОС.Дата КАК ДатаПринятияКУчету,
	|	""ОС"" КАК ВнеоборотныйАктив,
	|	ОСПринятыеКУчету.ОсновноеСредство КАК Объект,
	|	ПринятиеКУчетуОС.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ВЫБОР
	|		КОГДА ПринятиеКУчетуОС.ВариантРаздельногоУчетаНДС = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.Распределение)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК РаспределятьНДС,
	|	ВЫБОР
	|		КОГДА ПринятиеКУчетуОС.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.ВключениеВРасходыПриПринятииКУчету)
	|				И КОНЕЦПЕРИОДА(ПринятиеКУчетуОС.Дата, МЕСЯЦ) = &ПериодОкончание
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ 
	|	КОНЕЦ КАК ВключаетсяВРасходыНУПриПринятииКУчету,
	|	ПринятиеКУчетуОС.Местонахождение КАК ПодразделениеРасходовНУ,
	|	ПринятиеКУчетуОС.СтатьяРасходов КАК СтатьяРасходовНУ,
	|	ПринятиеКУчетуОС.СтатьяРасходов.ПринятиеКналоговомуУчету КАК ЭтоПринимаемаяСтатьяРасходовНУ,
	|	ПринятиеКУчетуОС.АналитикаРасходов КАК АналитикаРасходовНУ
	|ИЗ
	|	ВтРегистраторыПринятияКУчетуОС КАК ОСПринятыеКУчету
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПринятиеКУчетуОС КАК ПринятиеКУчетуОС
	|	ПО
	|		ОСПринятыеКУчету.Регистратор = ПринятиеКУчетуОС.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(НМАПринятыеКУчету.Регистратор КАК Документ.ПринятиеКУчетуНМА).Дата КАК ДатаПринятияКУчету,
	|	""НМА"" КАК ВнеоборотныйАктив,
	|	НМАПринятыеКУчету.НематериальныйАктив КАК АналитикаРасходов,
	|	ВЫРАЗИТЬ(НМАПринятыеКУчету.Регистратор КАК Документ.ПринятиеКУчетуНМА).НалогообложениеНДС КАК НалогообложениеНДС,
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(НМАПринятыеКУчету.Регистратор КАК Документ.ПринятиеКУчетуНМА).ВариантРаздельногоУчетаНДС = ЗНАЧЕНИЕ(Перечисление.ВариантыРаздельногоУчетаНДС.Распределение)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК РаспределятьНДС,
	|	ВЫБОР 
	|		КОГДА ВЫРАЗИТЬ(НМАПринятыеКУчету.Регистратор КАК Документ.ПринятиеКУчетуНМА).ПорядокСписанияНИОКРНаРасходыНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокСписанияНИОКРНУ.ПриПринятииКУчету)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ ВключаетсяВРасходыНУПриПринятииКУчету,
	|	ВЫРАЗИТЬ(НМАПринятыеКУчету.Регистратор КАК Документ.ПринятиеКУчетуНМА).Подразделение КАК ПодразделениеРасходовНУ,
	|   ВЫРАЗИТЬ(НМАПринятыеКУчету.Регистратор КАК Документ.ПринятиеКУчетуНМА).СтатьяРасходов КАК СтатьяРасходовНУ,
	|   ВЫРАЗИТЬ(НМАПринятыеКУчету.Регистратор КАК Документ.ПринятиеКУчетуНМА).СтатьяРасходов.ПринятиеКналоговомуУчету КАК ЭтоПринимаемаяСтатьяРасходовНУ,
	|	ВЫРАЗИТЬ(НМАПринятыеКУчету.Регистратор КАК Документ.ПринятиеКУчетуНМА).АналитикаРасходов КАК АналитикаРасходовНУ
	|ИЗ
	|	РегистрСведений.СостоянияНМАОрганизаций.СрезПоследних(
	|			&Граница,
	|			Организация = &Организация
	|				И Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.ПринятКУчету)
	|				И Регистратор ССЫЛКА Документ.ПринятиеКУчетуНМА) КАК НМАПринятыеКУчету
	|	
	|;
	|////////////////////////////////////////////////////////////////////
	//-- НЕ УТ
	|ВЫБРАТЬ
	|	Т.Организация                   КАК Организация,
	|	Т.Подразделение                 КАК Подразделение,
	|	Т.СтатьяРасходов                КАК СтатьяРасходов,
	|	Т.АналитикаРасходов             КАК АналитикаРасходов,
	|	Т.АналитикаАктивовПассивов      КАК АналитикаАктивовПассивов,
	|	Т.ДокументПоступленияРасходов   КАК ДокументПоступленияРасходов,
	|	Т.АналитикаУчетаПартий          КАК АналитикаУчетаПартий,
	|	Т.НаправлениеДеятельности       КАК НаправлениеДеятельности,
	|	Т.АналитикаУчетаНоменклатуры    КАК АналитикаУчетаНоменклатуры,
	|	Т.ВидДеятельностиНДС            КАК ВидДеятельностиНДС,
	|	СУММА(Т.Знак * Т.СтоимостьРегл) КАК СтоимостьРегл,
	|	СУММА(Т.Знак * Т.НДСРегл)       КАК НДСРегл
	|ПОМЕСТИТЬ ПартииПрочихРасходовОстатки
	|ИЗ
	|	(ВЫБРАТЬ
	|		1 							  КАК Знак,
	|		Т.Организация                 КАК Организация,
	|		Т.Подразделение               КАК Подразделение,
	|		Т.СтатьяРасходов              КАК СтатьяРасходов,
	|		Т.АналитикаРасходов           КАК АналитикаРасходов,
	|		Т.АналитикаАктивовПассивов    КАК АналитикаАктивовПассивов,
	|		Т.ДокументПоступленияРасходов КАК ДокументПоступленияРасходов,
	|		Т.АналитикаУчетаПартий        КАК АналитикаУчетаПартий,
	|		Т.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|		Т.АналитикаУчетаНоменклатуры  КАК АналитикаУчетаНоменклатуры,
	|		Т.ВидДеятельностиНДС          КАК ВидДеятельностиНДС,
	|		Т.СтоимостьРеглОстаток        КАК СтоимостьРегл,
	|		Т.НДСРеглОстаток              КАК НДСРегл
	|	ИЗ
	|		РегистрНакопления.ПартииПрочихРасходов.Остатки(&Граница, Организация = &Организация) КАК Т
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		-1 							  КАК Знак,
	|		Т.Организация                 КАК Организация,
	|		Т.Подразделение               КАК Подразделение,
	|		Т.СтатьяРасходов              КАК СтатьяРасходов,
	|		Т.АналитикаРасходов           КАК АналитикаРасходов,
	|		Т.АналитикаАктивовПассивов    КАК АналитикаАктивовПассивов,
	|		Т.ДокументПоступленияРасходов КАК ДокументПоступленияРасходов,
	|		Т.АналитикаУчетаПартий        КАК АналитикаУчетаПартий,
	|		Т.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|		Т.АналитикаУчетаНоменклатуры  КАК АналитикаУчетаНоменклатуры,
	|		Т.ВидДеятельностиНДС          КАК ВидДеятельностиНДС,
	|		Т.СтоимостьРеглОборот		  КАК СтоимостьРегл,
	|		Т.НДСРеглОборот			  	  КАК НДСРегл
	|	ИЗ
	|		РегистрНакопления.ПартииПрочихРасходов.Обороты(&ПериодНачало, &ПериодОкончание,
	|			Регистратор, Организация = &Организация) КАК Т
	|	ГДЕ
	|		Т.Регистратор = &ДокументСсылка
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Т.Знак						  КАК Знак,
	|		Т.Организация                 КАК Организация,
	|		Т.Подразделение               КАК Подразделение,
	|		Т.СтатьяРасходов              КАК СтатьяРасходов,
	|		Т.АналитикаРасходов           КАК АналитикаРасходов,
	|		Т.АналитикаАктивовПассивов    КАК АналитикаАктивовПассивов,
	|		Т.ДокументПоступленияРасходов КАК ДокументПоступленияРасходов,
	|		Т.АналитикаУчетаПартий        КАК АналитикаУчетаПартий,
	|		Т.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|		Т.АналитикаУчетаНоменклатуры  КАК АналитикаУчетаНоменклатуры,
	|		Т.ВидДеятельностиНДС          КАК ВидДеятельностиНДС,
	|		Т.СтоимостьРегл		  		  КАК СтоимостьРегл,
	|		Т.НДСРегл			  	  	  КАК НДСРегл
	|	ИЗ
	|		ВТКорректировкаОстатковПартииПрочихРасходов КАК Т
	|	
	|	) КАК Т
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Организация,
	|	Т.Подразделение,
	|	Т.СтатьяРасходов,
	|	Т.АналитикаРасходов,
	|	Т.АналитикаАктивовПассивов,
	|	Т.ДокументПоступленияРасходов,
	|	Т.АналитикаУчетаПартий,
	|	Т.НаправлениеДеятельности,
	|	Т.АналитикаУчетаНоменклатуры,
	|	Т.ВидДеятельностиНДС
	|
	|ИМЕЮЩИЕ
	|	СУММА(Т.Знак * Т.СтоимостьРегл) <> 0 
	|	ИЛИ СУММА(Т.Знак * Т.НДСРегл) <> 0
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ДокументПоступленияРасходов
	|;
	|
	|////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаОстатков.Организация                                   КАК Организация,
	|	ТаблицаОстатков.Подразделение                                 КАК Подразделение,
	|	ТаблицаОстатков.НаправлениеДеятельности                       КАК НаправлениеДеятельности,
	|	ТаблицаОстатков.СтатьяРасходов                                КАК СтатьяРасходов,
	|	ТаблицаОстатков.АналитикаРасходов                             КАК АналитикаРасходов,
	|	ТаблицаОстатков.АналитикаАктивовПассивов                      КАК АналитикаАктивовПассивов,
	|	ТаблицаОстатков.ДокументПоступленияРасходов                   КАК ДокументПоступленияРасходов,
	|	ТаблицаОстатков.АналитикаУчетаПартий                          КАК АналитикаУчетаПартий,
	|	ТаблицаОстатков.АналитикаУчетаНоменклатуры                    КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаОстатков.ВидДеятельностиНДС                            КАК ВидДеятельностиНДС,
	|	АналитикаПартий.Контрагент                                    КАК Контрагент,
	|	ВЫБОР
	|		КОГДА ТаблицаОстатков.ВидДеятельностиНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|			ТОГДА ТаблицаОстатков.ВидДеятельностиНДС // Версия ПартионногоУчета 2.2
	|		ИНАЧЕ АналитикаПартий.НалогообложениеНДС     // Версия ПартионногоУчета 2.1             
	|	КОНЕЦ                                                         КАК НалогообложениеНДСПартии,
	|	АналитикаПартий.СтавкаНДС                                     КАК СтавкаНДС,
	|	
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.ДатаРегистратора, &Период) КАК ДатаПоступленияРасходов,
	|	
	|	СтатьиРасходов.ВариантРаспределенияРасходов                   КАК ВариантРаспределенияРасходовСтатьи,
	|	СтатьиРасходов.ВидЦенностиНДС                                 КАК ВидЦенностиНДССтатьиРасходов,
	|	СтатьиРасходов.ПринятиеКналоговомуУчету                       КАК ЭтоПринимаемаяСтатьяРасходовНУ,
	|	
	|	ВЫБОР
	|		КОГДА АналитикаПартий.ВидЦенности <> ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПустаяСсылка)
	|			ТОГДА АналитикаПартий.ВидЦенности        // Версия ПартионногоУчета 2.2
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаОстатков.ДокументПоступленияРасходов) = ТИП(Документ.ТаможеннаяДекларацияИмпорт)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТаможенныеПлатежи)
	|		КОГДА АналитикаНоменклатуры.Номенклатура.ТипНоменклатуры В (
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|			ТОГДА ЕСТЬNULL(ГруппыФинансовогоУчетаНоменклатуры.ВидЦенностиНДС, ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары))
	|		КОГДА НЕ СтатьиРасходов.ВидЦенностиНДС ЕСТЬ NULL
	|			ТОГДА СтатьиРасходов.ВидЦенностиНДС
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
	|	КОНЕЦ                                                      КАК ВидЦенностиНДС,
	|	
	|	ТаблицаОстатков.СтоимостьРегл                              КАК СтоимостьРегл,
	|	ТаблицаОстатков.НДСРегл                                    КАК НДСРегл
	|ПОМЕСТИТЬ ТаблицаОстатков
	|ИЗ
	|	ПартииПрочихРасходовОстатки КАК ТаблицаОстатков
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|	ПО
	|		ТаблицаОстатков.Организация =  ДанныеПервичныхДокументов.Организация
	|		И ТаблицаОстатков.ДокументПоступленияРасходов = ДанныеПервичныхДокументов.Документ
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Справочник.КлючиАналитикиУчетаПартий КАК АналитикаПартий
	|	ПО 
	|		ТаблицаОстатков.АналитикаУчетаПартий = АналитикаПартий.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаНоменклатуры
	|	ПО 
	|		ТаблицаОстатков.АналитикаУчетаНоменклатуры = АналитикаНоменклатуры.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ГруппыФинансовогоУчетаНоменклатуры КАК ГруппыФинансовогоУчетаНоменклатуры
	|	ПО
	|		АналитикаНоменклатуры.Номенклатура.ГруппаФинансовогоУчета = ГруппыФинансовогоУчетаНоменклатуры.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|	ПО
	|		ТаблицаОстатков.СтатьяРасходов = СтатьиРасходов.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументПоступленияРасходов
	|;
	|
	|////////////////////////////////////////////////////////////////////
	//++ НЕ УТ
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ПараметрыАмортизацииОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ПараметрыАмортизацииОС.Организация КАК Организация,
	|	ПараметрыАмортизацииОС.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	ПараметрыАмортизацииОС.ОбъемПродукцииРабот КАК ОбъемПродукцииРабот,
	|	ПараметрыАмортизацииОС.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользованияДляВычисленияАмортизации,
	|	ПараметрыАмортизацииОС.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|	ПараметрыАмортизацииОС.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ПараметрыАмортизацииОС.КоэффициентАмортизации КАК КоэффициентАмортизации,
	|	ПараметрыАмортизацииОС.КоэффициентУскорения КАК КоэффициентУскорения,
	|	ЛОЖЬ КАК ЕстьИзменения
	|ИЗ
	|	РегистрСведений.ПараметрыАмортизацииОСБухгалтерскийУчет.СрезПоследних(
	|			&Граница,
	|			Регистратор <> &ДокументСсылка
	|			И Организация = &Организация
	|			И ОсновноеСредство В (ВЫБРАТЬ Т.Объект ИЗ ВтВнеобАктивы КАК Т)
	|	) КАК ПараметрыАмортизацииОС
	|;
	|
	|////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ПервоначальныеСведенияНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	ПервоначальныеСведенияНМА.Организация КАК Организация,
	|	ПервоначальныеСведенияНМА.НачислятьАмортизацию КАК НачислятьАмортизацию,
	|	ПервоначальныеСведенияНМА.СпособНачисленияАмортизации КАК СпособНачисленияАмортизации,
	|	ПервоначальныеСведенияНМА.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	ПервоначальныеСведенияНМА.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
	|	ПервоначальныеСведенияНМА.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ПервоначальныеСведенияНМА.Коэффициент КАК Коэффициент,
	|	ЛОЖЬ КАК ЕстьИзменения
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияНМАБухгалтерскийУчет.СрезПоследних(
	|			&Граница,
	|			Регистратор <> &ДокументСсылка
	|			И Организация = &Организация
	|			И НематериальныйАктив В (ВЫБРАТЬ Т.Объект ИЗ ВтВнеобАктивы КАК Т)
	|	) КАК ПервоначальныеСведенияНМА
	|;
	|
	|////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	ПервоначальныеСведенияОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ПервоначальныеСведенияОС.Организация КАК Организация,
	|	ПервоначальныеСведенияОС.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ПервоначальныеСведенияОС.СпособПоступления КАК СпособПоступления,
	|	ПервоначальныеСведенияОС.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость,
	|	ПервоначальныеСведенияОС.СпособНачисленияАмортизации КАК СпособНачисленияАмортизации,
	|	ПервоначальныеСведенияОС.ПоказательНаработки КАК ПоказательНаработки,
	|	ПервоначальныеСведенияОС.ПорядокПогашенияСтоимости КАК ПорядокПогашенияСтоимости,
	|	ПервоначальныеСведенияОС.ГруппаОС КАК ГруппаОС,
	|	ПервоначальныеСведенияОС.КодПоОКОФ КАК КодПоОКОФ,
	|	ПервоначальныеСведенияОС.ШифрПоЕНАОФ КАК ШифрПоЕНАОФ,
	|	ПервоначальныеСведенияОС.АмортизационнаяГруппа КАК АмортизационнаяГруппа,
	|	ПервоначальныеСведенияОС.ДоговорЛизинга КАК ДоговорЛизинга,
	|	ЛОЖЬ КАК ЕстьИзменения
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(
	|			&Граница,
	|			Регистратор <> &ДокументСсылка
	|				И Организация = &Организация
	|				И ОсновноеСредство В
	|					(ВЫБРАТЬ
	|						Т.Объект
	|					ИЗ
	|						ВтВнеобАктивы КАК Т)) КАК ПервоначальныеСведенияОС
	|;
	|////////////////////////////////////////////////////////////////////
	//-- НЕ УТ
	|ВЫБРАТЬ
	|	ТаблицаОстатков.Организация                      КАК Организация,
	|	ТаблицаОстатков.Подразделение                    КАК Подразделение,
	|	ТаблицаОстатков.НаправлениеДеятельности          КАК НаправлениеДеятельности,
	|	ТаблицаОстатков.СтатьяРасходов                   КАК СтатьяРасходов,
	|	ТаблицаОстатков.АналитикаРасходов                КАК АналитикаРасходов,
	|	ТаблицаОстатков.АналитикаАктивовПассивов         КАК АналитикаАктивовПассивов,
	|	ТаблицаОстатков.ДокументПоступленияРасходов      КАК ДокументПоступленияРасходов,
	|	ТаблицаОстатков.АналитикаУчетаПартий             КАК АналитикаУчетаПартий,
	|	ТаблицаОстатков.АналитикаУчетаНоменклатуры       КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаОстатков.ВидДеятельностиНДС               КАК ВидДеятельностиНДС,                                            
	|	
	|	ТаблицаОстатков.Контрагент                       КАК Контрагент,
	|	ТаблицаОстатков.НалогообложениеНДСПартии         КАК НалогообложениеНДСПартии,
	|	ТаблицаОстатков.СтавкаНДС                        КАК СтавкаНДС,
	|	
	|	ТаблицаОстатков.ДатаПоступленияРасходов          КАК ДатаПоступленияРасходов,
	|	
	|	ИСТИНА                                           КАК РаспределятьНДС,
	|
	|	ЕСТЬNULL(ПартииПрочихРасходовКорректировка.ПериодБазы, &ПериодБазы) КАК ПериодБазы,
	|	ЕСТЬNULL(ПартииПрочихРасходовКорректировка.ОснованиеКорректировки, НЕОПРЕДЕЛЕНО) КАК ОснованиеКорректировки,
	|
	|	НЕОПРЕДЕЛЕНО                                     КАК НовоеНалогообложениеНДС,
	|	
	|	ВЫБОР КОГДА ТаблицаОстатков.ВидЦенностиНДССтатьиРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ОбъектыНезавершенногоСтроительства)
	|		ТОГДА ""ОбъектСтроительства""
	|	ИНАЧЕ """"
	|	КОНЕЦ                                 			 КАК ВнеоборотныйАктив,
	|	ЛОЖЬ                                             КАК ВключаетсяВРасходыНУПриПринятииКУчету,
	|	НЕОПРЕДЕЛЕНО                                     КАК ПодразделениеРасходовНУ,
	|	НЕОПРЕДЕЛЕНО                                     КАК СтатьяРасходовНУ,
	|	ТаблицаОстатков.ЭтоПринимаемаяСтатьяРасходовНУ   КАК ЭтоПринимаемаяСтатьяРасходовНУ,
	|	НЕОПРЕДЕЛЕНО                                     КАК АналитикаРасходовНУ,
	|	
	|	ТаблицаОстатков.ВидЦенностиНДС                   КАК ВидЦенностиНДС,
	|	
	|	ТаблицаОстатков.СтоимостьРегл                    КАК СтоимостьРегл,
	|	ТаблицаОстатков.НДСРегл                          КАК НДСРегл
	|ИЗ
	|	ТаблицаОстатков КАК ТаблицаОстатков
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПартииПрочихРасходовКорректировка КАК ПартииПрочихРасходовКорректировка
	|	ПО
	|		ТаблицаОстатков.ДокументПоступленияРасходов = ПартииПрочихРасходовКорректировка.ДокументПоступленияРасходов
	|		И ТаблицаОстатков.Организация = ПартииПрочихРасходовКорректировка.Организация  
	|		И ТаблицаОстатков.Организация = ПартииПрочихРасходовКорректировка.Организация  
	|		И ТаблицаОстатков.Подразделение = ПартииПрочихРасходовКорректировка.Подразделение
	|		И ТаблицаОстатков.СтатьяРасходов = ПартииПрочихРасходовКорректировка.СтатьяРасходов
	|		И ТаблицаОстатков.АналитикаРасходов = ПартииПрочихРасходовКорректировка.АналитикаРасходов
	|		И ТаблицаОстатков.АналитикаАктивовПассивов = ПартииПрочихРасходовКорректировка.АналитикаАктивовПассивов
	|		И ТаблицаОстатков.АналитикаУчетаПартий = ПартииПрочихРасходовКорректировка.АналитикаУчетаПартий
	|		И ТаблицаОстатков.НаправлениеДеятельности = ПартииПрочихРасходовКорректировка.НаправлениеДеятельности
	|		И ТаблицаОстатков.АналитикаУчетаНоменклатуры = ПартииПрочихРасходовКорректировка.АналитикаУчетаНоменклатуры
	|		И ТаблицаОстатков.ВидДеятельностиНДС = ПартииПрочихРасходовКорректировка.ВидДеятельностиНДС
	|	
	|ГДЕ
	|	(ТаблицаОстатков.ВариантРаспределенияРасходовСтатьи В (
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности),
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты),
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаРасходыБудущихПериодов),
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы))
	|		ИЛИ ТаблицаОстатков.НалогообложениеНДСПартии = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением))
	//++ НЕ УТ
	|	И &ВыборкаПоКварталу
	|
	|ОБЪЕДИНИТЬ ВСЕ 
	|
	|ВЫБРАТЬ
	|	ТаблицаОстатков.Организация                      КАК Организация,
	|	ТаблицаОстатков.Подразделение                    КАК Подразделение,
	|	ТаблицаОстатков.НаправлениеДеятельности          КАК НаправлениеДеятельности,
	|	ТаблицаОстатков.СтатьяРасходов                   КАК СтатьяРасходов,
	|	ТаблицаОстатков.АналитикаРасходов                КАК АналитикаРасходов,
	|	ТаблицаОстатков.АналитикаАктивовПассивов         КАК АналитикаАктивовПассивов,
	|	ТаблицаОстатков.ДокументПоступленияРасходов      КАК ДокументПоступленияРасходов,
	|	ТаблицаОстатков.АналитикаУчетаПартий             КАК АналитикаУчетаПартий,
	|	ТаблицаОстатков.АналитикаУчетаНоменклатуры       КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаОстатков.ВидДеятельностиНДС               КАК ВидДеятельностиНДС,   
	|	
	|	ТаблицаОстатков.Контрагент                       КАК Контрагент,
	|	ТаблицаОстатков.НалогообложениеНДСПартии         КАК НалогообложениеНДСПартии,
	|	ТаблицаОстатков.СтавкаНДС                        КАК СтавкаНДС,
	|	
	|	ТаблицаОстатков.ДатаПоступленияРасходов          КАК ДатаПоступленияРасходов,
	|	
	|	ВнеобАктивы.РаспределятьНДС                      КАК РаспределятьНДС,
	|	
	|	ВЫБОР 
	|		КОГДА НЕ ПартииПрочихРасходовКорректировка.ДокументПоступленияРасходов ЕСТЬ NULL
	|			ТОГДА КОНЕЦПЕРИОДА(ВнеобАктивы.ДатаПринятияКУчету, МЕСЯЦ)
	|		ИНАЧЕ &ПериодБазы
	|	КОНЕЦ КАК ПериодБазы,
	|	ЕСТЬNULL(ПартииПрочихРасходовКорректировка.ОснованиеКорректировки, НЕОПРЕДЕЛЕНО) КАК ОснованиеКорректировки,
	|	
	|	ВЫБОР 
	|		КОГДА НЕ ВнеобАктивы.РаспределятьНДС
	|			ТОГДА ВнеобАктивы.НалогообложениеНДС
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ                                            КАК НовоеНалогообложениеНДС,
	|	ВнеобАктивы.ВнеоборотныйАктив                    КАК ВнеоборотныйАктив,
	|	
	|	ВнеобАктивы.ВключаетсяВРасходыНУПриПринятииКУчету КАК ВключаетсяВРасходыНУПриПринятииКУчету,
	|	ВнеобАктивы.ПодразделениеРасходовНУ               КАК ПодразделениеРасходовНУ,
	|	ВнеобАктивы.СтатьяРасходовНУ                      КАК СтатьяРасходовНУ,
	|	ВнеобАктивы.ЭтоПринимаемаяСтатьяРасходовНУ        КАК ЭтоПринимаемаяСтатьяРасходовНУ,
	|	ВнеобАктивы.АналитикаРасходовНУ                   КАК АналитикаРасходовНУ,
	|	
	|	ТаблицаОстатков.ВидЦенностиНДС                   КАК ВидЦенностиНДС,
	|	
	|	ТаблицаОстатков.СтоимостьРегл                    КАК СтоимостьРегл,
	|	ТаблицаОстатков.НДСРегл                          КАК НДСРегл
	|ИЗ
	|	ТаблицаОстатков КАК ТаблицаОстатков
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		ВтВнеобАктивы КАК ВнеобАктивы
	|	ПО 
	|		ТаблицаОстатков.АналитикаРасходов = ВнеобАктивы.Объект
	|		ИЛИ ТаблицаОстатков.АналитикаАктивовПассивов = ВнеобАктивы.Объект 
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПартииПрочихРасходовКорректировка КАК ПартииПрочихРасходовКорректировка
	|	ПО
	|		ТаблицаОстатков.ДокументПоступленияРасходов = ПартииПрочихРасходовКорректировка.ДокументПоступленияРасходов
	|		И ТаблицаОстатков.Организация = ПартииПрочихРасходовКорректировка.Организация  
	|		И ТаблицаОстатков.Подразделение = ПартииПрочихРасходовКорректировка.Подразделение
	|		И ТаблицаОстатков.СтатьяРасходов = ПартииПрочихРасходовКорректировка.СтатьяРасходов
	|		И ТаблицаОстатков.АналитикаРасходов = ПартииПрочихРасходовКорректировка.АналитикаРасходов
	|		И ТаблицаОстатков.АналитикаАктивовПассивов = ПартииПрочихРасходовКорректировка.АналитикаАктивовПассивов
	|		И ТаблицаОстатков.АналитикаУчетаПартий = ПартииПрочихРасходовКорректировка.АналитикаУчетаПартий
	|		И ТаблицаОстатков.НаправлениеДеятельности = ПартииПрочихРасходовКорректировка.НаправлениеДеятельности
	|		И ТаблицаОстатков.АналитикаУчетаНоменклатуры = ПартииПрочихРасходовКорректировка.АналитикаУчетаНоменклатуры
	|		И ТаблицаОстатков.ВидДеятельностиНДС = ПартииПрочихРасходовКорректировка.ВидДеятельностиНДС
	|	
	|ГДЕ
	|	(ТаблицаОстатков.ВариантРаспределенияРасходовСтатьи = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|		ИЛИ ТаблицаОстатков.НалогообложениеНДСПартии = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию))
	|	И ТаблицаОстатков.НалогообложениеНДСПартии <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|
	|ОБЪЕДИНИТЬ ВСЕ 
	|
	|ВЫБРАТЬ
	|	ТаблицаОстатков.Организация                      КАК Организация,
	|	ТаблицаОстатков.Подразделение                    КАК Подразделение,
	|	ТаблицаОстатков.НаправлениеДеятельности          КАК НаправлениеДеятельности,
	|	ТаблицаОстатков.СтатьяРасходов                   КАК СтатьяРасходов,
	|	ТаблицаОстатков.АналитикаРасходов                КАК АналитикаРасходов,
	|	ТаблицаОстатков.АналитикаАктивовПассивов         КАК АналитикаАктивовПассивов,
	|	ТаблицаОстатков.ДокументПоступленияРасходов      КАК ДокументПоступленияРасходов,
	|	ТаблицаОстатков.АналитикаУчетаПартий             КАК АналитикаУчетаПартий,
	|	ТаблицаОстатков.АналитикаУчетаНоменклатуры       КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаОстатков.ВидДеятельностиНДС               КАК ВидДеятельностиНДС,   
	|	
	|	ТаблицаОстатков.Контрагент                       КАК Контрагент,
	|	ТаблицаОстатков.НалогообложениеНДСПартии         КАК НалогообложениеНДСПартии,
	|	ТаблицаОстатков.СтавкаНДС                        КАК СтавкаНДС,
	|	
	|	ТаблицаОстатков.ДатаПоступленияРасходов          КАК ДатаПоступленияРасходов,
	|	
	|	ИСТИНА                                           КАК РаспределятьНДС,
	|	
	|	ЕСТЬNULL(ПартииПрочихРасходовКорректировка.ПериодБазы, &ПериодБазы) КАК ПериодБазы,
	|	ЕСТЬNULL(ПартииПрочихРасходовКорректировка.ОснованиеКорректировки, НЕОПРЕДЕЛЕНО) КАК ОснованиеКорректировки,
	|	
	|	НЕОПРЕДЕЛЕНО                                     КАК НовоеНалогообложениеНДС,
	|
	|	""ОбъектСтроительства""                          КАК ВнеоборотныйАктив,
	|	ЛОЖЬ                                             КАК ВключаетсяВРасходыНУПриПринятииКУчету,
	|	НЕОПРЕДЕЛЕНО                                     КАК ПодразделениеРасходовНУ,
	|	НЕОПРЕДЕЛЕНО                                     КАК СтатьяРасходовНУ,
	|	ТаблицаОстатков.ЭтоПринимаемаяСтатьяРасходовНУ   КАК ЭтоПринимаемаяСтатьяРасходовНУ,
	|	НЕОПРЕДЕЛЕНО                                     КАК АналитикаРасходовНУ,
	|
	|	ТаблицаОстатков.ВидЦенностиНДС                   КАК ВидЦенностиНДС,
	|	
	|	ТаблицаОстатков.СтоимостьРегл                    КАК СтоимостьРегл,
	|	ТаблицаОстатков.НДСРегл                          КАК НДСРегл
	|ИЗ
	|	ТаблицаОстатков КАК ТаблицаОстатков
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПартииПрочихРасходовКорректировка КАК ПартииПрочихРасходовКорректировка
	|	ПО
	|		ТаблицаОстатков.ДокументПоступленияРасходов = ПартииПрочихРасходовКорректировка.ДокументПоступленияРасходов
	|		И ТаблицаОстатков.Организация = ПартииПрочихРасходовКорректировка.Организация  
	|		И ТаблицаОстатков.Подразделение = ПартииПрочихРасходовКорректировка.Подразделение
	|		И ТаблицаОстатков.СтатьяРасходов = ПартииПрочихРасходовКорректировка.СтатьяРасходов
	|		И ТаблицаОстатков.АналитикаРасходов = ПартииПрочихРасходовКорректировка.АналитикаРасходов
	|		И ТаблицаОстатков.АналитикаАктивовПассивов = ПартииПрочихРасходовКорректировка.АналитикаАктивовПассивов
	|		И ТаблицаОстатков.АналитикаУчетаПартий = ПартииПрочихРасходовКорректировка.АналитикаУчетаПартий
	|		И ТаблицаОстатков.НаправлениеДеятельности = ПартииПрочихРасходовКорректировка.НаправлениеДеятельности
	|		И ТаблицаОстатков.АналитикаУчетаНоменклатуры = ПартииПрочихРасходовКорректировка.АналитикаУчетаНоменклатуры
	|		И ТаблицаОстатков.ВидДеятельностиНДС = ПартииПрочихРасходовКорректировка.ВидДеятельностиНДС
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		ВтВнеобАктивы КАК ВнеобАктивы
	|	ПО 
	|		ТаблицаОстатков.АналитикаРасходов = ВнеобАктивы.Объект
	|
	|ГДЕ
	|	ТаблицаОстатков.ВариантРаспределенияРасходовСтатьи = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|	И ТаблицаОстатков.ВидЦенностиНДССтатьиРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ОбъектыНезавершенногоСтроительства)
	|	И ТаблицаОстатков.НалогообложениеНДСПартии <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ОпределяетсяРаспределением)
	|	И ВнеобАктивы.Объект ЕСТЬ NULL
	|	И &ВыборкаПоКварталу
	//-- НЕ УТ
	|;
	|";
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	КоличествоЭлементов = РезультатыЗапроса.Количество();
	
	Результат = Новый Структура;
	
	//++ НЕ УТ
	Результат.Вставить("ПараметрыАмортизацииОС",     РезультатыЗапроса[КоличествоЭлементов-4].Выгрузить());
	Результат.Вставить("ПервоначальныеСведенияНМА",  РезультатыЗапроса[КоличествоЭлементов-3].Выгрузить());
	Результат.Вставить("ПервоначальныеСведенияОС",   РезультатыЗапроса[КоличествоЭлементов-2].Выгрузить());
	//-- НЕ УТ
	Результат.Вставить("ПартииПрочихРасходов",       РезультатыЗапроса[КоличествоЭлементов-1].Выгрузить());
	
	// Уничтожим все сформированные временные таблицы.
	// Если этого не сделать, то при вызове этого метода из партионного учета версии 2.2
	// возможно возникновение ошибки вида "Временная таблица уже существует".
	
	Запрос.Текст = "
	//++ НЕ УТ
	|УНИЧТОЖИТЬ ВтРегистраторыПринятияКУчетуОС
	|;
	|УНИЧТОЖИТЬ ВтВнеобАктивы
	|;
	//-- НЕ УТ
	|УНИЧТОЖИТЬ ВТКорректировкаОстатковПартииПрочихРасходов
	|;
	|УНИЧТОЖИТЬ ПартииПрочихРасходовОстатки
	|;
	|УНИЧТОЖИТЬ ТаблицаОстатков
	|;
	|УНИЧТОЖИТЬ ПартииПрочихРасходовКорректировка
	|";
	
	Запрос.Выполнить();
	
	Возврат Результат;
	
КонецФункции

Функция СформироватьДвижения(Реквизиты, ДополнительныеСвойства)
	
	ТаблицыДляДвижений     = ДополнительныеСвойства.ТаблицыДляДвижений;
	
	ДанныеДляРаспределения = ДанныеДляРаспределения(Реквизиты, ДополнительныеСвойства);
	
	РезультатРаспределения = РаспределитьПартииПрочихРасходовПоБазе(ДанныеДляРаспределения.ПартииПрочихРасходов, Реквизиты);
	
	ТаблицыДляДвижений.Вставить("ТаблицаПартииПрочихРасходов",  ТаблицаПартииПрочихРасходов(РезультатРаспределения, Реквизиты));
	
	//++ НЕ УТ
	ТаблицыДляДвижений.Вставить(
		"ТаблицаПрочиеРасходы", 
		ТаблицаПрочиеРасходы(РезультатРаспределения, Реквизиты));
		
	ТаблицыДляДвижений.Вставить(
		"ТаблицаПараметрыАмортизацииОСБухгалтерскийУчет", 
		ТаблицаПараметрыАмортизацииОС(РезультатРаспределения, ДанныеДляРаспределения.ПараметрыАмортизацииОС));
		
	ТаблицыДляДвижений.Вставить(
		"ТаблицаПервоначальныеСведенияОСБухгалтерскийУчет", 
		ТаблицаПервоначальныеСведенияОС(РезультатРаспределения, ДанныеДляРаспределения.ПервоначальныеСведенияОС));
		
	ТаблицыДляДвижений.Вставить(
		"ТаблицаПервоначальныеСведенияНМАБухгалтерскийУчет", 
		ТаблицаПервоначальныеСведенияНМА(РезультатРаспределения, ДанныеДляРаспределения.ПервоначальныеСведенияНМА));
	//-- НЕ УТ
	
КонецФункции

Функция РаспределитьПартииПрочихРасходовПоБазе(ПартииПрочихРасходов, Реквизиты)
	
	ТипыНалогообложения = Перечисления.ТипыНалогообложенияНДС;
	
	ПартииПрочихРасходовОблагаетсяНДС               = ТаблицаРезультатРаспределения();
	ПартииПрочихРасходовНеОблагаетсяНДС             = ТаблицаРезультатРаспределения();
	ПартииПрочихРасходовОблагаетсяЕНВД              = ТаблицаРезультатРаспределения();
	ПартииПрочихРасходовЭкспорт                     = ТаблицаРезультатРаспределения();
	ПартииПрочихРасходовЭкспортНесырьевыхТоваров    = ТаблицаРезультатРаспределения();
	ПартииПрочихРасходовЭкспортСырьевыхТоваровУслуг = ТаблицаРезультатРаспределения();
	
	
	ТаблицыРаспределения = Новый Соответствие;
	ТаблицыРаспределения.Вставить(ТипыНалогообложения.ПродажаОблагаетсяНДС,        ПартииПрочихРасходовОблагаетсяНДС);
	ТаблицыРаспределения.Вставить(ТипыНалогообложения.ПродажаНеОблагаетсяНДС,      ПартииПрочихРасходовНеОблагаетсяНДС);
	ТаблицыРаспределения.Вставить(ТипыНалогообложения.ПродажаОблагаетсяЕНВД,       ПартииПрочихРасходовОблагаетсяЕНВД);
	ТаблицыРаспределения.Вставить(ТипыНалогообложения.ПродажаНаЭкспорт,            ПартииПрочихРасходовЭкспорт);
	ТаблицыРаспределения.Вставить(ТипыНалогообложения.ЭкспортНесырьевыхТоваров,    ПартииПрочихРасходовЭкспортНесырьевыхТоваров);
	ТаблицыРаспределения.Вставить(ТипыНалогообложения.ЭкспортСырьевыхТоваровУслуг, ПартииПрочихРасходовЭкспортСырьевыхТоваровУслуг);
	
	БазаРаспределения = БазаРаспределения(ПартииПрочихРасходов, Реквизиты);
	
	Для каждого СтрокаПрочихРасходов Из ПартииПрочихРасходов Цикл
		
		УпрощенныйПорядокВычетаНДСПоНесырьевомуЭкспорту = УчетНДСУТ.УпрощенныйПорядокВычетаНДСПоНесырьевомуЭкспорту(СтрокаПрочихРасходов.ДатаПоступленияРасходов);
		
		Если СтрокаПрочихРасходов.РаспределятьНДС Тогда
			
			БазаПериода = БазаРаспределения.Найти(СтрокаПрочихРасходов.ПериодБазы, "ПериодБазы");
			
			БараРаспределенияПериода = Новый Соответствие;
			БараРаспределенияПериода.Вставить(ТипыНалогообложения.ПродажаОблагаетсяНДС,   БазаПериода.НДС);
			БараРаспределенияПериода.Вставить(ТипыНалогообложения.ПродажаНеОблагаетсяНДС, БазаПериода.НеНДС);
			БараРаспределенияПериода.Вставить(ТипыНалогообложения.ПродажаОблагаетсяЕНВД,  БазаПериода.ЕНВД);
			Если УпрощенныйПорядокВычетаНДСПоНесырьевомуЭкспорту Тогда
				// Партия поступила после 01.07.2016 - действует упрощенный порядок вычета НДС по несырьевому экспорт
				БараРаспределенияПериода.Вставить(ТипыНалогообложения.ПродажаНаЭкспорт, 0);
				БараРаспределенияПериода.Вставить(ТипыНалогообложения.ЭкспортНесырьевыхТоваров,    БазаПериода.НДС0НесырьевыеТовары);
				БараРаспределенияПериода.Вставить(ТипыНалогообложения.ЭкспортСырьевыхТоваровУслуг, БазаПериода.НДС0СырьевыеТоварыУслуги);
			Иначе
				// Партия поступила после 01.07.2016 - действует старый порядок вычета НДС по экспорту
				БараРаспределенияПериода.Вставить(ТипыНалогообложения.ПродажаНаЭкспорт, БазаПериода.НДС0);
				БараРаспределенияПериода.Вставить(ТипыНалогообложения.ЭкспортНесырьевыхТоваров,    0);
				БараРаспределенияПериода.Вставить(ТипыНалогообложения.ЭкспортСырьевыхТоваровУслуг, 0);
			КонецЕсли;
			
			Коэффициенты = Новый Массив;
			Для каждого КлючИЗначение Из БараРаспределенияПериода Цикл
				Коэффициенты.Добавить(КлючИЗначение.Значение);
			КонецЦикла;
			
			РаспределениеСтоимостьРегл = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(СтрокаПрочихРасходов.СтоимостьРегл, Коэффициенты); 
			РаспределениеНДСРегл = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(СтрокаПрочихРасходов.НДСРегл, Коэффициенты);
			
			Индекс = 0;
			Для каждого КлючИЗначение Из ТаблицыРаспределения Цикл
				
				Налогообложение = КлючИЗначение.Ключ;
				Таблица = КлючИЗначение.Значение;
				
				СтоимостьРегл = ?(РаспределениеСтоимостьРегл <> Неопределено, РаспределениеСтоимостьРегл[Индекс], 0);
				НДСРегл       = ?(РаспределениеНДСРегл       <> Неопределено, РаспределениеНДСРегл[Индекс], 0);
				
				Индекс = Индекс + 1;
				Если СтоимостьРегл = 0 И НДСРегл = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				НоваяСтрока = Таблица.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПрочихРасходов, , "СтоимостьРегл, НДСРегл");
				НоваяСтрока.СтоимостьРегл = СтоимостьРегл;
				НоваяСтрока.НДСРегл       = НДСРегл;
				
				Если ЗначениеЗаполнено(СтрокаПрочихРасходов.ВнеоборотныйАктив) Тогда
					Продолжить;
				КонецЕсли;
				
				Если Налогообложение = Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС Тогда
					Если НЕ БазаПериода.СписатьНДСПоРасходамАктивамНаСтатьиОтражения 
							ИЛИ НоваяСтрока.СтатьяРасходов = Неопределено Тогда
						НоваяСтрока.СтатьяОтраженияРасходов = БазаПериода.СтатьяРасходовНеНДС;
						НоваяСтрока.АналитикаОтраженияРасходов = БазаПериода.АналитикаРасходовНеНДС;
						НоваяСтрока.СтатьяРасходовНУ = БазаПериода.СтатьяРасходовНеНДС;
						НоваяСтрока.ЭтоПринимаемаяСтатьяРасходовНУ = БазаПериода.СтатьяРасходовНеНДС_ПринимаетсяКНУ;
					КонецЕсли;
				ИначеЕсли Налогообложение = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД Тогда
					Если НЕ БазаПериода.СписатьНДСПоРасходамАктивамНаСтатьиОтражения 
							ИЛИ НоваяСтрока.СтатьяРасходов = Неопределено Тогда
						НоваяСтрока.СтатьяОтраженияРасходов = БазаПериода.СтатьяРасходовЕНВД;
						НоваяСтрока.АналитикаОтраженияРасходов = БазаПериода.АналитикаРасходовЕНВД;
						НоваяСтрока.СтатьяРасходовНУ = БазаПериода.СтатьяРасходовЕНВД;
						НоваяСтрока.ЭтоПринимаемаяСтатьяРасходовНУ = БазаПериода.СтатьяРасходовЕНВД_ПринимаетсяКНУ;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
		Иначе
			
			Таблица = ТаблицыРаспределения[СтрокаПрочихРасходов.НовоеНалогообложениеНДС];
			Если Таблица = Неопределено Тогда
				ВызватьИсключение НСтр("ru = 'Не определено налогообложение по внеоборотнуму активу.'")
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(Таблица.Добавить(), СтрокаПрочихРасходов);
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Если 
	// - распределение выполняется после 01.07.2016
	// - партия поступила ранее 01.07.2016, 
	// то распределение также необходимо выполнить на документы несыерьвого экспорта.
	РаспределятьНаНесырьевойЭкспорт = УчетНДСУТ.УпрощенныйПорядокВычетаНДСПоНесырьевомуЭкспорту(Реквизиты.ПериодБазы);
	РаспределитьПартииПрочихРасходовЭкспортПоДокументам(ПартииПрочихРасходовЭкспорт, Реквизиты, РаспределятьНаНесырьевойЭкспорт);
	
	РаспределитьПартииПрочихРасходовЭкспортПоДокументам(ПартииПрочихРасходовЭкспортСырьевыхТоваровУслуг, Реквизиты, Ложь);
	
	Для Каждого КлючИЗначение Из ТаблицыРаспределения Цикл
		Налогообложение = КлючИЗначение.Ключ;
		Таблица = КлючИЗначение.Значение;
		Таблица.ЗаполнитьЗначения(Налогообложение, "НалогообложениеНДС");
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("ПартииПрочихРасходовОблагаетсяНДС",               ПартииПрочихРасходовОблагаетсяНДС);
	Результат.Вставить("ПартииПрочихРасходовНеОблагаетсяНДС",             ПартииПрочихРасходовНеОблагаетсяНДС);
	Результат.Вставить("ПартииПрочихРасходовОблагаетсяЕНВД",              ПартииПрочихРасходовОблагаетсяЕНВД);
	Результат.Вставить("ПартииПрочихРасходовЭкспорт",                     ПартииПрочихРасходовЭкспорт);
	Результат.Вставить("ПартииПрочихРасходовЭкспортНесырьевыхТоваров",    ПартииПрочихРасходовЭкспортНесырьевыхТоваров);
	Результат.Вставить("ПартииПрочихРасходовЭкспортСырьевыхТоваровУслуг", ПартииПрочихРасходовЭкспортСырьевыхТоваровУслуг);
	
	Возврат Результат;
	
КонецФункции

Процедура РаспределитьПартииПрочихРасходовЭкспортПоДокументам(ПартииПрочихРасходовЭкспорт, Реквизиты, РаспределятьНаНесырьевойЭкспорт)
	
	Если ПартииПрочихРасходовЭкспорт.Итог("НДСРегл") = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ИменаКолонок = УниверсальныеМеханизмыПартийИСебестоимости.ПолучитьИменаКолонокСтрокой(ПартииПрочихРасходовЭкспорт);
	УниверсальныеМеханизмыПартийИСебестоимости.ПоместитьТаблицуЗначенийВоВременнуюТаблицу(
		МенеджерВременныхТаблиц, "ПартииПрочихРасходовЭкспорт", ПартииПрочихРасходовЭкспорт, ИменаКолонок);
		
	ПартииПрочихРасходовЭкспорт.Очистить();
	
	РаспределитьПартииПрочихРасходовЭкспортПоДокументамТекущегоПериода(
		ПартииПрочихРасходовЭкспорт, 
		Реквизиты, 
		МенеджерВременныхТаблиц, 
		РаспределятьНаНесырьевойЭкспорт);
		
	РаспределитьПартииПрочихРасходовЭкспортПоДокументамПрошлыхПериодов(
		ПартииПрочихРасходовЭкспорт, 
		Реквизиты, 
		МенеджерВременныхТаблиц);
	
КонецПроцедуры

Процедура РаспределитьПартииПрочихРасходовЭкспортПоДокументамТекущегоПериода(ПартииПрочихРасходовЭкспорт, Реквизиты, МенеджерВременныхТаблиц, РаспределятьНаНесырьевойЭкспорт)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументыЭкспорт.Документ               КАК Документ,
	|	ДокументыЭкспорт.СуммаНесырьевыеТовары  КАК СуммаНесырьевыеТовары
	|ПОМЕСТИТЬ ДокументыЭкспортНесырьевыхТоваров
	|ИЗ
	|	&ДокументыЭкспорт КАК ДокументыЭкспорт
	|ГДЕ
	|	ДокументыЭкспорт.СуммаНесырьевыеТовары > 0
	|;
	|
	|ВЫБРАТЬ
	|	ДокументыЭкспорт.Документ.Дата КАК Дата,
	|	ДокументыЭкспорт.Документ      КАК Документ,
	|	ДокументыЭкспорт.Сумма         КАК Сумма
	|ИЗ
	|	Документ.РаспределениеНДС.ДокументыЭкспорт КАК ДокументыЭкспорт
	|ГДЕ
	|	ДокументыЭкспорт.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыЭкспорт.Документ.Дата КАК Дата,
	|	ДокументыЭкспорт.Документ      КАК Документ,
	|	ДокументыЭкспорт.СуммаНесырьевыеТовары  КАК Сумма
	|ИЗ
	|	ДокументыЭкспортНесырьевыхТоваров КАК ДокументыЭкспорт
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата
	|;
	|
	|ВЫБРАТЬ
	|	*
	|ИЗ
	|	ПартииПрочихРасходовЭкспорт КАК ПартииПрочихРасходовЭкспорт
	|ГДЕ
	|	ПартииПрочихРасходовЭкспорт.ПериодБазы = &ПериодБазы
	|	И ПартииПрочихРасходовЭкспорт.НДСРегл >= 0
	|
	|УПОРЯДОЧИТЬ ПО 
	|	ДатаПоступленияРасходов
	|;
	|
	|ВЫБРАТЬ
	|	*
	|ИЗ
	|	ПартииПрочихРасходовЭкспорт КАК ПартииПрочихРасходовЭкспорт
	|ГДЕ
	|	ПартииПрочихРасходовЭкспорт.ПериодБазы = &ПериодБазы
	|	И ПартииПрочихРасходовЭкспорт.НДСРегл < 0
	|
	|УПОРЯДОЧИТЬ ПО 
	|	ДатаПоступленияРасходов
	|";
	
	Запрос.УстановитьПараметр("ПериодБазы", Реквизиты.ПериодБазы);
	Запрос.УстановитьПараметр("Ссылка", Реквизиты.Ссылка);
	Если РаспределятьНаНесырьевойЭкспорт Тогда
		База = ПолучитьБазуРаспределения(Реквизиты.ПериодБазы, Реквизиты.Организация);
		Запрос.УстановитьПараметр("ДокументыЭкспорт", База.ДокументыЭкспорт);
	Иначе
		ДокументыЭкспорт = Новый ТаблицаЗначений;
		ДокументыЭкспорт.Колонки.Добавить("Документ", Новый ОписаниеТипов("ДокументСсылка.РеализацияТоваровУслуг"));
		ДокументыЭкспорт.Колонки.Добавить("СуммаНесырьевыеТовары", Новый ОписаниеТипов("Число"));
		Запрос.УстановитьПараметр("ДокументыЭкспорт", ДокументыЭкспорт);
	КонецЕсли;
	
	Результат = Запрос.ВыполнитьПакет();
	
	ДокументыРеализации                         = Результат[Результат.Количество() - 3].Выгрузить();
	ДокументыРеализации.Колонки.Добавить("НДС");
	
	// Положительные и отрицательные остатки распределим отдельно.
	ТаблицыДляРаспределения = Новый Массив;
	ТаблицыДляРаспределения.Добавить(Результат[Результат.Количество() - 2].Выгрузить()); // Положительные суммы по СФ и ИСФ, КСФ
	ТаблицыДляРаспределения.Добавить(Результат[Результат.Количество() - 1].Выгрузить()); // Отрицательные суммы по КСФ
	
	Для каждого ПартииПрочихРасходовЭкспортДляРаспределения Из ТаблицыДляРаспределения Цикл
	
		Если ПартииПрочихРасходовЭкспортДляРаспределения.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		Если ДокументыРеализации.Количество() = 0 Тогда
			// Нет документов для распределения, вернет таблицу как есть
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ПартииПрочихРасходовЭкспортДляРаспределения, ПартииПрочихРасходовЭкспорт);
			Возврат;
		КонецЕсли;
		
		НДСВсего         = ПартииПрочихРасходовЭкспортДляРаспределения.Итог("НДСРегл");
		Коэффициенты     = ДокументыРеализации.ВыгрузитьКолонку("Сумма");
		НДСПоРеализациям = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(НДСВсего, Коэффициенты); 
		
		ДокументыРеализации.ЗагрузитьКолонку(НДСПоРеализациям, "НДС");
		
		Для каждого СтрокаДокумент Из ДокументыРеализации Цикл
			
			НДСОсталосьРаспределить = СтрокаДокумент.НДС;
			
			РаспределенныеПартии = Новый Массив();
			
			Для каждого ПартияПрочихРасходов Из ПартииПрочихРасходовЭкспортДляРаспределения Цикл
				
				Если НДСОсталосьРаспределить = 0 Тогда
					Прервать;
				КонецЕсли;
				
				Если ПартияПрочихРасходов.НДСРегл = 0 Тогда
					
					Если ПартияПрочихРасходов.СтоимостьРегл > 0 Тогда
						// Распределять по реализациям не требуется, но остаток по регитстру необходимо закрыть
						НоваяСтрока = ПартииПрочихРасходовЭкспорт.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, ПартияПрочихРасходов);
					КонецЕсли;
					
					РаспределенныеПартии.Добавить(ПартияПрочихРасходов);
					
					Продолжить;
					
				КонецЕсли;
				
				Если НДСОсталосьРаспределить < 0 Тогда
					НДСРегл = Макс(НДСОсталосьРаспределить, ПартияПрочихРасходов.НДСРегл);
				Иначе
					НДСРегл = Мин(НДСОсталосьРаспределить, ПартияПрочихРасходов.НДСРегл);
				КонецЕсли;
				 
				СтоимостьРегл = Окр(ПартияПрочихРасходов.СтоимостьРегл * НДСРегл / ПартияПрочихРасходов.НДСРегл, 2);
				
				НоваяСтрока = ПартииПрочихРасходовЭкспорт.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ПартияПрочихРасходов, , "НДСРегл, СтоимостьРегл");
				
				НоваяСтрока.ДокументРеализации = СтрокаДокумент.Документ;
				
				НоваяСтрока.НДСРегл = НДСРегл;
				НоваяСтрока.СтоимостьРегл = СтоимостьРегл;
				
				ПартияПрочихРасходов.НДСРегл = ПартияПрочихРасходов.НДСРегл - НДСРегл;
				ПартияПрочихРасходов.СтоимостьРегл = ПартияПрочихРасходов.СтоимостьРегл - СтоимостьРегл;
				
				НДСОсталосьРаспределить = НДСОсталосьРаспределить - НДСРегл;
				
			КонецЦикла;
			
			Для каждого РаспределеннаяПартия Из РаспределенныеПартии Цикл
				ПартииПрочихРасходовЭкспортДляРаспределения.Удалить(РаспределеннаяПартия);
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура РаспределитьПартииПрочихРасходовЭкспортПоДокументамПрошлыхПериодов(ПартииПрочихРасходовЭкспорт, Реквизиты, МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПартииПрочихРасходовЭкспорт.ПериодБазы                  КАК ПериодБазы,
	|	ПартииПрочихРасходовЭкспорт.ДокументПоступленияРасходов КАК ДокументПоступленияРасходов,
	|	ПартииПрочихРасходовЭкспорт.ОснованиеКорректировки      КАК ОснованиеКорректировки
	|ПОМЕСТИТЬ ПартииПоДокументамПрошлыхПериодов
	|ИЗ
	|	ПартииПрочихРасходовЭкспорт КАК ПартииПрочихРасходовЭкспорт
	|ГДЕ
	|	ПартииПрочихРасходовЭкспорт.ПериодБазы < &ПериодБазы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПериодБазы,
	|	ОснованиеКорректировки
	|;
	|
	|ВЫБРАТЬ
	|	ПартииПоДокументамПрошлыхПериодов.ДокументПоступленияРасходов КАК ДокументПоступленияРасходов,
	|	ПартииПрочихРасходов.ДокументРеализации КАК ДокументРеализации,
	|	ПартииПрочихРасходов.НДСРегл КАК Сумма
	|ИЗ
	|	ПартииПоДокументамПрошлыхПериодов КАК ПартииПоДокументамПрошлыхПериодов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ПартииПрочихРасходов КАК ПартииПрочихРасходов
	|	ПО
	|		КОНЕЦПЕРИОДА(ПартииПрочихРасходов.Период, МЕСЯЦ) = ПартииПоДокументамПрошлыхПериодов.ПериодБазы
	|		И ПартииПрочихРасходов.Регистратор ССЫЛКА Документ.РаспределениеНДС 
	|		И ПартииПрочихРасходов.ДокументПоступленияРасходов = ПартииПоДокументамПрошлыхПериодов.ОснованиеКорректировки
	|		И ПартииПрочихРасходов.ДокументРеализации <> НЕОПРЕДЕЛЕНО
	|;
	|
	|ВЫБРАТЬ
	|	*
	|ИЗ
	|	ПартииПрочихРасходовЭкспорт КАК ПартииПрочихРасходовЭкспорт
	|ГДЕ
	|	ПартииПрочихРасходовЭкспорт.ПериодБазы < &ПериодБазы
	|";
	
	Запрос.УстановитьПараметр("ПериодБазы", Реквизиты.ПериодБазы);
	Запрос.УстановитьПараметр("Ссылка", Реквизиты.Ссылка);
	
	Результат = Запрос.ВыполнитьПакет();
	
	ДокументыРеализации                         = Результат[Результат.Количество() - 2].Выгрузить();
	ПартииПрочихРасходовЭкспортДляРаспределения = Результат[Результат.Количество() - 1].Выгрузить();
	
	Если ПартииПрочихРасходовЭкспортДляРаспределения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДокументыРеализации.Индексы.Добавить("ДокументПоступленияРасходов");
	ОтборДокументов = Новый Структура("ДокументПоступленияРасходов");
	
	Для каждого Партия Из ПартииПрочихРасходовЭкспортДляРаспределения Цикл
		
		ЗаполнитьЗначенияСвойств(ОтборДокументов, Партия);
		ДокументыПоПартии = ДокументыРеализации.НайтиСтроки(ОтборДокументов);
		
		Если ДокументыПоПартии.Количество() = 0 Тогда
			// Не удалось найти документы реализации, добавим строку без распределения по документам
			НоваяСтрока = ПартииПрочихРасходовЭкспорт.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Партия); 
			Продолжить;
		КонецЕсли;
		
		МассивКоэффициентов = Новый Массив();
		Для каждого Документ Из ДокументыПоПартии Цикл
			МассивКоэффициентов.Добавить(Документ.Сумма);
		КонецЦикла;
		
		МассивСтоимостьРегл = ОбщегоНазначенияКлиентСервер.РаспределитьСуммуПропорциональноКоэффициентам(Партия.СтоимостьРегл, МассивКоэффициентов);
		МассивНДСРегл = ОбщегоНазначенияКлиентСервер.РаспределитьСуммуПропорциональноКоэффициентам(Партия.НДСРегл, МассивКоэффициентов);
		
		Индекс = 0;
		Для каждого Документ Из ДокументыПоПартии Цикл
			
			НоваяСтрока = ПартииПрочихРасходовЭкспорт.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Партия);
			НоваяСтрока.СтоимостьРегл = МассивСтоимостьРегл[Индекс];
			НоваяСтрока.НДСРегл = МассивНДСРегл[Индекс];
			НоваяСтрока.ДокументРеализации = Документ.ДокументРеализации;
			
			Индекс = Индекс + 1;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Функция БазаРаспределения(ПартииПрочихРасходов, Реквизиты)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПериодыБазы.ПериодБазы
	|ПОМЕСТИТЬ ПериодыБазы
	|ИЗ
	|	&ПериодыБазы КАК ПериодыБазы
	|;
	|
	|////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыБазы.ПериодБазы,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(РаспределениеНДС.ПрименитьПравило5Процентов, &ПрименитьПравило5Процентов) ТОГДА
	|			ЕСТЬNULL(РаспределениеНДС.ВыручкаНДС,   &ВыручкаНДС)
	|				+ ЕСТЬNULL(РаспределениеНДС.ВыручкаНеНДС, &ВыручкаНеНДС)
	|				+ ЕСТЬNULL(РаспределениеНДС.ВыручкаЕНВД,  &ВыручкаЕНВД)
	|			ИНАЧЕ ЕСТЬNULL(РаспределениеНДС.ВыручкаНДС, &ВыручкаНДС)
	|	КОНЕЦ КАК НДС,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(РаспределениеНДС.ПрименитьПравило5Процентов, &ПрименитьПравило5Процентов)
	|			ТОГДА 0
	|			ИНАЧЕ ЕСТЬNULL(РаспределениеНДС.ВыручкаНеНДС, &ВыручкаНеНДС)
	|	КОНЕЦ КАК НеНДС,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(РаспределениеНДС.ПрименитьПравило5Процентов, &ПрименитьПравило5Процентов)
	|			ТОГДА 0
	|			ИНАЧЕ ЕСТЬNULL(РаспределениеНДС.ВыручкаЕНВД, &ВыручкаЕНВД)
	|	КОНЕЦ КАК ЕНВД,
	|	ЕСТЬNULL(РаспределениеНДС.ВыручкаНДС0, &ВыручкаНДС0) КАК НДС0,
	|	ЕСТЬNULL(РаспределениеНДС.ВыручкаНДС0СырьевыеТоварыУслуги, &ВыручкаНДС0СырьевыеТоварыУслуги) КАК НДС0СырьевыеТоварыУслуги,
	|	ЕСТЬNULL(РаспределениеНДС.ВыручкаНДС0НесырьевыеТовары, &ВыручкаНДС0НесырьевыеТовары) КАК НДС0НесырьевыеТовары,
	|	
	|	ЕСТЬNULL(РаспределениеНДС.СписатьНДСПоРасходамАктивамНаСтатьиОтражения, 
	|				&СписатьНДСПоРасходамАктивамНаСтатьиОтражения) КАК СписатьНДСПоРасходамАктивамНаСтатьиОтражения,
	|	ЕСТЬNULL(РаспределениеНДС.СтатьяРасходовНеНДС, 
	|				&СтатьяРасходовНеНДС) КАК СтатьяРасходовНеНДС,
	|	ЕСТЬNULL(РаспределениеНДС.АналитикаРасходовНеНДС, 
	|				&АналитикаРасходовНеНДС) КАК АналитикаРасходовНеНДС,
	|	ЕСТЬNULL(РаспределениеНДС.СтатьяРасходовЕНВД, 
	|				&СтатьяРасходовЕНВД) КАК СтатьяРасходовЕНВД,
	|	ЕСТЬNULL(РаспределениеНДС.АналитикаРасходовЕНВД, 
	|				&АналитикаРасходовЕНВД) КАК АналитикаРасходовЕНВД,
	|	
	|	ВЫРАЗИТЬ(ЕСТЬNULL(РаспределениеНДС.СтатьяРасходовНеНДС, 
	|				&СтатьяРасходовНеНДС) КАК ПланВидовХарактеристик.СтатьиРасходов).ПринятиеКналоговомуУчету КАК СтатьяРасходовНеНДС_ПринимаетсяКНУ,
	|	ВЫРАЗИТЬ(ЕСТЬNULL(РаспределениеНДС.СтатьяРасходовЕНВД, 
	|				&СтатьяРасходовЕНВД) КАК ПланВидовХарактеристик.СтатьиРасходов).ПринятиеКналоговомуУчету КАК СтатьяРасходовЕНВД_ПринимаетсяКНУ
	|ИЗ
	|	ПериодыБазы КАК ПериодыБазы
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.РаспределениеНДС КАК РаспределениеНДС
	|	ПО
	|		ПериодыБазы.ПериодБазы = КОНЕЦПЕРИОДА(РаспределениеНДС.Дата, МЕСЯЦ)
	|		И РаспределениеНДС.Организация = &Организация 
	|		И РаспределениеНДС.Проведен
	|";
	
	ПериодыБазы = ПартииПрочихРасходов.Скопировать();
	ПериодыБазы.Свернуть("ПериодБазы");
	
	Запрос.УстановитьПараметр("ПериодыБазы",                     ПериодыБазы);
	Запрос.УстановитьПараметр("Организация",                     Реквизиты.Организация);
	Запрос.УстановитьПараметр("ПрименитьПравило5Процентов",      Реквизиты.ПрименитьПравило5Процентов);
	Запрос.УстановитьПараметр("ВыручкаНДС",                      Реквизиты.ВыручкаНДС);
	Запрос.УстановитьПараметр("ВыручкаНеНДС",                    Реквизиты.ВыручкаНеНДС);
	Запрос.УстановитьПараметр("ВыручкаЕНВД",                     Реквизиты.ВыручкаЕНВД);
	Запрос.УстановитьПараметр("ВыручкаНДС0",                     Реквизиты.ВыручкаНДС0);
	Запрос.УстановитьПараметр("ВыручкаНДС0СырьевыеТоварыУслуги", Реквизиты.ВыручкаНДС0СырьевыеТоварыУслуги);
	Запрос.УстановитьПараметр("ВыручкаНДС0НесырьевыеТовары",     Реквизиты.ВыручкаНДС0НесырьевыеТовары);
	
	Запрос.УстановитьПараметр("СписатьНДСПоРасходамАктивамНаСтатьиОтражения", Реквизиты.СписатьНДСПоРасходамАктивамНаСтатьиОтражения);
	Запрос.УстановитьПараметр("СтатьяРасходовНеНДС",                          Реквизиты.СтатьяРасходовНеНДС);
	Запрос.УстановитьПараметр("АналитикаРасходовНеНДС",                       Реквизиты.АналитикаРасходовНеНДС);
	Запрос.УстановитьПараметр("СтатьяРасходовЕНВД",                           Реквизиты.СтатьяРасходовЕНВД);
	Запрос.УстановитьПараметр("АналитикаРасходовЕНВД",                        Реквизиты.АналитикаРасходовЕНВД);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции

Функция ТаблицаРезультатРаспределения()
	
	НаборЗаписей = РегистрыНакопления.ПартииПрочихРасходов.СоздатьНаборЗаписей();
	Результат = НаборЗаписей.ВыгрузитьКолонки();
	
	Результат.Колонки.Удалить("Активность");
	Результат.Колонки.Удалить("МоментВремени");
	
	Результат.Колонки.Добавить("ОснованиеКорректировки",  Метаданные.Документы.КорректировкаПоступления.Реквизиты.ДокументОснование.Тип);
	Результат.Колонки.Добавить("ДатаПоступленияРасходов", Новый ОписаниеТипов("Дата"));
	
	Результат.Колонки.Добавить("НалогообложениеНДСПартии",         Новый ОписаниеТипов("ПеречислениеСсылка.ТипыНалогообложенияНДС"));
	Результат.Колонки.Добавить("Контрагент",                       Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	Результат.Колонки.Добавить("СтавкаНДС",                        Новый ОписаниеТипов("ПеречислениеСсылка.СтавкиНДС"));
	Результат.Колонки.Добавить("ВидЦенностиНДС",                   Новый ОписаниеТипов("ПеречислениеСсылка.ВидыЦенностей"));
	
	Результат.Колонки.Добавить("ВнеоборотныйАктив",                     Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("ВключаетсяВРасходыНУПриПринятииКУчету", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ПодразделениеРасходовНУ",               Новый ОписаниеТипов("СправочникСсылка.СтруктураПредприятия"));
	Результат.Колонки.Добавить("СтатьяРасходовНУ",                      Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.СтатьиРасходов"));
	Результат.Колонки.Добавить("ЭтоПринимаемаяСтатьяРасходовНУ",        Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("АналитикаРасходовНУ",                   Новый ОписаниеТипов(Метаданные.ПланыВидовХарактеристик.СтатьиРасходов.Тип, "NULL"));
	
	Возврат Результат;
	
КонецФункции

Функция ТаблицаПартииПрочихРасходов(РезультатРаспределения, Реквизиты)
	
	ПартииПрочихРасходов = РегистрыНакопления.ПартииПрочихРасходов.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	ПартииПрочихРасходов.Колонки.Удалить("Активность");
	
	Для каждого ТаблицаРаспределения Из РезультатРаспределения Цикл
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаРаспределения.Значение, ПартииПрочихРасходов);
	КонецЦикла;
	
	ПартииПрочихРасходов.ЗаполнитьЗначения(ВидДвиженияНакопления.Расход, "ВидДвижения");
	ПартииПрочихРасходов.ЗаполнитьЗначения(Реквизиты.Период,             "Период");
	
	Возврат ПартииПрочихРасходов;
	
КонецФункции

//++ НЕ УТ

Функция ТаблицаПрочиеРасходы(РезультатРаспределения, Реквизиты)
	
	ПрочиеРасходы = РегистрыНакопления.ПрочиеРасходы.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	ПрочиеРасходы.Колонки.Удалить("Активность");
	
	ПрочиеРасходыДобавитьСтрокиВключениеВСтоимость(ПрочиеРасходы, РезультатРаспределения.ПартииПрочихРасходовНеОблагаетсяНДС, Реквизиты, Ложь);
	ПрочиеРасходыДобавитьСтрокиВключениеВСтоимость(ПрочиеРасходы, РезультатРаспределения.ПартииПрочихРасходовОблагаетсяЕНВД,  Реквизиты, Истина);
	
	ПрочиеРасходыДобавитьСтрокиИсключениеИзСтоимости(ПрочиеРасходы, РезультатРаспределения.ПартииПрочихРасходовОблагаетсяНДС, Реквизиты);
	ПрочиеРасходыДобавитьСтрокиИсключениеИзСтоимости(ПрочиеРасходы, РезультатРаспределения.ПартииПрочихРасходовЭкспорт,       Реквизиты);
	ПрочиеРасходыДобавитьСтрокиИсключениеИзСтоимости(ПрочиеРасходы, РезультатРаспределения.ПартииПрочихРасходовЭкспортНесырьевыхТоваров, Реквизиты);
	ПрочиеРасходыДобавитьСтрокиИсключениеИзСтоимости(ПрочиеРасходы, РезультатРаспределения.ПартииПрочихРасходовЭкспортСырьевыхТоваровУслуг, Реквизиты);
	
	Возврат ПрочиеРасходы;
	
КонецФункции

Процедура ПрочиеРасходыДобавитьСтрокиВключениеВСтоимость(ПрочиеРасходы, ТаблицаИсточник, Реквизиты, ЭтоРасходыЕНВД = Ложь)
	
	Для каждого Строка Из ТаблицаИсточник Цикл
		
		Если НЕ ПустаяСтрока(Строка.ВнеоборотныйАктив) 
				И НЕ Строка.ВключаетсяВРасходыНУПриПринятииКУчету Тогда 
			// Внеоборотные активы хранят данные в проводках
			Продолжить;
		КонецЕсли;
		
		Если Строка.НалогообложениеНДСПартии = Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС
				ИЛИ Строка.НалогообложениеНДСПартии = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД Тогда
			// Уже в расходах
			Продолжить;
		КонецЕсли;
		
		Если НЕ ПустаяСтрока(Строка.ВнеоборотныйАктив) И Строка.ВключаетсяВРасходыНУПриПринятииКУчету Тогда
			Подразделение       = Строка.ПодразделениеРасходовНУ;
			СтатьяРасходов		= Строка.СтатьяРасходовНУ;
			АналитикаРасходов	= Строка.АналитикаРасходовНУ;
		ИначеЕсли ЗначениеЗаполнено(Строка.СтатьяОтраженияРасходов) Тогда
			Подразделение		= Строка.Подразделение;
			СтатьяРасходов		= Строка.СтатьяОтраженияРасходов;
			АналитикаРасходов	= Строка.АналитикаОтраженияРасходов;
		Иначе
			Подразделение		= Строка.Подразделение;
			СтатьяРасходов		= Строка.СтатьяРасходов;
			АналитикаРасходов	= Строка.АналитикаРасходов;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(СтатьяРасходов) 
				ИЛИ ТипЗнч(СтатьяРасходов) <> Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов") Тогда
			// Это не расходы
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ПрочиеРасходы.Добавить();
		НоваяСтрока.Период					= Реквизиты.Период;
		НоваяСтрока.Организация				= Реквизиты.Организация;
		НоваяСтрока.ХозяйственнаяОперация	= Перечисления.ХозяйственныеОперации.РаспределениеНДС;
		НоваяСтрока.НаправлениеДеятельности = Строка.НаправлениеДеятельности;
		НоваяСтрока.Подразделение			= Подразделение;
		НоваяСтрока.СтатьяРасходов			= СтатьяРасходов;
		НоваяСтрока.АналитикаРасходов		= АналитикаРасходов;
		
		Если НЕ ПустаяСтрока(Строка.ВнеоборотныйАктив) И Строка.ВключаетсяВРасходыНУПриПринятииКУчету Тогда
			НоваяСтрока.ВременнаяРазница = -Строка.НДСРегл;
		Иначе
			НоваяСтрока.СуммаРегл = Строка.НДСРегл;
		КонецЕсли;
		Если НЕ Строка.ЭтоПринимаемаяСтатьяРасходовНУ Тогда
			НоваяСтрока.ПостояннаяРазница = Строка.НДСРегл;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПрочиеРасходыДобавитьСтрокиИсключениеИзСтоимости(ПрочиеРасходы, ТаблицаИсточник, Реквизиты)
	
	Для каждого Строка Из ТаблицаИсточник Цикл
		
		Если Не ПустаяСтрока(Строка.ВнеоборотныйАктив) Тогда 
			// Внеоборотные активы хранят данные в проводках
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Строка.СтатьяРасходов) Тогда
			// Это не расходы
			Продолжить;
		КонецЕсли;
		
		Если Строка.НалогообложениеНДСПартии <> Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС
			И Строка.НалогообложениеНДСПартии <> Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ПрочиеРасходы.Добавить();
		НоваяСтрока.Период					= Реквизиты.Период;
		НоваяСтрока.Организация				= Реквизиты.Организация;
		НоваяСтрока.Подразделение			= Строка.Подразделение;
		НоваяСтрока.НаправлениеДеятельности = Строка.НаправлениеДеятельности;
		НоваяСтрока.ХозяйственнаяОперация	= Перечисления.ХозяйственныеОперации.РаспределениеНДС;
		НоваяСтрока.СтатьяРасходов			= Строка.СтатьяРасходов;
		НоваяСтрока.АналитикаРасходов		= Строка.АналитикаРасходов;
		
		НоваяСтрока.СуммаРегл = -Строка.НДСРегл;
		Если НЕ Строка.ЭтоПринимаемаяСтатьяРасходовНУ Тогда
			НоваяСтрока.ПостояннаяРазница = -Строка.НДСРегл;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ТаблицаПервоначальныеСведенияНМА(РезультатРаспределения, ТаблицаПервоначальныеСведенияНМА)
	
	ОтборСтрок = Новый Структура("ВнеоборотныйАктив", "НМА");
	
	ТаблицыИсточники = Новый Структура;
	ТаблицыИсточники.Вставить("ПартииПрочихРасходовНеОблагаетсяНДС");
	ТаблицыИсточники.Вставить("ПартииПрочихРасходовОблагаетсяЕНВД");
	
	Для каждого ТаблицаРаспределения Из РезультатРаспределения Цикл
		
		Если НЕ ТаблицыИсточники.Свойство(ТаблицаРаспределения.Ключ) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокиНМА = ТаблицаРаспределения.Значение.НайтиСтроки(ОтборСтрок);
		Для каждого СтрокаНМА Из СтрокиНМА Цикл
			
			ПервоначальныеСведения = ТаблицаПервоначальныеСведенияНМА.Найти(СтрокаНМА.АналитикаРасходов, "НематериальныйАктив");
			Если ПервоначальныеСведения = Неопределено Тогда
				ПервоначальныеСведения = ТаблицаПервоначальныеСведенияНМА.Найти(СтрокаНМА.АналитикаАктивовПассивов, "НематериальныйАктив");
			КонецЕсли;
			Если ПервоначальныеСведения = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ПервоначальныеСведения.ПервоначальнаяСтоимость = ПервоначальныеСведения.ПервоначальнаяСтоимость + СтрокаНМА.НДСРегл;
			ПервоначальныеСведения.ЕстьИзменения = Истина;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТаблицаПервоначальныеСведенияНМА.Скопировать(Новый Структура("ЕстьИзменения", Истина));
	
КонецФункции

Функция ТаблицаПервоначальныеСведенияОС(РезультатРаспределения, ТаблицаПервоначальныеСведенияОС)
	
	ОтборСтрок = Новый Структура("ВнеоборотныйАктив", "ОС");
	
	ТаблицыИсточники = Новый Структура;
	ТаблицыИсточники.Вставить("ПартииПрочихРасходовНеОблагаетсяНДС");
	ТаблицыИсточники.Вставить("ПартииПрочихРасходовОблагаетсяЕНВД");
	
	Для каждого ТаблицаРаспределения Из РезультатРаспределения Цикл
		
		Если НЕ ТаблицыИсточники.Свойство(ТаблицаРаспределения.Ключ) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокиОС = ТаблицаРаспределения.Значение.НайтиСтроки(ОтборСтрок);
		Для каждого СтрокаОС Из СтрокиОС Цикл
			
			ПервоначальныеСведения = ТаблицаПервоначальныеСведенияОС.Найти(СтрокаОС.АналитикаРасходов, "ОсновноеСредство");
			Если ПервоначальныеСведения = Неопределено Тогда
				ПервоначальныеСведения = ТаблицаПервоначальныеСведенияОС.Найти(СтрокаОС.АналитикаАктивовПассивов, "ОсновноеСредство");
			КонецЕсли;
			Если ПервоначальныеСведения = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ПервоначальныеСведения.ПервоначальнаяСтоимость = ПервоначальныеСведения.ПервоначальнаяСтоимость + СтрокаОС.НДСРегл;
			ПервоначальныеСведения.ЕстьИзменения = Истина;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТаблицаПервоначальныеСведенияОС.Скопировать(Новый Структура("ЕстьИзменения", Истина));
	
КонецФункции

Функция ТаблицаПараметрыАмортизацииОС(РезультатРаспределения, ТаблицаПараметрыАмортизацииОС)
	
	ОтборСтрок = Новый Структура("ВнеоборотныйАктив", "ОС");
	
	ТаблицыИсточники = Новый Структура;
	ТаблицыИсточники.Вставить("ПартииПрочихРасходовНеОблагаетсяНДС");
	ТаблицыИсточники.Вставить("ПартииПрочихРасходовОблагаетсяЕНВД");
	
	Для каждого ТаблицаРаспределения Из РезультатРаспределения Цикл
		
		Если НЕ ТаблицыИсточники.Свойство(ТаблицаРаспределения.Ключ) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокиОС = ТаблицаРаспределения.Значение.НайтиСтроки(ОтборСтрок);
		Для каждого СтрокаОС Из СтрокиОС Цикл
			
			ПервоначальныеСведения = ТаблицаПараметрыАмортизацииОС.Найти(СтрокаОС.АналитикаРасходов, "ОсновноеСредство");
			Если ПервоначальныеСведения = Неопределено Тогда
				ПервоначальныеСведения = ТаблицаПараметрыАмортизацииОС.Найти(СтрокаОС.АналитикаАктивовПассивов, "ОсновноеСредство");
			КонецЕсли;
			Если ПервоначальныеСведения = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ПервоначальныеСведения.СтоимостьДляВычисленияАмортизации = ПервоначальныеСведения.СтоимостьДляВычисленияАмортизации + СтрокаОС.НДСРегл;
			ПервоначальныеСведения.ЕстьИзменения = Истина;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТаблицаПараметрыАмортизацииОС.Скопировать(Новый Структура("ЕстьИзменения", Истина));
	
КонецФункции

//-- НЕ УТ

#КонецОбласти

#Область ИнициализацияИЗаполнение

Функция ЗаполнитьПравилаСписанияНДС(Организация = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ДанныеДокумента.СписатьНДСПоРасходамАктивамНаСтатьиОтражения КАК СписатьНДСПоРасходамАктивамНаСтатьиОтражения,
	|	ДанныеДокумента.СтатьяРасходовНеНДС КАК СтатьяРасходовНеНДС,
	|	ДанныеДокумента.АналитикаРасходовНеНДС КАК АналитикаРасходовНеНДС,
	|	ДанныеДокумента.СтатьяРасходовЕНВД КАК СтатьяРасходовЕНВД,
	|	ДанныеДокумента.АналитикаРасходовЕНВД КАК АналитикаРасходовЕНВД
	|ИЗ
	|	Документ.РаспределениеНДС КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Проведен
	|	И (ВЫБОР
	|		КОГДА &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ДанныеДокумента.Организация = &Организация
	|	КОНЕЦ)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДанныеДокумента.Дата УБЫВ
	|";
	
	Запрос.УстановитьПараметр("Организация",?(Организация = Неопределено,Справочники.Организации.ПустаяСсылка(),Организация));
	
	Выборка = Запрос.Выполнить().Выбрать();
	ПравилаСписания = Новый Структура("СписатьНДСПоРасходамАктивамНаСтатьиОтражения,СтатьяРасходовНеНДС,АналитикаРасходовНеНДС,СтатьяРасходовЕНВД,АналитикаРасходовЕНВД");
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ПравилаСписания,Выборка);
	Иначе
		ПравилаСписания.СписатьНДСПоРасходамАктивамНаСтатьиОтражения	= 1;
	КонецЕсли;
	
	Возврат ПравилаСписания
	
КонецФункции

Функция ПолучитьБазуРаспределения(Дата, Организация) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДанных.Организация КАК Организация,
	|	СУММА(ТаблицаДанных.ВыручкаНДС) КАК ВыручкаНДС,
	|	СУММА(ТаблицаДанных.ВыручкаНеНДС) КАК ВыручкаНеНДС,
	|	СУММА(ТаблицаДанных.ВыручкаЕНВД) КАК ВыручкаЕНВД
	|ИЗ
	|	(ВЫБРАТЬ
	|		НДСЗаписиКнигиПродажОбороты.Организация КАК Организация,
	|		ЕстьNULL(НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,0) КАК ВыручкаНДС,
	|		0 КАК ВыручкаНеНДС,
	|		0 КАК ВыручкаЕНВД
	|	ИЗ
	|		РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|				&ПериодНачало,
	|				&ПериодОкончание,
	|				Квартал,
	|				Организация В (&Организация)
	|					И Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.Реализация)
	|					И НЕ СтавкаНДС В (ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС0), ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС))) КАК НДСЗаписиКнигиПродажОбороты
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НДСЗаписиКнигиПродажОбороты.Организация,
	|		0,
	|		ЕстьNULL(НДСЗаписиКнигиПродажОбороты.СуммаБезНДСОборот,0),
	|		0
	|	ИЗ
	|		РегистрНакопления.НДСЗаписиКнигиПродаж.Обороты(
	|				&ПериодНачало,
	|				&ПериодОкончание,
	|				Квартал,
	|				Организация В (&Организация)
	|					И Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.Реализация)
	|					И СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)) КАК НДСЗаписиКнигиПродажОбороты
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НачисленияПоЗаймамВыданным.Организация,
	|		0,
	|		НачисленияПоЗаймамВыданным.СуммаРеглОборот,
	|		0
	|	ИЗ
	|		РегистрНакопления.ДвиженияКонтрагентДоходыРасходы.Обороты(
	|			&ПериодНачало, 
	|			&ПериодОкончание,
	|			Квартал,
	|			ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачисленияПоЗаймамВыданным)
	|				И Организация В (&Организация)) КАК НачисленияПоЗаймамВыданным
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НачисленияПоЗаймамВыданным.Организация,
	|		0,
	|		НачисленияПоЗаймамВыданным.СуммаРеглОборот,
	|		0
	|	ИЗ
	|		РегистрНакопления.ДвиженияДоходыРасходыПрочиеАктивыПассивы.Обороты(
	|			&ПериодНачало, 
	|			&ПериодОкончание,
	|			Квартал,
	|			ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачисленияПоЗаймамВыданным)
	|				И Организация В (&Организация)) КАК НачисленияПоЗаймамВыданным
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		АналитикаПоПартнерам.Организация,
	|		0,
	|		0,
	|		ЕстьNULL(ВыручкаИСебестоимостьПродаж.СуммаВыручкиРегл,0)
	|	ИЗ
	|		РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК АналитикаПоПартнерам
	|			ПО ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам = АналитикаПоПартнерам.Ссылка
	|	ГДЕ
	|		АналитикаПоПартнерам.Организация В (&Организация)
	|		И ВыручкаИСебестоимостьПродаж.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|		И ВыручкаИСебестоимостьПродаж.Период МЕЖДУ &ПериодНачало И &ПериодОкончание
	|		И ВыручкаИСебестоимостьПродаж.Активность
	|		И ВыручкаИСебестоимостьПродаж.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|	
	|	) КАК ТаблицаДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДанных.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|	
	|ВЫБРАТЬ
	|	ТаблицаДанных.Организация               КАК Организация,
	|	ТаблицаДанных.Ссылка                    КАК Документ,
	|	ВЫБОР 
	|		КОГДА &УпрощенныйПорядокВычетаНДСПоНесырьевомуЭкспорту 
	|			ТОГДА СУММА(ТаблицаДанных.СуммаСырьевыеТоварыУслуги)
	|				ИНАЧЕ СУММА(ТаблицаДанных.СуммаНесырьевыеТовары + ТаблицаДанных.СуммаСырьевыеТоварыУслуги) 
	|	КОНЕЦ КАК Сумма,
	|	СУММА(ТаблицаДанных.СуммаНесырьевыеТовары)     КАК СуммаНесырьевыеТовары,
	|	СУММА(ТаблицаДанных.СуммаСырьевыеТоварыУслуги) КАК СуммаСырьевыеТоварыУслуги
	|ИЗ
	|	(ВЫБРАТЬ
	|		НДССостояниеРеализации0.Организация КАК Организация,
	|		ДанныеДокумента.Ссылка              КАК Ссылка,
	|		0                                   КАК СуммаНесырьевыеТовары,
	|		ВЫБОР
	|			КОГДА ЕСТЬNULL(СуммыВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|				ТОГДА ДанныеДокумента.СуммаСНДС
	|			ИНАЧЕ СуммыВВалютеРегл.БазаНДСРегл
	|		КОНЕЦ                               КАК СуммаСырьевыеТоварыУслуги
	|	ИЗ
	|		Документ.АктВыполненныхРабот.Услуги КАК ДанныеДокумента
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|			РегистрСведений.НДССостояниеРеализации0 КАК НДССостояниеРеализации0
	|		ПО 
	|			ДанныеДокумента.Ссылка = НДССостояниеРеализации0.ДокументРеализации
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ 
	|			РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыВВалютеРегл
	|		ПО 
	|			ДанныеДокумента.Ссылка = СуммыВВалютеРегл.Регистратор
	|			И ДанныеДокумента.ИдентификаторСтроки = СуммыВВалютеРегл.ИдентификаторСтроки
	|	ГДЕ
	|		НДССостояниеРеализации0.Организация В (&Организация)
	|		И НДССостояниеРеализации0.ДатаРеализации МЕЖДУ &ПериодНачало И &ПериодОкончание
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		НДССостояниеРеализации0.Организация  КАК Организация,
	|		ДанныеДокумента.Ссылка               КАК Ссылка,
	|		ВЫБОР 
	|			КОГДА НЕ ЕСТЬNULL(СпрНоменклатура.СырьевойТовар, ЛОЖЬ)
	|				ТОГДА
	|					ВЫБОР КОГДА ЕСТЬNULL(СуммыВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|						ТОГДА ДанныеДокумента.СуммаСНДС
	|						ИНАЧЕ СуммыВВалютеРегл.БазаНДСРегл
	|					КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ                                КАК СуммаНесырьевыеТовары,
	|		ВЫБОР 
	|			КОГДА ЕСТЬNULL(СпрНоменклатура.СырьевойТовар, ЛОЖЬ)
	|				ТОГДА
	|					ВЫБОР КОГДА ЕСТЬNULL(СуммыВВалютеРегл.СуммаБезНДСРегл,0) = 0
	|						ТОГДА ДанныеДокумента.СуммаСНДС
	|						ИНАЧЕ СуммыВВалютеРегл.СуммаБезНДСРегл
	|					КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ                                КАК СуммаСырьевыеТоварыУслуги
	|	ИЗ
	|		Документ.РеализацияТоваровУслуг.ВидыЗапасов КАК ДанныеДокумента
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			РегистрСведений.НДССостояниеРеализации0 КАК НДССостояниеРеализации0
	|		ПО 
	|			ДанныеДокумента.Ссылка = НДССостояниеРеализации0.ДокументРеализации
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыВВалютеРегл
	|		ПО 
	|			ДанныеДокумента.Ссылка = СуммыВВалютеРегл.Регистратор
	|			И ДанныеДокумента.ИдентификаторСтроки = СуммыВВалютеРегл.ИдентификаторСтроки
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ 
	|			Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО 
	|			ДанныеДокумента.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ 
	|			Справочник.Номенклатура КАК СпрНоменклатура 
	|		ПО
	|			СпрНоменклатура.Ссылка = Аналитика.Номенклатура
	|	ГДЕ
	|		НДССостояниеРеализации0.Организация В (&Организация)
	|		И НДССостояниеРеализации0.ДатаРеализации МЕЖДУ &ПериодНачало И &ПериодОкончание
	|		И ДанныеДокумента.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		НДССостояниеРеализации0.Организация КАК Организация,
	|		ДанныеДокумента.Ссылка              КАК Ссылка,
	|		0                                   КАК СуммСырьевыеТоварыУслуги,
	|		ВЫБОР
	|			КОГДА СуммыВВалютеРегл.СуммаБезНДСРегл ЕСТЬ NULL 
	|					ИЛИ СуммыВВалютеРегл.СуммаБезНДСРегл = 0
	|				ТОГДА ДанныеДокумента.СуммаСНДС
	|			ИНАЧЕ СуммыВВалютеРегл.БазаНДСРегл
	|		КОНЕЦ                               КАК СуммаСырьевыеТоварыУслуги
	|	ИЗ
	|		Документ.РеализацияТоваровУслуг.Товары КАК ДанныеДокумента
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НДССостояниеРеализации0 КАК НДССостояниеРеализации0
	|			ПО ДанныеДокумента.Ссылка = НДССостояниеРеализации0.ДокументРеализации
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СуммыДокументовВВалютеРегл КАК СуммыВВалютеРегл
	|			ПО ДанныеДокумента.Ссылка = СуммыВВалютеРегл.Регистратор
	|				И ДанныеДокумента.ИдентификаторСтроки = СуммыВВалютеРегл.ИдентификаторСтроки
	|	ГДЕ
	|		НДССостояниеРеализации0.Организация В (&Организация)
	|		И НДССостояниеРеализации0.ДатаРеализации МЕЖДУ &ПериодНачало И &ПериодОкончание
	|		И ДанныеДокумента.Номенклатура.ТипНоменклатуры НЕ В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|	) КАК ТаблицаДанных
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДанных.Организация,
	|	ТаблицаДанных.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаДанных.Ссылка.Дата
	|";
	
	ПериодНачало = НачалоКвартала(Дата);
	ПериодОкончание = КонецКвартала(Дата);
	//++ НЕ УТ
	Если Месяц(Дата)%3 <> 0 Тогда
		ПериодНачало = НачалоМесяца(Дата);
		ПериодОкончание = КонецМесяца(Дата);
	КонецЕсли;
	//-- НЕ УТ
	
	Запрос.УстановитьПараметр("ПериодНачало", ПериодНачало);
	Запрос.УстановитьПараметр("ПериодОкончание", ПериодОкончание);
	
	Если ТипЗнч(Организация) = Тип("СправочникСсылка.Организации") Тогда
		МассивОрганизаций = Новый Массив();
		МассивОрганизаций.Добавить(Организация);
		Запрос.УстановитьПараметр("Организация", МассивОрганизаций);
	Иначе
		Запрос.УстановитьПараметр("Организация", Организация)
	КонецЕсли;
	
	УпрощенныйПорядокВычетаНДСПоНесырьевомуЭкспорту = УчетНДСУТ.УпрощенныйПорядокВычетаНДСПоНесырьевомуЭкспорту(Дата);
	Запрос.УстановитьПараметр("УпрощенныйПорядокВычетаНДСПоНесырьевомуЭкспорту", УпрощенныйПорядокВычетаНДСПоНесырьевомуЭкспорту);
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицаВыручка          = Результат[0].Выгрузить(); 
	ТаблицаДокументыЭкспорт = Результат[1].Выгрузить();
	
	Результат = Новый Структура;
	Результат.Вставить("ВыручкаНДС",                      ТаблицаВыручка.Итог("ВыручкаНДС"));
	Результат.Вставить("ВыручкаНеНДС",                    ТаблицаВыручка.Итог("ВыручкаНеНДС"));
	Результат.Вставить("ВыручкаЕНВД",                     ТаблицаВыручка.Итог("ВыручкаЕНВД"));
	
	Результат.Вставить("ВыручкаНДС0",                     ТаблицаДокументыЭкспорт.Итог("СуммаНесырьевыеТовары") + ТаблицаДокументыЭкспорт.Итог("СуммаСырьевыеТоварыУслуги"));
	Результат.Вставить("ВыручкаНДС0НесырьевыеТовары",     ТаблицаДокументыЭкспорт.Итог("СуммаНесырьевыеТовары"));
	Результат.Вставить("ВыручкаНДС0СырьевыеТоварыУслуги", ТаблицаДокументыЭкспорт.Итог("СуммаСырьевыеТоварыУслуги"));
	
	Результат.Вставить("ДокументыЭкспорт", ТаблицаДокументыЭкспорт);
	
	Возврат Результат
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
