#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область СтандартныеПодсистемыКоманды

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Заполняет список команд создания на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании.
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании) Экспорт
	
	ПроизводствоСервер.ДобавитьКомандуСоздатьВыпускПродукцииБезЗаказаНаОсновании(КомандыСоздатьНаОсновании);
	Документы.ВыработкаСотрудников.ДобавитьКомандуСоздатьНаОснованииСозданиеВыработкиСотрудников(КомандыСоздатьНаОсновании);
	
КонецПроцедуры

// Заполняет список команд создания на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании.
// Возвращаемое значение:
//   КомандаФормы - Добавленная Команда.
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСоздатьНаОсновании) Экспорт
	
	
	
КонецФункции

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов.
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуДвиженияДокумента(КомандыОтчетов);
	
КонецПроцедуры

// СтандартныеПодсистемы.Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати – ТаблицаЗначений – состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Печать

#КонецОбласти

#Область Серии

// Имена реквизитов, от значений которых зависят параметры указания серий.
//
//	Возвращаемое значение:
//		Строка - имена реквизитов, перечисленные через запятую.
//
Функция ИменаРеквизитовДляЗаполненияПараметровУказанияСерий() Экспорт
	
	Возврат "Подразделение, Дата";
	
КонецФункции

// Возвращает параметры указания серий для товаров, указанных в документе.
//
//	Параметры:
//			Объект - Структура - структура значений реквизитов объекта, необходимых для заполнения параметров указания серий.
//	Возвращаемое значение:
//			Структура - Структура параметров указания серий для объекта.
//
Функция ПараметрыУказанияСерий(Объект) Экспорт
	
	ПараметрыУказанияСерий = Новый Структура;
	
	#Область ВыходныеИзделия
	
	ПараметрыУказанияСерийТЧ = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	
	ПараметрыУказанияСерийТЧ.ПолноеИмяОбъекта	= "Документ.ПроизводствоБезЗаказа";
	ПараметрыУказанияСерийТЧ.ИмяПоляСклад		= "Получатель";
	ПараметрыУказанияСерийТЧ.ИмяТЧТовары		= "ВыходныеИзделия";
	ПараметрыУказанияСерийТЧ.ИмяТЧСерии			= "ВыходныеИзделияСерии";
	
	ПараметрыУказанияСерийТЧ.УчитыватьСебестоимостьПоСериям	= ПолучитьФункциональнуюОпцию("УчитыватьСебестоимостьПоСериямСклад", Новый Структура());
	ПараметрыУказанияСерийТЧ.ИспользоватьСерииНоменклатуры	= ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатурыСклад", Новый Структура());
	
	ПараметрыУказанияСерийТЧ.ИменаПолейСтатусУказанияСерий.Добавить("СтатусУказанияСерий");
	
	ПараметрыУказанияСерийТЧ.ПоляСвязи.Добавить("Назначение");
	ПараметрыУказанияСерийТЧ.ПоляСвязи.Добавить("Получатель");
	
	ПараметрыУказанияСерийТЧ.СкладскиеОперации.Добавить(Перечисления.СкладскиеОперации.ПриемкаПродукцииИзПроизводства);
	
	ПараметрыУказанияСерийТЧ.ЭтоНакладная = Истина;
	
	ПараметрыУказанияСерийТЧ.СерииПриПланированииОтгрузкиУказываютсяВТЧСерии = Истина;
	ПараметрыУказанияСерийТЧ.ПланированиеОтгрузки = Ложь;
	ПараметрыУказанияСерийТЧ.ПланированиеОтбора   = Ложь;
	ПараметрыУказанияСерийТЧ.ФактОтбора           = Истина;
	
	ПараметрыУказанияСерийТЧ.РегистрироватьСерии = Истина;
	
	ПараметрыУказанияСерийТЧ.Дата = Объект.Дата;
	
	ПараметрыУказанияСерий.Вставить("ВыходныеИзделия", ПараметрыУказанияСерийТЧ);
	
	#КонецОбласти
	
	#Область ПобочныеИзделия
	
	ПараметрыУказанияСерийТЧ = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	
	ПараметрыУказанияСерийТЧ.ПолноеИмяОбъекта	= "Документ.ПроизводствоБезЗаказа";
	ПараметрыУказанияСерийТЧ.ИмяПоляСклад		= "Получатель";
	ПараметрыУказанияСерийТЧ.ИмяТЧТовары		= "ПобочныеИзделия";
	ПараметрыУказанияСерийТЧ.ИмяТЧСерии			= "ПобочныеИзделияСерии";
	
	ПараметрыУказанияСерийТЧ.УчитыватьСебестоимостьПоСериям	= ПолучитьФункциональнуюОпцию("УчитыватьСебестоимостьПоСериямСклад", Новый Структура());
	ПараметрыУказанияСерийТЧ.ИспользоватьСерииНоменклатуры	= ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатурыСклад", Новый Структура());
	
	ПараметрыУказанияСерийТЧ.ИменаПолейСтатусУказанияСерий.Добавить("СтатусУказанияСерий");
	
	ПараметрыУказанияСерийТЧ.ПоляСвязи.Добавить("Назначение");
	ПараметрыУказанияСерийТЧ.ПоляСвязи.Добавить("Получатель");
	
	ПараметрыУказанияСерийТЧ.СкладскиеОперации.Добавить(Перечисления.СкладскиеОперации.ПриемкаПродукцииИзПроизводства);
	
	ПараметрыУказанияСерийТЧ.ЭтоНакладная = Истина;
	
	ПараметрыУказанияСерийТЧ.СерииПриПланированииОтгрузкиУказываютсяВТЧСерии = Истина;
	ПараметрыУказанияСерийТЧ.ПланированиеОтгрузки = Ложь;
	ПараметрыУказанияСерийТЧ.ПланированиеОтбора   = Ложь;
	ПараметрыУказанияСерийТЧ.ФактОтбора           = Истина;
	
	ПараметрыУказанияСерийТЧ.РегистрироватьСерии = Истина;
	
	ПараметрыУказанияСерийТЧ.Дата = Объект.Дата;
	
	ПараметрыУказанияСерий.Вставить("ПобочныеИзделия", ПараметрыУказанияСерийТЧ);
	
	#КонецОбласти
	
	#Область МатериалыИРаботы
	
	ПараметрыУказанияСерийТЧ = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	
	ПараметрыУказанияСерийТЧ.ПолноеИмяОбъекта	= "Документ.ПроизводствоБезЗаказа";
	ПараметрыУказанияСерийТЧ.ИмяПоляСклад		= "Отправитель";
	ПараметрыУказанияСерийТЧ.ИмяТЧТовары		= "МатериалыИРаботы";
	ПараметрыУказанияСерийТЧ.ИмяТЧСерии			= "МатериалыИРаботы";
	
	ПараметрыУказанияСерийТЧ.УчитыватьСебестоимостьПоСериям	= ПолучитьФункциональнуюОпцию("УчитыватьСебестоимостьПоСериямСклад", Новый Структура());
	ПараметрыУказанияСерийТЧ.ИспользоватьСерииНоменклатуры	= ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатурыСклад", Новый Структура());
	
	ПараметрыУказанияСерийТЧ.ИменаПолейСтатусУказанияСерий.Добавить("СтатусУказанияСерий");
	
	ПараметрыУказанияСерийТЧ.ПоляСвязи.Добавить("Отправитель");
	
	ПараметрыУказанияСерийТЧ.СкладскиеОперации.Добавить(Перечисления.СкладскиеОперации.ПередачаВПроизводствоОтгрузка);
	ПараметрыУказанияСерийТЧ.СкладскиеОперации.Добавить(Перечисления.СкладскиеОперации.ПередачаВПроизводствоПриемка);
	
	ПараметрыУказанияСерийТЧ.ЭтоНакладная = Истина;
	
	ПараметрыУказанияСерийТЧ.ПланированиеОтгрузки = Ложь;
	ПараметрыУказанияСерийТЧ.ПланированиеОтбора   = Ложь;
	ПараметрыУказанияСерийТЧ.ФактОтбора           = Истина;
	
	ПараметрыУказанияСерийТЧ.РегистрироватьСерии = НоменклатураКлиентСервер.НеобходимоРегистрироватьСерии(ПараметрыУказанияСерийТЧ);
	
	ПараметрыУказанияСерийТЧ.Дата = Объект.Дата;
	
	ПараметрыУказанияСерий.Вставить("МатериалыИРаботы", ПараметрыУказанияСерийТЧ);
	
	#КонецОбласти
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Возвращает текст запроса для расчета статусов указания серий.
//	Параметры:
//		ПараметрыУказанияСерий - Структура - состав полей задается в функции НоменклатураКлиентСервер.ПараметрыУказанияСерий.
//	Возвращаемое значение:
//		Строка - текст запроса.
//
Функция ТекстЗапросаЗаполненияСтатусовУказанияСерий(ПараметрыУказанияСерий) Экспорт
	
	ТекстЗапроса = "";
	
	Если ПараметрыУказанияСерий.ИмяТЧТовары = "ВыходныеИзделия" 
		ИЛИ ПараметрыУказанияСерий.ИмяТЧТовары = "ПобочныеИзделия" Тогда
		ТекстЗапроса = ТекстЗапросаЗаполненияСтатусовУказанияСерийВыходныеИзделия();
	ИначеЕсли ПараметрыУказанияСерий.ИмяТЧТовары = "МатериалыИРаботы" Тогда
		ТекстЗапроса = ТекстЗапросаЗаполненияСтатусовУказанияСерийМатериалыИРаботы();
	КонецЕсли;
	
	Возврат ТекстЗапроса;

КонецФункции

#КонецОбласти

#Область Заполнение

// Формирует список документов, на основании переданных параметров.
//	Параметры:
//		Параметры - Структура - параметры для формирования документов.
//			Имеет структуру {ИзделияПоСпецификации, ДанныеШапки, ГруппировкаЗатрат}, все ключи являются обязательными.
//			ИзделияПоСпецификации - таблица значений, содержащая перечень выпущенной продукции, распределяемой по правилам.
//			ДанныеШапки - Структура - данные заполнения шапки документов.
//			ГруппировкаЗатрат - тип группировки затрат.
//		СписокОбъектов - Структура - список сформированных документов.
//
Процедура ДокументыПоПараметрам(Параметры, СписокОбъектов) Экспорт
	
	ТаблицаПродукции = ПолучитьИзВременногоХранилища(Параметры.ИзделияПоСпецификации);
	
	ЗаполнитьГруппыЗатратВКоллекции(ТаблицаПродукции);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ДанныеСпецификацииПоСпискуПродукции(ТаблицаПродукции, Параметры, МенеджерВременныхТаблиц);
	
	СформироватьДокументы(Параметры, МенеджерВременныхТаблиц, СписокОбъектов);
	
КонецПроцедуры

#КонецОбласти

#Область ГруппировкаЗатрат

// Возвращает перечень полей, идентифицирующих группу затрат.
//	Параметры:
//		ГруппировкаЗатрат - ПеречислениеСсылка.ГруппировкиЗатратВПроизводствеБезЗаказа - тип группировки затрат.
//	Возвращаемое значение:
//		СписокЗначений - перечень полей, идентифицирующих группу затрат.
//
Функция ПереченьПолейГруппыЗатрат(ГруппировкаЗатрат) Экспорт
	
	СписокПолей = Новый СписокЗначений;
	
	Если ГруппировкаЗатрат = Перечисления.ГруппировкиЗатратВПроизводствеБезЗаказа.ПоПродукции Тогда
		СписокПолей.Добавить("Номенклатура",	НСтр("ru = 'Номенклатура'"));
		СписокПолей.Добавить("Характеристика",	НСтр("ru = 'Характеристика'"));
	ИначеЕсли ГруппировкаЗатрат = Перечисления.ГруппировкиЗатратВПроизводствеБезЗаказа.ПоСпецификациям Тогда
		СписокПолей.Добавить("Спецификация",	НСтр("ru = 'Спецификация'"));
	ИначеЕсли ГруппировкаЗатрат = Перечисления.ГруппировкиЗатратВПроизводствеБезЗаказа.ПоСпецификациямИНазначениям Тогда
		СписокПолей.Добавить("Спецификация",	НСтр("ru = 'Спецификация'"));
		СписокПолей.Добавить("Назначение",		НСтр("ru = 'Назначение'"));
	ИначеЕсли ГруппировкаЗатрат = Перечисления.ГруппировкиЗатратВПроизводствеБезЗаказа.ПоПродукцииИНазначениям Тогда
		СписокПолей.Добавить("Номенклатура",	НСтр("ru = 'Номенклатура'"));
		СписокПолей.Добавить("Характеристика",	НСтр("ru = 'Характеристика'"));
		СписокПолей.Добавить("Назначение",		НСтр("ru = 'Назначение'"));
	КонецЕсли;
	
	Возврат СписокПолей;
	
КонецФункции

// Возвращает строку, содержащую перечень путей получения идентифицирующих группу затрат полей.
//	Параметры:
//		ИмяТаблицы - Строка - имя источника получения идентифицирующих группу затрат полей.
//		СписокПолей - СписокЗначений - перечень полей, идентифицирующих группу затрат.
//	Возвращаемое значение:
//		Строка - строка, содержащая перечень путей получения идентифицирующих группу затрат полей.
//
Функция ТекстЗапросаПоПолямГруппыЗатрат(ИмяТаблицы, СписокПолей) Экспорт
	
	ТекстЗапросаПоПолям = "";
	
	Для Каждого ТекПоле Из СписокПолей Цикл
		
		ТекстЗапросаПоПолям = ТекстЗапросаПоПолям + ", " + ИмяТаблицы + "." + ТекПоле.Значение;
		
	КонецЦикла;
	
	Возврат ТекстЗапросаПоПолям;
	
КонецФункции

// Возвращает структуру, содержащую перечень идентифицирующих группу затрат полей.
//	Параметры:
//		СписокПолей - СписокЗначений - перечень полей, идентифицирующих группу затрат.
//	Возвращаемое значение:
//		Структура - структура, содержащая перечень идентифицирующих группу затрат полей.
//
Функция СтруктураПоискаГруппыЗатрат(СписокПолей) Экспорт
	
	СтруктураПоиска = Новый Структура;
	
	Для Каждого ТекПоле Из СписокПолей Цикл
		СтруктураПоиска.Вставить(ТекПоле.Значение);
	КонецЦикла;
	
	Возврат СтруктураПоиска;
	
КонецФункции

#КонецОбласти

// Инициализирует параметры, обслуживающие выбор назначений в формах документа.
// 
//  Возвращаемое значение:
//  Структура - структура параметров, см. Справочники.Назначения.МакетФормыВыбораНазначений().
//
Функция МакетФормыВыбораНазначений() Экспорт
	
	МакетФормы = Справочники.Назначения.МакетФормыВыбораНазначений();
	
	// Шаблон для выходных изделий
	ШаблонНазначения = Справочники.Назначения.ДобавитьШаблонНазначений(МакетФормы, "Объект.ВыходныеИзделия.Назначение");
	ШаблонНазначения.НаправлениеДеятельности  = "Объект.НаправлениеДеятельности";
	ШаблонНазначения.Давальческое2_1          = "ЛОЖЬ";
	
	// Потребности в выпущенной продукции на складе.
	ОписаниеКолонок = Справочники.Назначения.ДобавитьОписаниеКолонок(МакетФормы, "ОбеспечениеЗаказов", Истина, "Объект.ВыходныеИзделия.Назначение", "ВыходныеИзделияПотребность");
	ОписаниеКолонок.Колонки.НайтиПоЗначению("Потребность").Пометка = Истина;
	ОписаниеКолонок.УсловиеИспользования = "Объект.ВыходныеИзделия.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)";
	
	ОписаниеКолонок.ПутиКДанным.Номенклатура     = "Объект.ВыходныеИзделия.Номенклатура";
	ОписаниеКолонок.ПутиКДанным.Характеристика   = "Объект.ВыходныеИзделия.Характеристика";
	ОписаниеКолонок.ПутиКДанным.Склад            = "Объект.ВыходныеИзделия.Получатель";
	
	// Потребности в выпущенных работах в подразделении-получателе.
	ОписаниеКолонок = Справочники.Назначения.ДобавитьОписаниеКолонок(МакетФормы, "ОбеспечениеЗаказовРаботами", Истина, "Объект.ВыходныеИзделия.Назначение", "ВыходныеИзделияПотребностьВРаботах");
	ОписаниеКолонок.Колонки.НайтиПоЗначению("Потребность").Пометка = Истина;
	ОписаниеКолонок.УсловиеИспользования = "Объект.ВыходныеИзделия.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)";
	
	ОписаниеКолонок.ПутиКДанным.Номенклатура     = "Объект.ВыходныеИзделия.Номенклатура";
	ОписаниеКолонок.ПутиКДанным.Характеристика   = "Объект.ВыходныеИзделия.Характеристика";
	ОписаниеКолонок.ПутиКДанным.Подразделение    = "Объект.Подразделение";
	
	// Шаблон для побочных изделий
	ШаблонНазначения = Справочники.Назначения.ДобавитьШаблонНазначений(МакетФормы, "Объект.ПобочныеИзделия.Назначение");
	ШаблонНазначения.НаправлениеДеятельности  = "Объект.ПобочныеИзделия.НаправлениеДеятельности";
	ШаблонНазначения.Давальческое2_1          = "ЛОЖЬ";
	
	// Потребности в побочных изделиях на складе.
	ОписаниеКолонок = Справочники.Назначения.ДобавитьОписаниеКолонок(МакетФормы, "ОбеспечениеЗаказов", Истина, "Объект.ПобочныеИзделия.Назначение", "ПобочныеИзделияПотребность");
	ОписаниеКолонок.Колонки.НайтиПоЗначению("Потребность").Пометка = Истина;
	ОписаниеКолонок.УсловиеИспользования = "Объект.ПобочныеИзделия.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)";
	
	ОписаниеКолонок.ПутиКДанным.Номенклатура     = "Объект.ПобочныеИзделия.Номенклатура";
	ОписаниеКолонок.ПутиКДанным.Характеристика   = "Объект.ПобочныеИзделия.Характеристика";
	ОписаниеКолонок.ПутиКДанным.Склад            = "Объект.ПобочныеИзделия.Получатель";
	
	// Потребности в побочных работах в подразделении-получателе.
	ОписаниеКолонок = Справочники.Назначения.ДобавитьОписаниеКолонок(МакетФормы, "ОбеспечениеЗаказовРаботами", Истина, "Объект.ПобочныеИзделия.Назначение", "ПобочныеИзделияПотребностьВРаботах");
	ОписаниеКолонок.Колонки.НайтиПоЗначению("Потребность").Пометка = Истина;
	ОписаниеКолонок.УсловиеИспользования = "Объект.ПобочныеИзделия.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)";
	
	ОписаниеКолонок.ПутиКДанным.Номенклатура     = "Объект.ПобочныеИзделия.Номенклатура";
	ОписаниеКолонок.ПутиКДанным.Характеристика   = "Объект.ПобочныеИзделия.Характеристика";
	ОписаниеКолонок.ПутиКДанным.Подразделение    = "Объект.Подразделение";
	
	// Шаблон для материалов
	ШаблонНазначения = Справочники.Назначения.ДобавитьШаблонНазначений(МакетФормы, "Объект.МатериалыИРаботы.Назначение");
	ШаблонНазначения.НаправлениеДеятельности  = "Объект.МатериалыИРаботы.НаправлениеДеятельности";
	ШаблонНазначения.Давальческое2_1          = "ЛОЖЬ";
	ШаблонНазначения.ДавальческаяПродукция2_2 = "ЛОЖЬ";
	
	// Остатки материалов в кладовой.
	ОписаниеКолонок = Справочники.Назначения.ДобавитьОписаниеКолонок(МакетФормы, "ОбеспечениеЗаказов", Истина, "Объект.МатериалыИРаботы.Назначение", "МатериалыИРаботыВНаличии");
	ОписаниеКолонок.Колонки.НайтиПоЗначению("ВНаличии").Пометка = Истина;
	ОписаниеКолонок.УсловиеИспользования = "Объект.МатериалыИРаботы.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)";
	
	ОписаниеКолонок.ПутиКДанным.Номенклатура     = "Объект.МатериалыИРаботы.Номенклатура";
	ОписаниеКолонок.ПутиКДанным.Характеристика   = "Объект.МатериалыИРаботы.Характеристика";
	ОписаниеКолонок.ПутиКДанным.Склад            = "Объект.МатериалыИРаботы.Отправитель";
	
	Возврат МакетФормы;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Заполнение

// Формирует перечень групп затрат и заполняет поле "НомерГруппыЗатрат" в переданной коллекции.
//	Параметры:
//		Коллекция - ТаблицаЗначений - коллекция, в которой необходимо заполнить номера групп затрат.
//
Процедура ЗаполнитьГруппыЗатратВКоллекции(Коллекция)
	
	Для Каждого ТекСтрока Из Коллекция Цикл
		Если ЗначениеЗаполнено(ТекСтрока.Назначение) Тогда
			Если ТекСтрока.ГруппировкаЗатрат = Перечисления.ГруппировкиЗатратВПроизводствеБезЗаказа.ПоСпецификациям Тогда
				ТекСтрока.ГруппировкаЗатрат = Перечисления.ГруппировкиЗатратВПроизводствеБезЗаказа.ПоСпецификациямИНазначениям;
			ИначеЕсли ТекСтрока.ГруппировкаЗатрат = Перечисления.ГруппировкиЗатратВПроизводствеБезЗаказа.ПоПродукции Тогда
				ТекСтрока.ГруппировкаЗатрат = Перечисления.ГруппировкиЗатратВПроизводствеБезЗаказа.ПоПродукцииИНазначениям;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаПродукции.Организация,
		|	ТаблицаПродукции.ГруппировкаЗатрат,
		|	ТаблицаПродукции.Подразделение,
		|	ТаблицаПродукции.Номенклатура,
		|	ТаблицаПродукции.Характеристика,
		|	ТаблицаПродукции.Назначение,
		|	ТаблицаПродукции.Спецификация
		|ПОМЕСТИТЬ ТаблицаПродукции
		|ИЗ
		|	&ТаблицаПродукции КАК ТаблицаПродукции
		|ГДЕ
		|	НЕ ТаблицаПродукции.Количество = 0
		|	И НЕ ТаблицаПродукции.ОшибкаВНастройкахМодели
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаПродукции.Организация КАК Организация,
		|	ТаблицаПродукции.ГруппировкаЗатрат КАК ГруппировкаЗатрат,
		|	ТаблицаПродукции.Подразделение КАК Подразделение,
		|	ТаблицаПродукции.Номенклатура,
		|	ТаблицаПродукции.Характеристика,
		|	ТаблицаПродукции.Назначение,
		|	ТаблицаПродукции.Спецификация
		|ИЗ
		|	ТаблицаПродукции КАК ТаблицаПродукции
		|ИТОГИ ПО
		|	Организация,
		|	Подразделение,
		|	ГруппировкаЗатрат";
	
	Запрос.УстановитьПараметр("ТаблицаПродукции",		Коллекция);
	
	РезультатДерево = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	ИндексГруппы = 1;
	
	Для Каждого ТекОрганизация Из РезультатДерево.Строки Цикл
		
		Для Каждого ТекПодразделение Из ТекОрганизация.Строки Цикл
			
			Для Каждого ТекГруппировкаЗатрат Из ТекПодразделение.Строки Цикл
				
				СписокПолей = ПереченьПолейГруппыЗатрат(ТекГруппировкаЗатрат.ГруппировкаЗатрат);
				ОбязательныеПоляГруппировки = "Организация, Подразделение, ГруппировкаЗатрат";
				ПоляГруппировки = "";
				
				Для Каждого ТекПоле Из СписокПолей Цикл
					ПоляГруппировки = ПоляГруппировки + ", " + ТекПоле.Значение;
				КонецЦикла;
				
				ПоляГруппировки = ОбязательныеПоляГруппировки + ПоляГруппировки;
				
				СтруктураПоиска = Новый Структура(ОбязательныеПоляГруппировки);
				ЗаполнитьЗначенияСвойств(СтруктураПоиска, ТекГруппировкаЗатрат);
				
				ГруппыЗатрат = Коллекция.Скопировать(СтруктураПоиска);
				ГруппыЗатрат.Свернуть(ПоляГруппировки);
				
				СтруктураПоиска = Новый Структура(ПоляГруппировки);
				
				Для Каждого ТекГруппа Из ГруппыЗатрат Цикл
					
					ЗаполнитьЗначенияСвойств(СтруктураПоиска, ТекГруппа);
					
					Строки = Коллекция.НайтиСтроки(СтруктураПоиска);
					Для Каждого ТекСтрока Из Строки Цикл
						ТекСтрока.НомерГруппыЗатрат = ИндексГруппы;
					КонецЦикла;
					
					ИндексГруппы = ИндексГруппы + 1;
					
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

// Формирует временные таблицы по данным спецификации для переданного списка продукции.
//
Процедура ДанныеСпецификацииПоСпискуПродукции(ТаблицаПродукции, Параметры, МенеджерВременныхТаблиц) Экспорт
	
	ПереченьДанных = Параметры.ПереченьДанных;
	МассивПродукции = Новый Массив;
	Для Каждого ТекСтрока Из ТаблицаПродукции Цикл
		МассивПродукции.Добавить(ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТекСтрока));
	КонецЦикла;
	
	ДанныеПродукции = Справочники.РесурсныеСпецификации.ДанныеСпецификацииПоСпискуНоменклатуры(
			МассивПродукции,
			Ложь,
			Истина,
			Истина,
			ПереченьДанных);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ИнициализироватьТаблицыДанныхСпецификаций(ТаблицаПродукции, МенеджерВременныхТаблиц);
	
	Если Не ПереченьДанных.Найти("ВозвратныеОтходы") = Неопределено Тогда
		Запрос.Текст = Запрос.Текст + ТекстЗапросаВозвратныеОтходы();
	КонецЕсли;
	
	Если Не ПереченьДанных.Найти("МатериалыИУслуги") = Неопределено Тогда
		Запрос.Текст = Запрос.Текст + ТекстЗапросаМатериалы();
	КонецЕсли;
	
	Если Не ПереченьДанных.Найти("Трудозатраты") = Неопределено Тогда
		Запрос.Текст = Запрос.Текст + ТекстЗапросаТрудозатраты();
	КонецЕсли;
	
	Индекс = 0;
	ПолучитьДанныеЗатрат = Не ПереченьДанных.Найти("ВозвратныеОтходы") = Неопределено
							ИЛИ Не ПереченьДанных.Найти("МатериалыИУслуги") = Неопределено
							ИЛИ Не ПереченьДанных.Найти("Трудозатраты") = Неопределено;
	
	Для Каждого ТекСтрока Из ТаблицаПродукции Цикл
		
		Если ДанныеПродукции[Индекс] = Неопределено Тогда
			Индекс = Индекс + 1;
			Продолжить;
		КонецЕсли;
		
		Запрос.УстановитьПараметр("НомерГруппыЗатрат", ТекСтрока.НомерГруппыЗатрат);
		Запрос.УстановитьПараметр("Подразделение", ТекСтрока.Подразделение);
		
		Если Не ПереченьДанных.Найти("ВозвратныеОтходы") = Неопределено Тогда
			Запрос.УстановитьПараметр("ВозвратныеОтходы", ДанныеПродукции[Индекс].ВозвратныеОтходы);
		КонецЕсли;
		
		Если Не ПереченьДанных.Найти("МатериалыИУслуги") = Неопределено Тогда
			Запрос.УстановитьПараметр("МатериалыИУслуги", ДанныеПродукции[Индекс].МатериалыИУслуги);
		КонецЕсли;
		
		Если Не ПереченьДанных.Найти("Трудозатраты") = Неопределено Тогда
			Запрос.УстановитьПараметр("Трудозатраты", ДанныеПродукции[Индекс].Трудозатраты);
		КонецЕсли;
		
		Если ПолучитьДанныеЗатрат Тогда
			Запрос.Выполнить();
		КонецЕсли;
		
		Индекс = Индекс + 1;
	КонецЦикла;
	
	ЗаполнитьНазначениеМатериаловПоОбособленнымОстаткам(МенеджерВременныхТаблиц, Параметры);
	
КонецПроцедуры

// Заполняет назначение материалов на основании остатков
//
Процедура ЗаполнитьНазначениеМатериаловПоОбособленнымОстаткам(МенеджерВременныхТаблиц, Параметры)
	
	Если Параметры.ПереченьДанных.Найти("МатериалыИУслуги") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаПродукции.НомерГруппыЗатрат	КАК НомерГруппыЗатрат,
		|	ТаблицаПродукции.Организация		КАК Организация,
		|	ТаблицаПродукции.Назначение			КАК Назначение,
		|	НазначенияДавальца.Ссылка			КАК НазначениеДавальца
		|ПОМЕСТИТЬ НазначенияПродукции
		|ИЗ
		|	ТаблицаПродукции КАК ТаблицаПродукции
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК СпрНазначения
		|	ПО СпрНазначения.Ссылка = ТаблицаПродукции.Назначение
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК НазначенияДавальца
		|	ПО НазначенияДавальца.Заказ = НЕОПРЕДЕЛЕНО
		|		И НазначенияДавальца.НаправлениеДеятельности = СпрНазначения.НаправлениеДеятельности
		|		И НазначенияДавальца.Партнер = СпрНазначения.Партнер
		|		И НЕ НазначенияДавальца.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
		|ГДЕ
		|	ТаблицаПродукции.ГруппировкаЗатрат В (ЗНАЧЕНИЕ(Перечисление.ГруппировкиЗатратВПроизводствеБезЗаказа.ПоПродукцииИНазначениям), ЗНАЧЕНИЕ(Перечисление.ГруппировкиЗатратВПроизводствеБезЗаказа.ПоСпецификациямИНазначениям))
		|	И НЕ ТаблицаПродукции.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаПродукции.Организация,
		|	ТаблицаПродукции.Назначение,
		|	ТаблицаПродукции.НомерГруппыЗатрат,
		|	НазначенияДавальца.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МатериалыИУслуги.НомерГруппыЗатрат	КАК НомерГруппыЗатрат,
		|	КлючиАналитики.КлючАналитики		КАК АналитикаУчетаНоменклатуры,
		|	НазначенияПродукции.Организация		КАК Организация,
		|	НазначенияПродукции.Назначение		КАК Назначение
		|ПОМЕСТИТЬ АналитикаОстатков
		|ИЗ
		|	МатериалыИУслуги КАК МатериалыИУслуги
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НазначенияПродукции КАК НазначенияПродукции
		|		ПО МатериалыИУслуги.НомерГруппыЗатрат = НазначенияПродукции.НомерГруппыЗатрат
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиАналитики
		|		ПО (КлючиАналитики.Номенклатура = МатериалыИУслуги.Номенклатура)
		|			И (КлючиАналитики.Характеристика = МатериалыИУслуги.Характеристика)
		|			И (КлючиАналитики.Склад = МатериалыИУслуги.Отправитель)
		|			И (КлючиАналитики.Назначение = НазначенияПродукции.Назначение)
		|			И (КлючиАналитики.СтатьяКалькуляции = ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка))
		|
		|СГРУППИРОВАТЬ ПО
		|	НазначенияПродукции.Назначение,
		|	НазначенияПродукции.Организация,
		|	КлючиАналитики.КлючАналитики,
		|	МатериалыИУслуги.НомерГруппыЗатрат
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	МатериалыИУслуги.НомерГруппыЗатрат		КАК НомерГруппыЗатрат,
		|	КлючиАналитики.КлючАналитики			КАК АналитикаУчетаНоменклатуры,
		|	НазначенияПродукции.Организация			КАК Организация,
		|	НазначенияПродукции.НазначениеДавальца	КАК Назначение
		|ИЗ
		|	МатериалыИУслуги КАК МатериалыИУслуги
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НазначенияПродукции КАК НазначенияПродукции
		|		ПО МатериалыИУслуги.НомерГруппыЗатрат = НазначенияПродукции.НомерГруппыЗатрат
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиАналитики
		|		ПО (КлючиАналитики.Номенклатура = МатериалыИУслуги.Номенклатура)
		|			И (КлючиАналитики.Характеристика = МатериалыИУслуги.Характеристика)
		|			И (КлючиАналитики.Склад = МатериалыИУслуги.Отправитель)
		|			И (КлючиАналитики.Назначение = НазначенияПродукции.НазначениеДавальца)
		|			И (КлючиАналитики.СтатьяКалькуляции = ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка))
		|ГДЕ
		|	НЕ НазначенияПродукции.НазначениеДавальца = НазначенияПродукции.Назначение
		|СГРУППИРОВАТЬ ПО
		|	НазначенияПродукции.НазначениеДавальца,
		|	НазначенияПродукции.Организация,
		|	КлючиАналитики.КлючАналитики,
		|	МатериалыИУслуги.НомерГруппыЗатрат
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	АналитикаУчетаНоменклатуры,
		|	НазначенияПродукции.Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АналитикаОстатков.НомерГруппыЗатрат,
		|	КлючиАналитики.Номенклатура,
		|	КлючиАналитики.Характеристика,
		|	КлючиАналитики.Склад КАК Отправитель,
		|	АналитикаОстатков.Назначение,
		|	СУММА(ТоварыОрганизацийОстатки.КоличествоОстаток) КАК Количество
		|ИЗ
		|	РегистрНакопления.ТоварыОрганизаций.Остатки(
		|			&Дата,
		|			(АналитикаУчетаНоменклатуры, Организация) В
		|				(ВЫБРАТЬ
		|					АналитикаОстатков.АналитикаУчетаНоменклатуры,
		|					АналитикаОстатков.Организация
		|				ИЗ
		|					АналитикаОстатков КАК АналитикаОстатков)) КАК ТоварыОрганизацийОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ АналитикаОстатков КАК АналитикаОстатков
		|		ПО (АналитикаОстатков.АналитикаУчетаНоменклатуры = ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры)
		|			И (АналитикаОстатков.Организация = ТоварыОрганизацийОстатки.Организация)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиАналитики
		|		ПО (КлючиАналитики.КлючАналитики = ТоварыОрганизацийОстатки.АналитикаУчетаНоменклатуры)
		|
		|СГРУППИРОВАТЬ ПО
		|	КлючиАналитики.Номенклатура,
		|	КлючиАналитики.Характеристика,
		|	КлючиАналитики.Склад,
		|	АналитикаОстатков.Назначение,
		|	АналитикаОстатков.НомерГруппыЗатрат
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МатериалыИУслуги.НомерГруппыЗатрат,
		|	МатериалыИУслуги.Отправитель,
		|	МатериалыИУслуги.Номенклатура,
		|	МатериалыИУслуги.Характеристика,
		|	МатериалыИУслуги.Назначение,
		|	МатериалыИУслуги.СтатьяКалькуляции,
		|	МатериалыИУслуги.Упаковка,
		|	СУММА(МатериалыИУслуги.КоличествоУпаковок) КАК КоличествоУпаковок,
		|	СУММА(МатериалыИУслуги.Количество) КАК Количество
		|ИЗ
		|	МатериалыИУслуги КАК МатериалыИУслуги
		|
		|СГРУППИРОВАТЬ ПО
		|	МатериалыИУслуги.Назначение,
		|	МатериалыИУслуги.СтатьяКалькуляции,
		|	МатериалыИУслуги.Упаковка,
		|	МатериалыИУслуги.Отправитель,
		|	МатериалыИУслуги.Номенклатура,
		|	МатериалыИУслуги.Характеристика,
		|	МатериалыИУслуги.НомерГруппыЗатрат
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ МатериалыИУслуги";
		
	Запрос.УстановитьПараметр("Дата", Параметры.ДанныеШапки.Дата);
	Результат = Запрос.ВыполнитьПакет();
	
	Индекс = Результат.ВГраница();
	ОстаткиМатериалов	= Результат[Индекс - 2].Выбрать();
	МатериалыИУслуги	= Результат[Индекс - 1].Выгрузить();
	
	ПоляОтбора = "НомерГруппыЗатрат, Номенклатура, Характеристика, Отправитель";
	МатериалыИУслуги.Индексы.Добавить(ПоляОтбора);
	ОтборТоваров = Новый Структура(ПоляОтбора);
	
	Пока ОстаткиМатериалов.Следующий() Цикл
		
		КоличествоТоваров = ОстаткиМатериалов.Количество;
		
		ЗаполнитьЗначенияСвойств(ОтборТоваров, ОстаткиМатериалов);
		
		Для Каждого СтрокаМатериалов Из МатериалыИУслуги.НайтиСтроки(ОтборТоваров) Цикл
			
			Если СтрокаМатериалов.Количество = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Количество = Мин(КоличествоТоваров, СтрокаМатериалов.Количество);
			
			НоваяСтрока = МатериалыИУслуги.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаМатериалов);
			
			НоваяСтрока.Назначение			= ОстаткиМатериалов.Назначение;
			НоваяСтрока.Количество			= Количество;
			НоваяСтрока.КоличествоУпаковок	= Количество / Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(НоваяСтрока.Упаковка, НоваяСтрока.Номенклатура);
			
			СтрокаМатериалов.Количество = СтрокаМатериалов.Количество - НоваяСтрока.Количество;
			СтрокаМатериалов.КоличествоУпаковок = СтрокаМатериалов.КоличествоУпаковок - НоваяСтрока.КоличествоУпаковок;
			
			КоличествоТоваров = КоличествоТоваров - НоваяСтрока.Количество;
			
			Если КоличествоТоваров = 0 Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	МассивУдаляемыхСтрок = МатериалыИУслуги.НайтиСтроки(Новый Структура("Количество", 0));
	Для Каждого СтрокаТаблицы Из МассивУдаляемыхСтрок Цикл
		МатериалыИУслуги.Удалить(СтрокаТаблицы);
	КонецЦикла;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	МатериалыИУслуги.НомерГруппыЗатрат,
		|	МатериалыИУслуги.Отправитель,
		|	МатериалыИУслуги.Номенклатура,
		|	МатериалыИУслуги.Характеристика,
		|	МатериалыИУслуги.Назначение,
		|	МатериалыИУслуги.СтатьяКалькуляции,
		|	МатериалыИУслуги.Упаковка,
		|	МатериалыИУслуги.КоличествоУпаковок КАК КоличествоУпаковок,
		|	МатериалыИУслуги.Количество КАК Количество
		|ПОМЕСТИТЬ МатериалыИУслуги
		|ИЗ
		|	&МатериалыИУслуги КАК МатериалыИУслуги";
	Запрос.УстановитьПараметр("МатериалыИУслуги", МатериалыИУслуги);
	Запрос.Выполнить();
КонецПроцедуры

// Создает и заполняет документы на основании переданных параметров.
//
Процедура СформироватьДокументы(Параметры, МенеджерВременныхТаблиц, СписокОбъектов)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапросаПродукцияПоГруппамЗатрат()
					+ ТекстЗапросаВозвратныеОтходыПоГруппамЗатрат()
					+ ТекстЗапросаМатериалыПоГруппамЗатрат()
					+ ТекстЗапросаТрудозатратыПоГруппамЗатрат();
		
	ВидЦен = Справочники.ВидыЦен.ВидЦеныПлановойСтоимостиТМЦ();
	
	Запрос.УстановитьПараметр("Дата", Параметры.ДанныеШапки.Дата);
	Запрос.УстановитьПараметр("ВидЦены", ВидЦен);
	Запрос.УстановитьПараметр("ЗаполнятьАвтоматически", Параметры.ЗаполнятьАвтоматически);
	
	Результат = Запрос.ВыполнитьПакет();
	Индекс = Результат.ВГраница();
	
	Продукция			= Результат[Индекс - 3].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	ВозвратныеОтходы	= Результат[Индекс - 2].Выгрузить();
	МатериалыИРаботы	= Результат[Индекс - 1].Выгрузить();
	Трудозатраты		= Результат[Индекс].Выгрузить();
	
	СтруктураПоиска = Новый Структура("НомерГруппыЗатрат");
	
	Для Каждого ТекОрганизация Из Продукция.Строки Цикл
		Для Каждого ТекГруппировкаЗатрат Из ТекОрганизация.Строки Цикл
			Для Каждого ТекПодразделение Из ТекГруппировкаЗатрат.Строки Цикл
				
				ТекДокумент = Документы.ПроизводствоБезЗаказа.СоздатьДокумент();
				ТекДокумент.ПолучитьСсылкуНового();
				ТекДокумент.ВидЦены = ВидЦен;
				
				СписокОбъектов.Добавить(ТекДокумент);
				
				ТекДокумент.Заполнить(Новый Структура("Организация", ТекПодразделение.Организация));
				
				ЗаполнитьЗначенияСвойств(ТекДокумент, Параметры.ДанныеШапки);
				ЗаполнитьЗначенияСвойств(ТекДокумент, ТекПодразделение);
				
				СписокГруппЗатрат = Новый Массив;
				
				Для Каждого ТекИзделие Из ТекПодразделение.Строки Цикл
					НоваяСтрока = ТекДокумент.ВыходныеИзделия.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекИзделие);
					СписокГруппЗатрат.Добавить(ТекИзделие.НомерГруппыЗатрат);
				КонецЦикла;
				
				Для Каждого ТекГруппа Из СписокГруппЗатрат Цикл
					СтруктураПоиска.НомерГруппыЗатрат = ТекГруппа;
					
					Строки = ВозвратныеОтходы.Скопировать(ВозвратныеОтходы.НайтиСтроки(СтруктураПоиска));
					ТекДокумент.ПобочныеИзделия.Загрузить(Строки);
					
					Строки = Трудозатраты.Скопировать(Трудозатраты.НайтиСтроки(СтруктураПоиска));
					ТекДокумент.Трудозатраты.Загрузить(Строки);
					
					Строки = МатериалыИРаботы.Скопировать(МатериалыИРаботы.НайтиСтроки(СтруктураПоиска));
					ТекДокумент.МатериалыИРаботы.Загрузить(Строки);
				КонецЦикла;
				
				ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ТекДокумент, Документы.ПроизводствоБезЗаказа);
				НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ТекДокумент, ПараметрыУказанияСерий.ВыходныеИзделия);
				НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ТекДокумент, ПараметрыУказанияСерий.ПобочныеИзделия);
				НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ТекДокумент, ПараметрыУказанияСерий.МатериалыИРаботы);
				
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

// Помещает таблицу продукции в ВТ, создает пустые ВТ для затрат
//
Функция ИнициализироватьТаблицыДанныхСпецификаций(ТаблицаПродукции, МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	ТекстВозвратныеОтходы = "";
	ТекстМатериалыИРаботы = "";
	ТекстТрудозатраты = "";
	
	ТекстВозвратныеОтходы =
		"ВЫБРАТЬ ПЕРВЫЕ 0
		|	ПобочныеИзделия.НомерГруппыЗатрат КАК НомерГруппыЗатрат,
		|	ПобочныеИзделия.Подразделение КАК Подразделение,
		|	ПобочныеИзделия.НаправлениеВыпуска КАК НаправлениеВыпуска,
		|	ПобочныеИзделия.Получатель КАК Получатель,
		|	ПобочныеИзделия.Номенклатура КАК Номенклатура,
		|	ПобочныеИзделия.Характеристика КАК Характеристика,
		|	ПобочныеИзделия.Назначение КАК Назначение,
		|	ПобочныеИзделия.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	ПобочныеИзделия.Упаковка КАК Упаковка,
		|	ПобочныеИзделия.КоличествоУпаковок КАК КоличествоУпаковок,
		|	ПобочныеИзделия.Количество КАК Количество
		|ПОМЕСТИТЬ ВозвратныеОтходы
		|ИЗ
		|	Документ.ПроизводствоБезЗаказа.ПобочныеИзделия КАК ПобочныеИзделия
		|;
		|";
	
	ТекстМатериалыИРаботы =
		"ВЫБРАТЬ ПЕРВЫЕ 0
		|	МатериалыИРаботы.НомерГруппыЗатрат КАК НомерГруппыЗатрат,
		|	МатериалыИРаботы.Отправитель КАК Отправитель,
		|	МатериалыИРаботы.Номенклатура КАК Номенклатура,
		|	МатериалыИРаботы.Характеристика КАК Характеристика,
		|	МатериалыИРаботы.Назначение КАК Назначение,
		|	МатериалыИРаботы.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	МатериалыИРаботы.Упаковка КАК Упаковка,
		|	МатериалыИРаботы.КоличествоУпаковок КАК КоличествоУпаковок,
		|	МатериалыИРаботы.Количество КАК Количество
		|ПОМЕСТИТЬ МатериалыИУслуги
		|ИЗ
		|	Документ.ПроизводствоБезЗаказа.МатериалыИРаботы КАК МатериалыИРаботы
		|;
		|";
	
	ТекстТрудозатраты =
		"ВЫБРАТЬ ПЕРВЫЕ 0
		|	Трудозатраты.НомерГруппыЗатрат КАК НомерГруппыЗатрат,
		|	Трудозатраты.Подразделение КАК Подразделение,
		|	Трудозатраты.ВидРабот КАК ВидРабот,
		|	Трудозатраты.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	Трудозатраты.Количество КАК Количество
		|ПОМЕСТИТЬ Трудозатраты
		|ИЗ
		|	Документ.ПроизводствоБезЗаказа.Трудозатраты КАК Трудозатраты
		|;
		|";
	
	Запрос.Текст = ТекстЗапросаПродукция()
					+ ТекстВозвратныеОтходы
					+ ТекстМатериалыИРаботы
					+ ТекстТрудозатраты;
	Запрос.УстановитьПараметр("ТаблицаПродукции", ТаблицаПродукции);
	Запрос.Выполнить();
	
КонецФункции

// Возвращает текст запроса для помещения таблицы продукции в ВТ.
//	Возвращаемое значение:
//		Строка - текст запроса.
//
Функция ТекстЗапросаПродукция()
	Возврат
		"ВЫБРАТЬ
		|	ТаблицаПродукции.НомерГруппыЗатрат,
		|	ТаблицаПродукции.Организация,
		|	ТаблицаПродукции.ГруппировкаЗатрат,
		|	ТаблицаПродукции.Подразделение,
		|	ТаблицаПродукции.НаправлениеВыпуска,
		|	ТаблицаПродукции.Получатель,
		|	ТаблицаПродукции.Номенклатура,
		|	ТаблицаПродукции.Характеристика,
		|	ТаблицаПродукции.Серия,
		|	ТаблицаПродукции.Назначение,
		|	ТаблицаПродукции.Спецификация,
		|	ТаблицаПродукции.Количество
		|ПОМЕСТИТЬ ТаблицаПродукции
		|ИЗ
		|	&ТаблицаПродукции КАК ТаблицаПродукции
		|ГДЕ
		|	НЕ ТаблицаПродукции.Количество = 0
		|	И НЕ ТаблицаПродукции.ОшибкаВНастройкахМодели
		|;
		|";
		
КонецФункции

// Возвращает текст запроса объединения данных спецификации с существующей ВТ
// в части возвратных отходов.
//	Возвращаемое значение:
//		Строка - текст запроса.
//
Функция ТекстЗапросаВозвратныеОтходы()
	Возврат
		"ВЫБРАТЬ
		|	&НомерГруппыЗатрат КАК НомерГруппыЗатрат,
		|	ВозвратныеОтходы.ПодразделениеЭтапа КАК Подразделение,
		|	ВЫБОР
		|		КОГДА ВозвратныеОтходы.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОПерации.ВыпускПродукцииВПодразделение)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОПерации.ВыпускПродукцииНаСклад)
		|	КОНЕЦ КАК НаправлениеВыпуска,
		|	ВЫБОР
		|		КОГДА ВозвратныеОтходы.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		|			ТОГДА &Подразделение
		|		ИНАЧЕ ВозвратныеОтходы.Получатель2_2
		|	КОНЕЦ КАК Получатель,
		|	ВозвратныеОтходы.Номенклатура,
		|	ВозвратныеОтходы.Характеристика,
		|	ВозвратныеОтходы.СтатьяКалькуляции,
		|	ВозвратныеОтходы.Упаковка,
		|	ВозвратныеОтходы.КоличествоУпаковок КАК КоличествоУпаковок,
		|	ВозвратныеОтходы.Количество КАК Количество
		|ПОМЕСТИТЬ ВозвратныеОтходыСпецификации
		|ИЗ
		|	&ВозвратныеОтходы КАК ВозвратныеОтходы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВозвратныеОтходы.НомерГруппыЗатрат,
		|	ВозвратныеОтходы.Подразделение,
		|	ВозвратныеОтходы.НаправлениеВыпуска,
		|	ВозвратныеОтходы.Получатель,
		|	ВозвратныеОтходы.Номенклатура,
		|	ВозвратныеОтходы.Характеристика,
		|	ВозвратныеОтходы.СтатьяКалькуляции,
		|	ВозвратныеОтходы.Упаковка,
		|	ВозвратныеОтходы.КоличествоУпаковок,
		|	ВозвратныеОтходы.Количество
		|ПОМЕСТИТЬ ВозвратныеОтходыПодготовительная
		|ИЗ
		|	ВозвратныеОтходыСпецификации КАК ВозвратныеОтходы
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВозвратныеОтходы.НомерГруппыЗатрат,
		|	ВозвратныеОтходы.Подразделение,
		|	ВозвратныеОтходы.НаправлениеВыпуска,
		|	ВозвратныеОтходы.Получатель,
		|	ВозвратныеОтходы.Номенклатура,
		|	ВозвратныеОтходы.Характеристика,
		|	ВозвратныеОтходы.СтатьяКалькуляции,
		|	ВозвратныеОтходы.Упаковка,
		|	ВозвратныеОтходы.КоличествоУпаковок,
		|	ВозвратныеОтходы.Количество
		|ИЗ
		|	ВозвратныеОтходы КАК ВозвратныеОтходы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВозвратныеОтходы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВозвратныеОтходыСпецификации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВозвратныеОтходы.НомерГруппыЗатрат,
		|	ВозвратныеОтходы.Подразделение,
		|	ВозвратныеОтходы.НаправлениеВыпуска,
		|	ВозвратныеОтходы.Получатель,
		|	ВозвратныеОтходы.Номенклатура,
		|	ВозвратныеОтходы.Характеристика,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	ВозвратныеОтходы.СтатьяКалькуляции,
		|	ВозвратныеОтходы.Упаковка,
		|	ВозвратныеОтходы.КоличествоУпаковок,
		|	ВозвратныеОтходы.Количество
		|ПОМЕСТИТЬ ВозвратныеОтходы
		|ИЗ
		|	ВозвратныеОтходыПодготовительная КАК ВозвратныеОтходы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВозвратныеОтходыПодготовительная
		|;
		|";
		
КонецФункции

// Возвращает текст запроса объединения данных спецификации с существующей ВТ
// в части материалов и работ.
//	Возвращаемое значение:
//		Строка - текст запроса.
//
Функция ТекстЗапросаМатериалы()
	Возврат
		"ВЫБРАТЬ
		|	&НомерГруппыЗатрат КАК НомерГруппыЗатрат,
		|	МатериалыИУслуги.Склад КАК Отправитель,
		|	МатериалыИУслуги.Номенклатура,
		|	МатериалыИУслуги.Характеристика,
		|	МатериалыИУслуги.СтатьяКалькуляции,
		|	МатериалыИУслуги.Упаковка,
		|	МатериалыИУслуги.КоличествоУпаковок КАК КоличествоУпаковок,
		|	МатериалыИУслуги.Количество КАК Количество
		|ПОМЕСТИТЬ МатериалыИУслугиСпецификации
		|ИЗ
		|	&МатериалыИУслуги КАК МатериалыИУслуги
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МатериалыИУслуги.НомерГруппыЗатрат,
		|	ВЫБОР
		|		КОГДА НЕ СпрСклады.Ссылка ЕСТЬ NULL
		|			И НЕ СпрСклады.ЦеховаяКладовая
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ МатериалыИУслуги.Отправитель
		|	КОНЕЦ КАК Отправитель,
		|	МатериалыИУслуги.Номенклатура,
		|	МатериалыИУслуги.Характеристика,
		|	МатериалыИУслуги.СтатьяКалькуляции,
		|	МатериалыИУслуги.Упаковка,
		|	МатериалыИУслуги.КоличествоУпаковок КАК КоличествоУпаковок,
		|	МатериалыИУслуги.Количество КАК Количество
		|ПОМЕСТИТЬ МатериалыИУслугиПодготовительная
		|ИЗ
		|	МатериалыИУслугиСпецификации КАК МатериалыИУслуги
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК СпрСклады
		|	ПО МатериалыИУслуги.Отправитель = СпрСклады.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	МатериалыИУслуги.НомерГруппыЗатрат,
		|	МатериалыИУслуги.Отправитель,
		|	МатериалыИУслуги.Номенклатура,
		|	МатериалыИУслуги.Характеристика,
		|	МатериалыИУслуги.СтатьяКалькуляции,
		|	МатериалыИУслуги.Упаковка,
		|	МатериалыИУслуги.КоличествоУпаковок,
		|	МатериалыИУслуги.Количество
		|ИЗ
		|	МатериалыИУслуги КАК МатериалыИУслуги
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ МатериалыИУслуги
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ МатериалыИУслугиСпецификации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МатериалыИУслуги.НомерГруппыЗатрат,
		|	МатериалыИУслуги.Отправитель,
		|	МатериалыИУслуги.Номенклатура,
		|	МатериалыИУслуги.Характеристика,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
		|	МатериалыИУслуги.СтатьяКалькуляции,
		|	МатериалыИУслуги.Упаковка,
		|	МатериалыИУслуги.КоличествоУпаковок,
		|	МатериалыИУслуги.Количество
		|ПОМЕСТИТЬ МатериалыИУслуги
		|ИЗ
		|	МатериалыИУслугиПодготовительная КАК МатериалыИУслуги
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ МатериалыИУслугиПодготовительная
		|;
		|";
		
КонецФункции

// Возвращает текст запроса объединения данных спецификации с существующей ВТ
// в части трудозатрат.
//	Возвращаемое значение:
//		Строка - текст запроса.
//
Функция ТекстЗапросаТрудозатраты()
	
	Возврат
		"ВЫБРАТЬ
		|	&НомерГруппыЗатрат КАК НомерГруппыЗатрат,
		|	Трудозатраты.ПодразделениеЭтапа КАК Подразделение,
		|	Трудозатраты.ВидРабот,
		|	Трудозатраты.СтатьяКалькуляции,
		|	Трудозатраты.Количество КАК Количество
		|ПОМЕСТИТЬ ТрудозатратыСпецификации
		|ИЗ
		|	&Трудозатраты КАК Трудозатраты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Трудозатраты.НомерГруппыЗатрат КАК НомерГруппыЗатрат,
		|	Трудозатраты.Подразделение КАК Подразделение,
		|	Трудозатраты.ВидРабот,
		|	Трудозатраты.СтатьяКалькуляции,
		|	Трудозатраты.Количество КАК Количество
		|ПОМЕСТИТЬ ТрудозатратыПодготовительная
		|ИЗ
		|	ТрудозатратыСпецификации КАК Трудозатраты
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Трудозатраты.НомерГруппыЗатрат КАК НомерГруппыЗатрат,
		|	Трудозатраты.Подразделение КАК Подразделение,
		|	Трудозатраты.ВидРабот,
		|	Трудозатраты.СтатьяКалькуляции,
		|	Трудозатраты.Количество КАК Количество
		|ИЗ
		|	Трудозатраты КАК Трудозатраты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ Трудозатраты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТрудозатратыСпецификации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Трудозатраты.НомерГруппыЗатрат КАК НомерГруппыЗатрат,
		|	Трудозатраты.Подразделение КАК Подразделение,
		|	Трудозатраты.ВидРабот,
		|	Трудозатраты.СтатьяКалькуляции,
		|	Трудозатраты.Количество КАК Количество
		|ПОМЕСТИТЬ Трудозатраты
		|ИЗ
		|	ТрудозатратыПодготовительная КАК Трудозатраты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТрудозатратыПодготовительная
		|;
		|";
		
КонецФункции

// Возвращает текст запроса для формирования итоговой таблицы
// в части продукции
Функция ТекстЗапросаПродукцияПоГруппамЗатрат()
	
	Возврат
		"ВЫБРАТЬ
		|	ТаблицаПродукции.НомерГруппыЗатрат,
		|	ТаблицаПродукции.Организация КАК Организация,
		|	ТаблицаПродукции.ГруппировкаЗатрат КАК ГруппировкаЗатрат,
		|	ТаблицаПродукции.Подразделение КАК Подразделение,
		|	ТаблицаПродукции.НаправлениеВыпуска,
		|	ТаблицаПродукции.Получатель,
		|	ТаблицаПродукции.Номенклатура,
		|	ТаблицаПродукции.Характеристика,
		|	ТаблицаПродукции.Серия,
		|	ТаблицаПродукции.Назначение,
		|	ТаблицаПродукции.Спецификация,
		|	СУММА(ТаблицаПродукции.Количество) КАК Количество,
		|	СУММА(ТаблицаПродукции.Количество) КАК КоличествоУпаковок,
		|	СУММА(ЕСТЬNULL(ДолиСтоимости.ДоляСтоимости, 0)) КАК ДоляСтоимости
		|ИЗ
		|	ТаблицаПродукции КАК ТаблицаПродукции
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК ДолиСтоимости
		|		ПО (ДолиСтоимости.Ссылка = ТаблицаПродукции.Спецификация)
		|			И (ДолиСтоимости.Номенклатура = ТаблицаПродукции.Номенклатура)
		|			И (ДолиСтоимости.Характеристика = ТаблицаПродукции.Характеристика
		|				ИЛИ ДолиСтоимости.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаПродукции.Спецификация,
		|	ТаблицаПродукции.Номенклатура,
		|	ТаблицаПродукции.НаправлениеВыпуска,
		|	ТаблицаПродукции.НомерГруппыЗатрат,
		|	ТаблицаПродукции.Характеристика,
		|	ТаблицаПродукции.Организация,
		|	ТаблицаПродукции.Назначение,
		|	ТаблицаПродукции.Получатель,
		|	ТаблицаПродукции.Серия,
		|	ТаблицаПродукции.Подразделение,
		|	ТаблицаПродукции.ГруппировкаЗатрат
		|ИТОГИ ПО
		|	Организация,
		|	ГруппировкаЗатрат,
		|	Подразделение
		|;
		|";
		
КонецФункции

// Возвращает текст запроса для формирования итоговой таблицы
// в части возвратных отходов
//	Возвращаемое значение:
//		Строка - текст запроса.
//
Функция ТекстЗапросаВозвратныеОтходыПоГруппамЗатрат() Экспорт
	Возврат
		"ВЫБРАТЬ
		|	ВозвратныеОтходы.НомерГруппыЗатрат,
		|	ВозвратныеОтходы.Подразделение,
		|	ВозвратныеОтходы.НаправлениеВыпуска,
		|	ВозвратныеОтходы.Получатель,
		|	ВозвратныеОтходы.Номенклатура,
		|	ВозвратныеОтходы.Характеристика,
		|	ВозвратныеОтходы.Назначение,
		|	ВозвратныеОтходы.СтатьяКалькуляции,
		|	ВозвратныеОтходы.Упаковка,
		|	СУММА(ВозвратныеОтходы.КоличествоУпаковок) КАК КоличествоУпаковок,
		|	СУММА(ВозвратныеОтходы.Количество) КАК Количество,
		|	ЦеныНоменклатуры.Цена КАК Цена,
		|	СУММА(ВозвратныеОтходы.Количество * ЦеныНоменклатуры.Цена) КАК Сумма
		|ИЗ
		|	ВозвратныеОтходы КАК ВозвратныеОтходы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&Дата, ) КАК ЦеныНоменклатуры
		|		ПО (ЦеныНоменклатуры.ВидЦены = &ВидЦены)
		|			И (ЦеныНоменклатуры.Номенклатура = ВозвратныеОтходы.Номенклатура)
		|			И (ЦеныНоменклатуры.Характеристика = ВозвратныеОтходы.Характеристика)
		|ГДЕ
		|	&ЗаполнятьАвтоматически
		|
		|СГРУППИРОВАТЬ ПО
		|	ВозвратныеОтходы.Упаковка,
		|	ВозвратныеОтходы.Назначение,
		|	ВозвратныеОтходы.НаправлениеВыпуска,
		|	ВозвратныеОтходы.СтатьяКалькуляции,
		|	ВозвратныеОтходы.Получатель,
		|	ВозвратныеОтходы.Характеристика,
		|	ВозвратныеОтходы.Номенклатура,
		|	ВозвратныеОтходы.Подразделение,
		|	ЦеныНоменклатуры.Цена,
		|	ВозвратныеОтходы.НомерГруппыЗатрат
		|;
		|";
		
КонецФункции

// Возвращает текст запроса для формирования итоговой таблицы
// в части материалов и работ
//	Возвращаемое значение:
//		Строка - текст запроса.
//
Функция ТекстЗапросаМатериалыПоГруппамЗатрат() Экспорт
	Возврат
		"ВЫБРАТЬ
		|	МатериалыИУслуги.НомерГруппыЗатрат,
		|	МатериалыИУслуги.Отправитель,
		|	МатериалыИУслуги.Номенклатура,
		|	МатериалыИУслуги.Характеристика,
		|	МатериалыИУслуги.Назначение,
		|	МатериалыИУслуги.СтатьяКалькуляции,
		|	МатериалыИУслуги.Упаковка,
		|	СУММА(МатериалыИУслуги.КоличествоУпаковок) КАК КоличествоУпаковок,
		|	СУММА(МатериалыИУслуги.Количество) КАК Количество
		|ИЗ
		|	МатериалыИУслуги КАК МатериалыИУслуги
		|ГДЕ
		|	&ЗаполнятьАвтоматически
		|
		|СГРУППИРОВАТЬ ПО
		|	МатериалыИУслуги.Назначение,
		|	МатериалыИУслуги.СтатьяКалькуляции,
		|	МатериалыИУслуги.Упаковка,
		|	МатериалыИУслуги.Отправитель,
		|	МатериалыИУслуги.Номенклатура,
		|	МатериалыИУслуги.Характеристика,
		|	МатериалыИУслуги.НомерГруппыЗатрат
		|;
		|";
		
КонецФункции

// Возвращает текст запроса для формирования итоговой таблицы
// в части трудозатрат
//	Возвращаемое значение:
//		Строка - текст запроса.
//
Функция ТекстЗапросаТрудозатратыПоГруппамЗатрат() Экспорт
	
	Возврат
		"ВЫБРАТЬ
		|	Трудозатраты.НомерГруппыЗатрат,
		|	Трудозатраты.Подразделение,
		|	Трудозатраты.ВидРабот,
		|	Трудозатраты.СтатьяКалькуляции,
		|	СУММА(Трудозатраты.Количество) КАК Количество
		|ИЗ
		|	Трудозатраты КАК Трудозатраты
		|ГДЕ
		|	&ЗаполнятьАвтоматически
		|
		|СГРУППИРОВАТЬ ПО
		|	Трудозатраты.СтатьяКалькуляции,
		|	Трудозатраты.ВидРабот,
		|	Трудозатраты.Подразделение,
		|	Трудозатраты.НомерГруппыЗатрат
		|;
		|";
		
КонецФункции

#КонецОбласти

#Область Проведение

#Область Инициализация

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	// Создание запроса инициализации движений и заполенение его параметров.
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	// Формирование текста запроса.
	ТекстыЗапроса = Новый СписокЗначений;
	
	ТекстЗапросаВтТовары(Запрос, ТекстыЗапроса);
	ТекстЗапросаВтВидыЗапасов(Запрос, ТекстыЗапроса);
	ТекстЗапросаТаблицаВыпускПродукции(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаДатыПоступленияТоваровОрганизаций(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаДвиженияСерийТоваров(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаМатериалыИРаботыВПроизводстве(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаОбеспечениеЗаказов(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаСвободныеОстатки(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаСебестоимостьТоваров(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаТоварыНаСкладах(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаТоварыОрганизаций(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаТоварыОрганизацийКПередаче(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаТрудозатратыКОформлению(Запрос, ТекстыЗапроса, Регистры);
	
	//++ НЕ УТКА
	ТекстЗапросаТаблицаУслугиДавальцуКОформлению(Запрос, ТекстыЗапроса, Регистры);
	//-- НЕ УТКА
	
	ТекстЗапросаДвиженияНоменклатураДоходыРасходы(Запрос, ТекстыЗапроса, Регистры);
	
	// Исполнение запроса и выгрузка полученных таблиц для движений.
	ПроведениеСерверУТ.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)

	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);

	Запрос.Текст =
		"ВЫБРАТЬ
		|	Реквизиты.Дата,
		|	Реквизиты.Ссылка,
		|	Реквизиты.Организация,
		|	Реквизиты.Подразделение,
		|	Реквизиты.ГруппировкаЗатрат,
		|	Реквизиты.ВыпускПодДеятельность,
		|	Реквизиты.НаправлениеДеятельности,
		|	Реквизиты.ВидЦены,
		|	Реквизиты.ВидыЗапасовУказаныВручную,
		|	Реквизиты.Валюта,
		|	Реквизиты.РаспоряжениеДляТрудозатрат
		|ИЗ
		|	Документ.ПроизводствоБезЗаказа КАК Реквизиты
		|ГДЕ
		|	Реквизиты.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	УниверсальныеМеханизмыПартийИСебестоимости.ЗаполнитьПараметрыИнициализации(Запрос, Реквизиты);
	
	Запрос.УстановитьПараметр("ФормироватьВидыЗапасовПоГруппамФинансовогоУчета",
								ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоГруппамФинансовогоУчета"));
	
	Запрос.УстановитьПараметр("АналитическийУчетПоГруппамПродукции",
								ПолучитьФункциональнуюОпцию("АналитическийУчетПоГруппамПродукции"));
	
	ОтборПоАналитикеПартии = Новый Структура("Дата, ВидЦенности", Реквизиты.Дата, Перечисления.ВидыЦенностей.Товары);
	Запрос.УстановитьПараметр("ПустаяАналитикаУчетаПартий",
								Справочники.КлючиАналитикиУчетаПартий.ПолучитьКлючАналитики(ОтборПоАналитикеПартии));
	
	УстановитьПараметрЗапросаВидЗапасовРаботы(Запрос);
	
КонецПроцедуры

Процедура УстановитьПараметрЗапросаВидЗапасовРаботы(Запрос)
	
	Если Запрос.Параметры.Свойство("ВидЗапасовРаботы") Тогда
		Возврат;
	КонецЕсли;
	
	ДопКолонки = Новый Структура("Комитент, Соглашение, Валюта, НалогообложениеНДС, НалогообложениеОрганизации, Поставщик, Подразделение, Менеджер, Сделка");
	ДопКолонки.Вставить("ОбособленныйУчетТоваровПоСделке",	Ложь);
	ДопКолонки.Вставить("ДеятельностьОблагаетсяЕНВД",		Ложь);
	ДопКолонки.Вставить("ВидЗапасов",						Справочники.ВидыЗапасов.ПустаяСсылка());
	ДопКолонки.Вставить("ТипЗапасов",						Перечисления.ТипыЗапасов.Услуга);
	ДопКолонки.Вставить("ГруппаФинансовогоУчета",			Справочники.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка());
	ДопКолонки.Вставить("ВариантОбособленногоУчетаТоваров",	Перечисления.ВариантыОбособленногоУчетаТоваров.НеВедется);
	ДопКолонки.Вставить("Организация",						Запрос.Параметры.Организация);
	ДопКолонки.Вставить("ХозяйственнаяОперация",			Перечисления.ХозяйственныеОперации.ПередачаВПроизводство);
	
	Запрос.УстановитьПараметр("ВидЗапасовРаботы", Справочники.ВидыЗапасов.ВидЗапасовДокумента(ДопКолонки.Организация, ДопКолонки.ХозяйственнаяОперация, ДопКолонки));
	
КонецПроцедуры

Процедура УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос)
	
	Если Запрос.Параметры.Свойство("КоэффициентПересчетаВВалютуУпр")
		И Запрос.Параметры.Свойство("КоэффициентПересчетаВВалютуРегл") Тогда
		Возврат;
	КонецЕсли;
	
	Коэффициенты = РаботаСКурсамивалютУТ.ПолучитьКоэффициентыПересчетаВалюты(Запрос.Параметры.Валюта,
		                                                                         Запрос.Параметры.Валюта,
		                                                                         Запрос.Параметры.Дата);
	
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуУпр",  Коэффициенты.КоэффициентПересчетаВВалютуУпр);
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуРегл", Коэффициенты.КоэффициентПересчетаВВалютуРегл);
	
КонецПроцедуры

Процедура ИнициализироватьКлючиАналитикиНоменклатуры(Запрос)

	Если Запрос = Неопределено ИЛИ Запрос.Параметры.Свойство("КлючиАналитикиНоменклатурыИнициализированы") Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросАналитики = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаТовары.Склад КАК Склад,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.Назначение КАК Назначение,
	|	ТаблицаТовары.Серия КАК Серия,
	|	ТаблицаТовары.СтатьяКалькуляции
	|ИЗ
	|	(
	// аналитика получателя продукции
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА ТаблицаТовары.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
	|				ТОГДА &Подразделение
	|			ИНАЧЕ ТаблицаТовары.Получатель
	|		КОНЕЦ КАК Склад,
	|		ТаблицаТовары.Номенклатура КАК Номенклатура,
	|		ТаблицаТовары.Характеристика КАК Характеристика,
	|		ТаблицаТовары.Назначение КАК Назначение,
	|		ВЫБОР
	|			КОГДА ТаблицаТовары.СтатусУказанияСерий = 14
	|				ТОГДА ТаблицаТовары.Серия
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		КОНЕЦ КАК Серия,
	|		ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) КАК СтатьяКалькуляции
	|	ИЗ
	|		Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// аналитика получателя работы по продукции давальца
	|	ВЫБРАТЬ
	|		ТаблицаТовары.Назначение.Заказ.Подразделение КАК Склад,
	|		ТаблицаТовары.Номенклатура КАК Номенклатура,
	|		ТаблицаТовары.Характеристика КАК Характеристика,
	|		ТаблицаТовары.Назначение КАК Назначение,
	|		ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
	|		ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) КАК СтатьяКалькуляции
	|	ИЗ
	|		Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка = &Ссылка
	|		И ТаблицаТовары.ПередатьДавальцу
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// аналитика получателя продукции без назначения
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА ТаблицаТовары.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
	|				ТОГДА &Подразделение
	|			ИНАЧЕ ТаблицаТовары.Получатель
	|		КОНЕЦ КАК Склад,
	|		ТаблицаТовары.Номенклатура КАК Номенклатура,
	|		ТаблицаТовары.Характеристика КАК Характеристика,
	|		ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
	|		ВЫБОР
	|			КОГДА ТаблицаТовары.СтатусУказанияСерий = 14
	|				ТОГДА ТаблицаТовары.Серия
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		КОНЕЦ КАК Серия,
	|		ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) КАК СтатьяКалькуляции
	|	ИЗ
	|		Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// аналитика получателя работы по продукции давальца без назначения
	|	ВЫБРАТЬ
	|		ТаблицаТовары.Назначение.Заказ.Подразделение КАК Склад,
	|		ТаблицаТовары.Номенклатура КАК Номенклатура,
	|		ТаблицаТовары.Характеристика КАК Характеристика,
	|		ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
	|		ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
	|		ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) КАК СтатьяКалькуляции
	|	ИЗ
	|		Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка = &Ссылка
	|		И ТаблицаТовары.ПередатьДавальцу
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// аналитика получателя возвратных отходов
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ВЫБОР
	|			КОГДА ТаблицаТовары.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
	|				ТОГДА ТаблицаТовары.Подразделение
	|			ИНАЧЕ ТаблицаТовары.Получатель
	|		КОНЕЦ КАК Склад,
	|		ТаблицаТовары.Номенклатура,
	|		ТаблицаТовары.Характеристика,
	|		ТаблицаТовары.Назначение КАК Назначение,
	|		ВЫБОР
	|			КОГДА ТаблицаТовары.СтатусУказанияСерий = 14
	|				ТОГДА ТаблицаТовары.Серия
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		КОНЕЦ КАК Серия,
	|		ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
	|	ИЗ
	|		Документ.ПроизводствоБезЗаказа.ПобочныеИзделия КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// аналитика получателя работы по возвратным отходам давальца
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ТаблицаТовары.Назначение.Заказ.Подразделение КАК Склад,
	|		ТаблицаТовары.Номенклатура,
	|		ТаблицаТовары.Характеристика,
	|		ТаблицаТовары.Назначение КАК Назначение,
	|		ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
	|		ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) КАК СтатьяКалькуляции
	|	ИЗ
	|		Документ.ПроизводствоБезЗаказа.ПобочныеИзделия КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка = &Ссылка
	|		И ТаблицаТовары.ПередатьДавальцу
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// аналитика получателя возвратных отходов без назначения
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ВЫБОР
	|			КОГДА ТаблицаТовары.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
	|				ТОГДА ТаблицаТовары.Подразделение
	|			ИНАЧЕ ТаблицаТовары.Получатель
	|		КОНЕЦ КАК Склад,
	|		ТаблицаТовары.Номенклатура,
	|		ТаблицаТовары.Характеристика,
	|		ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
	|		ВЫБОР
	|			КОГДА ТаблицаТовары.СтатусУказанияСерий = 14
	|				ТОГДА ТаблицаТовары.Серия
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		КОНЕЦ КАК Серия,
	|		ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
	|	ИЗ
	|		Документ.ПроизводствоБезЗаказа.ПобочныеИзделия КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// аналитика получателя работы по возвратным отходам давальца без назначения
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ТаблицаТовары.Назначение.Заказ.Подразделение КАК Склад,
	|		ТаблицаТовары.Номенклатура,
	|		ТаблицаТовары.Характеристика,
	|		ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
	|		ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) КАК Серия,
	|		ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) КАК СтатьяКалькуляции
	|	ИЗ
	|		Документ.ПроизводствоБезЗаказа.ПобочныеИзделия КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка = &Ссылка
	|		И ТаблицаТовары.ПередатьДавальцу
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// аналитика партии производства получателя возвратных отходов
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ВЫБОР
	|			КОГДА ТаблицаТовары.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
	|				ТОГДА ТаблицаТовары.Подразделение
	|			КОГДА ТаблицаТовары.Получатель ССЫЛКА Справочник.Склады
	|				ТОГДА ТаблицаТовары.Получатель.Подразделение
	|			ИНАЧЕ ТаблицаТовары.Получатель
	|		КОНЕЦ,
	|		ТаблицаТовары.Номенклатура,
	|		ТаблицаТовары.Характеристика,
	|		ТаблицаТовары.Назначение КАК Назначение,
	|		ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка),
	|		ТаблицаТовары.СтатьяКалькуляции
	|	ИЗ
	|		Документ.ПроизводствоБезЗаказа.ПобочныеИзделия КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// аналитика партии производства получателя возвратных отходов без назначения
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ВЫБОР
	|			КОГДА ТаблицаТовары.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
	|				ТОГДА ТаблицаТовары.Подразделение
	|			КОГДА ТаблицаТовары.Получатель ССЫЛКА Справочник.Склады
	|				ТОГДА ТаблицаТовары.Получатель.Подразделение
	|			ИНАЧЕ ТаблицаТовары.Получатель
	|		КОНЕЦ,
	|		ТаблицаТовары.Номенклатура,
	|		ТаблицаТовары.Характеристика,
	|		ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
	|		ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка),
	|		ТаблицаТовары.СтатьяКалькуляции
	|	ИЗ
	|		Документ.ПроизводствоБезЗаказа.ПобочныеИзделия КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// аналитика партии производства подразделения материалов и работ
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ВЫБОР
	|			КОГДА ТаблицаТовары.Отправитель ССЫЛКА Справочник.Склады
	|				ТОГДА ТаблицаТовары.Отправитель.Подразделение
	|			ИНАЧЕ ТаблицаТовары.Отправитель
	|		КОНЕЦ,
	|		ТаблицаТовары.Номенклатура,
	|		ТаблицаТовары.Характеристика,
	|		ТаблицаТовары.Назначение КАК Назначение,
	|		ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка),
	|		ТаблицаТовары.СтатьяКалькуляции
	|	ИЗ
	|		Документ.ПроизводствоБезЗаказа.МатериалыИРаботы КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// аналитика партии производства подразделения материалов и работ без назначения
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ВЫБОР
	|			КОГДА ТаблицаТовары.Отправитель ССЫЛКА Справочник.Склады
	|				ТОГДА ТаблицаТовары.Отправитель.Подразделение
	|			ИНАЧЕ ТаблицаТовары.Отправитель
	|		КОНЕЦ,
	|		ТаблицаТовары.Номенклатура,
	|		ТаблицаТовары.Характеристика,
	|		ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
	|		ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка),
	|		ТаблицаТовары.СтатьяКалькуляции
	|	ИЗ
	|		Документ.ПроизводствоБезЗаказа.МатериалыИРаботы КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// аналитика отправителя материалов и работ без назначения
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ТаблицаТовары.Отправитель,
	|		ТаблицаТовары.Номенклатура,
	|		ТаблицаТовары.Характеристика,
	|		ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
	|		ВЫБОР
	|			КОГДА ТаблицаТовары.СтатусУказанияСерий = 14
	|				ТОГДА ТаблицаТовары.Серия
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		КОНЕЦ,
	|		ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
	|	ИЗ
	|		Документ.ПроизводствоБезЗаказа.МатериалыИРаботы КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка = &Ссылка) КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|		ПО ТаблицаТовары.Номенклатура = Аналитика.Номенклатура
	|			И ТаблицаТовары.Характеристика = Аналитика.Характеристика
	|			И ТаблицаТовары.Серия = Аналитика.Серия
	|			И ТаблицаТовары.Склад = Аналитика.Склад
	|			И ТаблицаТовары.Назначение = Аналитика.Назначение
	|			И ТаблицаТовары.СтатьяКалькуляции = Аналитика.СтатьяКалькуляции
	|ГДЕ
	|	Аналитика.Номенклатура ЕСТЬ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Склад,
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Назначение,
	|	ТаблицаТовары.Серия,
	|	ТаблицаТовары.СтатьяКалькуляции");
	
	ЗапросАналитики.УстановитьПараметр("Ссылка", Запрос.Параметры.Ссылка);
	ЗапросАналитики.УстановитьПараметр("Подразделение", Запрос.Параметры.Подразделение);
	ЗапросАналитики.УстановитьПараметр("УчитыватьСебестоимостьТоваровПоВидамЗапасов", Запрос.Параметры.УчитыватьСебестоимостьТоваровПоВидамЗапасов);
	
	Выборка = ЗапросАналитики.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РегистрыСведений.АналитикаУчетаНоменклатуры.СоздатьКлючАналитики(Выборка)
	КонецЦикла;

	Запрос.УстановитьПараметр("КлючиАналитикиНоменклатурыИнициализированы", Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ПроведениеПоРеглУчету

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	#Область ТекстВыпускСобственнойПродукции // (Дт 2Х :: Кт 2Х)
	ТекстВыпускСобственнойПродукции =
	// Выпуск собственной продукции (Дт 2Х :: Кт 2Х).
	"ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	Стоимости.ИдентификаторСтроки КАК ИдентификаторСтроки,

	|	Стоимости.СуммаБалансовая КАК Сумма,

	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство) КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	КорСкладыПодразделения.Подразделение КАК МестоУчетаДт,

	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	КорСкладыПодразделения.Подразделение КАК ПодразделениеДт,
	|	Стоимости.КорНаправлениеДеятельности КАК НаправлениеДеятельностиДт,

	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.МатериальныеЗатраты) КАК СубконтоДт1,
	|	Стоимости.Номенклатура КАК СубконтоДт2,
	|	Стоимости.КорГруппаПродукции КАК СубконтоДт3,

	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Стоимости.СуммаНУ КАК СуммаНУДт,
	|	Стоимости.СуммаПР КАК СуммаПРДт,
	|	Стоимости.СуммаВР КАК СуммаВРДт,

	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство) КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	СкладыПодразделения.Подразделение КАК МестоУчетаКт,

	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	СкладыПодразделения.Подразделение КАК ПодразделениеКт,
	|	Стоимости.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,

	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	Стоимости.ГруппаПродукции КАК СубконтоКт3,

	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Стоимости.СуммаНУ КАК СуммаНУКт,
	|	Стоимости.СуммаПР КАК СуммаПРКт,
	|	Стоимости.СуммаВР КАК СуммаВРКт,
	|	""Выпуск собственной продукции"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа КАК Операция
	|		ПО ДокументыКОтражению.Ссылка = Операция.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСтоимости КАК Стоимости
	|		ПО Операция.Ссылка = Стоимости.Ссылка
	|			И Стоимости.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			И Стоимости.СтатьяАктивовПассивов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПустаяСсылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСкладыПодразделения КАК СкладыПодразделения
	|		ПО (СкладыПодразделения.Склад = Стоимости.Склад)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСкладыПодразделения КАК КорСкладыПодразделения
	|		ПО (КорСкладыПодразделения.Склад = Стоимости.КорСклад)
	|ГДЕ
	|	Стоимости.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И (НЕ Стоимости.СуммаБалансовая = 0
	|		ИЛИ НЕ Стоимости.СуммаНУ = 0)
	|	И (НЕ СкладыПодразделения.Подразделение = КорСкладыПодразделения.Подразделение
	|		ИЛИ НЕ Стоимости.НаправлениеДеятельности = Стоимости.КорНаправлениеДеятельности
	|		ИЛИ НЕ Стоимости.ГруппаПродукции = Стоимости.КорГруппаПродукции)";
	#КонецОбласти
	
	#Область ТекстВыпускСобственнойПродукцииНематериальныеЗатраты // (Дт 2Х :: Кт 2Х)
	ТекстВыпускСобственнойПродукцииНематериальныеЗатраты =
	// Выпуск собственной продукции, приход нематериальных затрат (Дт 2Х :: Кт 2Х).
	"ВЫБРАТЬ
	|	НематериальныеЗатраты.Ссылка КАК Ссылка,
	|	НематериальныеЗатраты.Период КАК Период,
	|	НематериальныеЗатраты.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,

	|	НематериальныеЗатраты.Сумма КАК Сумма,

	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство) КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НематериальныеЗатраты.ПодразделениеДт КАК МестоУчетаДт,

	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	НематериальныеЗатраты.ПодразделениеДт КАК ПодразделениеДт,
	|	НематериальныеЗатраты.КорНаправлениеДеятельности КАК НаправлениеДеятельностиДт,

	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	НематериальныеЗатраты.ТипЗатрат КАК СубконтоДт1,
	|	НематериальныеЗатраты.Затрата КАК СубконтоДт2,
	|	НематериальныеЗатраты.ГруппаПродукцииДт КАК СубконтоДт3,

	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	НематериальныеЗатраты.Сумма - НематериальныеЗатраты.СуммаПР - НематериальныеЗатраты.СуммаВР КАК СуммаНУДт,
	|	НематериальныеЗатраты.СуммаПР КАК СуммаПРДт,
	|	НематериальныеЗатраты.СуммаВР КАК СуммаВРДт,

	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство) КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НематериальныеЗатраты.ПодразделениеКт КАК МестоУчетаКт,

	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	НематериальныеЗатраты.ПодразделениеКт КАК ПодразделениеКт,
	|	НематериальныеЗатраты.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,

	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НематериальныеЗатраты.ГруппаПродукцииКт КАК СубконтоКт3,

	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	НематериальныеЗатраты.Сумма - НематериальныеЗатраты.СуммаПР - НематериальныеЗатраты.СуммаВР КАК СуммаНУКт,
	|	НематериальныеЗатраты.СуммаПР КАК СуммаПРКт,
	|	НематериальныеЗатраты.СуммаВР КАК СуммаВРКт,
	|	""Выпуск собственной продукции"" КАК Содержание
	|ИЗ
	|	НематериальныеЗатраты КАК НематериальныеЗатраты";
	#КонецОбласти
	
	#Область ТекстСписаниеПродукцииНаРасходы // (Дт СтатьяРасходов.СчетУчета :: Кт Счет учета затрат подразделения)
	// Списание продукции на статьи расходов (Дт СтатьяРасходов.СчетУчета :: Кт Счет учета затрат подразделения)
	ТекстСписаниеПродукцииНаРасходы =
	"ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	ЕСТЬNULL(Стоимости.ИдентификаторСтроки, Строки.ИдентификаторСтроки) КАК ИдентификаторСтроки,

	|	ЕСТЬNULL(Стоимости.Сумма, Строки.Сумма) КАК Сумма,

	|	ВЫБОР КОГДА Строки.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы)
	|	КОНЕЦ КАК ВидСчетаДт,
	|	ВЫБОР КОГДА Строки.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|		ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ Строки.СтатьяРасходов
	|	КОНЕЦ КАК АналитикаУчетаДт,
	|	Строки.Подразделение КАК МестоУчетаДт,

	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	ЕСТЬNULL(Стоимости.НаправлениеДеятельности, Строки.НаправлениеДеятельности) КАК НаправлениеДеятельностиДт,

	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	ВЫБОР
	|		КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА Строки.АналитикаРасходов
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее)
	|	КОНЕЦ КАК СубконтоДт1,
	|	Строки.СтатьяРасходов КАК СубконтоДт2,
	|	ВЫБОР
	|		КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный)
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СубконтоДт3,

	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ЕСТЬNULL(Стоимости.СуммаНУ, Строки.СуммаНУ) КАК СуммаНУДт,
	|	ЕСТЬNULL(Стоимости.СуммаПР, Строки.СуммаПР) КАК СуммаПРДт,
	|	ЕСТЬNULL(Стоимости.СуммаВР, Строки.СуммаВР) КАК СуммаВРДт,

	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство) КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	ВЫБОР
	|		КОГДА Строки.Склад ССЫЛКА Справочник.Склады
	|			ТОГДА Строки.Склад.Подразделение
	|		ИНАЧЕ Строки.Склад
	|	КОНЕЦ КАК МестоУчетаКт,

	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	ВЫБОР
	|		КОГДА Строки.Склад ССЫЛКА Справочник.Склады
	|			ТОГДА Строки.Склад.Подразделение
	|		ИНАЧЕ Строки.Склад
	|	КОНЕЦ КАК ПодразделениеКт,
	|	ЕСТЬNULL(Стоимости.НаправлениеДеятельности, Строки.НаправлениеДеятельности) КАК НаправлениеДеятельностиКт,

	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	ЕСТЬNULL(Стоимости.ГруппаПродукции, Строки.ГруппаПродукции) КАК СубконтоКт3,

	|	0 КАК ВалютнаяСуммаКт,
	|	Строки.Количество КАК КоличествоКт,
	|	ЕСТЬNULL(Стоимости.СуммаНУ, Строки.СуммаНУ) КАК СуммаНУКт,
	|	ЕСТЬNULL(Стоимости.СуммаПР, Строки.СуммаПР) КАК СуммаПРКт,
	|	ЕСТЬNULL(Стоимости.СуммаВР, Строки.СуммаВР) КАК СуммаВРКт,
	|	""Списание продукции на расходы"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа КАК Операция
	|		ПО ДокументыКОтражению.Ссылка = Операция.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСтроки КАК Строки
	|		ПО (Операция.Ссылка = Строки.Ссылка)
	|			И (Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
	|			И НЕ Строки.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтоимости КАК Стоимости
	|		ПО (Строки.Ссылка = Стоимости.Ссылка)
	|			И (Строки.Номенклатура = Стоимости.Номенклатура)
	|			И (Строки.Склад = Стоимости.Склад)
	|			И (Строки.ГруппаФинансовогоУчета = Стоимости.ГруппаФинансовогоУчета)
	|			И (Строки.ТипЗапасов = Стоимости.ТипЗапасов)
	|			И (Строки.Контрагент = Стоимости.Контрагент)
	|			И (Строки.СтатьяРасходов = Стоимости.СтатьяРасходов)
	|			И (Строки.АналитикаРасходов = Стоимости.АналитикаРасходов)
	|			И (Строки.СтатьяАктивовПассивов = Стоимости.СтатьяАктивовПассивов)
	|			И (Строки.АналитикаАктивовПассивов = Стоимости.АналитикаАктивовПассивов)
	|			И (Строки.РазделУчета = Стоимости.РазделУчета)
	|			И (Строки.ГруппаПродукции = Стоимости.ГруппаПродукции)
	|			И (Строки.ИдентификаторСтроки = Стоимости.ИдентификаторСтроки)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|		ПО (Строки.СтатьяРасходов = СтатьиСтроительства.Ссылка)
	|			И (СтатьиСтроительства.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы))
	|ГДЕ
	|	НЕ ЕСТЬNULL(Стоимости.Сумма, Строки.Сумма) = 0";
	#КонецОбласти
	
	#Область ТекстСписаниеПродукцииНаАктивыПассивы // (Дт Документ.СчетУчета :: Кт Счет учета затрат подразделения)
	// Списание продукции на прочие активы/пассивы (Дт Документ.СчетУчета :: Кт Счет учета затрат подразделения)
	ТекстСписаниеПродукцииНаАктивыПассивы =
	"ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	ЕСТЬNULL(Стоимости.ИдентификаторСтроки, Строки.ИдентификаторСтроки) КАК ИдентификаторСтроки,

	|	ЕСТЬNULL(Стоимости.Сумма, Строки.Сумма) КАК Сумма,

	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,

	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	ЕСТЬNULL(Стоимости.НаправлениеДеятельности, Строки.НаправлениеДеятельности) КАК НаправлениеДеятельностиДт,

	|	ТаблицаПродукции.СчетУчета КАК СчетДт,
	|	ТаблицаПродукции.Субконто1 КАК СубконтоДт1,
	|	ТаблицаПродукции.Субконто2 КАК СубконтоДт2,
	|	ТаблицаПродукции.Субконто3 КАК СубконтоДт3,

	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ЕСТЬNULL(Стоимости.СуммаНУ, Строки.СуммаНУ) КАК СуммаНУДт,
	|	ЕСТЬNULL(Стоимости.СуммаПР, Строки.СуммаПР) КАК СуммаПРДт,
	|	ЕСТЬNULL(Стоимости.СуммаВР, Строки.СуммаВР) КАК СуммаВРДт,

	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство) КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	ВЫБОР
	|		КОГДА Строки.Склад ССЫЛКА Справочник.Склады
	|			ТОГДА Строки.Склад.Подразделение
	|		ИНАЧЕ Строки.Склад
	|	КОНЕЦ КАК МестоУчетаКт,

	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	ВЫБОР
	|		КОГДА Строки.Склад ССЫЛКА Справочник.Склады
	|			ТОГДА Строки.Склад.Подразделение
	|		ИНАЧЕ Строки.Склад
	|	КОНЕЦ КАК ПодразделениеКт,
	|	ЕСТЬNULL(Стоимости.НаправлениеДеятельности, Строки.НаправлениеДеятельности) КАК НаправлениеДеятельностиКт,

	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	ЕСТЬNULL(Стоимости.ГруппаПродукции, Строки.ГруппаПродукции) КАК СубконтоКт3,

	|	0 КАК ВалютнаяСуммаКт,
	|	Строки.Количество КАК КоличествоКт,
	|	ЕСТЬNULL(Стоимости.СуммаНУ, Строки.СуммаНУ) КАК СуммаНУКт,
	|	ЕСТЬNULL(Стоимости.СуммаПР, Строки.СуммаПР) КАК СуммаПРКт,
	|	ЕСТЬNULL(Стоимости.СуммаВР, Строки.СуммаВР) КАК СуммаВРКт,

	|	""Списание продукции на расходы"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа КАК Операция
	|		ПО ДокументыКОтражению.Ссылка = Операция.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСтроки КАК Строки
	|		ПО (Операция.Ссылка = Строки.Ссылка)
	|			И (Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход))
	|			И НЕ Строки.СтатьяАктивовПассивов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПустаяСсылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ТаблицаПродукции
	|		ПО (Операция.Ссылка = ТаблицаПродукции.Ссылка)
	|			И (Строки.ИдентификаторСтроки = ТаблицаПродукции.ИдентификаторСтроки)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСтоимости КАК Стоимости
	|		ПО (Строки.Ссылка = Стоимости.Ссылка)
	|			И (Строки.Номенклатура = Стоимости.Номенклатура)
	|			И (Строки.Склад = Стоимости.Склад)
	|			И (Строки.ГруппаФинансовогоУчета = Стоимости.ГруппаФинансовогоУчета)
	|			И (Строки.ТипЗапасов = Стоимости.ТипЗапасов)
	|			И (Строки.Контрагент = Стоимости.Контрагент)
	|			И (Строки.СтатьяРасходов = Стоимости.СтатьяРасходов)
	|			И (Строки.АналитикаРасходов = Стоимости.АналитикаРасходов)
	|			И (Строки.СтатьяАктивовПассивов = Стоимости.СтатьяАктивовПассивов)
	|			И (Строки.АналитикаАктивовПассивов = Стоимости.АналитикаАктивовПассивов)
	|			И (Строки.РазделУчета = Стоимости.РазделУчета)
	|			И (Строки.ГруппаПродукции = Стоимости.ГруппаПродукции)
	|			И (Строки.ИдентификаторСтроки = Стоимости.ИдентификаторСтроки)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|		ПО (Строки.СтатьяРасходов = СтатьиСтроительства.Ссылка)
	|			И (СтатьиСтроительства.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы))
	|ГДЕ
	|	НЕ ЕСТЬNULL(Стоимости.Сумма, Строки.Сумма) = 0";
	#КонецОбласти
	
#Область ТекстВыпускПродукцииДавальца // (Дт 002 :: Кт )
	ТекстВыпускПродукцииДавальца = "
	|ВЫБРАТЬ //// Выпуск продукции давальца (Дт 002 :: Кт )
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ЕСТЬNULL(Стоимости.СуммаЗабалансовая, Строки.СуммаЗабалансовая) КАК Сумма,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТМЦпринятыеНаОтветственноеХранение) КАК СчетДт,
	|	Строки.Номенклатура КАК СубконтоДт1,
	|	Строки.Контрагент КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	Строки.Количество КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Выпуск продукции давальца"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПроизводствоБезЗаказа КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтСтроки КАК Строки
	|	ПО
	|		Операция.Ссылка = Строки.Ссылка
	|		И Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК Выходные
	|	ПО
	|		Строки.Ссылка = Выходные.Ссылка
	|		И Строки.ИдентификаторСтроки = Выходные.ИдентификаторСтроки
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.ПроизводствоБезЗаказа.ПобочныеИзделия КАК Побочные
	|	ПО
	|		Строки.Ссылка = Побочные.Ссылка
	|		И Строки.ИдентификаторСтроки = Побочные.ИдентификаторСтроки
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтСтоимости КАК Стоимости
	|	ПО
	|		Строки.Ссылка = Стоимости.Ссылка
	|		И Строки.Номенклатура = Стоимости.Номенклатура
	|		И Строки.Склад = Стоимости.Склад
	|		И Строки.ГруппаФинансовогоУчета = Стоимости.ГруппаФинансовогоУчета
	|		И Строки.РазделУчета = Стоимости.РазделУчета
	|		И Строки.ВидДвижения = Стоимости.ВидДвижения
	|		И НЕ Стоимости.СуммаЗабалансовая = 0
	|ГДЕ
	|	Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
	|	И Строки.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции),
	|										ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад),
	|										ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение),
	|										ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость))
	|";
#КонецОбласти

#Область ТекстСписаниеМатериаловДавальца // (Дт :: Кт 003.02)
	ТекстСписаниеМатериаловДавальца = "
	|ВЫБРАТЬ // Списание материалов давальца (Дт :: Кт 003.02)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ЕСТЬNULL(Стоимости.СуммаЗабалансовая, Строки.СуммаЗабалансовая) КАК Сумма,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Склад КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.МатериалыПринятыеВПереработкуВПроизводстве) КАК СчетКт,
	|	Строки.Контрагент КАК СубконтоКт1,
	|	Строки.Номенклатура КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	Строки.Количество КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Списание материалов давальца"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПроизводствоБезЗаказа КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтСтроки КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|		И Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтСтоимости КАК Стоимости
	|	ПО
	|		Строки.Ссылка = Стоимости.Ссылка
	|		И Строки.Номенклатура = Стоимости.Номенклатура
	|		И Строки.Склад = Стоимости.Склад
	|		И Строки.ГруппаФинансовогоУчета = Стоимости.ГруппаФинансовогоУчета
	|		И Строки.РазделУчета = Стоимости.РазделУчета
	|		И Строки.ВидДвижения = Стоимости.ВидДвижения
	|		И Строки.ХозяйственнаяОперация = Стоимости.ХозяйственнаяОперация
	|ГДЕ
	|	Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)
	|	И Строки.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции),
	|										ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад),
	|										ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение))
	|";
#КонецОбласти

#Область ТекстСписаниеПолуфабрикатовДавальца // (Дт :: Кт 002)
	ТекстСписаниеПолуфабрикатовДавальца = "
	|ВЫБРАТЬ // Списание полуфабрикатов давальца (Дт :: Кт 002)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ЕСТЬNULL(Стоимости.СуммаЗабалансовая, Строки.СуммаЗабалансовая) КАК Сумма,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Склад КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТМЦпринятыеНаОтветственноеХранение) КАК СчетКт,
	|	Строки.Контрагент КАК СубконтоКт1,
	|	Строки.Номенклатура КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	Строки.Количество КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Списание полуфабрикатов давальца"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПроизводствоБезЗаказа КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтСтроки КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|		И Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтСтоимости КАК Стоимости
	|	ПО
	|		Строки.Ссылка = Стоимости.Ссылка
	|		И Строки.Номенклатура = Стоимости.Номенклатура
	|		И Строки.Склад = Стоимости.Склад
	|		И Строки.ГруппаФинансовогоУчета = Стоимости.ГруппаФинансовогоУчета
	|		И Строки.РазделУчета = Стоимости.РазделУчета
	|		И Строки.ВидДвижения = Стоимости.ВидДвижения
	|		И Строки.ХозяйственнаяОперация = Стоимости.ХозяйственнаяОперация
	|		И НЕ Стоимости.СуммаЗабалансовая = 0
	|ГДЕ
	|	Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
	|	И Строки.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции),
	|										ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад),
	|										ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение))
	|";
#КонецОбласти

Возврат ТекстВыпускСобственнойПродукции
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстВыпускСобственнойПродукцииНематериальныеЗатраты
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстСписаниеПродукцииНаРасходы
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстСписаниеПродукцииНаАктивыПассивы
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстВыпускПродукцииДавальца
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстСписаниеМатериаловДавальца
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстСписаниеПолуфабрикатовДавальца;
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц, 
// необходимых для отражения в регламетированном учете.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	ТекстЗапроса = "
	|// Формирование временной таблицы соответствия подразделений складам
	|ВЫБРАТЬ
	|	Склады.Склад,
	|	Склады.Подразделение
	|ПОМЕСТИТЬ ВтСкладыПодразделения
	|ИЗ
	|	(ВЫБРАТЬ
	|		СтрокиСебестоимости.Склад КАК Склад,
	|		ВЫБОР
	|			КОГДА СтрокиСебестоимости.Склад ССЫЛКА Справочник.Склады
	|				ТОГДА СтрокиСебестоимости.Склад.Подразделение
	|			ИНАЧЕ СтрокиСебестоимости.Склад
	|		КОНЕЦ КАК Подразделение
	|	ИЗ
	|		ВтСтроки КАК СтрокиСебестоимости
	|	ГДЕ
	|		НЕ СтрокиСебестоимости.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		СтрокиСебестоимости.КорСклад КАК Склад,
	|		ВЫБОР
	|			КОГДА СтрокиСебестоимости.КорСклад ССЫЛКА Справочник.Склады
	|				ТОГДА СтрокиСебестоимости.КорСклад.Подразделение
	|			ИНАЧЕ СтрокиСебестоимости.КорСклад
	|		КОНЕЦ КАК Подразделение
	|	ИЗ
	|		ВтСтроки КАК СтрокиСебестоимости
	|	ГДЕ
	|		НЕ СтрокиСебестоимости.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		СтрокиСебестоимости.Склад КАК Склад,
	|		ВЫБОР
	|			КОГДА СтрокиСебестоимости.Склад ССЫЛКА Справочник.Склады
	|				ТОГДА СтрокиСебестоимости.Склад.Подразделение
	|			ИНАЧЕ СтрокиСебестоимости.Склад
	|		КОНЕЦ КАК Подразделение
	|	ИЗ
	|		ВтСтоимости КАК СтрокиСебестоимости
	|	ГДЕ
	|		НЕ СтрокиСебестоимости.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		СтрокиСебестоимости.КорСклад КАК Склад,
	|		ВЫБОР
	|			КОГДА СтрокиСебестоимости.КорСклад ССЫЛКА Справочник.Склады
	|				ТОГДА СтрокиСебестоимости.КорСклад.Подразделение
	|			ИНАЧЕ СтрокиСебестоимости.КорСклад
	|		КОНЕЦ КАК Подразделение
	|	ИЗ
	|		ВтСтоимости КАК СтрокиСебестоимости
	|	ГДЕ
	|		НЕ СтрокиСебестоимости.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)) КАК Склады
	|СГРУППИРОВАТЬ ПО
	|	Склады.Склад,
	|	Склады.Подразделение
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// Формирование временной таблицы нематериальных затрат
	|ВЫБРАТЬ
	|	ПрочиеРасходыНезавершенногоПроизводства.Регистратор КАК Ссылка,
	|	ПрочиеРасходыНезавершенногоПроизводства.Период КАК Период,
	|	ПрочиеРасходыНезавершенногоПроизводства.Организация,
	|	АналитикаУчетаНоменклатуры.Номенклатура КАК Затрата,
	|	ЕСТЬNULL(Назначения.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(КорНазначения.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК КорНаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.МатериальныеЗатраты) КАК ТипЗатрат,
	|	СкладыПодразделения.Подразделение КАК ПодразделениеДт,
	|	СпрНоменклатура.ГруппаАналитическогоУчета КАК ГруппаПродукцииДт,
	|	ПрочиеРасходыНезавершенногоПроизводства.Подразделение КАК ПодразделениеКт,
	|	ПрочиеРасходыНезавершенногоПроизводства.ГруппаПродукции КАК ГруппаПродукцииКт,
	|	СУММА(ПрочиеРасходыНезавершенногоПроизводства.СтоимостьРегл) КАК Сумма,
	|	СУММА(ПрочиеРасходыНезавершенногоПроизводства.ПостояннаяРазница) КАК СуммаПР,
	|	СУММА(ПрочиеРасходыНезавершенногоПроизводства.ВременнаяРазница) КАК СуммаВР
	|ПОМЕСТИТЬ НематериальныеЗатраты
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходыНезавершенногоПроизводства КАК ПрочиеРасходыНезавершенногоПроизводства
	|			ПО ДокументыКОтражению.Ссылка = ПрочиеРасходыНезавершенногоПроизводства.Регистратор
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
	|			ПО (ПрочиеРасходыНезавершенногоПроизводства.АналитикаУчетаПродукции = АналитикаУчетаНоменклатуры.КлючАналитики)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСкладыПодразделения КАК СкладыПодразделения
	|			ПО (СкладыПодразделения.Склад = АналитикаУчетаНоменклатуры.Склад)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартийПроизводства КАК КлючиПартийПроизводства
	|			ПО ПрочиеРасходыНезавершенногоПроизводства.АналитикаУчетаПартийПроизводства = КлючиПартийПроизводства.КлючАналитики
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК Назначения
	|			ПО КлючиПартийПроизводства.Назначение = Назначения.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК КорНазначения
	|			ПО АналитикаУчетаНоменклатуры.Назначение = КорНазначения.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|			ПО АналитикаУчетаНоменклатуры.Номенклатура = СпрНоменклатура.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константы КАК Константы
	|			ПО ИСТИНА
	|
	|ГДЕ
	|	ПрочиеРасходыНезавершенногоПроизводства.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И (НЕ СкладыПодразделения.Подразделение = ПрочиеРасходыНезавершенногоПроизводства.Подразделение
	|		ИЛИ НЕ ЕСТЬNULL(Назначения.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) =
	|			ЕСТЬNULL(КорНазначения.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
	|		ИЛИ (Константы.АналитическийУчетПоГруппамПродукции
	|			И НЕ ПрочиеРасходыНезавершенногоПроизводства.ГруппаПродукции =
	|				СпрНоменклатура.ГруппаАналитическогоУчета))
	|
	|СГРУППИРОВАТЬ ПО
	|	ПрочиеРасходыНезавершенногоПроизводства.Подразделение,
	|	ПрочиеРасходыНезавершенногоПроизводства.Период,
	|	ПрочиеРасходыНезавершенногоПроизводства.Организация,
	|	ПрочиеРасходыНезавершенногоПроизводства.ГруппаПродукции,
	|	ПрочиеРасходыНезавершенногоПроизводства.Регистратор,
	|	СпрНоменклатура.ГруппаАналитическогоУчета,
	|	АналитикаУчетаНоменклатуры.Номенклатура,
	|	СкладыПодразделения.Подразделение,
	|	Назначения.НаправлениеДеятельности,
	|	КорНазначения.НаправлениеДеятельности
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТрудозатратыНезавершенногоПроизводства.Регистратор КАК Ссылка,
	|	ТрудозатратыНезавершенногоПроизводства.Период КАК Период,
	|	ТрудозатратыНезавершенногоПроизводства.Организация,
	|	КлючиАналитикиУчетаНоменклатуры.Номенклатура КАК Затрата,
	|	ЕСТЬNULL(Назначения.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(КорНазначения.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК КорНаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.МатериальныеЗатраты) КАК ТипЗатрат,
	|	СкладыПодразделения.Подразделение КАК ПодразделениеДт,
	|	СпрНоменклатура.ГруппаАналитическогоУчета КАК ГруппаПродукцииДт,
	|	ТрудозатратыНезавершенногоПроизводства.Подразделение КАК ПодразделениеКт,
	|	ТрудозатратыНезавершенногоПроизводства.ГруппаПродукции КАК ГруппаПродукцииКт,
	|	СУММА(ТрудозатратыНезавершенногоПроизводства.СтоимостьРегл) КАК Сумма,
	|	0,
	|	0
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТрудозатратыНезавершенногоПроизводства КАК ТрудозатратыНезавершенногоПроизводства
	|			ПО ДокументыКОтражению.Ссылка = ТрудозатратыНезавершенногоПроизводства.Регистратор
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиАналитикиУчетаНоменклатуры
	|			ПО (ТрудозатратыНезавершенногоПроизводства.КорАналитикаУчетаПродукции = КлючиАналитикиУчетаНоменклатуры.КлючАналитики)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтСкладыПодразделения КАК СкладыПодразделения
	|			ПО (СкладыПодразделения.Склад = КлючиАналитикиУчетаНоменклатуры.Склад)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартийПроизводства КАК КлючиПартийПроизводства
	|			ПО ТрудозатратыНезавершенногоПроизводства.АналитикаУчетаПартийПроизводства = КлючиПартийПроизводства.КлючАналитики
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК Назначения
	|			ПО КлючиПартийПроизводства.Назначение = Назначения.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК КорНазначения
	|			ПО КлючиАналитикиУчетаНоменклатуры.Назначение = КорНазначения.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ВыходныеИзделияПроизводстваБезЗаказов
	|			ПО ВыходныеИзделияПроизводстваБезЗаказов.ОсновноеИзделиеГруппыЗатрат
	|			И ДокументыКОтражению.Ссылка = ВыходныеИзделияПроизводстваБезЗаказов.Ссылка
	|			И ТрудозатратыНезавершенногоПроизводства.АналитикаУчетаПартийПроизводства = ВыходныеИзделияПроизводстваБезЗаказов.АналитикаУчетаПартийПроизводства
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|			ПО СпрНоменклатура.Ссылка = ВыходныеИзделияПроизводстваБезЗаказов.Номенклатура
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константы КАК Константы
	|			ПО ИСТИНА
	|
	|ГДЕ
	|	ТрудозатратыНезавершенногоПроизводства.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И (НЕ СкладыПодразделения.Подразделение = ТрудозатратыНезавершенногоПроизводства.Подразделение
	|		ИЛИ НЕ ЕСТЬNULL(Назначения.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) =
	|			ЕСТЬNULL(КорНазначения.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
	|		ИЛИ (Константы.АналитическийУчетПоГруппамПродукции
	|			И НЕ СпрНоменклатура.ГруппаАналитическогоУчета =
	|				ТрудозатратыНезавершенногоПроизводства.ГруппаПродукции))
	|
	|СГРУППИРОВАТЬ ПО
	|	ТрудозатратыНезавершенногоПроизводства.Подразделение,
	|	ТрудозатратыНезавершенногоПроизводства.Период,
	|	ТрудозатратыНезавершенногоПроизводства.Организация,
	|	ТрудозатратыНезавершенногоПроизводства.ГруппаПродукции,
	|	ТрудозатратыНезавершенногоПроизводства.Регистратор,
	|	КлючиАналитикиУчетаНоменклатуры.Номенклатура,
	|	СпрНоменклатура.ГруппаАналитическогоУчета,
	|	СкладыПодразделения.Подразделение,
	|	Назначения.НаправлениеДеятельности,
	|	КорНазначения.НаправлениеДеятельности
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ТекстыЗапросовПроведения

Функция ТекстЗапросаВтТовары(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТовары";
	
	ТекстЗапроса = 
	// Выходные изделия по получателю
	"ВЫБРАТЬ
	|	ТаблицаТовары.НаправлениеВыпуска					КАК НаправлениеВыпуска,
	|	ТаблицаТовары.НаправлениеВыпуска					КАК ХозяйственнаяОперация,
	|	ТаблицаТовары.ВидЗапасов							КАК ВидЗапасов,
	|	НЕОПРЕДЕЛЕНО										КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО										КАК Отправитель,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
	|			ТОГДА &Подразделение
	|		ИНАЧЕ ТаблицаТовары.Получатель
	|	КОНЕЦ												КАК Получатель,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
	|			ТОГДА ТаблицаТовары.Получатель
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ												КАК СтатьяРасходов,
	|	ТаблицаТовары.АналитикаУчетаПартийПроизводства		КАК АналитикаУчетаПартийПроизводства,
	|	ТаблицаТовары.Номенклатура							КАК Номенклатура,
	|	ТаблицаТовары.Характеристика						КАК Характеристика,
	|	НЕОПРЕДЕЛЕНО										КАК НомерГТД,
	|	ТаблицаТовары.Назначение							КАК Назначение,
	|	ТаблицаТовары.СтатусУказанияСерий					КАК СтатусУказанияСерий,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)	КАК Серия,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияСерий = 14
	|			ТОГДА ТаблицаТовары.Серия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ												КАК КорСерия,
	|	ТаблицаТовары.Спецификация							КАК Спецификация,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)	КАК СтатьяКалькуляции,
	|	ТаблицаТовары.Количество							КАК Количество,
	|	0													КАК Сумма,
	|	ТаблицаТовары.ИдентификаторСтроки					КАК ИдентификаторСтроки,
	|	ТаблицаТовары.АналитикаРасходов						КАК АналитикаРасходов,
	|	ТаблицаТовары.АналитикаАктивовПассивов				КАК АналитикаАктивовПассивов,
	|	ЛОЖЬ												КАК РаботаДляДавальца
	|ПОМЕСТИТЬ втТовары
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Работа по выходным изделиям давальца
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение)	КАК НаправлениеВыпуска,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение)	КАК ХозяйственнаяОперация,
	|	&ВидЗапасовРаботы									КАК ВидЗапасов,
	|	ТаблицаТовары.ВидЗапасов							КАК КорВидЗапасов,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
	|			ТОГДА &Подразделение
	|		ИНАЧЕ ТаблицаТовары.Получатель
	|	КОНЕЦ												КАК Отправитель,
	|	ТаблицаТовары.Назначение.Заказ.Подразделение		КАК Получатель,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
	|			ТОГДА ТаблицаТовары.Получатель
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ												КАК СтатьяРасходов,
	|	ТаблицаТовары.АналитикаУчетаПартийПроизводства		КАК АналитикаУчетаПартийПроизводства,
	|	ТаблицаТовары.Номенклатура							КАК Номенклатура,
	|	ТаблицаТовары.Характеристика						КАК Характеристика,
	|	НЕОПРЕДЕЛЕНО										КАК НомерГТД,
	|	ТаблицаТовары.Назначение							КАК Назначение,
	|	ТаблицаТовары.СтатусУказанияСерий					КАК СтатусУказанияСерий,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияСерий = 14
	|			ТОГДА ТаблицаТовары.Серия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ												КАК Серия,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)	КАК КорСерия,
	|	ТаблицаТовары.Спецификация							КАК Спецификация,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)	КАК СтатьяКалькуляции,
	|	ТаблицаТовары.Количество							КАК Количество,
	|	0													КАК Сумма,
	|	ТаблицаТовары.ИдентификаторСтроки					КАК ИдентификаторСтроки,
	|	ТаблицаТовары.АналитикаРасходов						КАК АналитикаРасходов,
	|	ТаблицаТовары.АналитикаАктивовПассивов				КАК АналитикаАктивовПассивов,
	|	ИСТИНА												КАК РаботаДляДавальца
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.ПередатьДавальцу
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Побочные изделия по получателю
	|ВЫБРАТЬ
	|	ТаблицаТовары.НаправлениеВыпуска													КАК НаправлениеВыпуска,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)	КАК ХозяйственнаяОперация,
	|	ТаблицаТовары.ВидЗапасов															КАК ВидЗапасов,
	|	НЕОПРЕДЕЛЕНО																		КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО																		КАК Отправитель,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
	|			ТОГДА ТаблицаТовары.Подразделение
	|		ИНАЧЕ ТаблицаТовары.Получатель
	|	КОНЕЦ																				КАК Получатель,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
	|			ТОГДА ТаблицаТовары.Получатель
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																				КАК СтатьяРасходов,
	|	ТаблицаТовары.АналитикаУчетаПартийПроизводства										КАК АналитикаУчетаПартийПроизводства,
	|	ТаблицаТовары.Номенклатура															КАК Номенклатура,
	|	ТаблицаТовары.Характеристика														КАК Характеристика,
	|	НЕОПРЕДЕЛЕНО																		КАК НомерГТД,
	|	ТаблицаТовары.Назначение															КАК Назначение,
	|	ТаблицаТовары.СтатусУказанияСерий													КАК СтатусУказанияСерий,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)									КАК Серия,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияСерий = 14
	|			ТОГДА ТаблицаТовары.Серия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ																				КАК КорСерия,
	|	НЕОПРЕДЕЛЕНО																		КАК Спецификация,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)									КАК СтатьяКалькуляции,
	|	ТаблицаТовары.Количество															КАК Количество,
	|	ТаблицаТовары.Сумма																	КАК Сумма,
	|	ТаблицаТовары.ИдентификаторСтроки													КАК ИдентификаторСтроки,
	|	ТаблицаТовары.АналитикаРасходов														КАК АналитикаРасходов,
	|	ТаблицаТовары.АналитикаАктивовПассивов												КАК АналитикаАктивовПассивов,
	|	ЛОЖЬ																				КАК РаботаДляДавальца
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ПобочныеИзделия КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Работа по побочным изделиям давальца
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение)			КАК НаправлениеВыпуска,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)	КАК ХозяйственнаяОперация,
	|	&ВидЗапасовРаботы																	КАК ВидЗапасов,
	|	ТаблицаТовары.ВидЗапасов															КАК КорВидЗапасов,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
	|			ТОГДА ТаблицаТовары.Подразделение
	|		ИНАЧЕ ТаблицаТовары.Получатель
	|	КОНЕЦ																				КАК Отправитель,
	|	ТаблицаТовары.Назначение.Заказ.Подразделение										КАК Получатель,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
	|			ТОГДА ТаблицаТовары.Получатель
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																				КАК СтатьяРасходов,
	|	ТаблицаТовары.АналитикаУчетаПартийПроизводства										КАК АналитикаУчетаПартийПроизводства,
	|	ТаблицаТовары.Номенклатура															КАК Номенклатура,
	|	ТаблицаТовары.Характеристика														КАК Характеристика,
	|	НЕОПРЕДЕЛЕНО																		КАК НомерГТД,
	|	ТаблицаТовары.Назначение															КАК Назначение,
	|	ТаблицаТовары.СтатусУказанияСерий													КАК СтатусУказанияСерий,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияСерий = 14
	|			ТОГДА ТаблицаТовары.Серия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ																				КАК Серия,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)									КАК КорСерия,
	|	НЕОПРЕДЕЛЕНО																		КАК Спецификация,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)									КАК СтатьяКалькуляции,
	|	ТаблицаТовары.Количество															КАК Количество,
	|	ТаблицаТовары.Сумма																	КАК Сумма,
	|	ТаблицаТовары.ИдентификаторСтроки													КАК ИдентификаторСтроки,
	|	ТаблицаТовары.АналитикаРасходов														КАК АналитикаРасходов,
	|	ТаблицаТовары.АналитикаАктивовПассивов												КАК АналитикаАктивовПассивов,
	|	ИСТИНА																				КАК РаботаДляДавальца
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ПобочныеИзделия КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.ПередатьДавальцу
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Побочные изделия по партии
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРасходовНаПартииПроизводства)	КАК НаправлениеВыпуска,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)	КАК ХозяйственнаяОперация,
	|	ТаблицаТовары.ВидЗапасов															КАК ВидЗапасов,
	|	НЕОПРЕДЕЛЕНО																		КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО																		КАК Отправитель,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
	|			ТОГДА ТаблицаТовары.Подразделение
	|		КОГДА ТаблицаТовары.Получатель ССЫЛКА Справочник.Склады
	|			ТОГДА ТаблицаТовары.Получатель.Подразделение
	|		ИНАЧЕ ТаблицаТовары.Получатель
	|	КОНЕЦ																				КАК Получатель,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяРасходов,
	|	ТаблицаТовары.АналитикаУчетаПартийПроизводства										КАК АналитикаУчетаПартийПроизводства,
	|	ТаблицаТовары.Номенклатура															КАК Номенклатура,
	|	ТаблицаТовары.Характеристика														КАК Характеристика,
	|	НЕОПРЕДЕЛЕНО																		КАК НомерГТД,
	|	ТаблицаТовары.Назначение															КАК Назначение,
	|	ТаблицаТовары.СтатусУказанияСерий													КАК СтатусУказанияСерий,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)									КАК Серия,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)									КАК КорСерия,
	|	НЕОПРЕДЕЛЕНО																		КАК Спецификация,
	|	ТаблицаТовары.СтатьяКалькуляции														КАК СтатьяКалькуляции,
	|	-1 * ТаблицаТовары.Количество														КАК Количество,
	|	-1 * ТаблицаТовары.Сумма															КАК Сумма,
	|	ТаблицаТовары.ИдентификаторСтроки													КАК ИдентификаторСтроки,
	|	ТаблицаТовары.АналитикаРасходов														КАК АналитикаРасходов,
	|	ТаблицаТовары.АналитикаАктивовПассивов												КАК АналитикаАктивовПассивов,
	|	ЛОЖЬ																				КАК РаботаДляДавальца
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ПобочныеИзделия КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Работы
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРасходовНаПартииПроизводства)	КАК НаправлениеВыпуска,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРасходовНаПартииПроизводства)	КАК ХозяйственнаяОперация,
	|	&ВидЗапасовРаботы																	КАК ВидЗапасов,
	|	НЕОПРЕДЕЛЕНО																		КАК КорВидЗапасов,
	|	ТаблицаТовары.Отправитель															КАК Отправитель,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Отправитель ССЫЛКА Справочник.Склады
	|			ТОГДА ТаблицаТовары.Отправитель.Подразделение
	|		ИНАЧЕ ТаблицаТовары.Отправитель
	|	КОНЕЦ																				КАК Получатель,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяРасходов,
	|	ТаблицаТовары.АналитикаУчетаПартийПроизводства										КАК АналитикаУчетаПартийПроизводства,
	|	ТаблицаТовары.Номенклатура															КАК Номенклатура,
	|	ТаблицаТовары.Характеристика														КАК Характеристика,
	|	НЕОПРЕДЕЛЕНО																		КАК НомерГТД,
	|	ТаблицаТовары.Назначение															КАК Назначение,
	|	ТаблицаТовары.СтатусУказанияСерий													КАК СтатусУказанияСерий,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)									КАК Серия,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)									КАК КорСерия,
	|	НЕОПРЕДЕЛЕНО																		КАК Спецификация,
	|	ТаблицаТовары.СтатьяКалькуляции														КАК СтатьяКалькуляции,
	|	ТаблицаТовары.Количество															КАК Количество,
	|	0																					КАК Сумма,
	|	НЕОПРЕДЕЛЕНО																		КАК ИдентификаторСтроки,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаАктивовПассивов,
	|	ЛОЖЬ																				КАК РаботаДляДавальца
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.МатериалыИРаботы КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО (СпрНоменклатура.Ссылка = ТаблицаТовары.Номенклатура)
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И СпрНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Материалы
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРасходовНаПартииПроизводства)	КАК НаправлениеВыпуска,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРасходовНаПартииПроизводства)	КАК ХозяйственнаяОперация,
	|	ТаблицаТовары.ВидЗапасов															КАК ВидЗапасов,
	|	НЕОПРЕДЕЛЕНО																		КАК КорВидЗапасов,
	|	КлючиНоменклатуры.Склад																КАК Отправитель,
	|	ВЫБОР
	|		КОГДА КлючиНоменклатуры.Склад ССЫЛКА Справочник.Склады
	|			ТОГДА КлючиНоменклатуры.Склад.Подразделение
	|		ИНАЧЕ КлючиНоменклатуры.Склад
	|	КОНЕЦ																				КАК Получатель,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяРасходов,
	|	ТаблицаТовары.АналитикаУчетаПартийПроизводства										КАК АналитикаУчетаПартийПроизводства,
	|	КлючиНоменклатуры.Номенклатура														КАК Номенклатура,
	|	КлючиНоменклатуры.Характеристика													КАК Характеристика,
	|	ТаблицаТовары.НомерГТД																КАК НомерГТД,
	|	КлючиНоменклатуры.Назначение														КАК Назначение,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатусУказанияСерий,
	|	КлючиНоменклатуры.Серия																КАК Серия,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)									КАК КорСерия,
	|	НЕОПРЕДЕЛЕНО																		КАК Спецификация,
	|	ТаблицаТовары.СтатьяКалькуляции														КАК СтатьяКалькуляции,
	|	ТаблицаТовары.Количество															КАК Количество,
	|	0																					КАК Сумма,
	|	НЕОПРЕДЕЛЕНО																		КАК ИдентификаторСтроки,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаАктивовПассивов,
	|	ЛОЖЬ																				КАК РаботаДляДавальца
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ВидыЗапасов КАК ТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиНоменклатуры
	|		ПО (КлючиНоменклатуры.КлючАналитики = ТаблицаТовары.АналитикаУчетаНоменклатуры)
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра); 
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтВидыЗапасов(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтВидыЗапасов";
	
	ИнициализироватьКлючиАналитикиНоменклатуры(Запрос);
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	втТовары.НаправлениеВыпуска									КАК НаправлениеВыпуска,
		|	втТовары.ХозяйственнаяОперация								КАК ХозяйственнаяОперация,
		|	втТовары.ВидЗапасов											КАК ВидЗапасов,
		|	СпрВидыЗапасов.ВидЗапасовВладельца							КАК ВидЗапасовВладельца,
		|	СпрВидыЗапасов.РеализацияЗапасовДругойОрганизации			КАК РеализацияЗапасовДругойОрганизации,
		|	втТовары.КорВидЗапасов										КАК КорВидЗапасов,
		|	втТовары.Отправитель										КАК Отправитель,
		|	КлючиНоменклатурыПоОтправителю.КлючАналитики				КАК АналитикаУчетаНоменклатурыПоОтправителю,
		|	КлючиНоменклатурыПоОтправителюБезНазначения.КлючАналитики	КАК АналитикаУчетаНоменклатурыПоОтправителюБезНазначения,
		|	втТовары.Получатель											КАК Получатель,
		|	втТовары.СтатьяРасходов										КАК СтатьяРасходов,
		|	КлючиНоменклатурыПолучатель.КлючАналитики					КАК АналитикаУчетаНоменклатурыПоПолучателю,
		|	КлючиНоменклатурыПолучательБезНазначения.КлючАналитики		КАК АналитикаУчетаНоменклатурыПоПолучателюБезНазначения,
		|	втТовары.АналитикаУчетаПартийПроизводства					КАК АналитикаУчетаПартийПроизводства,
		|	втТовары.Номенклатура										КАК Номенклатура,
		|	втТовары.Характеристика										КАК Характеристика,
		|	втТовары.Назначение											КАК Назначение,
		|	втТовары.НомерГТД											КАК НомерГТД,
		|	втТовары.Серия												КАК Серия,
		|	втТовары.КорСерия											КАК КорСерия,
		|	втТовары.СтатьяКалькуляции									КАК СтатьяКалькуляции,
		|	втТовары.Спецификация										КАК Спецификация,
		|	втТовары.Количество											КАК Количество,
		|	втТовары.Сумма												КАК Сумма,
		|	втТовары.ИдентификаторСтроки								КАК ИдентификаторСтроки,
		|	втТовары.АналитикаРасходов									КАК АналитикаРасходов,
		|	втТовары.АналитикаАктивовПассивов							КАК АналитикаАктивовПассивов,
		|	втТовары.РаботаДляДавальца									КАК РаботаДляДавальца
		|ПОМЕСТИТЬ втВидыЗапасов
		|ИЗ
		|	втТовары КАК втТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиНоменклатурыПоОтправителю
		|		ПО втТовары.Номенклатура = КлючиНоменклатурыПоОтправителю.Номенклатура
		|			И втТовары.Характеристика = КлючиНоменклатурыПоОтправителю.Характеристика
		|			И втТовары.Серия = КлючиНоменклатурыПоОтправителю.Серия
		|			И втТовары.Отправитель = КлючиНоменклатурыПоОтправителю.Склад
		|			И втТовары.Назначение = КлючиНоменклатурыПоОтправителю.Назначение
		|			И КлючиНоменклатурыПоОтправителю.СтатьяКалькуляции = ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиНоменклатурыПоОтправителюБезНазначения
		|		ПО втТовары.Номенклатура = КлючиНоменклатурыПоОтправителюБезНазначения.Номенклатура
		|			И втТовары.Характеристика = КлючиНоменклатурыПоОтправителюБезНазначения.Характеристика
		|			И втТовары.Серия = КлючиНоменклатурыПоОтправителюБезНазначения.Серия
		|			И втТовары.Отправитель = КлючиНоменклатурыПоОтправителюБезНазначения.Склад
		|			И КлючиНоменклатурыПоОтправителюБезНазначения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|			И КлючиНоменклатурыПоОтправителюБезНазначения.СтатьяКалькуляции = ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиНоменклатурыПолучатель
		|		ПО втТовары.Номенклатура = КлючиНоменклатурыПолучатель.Номенклатура
		|			И втТовары.Характеристика = КлючиНоменклатурыПолучатель.Характеристика
		|			И втТовары.КорСерия = КлючиНоменклатурыПолучатель.Серия
		|			И втТовары.Получатель = КлючиНоменклатурыПолучатель.Склад
		|			И втТовары.Назначение = КлючиНоменклатурыПолучатель.Назначение
		|			И втТовары.СтатьяКалькуляции = КлючиНоменклатурыПолучатель.СтатьяКалькуляции
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиНоменклатурыПолучательБезНазначения
		|		ПО втТовары.Номенклатура = КлючиНоменклатурыПолучательБезНазначения.Номенклатура
		|			И втТовары.Характеристика = КлючиНоменклатурыПолучательБезНазначения.Характеристика
		|			И втТовары.КорСерия = КлючиНоменклатурыПолучательБезНазначения.Серия
		|			И втТовары.Получатель = КлючиНоменклатурыПолучательБезНазначения.Склад
		|			И КлючиНоменклатурыПолучательБезНазначения.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|			И втТовары.СтатьяКалькуляции = КлючиНоменклатурыПолучательБезНазначения.СтатьяКалькуляции
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК СпрВидыЗапасов
		|		ПО СпрВидыЗапасов.Ссылка = втТовары.ВидЗапасов";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра); 
	Возврат ТекстЗапроса;
	
КонецФункции

//++ НЕ УТКА
Функция ТекстЗапросаТаблицаУслугиДавальцуКОформлению(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "УслугиДавальцуКОформлению";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)							КАК ВидДвижения,
	|	&Дата															КАК Период,
	|	ТаблицаТовары.Назначение.Заказ									КАК ЗаказДавальца,
	|	ТаблицаТовары.Номенклатура										КАК Номенклатура,
	|	ТаблицаТовары.Характеристика									КАК Характеристика,
	|	ТаблицаТовары.Количество										КАК КОформлению,
	|	0																КАК Заказано,
	|	0																КАК Сумма,
	|	ЗНАЧЕНИЕ(Справочник.ПричиныОтменыЗаказовКлиентов.ПустаяСсылка)	КАК ПричинаОтмены,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС)					КАК НалогообложениеНДС
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.ПередатьДавальцу
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)							КАК ВидДвижения,
	|	&Дата															КАК Период,
	|	ТаблицаТовары.Назначение.Заказ									КАК ЗаказДавальца,
	|	ТаблицаТовары.Номенклатура										КАК Номенклатура,
	|	ТаблицаТовары.Характеристика									КАК Характеристика,
	|	ТаблицаТовары.Количество										КАК КОформлению,
	|	0																КАК Заказано,
	|	0																КАК Сумма,
	|	ЗНАЧЕНИЕ(Справочник.ПричиныОтменыЗаказовКлиентов.ПустаяСсылка)	КАК ПричинаОтмены,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС)					КАК НалогообложениеНДС
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ПобочныеИзделия КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.ПередатьДавальцу";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции
//-- НЕ УТКА

Функция ТекстЗапросаТаблицаТоварыОрганизаций(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТоварыОрганизаций";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса =
	// приход продукции и возвратных отходов в кладовую
	"ВЫБРАТЬ
	|	&Дата													КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)					КАК ВидДвижения,
	|	втВидыЗапасов.АналитикаУчетаНоменклатурыПоПолучателю	КАК АналитикаУчетаНоменклатуры,
	|	&Организация											КАК Организация,
	|	втВидыЗапасов.ВидЗапасов								КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)				КАК НомерГТД,
	|	втВидыЗапасов.Количество								КАК Количество,
	|	втВидыЗапасов.ХозяйственнаяОперация						КАК ХозяйственнаяОперация,
	|	&ВыпускПодДеятельность									КАК НалогообложениеНДС,
	|	втВидыЗапасов.Номенклатура								КАК Номенклатура,
	|	втВидыЗапасов.Характеристика							КАК Характеристика,
	|	ИСТИНА													КАК Первичное
	|ИЗ
	|	втВидыЗапасов КАК втВидыЗапасов
	|ГДЕ
	|	втВидыЗапасов.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	// списание материалов из кладовых
	|ВЫБРАТЬ
	|	&Дата													КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)					КАК ВидДвижения,
	|	втВидыЗапасов.АналитикаУчетаНоменклатурыПоОтправителю	КАК АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА втВидыЗапасов.РеализацияЗапасовДругойОрганизации
	|			ТОГДА втВидыЗапасов.ВидЗапасовВладельца.Организация
	|		ИНАЧЕ &Организация
	|	КОНЕЦ													КАК Организация,
	|	ВЫБОР
	|		КОГДА втВидыЗапасов.РеализацияЗапасовДругойОрганизации
	|			ТОГДА втВидыЗапасов.ВидЗапасовВладельца
	|		ИНАЧЕ втВидыЗапасов.ВидЗапасов
	|	КОНЕЦ													КАК ВидЗапасов,
	|	втВидыЗапасов.НомерГТД									КАК НомерГТД,
	|	втВидыЗапасов.Количество								КАК Количество,
	|	втВидыЗапасов.ХозяйственнаяОперация						КАК ХозяйственнаяОперация,
	|	&ВыпускПодДеятельность									КАК НалогообложениеНДС,
	|	втВидыЗапасов.Номенклатура								КАК Номенклатура,
	|	втВидыЗапасов.Характеристика							КАК Характеристика,
	|	ИСТИНА													КАК Первичное
	|ИЗ
	|	втВидыЗапасов КАК втВидыЗапасов
	|ГДЕ
	|	втВидыЗапасов.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРасходовНаПартииПроизводства)
	|	И НЕ втВидыЗапасов.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
	|	И НЕ втВидыЗапасов.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаТоварыОрганизацийКПередаче(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТоварыОрганизацийКПередаче";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)					КАК ВидДвижения,
	|	&Дата													КАК Период,
	|	ВтВидыЗапасов.Отправитель								КАК Склад,
	|	ВтВидыЗапасов.ВидЗапасовВладельца.Организация			КАК ОрганизацияВладелец,
	|	ВтВидыЗапасов.АналитикаУчетаНоменклатурыПоОтправителю	КАК АналитикаУчетаНоменклатуры,
	|	ВтВидыЗапасов.ВидЗапасов								КАК ВидЗапасовПродавца,
	|	втВидыЗапасов.НомерГТД									КАК НомерГТД,
	|	ВтВидыЗапасов.Номенклатура								КАК Номенклатура,
	|	ВтВидыЗапасов.Характеристика							КАК Характеристика,
	|	ВтВидыЗапасов.Количество								КАК Количество,
	|	ВтВидыЗапасов.ХозяйственнаяОперация						КАК ХозяйственнаяОперация,
	|	&ВыпускПодДеятельность									КАК НалогообложениеНДС
	|ИЗ
	|	втВидыЗапасов КАК втВидыЗапасов
	|ГДЕ
	|	втВидыЗапасов.РеализацияЗапасовДругойОрганизации
	|	И втВидыЗапасов.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРасходовНаПартииПроизводства)
	|	И НЕ втВидыЗапасов.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
	|	И НЕ втВидыЗапасов.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаТоварыНаСкладах(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТоварыНаСкладах";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса =
	// приход продукции в кладовую
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|	&Дата									КАК Период,
	|	ТаблицаТовары.Получатель				КАК Склад,
	|	ТаблицаТовары.Номенклатура				КАК Номенклатура,
	|	ТаблицаТовары.Характеристика			КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Назначение.ДвиженияПоСкладскимРегистрам
	|			ТОГДА ТаблицаТовары.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ									КАК Назначение,
	|	ТаблицаТовары.Серия						КАК Серия,
	|	ТаблицаТовары.Количество				КАК ВНаличии,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияСерий = 14
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ									КАК КонтролироватьОстатки
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад)
	|	И НЕ ТаблицаТовары.СтатусУказанияСерий В (4, 6, 8, 10)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|	&Дата									КАК Период,
	|	ТаблицаСерий.Получатель					КАК Склад,
	|	ТаблицаСерий.Номенклатура				КАК Номенклатура,
	|	ТаблицаСерий.Характеристика				КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ТаблицаСерий.Назначение.ДвиженияПоСкладскимРегистрам
	|			ТОГДА ТаблицаСерий.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ									КАК Назначение,
	|	ТаблицаСерий.Серия						КАК Серия,
	|	ТаблицаСерий.Количество					КАК ВНаличии,
	|	ЛОЖЬ									КАК КонтролироватьОстатки
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ВыходныеИзделияСерии КАК ТаблицаСерий
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ТаблицаТовары
	|		ПО ТаблицаСерий.Ссылка = ТаблицаТовары.Ссылка
	|			И ТаблицаСерий.Номенклатура = ТаблицаТовары.Номенклатура
	|			И ТаблицаСерий.Характеристика = ТаблицаТовары.Характеристика
	|			И ТаблицаСерий.Назначение = ТаблицаТовары.Назначение
	|			И ТаблицаСерий.Получатель = ТаблицаТовары.Получатель
	|ГДЕ
	|	ТаблицаСерий.Ссылка = &Ссылка
	|	И ТаблицаТовары.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад)
	|	И ТаблицаТовары.СтатусУказанияСерий В (4, 6, 8, 10)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	// приход возвратных отходов в кладовую
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|	&Дата									КАК Период,
	|	ТаблицаТовары.Получатель				КАК Склад,
	|	ТаблицаТовары.Номенклатура				КАК Номенклатура,
	|	ТаблицаТовары.Характеристика			КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Назначение.ДвиженияПоСкладскимРегистрам
	|			ТОГДА ТаблицаТовары.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ									КАК Назначение,
	|	ТаблицаТовары.Серия						КАК Серия,
	|	ТаблицаТовары.Количество				КАК ВНаличии,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.СтатусУказанияСерий = 14
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ									КАК КонтролироватьОстатки
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ПобочныеИзделия КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад)
	|	И НЕ ТаблицаТовары.СтатусУказанияСерий В (4, 6, 8, 10)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|	&Дата									КАК Период,
	|	ТаблицаСерий.Получатель					КАК Склад,
	|	ТаблицаСерий.Номенклатура				КАК Номенклатура,
	|	ТаблицаСерий.Характеристика				КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ТаблицаСерий.Назначение.ДвиженияПоСкладскимРегистрам
	|			ТОГДА ТаблицаСерий.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ									КАК Назначение,
	|	ТаблицаСерий.Серия						КАК Серия,
	|	ТаблицаСерий.Количество					КАК ВНаличии,
	|	ЛОЖЬ									КАК КонтролироватьОстатки
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ПобочныеИзделияСерии КАК ТаблицаСерий
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа.ПобочныеИзделия КАК ТаблицаТовары
	|		ПО ТаблицаСерий.Ссылка = ТаблицаТовары.Ссылка
	|			И ТаблицаСерий.Номенклатура = ТаблицаТовары.Номенклатура
	|			И ТаблицаСерий.Характеристика = ТаблицаТовары.Характеристика
	|			И ТаблицаСерий.Назначение = ТаблицаТовары.Назначение
	|			И ТаблицаСерий.Получатель = ТаблицаТовары.Получатель
	|ГДЕ
	|	ТаблицаСерий.Ссылка = &Ссылка
	|	И ТаблицаТовары.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад)
	|	И ТаблицаТовары.СтатусУказанияСерий В (4, 6, 8, 10)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	// расход материалов из кладовой
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)	КАК ВидДвижения,
	|	&Дата									КАК Период,
	|	МатериалыИРаботы.Отправитель			КАК Склад,
	|	МатериалыИРаботы.Номенклатура			КАК Номенклатура,
	|	МатериалыИРаботы.Характеристика			КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(МатериалыИРаботы.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|			ТОГДА МатериалыИРаботы.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ									КАК Назначение,
	|	ВЫБОР
	|		КОГДА МатериалыИРаботы.СтатусУказанияСерий В (4, 6, 8, 10, 14)
	|			ТОГДА МатериалыИРаботы.Серия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ									КАК Серия,
	|	МатериалыИРаботы.Количество				КАК ВНаличии,
	|	ВЫБОР
	|		КОГДА МатериалыИРаботы.СтатусУказанияСерий В (6, 8, 10, 14)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ									КАК КонтролироватьОстатки
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.МатериалыИРаботы КАК МатериалыИРаботы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО (СпрНоменклатура.Ссылка = МатериалыИРаботы.Номенклатура)
	|		И НЕ СпрНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|ГДЕ
	|	МатериалыИРаботы.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаСебестоимостьТоваров(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СебестоимостьТоваров";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	
	ТекстЗапроса =
	// приход продукции и возвратных отходов
	"ВЫБРАТЬ
	|	1																				КАК Порядок,
	|	&Дата																			КАК Период,
	|	&Ссылка																			КАК ДокументДвижения,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)											КАК ВидДвижения,
	|	ВтВидыЗапасов.ИдентификаторСтроки												КАК ИдентификаторСтроки,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)									КАК ТипЗаписи,
	|	ВЫБОР
	|		КОГДА ВтВидыЗапасов.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
	|		КОГДА СпрНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад)
	|	КОНЕЦ																			КАК ХозяйственнаяОперация,
	|	ВЫБОР
	|		КОГДА ВтВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)
	|		КОГДА ВтВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПродукцияДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|	КОНЕЦ																			КАК РазделУчета,
	|	&Организация																	КАК Организация,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов
	|			ТОГДА ВтВидыЗапасов.ВидЗапасов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																			КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов
	|			ТОГДА ВтВидыЗапасов.АналитикаУчетаНоменклатурыПоПолучателю
	|		ИНАЧЕ ВтВидыЗапасов.АналитикаУчетаНоменклатурыПоПолучателюБезНазначения
	|	КОНЕЦ																			КАК АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|				И &ФИФОСкользящаяОценка
	|			ТОГДА &Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																			КАК Партия,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|				И &ФИФОСкользящаяОценка
	|			ТОГДА &ПустаяАналитикаУчетаПартий
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ 																			КАК АналитикаУчетаПартий,
	|	ВЫБОР
	|		КОГДА &АналитическийУчетПоГруппамПродукции
	|			И НЕ ОсновныеИзделия.Номенклатура.ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА ОсновныеИзделия.Номенклатура.ГруппаАналитическогоУчета
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																			КАК АналитикаФинансовогоУчета,
	|	&ВыпускПодДеятельность															КАК ВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО																	КАК КорРазделУчета,
	|	НЕОПРЕДЕЛЕНО																	КАК КорОрганизация,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов
	|			ТОГДА ВтВидыЗапасов.КорВидЗапасов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																			КАК КорВидЗапасов,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов
	|			ТОГДА ВтВидыЗапасов.АналитикаУчетаНоменклатурыПоОтправителю
	|		ИНАЧЕ ВтВидыЗапасов.АналитикаУчетаНоменклатурыПоОтправителюБезНазначения
	|	КОНЕЦ																			КАК КорАналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО																	КАК КорВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО																	КАК КорПартия,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|				И НЕ &ФИФОСкользящаяОценка
	|			ТОГДА &ПустаяАналитикаУчетаПартий
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ 																			КАК КорАналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО																	КАК КорАналитикаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО																	КАК АналитикаУчетаПартийПроизводства,
	|	НЕОПРЕДЕЛЕНО																	КАК СтатьяКалькуляции,
	|	НЕОПРЕДЕЛЕНО																	КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО																	КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО																	КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО																	КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО																	КАК АналитикаАктивовПассивов,
	|	ВтВидыЗапасов.Количество														КАК Количество,
	|	ВЫРАЗИТЬ(ВтВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2))	КАК Стоимость,
	|	ВЫРАЗИТЬ(ВтВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2))	КАК СтоимостьБезНДС,
	|	ВЫРАЗИТЬ(ВтВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))	КАК СтоимостьРегл
	|ИЗ
	|	ВтВидыЗапасов КАК ВтВидыЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО ВтВидыЗапасов.Номенклатура = СпрНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ОсновныеИзделия
	|		ПО &АналитическийУчетПоГруппамПродукции
	|			И ОсновныеИзделия.ОсновноеИзделиеГруппыЗатрат
	|			И ОсновныеИзделия.Ссылка = &Ссылка
	|			И ОсновныеИзделия.АналитикаУчетаПартийПроизводства = ВтВидыЗапасов.АналитикаУчетаПартийПроизводства
	|ГДЕ
	|	втВидыЗапасов.НаправлениеВыпуска В
	|		(ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// расход продукции и возвратных отходов при списании на расходы
	|ВЫБРАТЬ
	|	2																				КАК Порядок,
	|	&Дата																			КАК Период,
	|	&Ссылка																			КАК ДокументДвижения,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)											КАК ВидДвижения,
	|	втВидыЗапасов.ИдентификаторСтроки												КАК ИдентификаторСтроки,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Потребление)							КАК ТипЗаписи,
	|	втВидыЗапасов.НаправлениеВыпуска												КАК ХозяйственнаяОперация,
	|	ВЫБОР
	|		КОГДА ВтВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)
	|		КОГДА ВтВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПродукцияДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|	КОНЕЦ																			КАК РазделУчета,
	|	&Организация																	КАК Организация,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов
	|			ТОГДА ВтВидыЗапасов.ВидЗапасов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																			КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов
	|			ТОГДА ВтВидыЗапасов.АналитикаУчетаНоменклатурыПоПолучателю
	|		ИНАЧЕ ВтВидыЗапасов.АналитикаУчетаНоменклатурыПоПолучателюБезНазначения
	|	КОНЕЦ																			КАК АналитикаУчетаНоменклатуры,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|				И &ФИФОСкользящаяОценка
	|			ТОГДА &Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																			КАК Партия,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|				И &ФИФОСкользящаяОценка
	|			ТОГДА &ПустаяАналитикаУчетаПартий
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ 																			КАК АналитикаУчетаПартий,
	|	ВЫБОР
	|		КОГДА &АналитическийУчетПоГруппамПродукции
	|			И НЕ ОсновныеИзделия.Номенклатура.ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА ОсновныеИзделия.Номенклатура.ГруппаАналитическогоУчета
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																			КАК АналитикаФинансовогоУчета,
	|	&ВыпускПодДеятельность															КАК ВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО																	КАК КорРазделУчета,
	|	&Организация																	КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО																	КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО																	КАК КорАналитикаУчетаНоменклатуры,
	|	&ВыпускПодДеятельность															КАК КорВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО																	КАК КорПартия,
	|	ВЫБОР
	|		КОГДА &ПартионныйУчетВерсии22
	|				И НЕ &ФИФОСкользящаяОценка
	|			ТОГДА &ПустаяАналитикаУчетаПартий
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ 																			КАК КорАналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО																	КАК КорАналитикаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО																	КАК АналитикаУчетаПартийПроизводства,
	|	НЕОПРЕДЕЛЕНО																	КАК СтатьяКалькуляции,
	|	&Подразделение																	КАК Подразделение,
	|	втВидыЗапасов.СтатьяРасходов													КАК СтатьяРасходовСписания,
	|	втВидыЗапасов.АналитикаРасходов													КАК АналитикаРасходов,
	|	втВидыЗапасов.СтатьяРасходов													КАК СтатьяАктивовПассивов,
	|	втВидыЗапасов.АналитикаАктивовПассивов											КАК АналитикаАктивовПассивов,
	|	втВидыЗапасов.Количество														КАК Количество,
	|	ВЫРАЗИТЬ(ВтВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2))  КАК Стоимость,
	|	ВЫРАЗИТЬ(ВтВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2))  КАК СтоимостьБезНДС,
	|	ВЫРАЗИТЬ(ВтВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2)) КАК СтоимостьРегл
	|ИЗ
	|	ВтВидыЗапасов КАК ВтВидыЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ОсновныеИзделия
	|		ПО &АналитическийУчетПоГруппамПродукции
	|			И ОсновныеИзделия.ОсновноеИзделиеГруппыЗатрат
	|			И ОсновныеИзделия.Ссылка = &Ссылка
	|			И ОсновныеИзделия.АналитикаУчетаПартийПроизводства = ВтВидыЗапасов.АналитикаУчетаПартийПроизводства
	|ГДЕ
	|	ВтВидыЗапасов.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// расход материалов и работ на партию производства
	|ВЫБРАТЬ
	|	3																					КАК Порядок,
	|	&Дата																				КАК Период,
	|	&Ссылка																				КАК ДокументДвижения,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)												КАК ВидДвижения,
	|	НЕОПРЕДЕЛЕНО																		КАК ИдентификаторСтроки,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Потребление)								КАК ТипЗаписи,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРасходовНаПартииПроизводства)	КАК ХозяйственнаяОперация,
	|	ВЫБОР
	|		КОГДА ВтВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)
	|		КОГДА ВтВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПродукцияДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|	КОНЕЦ																				КАК РазделУчета,
	|	&Организация																		КАК Организация,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов
	|			ТОГДА ВтВидыЗапасов.ВидЗапасов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																				КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов
	|			ТОГДА ВтВидыЗапасов.АналитикаУчетаНоменклатурыПоОтправителю
	|		ИНАЧЕ ВтВидыЗапасов.АналитикаУчетаНоменклатурыПоОтправителюБезНазначения
	|	КОНЕЦ																				КАК АналитикаУчетаНоменклатуры,
	|	НЕОПРЕДЕЛЕНО																		КАК Партия,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаФинансовогоУчета,
	|	НЕОПРЕДЕЛЕНО																		КАК ВидДеятельностиНДС,
	|	ВЫБОР
	|		КОГДА ВтВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)
	|		КОГДА ВтВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПродукцияДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство)
	|	КОНЕЦ																				КАК КорРазделУчета,
	|	&Организация																		КАК КорОрганизация,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов
	|			ТОГДА ВтВидыЗапасов.ВидЗапасов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																				КАК КорВидЗапасов,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов
	|			ТОГДА ВтВидыЗапасов.АналитикаУчетаНоменклатурыПоПолучателю
	|		ИНАЧЕ ВтВидыЗапасов.АналитикаУчетаНоменклатурыПоПолучателюБезНазначения
	|	КОНЕЦ																				КАК КорАналитикаУчетаНоменклатуры,
	|	&ВыпускПодДеятельность																КАК КорВидДеятельностиНДС,
	|	&Ссылка																				КАК КорПартия,
	|	НЕОПРЕДЕЛЕНО																		КАК КорАналитикаУчетаПартий,
	|	ВЫБОР
	|		КОГДА &АналитическийУчетПоГруппамПродукции
	|			И НЕ ОсновныеИзделия.Номенклатура.ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА ОсновныеИзделия.Номенклатура.ГруппаАналитическогоУчета
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																				КАК КорАналитикаФинансовогоУчета,
	|	ВтВидыЗапасов.АналитикаУчетаПартийПроизводства										КАК АналитикаУчетаПартийПроизводства,
	|	ВтВидыЗапасов.СтатьяКалькуляции														КАК СтатьяКалькуляции,
	|	НЕОПРЕДЕЛЕНО																		КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаАктивовПассивов,
	|	ВтВидыЗапасов.Количество															КАК Количество,
	|	0																					КАК Стоимость,
	|	0																					КАК СтоимостьБезНДС,
	|	0																					КАК СтоимостьРегл
	|ИЗ
	|	ВтВидыЗапасов КАК ВтВидыЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ОсновныеИзделия
	|		ПО &АналитическийУчетПоГруппамПродукции
	|			И ОсновныеИзделия.ОсновноеИзделиеГруппыЗатрат
	|			И ОсновныеИзделия.Ссылка = &Ссылка
	|			И ОсновныеИзделия.АналитикаУчетаПартийПроизводства = ВтВидыЗапасов.АналитикаУчетаПартийПроизводства
	|ГДЕ
	|	ВтВидыЗапасов.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРасходовНаПартииПроизводства)
	|	И НЕ ВтВидыЗапасов.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// приход материалов, работ, возвратных отходов по партии производства
	|ВЫБРАТЬ
	|	4																					КАК Порядок,
	|	&Дата																				КАК Период,
	|	&Ссылка																				КАК ДокументДвижения,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)												КАК ВидДвижения,
	|	ВтВидыЗапасов.ИдентификаторСтроки													КАК ИдентификаторСтроки,
	|	ВЫБОР
	|		КОГДА ВтВидыЗапасов.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРасходовНаПартииПроизводства)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Перемещение)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)
	|	КОНЕЦ																				КАК ТипЗаписи,
	|	ВтВидыЗапасов.ХозяйственнаяОперация													КАК ХозяйственнаяОперация,
	|	ВЫБОР
	|		КОГДА ВтВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.МатериалДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)
	|		КОГДА ВтВидыЗапасов.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПродукцияДавальца)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство)
	|	КОНЕЦ																				КАК РазделУчета,
	|	&Организация																		КАК Организация,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов
	|			ТОГДА ВтВидыЗапасов.ВидЗапасов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																				КАК ВидЗапасов,
	|	ВЫБОР
	|		КОГДА &УчитыватьСебестоимостьТоваровПоВидамЗапасов
	|			ТОГДА ВтВидыЗапасов.АналитикаУчетаНоменклатурыПоПолучателю
	|		ИНАЧЕ ВтВидыЗапасов.АналитикаУчетаНоменклатурыПоПолучателюБезНазначения
	|	КОНЕЦ																				КАК АналитикаУчетаНоменклатуры,
	|	&Ссылка																				КАК Партия,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаУчетаПартий,
	|	ВЫБОР
	|		КОГДА &АналитическийУчетПоГруппамПродукции
	|			И НЕ ОсновныеИзделия.Номенклатура.ГруппаАналитическогоУчета = ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|			ТОГДА ОсновныеИзделия.Номенклатура.ГруппаАналитическогоУчета
	|		ИНАЧЕ
	|			НЕОПРЕДЕЛЕНО
	|	КОНЕЦ																				КАК АналитикаФинансовогоУчета,
	|	&ВыпускПодДеятельность																КАК ВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО																		КАК КорРазделУчета,
	|	&Организация																		КАК КорОрганизация,
	|	НЕОПРЕДЕЛЕНО																		КАК КорВидЗапасов,
	|	НЕОПРЕДЕЛЕНО																		КАК КорАналитикаУчетаНоменклатуры,
	|	&ВыпускПодДеятельность																КАК КорВидДеятельностиНДС,
	|	НЕОПРЕДЕЛЕНО																		КАК КорПартия,
	|	НЕОПРЕДЕЛЕНО																		КАК КорАналитикаУчетаПартий,
	|	НЕОПРЕДЕЛЕНО																		КАК КорАналитикаФинансовогоУчета,
	|	ВтВидыЗапасов.АналитикаУчетаПартийПроизводства										КАК АналитикаУчетаПартийПроизводства,
	|	ВтВидыЗапасов.СтатьяКалькуляции														КАК СтатьяКалькуляции,
	|	НЕОПРЕДЕЛЕНО																		КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяРасходовСписания,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО																		КАК СтатьяАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО																		КАК АналитикаАктивовПассивов,
	|	ВтВидыЗапасов.Количество															КАК Количество,
	|	ВЫРАЗИТЬ(ВтВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2))	КАК Стоимость,
	|	ВЫРАЗИТЬ(ВтВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2))	КАК СтоимостьБезНДС,
	|	ВЫРАЗИТЬ(ВтВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))	КАК СтоимостьРегл
	|ИЗ
	|	ВтВидыЗапасов КАК ВтВидыЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ОсновныеИзделия
	|		ПО &АналитическийУчетПоГруппамПродукции
	|			И ОсновныеИзделия.ОсновноеИзделиеГруппыЗатрат
	|			И ОсновныеИзделия.Ссылка = &Ссылка
	|			И ОсновныеИзделия.АналитикаУчетаПартийПроизводства = ВтВидыЗапасов.АналитикаУчетаПартийПроизводства
	|ГДЕ
	|	ВтВидыЗапасов.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРасходовНаПартииПроизводства)
	|УПОРЯДОЧИТЬ ПО
	|	Порядок";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаМатериалыИРаботыВПроизводстве(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "МатериалыИРаботыВПроизводстве";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	// приход работ по расчитываемой стоимости по получателю
	"ВЫБРАТЬ
	|	&Дата									КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|	&Организация							КАК Организация,
	|	ВыходныеИзделия.Получатель				КАК Подразделение,
	|	ВыходныеИзделия.Номенклатура			КАК Номенклатура,
	|	ВыходныеИзделия.Характеристика			КАК Характеристика,
	|	ВыходныеИзделия.Назначение				КАК Назначение,
	|	ВыходныеИзделия.Количество				КАК Количество
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ВыходныеИзделия
	|ГДЕ
	|	ВыходныеИзделия.Ссылка = &Ссылка
	|	И ВыходныеИзделия.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	// приход работ по фиксированной стоимости по получателю
	|ВЫБРАТЬ
	|	&Дата									КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|	&Организация							КАК Организация,
	|	ПобочныеРаботы.Получатель				КАК Подразделение,
	|	ПобочныеРаботы.Номенклатура				КАК Номенклатура,
	|	ПобочныеРаботы.Характеристика			КАК Характеристика,
	|	ПобочныеРаботы.Назначение				КАК Назначение,
	|	ПобочныеРаботы.Количество				КАК Количество
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ПобочныеИзделия КАК ПобочныеРаботы
	|ГДЕ
	|	ПобочныеРаботы.Ссылка = &Ссылка
	|	И ПобочныеРаботы.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	// расход работ
	|ВЫБРАТЬ
	|	&Дата									КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)	КАК ВидДвижения,
	|	&Организация							КАК Организация,
	|	РасходРабот.Отправитель					КАК Подразделение,
	|	РасходРабот.Номенклатура				КАК Номенклатура,
	|	РасходРабот.Характеристика				КАК Характеристика,
	|	РасходРабот.Назначение					КАК Назначение,
	|	РасходРабот.Количество					КАК Количество
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.МатериалыИРаботы КАК РасходРабот
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО РасходРабот.Номенклатура = СпрНоменклатура.Ссылка
	|		И СпрНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|ГДЕ
	|	РасходРабот.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВыпускПродукции(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ВыпускПродукции";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НЕОПРЕДЕЛЕНО											КАК КодСтроки,
	|	&Дата													КАК Период,
	|	втВидыЗапасов.АналитикаУчетаНоменклатурыПоПолучателю	КАК АналитикаУчетаНоменклатуры,
	|	&Организация											КАК Организация,
	|	НЕОПРЕДЕЛЕНО											КАК Распоряжение,
	|	втВидыЗапасов.Количество								КАК Количество,
	|	втВидыЗапасов.ВидЗапасов								КАК ВидЗапасов,
	|	втВидыЗапасов.Спецификация								КАК Спецификация,
	|	&Подразделение											КАК Подразделение,
	|	втВидыЗапасов.Получатель								КАК СтатьяРасходов,
	|	втВидыЗапасов.АналитикаРасходов							КАК АналитикаРасходов,
	|	втВидыЗапасов.АналитикаАктивовПассивов					КАК АналитикаАктивовПассивов,
	|	НЕОПРЕДЕЛЕНО											КАК Заказ,
	|	ВЫБОР
	|		КОГДА втВидыЗапасов.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
	|			ТОГДА втВидыЗапасов.АналитикаУчетаНоменклатурыПоПолучателю
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ													КАК АналитикаУчетаПродукции
	|ИЗ
	|	втВидыЗапасов КАК втВидыЗапасов
	|ГДЕ
	|	втВидыЗапасов.НаправлениеВыпуска В
	|		(ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию))
	|	И НЕ втВидыЗапасов.РаботаДляДавальца";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаОбеспечениеЗаказов(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ОбеспечениеЗаказов";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	// Отгрузка обособленного товара со склада.
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)	КАК ВидДвижения,
	|	&Дата									КАК Период,
	|	ТаблицаТовары.Отправитель				КАК Склад,
	|	ТаблицаТовары.Номенклатура				КАК Номенклатура,
	|	ТаблицаТовары.Характеристика			КАК Характеристика,
	|	ТаблицаТовары.Назначение				КАК Назначение,
	|	ТаблицаТовары.Количество				КАК Потребность,
	|	0										КАК КЗаказу,
	|	ТаблицаТовары.Количество				КАК НаличиеПодЗаказ
	|
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.МатериалыИРаботы КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТИПЗНАЧЕНИЯ(ТаблицаТовары.Отправитель) = ТИП(Справочник.Склады)
	|	И НЕ ТаблицаТовары.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	И ТаблицаТовары.Номенклатура.ТипНоменклатуры В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Отгрузка обособленного товара со склада, приход "К заказу".
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|	&Дата									КАК Период,
	|	ТаблицаТовары.Отправитель				КАК Склад,
	|	ТаблицаТовары.Номенклатура				КАК Номенклатура,
	|	ТаблицаТовары.Характеристика			КАК Характеристика,
	|	ТаблицаТовары.Назначение				КАК Назначение,
	|	ТаблицаТовары.Количество				КАК Потребность,
	|	ТаблицаТовары.Количество				КАК КЗаказу,
	|	0										КАК НаличиеПодЗаказ
	|
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.МатериалыИРаботы КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТИПЗНАЧЕНИЯ(ТаблицаТовары.Отправитель) = ТИП(Справочник.Склады)
	|	И НЕ ТаблицаТовары.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	И ТаблицаТовары.Номенклатура.ТипНоменклатуры В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Обеспечение склада выходными изделиями.
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|	&Дата									КАК Период,
	|	ТаблицаТовары.Получатель				КАК Склад,
	|	ТаблицаТовары.Номенклатура				КАК Номенклатура,
	|	ТаблицаТовары.Характеристика			КАК Характеристика,
	|	ТаблицаТовары.Назначение				КАК Назначение,
	|	0										КАК Потребность,
	|	-ТаблицаТовары.Количество				КАК КЗаказу,
	|	ТаблицаТовары.Количество				КАК НаличиеПодЗаказ
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТИПЗНАЧЕНИЯ(ТаблицаТовары.Получатель) = ТИП(Справочник.Склады)
	|	И НЕ ТаблицаТовары.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	И ТаблицаТовары.Номенклатура.ТипНоменклатуры В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Обеспечение склада побочными изделиями.
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|	&Дата									КАК Период,
	|	ТаблицаТовары.Получатель				КАК Склад,
	|	ТаблицаТовары.Номенклатура				КАК Номенклатура,
	|	ТаблицаТовары.Характеристика			КАК Характеристика,
	|	ТаблицаТовары.Назначение				КАК Назначение,
	|	0										КАК Потребность,
	|	-ТаблицаТовары.Количество				КАК КЗаказу,
	|	ТаблицаТовары.Количество				КАК НаличиеПодЗаказ
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ПобочныеИзделия КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТИПЗНАЧЕНИЯ(ТаблицаТовары.Получатель) = ТИП(Справочник.Склады)
	|	И НЕ ТаблицаТовары.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	И ТаблицаТовары.Номенклатура.ТипНоменклатуры В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаСвободныеОстатки(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "СвободныеОстатки";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса = 
	// Поступление выходных изделий в кладовую.
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|	&Дата									КАК Период,
	|	ТаблицаТовары.Получатель				КАК Склад,
	|	ТаблицаТовары.Номенклатура				КАК Номенклатура,
	|	ТаблицаТовары.Характеристика			КАК Характеристика,
	|	0										КАК ВРезервеСоСклада,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаТовары.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|			ТОГДА ТаблицаТовары.Количество
	|		ИНАЧЕ 0
	|	КОНЕЦ									КАК ВРезервеПодЗаказ,
	|	ТаблицаТовары.Количество				КАК ВНаличии
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Поступление побочных изделий в кладовую.
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|	&Дата									КАК Период,
	|	ТаблицаТовары.Получатель				КАК Склад,
	|	ТаблицаТовары.Номенклатура				КАК Номенклатура,
	|	ТаблицаТовары.Характеристика			КАК Характеристика,
	|	0										КАК ВРезервеСоСклада,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаТовары.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|			ТОГДА ТаблицаТовары.Количество
	|		ИНАЧЕ 0
	|	КОНЕЦ									КАК ВРезервеПодЗаказ,
	|	ТаблицаТовары.Количество				КАК ВНаличии
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ПобочныеИзделия КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Расход материалов из кладовых.
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)	КАК ВидДвижения,
	|	&Дата									КАК Период,
	|	ТаблицаТовары.Отправитель				КАК Склад,
	|	ТаблицаТовары.Номенклатура				КАК Номенклатура,
	|	ТаблицаТовары.Характеристика			КАК Характеристика,
	|	0										КАК ВРезервеСоСклада,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаТовары.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|			ТОГДА ТаблицаТовары.Количество
	|		ИНАЧЕ 0
	|	КОНЕЦ									КАК ВРезервеПодЗаказ,
	|	ТаблицаТовары.Количество				КАК ВНаличии
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.МатериалыИРаботы КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.Номенклатура.ТипНоменклатуры В(
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаДвиженияСерийТоваров(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ДвиженияСерийТоваров";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса = "
	// отгрузка материалов из цеховой кладовой
	|ВЫБРАТЬ
	|	&Дата																	КАК Период,
	|	ТаблицаТовары.Номенклатура												КАК Номенклатура,
	|	ТаблицаТовары.Характеристика											КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаТовары.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|			ТОГДА ТаблицаТовары.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ																	КАК Назначение,
	|	ТаблицаТовары.Серия														КАК Серия,
	|	ТаблицаТовары.Количество												КАК Количество,
	|	ТаблицаТовары.Отправитель												КАК Отправитель,
	|	СпрСклады.Подразделение													КАК Получатель,
	|	ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПередачаВПроизводствоОтгрузка)	КАК СкладскаяОперация,
	|	&Ссылка																	КАК Документ,
	|	НЕ СпрСклады.ИспользоватьОрдернуюСхемуПриОтгрузке
	|		ИЛИ &Дата < СпрСклады.ДатаНачалаОрдернойСхемыПриОтгрузке			КАК ЭтоСкладскоеДвижение
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.МатериалыИРаботы КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК СпрСклады
	|		ПО СпрСклады.Ссылка = ТаблицаТовары.Отправитель
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И НЕ ТаблицаТовары.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	// производство продукции в кладовую
	|ВЫБРАТЬ
	|	&Дата																	КАК Период,
	|	ТаблицаТовары.Номенклатура												КАК Номенклатура,
	|	ТаблицаТовары.Характеристика											КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаТовары.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|			ТОГДА ТаблицаТовары.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ																	КАК Назначение,
	|	ТаблицаТовары.Серия														КАК Серия,
	|	ТаблицаТовары.Количество												КАК Количество,
	|	ТаблицаТовары.Получатель												КАК Отправитель,
	|	ТаблицаТовары.Получатель												КАК Получатель,
	|	ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПриемкаПродукцииИзПроизводства)	КАК СкладскаяОперация,
	|	&Ссылка																	КАК Документ,
	|	НЕ СпрСклады.ИспользоватьОрдернуюСхемуПриПоступлении
	|		ИЛИ &Дата < СпрСклады.ДатаНачалаОрдернойСхемыПриПоступлении			КАК ЭтоСкладскоеДвижение
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК СпрСклады
	|		ПО СпрСклады.Ссылка = ТаблицаТовары.Получатель
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И НЕ ТаблицаТовары.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	// производство продукции в кладовую
	|ВЫБРАТЬ
	|	&Дата																	КАК Период,
	|	ТаблицаТовары.Номенклатура												КАК Номенклатура,
	|	ТаблицаТовары.Характеристика											КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаТовары.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|			ТОГДА ТаблицаТовары.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ																	КАК Назначение,
	|	ТаблицаТовары.Серия														КАК Серия,
	|	ТаблицаТовары.Количество												КАК Количество,
	|	ТаблицаТовары.Получатель												КАК Отправитель,
	|	ТаблицаТовары.Получатель												КАК Получатель,
	|	ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПриемкаПродукцииИзПроизводства)	КАК СкладскаяОперация,
	|	&Ссылка																	КАК Документ,
	|	НЕ СпрСклады.ИспользоватьОрдернуюСхемуПриПоступлении
	|		ИЛИ &Дата < СпрСклады.ДатаНачалаОрдернойСхемыПриПоступлении			КАК ЭтоСкладскоеДвижение
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ВыходныеИзделияСерии КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК СпрСклады
	|		ПО СпрСклады.Ссылка = ТаблицаТовары.Получатель
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	// производство возвратных отходов в кладовую
	|ВЫБРАТЬ
	|	&Дата															КАК Период,
	|	ТаблицаТовары.Номенклатура										КАК Номенклатура,
	|	ТаблицаТовары.Характеристика									КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаТовары.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|			ТОГДА ТаблицаТовары.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ															КАК Назначение,
	|	ТаблицаТовары.Серия												КАК Серия,
	|	ТаблицаТовары.Количество										КАК Количество,
	|	ТаблицаТовары.Получатель										КАК Отправитель,
	|	ТаблицаТовары.Получатель										КАК Получатель,
	|	ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПриемкаИзПроизводства)	КАК СкладскаяОперация,
	|	&Ссылка															КАК Документ,
	|	НЕ СпрСклады.ИспользоватьОрдернуюСхемуПриПоступлении
	|		ИЛИ &Дата < СпрСклады.ДатаНачалаОрдернойСхемыПриПоступлении	КАК ЭтоСкладскоеДвижение
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ПобочныеИзделия КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК СпрСклады
	|		ПО СпрСклады.Ссылка = ТаблицаТовары.Получатель
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И НЕ ТаблицаТовары.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	// производство возвратных отходов в кладовую
	|ВЫБРАТЬ
	|	&Дата															КАК Период,
	|	ТаблицаТовары.Номенклатура										КАК Номенклатура,
	|	ТаблицаТовары.Характеристика									КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаТовары.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|			ТОГДА ТаблицаТовары.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ															КАК Назначение,
	|	ТаблицаТовары.Серия												КАК Серия,
	|	ТаблицаТовары.Количество										КАК Количество,
	|	ТаблицаТовары.Получатель										КАК Отправитель,
	|	ТаблицаТовары.Получатель										КАК Получатель,
	|	ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПриемкаИзПроизводства)	КАК СкладскаяОперация,
	|	&Ссылка															КАК Документ,
	|	НЕ СпрСклады.ИспользоватьОрдернуюСхемуПриПоступлении
	|		ИЛИ &Дата < СпрСклады.ДатаНачалаОрдернойСхемыПриПоступлении	КАК ЭтоСкладскоеДвижение
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ПобочныеИзделияСерии КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК СпрСклады
	|		ПО СпрСклады.Ссылка = ТаблицаТовары.Получатель
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаДатыПоступленияТоваровОрганизаций(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДатыПоступленияТоваровОрганизаций";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВыходныеИзделия.НомерСтроки					КАК НомерСтроки,
	|	&Дата										КАК ДатаПоступления,
	|	ВыходныеИзделия.Номенклатура				КАК Номенклатура,
	|	ВыходныеИзделия.Характеристика				КАК Характеристика,
	|	ВыходныеИзделия.Серия						КАК Серия,
	|	ВыходныеИзделия.Назначение					КАК Назначение,
	|	ВыходныеИзделия.ВидЗапасов					КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)	КАК НомерГТД
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ВыходныеИзделия
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыПоступленияТоваровОрганизаций КАК ПоступленияТоваров
	|		ПО ВыходныеИзделия.ВидЗапасов = ПоступленияТоваров.ВидЗапасов
	|			И ВыходныеИзделия.Номенклатура = ПоступленияТоваров.Номенклатура
	|			И ВыходныеИзделия.Характеристика = ПоступленияТоваров.Характеристика
	|			И ВыходныеИзделия.Серия = ПоступленияТоваров.Серия
	|			И ВыходныеИзделия.Назначение = ПоступленияТоваров.Назначение
	|			И (ПоступленияТоваров.НомерГТД = ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка))
	|ГДЕ
	|	ВыходныеИзделия.Ссылка = &Ссылка
	|	И (ПоступленияТоваров.ВидЗапасов ЕСТЬ NULL 
	|			ИЛИ ПоступленияТоваров.ДатаПоступления < &Дата)
	|	И ВыходныеИзделия.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПобочныеИзделия.НомерСтроки					КАК НомерСтроки,
	|	&Дата										КАК ДатаПоступления,
	|	ПобочныеИзделия.Номенклатура				КАК Номенклатура,
	|	ПобочныеИзделия.Характеристика				КАК Характеристика,
	|	ПобочныеИзделия.Серия						КАК Серия,
	|	ПобочныеИзделия.Назначение					КАК Назначение,
	|	ПобочныеИзделия.ВидЗапасов					КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)	КАК НомерГТД
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.ПобочныеИзделия КАК ПобочныеИзделия
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыПоступленияТоваровОрганизаций КАК ПоступленияТоваров
	|		ПО ПобочныеИзделия.ВидЗапасов = ПоступленияТоваров.ВидЗапасов
	|			И ПобочныеИзделия.Номенклатура = ПоступленияТоваров.Номенклатура
	|			И ПобочныеИзделия.Характеристика = ПоступленияТоваров.Характеристика
	|			И ПобочныеИзделия.Серия = ПоступленияТоваров.Серия
	|			И ПобочныеИзделия.Назначение = ПоступленияТоваров.Назначение
	|			И (ПоступленияТоваров.НомерГТД = ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка))
	|ГДЕ
	|	ПобочныеИзделия.Ссылка = &Ссылка
	|	И (ПоступленияТоваров.ВидЗапасов ЕСТЬ NULL 
	|			ИЛИ ПоступленияТоваров.ДатаПоступления < &Дата)
	|	И ПобочныеИзделия.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВидЗапасов,
	|	Номенклатура,
	|	Характеристика,
	|	Серия,
	|	Назначение";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Дата																				КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)												КАК ВидДвижения,
	|	&Организация																		КАК Организация,
	|	ВтВидыЗапасов.Получатель															КАК Подразделение,
	|	ВтВидыЗапасов.СтатьяРасходов														КАК СтатьяРасходов,
	|	ВтВидыЗапасов.АналитикаРасходов														КАК АналитикаРасходов,
	|	ВЫРАЗИТЬ(ВтВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2))	КАК Сумма,
	|	ВЫРАЗИТЬ(ВтВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2))	КАК СуммаБезНДС,
	|	ВЫРАЗИТЬ(ВтВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))	КАК СуммаРегл,
	|	ВЫБОР
	|		КОГДА Не ВтВидыЗапасов.СтатьяРасходов.ПринятиеКНалоговомуУчету
	|			ТОГДА ВЫРАЗИТЬ(ВтВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ																				КАК ПостояннаяРазница,
	|	0																					КАК ВременнаяРазница,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииФиксированнаяСтоимость)	КАК ХозяйственнаяОперация,
	|	ВтВидыЗапасов.АналитикаУчетаНоменклатурыПоПолучателю								КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	ВтВидыЗапасов КАК ВтВидыЗапасов
	|ГДЕ
	|	втВидыЗапасов.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
	|	И ВтВидыЗапасов.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов
	|	И НЕ ВтВидыЗапасов.Сумма = 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеАктивыПассивы";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Дата																				КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)												КАК ВидДвижения,
	|	&Организация																		КАК Организация,
	|	ВтВидыЗапасов.СтатьяРасходов														КАК Статья,
	|	ВтВидыЗапасов.АналитикаАктивовПассивов												КАК Аналитика,
	|	ВЫРАЗИТЬ(ВтВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2))	КАК Сумма
	|ИЗ
	|	ВтВидыЗапасов КАК ВтВидыЗапасов
	|ГДЕ
	|	втВидыЗапасов.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
	|	И ВтВидыЗапасов.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|	И НЕ ВтВидыЗапасов.Сумма = 0";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДвиженияНоменклатураДоходыРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДвиженияНоменклатураДоходыРасходы";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	УстановитьПараметрыЗапросаКоэффициентыВалют(Запрос);
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	&Дата																				КАК Период,
		|	ВЫБОР
		|		КОГДА ВтВидыЗапасов.СтатьяРасходов.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
		|			ТОГДА ВЫБОР
		|					КОГДА ВтВидыЗапасов.СтатьяРасходов.ВидЦенностиНДС В (ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ОС), ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ОбъектыНезавершенногоСтроительства))
		|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаВСоставОС)
		|					КОГДА ВтВидыЗапасов.СтатьяРасходов.ВидЦенностиНДС = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.НМА)
		|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаВСоставНМА)
		|				КОНЕЦ
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)
		|	КОНЕЦ																				КАК ХозяйственнаяОперация,
		|	&Организация																		КАК Организация,
		|	&Подразделение																		КАК Подразделение,
		|	ВтВидыЗапасов.АналитикаУчетаНоменклатурыПоПолучателю								КАК АналитикаУчетаНоменклатуры,
		|	&Подразделение																		КАК Склад,
		|	ВтВидыЗапасов.ВидЗапасов															КАК ВидЗапасов,
		|	ВтВидыЗапасов.ВидЗапасов.ТипЗапасов													КАК ТипЗапасов,
		|	ВтВидыЗапасов.СтатьяРасходов														КАК СтатьяДоходовРасходов,
		|	ВтВидыЗапасов.АналитикаРасходов														КАК АналитикаРасходов,
		|	ВтВидыЗапасов.АналитикаАктивовПассивов												КАК АналитикаАктивовПассивов,
		|	ВтВидыЗапасов.Количество															КАК Количество,
		|	ВЫРАЗИТЬ(ВтВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2))	КАК Стоимость,
		|	ВЫРАЗИТЬ(ВтВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуУпр КАК ЧИСЛО(15, 2))	КАК СтоимостьБезНДС,
		|	ВЫРАЗИТЬ(ВтВидыЗапасов.Сумма * &КоэффициентПересчетаВВалютуРегл КАК ЧИСЛО(15, 2))	КАК СтоимостьРегл,
		|	ВЫБОР
		|		КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета
		|			ТОГДА ВтВидыЗапасов.ВидЗапасов
		|		ИНАЧЕ ВтВидыЗапасов.Номенклатура
		|	КОНЕЦ																				КАК ИсточникГФУНоменклатуры
		|ИЗ
		|	ВтВидыЗапасов КАК ВтВидыЗапасов
		|ГДЕ
		|	ВтВидыЗапасов.НаправлениеВыпуска = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Дата КАК Период,
	|	&Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ) КАК ДатаОтражения
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Дата КАК Период,
	|	&Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ), ДЕНЬ) КАК ДатаОтражения
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаТрудозатратыКОформлению(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТрудозатратыКОформлению";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)	КАК ВидДвижения,
	|	&Дата									КАК Период,
	|	&Организация							КАК Организация,
	|	Трудозатраты.Ссылка						КАК Распоряжение,
	|	Трудозатраты.ВидРабот					КАК ВидРабот,
	|	Трудозатраты.Бригада					КАК Бригада,
	|	Трудозатраты.КодСтроки					КАК КодСтрокиРаспоряжения,
	|	Трудозатраты.Количество					КАК Количество
	|ИЗ
	|	Документ.ПроизводствоБезЗаказа.Трудозатраты КАК Трудозатраты
	|ГДЕ
	|	Трудозатраты.Ссылка = &Ссылка
	|	И &РаспоряжениеДляТрудозатрат";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область Серии

Функция ТекстЗапросаЗаполненияСтатусовУказанияСерийВыходныеИзделия()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	Товары.Назначение КАК Назначение,
	|	Товары.Серия КАК Серия,
	|	Товары.Получатель КАК Получатель,
	|	Товары.Количество КАК Количество,
	|	Товары.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	Товары.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Назначение,
	|	Товары.Серия,
	|	Товары.Получатель,
	|	ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура).ВидНоменклатуры КАК ВидНоменклатуры,
	|	СУММА(Товары.Количество) КАК Количество
	|ПОМЕСТИТЬ ТоварыДляЗапроса
	|ИЗ
	|	Товары КАК Товары
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Назначение,
	|	Товары.Серия,
	|	Товары.Получатель,
	|	ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура).ВидНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Серии.Номенклатура,
	|	Серии.Характеристика,
	|	Серии.Назначение,
	|	Серии.Получатель,
	|	Серии.Количество КАК Количество
	|ПОМЕСТИТЬ Серии
	|ИЗ
	|	&Серии КАК Серии
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Серии.Номенклатура,
	|	Серии.Характеристика,
	|	Серии.Назначение,
	|	Серии.Получатель,
	|	СУММА(Серии.Количество) КАК Количество
	|ПОМЕСТИТЬ СерииДляЗапроса
	|ИЗ
	|	Серии КАК Серии
	|
	|СГРУППИРОВАТЬ ПО
	|	Серии.Номенклатура,
	|	Серии.Характеристика,
	|	Серии.Назначение,
	|	Серии.Получатель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.СтатусУказанияСерий КАК СтарыйСтатусУказанияСерий,
	|	ВЫБОР
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий ЕСТЬ NULL 
	|			ТОГДА 0
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьСебестоимостьПоСериям
	|			ТОГДА ВЫБОР
	|					КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|						ТОГДА 14
	|					ИНАЧЕ 13
	|				КОНЕЦ
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриПланированииОтгрузки
	|			ТОГДА ВЫБОР
	|					КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|							И ТоварыДляЗапроса.Количество > 0
	|						ТОГДА 10
	|					ИНАЧЕ 9
	|				КОНЕЦ
	|		КОГДА Склады.ИспользоватьОрдернуюСхемуПриОтгрузке
	|				И &Дата >= Склады.ДатаНачалаОрдернойСхемыПриОтгрузке
	|			ТОГДА 0
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриПланированииОтбора
	|			ТОГДА ВЫБОР
	|					КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчетСерийПоFEFO
	|						ТОГДА ВЫБОР
	|								КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|										И ТоварыДляЗапроса.Количество > 0
	|									ТОГДА 6
	|								ИНАЧЕ 5
	|							КОНЕЦ
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|									И ТоварыДляЗапроса.Количество > 0
	|								ТОГДА 8
	|							ИНАЧЕ 7
	|						КОНЕЦ
	|				КОНЕЦ
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПоФактуОтбора
	|				И &ФактОтбора
	|				И ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриОтгрузкеКомплектовДляРазборки
	|			ТОГДА ВЫБОР
	|					КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьОстаткиСерий
	|						ТОГДА ВЫБОР
	|								КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|										И ТоварыДляЗапроса.Количество > 0
	|									ТОГДА 4
	|								ИНАЧЕ 3
	|							КОНЕЦ
	|					ИНАЧЕ ВЫБОР
	|							КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|									И ТоварыДляЗапроса.Количество > 0
	|								ТОГДА 2
	|							ИНАЧЕ 1
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтатусУказанияСерий
	|ПОМЕСТИТЬ Статусы
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыДляЗапроса КАК ТоварыДляЗапроса
	|			ЛЕВОЕ СОЕДИНЕНИЕ СерииДляЗапроса КАК СерииДляЗапроса
	|			ПО ТоварыДляЗапроса.Номенклатура = СерииДляЗапроса.Номенклатура
	|				И ТоварыДляЗапроса.Характеристика = СерииДляЗапроса.Характеристика
	|				И ТоварыДляЗапроса.Назначение = СерииДляЗапроса.Назначение
	|				И ТоварыДляЗапроса.Получатель = СерииДляЗапроса.Получатель
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ПолитикиУчетаСерий
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|				ПО ПолитикиУчетаСерий.Склад = Склады.Ссылка
	|			ПО (ПолитикиУчетаСерий.Склад = ТоварыДляЗапроса.Получатель)
	|				И ТоварыДляЗапроса.ВидНоменклатуры = ПолитикиУчетаСерий.Ссылка
	|		ПО Товары.Номенклатура = ТоварыДляЗапроса.Номенклатура
	|			И Товары.Характеристика = ТоварыДляЗапроса.Характеристика
	|			И Товары.Назначение = ТоварыДляЗапроса.Назначение
	|			И Товары.Получатель = ТоварыДляЗапроса.Получатель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Статусы.НомерСтроки КАК НомерСтроки,
	|	Статусы.СтатусУказанияСерий КАК СтатусУказанияСерий
	|ИЗ
	|	Статусы КАК Статусы
	|ГДЕ
	|	Статусы.СтатусУказанияСерий <> Статусы.СтарыйСтатусУказанияСерий
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаЗаполненияСтатусовУказанияСерийМатериалыИРаботы()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Товары.Отправитель,
	|	Товары.Номенклатура,
	|	Товары.Серия,
	|	Товары.СтатусУказанияСерий,
	|	Товары.НомерСтроки
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.СтатусУказанияСерий КАК СтарыйСтатусУказанияСерий,
	|	ВЫБОР
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий ЕСТЬ NULL 
	|			ТОГДА 0
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьСебестоимостьПоСериям
	|			ТОГДА ВЫБОР
	|					КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|						ТОГДА 14
	|					ИНАЧЕ 13
	|				КОНЕЦ
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриПланированииОтгрузки
	|			ТОГДА ВЫБОР
	|					КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|						ТОГДА 10
	|					ИНАЧЕ 9
	|				КОНЕЦ
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриПланированииОтбора
	|			ТОГДА ВЫБОР
	|					КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчетСерийПоFEFO
	|						ТОГДА ВЫБОР
	|								КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|									ТОГДА 6
	|								ИНАЧЕ 5
	|							КОНЕЦ
	|					ИНАЧЕ ВЫБОР
	|							КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|								ТОГДА 8
	|							ИНАЧЕ 7
	|						КОНЕЦ
	|				КОНЕЦ
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПоФактуОтбора
	|				И ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриОтгрузкеНаВнутренниеНужды
	|			ТОГДА ВЫБОР
	|					КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьОстаткиСерий
	|						ТОГДА ВЫБОР
	|								КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|									ТОГДА 4
	|								ИНАЧЕ 3
	|							КОНЕЦ
	|					ИНАЧЕ ВЫБОР
	|							КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|								ТОГДА 2
	|							ИНАЧЕ 1
	|						КОНЕЦ
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтатусУказанияСерий
	|ПОМЕСТИТЬ Статусы
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ПолитикиУчетаСерий
	|		ПО (ПолитикиУчетаСерий.Склад = Товары.Отправитель)
	|			И (ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура).ВидНоменклатуры = ПолитикиУчетаСерий.Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Статусы.НомерСтроки,
	|	Статусы.СтатусУказанияСерий
	|ИЗ
	|	Статусы КАК Статусы
	|ГДЕ
	|	Статусы.СтатусУказанияСерий <> Статусы.СтарыйСтатусУказанияСерий
	|
	|УПОРЯДОЧИТЬ ПО
	|	Статусы.НомерСтроки";

	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

Функция ОписаниеФормыДокументаДляЗаполненияРеквизитовСвязанныхСНаправлениемДеятельности() Экспорт
	
	СтруктураОбъекта = НаправленияДеятельностиСервер.СтруктураОбъекта();
	СтруктураОбъекта.ОформляетсяПоЗаказу = Ложь;
	СтруктураОбъекта.ТабЧасти.Удалить("Товары");
	
	ФильтрХозОперация = Новый Массив();
	ФильтрХозОперация.Добавить("ПОСТУПЛЕНИЕ");
	ФильтрХозОперация.Добавить("ОТГРУЗКА");
	ОписаниеТабЧасти = Новый Структура("ФильтрХозОперация, ОформляетсяПоЗаказу", ФильтрХозОперация, Ложь);
	
	СтруктураОбъекта.ТабЧасти.Вставить("ВыходныеИзделия",		ОписаниеТабЧасти);
	СтруктураОбъекта.ТабЧасти.Вставить("ВыходныеИзделияСерии",	ОписаниеТабЧасти);
	СтруктураОбъекта.ТабЧасти.Вставить("ПобочныеИзделия",		ОписаниеТабЧасти);
	СтруктураОбъекта.ТабЧасти.Вставить("ПобочныеИзделияСерии",	ОписаниеТабЧасти);
	СтруктураОбъекта.ТабЧасти.Вставить("МатериалыИРаботы",		ОписаниеТабЧасти);
	
	Возврат СтруктураОбъекта;
	
КонецФункции

#КонецОбласти

#КонецЕсли