#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Подсистема "Управление доступом".

// Процедура ЗаполнитьНаборыЗначенийДоступа по свойствам объекта заполняет наборы значений доступа
// в таблице с полями:
//    НомерНабора     - Число                                     (необязательно, если набор один),
//    ВидДоступа      - ПланВидовХарактеристикСсылка.ВидыДоступа, (обязательно),
//    ЗначениеДоступа - Неопределено, СправочникСсылка или др.    (обязательно),
//    Чтение          - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Добавление      - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Изменение       - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Удаление        - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//
//  Вызывается из процедуры УправлениеДоступомСлужебный.ЗаписатьНаборыЗначенийДоступа(),
// если объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьНаборыЗначенийДоступа" и
// из таких же процедур объектов, у которых наборы значений доступа зависят от наборов этого
// объекта (в этом случае объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьЗависимыеНаборыЗначенийДоступа").
//
// Параметры:
//  Таблица      - ТабличнаяЧасть,
//                 РегистрСведенийНаборЗаписей.НаборыЗначенийДоступа,
//                 ТаблицаЗначений, возвращаемая УправлениеДоступом.ТаблицаНаборыЗначенийДоступа().
//
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	
	ЗарплатаКадры.ЗаполнитьНаборыПоОрганизацииИФизическимЛицам(ЭтотОбъект, Таблица, "Организация", "Сотрудники.Сотрудник");
	
КонецПроцедуры

// Подсистема "Управление доступом".

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	ДанныеДляПроведения = ПолучитьДанныеДляПроведения();
	
	УчетНДФЛ.СформироватьНалогиВычеты(Движения, Отказ, Организация, КонецМесяца(МесяцПерерасчета), ДанныеДляПроведения.НалогиВычеты);
	УчетНачисленнойЗарплаты.ПодготовитьДанныеНДФЛКРегистрации(ДанныеДляПроведения.РезультатыРасчетаНДФЛ);
	УчетНачисленнойЗарплаты.ЗарегистрироватьНДФЛ(Движения, Отказ, Организация, МесяцПерерасчета, ДанныеДляПроведения.РезультатыРасчетаНДФЛ, ДанныеДляПроведения.МенеджерВременныхТаблиц, Перечисления.ХарактерВыплатыЗарплаты.Зарплата);
	УчетНачисленнойЗарплаты.ЗарегистрироватьКорректировкиВыплаты(Движения, Отказ, Организация, МесяцПерерасчета, ДанныеДляПроведения.РезультатыРасчетаКорректировкиВыплаты, ДанныеДляПроведения.МенеджерВременныхТаблиц, Перечисления.ХарактерВыплатыЗарплаты.Зарплата);
	
	ДанныеДляПроведения = ОтражениеЗарплатыВУчете.НоваяСтруктураРезультатыРасчетаЗарплаты();
	ДанныеДляПроведения.НачисленияУдержания = Движения.НачисленияУдержанияПоСотрудникам.Выгрузить();
	СтрокаСписокТаблиц = "НачисленныйНДФЛ";
	ОтражениеЗарплатыВБухучете.СформироватьДвиженияПоДокументу(Движения, Отказ, Организация, НачалоМесяца(МесяцПерерасчета), ДанныеДляПроведения, СтрокаСписокТаблиц);

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Для Каждого СтрокаСотрудника Из Сотрудники Цикл
		
		СуммаВычетовНаДетейИИмущественных = ПримененныеВычетыНаДетейИИмущественные.Выгрузить(
			Новый Структура("ИдентификаторСтрокиНДФЛ", СтрокаСотрудника.ИдентификаторСтрокиНДФЛ), "РазмерВычета").Итог("РазмерВычета");
		
			Если СтрокаСотрудника.НалогПоСтавке09 * СтрокаСотрудника.НалогПоСтавке09 
				+ СтрокаСотрудника.НалогПоСтавке13 * СтрокаСотрудника.НалогПоСтавке13 
				+ СтрокаСотрудника.НалогПоСтавке35 * СтрокаСотрудника.НалогПоСтавке35
				+ СтрокаСотрудника.ЗачтеноАвансовыхПлатежейПоСтавке09 * СтрокаСотрудника.ЗачтеноАвансовыхПлатежейПоСтавке09
				+ СтрокаСотрудника.ЗачтеноАвансовыхПлатежейПоСтавке13 * СтрокаСотрудника.ЗачтеноАвансовыхПлатежейПоСтавке13
				+ СтрокаСотрудника.ЗачтеноАвансовыхПлатежейПоСтавке35 * СтрокаСотрудника.ЗачтеноАвансовыхПлатежейПоСтавке35
				+ СтрокаСотрудника.ПримененныйВычетЛичный * СтрокаСотрудника.ПримененныйВычетЛичный
				+ СтрокаСотрудника.ПримененныйВычетЛичныйКЗачетуВозврату * СтрокаСотрудника.ПримененныйВычетЛичныйКЗачетуВозврату
				+ СуммаВычетовНаДетейИИмущественных * СуммаВычетовНаДетейИИмущественных = 0 Тогда
			
			НалогКЗачетуВозврату = 0;
			НайденныеСтроки = КорректировкиВыплаты.НайтиСтроки(Новый Структура("ФизическоеЛицо", СтрокаСотрудника.Сотрудник));
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
				НалогКЗачетуВозврату = НалогКЗачетуВозврату + НайденнаяСтрока.КорректировкаВыплаты;
			КонецЦикла;
			
			Если НалогКЗачетуВозврату = 0 Тогда
				СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='В строке №%1 не указаны суммы перерасчета налога.'"), СтрокаСотрудника.НомерСтроки);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке,,,,Отказ);
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьДанныеДляПроведения()
	
	СписокФизическихЛиц = Неопределено;
	Если ДополнительныеСвойства.Свойство("ФизическиеЛица")
		И ДополнительныеСвойства.ФизическиеЛица.Количество() > 0 Тогда
		
		СписокФизическихЛиц = ДополнительныеСвойства.ФизическиеЛица;
		ОтборПоФизическимЛицам = Истина;
		
	Иначе
		ОтборПоФизическимЛицам = Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ЗарплатаКадрыПовтИсп.ГоловнаяОрганизация(Организация));
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПерерасчетНДФЛСотрудники.Ссылка,
	|	ПерерасчетНДФЛСотрудники.НомерСтроки,
	|	ПерерасчетНДФЛСотрудники.ОбособленноеПодразделение КАК Подразделение,
	|	ПерерасчетНДФЛСотрудники.Сотрудник КАК ФизическоеЛицо,
	|	ПерерасчетНДФЛСотрудники.МесяцНалоговогоПериода,
	|	ПерерасчетНДФЛСотрудники.НалогПоСтавке09,
	|	ПерерасчетНДФЛСотрудники.НалогПоСтавке13,
	|	ПерерасчетНДФЛСотрудники.НалогПоСтавке35,
	|	ПерерасчетНДФЛСотрудники.НалогПоСтавке13 КАК Налог,
	|	ПерерасчетНДФЛСотрудники.ПримененныйВычетЛичный,
	|	ПерерасчетНДФЛСотрудники.ПримененныйВычетЛичныйКодВычета,
	|	ПерерасчетНДФЛСотрудники.ПримененныйВычетЛичныйКЗачетуВозврату,
	|	ПерерасчетНДФЛСотрудники.ПримененныйВычетЛичныйКЗачетуВозвратуКодВычета,
	// 4D:ERP для Беларуси, Яна, 30.06.2017 12:22:28 
	// Документ "Отпуск" переработать в соответствии с законодательством РБ, №15304 
	// {
	|	ПерерасчетНДФЛСотрудники.ПримененныйВычетЛичныйВторой,
	|	ПерерасчетНДФЛСотрудники.ПримененныйВычетЛичныйВторойКодВычета,
	|	ПерерасчетНДФЛСотрудники.ПримененныйВычетЛичныйВторойКЗачетуВозврату,
	|	ПерерасчетНДФЛСотрудники.ПримененныйВычетЛичныйВторойКЗачетуВозвратуКодВычета,
	// }
	// 4D
	|	ПерерасчетНДФЛСотрудники.ИдентификаторСтрокиНДФЛ,
	|	ПерерасчетНДФЛСотрудники.ЗачтеноАвансовыхПлатежейПоСтавке09,
	|	ПерерасчетНДФЛСотрудники.ЗачтеноАвансовыхПлатежейПоСтавке13,
	|	ПерерасчетНДФЛСотрудники.ЗачтеноАвансовыхПлатежейПоСтавке13 КАК ЗачтеноАвансовыхПлатежей,
	|	ПерерасчетНДФЛСотрудники.ЗачтеноАвансовыхПлатежейПоСтавке35
	|ПОМЕСТИТЬ ВТСтрокиДокумента
	|ИЗ
	|	Документ.ПерерасчетНДФЛ.Сотрудники КАК ПерерасчетНДФЛСотрудники
	|ГДЕ
	|	ПерерасчетНДФЛСотрудники.Ссылка = &Ссылка
	|	И ПерерасчетНДФЛСотрудники.Сотрудник В(&ФизическиеЛица)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПерерасчетНДФЛКорректировкиВыплаты.Ссылка,
	|	ПерерасчетНДФЛКорректировкиВыплаты.НомерСтроки,
	|	ПерерасчетНДФЛКорректировкиВыплаты.ОбособленноеПодразделение КАК Подразделение,
	|	ПерерасчетНДФЛКорректировкиВыплаты.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ПерерасчетНДФЛКорректировкиВыплаты.КорректировкаВыплаты КАК КорректировкаВыплаты,
	|	ПерерасчетНДФЛКорректировкиВыплаты.ИдентификаторСтроки КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТСтрокиКорректировкиВыплатыДокумента
	|ИЗ
	|	Документ.ПерерасчетНДФЛ.КорректировкиВыплаты КАК ПерерасчетНДФЛКорректировкиВыплаты
	|ГДЕ
	|	ПерерасчетНДФЛКорректировкиВыплаты.Ссылка = &Ссылка
	|	И ПерерасчетНДФЛКорректировкиВыплаты.ФизическоеЛицо В(&ФизическиеЛица)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтрокиДокумента.Подразделение,
	|	СтрокиДокумента.ФизическоеЛицо,
	|	СтрокиДокумента.МесяцНалоговогоПериода,
	|	СтрокиДокумента.НалогПоСтавке09,
	|	СтрокиДокумента.НалогПоСтавке35,
	|	СтрокиДокумента.ЗачтеноАвансовыхПлатежейПоСтавке09,
	|	СтрокиДокумента.ЗачтеноАвансовыхПлатежейПоСтавке35
	|ИЗ
	|	ВТСтрокиДокумента КАК СтрокиДокумента
	|ГДЕ
	|	(СтрокиДокумента.НалогПоСтавке35 <> 0
	|			ИЛИ СтрокиДокумента.НалогПоСтавке09 <> 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Сотрудники.ФизическоеЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ВТФизическиеЛица
	|ИЗ
	|	ВТСтрокиДокумента КАК Сотрудники
	|ГДЕ
	|	Сотрудники.ФизическоеЛицо <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 0
	|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК ФизическоеЛицо,
	|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка) КАК Сотрудник,
	|	0 КАК СуммаДохода,
	|	0 КАК СуммаВычета,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК Подразделение
	|ПОМЕСТИТЬ ВТДанныеДляРасчетаНДФЛ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ДанныеДокумента.Сумма) КАК Сумма,
	|	ДанныеДокумента.Подразделение,
	|	ЕСТЬNULL(ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)) КАК ПодразделениеСотрудника,
	|	ЕСТЬNULL(ТекущиеКадровыеДанныеСотрудников.Сотрудник, ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)) КАК Сотрудник,
	|	ДанныеДокумента.ФизическоеЛицо,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов,
	|	ДанныеДокумента.МесяцНалоговогоПериода
	|ИЗ
	|	(ВЫБРАТЬ
	|		СтрокиДокумента.НалогПоСтавке13 - СтрокиДокумента.ЗачтеноАвансовыхПлатежейПоСтавке13 КАК Сумма,
	|		СтрокиДокумента.Подразделение КАК Подразделение,
	|		СтрокиДокумента.ФизическоеЛицо КАК ФизическоеЛицо,
	|		СтрокиДокумента.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода
	|	ИЗ
	|		ВТСтрокиДокумента КАК СтрокиДокумента
	|	ГДЕ
	|		СтрокиДокумента.НалогПоСтавке13 <> 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СтрокиДокумента.НалогПоСтавке09 - СтрокиДокумента.ЗачтеноАвансовыхПлатежейПоСтавке09,
	|		СтрокиДокумента.Подразделение,
	|		СтрокиДокумента.ФизическоеЛицо,
	|		СтрокиДокумента.МесяцНалоговогоПериода
	|	ИЗ
	|		ВТСтрокиДокумента КАК СтрокиДокумента
	|	ГДЕ
	|		СтрокиДокумента.НалогПоСтавке09 <> 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СтрокиДокумента.НалогПоСтавке35 - СтрокиДокумента.ЗачтеноАвансовыхПлатежейПоСтавке35,
	|		СтрокиДокумента.Подразделение,
	|		СтрокиДокумента.ФизическоеЛицо,
	|		СтрокиДокумента.МесяцНалоговогоПериода
	|	ИЗ
	|		ВТСтрокиДокумента КАК СтрокиДокумента
	|	ГДЕ
	|		СтрокиДокумента.НалогПоСтавке35 <> 0) КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
	|		ПО (ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо = ДанныеДокумента.ФизическоеЛицо)
	|			И (ТекущиеКадровыеДанныеСотрудников.ОсновноеРабочееМестоВОрганизации)
	|			И (ТекущиеКадровыеДанныеСотрудников.ГоловнаяОрганизация = &ГоловнаяОрганизация)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Подразделение,
	|	ДанныеДокумента.ФизическоеЛицо,
	|	ТекущиеКадровыеДанныеСотрудников.Сотрудник,
	|	ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение,
	|	ДанныеДокумента.МесяцНалоговогоПериода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ДанныеДокумента.КорректировкаВыплаты) КАК КорректировкаВыплаты,
	|	ДанныеДокумента.Подразделение,
	|	ЕСТЬNULL(ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)) КАК ПодразделениеСотрудника,
	|	ЕСТЬNULL(ТекущиеКадровыеДанныеСотрудников.Сотрудник, ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка)) КАК Сотрудник,
	|	ДанныеДокумента.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов
	|ИЗ
	|	ВТСтрокиКорректировкиВыплатыДокумента КАК ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
	|		ПО (ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо = ДанныеДокумента.ФизическоеЛицо)
	|			И (ТекущиеКадровыеДанныеСотрудников.ОсновноеРабочееМестоВОрганизации)
	|			И (ТекущиеКадровыеДанныеСотрудников.ГоловнаяОрганизация = &ГоловнаяОрганизация)
	|ГДЕ
	|	ДанныеДокумента.КорректировкаВыплаты <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Подразделение,
	|	ДанныеДокумента.ФизическоеЛицо,
	|	ТекущиеКадровыеДанныеСотрудников.Сотрудник,
	|	ТекущиеКадровыеДанныеСотрудников.ТекущееПодразделение";
	
	Если ОтборПоФизическимЛицам Тогда
		Запрос.УстановитьПараметр("ФизическиеЛица", СписокФизическихЛиц);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И ПерерасчетНДФЛСотрудники.Сотрудник В(&ФизическиеЛица)", "");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И ПерерасчетНДФЛКорректировкиВыплаты.ФизическоеЛицо В(&ФизическиеЛица)", "");
	КонецЕсли;
	
	Результаты = Запрос.ВыполнитьПакет();
	
	ОписаниеТаблиц = Новый Структура("ИмяТаблицыСНалогами,ИмяТаблицыСВычетами", "ВТСтрокиДокумента", "Документ.ПерерасчетНДФЛ.ПримененныеВычетыНаДетейИИмущественные");
	НалогиВычеты = УчетНДФЛ.ДанныеДокументаОНалогеПоОсновнойСтавкеИВычетах(Ссылка, ОписаниеТаблиц, Запрос.МенеджерВременныхТаблиц);
	
	Если Не Результаты[2].Пустой() Тогда
		НалогиВычеты.Колонки.Добавить("НалогПоСтавке09", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(13, 0)));
		НалогиВычеты.Колонки.Добавить("НалогПоСтавке35", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(13, 0)));
		НалогиВычеты.Колонки.Добавить("ЗачтеноАвансовыхПлатежейПоСтавке09", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(13, 0)));
		НалогиВычеты.Колонки.Добавить("ЗачтеноАвансовыхПлатежейПоСтавке35", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(13, 0)));
		Выборка = Результаты[2].Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(НалогиВычеты, Выборка);
		КонецЦикла;
	КонецЕсли;
	
	РезультатыРасчетаНДФЛ = Результаты[5].Выгрузить();
	
	РезультатыРасчетаКорректировкиВыплаты = Результаты[6].Выгрузить();
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплата") Тогда
		РезультатыРасчета = Новый Структура("РезультатыРасчетаНДФЛ,РезультатыРасчетаКорректировкиВыплаты",РезультатыРасчетаНДФЛ,РезультатыРасчетаКорректировкиВыплаты);
		ОтражениеЗарплатыВБухучетеВнутренний.ПерерасчетНДФЛРаспределитьПоСтатьям(Организация, НачалоМесяца(МесяцПерерасчета), Ссылка, РезультатыРасчета);
		РезультатыРасчетаНДФЛ = РезультатыРасчета.РезультатыРасчетаНДФЛ;
		РезультатыРасчетаКорректировкиВыплаты = РезультатыРасчета.РезультатыРасчетаКорректировкиВыплаты;
	КонецЕсли;
	
	ДанныеДляПроведения = Новый Структура;
	ДанныеДляПроведения.Вставить("НалогиВычеты", НалогиВычеты);
	ДанныеДляПроведения.Вставить("РезультатыРасчетаНДФЛ", РезультатыРасчетаНДФЛ);
	ДанныеДляПроведения.Вставить("РезультатыРасчетаКорректировкиВыплаты", РезультатыРасчетаКорректировкиВыплаты);
	ДанныеДляПроведения.Вставить("МенеджерВременныхТаблиц", Запрос.МенеджерВременныхТаблиц);
	
	Возврат ДанныеДляПроведения;
	
КонецФункции

#КонецОбласти

#КонецЕсли
