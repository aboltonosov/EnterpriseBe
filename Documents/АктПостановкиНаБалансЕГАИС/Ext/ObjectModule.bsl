#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(Идентификатор) Тогда
		Идентификатор = Строка(Новый УникальныйИдентификатор());
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	ИнтеграцияЕГАИСПереопределяемый.ЗаписатьСоответствиеНоменклатуры(ЭтотОбъект);
	
	ИнтеграцияЕГАИСПереопределяемый.ПередЗаписью(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ИнтеграцияЕГАИСПереопределяемый.ОбработкаЗаполненияДокумента(ЭтотОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ВидДокумента = Перечисления.ВидыДокументовЕГАИС.АктПостановкиНаБалансВРегистр1 Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	АктПостановкиНаБалансЕГАИСТовары.АлкогольнаяПродукция,
		|	АктПостановкиНаБалансЕГАИСТовары.ИдентификаторСтроки,
		|	АктПостановкиНаБалансЕГАИСТовары.Количество,
		|	АктПостановкиНаБалансЕГАИСТовары.НомерСтроки
		|ПОМЕСТИТЬ АктПостановкиНаБалансЕГАИСТовары
		|ИЗ
		|	&АктПостановкиНаБалансЕГАИСТовары КАК АктПостановкиНаБалансЕГАИСТовары
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АктПостановкиНаБалансЕГАИСАкцизныеМарки.ИдентификаторСтроки
		|ПОМЕСТИТЬ АктПостановкиНаБалансЕГАИСАкцизныеМарки
		|ИЗ
		|	&АктПостановкиНаБалансЕГАИСАкцизныеМарки КАК АктПостановкиНаБалансЕГАИСАкцизныеМарки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АктПостановкиНаБалансЕГАИСТовары.АлкогольнаяПродукция,
		|	АктПостановкиНаБалансЕГАИСТовары.ИдентификаторСтроки,
		|	АктПостановкиНаБалансЕГАИСТовары.Количество,
		|	АктПостановкиНаБалансЕГАИСТовары.НомерСтроки
		|ПОМЕСТИТЬ МаркируемаяПродукцияКоличество
		|ИЗ
		|	АктПостановкиНаБалансЕГАИСТовары КАК АктПостановкиНаБалансЕГАИСТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторАлкогольнойПродукцииЕГАИС КАК КлассификаторАлкогольнойПродукцииЕГАИС
		|		ПО АктПостановкиНаБалансЕГАИСТовары.АлкогольнаяПродукция = КлассификаторАлкогольнойПродукцииЕГАИС.Ссылка
		|ГДЕ
		|	КлассификаторАлкогольнойПродукцииЕГАИС.ВидПродукции.Маркируемый
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АктПостановкиНаБалансЕГАИСАкцизныеМарки.ИдентификаторСтроки,
		|	СУММА(1) КАК Количество
		|ПОМЕСТИТЬ АкцизныеМаркиКоличество
		|ИЗ
		|	АктПостановкиНаБалансЕГАИСАкцизныеМарки КАК АктПостановкиНаБалансЕГАИСАкцизныеМарки
		|
		|СГРУППИРОВАТЬ ПО
		|	АктПостановкиНаБалансЕГАИСАкцизныеМарки.ИдентификаторСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МаркируемаяПродукцияКоличество.АлкогольнаяПродукция,
		|	МаркируемаяПродукцияКоличество.Количество КАК КоличествоМаркируемаяПродукция,
		|	ЕСТЬNULL(АкцизныеМаркиКоличество.Количество, 0) КАК КоличествоАкцизныеМарки,
		|	МаркируемаяПродукцияКоличество.НомерСтроки
		|ИЗ
		|	МаркируемаяПродукцияКоличество КАК МаркируемаяПродукцияКоличество
		|		ЛЕВОЕ СОЕДИНЕНИЕ АкцизныеМаркиКоличество КАК АкцизныеМаркиКоличество
		|		ПО МаркируемаяПродукцияКоличество.ИдентификаторСтроки = АкцизныеМаркиКоличество.ИдентификаторСтроки
		|ГДЕ
		|	МаркируемаяПродукцияКоличество.Количество <> ЕСТЬNULL(АкцизныеМаркиКоличество.Количество, 0)");
		
		Запрос.УстановитьПараметр("АктПостановкиНаБалансЕГАИСТовары", Товары.Выгрузить( , "АлкогольнаяПродукция, ИдентификаторСтроки, Количество, НомерСтроки"));
		Запрос.УстановитьПараметр("АктПостановкиНаБалансЕГАИСАкцизныеМарки", АкцизныеМарки.Выгрузить( , " ИдентификаторСтроки"));
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
		
			ТекстОшибки = НСтр("ru = 'В строке %1 для %2 должно быть указано акцизных марок - %3, а указано - %4.'");
			ТекстОшибки = СтрШаблон(ТекстОшибки,
			                        Выборка.НомерСтроки,
			                        Выборка.АлкогольнаяПродукция,
			                        Выборка.КоличествоМаркируемаяПродукция,
			                        Выборка.КоличествоАкцизныеМарки);
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					ЭтотОбъект,
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Товары", Выборка.НомерСтроки, "Количество"),
					,
					Отказ);
		
		КонецЦикла;
		
	КонецЕсли;
	
	Если ВидДокумента = Перечисления.ВидыДокументовЕГАИС.АктПостановкиНаБалансВРегистр2 Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Товары.КоличествоПоСправке1");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.НомерТТН");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.ДатаТТН");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.ДатаРозлива");
	КонецЕсли;
	
	Если ПричинаПостановкиНаБаланс <> Перечисления.ПричиныПостановкиНаБалансЕГАИС.Пересортица Тогда
		МассивНепроверяемыхРеквизитов.Добавить("АктСписанияЕГАИС");
	КонецЕсли;
	
	ИнтеграцияЕГАИСПереопределяемый.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ИнтеграцияЕГАИС.ПровестиДокумент(ЭтотОбъект, Отказ, РежимПроведения);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	СтатусОбработки = Перечисления.СтатусыОбработкиАктаПостановкиНаБалансЕГАИС.Новый;
	ДокументОснование = Неопределено;
	
	Идентификатор = "";
	ИдентификаторЕГАИС = "";
	ДатаРегистрацииДвижений = '00010101';
	
	Товары.ЗагрузитьКолонку(Новый Массив(Товары.Количество()), "Справка2");
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
