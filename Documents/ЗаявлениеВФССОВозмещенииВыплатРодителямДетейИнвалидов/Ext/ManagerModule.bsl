#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Печать заявления о выплате пособия.
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Обработчик = "ЗарплатаКадрыКлиент.ВыполнитьКомандуПечати";
	КомандаПечати.Идентификатор = "ЗаявлениеВФССДополнительныеДниОтпуска_2012";
	КомандаПечати.Представление = НСтр("ru = 'Заявление о возмещении расходов'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПараметрыВывода.ДоступнаПечатьПоКомплектно = Истина;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЗаявлениеВФССДополнительныеДниОтпуска_2012") Тогда
		ТабличныйДокумент = ТабличныйДокументЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов(МассивОбъектов, ОбъектыПечати);
		Если НЕ ТабличныйДокумент = Неопределено Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,	"ЗаявлениеВФССДополнительныеДниОтпуска_2012", НСтр("ru = 'Заявление о возмещении расходов'"), ТабличныйДокумент);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ТабличныйДокументЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов(МассивСсылок, ОбъектыПечати)
	
	ДокументРезультат = Новый ТабличныйДокумент;
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ДокументРезультат.АвтоМасштаб = Истина;
	ДокументРезультат.ПолеСверху 	= 0;
	ДокументРезультат.ПолеСлева 	= 0;
	ДокументРезультат.ПолеСнизу 	= 0;
	ДокументРезультат.ПолеСправа 	= 0;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	
	СоздатьВТКадровыхДанных(Запрос);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Ссылка,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Номер,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Дата,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Проведен,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Организация,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.НаименованиеТерриториальногоОрганаФСС,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.РегистрационныйНомерФСС,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.ДополнительныйКодФСС,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.КодПодчиненностиФСС,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Руководитель,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.ДолжностьРуководителя,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.АдресОрганизации,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.НаименованиеБанка,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.НомерЛицевогоСчета,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.НомерСчета,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.БИКБанка,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.СтатусДокумента,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.ТелефонСоставителя,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.ЗаявлениеСоставил,
	|	ТЧОплаты.Сотрудник,
	|	ТЧОплаты.Статус,
	|	ТЧОплаты.СреднедневнойЗаработок,
	|	ТЧОплаты.ДокументОснование,
	|	ТЧОплаты.КоличествоСтраниц КАК КоличествоСтраниц,
	|	ТЧОплаты.СуммаПособия,
	|	КадровыеДанныеРуководителей.ФИОПолные КАК ФИОРуководителя,
	|	ВТКадровыеДанныеСотрудников.Фамилия КАК Фамилия,
	|	ВТКадровыеДанныеСотрудников.Имя КАК Имя,
	|	ВТКадровыеДанныеСотрудников.Отчество КАК Отчество,
	|	ВЫБОР
	|		КОГДА (ВЫРАЗИТЬ(Организации.НаименованиеПолное КАК СТРОКА(1000))) = """"
	|			ТОГДА Организации.НаименованиеСокращенное
	|		ИНАЧЕ ВЫРАЗИТЬ(Организации.НаименованиеПолное КАК СТРОКА(1000))
	|	КОНЕЦ КАК ОрганизацияНаименование,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.ИНН,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.КПП
	|ИЗ
	|	Документ.ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов КАК ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеФизическихЛиц КАК КадровыеДанныеРуководителей
	|		ПО ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Руководитель = КадровыеДанныеРуководителей.ФизическоеЛицо
	|			И ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Дата = КадровыеДанныеРуководителей.Период
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Оплаты КАК ТЧОплаты
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудников КАК ВТКадровыеДанныеСотрудников
	|			ПО ТЧОплаты.Сотрудник = ВТКадровыеДанныеСотрудников.Сотрудник
	|				И ТЧОплаты.Ссылка.Дата = ВТКадровыеДанныеСотрудников.Период
	|		ПО ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Ссылка = ТЧОплаты.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Организация = Организации.Ссылка
	|ГДЕ
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Ссылка В(&МассивСсылок)";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Макет = Неопределено;
	ВыведеноСтрок = 0;
	ИтогоПособия = 0;
	ИтогоСреднедневнойЗаработок = 0;
	ВсегоСтраниц = 0;
	
	Пока Выборка.СледующийПоЗначениюПоля("Ссылка") Цикл
		Пока Выборка.Следующий() Цикл
			ВыведеноСтрок = (ВыведеноСтрок + 1) % 4;
			Если ВыведеноСтрок = 1 Тогда
				Если Макет <> Неопределено Тогда
					ЗарплатаКадры.ВывестиСуммуВРубляхКопейкахВЯчейки(ИтогоСреднедневнойЗаработок, Макет, "ИтогоРазмерПособия_", 9);
					ЗарплатаКадры.ВывестиСуммуВРубляхКопейкахВЯчейки(ИтогоПособия, Макет, "ИтогоРазмерПособия2_", 9);
					ЗарплатаКадры.ВывестиДанныеПоБуквенно(Строка(ВсегоСтраниц), Макет, "КоличествоСтраниц_", 2);
					ДокументРезультат.Вывести(Макет);
					ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
				КонецЕсли;
				ИтогоПособия = 0;
				ВсегоСтраниц = 0;
				Макет = МакетЗаявленияОВозмещении2012СЗаполненнымиШапкойИПодвалом(Выборка, УправлениеПечатью.МакетПечатнойФормы("Документ.ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.ЗаявлениеВФССДополнительныеДниОтпуска_2012"));
			КонецЕсли;
			
			Фамилия 	= Выборка.Фамилия;
			Имя 		= Выборка.Имя;
			Отчество 	= Выборка.Отчество;
			
			НомерСтроки = ?(ВыведеноСтрок = 0, 4, ВыведеноСтрок);
			ПрефиксСтроки = "ФИО_" + Формат(НомерСтроки,"ЧЦ=2; ЧВН=") + "_";
			ЗарплатаКадры.ВывестиДанныеПоБуквенно(ВРег(Фамилия), Макет, ПрефиксСтроки, 24);
			ЗарплатаКадры.ВывестиДанныеПоБуквенно(ВРег(Имя), 	Макет, ПрефиксСтроки, 24, 25);
			ЗарплатаКадры.ВывестиДанныеПоБуквенно(ВРег(Отчество),Макет, ПрефиксСтроки, 24, 49);
			
			ЗарплатаКадры.ВывестиСуммуВРубляхКопейкахВЯчейки(Выборка.СреднедневнойЗаработок, Макет, "РазмерПособия" + НомерСтроки + "_", 8);
			Макет.Области["Статус" + НомерСтроки].Текст = Выборка.Статус;
			
			ИтогоСреднедневнойЗаработок = ИтогоСреднедневнойЗаработок + Выборка.СреднедневнойЗаработок;
			ИтогоПособия = ИтогоПособия + Выборка.СуммаПособия;
			ВсегоСтраниц = ВсегоСтраниц + Выборка.КоличествоСтраниц;
			
		КонецЦикла;
		
		ЗарплатаКадры.ВывестиСуммуВРубляхКопейкахВЯчейки(ИтогоСреднедневнойЗаработок, Макет, "ИтогоРазмерПособия_", 9);
		ЗарплатаКадры.ВывестиСуммуВРубляхКопейкахВЯчейки(ИтогоПособия, Макет, "ИтогоРазмерПособия2_", 9);
		ЗарплатаКадры.ВывестиДанныеПоБуквенно(Строка(ВсегоСтраниц), Макет, "КоличествоСтраниц_", 2);
		ДокументРезультат.Вывести(Макет);
	КонецЦикла;
	
	Возврат ДокументРезультат;
	
КонецФункции

Функция МакетЗаявленияОВозмещении2012СЗаполненнымиШапкойИПодвалом(СтруктураДанных, Макет) Экспорт
	
	ДлиныСтрок = Новый Массив();
	ДлиныСтрок.Добавить(27);
	ЗарплатаКадры.ВывестиДанныеПоБуквенно(ЗарплатаКадры.РазбитьСтрокуНаПодСтроки(ВРег(СтруктураДанных.НаименованиеТерриториальногоОрганаФСС), ДлиныСтрок), Макет, "Наименование_ФСС_", 135);
	ЗарплатаКадры.ВывестиДанныеПоБуквенно(ЗарплатаКадры.РазбитьСтрокуНаПодСтроки(ВРег(СтруктураДанных.ОрганизацияНаименование), ДлиныСтрок), Макет, "ФИО_Заявителя_", 135);
	
	ЗарплатаКадры.ВывестиДанныеПоБуквенно(СтруктураДанных.РегистрационныйНомерФСС, Макет, "РегистрационныйНомер_", 10);
	ЗарплатаКадры.ВывестиДанныеПоБуквенно(СтруктураДанных.ДополнительныйКодФСС, Макет, "ДополнительныйКод_", 10);
	ЗарплатаКадры.ВывестиДанныеПоБуквенно(СтруктураДанных.КодПодчиненностиФСС, Макет, "КодПодчиненности_", 5);
	ЗарплатаКадры.ВывестиДанныеПоБуквенно(СтруктураДанных.ИНН, Макет, "ИНН_", 12);
	ЗарплатаКадры.ВывестиДанныеПоБуквенно(СтруктураДанных.КПП, Макет, "КПП_", 9);
	
	Если ЗначениеЗаполнено(СтруктураДанных.АдресОрганизации) Тогда
		СтруктураАдреса = ЗарплатаКадры.СтруктураАдресаИзXML(СтруктураДанных.АдресОрганизации, ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ЮрАдресОрганизации"));	
		Если СтруктураАдреса <> Неопределено 
			И СтруктураАдреса.Свойство("Индекс") Тогда
			ЗарплатаКадры.ВывестиДанныеПоБуквенно(СтруктураАдреса.Индекс, Макет, "АдресРегистрации_", 6);
			ЗарплатаКадры.ВывестиДанныеПоБуквенно(ВРег(СтруктураАдреса.Регион), Макет, "АдресРегистрации_", 17, 7);
			ЗарплатаКадры.ВывестиДанныеПоБуквенно(ВРег(СтруктураАдреса.Район), Макет, "АдресРегистрации_", 17, 24);
			ЗарплатаКадры.ВывестиДанныеПоБуквенно(ВРег(?(ЗначениеЗаполнено(СтруктураАдреса.Город), СтруктураАдреса.Город + " " + СтруктураАдреса.НаселенныйПункт, СтруктураАдреса.НаселенныйПункт)), Макет, "АдресРегистрации_", 34, 41);
			ЗарплатаКадры.ВывестиДанныеПоБуквенно(ВРег(СтруктураАдреса.Улица), Макет, "АдресРегистрации_", 37, 75);
			ЗарплатаКадры.ВывестиДанныеПоБуквенно(ВРег(СтруктураАдреса.Дом), Макет, "АдресРегистрации_", 8, 112);
			Если ВРег(СтруктураАдреса.ТипКорпуса) = "СТРОЕНИЕ" Тогда
				ЗарплатаКадры.ВывестиДанныеПоБуквенно(ВРег(СтруктураАдреса.Корпус), Макет, "АдресРегистрации_", 3, 123);
			Иначе
				ЗарплатаКадры.ВывестиДанныеПоБуквенно(ВРег(СтруктураАдреса.Корпус), Макет, "АдресРегистрации_", 3, 120);
			КонецЕсли;;
			ЗарплатаКадры.ВывестиДанныеПоБуквенно(ВРег(СтруктураАдреса.Квартира), Макет, "АдресРегистрации_", 6, 126);
		КонецЕсли;
	КонецЕсли;
	
	ДлиныСтрок = Новый Массив();
	ДлиныСтрок.Добавить(39);
	ЗарплатаКадры.ВывестиДанныеПоБуквенно(ЗарплатаКадры.РазбитьСтрокуНаПодСтроки(ВРег(СтруктураДанных.НаименованиеБанка), ДлиныСтрок), Макет, "НаименованиеБанка_", 78);
	
	ЗарплатаКадры.ВывестиДанныеПоБуквенно(СтруктураДанных.НомерСчета, Макет, "НомерСчета_", 20);
	ЗарплатаКадры.ВывестиДанныеПоБуквенно(СтруктураДанных.БИКБанка, Макет, "БИК_", 9);
	ЗарплатаКадры.ВывестиДанныеПоБуквенно(СтруктураДанных.НомерЛицевогоСчета, Макет, "ЛицСчет_", 12);
	
	Если ЗначениеЗаполнено(СтруктураДанных.ТелефонСоставителя) Тогда
		СтруктураТелефона = УправлениеКонтактнойИнформацией.ПредыдущаяСтруктураКонтактнойИнформацииXML(СтруктураДанных.ТелефонСоставителя, ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ТелефонОрганизации"));	
		Если СтруктураТелефона <> Неопределено Тогда
			Телефон = СтрЗаменить(СтруктураТелефона.КодГорода, "-", "") + СтрЗаменить(СтруктураТелефона.НомерТелефона, "-", "");
			Телефон = СтрЗаменить(Телефон, " ", ""); 
			ЗарплатаКадры.ВывестиДанныеПоБуквенно(Телефон, Макет, "ТелефонСоставителя_", 10);
		КонецЕсли;
	КонецЕсли;
	Макет.Параметры.ДолжностьРуководителя = "" + СтруктураДанных.ДолжностьРуководителя;
	Макет.Параметры.ФИОРуководителя = "" + СтруктураДанных.ФИОРуководителя;
	
	Возврат Макет;
	
КонецФункции 

Процедура СоздатьВТКадровыхДанных(Запрос)
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Оплаты.Сотрудник,
	|	Оплаты.Ссылка.Дата КАК Период
	|ПОМЕСТИТЬ ВТСотрудникиОбщийСписок
	|ИЗ
	|	Документ.ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Оплаты КАК Оплаты
	|ГДЕ
	|	Оплаты.Ссылка В(&МассивСсылок)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Руководитель КАК ФизическоеЛицо,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Дата КАК Период
	|ПОМЕСТИТЬ ВТФизическиеЛицаОбщийСписок
	|ИЗ
	|	Документ.ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов КАК ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов
	|ГДЕ
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Ссылка В(&МассивСсылок)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.ЗаявлениеСоставил,
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Дата
	|ИЗ
	|	Документ.ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов КАК ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов
	|ГДЕ
	|	ЗаявлениеВФССОВозмещенииВыплатРодителямДетейИнвалидов.Ссылка В(&МассивСсылок)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТСотрудникиОбщийСписок.Сотрудник,
	|	ВТСотрудникиОбщийСписок.Период
	|ПОМЕСТИТЬ ВТСотрудники
	|ИЗ
	|	ВТСотрудникиОбщийСписок КАК ВТСотрудникиОбщийСписок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТФизическиеЛицаОбщийСписок.ФизическоеЛицо,
	|	ВТФизическиеЛицаОбщийСписок.Период
	|ПОМЕСТИТЬ ВТФизическиеЛица
	|ИЗ
	|	ВТФизическиеЛицаОбщийСписок КАК ВТФизическиеЛицаОбщийСписок";
	
	Запрос.Выполнить();
	
	Описатель = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(Запрос.МенеджерВременныхТаблиц, "ВТСотрудники");
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(Описатель, Истина, "ФИОПолные,Фамилия,Имя,Отчество");
	
	Описатель = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеФизическихЛиц(Запрос.МенеджерВременныхТаблиц, "ВТФизическиеЛица");
	КадровыйУчет.СоздатьВТКадровыеДанныеФизическихЛиц(Описатель, Истина, "ФИОПолные,ФамилияИО");
	
КонецПроцедуры

#КонецОбласти

#Область МеханизмФиксацииИзменений

Функция ПараметрыФиксацииВторичныхДанных() Экспорт
	Возврат ФиксацияВторичныхДанныхВДокументах.ПараметрыФиксацииВторичныхДанных(ФиксацияОписаниеФиксацииРеквизитов(), , ФиксацияОписанияТЧ());
КонецФункции

Функция ФиксацияОписаниеФиксацииРеквизитов()
	
	Возврат ПрямыеВыплатыПособийСоциальногоСтрахования.ОписаниеФиксацииРеквизитовЗаявленияВФССОВозмещенииВыплатРодителямДетейИнвалидов();
	
КонецФункции

Функция ФиксацияОписанияТЧ()
	
	МассивПолей = Новый Массив;
	МассивПолей.Добавить("ДокументОснование");
	
	СтруктураКлючевыхПолей = Новый Структура("Оплаты", МассивПолей);
	
	Возврат СтруктураКлючевыхПолей;
	
КонецФункции

#КонецОбласти

#КонецЕсли
