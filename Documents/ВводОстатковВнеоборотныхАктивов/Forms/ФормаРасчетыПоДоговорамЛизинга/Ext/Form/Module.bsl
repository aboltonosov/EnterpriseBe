
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОФормление();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриСозданииЧтенииНаСервере();
	КонецЕсли;
	
	УправлениеПечатью.ПриСозданииНаСервере(ЭтаФорма, Элементы.ПодменюПечать);
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	МенюОтчеты.ПриСозданииНаСервере(ЭтотОбъект, Элементы.ПодменюОтчеты);
	ВводНаОсновании.ПриСозданииНаСервере(ЭтотОбъект, Элементы.ПодменюСоздатьНаОсновании);
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриСозданииЧтенииНаСервере();
	
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Заголовок = Документы.ВводОстатковВнеоборотныхАктивов.ПредставлениеДокумента(Объект);
	
	ЗаполнитьСлужебныеРеквизитыТаблицы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура РасчетыПоДоговорамЛизингаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Организация", Объект.Организация);
	
	ПараметрыФормыВыбора = Новый Структура;
	ПараметрыФормыВыбора.Вставить("Отбор", СтруктураОтбора);
	
	ОткрытьФорму(
		"Справочник.ДоговорыЛизинга.ФормаВыбора",
		ПараметрыФормыВыбора,
		ЭтаФорма,
		ЭтаФорма,,,
		Новый ОписаниеОповещения(
			"ОбработкаВыбораДоговора",
			ЭтаФорма),
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетыПоДоговорамЛизингаКонтрагентПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.РасчетыПоДоговорамЛизинга.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.Контрагент) Тогда
		ЗаполнитьСлужебныеРеквизитыТаблицы(Элементы.РасчетыПоДоговорамЛизинга.ТекущаяСтрока);
	Иначе
		ТекущиеДанные.ДоговорВВалютеРегл = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетыПоДоговорамЛизингаДоговорПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.РасчетыПоДоговорамЛизинга.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.Договор) Тогда
		ЗаполнитьСлужебныеРеквизитыТаблицы(Элементы.РасчетыПоДоговорамЛизинга.ТекущаяСтрока);
	Иначе
		ТекущиеДанные.ДоговорВВалютеРегл = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетыПоДоговорамЛизингаТипЗадолженностиПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.РасчетыПоДоговорамЛизинга.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.ТипЗадолженности) Тогда
		ЗаполнитьСлужебныеРеквизитыТаблицы(Элементы.РасчетыПоДоговорамЛизинга.ТекущаяСтрока);
	Иначе
		ТекущиеДанные.ДоговорВВалютеРегл = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораДоговора(Результат, ДополнительныеПараметры) Экспорт
	
	ОбработкаВыбораДоговораНаСервере(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования,
		ЭтотОбъект,
		"Объект.Комментарий");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#Область СтандартныеПодсистемы

&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
	
	ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтаФорма, ИмяЭлемента, РезультатВыполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
	
	Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтаФорма, Команда.Имя) Тогда
		РезультатВыполнения = Неопределено;
		ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтаФорма, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуСоздатьНаОсновании(Команда)
	
	ВводНаОснованииКлиент.ВыполнитьПодключаемуюКомандуСоздатьНаОсновании(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуОтчет(Команда)
	
	МенюОтчетыКлиент.ВыполнитьПодключаемуюКомандуОтчет(Команда, ЭтаФорма, Объект);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриСозданииЧтенииНаСервере()
	
	Заголовок = Документы.ВводОстатковВнеоборотныхАктивов.ПредставлениеДокумента(Объект);
	
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	ЗаполнитьСлужебныеРеквизитыТаблицы();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОФормление()
	
	УсловноеОформление.Элементы.Очистить();
	
	#Область СуммаРегл
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.РасчетыПоДоговорамЛизингаСуммаРегл.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.РасчетыПоДоговорамЛизинга.ДоговорВВалютеРегл");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='<не требуется>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	
	#КонецОбласти
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораДоговораНаСервере(ВыбранноеЗначение)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Договор", ВыбранноеЗначение);
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", ВалютаРегламентированногоУчета);
	#Область ТекстЗапроса
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.Контрагент КАК Контрагент,
	|	Т.Ссылка КАК Договор,
	|	Т.СчетУчетаУслугиПоЛизингу КАК СчетУчета,
	|	ЛОЖЬ КАК СальдоДебетовое,
	|	ВЫБОР
	|		КОГДА Т.ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ДоговорВВалютеРегл,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийПоЛизингу.УслугаПоЛизингу) КАК ТипЗадолженности
	|ИЗ
	|	Справочник.ДоговорыЛизинга КАК Т
	|ГДЕ
	|	Т.Ссылка = &Договор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Т.Контрагент,
	|	Т.Ссылка,
	|	Т.СчетУчетаВыкупПредметаЛизинга,
	|	ЛОЖЬ,
	|	ВЫБОР
	|		КОГДА Т.ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийПоЛизингу.ВыкупПредметаЛизинга)
	|ИЗ
	|	Справочник.ДоговорыЛизинга КАК Т
	|ГДЕ
	|	Т.Ссылка = &Договор
	|	И Т.ЕстьВыкупПредметаЛизинга
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Т.Контрагент,
	|	Т.Ссылка,
	|	Т.СчетУчетаОбеспечительныйПлатеж,
	|	ИСТИНА,
	|	ВЫБОР
	|		КОГДА Т.ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийПоЛизингу.ЗачетОбеспечительногоПлатежа)
	|ИЗ
	|	Справочник.ДоговорыЛизинга КАК Т
	|ГДЕ
	|	Т.Ссылка = &Договор
	|	И Т.ЕстьОбеспечительныйПлатеж
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Т.Контрагент,
	|	Т.Ссылка,
	|	Т.СчетУчетаАрендныеОбязательства,
	|	ЛОЖЬ,
	|	ВЫБОР
	|		КОГДА Т.ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийПоЛизингу.АрендныеОбязательства)
	|ИЗ
	|	Справочник.ДоговорыЛизинга КАК Т
	|ГДЕ
	|	Т.Ссылка = &Договор
	|	И Т.ВариантУчетаИмущества = ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаИмуществаПриЛизинге.НаБалансе)";
	#КонецОбласти
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ЗаполнитьЗначенияСвойств(Объект.РасчетыПоДоговорамЛизинга.Добавить(), Выборка);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизитыТаблицы(ИдентификаторСтроки=Неопределено)
	
	МассивВыделенныхСтрок = Неопределено;
	Если ЗначениеЗаполнено(ИдентификаторСтроки) Тогда
		МассивВыделенныхСтрок = Новый Массив;
		МассивВыделенныхСтрок.Добавить(Объект.РасчетыПоДоговорамЛизинга.НайтиПоИдентификатору(ИдентификаторСтроки));
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Таблица", Объект.РасчетыПоДоговорамЛизинга.Выгрузить(МассивВыделенныхСтрок, "НомерСтроки, Договор"));
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", ВалютаРегламентированногоУчета);
	#Область ТекстЗапроса
	Запрос.Текст =
	"ВЫБРАТЬ
	|	(ВЫРАЗИТЬ(Т.НомерСтроки КАК ЧИСЛО)) - 1 КАК ИндексСтроки,
	|	ВЫРАЗИТЬ(Т.Договор КАК Справочник.ДоговорыЛизинга) КАК Договор
	|ПОМЕСТИТЬ втТаблицаДокумента
	|ИЗ
	|	&Таблица КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.ИндексСтроки КАК ИндексСтроки,
	|	ВЫБОР
	|		КОГДА Т.Договор.ВалютаВзаиморасчетов = &ВалютаРегламентированногоУчета
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ДоговорВВалютеРегл
	|ИЗ
	|	втТаблицаДокумента КАК Т";
	#КонецОбласти
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ЗаполнитьЗначенияСвойств(Объект.РасчетыПоДоговорамЛизинга[Выборка.ИндексСтроки], Выборка);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

