#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Формирование списка доступных типов операций ввода начальных остатков.
//
// Параметры:
// 		СписокТиповОпераций - СписокЗначений - Возврат, Список значений операций документа
//
Процедура ПолучитьСписокТиповОпераций(СписокТиповОпераций) Экспорт
	
	СписокТиповОпераций.Добавить(Перечисления.ТипыОперацийВводаОстатков.ОстаткиОС);
	СписокТиповОпераций.Добавить(Перечисления.ТипыОперацийВводаОстатков.ОстаткиНМА);
	СписокТиповОпераций.Добавить(Перечисления.ТипыОперацийВводаОстатков.ОстаткиАрендованныхОС);
	
	СписокТиповОпераций.Добавить(Перечисления.ТипыОперацийВводаОстатков.ОстаткиВзаиморасчетовПоДоговорамЛизинга);
	СписокТиповОпераций.Добавить(Перечисления.ТипыОперацийВводаОстатков.ОстаткиПредметовЛизингаЗаБалансом);
	СписокТиповОпераций.Добавить(Перечисления.ТипыОперацийВводаОстатков.ОстаткиПредметовЛизингаНаБалансе);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

#Область ПрограммныйИнтерфейс

#Область Представление

// Возвращает структуру параметров процедуры "ПредставлениеДокумента"
//
Функция ПредставлениеДокументаСтруктураПараметров() Экспорт
	
	Возврат Новый Структура("Ссылка, Номер, Дата, ТипОперации");
	
КонецФункции

// Возвращает представлние документа
//
// Параметры:
// 		Объект - ДанныеФормыСтруктура, Структура - Структура описания объекта, по которому требуется получить представление,
// 			необходимые реквизиты описаны в функции "ПредставлениеДокументаСтруктураПараметров"
//
// Возвращаемое значение:
// 		Строка - Представление документа
//
Функция ПредставлениеДокумента(Объект) Экспорт
	
	РеквизитыОбъекта = ПредставлениеДокументаСтруктураПараметров();
	ЗаполнитьЗначенияСвойств(РеквизитыОбъекта, Объект);
	
	Если Не ЗначениеЗаполнено(РеквизитыОбъекта.ТипОперации) Тогда
		Возврат "";
	КонецЕсли;
	
	ПредставлениеНомерДата = НСтр("ru='(создание)'");
	Если ЗначениеЗаполнено(РеквизитыОбъекта.Ссылка) Тогда
		ПредставлениеНомерДата = СтрШаблон(НСтр("ru='%1 от %2'"), РеквизитыОбъекта.Номер, РеквизитыОбъекта.Дата);
	КонецЕсли;
	
	Представление = НСтр("ru='Ввод начальных остатков внеоборотных активов %1'");
	Если РеквизитыОбъекта.ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыОперацийВводаОстатков.ОстаткиОС") Тогда
		Представление = НСтр("ru='Ввод начальных остатков основных средств %1'");
	ИначеЕсли РеквизитыОбъекта.ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыОперацийВводаОстатков.ОстаткиПереданныхВАрендуОС") Тогда
		Представление = НСтр("ru='Ввод начальных остатков переданных в аренду основных средств %1'");
	ИначеЕсли РеквизитыОбъекта.ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыОперацийВводаОстатков.ОстаткиАрендованныхОС") Тогда
		Представление = НСтр("ru='Ввод начальных остатков полученных в аренду основных средств %1'");
	ИначеЕсли РеквизитыОбъекта.ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыОперацийВводаОстатков.ОстаткиНМА") Тогда
		Представление = НСтр("ru='Ввод начальных остатков нематериальных активов и расходов на НИОКР %1'");
	ИначеЕсли РеквизитыОбъекта.ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыОперацийВводаОстатков.ОстаткиВзаиморасчетовПоДоговорамЛизинга") Тогда
		Представление = НСтр("ru='Ввод начальных остатков взаиморасчетов по договорам лизинга %1'");
	ИначеЕсли РеквизитыОбъекта.ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыОперацийВводаОстатков.ОстаткиПредметовЛизингаНаБалансе") Тогда
		Представление = НСтр("ru='Ввод начальных остатков предметов лизинга на балансе %1'");
	ИначеЕсли РеквизитыОбъекта.ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыОперацийВводаОстатков.ОстаткиПереданныхВАрендуПредметовЛизингаНаБалансе") Тогда
		Представление = НСтр("ru='Ввод начальных остатков переданных в аренду предметов лизинга на балансе %1'");
	ИначеЕсли РеквизитыОбъекта.ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыОперацийВводаОстатков.ОстаткиПредметовЛизингаЗаБалансом") Тогда
		Представление = НСтр("ru='Ввод начальных остатков предметов лизинга за балансом %1'");
	КонецЕсли;
	
	Возврат СтрШаблон(Представление, ПредставлениеНомерДата);
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	ВыбраннаяОперация = Неопределено;
	
	Если Параметры.Свойство("Ключ") И ЗначениеЗаполнено(Параметры.Ключ) Тогда
		ВыбраннаяОперация = Параметры.Ключ.ТипОперации;
	ИначеЕсли Параметры.Свойство("ЗначенияЗаполнения") Тогда
		Параметры.ЗначенияЗаполнения.Свойство("ТипОперации", ВыбраннаяОперация);
	ИначеЕсли Параметры.Свойство("ЗначениеКопирования") Тогда
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	ВводОстатковВнеоборотныхАктивов.ТипОперации КАК ТипОперации
			|ИЗ
			|	Документ.ВводОстатковВнеоборотныхАктивов КАК ВводОстатковВнеоборотныхАктивов
			|ГДЕ
			|	ВводОстатковВнеоборотныхАктивов.Ссылка = &ЗначениеКопирования");
		Запрос.УстановитьПараметр("ЗначениеКопирования", Параметры.ЗначениеКопирования);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ВыбраннаяОперация = Выборка.ТипОперации;
		КонецЕсли;
		
	ИначеЕсли Параметры.Свойство("ОтборПоТипамОпераций") И Параметры.ОтборПоТипамОпераций.Количество() = 1 Тогда
		ВыбраннаяОперация = Параметры.ОтборПоТипамОпераций[0].Значение;
		ЗначенияЗаполнения = Новый Структура;
		ЗначенияЗаполнения.Вставить("ТипОперации", ВыбраннаяОперация);
		Параметры.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		Если Параметры.Свойство("Организация") Тогда
			ЗначенияЗаполнения.Вставить("Организация", Параметры.Организация);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыбраннаяОперация) Тогда
		Если ВыбраннаяОперация = Перечисления.ТипыОперацийВводаОстатков.ОстаткиОС
			Или ВыбраннаяОперация = Перечисления.ТипыОперацийВводаОстатков.ОстаткиПереданныхВАрендуОС
			Или ВыбраннаяОперация = Перечисления.ТипыОперацийВводаОстатков.ОстаткиПереданныхВАрендуПредметовЛизингаНаБалансе
			Или ВыбраннаяОперация = Перечисления.ТипыОперацийВводаОстатков.ОстаткиПредметовЛизингаНаБалансе Тогда
			СтандартнаяОбработка = Ложь;
			ВыбраннаяФорма = "ФормаОсновныеСредства";
		ИначеЕсли ВыбраннаяОперация = Перечисления.ТипыОперацийВводаОстатков.ОстаткиНМА Тогда
			СтандартнаяОбработка = Ложь;
			ВыбраннаяФорма = "ФормаНематериальныеАктивы";
		ИначеЕсли ВыбраннаяОперация = Перечисления.ТипыОперацийВводаОстатков.ОстаткиАрендованныхОС
			Или ВыбраннаяОперация = Перечисления.ТипыОперацийВводаОстатков.ОстаткиПредметовЛизингаЗаБалансом Тогда
			СтандартнаяОбработка = Ложь;
			ВыбраннаяФорма = "ФормаАрендованныеОсновныеСредства";
		ИначеЕсли ВыбраннаяОперация = Перечисления.ТипыОперацийВводаОстатков.ОстаткиВзаиморасчетовПоДоговорамЛизинга Тогда
			СтандартнаяОбработка = Ложь;
			ВыбраннаяФорма = "ФормаРасчетыПоДоговорамЛизинга";
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Поля.Добавить("Ссылка");
	Поля.Добавить("Номер");
	Поля.Добавить("Дата");
	Поля.Добавить("ТипОперации");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	Представление = ПредставлениеДокумента(Данные);
	Если Не ПустаяСтрока(Представление) Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	
	Возврат ИсточникиДанных;
	
КонецФункции

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	ГрафикиАмортизацииОСБухгалтерскийУчет(ТекстыЗапроса, Регистры);
	МестонахождениеОСБухгалтерскийУчет(ТекстыЗапроса, Регистры);
	НачислениеАмортизацииОСБухгалтерскийУчет(ТекстыЗапроса, Регистры);
	НачислениеАмортизацииОСНалоговыйУчет(ТекстыЗапроса, Регистры);
	НачислениеАмортизацииОССпециальныйКоэффициентНалоговыйУчет(ТекстыЗапроса, Регистры);
	НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчет(ТекстыЗапроса, Регистры);
	ПараметрыАмортизацииОСБухгалтерскийУчет(ТекстыЗапроса, Регистры);
	ПараметрыАмортизацииОСНалоговыйУчет(ТекстыЗапроса, Регистры);
	ПервоначальныеСведенияОСБухгалтерскийУчет(ТекстыЗапроса, Регистры);
	ПервоначальныеСведенияНМАБухгалтерскийУчет(ТекстыЗапроса, Регистры);
	ПервоначальныеСведенияОСНалоговыйУчет(ТекстыЗапроса, Регистры);
	ПервоначальныеСведенияНМАНалоговыйУчет(ТекстыЗапроса, Регистры);
	СобытияОСОрганизаций(ТекстыЗапроса, Регистры);
	СостоянияОСОрганизаций(ТекстыЗапроса, Регистры);
	СостоянияНМАОрганизаций(ТекстыЗапроса, Регистры);
	СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчет(ТекстыЗапроса, Регистры);
	СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчет(ТекстыЗапроса, Регистры);
	СчетаБухгалтерскогоУчетаОС(ТекстыЗапроса, Регистры);
	СчетаБухгалтерскогоУчетаНМА(ТекстыЗапроса, Регистры);
	СпособыОтраженияРасходовПоИмущественнымНалогам(ТекстыЗапроса, Регистры);
	ПараметрыЦелевогоФинансированияНМА(ТекстыЗапроса, Регистры);
	ПараметрыЦелевогоФинансированияОС(ТекстыЗапроса, Регистры);
	ОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры);
	
	ПроведениеСерверУТ.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Ложь, Ложь, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.ТипОперации КАК ТипОперации,
	|	ДанныеДокумента.ТипОперации В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиОС),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПереданныхВАрендуОС),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПредметовЛизингаНаБалансе),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПереданныхВАрендуПредметовЛизингаНаБалансе)
	|	) КАК ЭтоОстаткиБалансовыхОС,
	|	ДанныеДокумента.ТипОперации В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПереданныхВАрендуОС),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПереданныхВАрендуПредметовЛизингаНаБалансе)
	|	) КАК ЭтоОстаткиПереданныхВАрендуБалансовыхОС,
	|	ДанныеДокумента.ТипОперации В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиАрендованныхОС),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПредметовЛизингаЗаБалансом)
	|	) КАК ЭтоОстаткиЗабалансовыхОС,
	|	ДанныеДокумента.ТипОперации В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиНМА)
	|	) КАК ЭтоОстаткиНМА
	|ИЗ
	|	Документ.ВводОстатковВнеоборотныхАктивов КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
КонецПроцедуры

Процедура ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса)
	
	ИмяТаблицы = "ТаблицаОС";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица ТаблицаОС
	|"+
	"ВЫБРАТЬ
	|	ТаблицаОС.Ссылка,
	|	ТаблицаОС.НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство,
	|	ТаблицаОС.ОсновноеСредство.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаОС.ИнвентарныйНомер,
	|	ТаблицаОС.ПорядокУчетаБУ,
	|	ТаблицаОС.ПорядокУчетаНУ,
	|	ТаблицаОС.СчетУчета,
	|	ТаблицаОС.СчетАмортизации,
	|	ТаблицаОС.МОЛ,
	|	ТаблицаОС.АдресМестонахождения,
	|	ТаблицаОС.ГруппаОС,
	|	ТаблицаОС.АмортизационнаяГруппа,
	|	ТаблицаОС.КодПоОКОФ,
	|	ТаблицаОС.ШифрПоЕНАОФ,
	|	ТаблицаОС.СпособПоступления,
	|	ТаблицаОС.НачислятьАмортизациюБУ,
	|	ВЫБОР
	|		КОГДА ТаблицаОС.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПоЕНАОФ)
	|		ИНАЧЕ ТаблицаОС.МетодНачисленияАмортизацииБУ
	|	КОНЕЦ КАК МетодНачисленияАмортизацииБУ,
	|	ТаблицаОС.СрокИспользованияБУ,
	|	ТаблицаОС.КоэффициентАмортизацииБУ,
	|	ТаблицаОС.КоэффициентУскоренияБУ,
	|	ТаблицаОС.ГрафикАмортизацииБУ,
	|	ТаблицаОС.ПоказательНаработки,
	|	ТаблицаОС.ОбъемНаработкиБУ,
	|	ТаблицаОС.НачислятьАмортизациюНУ,
	|	ТаблицаОС.СрокИспользованияНУ,
	|	ТаблицаОС.СрокИспользованияНУДо2009,
	|	ТаблицаОС.СпециальныйКоэффициентНУ,
	|	ТаблицаОС.СтатьяРасходовАмортизации,
	|	ТаблицаОС.АналитикаРасходовАмортизации,
	|	ТаблицаОС.ПервоначальнаяСтоимостьБУ,
	|	ТаблицаОС.ПервоначальнаяСтоимостьНУ,
	|	ТаблицаОС.ПервоначальнаяСтоимостьПР,
	|	ТаблицаОС.ТекущаяСтоимостьБУ + ВЫБОР
	|		КОГДА ТаблицаОС.ПрименениеЦелевогоФинансирования
	|			ТОГДА ТаблицаОС.ТекущаяСтоимостьБУЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ТекущаяСтоимостьБУ,
	|	ТаблицаОС.ТекущаяСтоимостьНУ + ВЫБОР
	|		КОГДА ТаблицаОС.ПрименениеЦелевогоФинансирования
	|			ТОГДА ТаблицаОС.ТекущаяСтоимостьНУЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ТекущаяСтоимостьНУ,
	|	ТаблицаОС.ТекущаяСтоимостьПР + ВЫБОР
	|		КОГДА ТаблицаОС.ПрименениеЦелевогоФинансирования
	|			ТОГДА ТаблицаОС.ТекущаяСтоимостьПРЦФ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ТекущаяСтоимостьПР,
	|	ТаблицаОС.НакопленнаяАмортизацияБУ,
	|	ТаблицаОС.НакопленнаяАмортизацияНУ,
	|	ТаблицаОС.НакопленнаяАмортизацияПР,
	|	ТаблицаОС.НакопленнаяАмортизацияНУДо2009,
	|	ТаблицаОС.НазваниеДокументаПринятияКУчету,
	|	ТаблицаОС.НомерДокументаПринятияКУчету,
	|	ВЫБОР
	|		КОГДА ТаблицаОС.ДатаПринятияКУчету = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА &Период
	|		ИНАЧЕ ТаблицаОС.ДатаПринятияКУчету
	|	КОНЕЦ КАК ДатаПринятияКУчету,
	|	ТаблицаОС.СобытиеПринятияКУчету,
	|	ТаблицаОС.НазваниеДокументаМодернизации,
	|	ТаблицаОС.НомерДокументаМодернизации,
	|	ТаблицаОС.ДатаМодернизации,
	|	ТаблицаОС.СобытиеМодернизации,
	|	ТаблицаОС.СтатьяРасходовНалог,
	|	ТаблицаОС.АналитикаРасходовНалог,
	|	ТаблицаОС.ПередаватьРасходыВДругуюОрганизацию,
	|	ТаблицаОС.ОрганизацияПолучательРасходов,
	|	ТаблицаОС.СчетПередачиРасходов,
	|	ТаблицаОС.ЕстьСобытияМодернизации,
	|	ТаблицаОС.СтоимостьДляВычисленияАмортизации,
	|	ТаблицаОС.ДоговорЛизинга,
	|	ТаблицаОС.ПрименениеЦелевогоФинансирования,
	|	ТаблицаОС.СчетУчетаЦФ,
	|	ТаблицаОС.СчетАмортизацииЦФ,
	|	ТаблицаОС.СтатьяДоходов,
	|	ТаблицаОС.АналитикаДоходов,
	|	ТаблицаОС.ВключитьАмортизационнуюПремиюВСоставРасходов,
	|	ТаблицаОС.РезервПереоценкиСтоимости,
	|	ТаблицаОС.РезервПереоценкиАмортизации,
	|	ТаблицаОС.РасчетыМеждуОрганизациямиАрендатор,
	|	ТаблицаОС.СчетУчетаАрендатора,
	|	ТаблицаОС.ПодразделениеАрендатора,
	|	ТаблицаОС.Контрагент
	|ПОМЕСТИТЬ ТаблицаОС
	|ИЗ
	|	Документ.ВводОстатковВнеоборотныхАктивов.ОС КАК ТаблицаОС
	|ГДЕ
	|	&ТипОперации В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиАрендованныхОС),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиОС),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПереданныхВАрендуОС),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПредметовЛизингаНаБалансе),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПредметовЛизингаЗаБалансом),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПереданныхВАрендуПредметовЛизингаНаБалансе)
	|	)
	|	И ТаблицаОС.Ссылка = &Ссылка"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ВременнаяТаблицаНематериальныхАктивов(ТекстыЗапроса)
	
	ИмяТаблицы = "ТаблицаНМА";
	
	Если ПроведениеСерверУТ.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица ТаблицаНМА
	|"+
	"ВЫБРАТЬ
	|	ТаблицаНМА.Ссылка,
	|	ТаблицаНМА.НомерСтроки,
	|	ТаблицаНМА.АмортизацияДо2009,
	|	ТаблицаНМА.ВидОбъектаУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаНМА.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоНМА,
	|	ВЫБОР
	|		КОГДА ТаблицаНМА.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.РасходыНаНИОКР)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоНИОКР,
	|	ВЫБОР
	|		КОГДА ТаблицаНМА.ДатаПринятияКУчетуРегл = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА &Период
	|		ИНАЧЕ ТаблицаНМА.ДатаПринятияКУчетуРегл
	|	КОНЕЦ КАК ДатаПринятияКУчетуРегл,
	|	ТаблицаНМА.ДатаПриобретения,
	|	ТаблицаНМА.КоэффициентБУ,
	|	ТаблицаНМА.МетодНачисленияАмортизацииНУ,
	|	ТаблицаНМА.НакопленнаяАмортизацияБУ,
	|	ТаблицаНМА.НакопленнаяАмортизацияВР,
	|	ТаблицаНМА.НакопленнаяАмортизацияНУ,
	|	ТаблицаНМА.НакопленнаяАмортизацияПР,
	|	ТаблицаНМА.НачислятьАмортизациюБУ,
	|	ТаблицаНМА.НачислятьАмортизациюНУ,
	|	ТаблицаНМА.НематериальныйАктив,
	|	ТаблицаНМА.НематериальныйАктив.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаНМА.ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ТаблицаНМА.ПервоначальнаяСтоимостьБУ,
	|	ТаблицаНМА.ПервоначальнаяСтоимостьНУ,
	|	ТаблицаНМА.ПорядокСписанияНИОКРНаРасходыНУ,
	|	ТаблицаНМА.РеквизитыДокументаОплаты,
	|	ТаблицаНМА.СпециальныйКоэффициентНУ,
	|	ТаблицаНМА.СпособНачисленияАмортизацииБУ,
	|	ТаблицаНМА.СпособПоступления,
	|	ТаблицаНМА.СрокПолезногоИспользованияБУ,
	|	ТаблицаНМА.СрокПолезногоИспользованияНУ,
	|	ТаблицаНМА.СчетАмортизации,
	|	ТаблицаНМА.СчетУчета,
	|	ТаблицаНМА.ТекущаяСтоимостьБУ + ВЫБОР КОГДА ТаблицаНМА.ПрименениеЦелевогоФинансирования ТОГДА ТаблицаНМА.ТекущаяСтоимостьБУЦФ ИНАЧЕ 0 КОНЕЦ КАК ТекущаяСтоимостьБУ,
	|	ТаблицаНМА.ТекущаяСтоимостьНУ + ВЫБОР КОГДА ТаблицаНМА.ПрименениеЦелевогоФинансирования ТОГДА ТаблицаНМА.ТекущаяСтоимостьНУЦФ ИНАЧЕ 0 КОНЕЦ КАК ТекущаяСтоимостьНУ,
	|	ТаблицаНМА.ТекущаяСтоимостьПР + ВЫБОР КОГДА ТаблицаНМА.ПрименениеЦелевогоФинансирования ТОГДА ТаблицаНМА.ТекущаяСтоимостьПРЦФ ИНАЧЕ 0 КОНЕЦ КАК ТекущаяСтоимостьПР,
	|	ТаблицаНМА.СтатьяРасходовАмортизации,
	|	ТаблицаНМА.АналитикаРасходовАмортизации,
	|	ТаблицаНМА.ФактическийСрокИспользованияДо2009,
	|	ТаблицаНМА.ПередаватьРасходыВДругуюОрганизацию,
	|	ТаблицаНМА.ОрганизацияПолучательРасходов,
	|	ТаблицаНМА.СчетПередачиРасходов,
	|	ТаблицаНМА.ПрименениеЦелевогоФинансирования,
	|	ТаблицаНМА.СчетУчетаЦФ,
	|	ТаблицаНМА.СчетАмортизацииЦФ,
	|	ТаблицаНМА.СтатьяДоходов,
	|	ТаблицаНМА.АналитикаДоходов,
	|	ТаблицаНМА.РезервПереоценкиСтоимости,
	|	ТаблицаНМА.РезервПереоценкиАмортизации
	|ПОМЕСТИТЬ ТаблицаНМА
	|ИЗ
	|	Документ.ВводОстатковВнеоборотныхАктивов.НМА КАК ТаблицаНМА
	|ГДЕ
	|	&ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиНМА)
	|	И ТаблицаНМА.Ссылка = &Ссылка"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ГрафикиАмортизацииОСБухгалтерскийУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ГрафикиАмортизацииОСБухгалтерскийУчет";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица ГрафикиАмортизацииОСБухгалтерскийУчет
	|"+
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	ТаблицаОС.ДатаПринятияКУчету КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	ТаблицаОС.ГрафикАмортизацииБУ КАК ГрафикАмортизации
	|	
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.ГрафикАмортизацииБУ <> ЗНАЧЕНИЕ(Справочник.ГодовыеГрафикиАмортизацииОС.ПустаяСсылка)
	|	И &ЭтоОстаткиБалансовыхОС
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура МестонахождениеОСБухгалтерскийУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "МестонахождениеОСБухгалтерскийУчет";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица МестонахождениеОСБухгалтерскийУчет
	|"+
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	ТаблицаОС.ДатаПринятияКУчету КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	ТаблицаОС.МОЛ КАК МОЛ,
	|	&Подразделение КАК Местонахождение,
	|	ТаблицаОС.АдресМестонахождения КАК АдресМестонахождения
	|	
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	&ЭтоОстаткиБалансовыхОС
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	ТаблицаОС.ДатаПринятияКУчету КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	НЕОПРЕДЕЛЕНО КАК МОЛ,
	|	&Подразделение КАК Местонахождение,
	|	НЕОПРЕДЕЛЕНО КАК АдресМестонахождения
	|	
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	&ЭтоОстаткиЗабалансовыхОС
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	ТаблицаОС.Контрагент КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	НЕОПРЕДЕЛЕНО КАК МОЛ,
	|	ТаблицаОС.ПодразделениеАрендатора КАК Местонахождение,
	|	НЕОПРЕДЕЛЕНО КАК АдресМестонахождения
	|	
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	&ЭтоОстаткиПереданныхВАрендуБалансовыхОС
	|	И ТаблицаОС.РасчетыМеждуОрганизациямиАрендатор
	|	
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура НачислениеАмортизацииОСБухгалтерскийУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "НачислениеАмортизацииОСБухгалтерскийУчет";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица НачислениеАмортизацииОСБухгалтерскийУчет
	|"+
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	ТаблицаОС.ДатаПринятияКУчету КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	ТаблицаОС.НачислятьАмортизациюБУ КАК НачислятьАмортизацию
	|	
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	&ЭтоОстаткиБалансовыхОС
	|	
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура НачислениеАмортизацииОСНалоговыйУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "НачислениеАмортизацииОСНалоговыйУчет";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица НачислениеАмортизацииОСНалоговыйУчет
	|"+
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	ТаблицаОС.ДатаПринятияКУчету КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	ВЫБОР КОГДА ТаблицаОС.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации)
	|		ТОГДА ТаблицаОС.НачислятьАмортизациюНУ
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НачислятьАмортизацию
	|	
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	&ЭтоОстаткиБалансовыхОС
	|	
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура НачислениеАмортизацииОССпециальныйКоэффициентНалоговыйУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "НачислениеАмортизацииОССпециальныйКоэффициентНалоговыйУчет";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица НачислениеАмортизацииОССпециальныйКоэффициентНалоговыйУчет
	|"+
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	ТаблицаОС.ДатаПринятияКУчету КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	ТаблицаОС.СпециальныйКоэффициентНУ КАК СпециальныйКоэффициент
	|	
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации)
	|	И &ЭтоОстаткиБалансовыхОС
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчет";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаНематериальныхАктивов(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица НачислениеАмортизацииНМАСпециальныйКоэффициентНалоговыйУчет
	|"+
	"ВЫБРАТЬ
	|	ТаблицаНМА.НомерСтроки,
	|
	|	&Ссылка КАК Регистратор,
	|	ТаблицаНМА.ДатаПринятияКУчетуРегл КАК Период,
	|
	|	&Организация КАК Организация,
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|
	|	ТаблицаНМА.СпециальныйКоэффициентНУ КАК СпециальныйКоэффициент
	|
	|ИЗ
	|	ТаблицаНМА КАК ТаблицаНМА
	|ГДЕ
	|	&ЭтоОстаткиНМА
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаНМА.НомерСтроки"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ПараметрыАмортизацииОСБухгалтерскийУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПараметрыАмортизацииОСБухгалтерскийУчет";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица ПараметрыАмортизацииОСБухгалтерскийУчет
	|"+
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	ТаблицаОС.ДатаПринятияКУчету КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	ТаблицаОС.СрокИспользованияБУ КАК СрокПолезногоИспользования,
	|	ТаблицаОС.ОбъемНаработкиБУ КАК ОбъемПродукцииРабот,
	|	ТаблицаОС.СрокИспользованияБУ КАК СрокИспользованияДляВычисленияАмортизации,
	|	ТаблицаОС.ТекущаяСтоимостьБУ КАК СтоимостьДляВычисленияАмортизации,
	|	ТаблицаОС.ОбъемНаработкиБУ КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ТаблицаОС.КоэффициентАмортизацииБУ КАК КоэффициентАмортизации,
	|	ТаблицаОС.КоэффициентУскоренияБУ КАК КоэффициентУскорения
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	&ЭтоОстаткиБалансовыхОС
	|	И НЕ ТаблицаОС.ЕстьСобытияМодернизации
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	ТаблицаОС.ДатаМодернизации КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	ТаблицаОС.СрокИспользованияБУ + РАЗНОСТЬДАТ(ТаблицаОС.ДатаПринятияКУчету, ТаблицаОС.ДатаМодернизации, МЕСЯЦ) КАК СрокПолезногоИспользования,
	|	ТаблицаОС.ОбъемНаработкиБУ КАК ОбъемПродукцииРабот,
	|	ТаблицаОС.СрокИспользованияБУ КАК СрокИспользованияДляВычисленияАмортизации,
	|	ТаблицаОС.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|	ТаблицаОС.ОбъемНаработкиБУ КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ТаблицаОС.КоэффициентАмортизацииБУ КАК КоэффициентАмортизации,
	|	ТаблицаОС.КоэффициентУскоренияБУ КАК КоэффициентУскорения
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	&ЭтоОстаткиБалансовыхОС
	|	И ТаблицаОС.ЕстьСобытияМодернизации
	|	И (ТаблицаОС.МетодНачисленияАмортизацииБУ <> ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|		И ТаблицаОС.МетодНачисленияАмортизацииБУ <> ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПоЕНАОФНа1000кмПробега)
	|	)
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	ТаблицаОС.ДатаМодернизации КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	ТаблицаОС.СрокИспользованияБУ КАК СрокПолезногоИспользования,
	|	ТаблицаОС.ОбъемНаработкиБУ КАК ОбъемПродукцииРабот,
	|	ТаблицаОС.СрокИспользованияБУ КАК СрокИспользованияДляВычисленияАмортизации,
	|	ТаблицаОС.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизации,
	|	ТаблицаОС.ОбъемНаработкиБУ КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ТаблицаОС.КоэффициентАмортизацииБУ КАК КоэффициентАмортизации,
	|	ТаблицаОС.КоэффициентУскоренияБУ КАК КоэффициентУскорения
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	&ЭтоОстаткиБалансовыхОС
	|	И ТаблицаОС.ЕстьСобытияМодернизации
	|	И (ТаблицаОС.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции)
	|		ИЛИ ТаблицаОС.МетодНачисленияАмортизацииБУ = ЗНАЧЕНИЕ(Перечисление.СпособыНачисленияАмортизацииОС.ПоЕНАОФНа1000кмПробега)
	|	)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ПараметрыАмортизацииОСНалоговыйУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПараметрыАмортизацииОСНалоговыйУчет";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица ПараметрыАмортизацииОСНалоговыйУчет
	|"+
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	ТаблицаОС.ДатаПринятияКУчету КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	ВЫБОР КОГДА ТаблицаОС.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации)
	|		ТОГДА ТаблицаОС.СрокИспользованияНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СрокПолезногоИспользования,
	|	0 КАК КоэффициентАмортизации,
	|	ТаблицаОС.ТекущаяСтоимостьПР КАК ПРДляВычисленияАмортизации
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	&ЭтоОстаткиБалансовыхОС
	|	И НЕ ТаблицаОС.ЕстьСобытияМодернизации
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	ТаблицаОС.ДатаПринятияКУчету КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	ВЫБОР КОГДА ТаблицаОС.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации)
	|		ТОГДА ТаблицаОС.СрокИспользованияНУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СрокПолезногоИспользования,
	|	0 КАК КоэффициентАмортизации,
	|	ВЫРАЗИТЬ(ТаблицаОС.ТекущаяСтоимостьПР * (ТаблицаОС.СтоимостьДляВычисленияАмортизации/ТаблицаОС.ТекущаяСтоимостьБУ) КАК ЧИСЛО(15, 2)) КАК ПРДляВычисленияАмортизации
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	&ЭтоОстаткиБалансовыхОС
	|	И ТаблицаОС.ЕстьСобытияМодернизации
	|	
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ПервоначальныеСведенияОСБухгалтерскийУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПервоначальныеСведенияОСБухгалтерскийУчет";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица ПервоначальныеСведенияОСБухгалтерскийУчет
	|"+
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	ТаблицаОС.ДатаПринятияКУчету КАК Период,
	|	
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ТаблицаОС.ДоговорЛизинга КАК ДоговорЛизинга,
	|	ТаблицаОС.СпособПоступления КАК СпособПоступления,
	|	ТаблицаОС.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимость,
	|	ТаблицаОС.МетодНачисленияАмортизацииБУ КАК СпособНачисленияАмортизации,
	|	ТаблицаОС.ПоказательНаработки КАК ПоказательНаработки,
	|	ТаблицаОС.ПорядокУчетаБУ КАК ПорядокПогашенияСтоимости,
	|	ТаблицаОС.ГруппаОС КАК ГруппаОС,
	|	ТаблицаОС.КодПоОКОФ КАК КодПоОКОФ,
	|	ТаблицаОС.ШифрПоЕНАОФ КАК ШифрПоЕНАОФ,
	|	ТаблицаОС.АмортизационнаяГруппа КАК АмортизационнаяГруппа,
	|	ТаблицаОС.РезервПереоценкиСтоимости КАК РезервПереоценкиСтоимости,
	|	ТаблицаОС.РезервПереоценкиАмортизации КАК РезервПереоценкиАмортизации
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	&ЭтоОстаткиБалансовыхОС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	ТаблицаОС.ДатаПринятияКУчету КАК Период,
	|	
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ИнвентарныйНомер КАК ИнвентарныйНомер,
	|	ТаблицаОС.ДоговорЛизинга КАК ДоговорЛизинга,
	|	НЕОПРЕДЕЛЕНО КАК СпособПоступления,
	|	ТаблицаОС.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимость,
	|	НЕОПРЕДЕЛЕНО КАК СпособНачисленияАмортизации,
	|	НЕОПРЕДЕЛЕНО КАК ПоказательНаработки,
	|	НЕОПРЕДЕЛЕНО КАК ПорядокПогашенияСтоимости,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаОС,
	|	НЕОПРЕДЕЛЕНО КАК КодПоОКОФ,
	|	НЕОПРЕДЕЛЕНО КАК ШифрПоЕНАОФ,
	|	НЕОПРЕДЕЛЕНО КАК АмортизационнаяГруппа,
	|	НЕОПРЕДЕЛЕНО КАК РезервПереоценкиСтоимости,
	|	НЕОПРЕДЕЛЕНО КАК РезервПереоценкиАмортизации
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	&ЭтоОстаткиЗабалансовыхОС
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ПервоначальныеСведенияНМАБухгалтерскийУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПервоначальныеСведенияНМАБухгалтерскийУчет";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаНематериальныхАктивов(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица ПервоначальныеСведенияНМАБухгалтерскийУчет
	|"+
	"ВЫБРАТЬ
	|	ТаблицаНМА.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаНМА.ТекущаяСтоимостьБУ КАК ПервоначальнаяСтоимость,
	|	ТаблицаНМА.СпособПоступления КАК СпособПоступления,
	|	ТаблицаНМА.НачислятьАмортизациюБУ КАК НачислятьАмортизацию,
	|	ТаблицаНМА.СпособНачисленияАмортизацииБУ КАК СпособНачисленияАмортизации,
	|	ТаблицаНМА.СрокПолезногоИспользованияБУ КАК СрокПолезногоИспользования,
	|	ТаблицаНМА.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ТаблицаНМА.КоэффициентБУ КАК Коэффициент,
	|	ТаблицаНМА.РезервПереоценкиСтоимости КАК РезервПереоценкиСтоимости,
	|	ТаблицаНМА.РезервПереоценкиАмортизации КАК РезервПереоценкиАмортизации
	|ИЗ
	|	ТаблицаНМА КАК ТаблицаНМА
	|ГДЕ
	|	ТаблицаНМА.ЭтоНМА
	|	И &ЭтоОстаткиНМА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаНМА.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаНМА.ПервоначальнаяСтоимостьБУ КАК ПервоначальнаяСтоимость,
	|	ТаблицаНМА.СпособПоступления КАК СпособПоступления,
	|	ТаблицаНМА.НачислятьАмортизациюБУ КАК НачислятьАмортизацию,
	|	ТаблицаНМА.СпособНачисленияАмортизацииБУ КАК СпособНачисленияАмортизации,
	|	ТаблицаНМА.СрокПолезногоИспользованияБУ КАК СрокПолезногоИспользования,
	|	ТаблицаНМА.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ТаблицаНМА.КоэффициентБУ КАК Коэффициент,
	|	0 КАК РезервПереоценкиСтоимости,
	|	0 КАК РезервПереоценкиАмортизации
	|ИЗ
	|	ТаблицаНМА КАК ТаблицаНМА
	|ГДЕ
	|	ТаблицаНМА.ЭтоНИОКР
	|	И &ЭтоОстаткиНМА
	|	
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаНМА.НомерСтроки"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ПервоначальныеСведенияОСНалоговыйУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПервоначальныеСведенияОСНалоговыйУчет";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица ПервоначальныеСведенияОСНалоговыйУчет
	|"+
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	ТаблицаОС.ДатаПринятияКУчету КАК Период,
	|	
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.МетодыНачисленияАмортизации.Линейный) КАК МетодНачисленияАмортизации,
	|	&Организация КАК Организация,
	|	ТаблицаОС.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
	|	ТаблицаОС.ПорядокУчетаНУ КАК ПорядокВключенияСтоимостиВСоставРасходов,
	|	ТаблицаОС.НакопленнаяАмортизацияНУДо2009 КАК АмортизацияДо2009,
	|	ТаблицаОС.СрокИспользованияНУДо2009 КАК ФактическийСрокИспользованияДо2009
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	&ЭтоОстаткиБалансовыхОС
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ПервоначальныеСведенияНМАНалоговыйУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПервоначальныеСведенияНМАНалоговыйУчет";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаНематериальныхАктивов(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица ПервоначальныеСведенияНМАНалоговыйУчет
	|"+
	"ВЫБРАТЬ
	|	ТаблицаНМА.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	
	|	0 КАК АмортизацияДо2002,
	|	ТаблицаНМА.АмортизацияДо2009 КАК АмортизацияДо2009,
	|	ТаблицаНМА.ДатаПриобретения КАК ДатаПриобретения,
	|	ТаблицаНМА.МетодНачисленияАмортизацииНУ КАК МетодНачисленияАмортизации,
	|	ТаблицаНМА.НачислятьАмортизациюНУ КАК НачислятьАмортизацию,
	|	&Организация КАК Организация,
	|	ТаблицаНМА.ТекущаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
	|	ТаблицаНМА.СрокПолезногоИспользованияНУ КАК СрокПолезногоИспользования,
	|	0 КАК СтоимостьДо2002,
	|	ТаблицаНМА.ФактическийСрокИспользованияДо2009 КАК ФактическийСрокИспользованияДо2009
	|ИЗ
	|	ТаблицаНМА КАК ТаблицаНМА
	|ГДЕ
	|	ТаблицаНМА.ЭтоНМА
	|	И &ЭтоОстаткиНМА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаНМА.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	
	|	0 КАК АмортизацияДо2002,
	|	ТаблицаНМА.АмортизацияДо2009 КАК АмортизацияДо2009,
	|	ТаблицаНМА.ДатаПриобретения КАК ДатаПриобретения,
	|	ТаблицаНМА.МетодНачисленияАмортизацииНУ КАК МетодНачисленияАмортизации,
	|	ТаблицаНМА.НачислятьАмортизациюНУ КАК НачислятьАмортизацию,
	|	&Организация КАК Организация,
	|	ТаблицаНМА.ПервоначальнаяСтоимостьНУ КАК ПервоначальнаяСтоимостьНУ,
	|	ТаблицаНМА.СрокПолезногоИспользованияНУ КАК СрокПолезногоИспользования,
	|	0 КАК СтоимостьДо2002,
	|	ТаблицаНМА.ФактическийСрокИспользованияДо2009 КАК ФактическийСрокИспользованияДо2009
	|ИЗ
	|	ТаблицаНМА КАК ТаблицаНМА
	|ГДЕ
	|	ТаблицаНМА.ЭтоНИОКР
	|	И &ЭтоОстаткиНМА
	|	
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаНМА.НомерСтроки"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура СобытияОСОрганизаций(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СобытияОСОрганизаций";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица СобытияОСОрганизаций
	|"+
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	ТаблицаОС.ДатаПринятияКУчету КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	ТаблицаОС.СобытиеПринятияКУчету КАК Событие,
	|	
	|	ТаблицаОС.НазваниеДокументаПринятияКУчету КАК НазваниеДокумента,
	|	ТаблицаОС.НомерДокументаПринятияКУчету КАК НомерДокумента
	|	
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.ДатаПринятияКУчету<>ДАТАВРЕМЯ(1,1,1,0,0,0)
	|	И &ЭтоОстаткиБалансовыхОС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	ТаблицаОС.ДатаМодернизации КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	ТаблицаОС.СобытиеМодернизации КАК Событие,
	|	
	|	ТаблицаОС.НазваниеДокументаМодернизации КАК НазваниеДокумента,
	|	ТаблицаОС.НомерДокументаМодернизации КАК НомерДокумента
	|	
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.ЕстьСобытияМодернизации
	|	И &ЭтоОстаткиБалансовыхОС
	|	
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура СостоянияОСОрганизаций(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СостоянияОСОрганизаций";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица СостоянияОСОрганизаций
	|"+
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	ТаблицаОС.ДатаПринятияКУчету КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	ТаблицаОС.ДатаПринятияКУчету КАК ДатаСостояния,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету) КАК Состояние
	|	
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	&ЭтоОстаткиБалансовыхОС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	ТаблицаОС.ДатаПринятияКУчету КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	ТаблицаОС.ДатаПринятияКУчету КАК ДатаСостояния,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКЗабалансовомуУчету) КАК Состояние
	|	
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	&ЭтоОстаткиЗабалансовыхОС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	ТаблицаОС.Контрагент КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	&Период КАК ДатаСостояния,
	|	ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКЗабалансовомуУчету) КАК Состояние
	|	
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	&ЭтоОстаткиПереданныхВАрендуБалансовыхОС
	|	И ТаблицаОС.РасчетыМеждуОрганизациямиАрендатор
	|	
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура СостоянияНМАОрганизаций(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СостоянияНМАОрганизаций";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаНематериальныхАктивов(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица СостоянияНМАОрганизаций
	|"+
	"ВЫБРАТЬ
	|	ТаблицаНМА.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	ТаблицаНМА.ДатаПринятияКУчетуРегл КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.ПринятКУчету) КАК Состояние
	|	
	|ИЗ
	|	ТаблицаНМА КАК ТаблицаНМА
	|ГДЕ
	|	&ЭтоОстаткиНМА
	|	
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаНМА.НомерСтроки"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчет";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчет
	|"+
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	ТаблицаОС.ДатаПринятияКУчету КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	ТаблицаОС.СтатьяРасходовАмортизации КАК СтатьяРасходов,
	|	ТаблицаОС.АналитикаРасходовАмортизации КАК АналитикаРасходов,
	|	ТаблицаОС.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ТаблицаОС.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	ТаблицаОС.СчетПередачиРасходов КАК СчетПередачиРасходов
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	&ЭтоОстаткиБалансовыхОС
	|	И (
	|		ТаблицаОС.ПорядокУчетаБУ = ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеАмортизации)
	|		ИЛИ ТаблицаОС.ПорядокУчетаНУ = ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации)
	|	)
	|	
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчет";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаНематериальныхАктивов(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчет
	|"+
	"ВЫБРАТЬ
	|	ТаблицаНМА.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	ТаблицаНМА.ДатаПринятияКУчетуРегл КАК Период,
	|	
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	&Организация КАК Организация,
	|	
	|	ТаблицаНМА.СтатьяРасходовАмортизации КАК СтатьяРасходов,
	|	ТаблицаНМА.АналитикаРасходовАмортизации КАК АналитикаРасходов,
	|	ТаблицаНМА.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ТаблицаНМА.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	ТаблицаНМА.СчетПередачиРасходов КАК СчетПередачиРасходов
	|	
	|ИЗ
	|	ТаблицаНМА КАК ТаблицаНМА
	|ГДЕ
	|	&ЭтоОстаткиНМА
	|	И (ТаблицаНМА.НачислятьАмортизациюБУ ИЛИ ТаблицаНМА.НачислятьАмортизациюНУ)
	|	
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаНМА.НомерСтроки"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура СчетаБухгалтерскогоУчетаОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СчетаБухгалтерскогоУчетаОС";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица СчетаБухгалтерскогоУчетаОС
	|"+
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	ТаблицаОС.ДатаПринятияКУчету КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	ТаблицаОС.СчетУчета КАК СчетУчета,
	|	ТаблицаОС.СчетАмортизации КАК СчетНачисленияАмортизации
	|	
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	&ЭтоОстаткиБалансовыхОС
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	ТаблицаОС.ДатаПринятияКУчету КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	ТаблицаОС.СчетУчета КАК СчетУчета,
	|	ТаблицаОС.СчетАмортизации КАК СчетНачисленияАмортизации
	|	
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	&ЭтоОстаткиЗабалансовыхОС
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	ТаблицаОС.Контрагент КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	ТаблицаОС.СчетУчетаАрендатора КАК СчетУчета,
	|	НЕОПРЕДЕЛЕНО КАК СчетНачисленияАмортизации
	|	
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	&ЭтоОстаткиПереданныхВАрендуБалансовыхОС
	|	И ТаблицаОС.РасчетыМеждуОрганизациямиАрендатор
	|	
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура СчетаБухгалтерскогоУчетаНМА(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СчетаБухгалтерскогоУчетаНМА";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаНематериальныхАктивов(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица СчетаБухгалтерскогоУчетаНМА
	|"+
	"ВЫБРАТЬ
	|	ТаблицаНМА.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	ТаблицаНМА.ДатаПринятияКУчетуРегл КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	
	|	ТаблицаНМА.СчетУчета КАК СчетУчета,
	|	ТаблицаНМА.СчетАмортизации КАК СчетНачисленияАмортизации
	|	
	|ИЗ
	|	ТаблицаНМА КАК ТаблицаНМА
	|ГДЕ
	|	&ЭтоОстаткиНМА
	|	
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаНМА.НомерСтроки"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура СпособыОтраженияРасходовПоИмущественнымНалогам(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СпособыОтраженияРасходовПоИмущественнымНалогам";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица СпособыОтраженияРасходовПоИмущественнымНалогам
	|"+
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	ТаблицаОС.СтатьяРасходовНалог КАК СтатьяРасходов,
	|	ТаблицаОС.АналитикаРасходовНалог КАК АналитикаРасходов
	|	
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	&ЭтоОстаткиБалансовыхОС
	|	
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ПараметрыЦелевогоФинансированияНМА(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПараметрыЦелевогоФинансированияНМА";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаНематериальныхАктивов(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица ПараметрыЦелевогоФинансированияНМА
	|"+
	"ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	ТаблицаНМА.ДатаПринятияКУчетуРегл КАК Период,
	|	
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	
	|	ИСТИНА КАК ПрименениеЦелевогоФинансирования,
	|	ТаблицаНМА.СчетУчетаЦФ КАК СчетУчета,
	|	ТаблицаНМА.СчетАмортизацииЦФ КАК СчетАмортизации,
	|	ТаблицаНМА.СтатьяДоходов КАК СтатьяДоходов,
	|	ТаблицаНМА.АналитикаДоходов КАК АналитикаДоходов
	|	
	|ИЗ
	|	ТаблицаНМА КАК ТаблицаНМА
	|	
	|ГДЕ
	|	&ЭтоОстаткиНМА
	|	И ТаблицаНМА.ПрименениеЦелевогоФинансирования"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ПараметрыЦелевогоФинансированияОС(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПараметрыЦелевогоФинансированияОС";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица ПараметрыЦелевогоФинансированияОС
	|"+
	"ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	ТаблицаОС.ДатаПринятияКУчету КАК Период,
	|	
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	ИСТИНА КАК ПрименениеЦелевогоФинансирования,
	|	ТаблицаОС.СчетУчетаЦФ КАК СчетУчета,
	|	ТаблицаОС.СчетАмортизацииЦФ КАК СчетАмортизации,
	|	ТаблицаОС.СтатьяДоходов КАК СтатьяДоходов,
	|	ТаблицаОС.АналитикаДоходов КАК АналитикаДоходов
	|	
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|	
	|ГДЕ
	|	&ЭтоОстаткиБалансовыхОС
	|	И ТаблицаОС.ПрименениеЦелевогоФинансирования"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(ТаблицаОС.ДатаПринятияКУчету, ДЕНЬ) КАК ДатаОтражения
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.ВключитьАмортизационнуюПремиюВСоставРасходов
	|	И НАЧАЛОПЕРИОДА(ТаблицаОС.ДатаПринятияКУчету, ДЕНЬ) <> НАЧАЛОПЕРИОДА(&Период, ДЕНЬ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Период КАК Период,
	|	ТаблицаОС.Контрагент КАК Организация,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ) КАК ДатаОтражения
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|ГДЕ
	|	&ЭтоОстаткиПереданныхВАрендуБалансовыхОС
	|	И ТаблицаОС.РасчетыМеждуОрганизациямиАрендатор
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ПроведениеРегламентированныйУчет

Функция ТекстОтраженияВРеглУчете() Экспорт
	
	Разделитель = Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС;
	
	Возврат ИзносОС()
		+ Разделитель + СчетУчетаОС()
		+ Разделитель + СчетУчетаОСЦФ()
		+ Разделитель + АмортизацияОС()
		+ Разделитель + АмортизацияОСЦФ()
		+ Разделитель + СчетУчетаНМА()
		+ Разделитель + СчетУчетаНМАЦФ()
		+ Разделитель + АмортизацияНМА()
		+ Разделитель + АмортизацияНМАЦФ()
		+ Разделитель + ЗабалансовыйУчетАрендованныхОС()
		+ Разделитель + ЗабалансовыйУчетПереданныхАрендуБалансовыхОС()
		+ Разделитель + АмортизационнаяПремияДтКВ()
		+ Разделитель + РасчетыПоДоговорамЛизинга();
	
КонецФункции

Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	Возврат "";
	
КонецФункции

Функция СчетУчетаОС()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Остатки счета учета ОС (Дт СчетУчета:: Кт 000)
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.ТекущаяСтоимостьБУ КАК Сумма,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	Строки.ОсновноеСредство.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	Строки.СчетУчета КАК СчетДт,
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	Строки.Контрагент КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.ТекущаяСтоимостьНУ КАК СуммаНУДт,
	|	Строки.ТекущаяСтоимостьПР КАК СуммаПРДт,
	|	Строки.ТекущаяСтоимостьБУ - Строки.ТекущаяСтоимостьНУ - Строки.ТекущаяСтоимостьПР КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Вспомогательный) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Остатки счета учета ОС"" КАК Содержание
	|ИЗ
	|	Документ.ВводОстатковВнеоборотныхАктивов КАК Операция
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов.ОС КАК Строки
	|	ПО Строки.Ссылка = Операция.Ссылка
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиОС),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПереданныхВАрендуОС),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПредметовЛизингаНаБалансе),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПереданныхВАрендуПредметовЛизингаНаБалансе)
	|	)";
	
КонецФункции

Функция СчетУчетаОСЦФ()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Остатки целевых средств на счете учета ОС (Дт СчетУчета:: Кт 000)
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.ТекущаяСтоимостьБУЦФ КАК Сумма,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	Строки.ОсновноеСредство.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	Строки.СчетУчетаЦФ КАК СчетДт,
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	Строки.Контрагент КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.ТекущаяСтоимостьНУЦФ КАК СуммаНУДт,
	|	Строки.ТекущаяСтоимостьПРЦФ КАК СуммаПРДт,
	|	Строки.ТекущаяСтоимостьБУЦФ - Строки.ТекущаяСтоимостьНУЦФ - Строки.ТекущаяСтоимостьПРЦФ КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Вспомогательный) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Остатки целевых средств в восстановительной стоимости ОС"" КАК Содержание
	|ИЗ
	|	Документ.ВводОстатковВнеоборотныхАктивов КАК Операция
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов.ОС КАК Строки
	|	ПО Строки.Ссылка = Операция.Ссылка
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Строки.ПрименениеЦелевогоФинансирования
	|	И Операция.ТипОперации В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиОС),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПереданныхВАрендуОС),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПредметовЛизингаНаБалансе),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПереданныхВАрендуПредметовЛизингаНаБалансе)
	|	)";
	
КонецФункции

Функция АмортизацияОС()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Накопленная амортизация ОС (Дт 000:: Кт СчетАмортизации)
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.НакопленнаяАмортизацияБУ КАК Сумма,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Вспомогательный) КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	Строки.ОсновноеСредство.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	Строки.СчетАмортизации КАК СчетКт,
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.НакопленнаяАмортизацияНУ КАК СуммаНУКт,
	|	Строки.НакопленнаяАмортизацияПР КАК СуммаПРКт,
	|	Строки.НакопленнаяАмортизацияБУ - Строки.НакопленнаяАмортизацияНУ - Строки.НакопленнаяАмортизацияПР КАК СуммаВРКт,
	|	""Накопленная амортизация ОС"" КАК Содержание
	|ИЗ
	|	Документ.ВводОстатковВнеоборотныхАктивов КАК Операция
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов.ОС КАК Строки
	|	ПО Строки.Ссылка = Операция.Ссылка
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиОС),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПереданныхВАрендуОС),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПредметовЛизингаНаБалансе),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПереданныхВАрендуПредметовЛизингаНаБалансе)
	|	)
	|	И (
	|		Строки.ПорядокУчетаБУ В (
	|			ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеАмортизации),
	|			ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена)
	|		)
	|		ИЛИ Строки.ПорядокУчетаНУ В (
	|			ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации),
	|			ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.АмортизацияНачислена)
	|		)
	|	)
	|";
	
КонецФункции

Функция АмортизацияОСЦФ()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Целевые средства в накопленной амортизации ОС (Дт 000:: Кт СчетАмортизации)
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.НакопленнаяАмортизацияБУЦФ КАК Сумма,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Вспомогательный) КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	Строки.ОсновноеСредство.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	Строки.СчетАмортизацииЦФ КАК СчетКт,
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.НакопленнаяАмортизацияНУЦФ КАК СуммаНУКт,
	|	Строки.НакопленнаяАмортизацияПРЦФ КАК СуммаПРКт,
	|	Строки.НакопленнаяАмортизацияБУЦФ - Строки.НакопленнаяАмортизацияНУЦФ - Строки.НакопленнаяАмортизацияПРЦФ КАК СуммаВРКт,
	|	""Остатки целевых средств в накопленной амортизации ОС"" КАК Содержание
	|ИЗ
	|	Документ.ВводОстатковВнеоборотныхАктивов КАК Операция
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов.ОС КАК Строки
	|	ПО Строки.Ссылка = Операция.Ссылка
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Строки.ПрименениеЦелевогоФинансирования
	|	И Операция.ТипОперации В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиОС),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПереданныхВАрендуОС),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПредметовЛизингаНаБалансе),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПереданныхВАрендуПредметовЛизингаНаБалансе)
	|	)
	|	И (
	|		Строки.ПорядокУчетаБУ В (
	|			ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеАмортизации),
	|			ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.АмортизацияНачислена)
	|		)
	|		ИЛИ Строки.ПорядокУчетаНУ В (
	|			ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.НачислениеАмортизации),
	|			ЗНАЧЕНИЕ(Перечисление.ПорядокВключенияСтоимостиОСВСоставРасходовНУ.АмортизацияНачислена)
	|		)
	|	)
	|";
	
КонецФункции

Функция ИзносОС()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Накопленный износ ОС (Дт СчетИзноса:: Кт 000)
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.НакопленнаяАмортизацияБУ КАК Сумма,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	Строки.СчетАмортизации КАК СчетДт,
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Накопленный износ ОС"" КАК Содержание
	|ИЗ
	|	Документ.ВводОстатковВнеоборотныхАктивов КАК Операция
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов.ОС КАК Строки
	|	ПО Строки.Ссылка = Операция.Ссылка
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиОС),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПереданныхВАрендуОС),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПредметовЛизингаНаБалансе),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПереданныхВАрендуПредметовЛизингаНаБалансе)
	|	)
	|	И Строки.ПорядокУчетаБУ В (
	|		ЗНАЧЕНИЕ(Перечисление.ПорядокПогашенияСтоимостиОС.НачислениеИзносаПоЕНАОФ)
	|	)";
	
КонецФункции

Функция СчетУчетаНМА()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Остатки счета учета НМА (Дт СчетУчета:: Кт 000)
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.ТекущаяСтоимостьБУ КАК Сумма,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	Строки.НематериальныйАктив.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	Строки.СчетУчета КАК СчетДт,
	|	Строки.НематериальныйАктив КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.ТекущаяСтоимостьНУ КАК СуммаНУДт,
	|	Строки.ТекущаяСтоимостьПР КАК СуммаПРДт,
	|	Строки.ТекущаяСтоимостьБУ - Строки.ТекущаяСтоимостьНУ - Строки.ТекущаяСтоимостьПР КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Вспомогательный) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Остатки счета учета НМА"" КАК Содержание
	|ИЗ
	|	Документ.ВводОстатковВнеоборотныхАктивов КАК Операция
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов.НМА КАК Строки
	|	ПО Строки.Ссылка = Операция.Ссылка
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиНМА)";
	
КонецФункции

Функция СчетУчетаНМАЦФ()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Остатки целевых средств в восстановительности стоимости НМА (Дт СчетУчета:: Кт 000)
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.ТекущаяСтоимостьБУЦФ КАК Сумма,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	Строки.НематериальныйАктив.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	
	|	Строки.СчетУчетаЦФ КАК СчетДт,
	|	Строки.НематериальныйАктив КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.ТекущаяСтоимостьНУЦФ КАК СуммаНУДт,
	|	Строки.ТекущаяСтоимостьПРЦФ КАК СуммаПРДт,
	|	Строки.ТекущаяСтоимостьБУЦФ - Строки.ТекущаяСтоимостьНУЦФ - Строки.ТекущаяСтоимостьПРЦФ КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Вспомогательный) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Остатки целевых средств в восстановительной стоиомсти НМА"" КАК Содержание
	|ИЗ
	|	Документ.ВводОстатковВнеоборотныхАктивов КАК Операция
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов.НМА КАК Строки
	|	ПО Строки.Ссылка = Операция.Ссылка
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Строки.ПрименениеЦелевогоФинансирования
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиНМА)";
	
КонецФункции

Функция АмортизацияНМА()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Накопленная амортизация НМА (Дт 000:: Кт СчетАмортизации)
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.НакопленнаяАмортизацияБУ КАК Сумма,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Вспомогательный) КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	Строки.НематериальныйАктив.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	Строки.СчетАмортизации КАК СчетКт,
	|	Строки.НематериальныйАктив КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.НакопленнаяАмортизацияНУ КАК СуммаНУКт,
	|	Строки.НакопленнаяАмортизацияПР КАК СуммаПРКт,
	|	Строки.НакопленнаяАмортизацияБУ - Строки.НакопленнаяАмортизацияНУ - Строки.НакопленнаяАмортизацияПР КАК СуммаВРКт,
	|	""Накопленная амортизация НМА"" КАК Содержание
	|ИЗ
	|	Документ.ВводОстатковВнеоборотныхАктивов КАК Операция
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов.НМА КАК Строки
	|	ПО Строки.Ссылка = Операция.Ссылка
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиНМА)
	|	И Строки.НематериальныйАктив.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив)";
	
КонецФункции

Функция АмортизацияНМАЦФ()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Целевые средства в накопленной амортизации НМА (Дт 000:: Кт СчетАмортизации)
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.НакопленнаяАмортизацияБУЦФ КАК Сумма,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Вспомогательный) КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	Строки.НематериальныйАктив.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	
	|	Строки.СчетАмортизацииЦФ КАК СчетКт,
	|	Строки.НематериальныйАктив КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.НакопленнаяАмортизацияНУЦФ КАК СуммаНУКт,
	|	Строки.НакопленнаяАмортизацияПРЦФ КАК СуммаПРКт,
	|	Строки.НакопленнаяАмортизацияБУЦФ - Строки.НакопленнаяАмортизацияНУЦФ - Строки.НакопленнаяАмортизацияПРЦФ КАК СуммаВРКт,
	|	""Целевые средства в накопленной амортизации НМА"" КАК Содержание
	|ИЗ
	|	Документ.ВводОстатковВнеоборотныхАктивов КАК Операция
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов.НМА КАК Строки
	|	ПО Строки.Ссылка = Операция.Ссылка
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Строки.ПрименениеЦелевогоФинансирования
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиНМА)
	|	И Строки.НематериальныйАктив.ВидОбъектаУчета = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовУчетаНМА.НематериальныйАктив)";
	
КонецФункции

Функция ЗабалансовыйУчетАрендованныхОС()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Поступление на забалансовый учет арендуемых ОС (Дт СчетЗабалансовогоУчета:: Кт ---)
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.ПервоначальнаяСтоимостьБУ КАК Сумма,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	Строки.СчетУчета КАК СчетДт,
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	Строки.Контрагент КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Поступление на забалансовый учет арендуемых ОС"" КАК Содержание
	|ИЗ
	|	Документ.ВводОстатковВнеоборотныхАктивов КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов.ОС КАК Строки
	|		ПО Строки.Ссылка = Операция.Ссылка
	|	
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиАрендованныхОС),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПредметовЛизингаЗаБалансом)
	|	)
	|";
	
КонецФункции

Функция АмортизационнаяПремияДтКВ()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Амортизационная премия (Дт КВ :: Кт <ПустаяСсылка>)
	|//
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Строки.ДатаПринятияКУчету КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	0 КАК Сумма,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасходыНаКапитальныеВложения) КАК СчетДт,
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	&Ссылка КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СуммаКапитальныхВложенийВключаемыхВРасходыНУ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Амортизационная премия"" КАК Содержание
	|ИЗ
	|	Документ.ВводОстатковВнеоборотныхАктивов КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов.ОС КАК Строки
	|	ПО Строки.Ссылка = Операция.Ссылка
	|	
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиОС),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПереданныхВАрендуОС),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПредметовЛизингаНаБалансе),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПереданныхВАрендуПредметовЛизингаНаБалансе)
	|	)
	|	И Строки.ВключитьАмортизационнуюПремиюВСоставРасходов
	|";
	
КонецФункции

Функция РасчетыПоДоговорамЛизинга()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Зачет обеспечительного платежа
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.СуммаРегл КАК Сумма,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	Строки.Договор.ВалютаВзаиморасчетов КАК ВалютаДт,
	|	Строки.Договор.Подразделение КАК ПодразделениеДт,
	|	Строки.Договор.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	Строки.СчетУчета КАК СчетДт,
	|	Строки.Договор КАК СубконтоДт1,
	|	Строки.Контрагент КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СуммаРегл КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Вспомогательный) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	Строки.Сумма КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Расчеты по договорам лизинга"" КАК Содержание
	|ИЗ
	|	Документ.ВводОстатковВнеоборотныхАктивов КАК Операция
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов.РасчетыПоДоговорамЛизинга КАК Строки
	|	ПО Строки.Ссылка = Операция.Ссылка
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиВзаиморасчетовПоДоговорамЛизинга)
	|	)
	|	И Строки.СальдоДебетовое
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.СуммаРегл КАК Сумма,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Вспомогательный) КАК СчетДт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	Строки.Договор.ВалютаВзаиморасчетов КАК ВалютаКт,
	|	Строки.Договор.Подразделение КАК ПодразделениеКт,
	|	Строки.Договор.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	Строки.СчетУчета КАК СчетКт,
	|	Строки.Договор КАК СубконтоКт1,
	|	Строки.Контрагент КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СуммаРегл КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Расчеты по договорам лизинга"" КАК Содержание
	|ИЗ
	|	Документ.ВводОстатковВнеоборотныхАктивов КАК Операция
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов.РасчетыПоДоговорамЛизинга КАК Строки
	|	ПО Строки.Ссылка = Операция.Ссылка
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиВзаиморасчетовПоДоговорамЛизинга)
	|	)
	|	И НЕ Строки.СальдоДебетовое";
	
КонецФункции

Функция ЗабалансовыйУчетПереданныхАрендуБалансовыхОС()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Поступление на забалансовый учет переданных в аренду ОС (Дт СчетЗабалансовогоУчета:: Кт ---)
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Строки.Контрагент КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.ЗалоговаяСтоимость КАК Сумма,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.ПодразделениеАрендатора КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|
	|	Строки.СчетУчетаАрендатора КАК СчетДт,
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	Операция.Организация КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Поступление на забалансовый учет арендуемых ОС"" КАК Содержание
	|ИЗ
	|	Документ.ВводОстатковВнеоборотныхАктивов КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов.ОС КАК Строки
	|		ПО Строки.Ссылка = Операция.Ссылка
	|	
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПереданныхВАрендуОС),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПереданныхВАрендуПредметовЛизингаНаБалансе)
	|	)
	|";
	
КонецФункции

#КонецОбласти

#Область ОбработчикиОбновления

Процедура ЗарегистироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыСубконто1.Ссылка
	|ПОМЕСТИТЬ втСчетаУчетаПереданныхВАрендуОС
	|ИЗ
	|	ПланСчетов.Хозрасчетный.ВидыСубконто КАК ВидыСубконто1
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ВидыСубконто2
	|		ПО ВидыСубконто1.Ссылка = ВидыСубконто2.Ссылка
	|			И (ВидыСубконто2.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства))
	|			И (ВидыСубконто2.НомерСтроки = 2)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ВидыСубконто3
	|		ПО (ВидыСубконто1.Ссылка = ВидыСубконто3.Ссылка)
	|			И (ВидыСубконто3.НомерСтроки = 3)
	|ГДЕ
	|	ВидыСубконто1.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОсновныеСредства), ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДоходныеВложенияВ_МЦ))
	|	И ВидыСубконто1.НомерСтроки = 1
	|	И ВидыСубконто1.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты)
	|	И ВидыСубконто3.Ссылка ЕСТЬ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВводОстатков.Ссылка КАК Ссылка,
	|	СУММА(ВЫБОР
	|			КОГДА втСчетаУчетаПереданныхВАрендуОС.Ссылка ЕСТЬ NULL 
	|				ТОГДА 0
	|			ИНАЧЕ 1
	|		КОНЕЦ) КАК ПереданоВАренду
	|ПОМЕСТИТЬ втРезультаты
	|ИЗ
	|	Документ.ВводОстатковВнеоборотныхАктивов КАК ВводОстатков
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов.ОС КАК ВводОстатковТабличнаяЧасть
	|		ПО (ВводОстатковТабличнаяЧасть.Ссылка = ВводОстатков.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСчетаУчетаПереданныхВАрендуОС КАК втСчетаУчетаПереданныхВАрендуОС
	|		ПО (ВводОстатковТабличнаяЧасть.СчетУчета = втСчетаУчетаПереданныхВАрендуОС.Ссылка)
	|ГДЕ
	|	ВводОстатков.Проведен
	|	И НЕ ВводОстатков.ПометкаУдаления
	|	И ВводОстатков.ТипОперации В (ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиОС), ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиПредметовЛизингаНаБалансе))
	|
	|СГРУППИРОВАТЬ ПО
	|	ВводОстатков.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втРезультаты.Ссылка
	|ИЗ
	|	втРезультаты КАК втРезультаты
	|ГДЕ
	|	втРезультаты.ПереданоВАренду > 0";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "Документ.ВводОстатковВнеоборотныхАктивов";
	МетаданныеОбъекта = Метаданные.Документы.ВводОстатковВнеоборотныхАктивов;
	
	МВТ = Новый МенеджерВременныхТаблиц;
	
	ПараметрыВыборкиДляОбработки = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ПараметрыВыборкиДляОбработки.ИмяВременнойТаблицы = "ВТСсылокДляОбработки";
	Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуСсылокДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта, МВТ, ПараметрыВыборкиДляОбработки);
	Если Не Результат.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Если Не Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТ;
	#Область ТекстЗапроса
	Запрос.Текст=
	"ВЫБРАТЬ
	|	ВидыСубконто1.Ссылка
	|ПОМЕСТИТЬ втСчетаУчетаПереданныхВАрендуОС
	|ИЗ
	|	ПланСчетов.Хозрасчетный.ВидыСубконто КАК ВидыСубконто1
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ВидыСубконто2
	|		ПО ВидыСубконто1.Ссылка = ВидыСубконто2.Ссылка
	|			И (ВидыСубконто2.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства))
	|			И (ВидыСубконто2.НомерСтроки = 2)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ВидыСубконто3
	|		ПО ВидыСубконто1.Ссылка = ВидыСубконто3.Ссылка
	|			И (ВидыСубконто3.НомерСтроки = 3)
	|ГДЕ
	|	ВидыСубконто1.Ссылка В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОсновныеСредства), ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ДоходныеВложенияВ_МЦ))
	|	И ВидыСубконто1.НомерСтроки = 1
	|	И ВидыСубконто1.ВидСубконто = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты)
	|	И ВидыСубконто3.Ссылка ЕСТЬ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧастьОС.Ссылка КАК Ссылка,
	|	ТабличнаяЧастьОС.НомерСтроки - 1 КАК ИндексСтроки,
	|	ВЫБОР
	|		КОГДА втСчетаУчетаПереданныхВАрендуОС.Ссылка ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ПереданоВАренду
	|ПОМЕСТИТЬ втТабличныеЧасти
	|ИЗ
	|	Документ.ВводОстатковВнеоборотныхАктивов.ОС КАК ТабличнаяЧастьОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСсылокДляОбработки КАК ВТСсылокДляОбработки
	|		ПО ТабличнаяЧастьОС.Ссылка = ВТСсылокДляОбработки.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСчетаУчетаПереданныхВАрендуОС КАК втСчетаУчетаПереданныхВАрендуОС
	|		ПО ТабличнаяЧастьОС.СчетУчета = втСчетаУчетаПереданныхВАрендуОС.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТабличныеЧасти.Ссылка,
	|	КОЛИЧЕСТВО(втТабличныеЧасти.ИндексСтроки) КАК КоличествоСтрок,
	|	СУММА(ВЫБОР
	|			КОГДА втТабличныеЧасти.ПереданоВАренду
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоПереданоВАренду
	|ПОМЕСТИТЬ втТабличныеЧастиСгруппированные
	|ИЗ
	|	втТабличныеЧасти КАК втТабличныеЧасти
	|
	|СГРУППИРОВАТЬ ПО
	|	втТабличныеЧасти.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСсылокДляОбработки.Ссылка,
	|	(втТабличныеЧастиСгруппированные.КоличествоСтрок = втТабличныеЧастиСгруппированные.КоличествоПереданоВАренду) КАК ТребуетсяТолькоПоменятьТипОперации
	|ИЗ
	|	ВТСсылокДляОбработки КАК ВТСсылокДляОбработки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТабличныеЧастиСгруппированные КАК втТабличныеЧастиСгруппированные
	|		ПО ВТСсылокДляОбработки.Ссылка = втТабличныеЧастиСгруппированные.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втТабличныеЧасти.Ссылка,
	|	втТабличныеЧасти.ИндексСтроки,
	|	втТабличныеЧасти.ПереданоВАренду
	|ИЗ
	|	втТабличныеЧасти КАК втТабличныеЧасти";
	#КонецОбласти
	
	Пакет = Запрос.ВыполнитьПакет();
	
	Выборка = Пакет[Пакет.Количество()-2].Выбрать();
	ВыборкаСтрок = Пакет[Пакет.Количество()-1].Выбрать();
	СтруктураПоискаСтрок = Новый Структура("Ссылка", Неопределено);
	
	ОбработаноОбъектов = 0;
	ПроблемныхОбъектов = 0;
	
	СоответствиеОпераций = Новый Соответствие;
	СоответствиеОпераций.Вставить(
		Перечисления.ТипыОперацийВводаОстатков.ОстаткиОС,
		Перечисления.ТипыОперацийВводаОстатков.ОстаткиПереданныхВАрендуОС);
	СоответствиеОпераций.Вставить(
		Перечисления.ТипыОперацийВводаОстатков.ОстаткиПредметовЛизингаЗаБалансом,
		Перечисления.ТипыОперацийВводаОстатков.ОстаткиПереданныхВАрендуПредметовЛизингаНаБалансе);
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			Блокировка.Заблокировать();
		Исключение
			ОтменитьТранзакцию();
			ТекстСообщения = НСтр("ru = 'Не удалось заблокировать ввод остатков ОС: %1 по причине: %2'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.Ссылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				МетаданныеОбъекта,
				Выборка.Ссылка,
				ТекстСообщения);
			Продолжить;
		КонецПопытки;
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Если Выборка.ТребуетсяТолькоПоменятьТипОперации Тогда
			ДокументОбъект.ТипОперации = СоответствиеОпераций.Получить(ДокументОбъект.ТипОперации);
		Иначе
			ТаблицаДокумента = ДокументОбъект.ОС.Выгрузить();
			ДокументОбъект.ОС.Очистить();
			
			ДокументОбъектНовый = Документы.ВводОстатковВнеоборотныхАктивов.СоздатьДокумент();
			ЗаполнитьЗначенияСвойств(ДокументОбъектНовый, ДокументОбъект);
			ДокументОбъектНовый.ТипОперации = СоответствиеОпераций.Получить(ДокументОбъект.ТипОперации);
			
			ВыборкаСтрок.Сбросить();
			ЗаполнитьЗначенияСвойств(СтруктураПоискаСтрок, Выборка);
			Пока ВыборкаСтрок.НайтиСледующий(СтруктураПоискаСтрок) Цикл
				СтрокаТаблицы = ТаблицаДокумента[ВыборкаСтрок.ИндексСтроки];
				Если ВыборкаСтрок.ПереданоВАренду Тогда
					ЗаполнитьЗначенияСвойств(ДокументОбъектНовый.ОС.Добавить(), СтрокаТаблицы);
				Иначе
					ЗаполнитьЗначенияСвойств(ДокументОбъект.ОС.Добавить(), СтрокаТаблицы);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		ОбработаноОбъектов = ОбработаноОбъектов + 1;
		
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект, Истина, Ложь, РежимЗаписиДокумента.Проведение);
			Если Не Выборка.ТребуетсяТолькоПоменятьТипОперации Тогда
				ДокументОбъектНовый.Записать(РежимЗаписиДокумента.Проведение);
			КонецЕсли;
			ЗафиксироватьТранзакцию();
		Исключение
			
			ОбработаноОбъектов = ОбработаноОбъектов - 1;
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ТекстСообщения = НСтр("ru = 'Не удалось поменять тип операции или разделить по типам операций документ ввода остатков ОС""%1"" по причине: %2'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.Ссылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				МетаданныеОбъекта,
				Выборка.Ссылка,
				ТекстСообщения);
			ОтменитьТранзакцию();
			
			Продолжить;
			
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбработаноОбъектов = 0 И ПроблемныхОбъектов > 0 Тогда
		
		ТекстСообщения = НСтр("ru = 'Не выполнено изменение типа операции или разеление по типам операций некоторых документов ввода остатков (пропущены): %1'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
		
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#Область СтандартныеКоманды

// Заполняет список команд создания на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании) Экспорт
	
	Команда = Документы.ИзменениеПараметровОС.ДобавитьКомандуСоздатьНаОсновании(КомандыСоздатьНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.СписокФорм = "ФормаОсновныеСредства";
	КонецЕсли;
	
КонецПроцедуры

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов) Экспорт
	
	КомандаОтчет = ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуДвиженияДокумента(КомандыОтчетов);
	
	Если КомандаОтчет <> Неопределено Тогда
		КомандаОтчет.СписокФорм = "ФормаСписка, ФормаОсновныеСредства, ФормаНематериальныеАктивы, ФормаАрендованныеОсновныеСредства, ФормаРасчетыПоДоговорамЛизинга";
	КонецЕсли;
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли