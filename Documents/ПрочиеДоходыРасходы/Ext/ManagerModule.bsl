#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет список команд создания на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании) Экспорт



КонецПроцедуры

// Заполняет список команд создания на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании
//
// Возвращаемое значение:
//	 КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСоздатьНаОсновании) Экспорт

	Если ПравоДоступа("Добавление", Метаданные.Документы.ПрочиеДоходыРасходы) Тогда
		КомандаСоздатьНаОсновании = КомандыСоздатьНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Идентификатор = Метаданные.Документы.ПрочиеДоходыРасходы.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ВводНаОсновании.ПредставлениеОбъекта(Метаданные.Документы.ПрочиеДоходыРасходы);
		КомандаСоздатьНаОсновании.ПроверкаПроведенияПередСозданиемНаОсновании = Истина;
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьУчетПрочихДоходовРасходов";
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;

КонецФункции

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов) Экспорт

	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);

	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуДвиженияДокумента(КомандыОтчетов);

КонецПроцедуры

// Заполняет таблицу реквизитов, зависимых от хозяйственной операции
//
// Параметры:
//	ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - хозяйственная операция соглашения
//	МассивВсехРеквизитов - Массив - реквизиты, которые не зависят от хозяйственной операции
//	МассивРеквизитовОперации - Массив - реквизиты, которые зависят от хозяйственной операции
//
Процедура ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(ХозяйственнаяОперация, МассивВсехРеквизитов, МассивРеквизитовОперации) Экспорт
	
	МассивВсехРеквизитов = Новый Массив;
	МассивВсехРеквизитов.Добавить("Валюта");
	
	МассивВсехРеквизитов.Добавить("ПрочиеРасходы.КорСтатьяРасходов");
	МассивВсехРеквизитов.Добавить("ПрочиеРасходы.КорАналитикаРасходов");
	МассивВсехРеквизитов.Добавить("ПрочиеРасходы.СтатьяАктивовПассивов");
	МассивВсехРеквизитов.Добавить("ПрочиеРасходы.АналитикаАктивовПассивов");
	МассивВсехРеквизитов.Добавить("ПрочиеРасходы.ПредставлениеОтраженияВРеглУчете");
	
	МассивВсехРеквизитов.Добавить("ПрочиеДоходы.КорСтатьяДоходов");
	МассивВсехРеквизитов.Добавить("ПрочиеДоходы.КорАналитикаДоходов");
	МассивВсехРеквизитов.Добавить("ПрочиеДоходы.СтатьяАктивовПассивов");
	МассивВсехРеквизитов.Добавить("ПрочиеДоходы.АналитикаАктивовПассивов");
	МассивВсехРеквизитов.Добавить("ПрочиеДоходы.ПредставлениеОтраженияВРеглУчете");
	
	МассивРеквизитовОперации = Новый Массив;
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочиеРасходы Тогда
		МассивРеквизитовОперации.Добавить("Валюта");
		МассивРеквизитовОперации.Добавить("ПрочиеРасходы.СтатьяАктивовПассивов");
		МассивРеквизитовОперации.Добавить("ПрочиеРасходы.АналитикаАктивовПассивов");
		МассивРеквизитовОперации.Добавить("ПрочиеРасходы.ПредставлениеОтраженияВРеглУчете");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеПрочихРасходов
			ИЛИ ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СторнированиеПрочихРасходов Тогда
		МассивРеквизитовОперации.Добавить("ПрочиеРасходы.СтатьяАктивовПассивов");
		МассивРеквизитовОперации.Добавить("ПрочиеРасходы.АналитикаАктивовПассивов");
		МассивРеквизитовОперации.Добавить("ПрочиеРасходы.ПредставлениеОтраженияВРеглУчете");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеклассификацияРасходов Тогда
		МассивРеквизитовОперации.Добавить("ПрочиеРасходы.КорСтатьяРасходов");
		МассивРеквизитовОперации.Добавить("ПрочиеРасходы.КорАналитикаРасходов");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПрочиеДоходы Тогда
		МассивРеквизитовОперации.Добавить("Валюта");
		МассивРеквизитовОперации.Добавить("ПрочиеДоходы.СтатьяАктивовПассивов");
		МассивРеквизитовОперации.Добавить("ПрочиеДоходы.АналитикаАктивовПассивов");
		МассивРеквизитовОперации.Добавить("ПрочиеДоходы.ПредставлениеОтраженияВРеглУчете");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СторнированиеПрочихДоходов Тогда
		МассивРеквизитовОперации.Добавить("ПрочиеДоходы.СтатьяАктивовПассивов");
		МассивРеквизитовОперации.Добавить("ПрочиеДоходы.АналитикаАктивовПассивов");
		МассивРеквизитовОперации.Добавить("ПрочиеДоходы.ПредставлениеОтраженияВРеглУчете");
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеклассификацияДоходов Тогда
		МассивРеквизитовОперации.Добавить("ПрочиеДоходы.КорСтатьяДоходов");
		МассивРеквизитовОперации.Добавить("ПрочиеДоходы.КорАналитикаДоходов");
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняет таблицу остатков расходов для подбора
//
// Параметры:
//	ДанныеОтбора			 - Структура - поля, по которым будут отобраны остатки для заполнения документа
//		ДанныеОтбора.Дата		 - Дата - дата среза, на которую будут получены остатки
//		ДанныеОтбора.Валюта		 - СправочникСсылка.Валюта - валюта документа
//		ДанныеОтбора.Ссылка		 - ДокументСсылка.РаспределениеРасходовНаПрочиеАктивы, Неопределено - документ, который необходимо обновить
//		ДанныеОтбора.Организация - СправочникСсылка.Организации, Неопределено - Организация, по которой будет осуществляться отбор остатков
//	Расходы					 - ДанныеФормыКоллекция - таблица расходов, в которую будут помещены данные
//
Процедура ПолучитьОстаткиПрочихРасходов(ДанныеОтбора, Расходы) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	// Параметры отбора
	Запрос.УстановитьПараметр("Ссылка",			ДанныеОтбора.Ссылка);
	Запрос.УстановитьПараметр("Организация",	ДанныеОтбора.Организация);
	Запрос.УстановитьПараметр("Граница",		Новый Граница(КонецДня(ДанныеОтбора.Дата), ВидГраницы.Включая));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.Подразделение КАК Подразделение,
	|	ВложенныйЗапрос.СтатьяРасходов КАК СтатьяРасходов,
	|	ВложенныйЗапрос.АналитикаРасходов КАК АналитикаРасходов,
	|	СУММА(ВложенныйЗапрос.Сумма) КАК Сумма,
	|	СУММА(ВложенныйЗапрос.СуммаБезНДС) КАК СуммаБезНДС,
	|	СУММА(ВложенныйЗапрос.СуммаРегл) КАК СуммаРегл,
	|	СУММА(ВложенныйЗапрос.ПостояннаяРазница) КАК ПостояннаяРазница,
	|	СУММА(ВложенныйЗапрос.ВременнаяРазница) КАК ВременнаяРазница
	|ИЗ
	|	(ВЫБРАТЬ
	|		Регистр.Организация КАК Организация,
	|		Регистр.Подразделение КАК Подразделение,
	|		Регистр.СтатьяРасходов КАК СтатьяРасходов,
	|		Регистр.АналитикаРасходов КАК АналитикаРасходов,
	|		Регистр.СуммаОстаток КАК Сумма,
	|		Регистр.СуммаБезНДСОстаток КАК СуммаБезНДС,
	|		Регистр.СуммаРеглОстаток КАК СуммаРегл,
	|		Регистр.ПостояннаяРазницаОстаток КАК ПостояннаяРазница,
	|		Регистр.ВременнаяРазницаОстаток КАК ВременнаяРазница
	|	ИЗ
	|		РегистрНакопления.ПрочиеРасходы.Остатки(&Граница, Организация = &Организация) КАК Регистр
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Регистр.Организация,
	|		Регистр.Подразделение,
	|		Регистр.СтатьяРасходов,
	|		Регистр.АналитикаРасходов,
	|		ВЫБОР
	|			КОГДА Регистр.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА Регистр.Сумма
	|			ИНАЧЕ -Регистр.Сумма
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА Регистр.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА Регистр.СуммаБезНДС
	|			ИНАЧЕ -Регистр.СуммаБезНДС
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА Регистр.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА Регистр.СуммаРегл
	|			ИНАЧЕ -Регистр.СуммаРегл
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА Регистр.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА Регистр.ПостояннаяРазница
	|			ИНАЧЕ -Регистр.ПостояннаяРазница
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА Регистр.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА Регистр.ВременнаяРазница
	|			ИНАЧЕ -Регистр.ВременнаяРазница
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.ПрочиеРасходы КАК Регистр
	|	ГДЕ
	|		Регистр.Регистратор = &Ссылка) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.Подразделение,
	|	ВложенныйЗапрос.СтатьяРасходов,
	|	ВложенныйЗапрос.АналитикаРасходов
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВложенныйЗапрос.Сумма) > 0
	|		ИЛИ СУММА(ВложенныйЗапрос.СуммаБезНДС) > 0
	|		ИЛИ СУММА(ВложенныйЗапрос.СуммаРегл) > 0
	|		ИЛИ СУММА(ВложенныйЗапрос.ПостояннаяРазница) > 0
	|		ИЛИ СУММА(ВложенныйЗапрос.ВременнаяРазница) > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация,
	|	Подразделение,
	|	СтатьяРасходов,
	|	АналитикаРасходов";
	
	ВыборкаРасходы = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаРасходы.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Расходы.Добавить(), ВыборкаРасходы);
	КонецЦикла;
	
КонецПроцедуры

//++ НЕ УТ

// Возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт

#Область РегистрацияПрочихРасходов
	ТекстРегистрацияПрочихРасходов = "
	|ВЫБРАТЬ // Прочие расходы (Дт <25, 26, 44, 20, 23> :: Кт <разные>)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	ВЫБОР КОГДА РАЗНОСТЬДАТ(Строки.ДатаОтражения, Операция.Дата, ДЕНЬ) = 0
	|				ИЛИ Строки.ДатаОтражения = ДАТАВРЕМЯ(1, 1, 1) ТОГДА
	|		Операция.Дата
	|	ИНАЧЕ
	|		Строки.ДатаОтражения
	|	КОНЕЦ КАК Период,
	|	Операция.Организация КАК Организация,
	|	Строки.НомерСтроки КАК ИдентификаторСтроки,
	|	
	|	Строки.СуммаРегл КАК Сумма,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Строки.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.СтатьяРасходов КАК СубконтоДт1,
	|	Строки.АналитикаРасходов КАК СубконтоДт2, 
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА 
	|			ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный) 
	|		ИНАЧЕ 
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) 
	|	КОНЕЦ КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	(Строки.СуммаРегл - Строки.ПостояннаяРазница - Строки.ВременнаяРазница) КАК СуммаНУДт,
	|	Строки.ПостояннаяРазница КАК СуммаПРДт,
	|	Строки.ВременнаяРазница КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	Операция.Валюта КАК ВалютаКт,
	|	ВЫБОР 
	|		КОГДА Строки.КорПодразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ТОГДА Строки.КорПодразделение
	|		ИНАЧЕ Строки.Подразделение 
	|	КОНЕЦ КАК ПодразделениеКт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	Строки.СчетУчета КАК СчетКт,
	|	Строки.Субконто1 КАК СубконтоКт1,
	|	Строки.Субконто2 КАК СубконтоКт2,
	|	Строки.Субконто3 КАК СубконтоКт3,
	|
	|	Строки.Сумма КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	(Строки.СуммаРегл - Строки.ПостояннаяРазница - Строки.ВременнаяРазница) КАК СуммаНУКт,
	|	Строки.ПостояннаяРазница КАК СуммаПРКт,
	|	Строки.ВременнаяРазница КАК СуммаВРКт,
	|	""Прочие расходы"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы.ПрочиеРасходы КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СуммыДокументовВВалютеРегл КАК Суммы
	|	ПО
	|		Суммы.Регистратор = Строки.Ссылка
	|		И Суммы.ИдентификаторСтроки = Строки.ИдентификаторСтроки
	|		И Суммы.СуммаБезНДСРегл <> 0
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО
	|		Строки.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|	
	|ГДЕ
	|	Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеРасходы)
	|
	|";
#КонецОбласти

#Область СписаниеПрочихРасходов
	ТекстСписаниеПрочихРасходов = "
	|ВЫБРАТЬ // Списание прочих расходов (Дт <разные> :: Дт <25, 26, 44, 20, 23>)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	ВЫБОР КОГДА РАЗНОСТЬДАТ(Строки.ДатаОтражения, Операция.Дата, ДЕНЬ) = 0
	|				ИЛИ Строки.ДатаОтражения = ДАТАВРЕМЯ(1, 1, 1) ТОГДА
	|		Операция.Дата
	|	ИНАЧЕ
	|		Строки.ДатаОтражения
	|	КОНЕЦ КАК Период,
	|	Операция.Организация КАК Организация,
	|	Строки.НомерСтроки КАК ИдентификаторСтроки,
	|	
	|	Строки.СуммаРегл КАК Сумма,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	ВЫБОР 
	|		КОГДА Строки.КорПодразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) 
	|			ТОГДА Строки.КорПодразделение
	|		ИНАЧЕ Строки.Подразделение
	|	КОНЕЦ КАК ПодразделениеДт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	Строки.СчетУчета КАК СчетДт,
	|	Строки.Субконто1 КАК СубконтоДт1,
	|	Строки.Субконто2 КАК СубконтоДт2,
	|	Строки.Субконто3 КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	(Строки.СуммаРегл - Строки.ПостояннаяРазница - Строки.ВременнаяРазница) КАК СуммаНУДт,
	|	Строки.ПостояннаяРазница КАК СуммаПРДт,
	|	Строки.ВременнаяРазница КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаКт,
	|	Строки.Подразделение КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|	
	|	Строки.СтатьяРасходов КАК СубконтоКт1,
	|	Строки.АналитикаРасходов КАК СубконтоКт2, 
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА 
	|			ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный) 
	|		ИНАЧЕ 
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) 
	|	КОНЕЦ КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	(ВЫБОР КОГДА НЕ Строки.СтатьяРасходов.ПринятиеКНалоговомуУчету
	|		ТОГДА 0
	|		ИНАЧЕ Строки.СуммаРегл - Строки.ПостояннаяРазница - Строки.ВременнаяРазница КОНЕЦ) КАК СуммаНУКт,
	|	(ВЫБОР КОГДА НЕ Строки.СтатьяРасходов.ПринятиеКНалоговомуУчету
	|		ТОГДА Строки.СуммаРегл
	|		ИНАЧЕ Строки.ПостояннаяРазница КОНЕЦ) КАК СуммаПРКт,
	|	Строки.ВременнаяРазница КАК СуммаВРКт,
	|	""Списание прочих расходов"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы.ПрочиеРасходы КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО
	|		Строки.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|	
	|ГДЕ
	|	Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПрочихРасходов)
	|	И (Строки.СуммаРегл <> 0 ИЛИ Строки.ВременнаяРазница <> 0 ИЛИ Строки.ПостояннаяРазница <> 0)
	|
	|";
#КонецОбласти

#Область СторнированиеПрочихРасходов
	ТекстСторнированиеПрочихРасходов = "
	|ВЫБРАТЬ // Прочие расходы (сторно) (Дт <25, 26, 44, 20, 23> :: Кт <разные>)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	ВЫБОР КОГДА РАЗНОСТЬДАТ(Строки.ДатаОтражения, Операция.Дата, ДЕНЬ) = 0
	|				ИЛИ Строки.ДатаОтражения = ДАТАВРЕМЯ(1, 1, 1) ТОГДА
	|		Операция.Дата
	|	ИНАЧЕ
	|		Строки.ДатаОтражения
	|	КОНЕЦ КАК Период,
	|	Операция.Организация КАК Организация,
	|	Строки.НомерСтроки КАК ИдентификаторСтроки,
	|	
	|	-Строки.СуммаРегл КАК Сумма,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Строки.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.СтатьяРасходов КАК СубконтоДт1, 
	|	Строки.АналитикаРасходов КАК СубконтоДт2, 
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА 
	|			ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный) 
	|		ИНАЧЕ 
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) 
	|	КОНЕЦ КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	-(Строки.СуммаРегл - Строки.ПостояннаяРазница - Строки.ВременнаяРазница) КАК СуммаНУДт,
	|	-Строки.ПостояннаяРазница КАК СуммаПРДт,
	|	-Строки.ВременнаяРазница КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	ВЫБОР 
	|		КОГДА Строки.КорПодразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) 
	|			ТОГДА Строки.КорПодразделение
	|		ИНАЧЕ Строки.Подразделение
	|	КОНЕЦ КАК ПодразделениеКт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	Строки.СчетУчета КАК СчетКт,
	|	Строки.Субконто1 КАК СубконтоКт1,
	|	Строки.Субконто2 КАК СубконтоКт2,
	|	Строки.Субконто3 КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	-(Строки.СуммаРегл - Строки.ПостояннаяРазница - Строки.ВременнаяРазница) КАК СуммаНУКт,
	|	-Строки.ПостояннаяРазница КАК СуммаПРКт,
	|	-Строки.ВременнаяРазница КАК СуммаВРКт,
	|	""Прочих расходы (сторно)"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы.ПрочиеРасходы КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СуммыДокументовВВалютеРегл КАК Суммы
	|	ПО
	|		Суммы.Регистратор = Строки.Ссылка
	|		И Суммы.ИдентификаторСтроки = Строки.ИдентификаторСтроки
	|		И Суммы.СуммаБезНДСРегл <> 0
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО
	|		Строки.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|	
	|ГДЕ
	|	Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторнированиеПрочихРасходов)
	|
	|";
#КонецОбласти

#Область РегистрацияПрочихДоходов
	ТекстРегистрацияПрочихДоходов = "
	|ВЫБРАТЬ // Прочие доходы (Дт <разные>:: Кт <91.01>)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	ВЫБОР КОГДА РАЗНОСТЬДАТ(Строки.ДатаОтражения, Операция.Дата, ДЕНЬ) = 0
	|				ИЛИ Строки.ДатаОтражения = ДАТАВРЕМЯ(1, 1, 1) ТОГДА
	|		Операция.Дата
	|	ИНАЧЕ
	|		Строки.ДатаОтражения
	|	КОНЕЦ КАК Период,
	|	Операция.Организация КАК Организация,
	|	Строки.НомерСтроки КАК ИдентификаторСтроки,
	|	
	|	Строки.СуммаРегл КАК Сумма,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	Строки.Подразделение КАК МестоУчетаДт,
	|
	|	Операция.Валюта КАК ВалютаДт,
	|	ВЫБОР 
	|		КОГДА Строки.КорПодразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) 
	|			ТОГДА Строки.КорПодразделение
	|		ИНАЧЕ Строки.Подразделение
	|	КОНЕЦ КАК ПодразделениеДт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	Строки.СчетУчета КАК СчетДт,
	|
	|	Строки.Субконто1 КАК СубконтоДт1,
	|	Строки.Субконто2 КАК СубконтоДт2,
	|	Строки.Субконто3 КАК СубконтоДт3,
	|	
	|	Строки.Сумма КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	(Строки.СуммаРегл - Строки.ПостояннаяРазница - Строки.ВременнаяРазница) КАК СуммаНУДт,
	|	Строки.ПостояннаяРазница КАК СуммаПРДт,
	|	Строки.ВременнаяРазница КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы) КАК ВидСчетаКт,
	|	Строки.СтатьяДоходов КАК АналитикаУчетаКт,
	|	Строки.Подразделение КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	(Строки.СуммаРегл - Строки.ПостояннаяРазница - Строки.ВременнаяРазница) КАК СуммаНУКт,
	|	Строки.ПостояннаяРазница КАК СуммаПРКт,
	|	Строки.ВременнаяРазница КАК СуммаВРКт,
	|	""Прочие доходы"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы.ПрочиеДоходы КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СуммыДокументовВВалютеРегл КАК Суммы
	|	ПО
	|		Суммы.Регистратор = Строки.Ссылка
	|		И Суммы.ИдентификаторСтроки = Строки.ИдентификаторСтроки
	|		И Суммы.СуммаБезНДСРегл <> 0
	|ГДЕ
	|	Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеДоходы)
	|
	|";
#КонецОбласти

#Область СторнированиеПрочихДоходов
	ТекстСторнированиеПрочихДоходов = "
	|ВЫБРАТЬ // Прочие доходы (сторно) (Дт <разные>:: Кт <91.01>)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	ВЫБОР КОГДА РАЗНОСТЬДАТ(Строки.ДатаОтражения, Операция.Дата, ДЕНЬ) = 0
	|				ИЛИ Строки.ДатаОтражения = ДАТАВРЕМЯ(1, 1, 1) ТОГДА
	|		Операция.Дата
	|	ИНАЧЕ
	|		Строки.ДатаОтражения
	|	КОНЕЦ КАК Период,
	|	Операция.Организация КАК Организация,
	|	Строки.НомерСтроки КАК ИдентификаторСтроки,
	|	
	|	-Строки.СуммаРегл КАК Сумма,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	Строки.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	ВЫБОР 
	|		КОГДА Строки.КорПодразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) 
	|			ТОГДА Строки.КорПодразделение
	|		ИНАЧЕ Строки.Подразделение
	|	КОНЕЦ КАК ПодразделениеДт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	Строки.СчетУчета КАК СчетДт,
	|
	|	Строки.Субконто1 КАК СубконтоДт1,
	|	Строки.Субконто2 КАК СубконтоДт2,
	|	Строки.Субконто3 КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	-(Строки.СуммаРегл - Строки.ПостояннаяРазница - Строки.ВременнаяРазница) КАК СуммаНУДт,
	|	-Строки.ПостояннаяРазница КАК СуммаПРДт,
	|	-Строки.ВременнаяРазница КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы) КАК ВидСчетаКт,
	|	Строки.СтатьяДоходов КАК АналитикаУчетаКт,
	|	Строки.Подразделение КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	-(Строки.СуммаРегл - Строки.ПостояннаяРазница - Строки.ВременнаяРазница) КАК СуммаНУКт,
	|	-Строки.ПостояннаяРазница КАК СуммаПРКт,
	|	-Строки.ВременнаяРазница КАК СуммаВРКт,
	|	""Прочие доходы (сторно)"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы.ПрочиеДоходы КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СуммыДокументовВВалютеРегл КАК Суммы
	|	ПО
	|		Суммы.Регистратор = Строки.Ссылка
	|		И Суммы.ИдентификаторСтроки = Строки.ИдентификаторСтроки
	|		И Суммы.СуммаБезНДСРегл <> 0
	|ГДЕ
	|	Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторнированиеПрочихДоходов)
	|
	|";
#КонецОбласти

#Область РекласификацияРасходов
	ТекстРеклассификацияРасходов = "
	|ВЫБРАТЬ // Реклассификация расходов (Дт <25, 26, 44, 20, 23> :: Кт <25, 26, 44, 20, 23>)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	ВЫБОР КОГДА РАЗНОСТЬДАТ(Строки.ДатаОтражения, Операция.Дата, ДЕНЬ) = 0
	|				ИЛИ Строки.ДатаОтражения = ДАТАВРЕМЯ(1, 1, 1) ТОГДА
	|		Операция.Дата
	|	ИНАЧЕ
	|		Строки.ДатаОтражения
	|	КОНЕЦ КАК Период,
	|	Операция.Организация КАК Организация,
	|	Строки.НомерСтроки КАК ИдентификаторСтроки,
	|	
	|	Строки.СуммаРегл КАК Сумма,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Строки.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.СтатьяРасходов КАК СубконтоДт1, 
	|	Строки.АналитикаРасходов КАК СубконтоДт2, 
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА 
	|			ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный) 
	|		ИНАЧЕ 
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) 
	|	КОНЕЦ КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	(Строки.СуммаРегл - Строки.ПостояннаяРазница - Строки.ВременнаяРазница) КАК СуммаНУДт,
	|	Строки.ПостояннаяРазница КАК СуммаПРДт,
	|	Строки.ВременнаяРазница КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|	Строки.КорСтатьяРасходов КАК АналитикаУчетаКт,
	|	ВЫБОР 
	|		КОГДА Строки.КорПодразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) 
	|			ТОГДА Строки.КорПодразделение
	|		ИНАЧЕ Строки.Подразделение
	|	КОНЕЦ КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	ВЫБОР 
	|		КОГДА Строки.КорПодразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) 
	|			ТОГДА Строки.КорПодразделение
	|		ИНАЧЕ Строки.Подразделение
	|	КОНЕЦ КАК ПодразделениеКт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	Строки.КорСтатьяРасходов КАК СубконтоКт1,
	|	Строки.КорАналитикаРасходов КАК СубконтоКт2,
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА 
	|			ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный) 
	|		ИНАЧЕ 
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) 
	|	КОНЕЦ КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	(Строки.СуммаРегл - Строки.ПостояннаяРазница - Строки.ВременнаяРазница) КАК СуммаНУКт,
	|	Строки.ПостояннаяРазница КАК СуммаПРКт,
	|	Строки.ВременнаяРазница КАК СуммаВРКт,
	|	""Реклассификация расходов"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы.ПрочиеРасходы КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СуммыДокументовВВалютеРегл КАК Суммы
	|	ПО
	|		Суммы.Регистратор = Строки.Ссылка
	|		И Суммы.ИдентификаторСтроки = Строки.ИдентификаторСтроки
	|		И Суммы.СуммаБезНДСРегл <> 0
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО
	|		Строки.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|	
	|ГДЕ
	|	Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеклассификацияРасходов)
	|
	|";
#КонецОбласти

#Область РеклассификацияДоходов
	ТекстРеклассификацияДоходов = "
	|ВЫБРАТЬ // Реклассификация доходов - сторно (Дт <91.09>:: Кт <91.01>)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	ВЫБОР КОГДА РАЗНОСТЬДАТ(Строки.ДатаОтражения, Операция.Дата, ДЕНЬ) = 0
	|				ИЛИ Строки.ДатаОтражения = ДАТАВРЕМЯ(1, 1, 1) ТОГДА
	|		Операция.Дата
	|	ИНАЧЕ
	|		Строки.ДатаОтражения
	|	КОНЕЦ КАК Период,
	|	Операция.Организация КАК Организация,
	|	Строки.НомерСтроки КАК ИдентификаторСтроки,
	|	
	|	-Строки.СуммаРегл КАК Сумма,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СальдоПрочихДоходовИРасходов) КАК СчетДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы) КАК ВидСчетаКт,
	|	Строки.СтатьяДоходов КАК АналитикаУчетаКт,
	|	Строки.Подразделение КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	-(Строки.СуммаРегл - Строки.ПостояннаяРазница - Строки.ВременнаяРазница) КАК СуммаНУКт,
	|	-Строки.ПостояннаяРазница КАК СуммаПРКт,
	|	-Строки.ВременнаяРазница КАК СуммаВРКт,
	|	""Реклассификация доходов"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы.ПрочиеДоходы КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СуммыДокументовВВалютеРегл КАК Суммы
	|	ПО
	|		Суммы.Регистратор = Строки.Ссылка
	|		И Суммы.ИдентификаторСтроки = Строки.ИдентификаторСтроки
	|		И Суммы.СуммаБезНДСРегл <> 0
	|ГДЕ
	|	Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеклассификацияДоходов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // Реклассификация доходов (Дт <91.09>:: Кт <91.01>)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	ВЫБОР КОГДА РАЗНОСТЬДАТ(Строки.ДатаОтражения, Операция.Дата, ДЕНЬ) = 0
	|				ИЛИ Строки.ДатаОтражения = ДАТАВРЕМЯ(1, 1, 1) ТОГДА
	|		Операция.Дата
	|	ИНАЧЕ
	|		Строки.ДатаОтражения
	|	КОНЕЦ КАК Период,
	|	Операция.Организация КАК Организация,
	|	Строки.НомерСтроки КАК ИдентификаторСтроки,
	|	
	|	Строки.СуммаРегл КАК Сумма,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	НЕОПРЕДЕЛЕНО КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.СальдоПрочихДоходовИРасходов) КАК СчетДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы) КАК ВидСчетаКт,
	|	Строки.КорСтатьяДоходов КАК АналитикаУчетаКт,
	|	ВЫБОР 
	|		КОГДА Строки.КорПодразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) 
	|			ТОГДА Строки.КорПодразделение
	|		ИНАЧЕ Строки.Подразделение
	|	КОНЕЦ КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	ВЫБОР 
	|		КОГДА Строки.КорПодразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) 
	|			ТОГДА Строки.КорПодразделение
	|		ИНАЧЕ Строки.Подразделение
	|	КОНЕЦ КАК ПодразделениеКт,
	|	Строки.КорНаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	(Строки.СуммаРегл - Строки.ПостояннаяРазница - Строки.ВременнаяРазница) КАК СуммаНУКт,
	|	Строки.ПостояннаяРазница КАК СуммаПРКт,
	|	Строки.ВременнаяРазница КАК СуммаВРКт,
	|	""Реклассификация доходов"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы.ПрочиеДоходы КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СуммыДокументовВВалютеРегл КАК Суммы
	|	ПО
	|		Суммы.Регистратор = Строки.Ссылка
	|		И Суммы.ИдентификаторСтроки = Строки.ИдентификаторСтроки
	|		И Суммы.СуммаБезНДСРегл <> 0
	|ГДЕ
	|	Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеклассификацияДоходов)
	|
	|";
#КонецОбласти
	
	Возврат ТекстРегистрацияПрочихРасходов
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстСписаниеПрочихРасходов
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстСторнированиеПрочихРасходов
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстРегистрацияПрочихДоходов
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстСторнированиеПрочихДоходов
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстРеклассификацияРасходов
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстРеклассификацияДоходов;
		
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц, 
// необходимых для отражения в регламентированном учете
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	Возврат "";
	
КонецФункции

//-- НЕ УТ

#КонецОбласти

#Область ОбработчикиСобытий

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	////////////////////////////////////////////////////////////////////////////
	// Создадим запрос инициализации движений
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	////////////////////////////////////////////////////////////////////////////
	// Сформируем текст запроса
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаПрочиеДоходы(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаДвиженияДоходыРасходыПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры);
	//++ НЕ УТ
	ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры);
	//-- НЕ УТ
	
	ПроведениеСерверУТ.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Дата                  КАК Дата,
	|	ДанныеДокумента.Валюта                КАК Валюта,
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация           КАК Организация
	|ИЗ
	|	Документ.ПрочиеДоходыРасходы КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период",                          Реквизиты.Дата);
	Запрос.УстановитьПараметр("Валюта",                          Реквизиты.Валюта);
	Запрос.УстановитьПараметр("Организация",                     Реквизиты.Организация);
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета",  Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаУправленческогоУчета",      Константы.ВалютаУправленческогоУчета.Получить());
	Запрос.УстановитьПараметр("ХозяйственнаяОперация",           Реквизиты.ХозяйственнаяОперация);
	
КонецПроцедуры

Процедура УстановитьПараметрКоэффициентПересчетаВВалютуУпр(Запрос)
	
	Если Запрос.Параметры.Свойство("КоэффициентПересчетаВВалютуУпр") Тогда
		Возврат;
	КонецЕсли;
	
	КоэффициентПересчетаВВалютуУпр = РаботаСКурсамиВалютУТ.ПолучитьКоэффициентПересчетаИзВалютыВВалюту(
	                                   Запрос.Параметры.Валюта, 
	                                   Запрос.Параметры.ВалютаУправленческогоУчета, 
	                                   Запрос.Параметры.Период);
	
	Запрос.УстановитьПараметр("КоэффициентПересчетаВВалютуУпр", КоэффициентПересчетаВВалютуУпр);
	
КонецПроцедуры

Функция ТекстЗапросаВтДанныеРасходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтДанныеРасходы";
	
	УстановитьПараметрКоэффициентПересчетаВВалютуУпр(Запрос);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Организация                                 КАК Организация,
	|	ВЫБОР КОГДА РАЗНОСТЬДАТ(ДанныеДокументаРасходы.ДатаОтражения, ДанныеДокумента.Дата, ДЕНЬ) = 0
	|				ИЛИ ДанныеДокументаРасходы.ДатаОтражения = ДАТАВРЕМЯ(1, 1, 1) ТОГДА
	|		ДанныеДокумента.Дата
	|	ИНАЧЕ
	|		ДанныеДокументаРасходы.ДатаОтражения
	|	КОНЕЦ                                                       КАК Дата,
	|	ДанныеДокументаРасходы.НомерСтроки                          КАК НомерСтроки,
	|
	|	ДанныеДокументаРасходы.Подразделение                        КАК Подразделение,
	|	ДанныеДокументаРасходы.НаправлениеДеятельности              КАК НаправлениеДеятельности,
	|	ДанныеДокументаРасходы.СтатьяРасходов                       КАК СтатьяРасходов,
	|	ДанныеДокументаРасходы.АналитикаРасходов                    КАК АналитикаРасходов,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокументаРасходы.КорПодразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) 
	|			ТОГДА ДанныеДокументаРасходы.КорПодразделение
	|		ИНАЧЕ ДанныеДокументаРасходы.Подразделение
	|	КОНЕЦ                                                       КАК КорПодразделение,
	|	ДанныеДокументаРасходы.КорНаправлениеДеятельности           КАК КорНаправлениеДеятельности,
	|	ДанныеДокументаРасходы.КорСтатьяРасходов                    КАК КорСтатьяРасходов,
	|	ДанныеДокументаРасходы.КорАналитикаРасходов                 КАК КорАналитикаРасходов,
	|
	|	ДанныеДокументаРасходы.СтатьяАктивовПассивов                КАК СтатьяАктивовПассивов,
	|	ДанныеДокументаРасходы.АналитикаАктивовПассивов             КАК АналитикаАктивовПассивов,
	|
	|	ДанныеДокументаРасходы.Ссылка                               КАК ДокументПоступленияРасходов,
	|	СтатьиРасходов.ВариантРаспределенияРасходов                 КАК ВариантРаспределенияРасходов,
	|	СтатьиРасходов.ПринятиеКНалоговомуУчету                     КАК ПринятиеКНалоговомуУчету,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка) КАК АналитикаУчетаПартий,
	|	ВЫБОР
	|		КОГДА &ХозяйственнаяОперация В (
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПрочихРасходов),
	|				ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторнированиеПрочихРасходов))
	|			ТОГДА ДанныеДокументаРасходы.Сумма
	|		ИНАЧЕ ДанныеДокументаРасходы.Сумма * &КоэффициентПересчетаВВалютуУпр
	|	КОНЕЦ                                      КАК Сумма,
	|	ВЫБОР КОГДА СтатьиРасходов.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты) ТОГДА
	|		ВЫБОР
	|			КОГДА &ХозяйственнаяОперация В (
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПрочихРасходов),
	|					ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторнированиеПрочихРасходов))
	|				ТОГДА ДанныеДокументаРасходы.СуммаБезНДС
	|			ИНАЧЕ ДанныеДокументаРасходы.СуммаБезНДС * &КоэффициентПересчетаВВалютуУпр
	|		КОНЕЦ
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаБезНДС,
	|	ВЫБОР КОГДА СтатьиРасходов.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы) ТОГДА
	|		0
	|	ИНАЧЕ
	|		ДанныеДокументаРасходы.СуммаРегл
	|	КОНЕЦ КАК СуммаРегл,
	|	ВЫБОР КОГДА СтатьиРасходов.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|	ТОГДА
	|		0
	|	КОГДА Не СтатьиРасходов.ПринятиеКНалоговомуУчету ТОГДА
	|		ДанныеДокументаРасходы.СуммаРегл - ДанныеДокументаРасходы.ВременнаяРазница
	|	ИНАЧЕ
	|		ДанныеДокументаРасходы.ПостояннаяРазница
	|	КОНЕЦ КАК ПостояннаяРазница,
	|
	|	ВЫБОР КОГДА Не КорСтатьиРасходов.ПринятиеКНалоговомуУчету ТОГДА
	|		ДанныеДокументаРасходы.СуммаРегл - ДанныеДокументаРасходы.ВременнаяРазница
	|	ИНАЧЕ
	|		ДанныеДокументаРасходы.ПостояннаяРазница
	|	КОНЕЦ КАК КорПостояннаяРазница,
	|
	|	ВЫБОР КОГДА СтатьиРасходов.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|	ТОГДА
	|		0
	|	ИНАЧЕ
	|		ДанныеДокументаРасходы.ВременнаяРазница
	|	КОНЕЦ КАК ВременнаяРазница,
	|	ДанныеДокументаРасходы.ВременнаяРазница КАК КорВременнаяРазница,
	|
	|	ДанныеДокументаРасходы.Сумма               КАК СуммаВВалюте
	|ПОМЕСТИТЬ ВтДанныеРасходы
	|ИЗ
	|	Документ.ПрочиеДоходыРасходы.ПрочиеРасходы КАК ДанныеДокументаРасходы
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ПрочиеДоходыРасходы КАК ДанныеДокумента
	|	ПО
	|		ДанныеДокументаРасходы.Ссылка = ДанныеДокумента.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|	ПО
	|		ДанныеДокументаРасходы.СтатьяРасходов = СтатьиРасходов.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК КорСтатьиРасходов
	|	ПО
	|		ДанныеДокументаРасходы.КорСтатьяРасходов = КорСтатьиРасходов.Ссылка
	|
	|ГДЕ
	|	ДанныеДокументаРасходы.Ссылка = &Ссылка
	|	И &ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеРасходы),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПрочихРасходов),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторнированиеПрочихРасходов),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеклассификацияРасходов))";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра); 
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтДанныеДоходы(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтДанныеДоходы";
	
	УстановитьПараметрКоэффициентПересчетаВВалютуУпр(Запрос);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Организация                      КАК Организация,
	|	ВЫБОР КОГДА РАЗНОСТЬДАТ(ДанныеДокументаДоходы.ДатаОтражения, ДанныеДокумента.Дата, ДЕНЬ) = 0
	|				ИЛИ ДанныеДокументаДоходы.ДатаОтражения = ДАТАВРЕМЯ(1, 1, 1) ТОГДА
	|		ДанныеДокумента.Дата
	|	ИНАЧЕ
	|		ДанныеДокументаДоходы.ДатаОтражения
	|	КОНЕЦ                                            КАК Дата,
	|	ДанныеДокументаДоходы.НомерСтроки                КАК НомерСтроки,
	|
	|	ДанныеДокументаДоходы.Подразделение              КАК Подразделение,
	|	ДанныеДокументаДоходы.НаправлениеДеятельности    КАК НаправлениеДеятельности,
	|	ДанныеДокументаДоходы.СтатьяДоходов              КАК СтатьяДоходов,
	|	ДанныеДокументаДоходы.АналитикаДоходов           КАК АналитикаДоходов,
	|
	|	ВЫБОР 
	|		КОГДА ДанныеДокументаДоходы.КорПодразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) 
	|			ТОГДА ДанныеДокументаДоходы.КорПодразделение
	|		ИНАЧЕ ДанныеДокументаДоходы.Подразделение
	|	КОНЕЦ                                            КАК КорПодразделение,
	|	ДанныеДокументаДоходы.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	ДанныеДокументаДоходы.КорСтатьяДоходов           КАК КорСтатьяДоходов,
	|	ДанныеДокументаДоходы.КорАналитикаДоходов        КАК КорАналитикаДоходов,
	|
	|	ДанныеДокументаДоходы.СтатьяАктивовПассивов      КАК СтатьяАктивовПассивов,
	|	ДанныеДокументаДоходы.АналитикаАктивовПассивов   КАК АналитикаАктивовПассивов,
	|
	|	ДанныеДокументаДоходы.Сумма*&КоэффициентПересчетаВВалютуУпр КАК Сумма,
	|	ДанныеДокументаДоходы.СуммаРегл                             КАК СуммаРегл,
	|	ДанныеДокументаДоходы.Сумма                                 КАК СуммаВВалюте
	|ПОМЕСТИТЬ ВтДанныеДоходы
	|ИЗ
	|	Документ.ПрочиеДоходыРасходы.ПрочиеДоходы КАК ДанныеДокументаДоходы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПрочиеДоходыРасходы КАК ДанныеДокумента
	|		ПО ДанныеДокументаДоходы.Ссылка = ДанныеДокумента.Ссылка
	|ГДЕ
	|	ДанныеДокументаДоходы.Ссылка = &Ссылка
	|	И &ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеДоходы),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторнированиеПрочихДоходов),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеклассификацияДоходов))";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра); 
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПрочиеРасходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеРасходы";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтДанныеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаВтДанныеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВтДанныеРасходы.НомерСтроки КАК НомерСтроки,
	|	ВтДанныеРасходы.Дата КАК Период,
	|	ВтДанныеРасходы.Организация КАК Организация,
	|	ВтДанныеРасходы.Подразделение КАК Подразделение,
	|	ВтДанныеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВтДанныеРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|	ВтДанныеРасходы.АналитикаРасходов КАК АналитикаРасходов,
	//++ НЕ УТ
	|	ВтДанныеРасходы.СуммаБезНДС КАК СуммаБезНДС,
	|	ВтДанныеРасходы.СуммаРегл КАК СуммаРегл,
	|	ВтДанныеРасходы.ПостояннаяРазница КАК ПостояннаяРазница,
	|	ВтДанныеРасходы.ВременнаяРазница КАК ВременнаяРазница,
	//-- НЕ УТ
	|	ВтДанныеРасходы.Сумма
	|ИЗ
	|	ВтДанныеРасходы КАК ВтДанныеРасходы
	|
	|ГДЕ
	|	ВтДанныеРасходы.ВариантРаспределенияРасходов <> ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеРасходы)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ВтДанныеРасходы.НомерСтроки КАК НомерСтроки,
	|	ВтДанныеРасходы.Дата КАК Период,
	|	ВтДанныеРасходы.Организация,
	|	ВтДанныеРасходы.Подразделение,
	|	ВтДанныеРасходы.НаправлениеДеятельности,
	|	ВтДанныеРасходы.СтатьяРасходов,
	|	ВтДанныеРасходы.АналитикаРасходов,
	//++ НЕ УТ
	|	ВтДанныеРасходы.СуммаБезНДС КАК СуммаБезНДС,
	|	ВтДанныеРасходы.СуммаРегл КАК СуммаРегл,
	|	ВтДанныеРасходы.ПостояннаяРазница КАК ПостояннаяРазница,
	|	ВтДанныеРасходы.ВременнаяРазница КАК ВременнаяРазница,
	//-- НЕ УТ
	|	ВтДанныеРасходы.Сумма КАК Сумма
	|ИЗ
	|	ВтДанныеРасходы КАК ВтДанныеРасходы
	|
	|ГДЕ
	|	ВтДанныеРасходы.ВариантРаспределенияРасходов <> ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПрочихРасходов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВтДанныеРасходы.НомерСтроки КАК НомерСтроки,
	|	ВтДанныеРасходы.Дата КАК Период,
	|	ВтДанныеРасходы.Организация,
	|	ВтДанныеРасходы.Подразделение,
	|	ВтДанныеРасходы.НаправлениеДеятельности,
	|	ВтДанныеРасходы.СтатьяРасходов,
	|	ВтДанныеРасходы.АналитикаРасходов,
	//++ НЕ УТ
	|	- ВтДанныеРасходы.СуммаБезНДС КАК СуммаБезНДС,
	|	- ВтДанныеРасходы.СуммаРегл КАК СуммаРегл,
	|	- ВтДанныеРасходы.ПостояннаяРазница КАК ПостояннаяРазница,
	|	- ВтДанныеРасходы.ВременнаяРазница КАК ВременнаяРазница,
	//-- НЕ УТ
	|	- ВтДанныеРасходы.Сумма КАК Сумма
	|ИЗ
	|	ВтДанныеРасходы КАК ВтДанныеРасходы
	|
	|ГДЕ
	|	ВтДанныеРасходы.ВариантРаспределенияРасходов <> ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторнированиеПрочихРасходов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВтДанныеРасходы.НомерСтроки КАК НомерСтроки,
	|	ВтДанныеРасходы.Дата КАК Период,
	|	ВтДанныеРасходы.Организация,
	|	ВтДанныеРасходы.Подразделение,
	|	ВтДанныеРасходы.НаправлениеДеятельности,
	|	ВтДанныеРасходы.СтатьяРасходов,
	|	ВтДанныеРасходы.АналитикаРасходов,
	//++ НЕ УТ
	|	ВтДанныеРасходы.СуммаБезНДС КАК СуммаБезНДС,
	|	ВтДанныеРасходы.СуммаРегл КАК СуммаРегл,
	|	ВтДанныеРасходы.ПостояннаяРазница КАК ПостояннаяРазница,
	|	ВтДанныеРасходы.ВременнаяРазница КАК ВременнаяРазница,
	//-- НЕ УТ
	|	ВтДанныеРасходы.Сумма КАК Сумма
	|ИЗ
	|	ВтДанныеРасходы КАК ВтДанныеРасходы
	|
	|ГДЕ
	|	ВтДанныеРасходы.ВариантРаспределенияРасходов <> ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеклассификацияРасходов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВтДанныеРасходы.НомерСтроки КАК НомерСтроки,
	|	ВтДанныеРасходы.Дата КАК Период,
	|	ВтДанныеРасходы.Организация,
	|	ВтДанныеРасходы.КорПодразделение,
	|	ВтДанныеРасходы.КорНаправлениеДеятельности,
	|	ВтДанныеРасходы.КорСтатьяРасходов,
	|	ВтДанныеРасходы.КорАналитикаРасходов,
	//++ НЕ УТ
	|	-ВтДанныеРасходы.СуммаБезНДС КАК СуммаБезНДС,
	|	-ВтДанныеРасходы.СуммаРегл КАК СуммаРегл,
	|	-ВтДанныеРасходы.КорПостояннаяРазница КАК ПостояннаяРазница,
	|	-ВтДанныеРасходы.КорВременнаяРазница КАК ВременнаяРазница,
	//-- НЕ УТ
	|	-ВтДанныеРасходы.Сумма
	|ИЗ
	|	ВтДанныеРасходы КАК ВтДанныеРасходы
	|
	|ГДЕ
	|	ВтДанныеРасходы.ВариантРаспределенияРасходов <> ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеклассификацияРасходов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПартииПрочихРасходов(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПартииПрочихРасходов";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтДанныеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаВтДанныеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ВтДанныеРасходы.НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)      КАК ВидДвижения,
	|	ВтДанныеРасходы.Дата                        КАК Период,
	|	ВтДанныеРасходы.Организация                 КАК Организация,
	|	ВтДанныеРасходы.Подразделение               КАК Подразделение,
	|	ВтДанныеРасходы.НаправлениеДеятельности     КАК НаправлениеДеятельности,
	|	ВтДанныеРасходы.СтатьяРасходов              КАК СтатьяРасходов,
	|	ВтДанныеРасходы.АналитикаРасходов           КАК АналитикаРасходов,
	|	ВтДанныеРасходы.ДокументПоступленияРасходов КАК ДокументПоступленияРасходов,
	|	ВтДанныеРасходы.АналитикаУчетаПартий        КАК АналитикаУчетаПартий,
	|	ВтДанныеРасходы.Сумма                       КАК Стоимость,
	|	ВтДанныеРасходы.СуммаБезНДС                 КАК СтоимостьБезНДС,
	|	ВтДанныеРасходы.СуммаРегл                   КАК СтоимостьРегл,
	|	ВтДанныеРасходы.ПостояннаяРазница           КАК ПостояннаяРазница,
	|	ВтДанныеРасходы.ВременнаяРазница            КАК ВременнаяРазница,
	|	0 КАК НДСРегл
	|ИЗ
	|	ВтДанныеРасходы КАК ВтДанныеРасходы
	|
	|ГДЕ
	|	ВтДанныеРасходы.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеРасходы)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВтДанныеРасходы.НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ВтДанныеРасходы.Дата,
	|	ВтДанныеРасходы.Организация,
	|	ВтДанныеРасходы.Подразделение,
	|	ВтДанныеРасходы.НаправлениеДеятельности,
	|	ВтДанныеРасходы.СтатьяРасходов,
	|	ВтДанныеРасходы.АналитикаРасходов,
	|	ВтДанныеРасходы.ДокументПоступленияРасходов,
	|	ВтДанныеРасходы.АналитикаУчетаПартий,
	|	ВтДанныеРасходы.Сумма,
	|	ВтДанныеРасходы.СуммаБезНДС,
	|	ВтДанныеРасходы.СуммаРегл,
	|	
	|	ВтДанныеРасходы.ПостояннаяРазница КАК ПостояннаяРазница,
	|	ВтДанныеРасходы.ВременнаяРазница КАК ВременнаяРазница,
	|	
	|	0 КАК НДСРегл
	|ИЗ
	|	ВтДанныеРасходы КАК ВтДанныеРасходы
	|
	|ГДЕ
	|	ВтДанныеРасходы.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПрочихРасходов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВтДанныеРасходы.НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВтДанныеРасходы.Дата,
	|	ВтДанныеРасходы.Организация,
	|	ВтДанныеРасходы.Подразделение,
	|	ВтДанныеРасходы.НаправлениеДеятельности,
	|	ВтДанныеРасходы.СтатьяРасходов,
	|	ВтДанныеРасходы.АналитикаРасходов,
	|	ВтДанныеРасходы.ДокументПоступленияРасходов,
	|	ВтДанныеРасходы.АналитикаУчетаПартий,
	|	-ВтДанныеРасходы.Сумма,
	|	-ВтДанныеРасходы.СуммаБезНДС,
	|	-ВтДанныеРасходы.СуммаРегл,
	|
	|	-ВтДанныеРасходы.ПостояннаяРазница КАК ПостояннаяРазница,
	|	-ВтДанныеРасходы.ВременнаяРазница КАК ВременнаяРазница,
	|
	|	0 КАК НДСРегл
	|ИЗ
	|	ВтДанныеРасходы КАК ВтДанныеРасходы
	|
	|ГДЕ
	|	ВтДанныеРасходы.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторнированиеПрочихРасходов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВтДанныеРасходы.НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВтДанныеРасходы.Дата,
	|	ВтДанныеРасходы.Организация,
	|	ВтДанныеРасходы.Подразделение,
	|	ВтДанныеРасходы.НаправлениеДеятельности,
	|	ВтДанныеРасходы.СтатьяРасходов,
	|	ВтДанныеРасходы.АналитикаРасходов,
	|	ВтДанныеРасходы.ДокументПоступленияРасходов,
	|	ВтДанныеРасходы.АналитикаУчетаПартий,
	|	ВтДанныеРасходы.Сумма,
	|	ВтДанныеРасходы.СуммаБезНДС,
	|	ВтДанныеРасходы.СуммаРегл,
	|
	|	ВтДанныеРасходы.ПостояннаяРазница КАК ПостояннаяРазница,
	|	ВтДанныеРасходы.ВременнаяРазница КАК ВременнаяРазница,
	|
	|	0 КАК НДСРегл
	|ИЗ
	|	ВтДанныеРасходы КАК ВтДанныеРасходы
	|
	|ГДЕ
	|	ВтДанныеРасходы.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеклассификацияРасходов)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВтДанныеРасходы.НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ВтДанныеРасходы.Дата,
	|	ВтДанныеРасходы.Организация,
	|	ВтДанныеРасходы.КорПодразделение,
	|	ВтДанныеРасходы.КорНаправлениеДеятельности,
	|	ВтДанныеРасходы.КорСтатьяРасходов,
	|	ВтДанныеРасходы.КорАналитикаРасходов,
	|	ВтДанныеРасходы.ДокументПоступленияРасходов,
	|	ВтДанныеРасходы.АналитикаУчетаПартий,
	|	ВтДанныеРасходы.Сумма,
	|	ВтДанныеРасходы.СуммаБезНДС,
	|	ВтДанныеРасходы.СуммаРегл,
	|	
	|	ВтДанныеРасходы.КорПостояннаяРазница КАК ПостояннаяРазница,
	|	ВтДанныеРасходы.КорВременнаяРазница КАК ВременнаяРазница,
	|	
	|	0 КАК НДСРегл
	|ИЗ
	|	ВтДанныеРасходы КАК ВтДанныеРасходы
	|
	|ГДЕ
	|	ВтДанныеРасходы.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|	И &ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеклассификацияРасходов)
	|	
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПрочиеДоходы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеДоходы";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтДанныеДоходы", ТекстыЗапроса) Тогда
		ТекстЗапросаВтДанныеДоходы(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВтДанныеДоходы.НомерСтроки             КАК НомерСтроки,
	|	ВтДанныеДоходы.Дата                    КАК Период,
	|	ВтДанныеДоходы.Организация             КАК Организация,
	|	ВтДанныеДоходы.Подразделение           КАК Подразделение,
	|	ВтДанныеДоходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВтДанныеДоходы.СтатьяДоходов           КАК СтатьяДоходов,
	|	ВтДанныеДоходы.АналитикаДоходов        КАК АналитикаДоходов,
	|	ВтДанныеДоходы.Сумма                   КАК Сумма
	|ИЗ
	|	ВтДанныеДоходы КАК ВтДанныеДоходы
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеДоходы)
	|	И ВтДанныеДоходы.Сумма + ВтДанныеДоходы.СуммаРегл <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВтДанныеДоходы.НомерСтроки             КАК НомерСтроки,
	|	ВтДанныеДоходы.Дата                    КАК Период,
	|	ВтДанныеДоходы.Организация             КАК Организация,
	|	ВтДанныеДоходы.Подразделение           КАК Подразделение,
	|	ВтДанныеДоходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВтДанныеДоходы.СтатьяДоходов           КАК СтатьяДоходов,
	|	ВтДанныеДоходы.АналитикаДоходов        КАК АналитикаДоходов,
	|	-ВтДанныеДоходы.Сумма                  КАК Сумма
	|ИЗ
	|	ВтДанныеДоходы КАК ВтДанныеДоходы
	|ГДЕ
	|	&ХозяйственнаяОперация  = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторнированиеПрочихДоходов)
	|	И ВтДанныеДоходы.Сумма + ВтДанныеДоходы.СуммаРегл <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВтДанныеДоходы.НомерСтроки             КАК НомерСтроки,
	|	ВтДанныеДоходы.Дата                    КАК Период,
	|	ВтДанныеДоходы.Организация             КАК Организация,
	|	ВтДанныеДоходы.Подразделение           КАК Подразделение,
	|	ВтДанныеДоходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВтДанныеДоходы.СтатьяДоходов           КАК СтатьяДоходов,
	|	ВтДанныеДоходы.АналитикаДоходов        КАК АналитикаДоходов,
	|	-ВтДанныеДоходы.Сумма                  КАК Сумма
	|ИЗ
	|	ВтДанныеДоходы КАК ВтДанныеДоходы
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеклассификацияДоходов)
	|	И ВтДанныеДоходы.Сумма + ВтДанныеДоходы.СуммаРегл <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)    КАК ВидДвижения,
	|	ВтДанныеДоходы.НомерСтроки                КАК НомерСтроки,
	|	ВтДанныеДоходы.Дата                       КАК Период,
	|	ВтДанныеДоходы.Организация                КАК Организация,
	|	ВтДанныеДоходы.КорПодразделение           КАК Подразделение,
	|	ВтДанныеДоходы.КорНаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВтДанныеДоходы.КорСтатьяДоходов           КАК СтатьяДоходов,
	|	ВтДанныеДоходы.КорАналитикаДоходов        КАК АналитикаДоходов,
	|	ВтДанныеДоходы.Сумма                  КАК Сумма
	|ИЗ
	|	ВтДанныеДоходы КАК ВтДанныеДоходы
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеклассификацияДоходов)
	|	И ВтДанныеДоходы.Сумма + ВтДанныеДоходы.СуммаРегл <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПрочиеАктивыПассивы";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтДанныеДоходы", ТекстыЗапроса) Тогда
		ТекстЗапросаВтДанныеДоходы(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтДанныеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаВтДанныеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВтДанныеДоходы.Дата КАК Период,
	|	ВтДанныеДоходы.НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВтДанныеДоходы.Организация КАК Организация,
	|	ВтДанныеДоходы.КорПодразделение КАК Подразделение,
	|	ВтДанныеДоходы.КорНаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВтДанныеДоходы.СтатьяАктивовПассивов КАК Статья,
	|	ВтДанныеДоходы.АналитикаАктивовПассивов КАК Аналитика,
	|	ВтДанныеДоходы.Сумма КАК Сумма
	|ИЗ
	|	ВтДанныеДоходы КАК ВтДанныеДоходы
	|	
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеДоходы)
	|	И ВтДанныеДоходы.Сумма + ВтДанныеДоходы.СуммаРегл <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВтДанныеДоходы.Дата КАК Период,
	|	ВтДанныеДоходы.НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВтДанныеДоходы.Организация КАК Организация,
	|	ВтДанныеДоходы.КорПодразделение КАК Подразделение,
	|	ВтДанныеДоходы.КорНаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВтДанныеДоходы.СтатьяАктивовПассивов КАК Статья,
	|	ВтДанныеДоходы.АналитикаАктивовПассивов КАК Аналитика,
	|	-ВтДанныеДоходы.Сумма КАК Сумма
	|ИЗ
	|	ВтДанныеДоходы КАК ВтДанныеДоходы
	|	
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторнированиеПрочихДоходов)
	|	И ВтДанныеДоходы.Сумма + ВтДанныеДоходы.СуммаРегл <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВтДанныеРасходы.Дата КАК Период,
	|	ВтДанныеРасходы.НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ВтДанныеРасходы.Организация КАК Организация,
	|	ВтДанныеРасходы.КорПодразделение КАК Подразделение,
	|	ВтДанныеРасходы.КорНаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВтДанныеРасходы.СтатьяАктивовПассивов КАК Статья,
	|	ВтДанныеРасходы.АналитикаАктивовПассивов КАК Аналитика,
	|	ВтДанныеРасходы.Сумма КАК Сумма
	|ИЗ
	|	ВтДанныеРасходы КАК ВтДанныеРасходы
	|
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеРасходы)
	|	И ВтДанныеРасходы.Сумма + ВтДанныеРасходы.СуммаРегл <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВтДанныеРасходы.Дата КАК Период,
	|	ВтДанныеРасходы.НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВтДанныеРасходы.Организация КАК Организация,
	|	ВтДанныеРасходы.КорПодразделение КАК Подразделение,
	|	ВтДанныеРасходы.КорНаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВтДанныеРасходы.СтатьяАктивовПассивов КАК Статья,
	|	ВтДанныеРасходы.АналитикаАктивовПассивов КАК Аналитика,
	|	ВтДанныеРасходы.Сумма КАК Сумма
	|ИЗ
	|	ВтДанныеРасходы КАК ВтДанныеРасходы
	|
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПрочихРасходов)
	|	И ВтДанныеРасходы.Сумма + ВтДанныеРасходы.СуммаРегл <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВтДанныеРасходы.Дата КАК Период,
	|	ВтДанныеРасходы.НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ВтДанныеРасходы.Организация КАК Организация,
	|	ВтДанныеРасходы.КорПодразделение КАК Подразделение,
	|	ВтДанныеРасходы.КорНаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВтДанныеРасходы.СтатьяАктивовПассивов КАК Статья,
	|	ВтДанныеРасходы.АналитикаАктивовПассивов КАК Аналитика,
	|	-ВтДанныеРасходы.Сумма КАК Сумма
	|ИЗ
	|	ВтДанныеРасходы КАК ВтДанныеРасходы
	|
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторнированиеПрочихРасходов)
	|	И ВтДанныеРасходы.Сумма + ВтДанныеРасходы.СуммаРегл <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаДвиженияДоходыРасходыПрочиеАктивыПассивы(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ДвиженияДоходыРасходыПрочиеАктивыПассивы";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтДанныеДоходы", ТекстыЗапроса) Тогда
		ТекстЗапросаВтДанныеДоходы(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтДанныеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаВтДанныеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВтДанныеДоходы.Дата КАК Период,
	|	ВтДанныеДоходы.НомерСтроки,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеДоходыАктивыПассивы) КАК ХозяйственнаяОперация,
	|	ВтДанныеДоходы.Организация КАК Организация,
	|
	|	ВтДанныеДоходы.Подразделение КАК Подразделение,
	|	ВтДанныеДоходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВтДанныеДоходы.СтатьяДоходов КАК Статья,
	|	ВтДанныеДоходы.АналитикаДоходов КАК АналитикаДоходов,
	|	NULL КАК АналитикаРасходов,
	|	NULL КАК АналитикаАктивовПассивов,
	|
	|	ВтДанныеДоходы.КорПодразделение КАК КорПодразделение,
	|	ВтДанныеДоходы.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	ВтДанныеДоходы.СтатьяАктивовПассивов КАК КорСтатья,
	|	NULL КАК КорАналитикаДоходов,
	|	NULL КАК КорАналитикаРасходов,
	|	ВтДанныеДоходы.АналитикаАктивовПассивов КАК КорАналитикаАктивовПассивов,
	|
	|	ВтДанныеДоходы.Сумма КАК Сумма,
	|	ВтДанныеДоходы.СуммаРегл КАК СуммаРегл,
	|
	|	&Валюта КАК Валюта,
	|	ВтДанныеДоходы.СуммаВВалюте КАК СуммаВВалюте
	|
	|ИЗ
	|	ВтДанныеДоходы КАК ВтДанныеДоходы
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеДоходы)
	|	И ВтДанныеДоходы.Сумма + ВтДанныеДоходы.СуммаРегл <> 0
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВтДанныеДоходы.Дата КАК Период,
	|	ВтДанныеДоходы.НомерСтроки,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторнированиеПрочихДоходов) КАК ХозяйственнаяОперация,
	|	ВтДанныеДоходы.Организация КАК Организация,
	|
	|	ВтДанныеДоходы.Подразделение КАК Подразделение,
	|	ВтДанныеДоходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВтДанныеДоходы.СтатьяДоходов КАК Статья,
	|	ВтДанныеДоходы.АналитикаДоходов КАК АналитикаДоходов,
	|	NULL КАК АналитикаРасходов,
	|	NULL КАК АналитикаАктивовПассивов,
	|
	|	ВтДанныеДоходы.КорПодразделение КАК КорПодразделение,
	|	ВтДанныеДоходы.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	ВтДанныеДоходы.СтатьяАктивовПассивов КАК КорСтатья,
	|	NULL КАК КорАналитикаДоходов,
	|	NULL КАК КорАналитикаРасходов,
	|	ВтДанныеДоходы.АналитикаАктивовПассивов КАК КорАналитикаАктивовПассивов,
	|
	|	-ВтДанныеДоходы.Сумма КАК Сумма,
	|	-ВтДанныеДоходы.СуммаРегл КАК СуммаРегл,
	|
	|	&Валюта КАК Валюта,
	|	-ВтДанныеДоходы.СуммаВВалюте КАК СуммаВВалюте
	|
	|ИЗ
	|	ВтДанныеДоходы КАК ВтДанныеДоходы
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторнированиеПрочихДоходов)
	|	И ВтДанныеДоходы.Сумма + ВтДанныеДоходы.СуммаРегл <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВтДанныеРасходы.Дата КАК Период,
	|	ВтДанныеРасходы.НомерСтроки,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеРасходыАктивыПассивы) КАК ХозяйственнаяОперация,
	|	ВтДанныеРасходы.Организация КАК Организация,
	|
	|	ВтДанныеРасходы.КорПодразделение КАК Подразделение,
	|	ВтДанныеРасходы.КорНаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВтДанныеРасходы.СтатьяАктивовПассивов КАК Статья,
	|	NULL КАК АналитикаДоходов,
	|	NULL КАК АналитикаРасходов,
	|	ВтДанныеРасходы.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|
	|	ВтДанныеРасходы.Подразделение КАК КорПодразделение,
	|	ВтДанныеРасходы.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	ВтДанныеРасходы.СтатьяРасходов КАК КорСтатья,
	|	NULL КАК КорАналитикаДоходов,
	|	ВтДанныеРасходы.АналитикаРасходов КАК КорАналитикаРасходов,
	|	NULL КАК КорАналитикаАктивовПассивов,
	|
	|	ВтДанныеРасходы.Сумма КАК Сумма,
	|	ВтДанныеРасходы.СуммаРегл КАК СуммаРегл,
	|
	|	&Валюта КАК Валюта,
	|	ВтДанныеРасходы.СуммаВВалюте КАК СуммаВВалюте
	|
	|ИЗ
	|	ВтДанныеРасходы КАК ВтДанныеРасходы
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеРасходы)
	|	И ВтДанныеРасходы.Сумма + ВтДанныеРасходы.СуммаРегл <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВтДанныеРасходы.Дата КАК Период,
	|	ВтДанныеРасходы.НомерСтроки,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочиеРасходыАктивыПассивы) КАК ХозяйственнаяОперация,
	|	ВтДанныеРасходы.Организация КАК Организация,
	|
	|	ВтДанныеРасходы.Подразделение КАК Подразделение,
	|	ВтДанныеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВтДанныеРасходы.СтатьяРасходов КАК Статья,
	|	NULL КАК АналитикаДоходов,
	|	ВтДанныеРасходы.АналитикаРасходов КАК АналитикаРасходов,
	|	NULL КАК АналитикаАктивовПассивов,
	|
	|	ВтДанныеРасходы.КорПодразделение КАК КорПодразделение,
	|	ВтДанныеРасходы.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	ВтДанныеРасходы.СтатьяАктивовПассивов КАК КорСтатья,
	|	NULL КАК КорАналитикаДоходов,
	|	NULL КАК КорАналитикаРасходов,
	|	ВтДанныеРасходы.АналитикаАктивовПассивов КАК КорАналитикаАктивовПассивов,
	|
	|	ВтДанныеРасходы.Сумма КАК Сумма,
	|	ВтДанныеРасходы.СуммаРегл КАК СуммаРегл,
	|
	|	&Валюта КАК Валюта,
	|	ВтДанныеРасходы.СуммаВВалюте КАК СуммаВВалюте
	|
	|ИЗ
	|	ВтДанныеРасходы КАК ВтДанныеРасходы
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеПрочихРасходов)
	|	И ВтДанныеРасходы.Сумма + ВтДанныеРасходы.СуммаРегл <> 0
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВтДанныеРасходы.Дата КАК Период,
	|	ВтДанныеРасходы.НомерСтроки,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторнированиеПрочихРасходов) КАК ХозяйственнаяОперация,
	|	ВтДанныеРасходы.Организация КАК Организация,
	|
	|	ВтДанныеРасходы.КорПодразделение КАК Подразделение,
	|	ВтДанныеРасходы.КорНаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВтДанныеРасходы.СтатьяАктивовПассивов КАК Статья,
	|	NULL КАК АналитикаДоходов,
	|	NULL КАК АналитикаРасходов,
	|	ВтДанныеРасходы.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|
	|	ВтДанныеРасходы.Подразделение КАК КорПодразделение,
	|	ВтДанныеРасходы.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	ВтДанныеРасходы.СтатьяРасходов КАК КорСтатья,
	|	NULL КАК КорАналитикаДоходов,
	|	ВтДанныеРасходы.АналитикаРасходов КАК КорАналитикаРасходов,
	|	NULL КАК КорАналитикаАктивовПассивов,
	|
	|	-ВтДанныеРасходы.Сумма КАК Сумма,
	|	-ВтДанныеРасходы.СуммаРегл КАК СуммаРегл,
	|
	|	&Валюта КАК Валюта,
	|	-ВтДанныеРасходы.СуммаВВалюте КАК СуммаВВалюте
	|
	|ИЗ
	|	ВтДанныеРасходы КАК ВтДанныеРасходы
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СторнированиеПрочихРасходов)
	|	И ВтДанныеРасходы.Сумма + ВтДанныеРасходы.СуммаРегл <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВтДанныеРасходы.Дата КАК Период,
	|	ВтДанныеРасходы.НомерСтроки,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеклассификацияРасходов) КАК ХозяйственнаяОперация,
	|	ВтДанныеРасходы.Организация КАК Организация,
	|
	|	ВтДанныеРасходы.КорПодразделение КАК Подразделение,
	|	ВтДанныеРасходы.КорНаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВтДанныеРасходы.КорСтатьяРасходов КАК Статья,
	|	NULL КАК АналитикаДоходов,
	|	ВтДанныеРасходы.КорАналитикаРасходов КАК АналитикаРасходов,
	|	NULL КАК АналитикаАктивовПассивов,
	|
	|	ВтДанныеРасходы.Подразделение КАК КорПодразделение,
	|	ВтДанныеРасходы.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	ВтДанныеРасходы.СтатьяРасходов КАК КорСтатья,
	|	NULL КАК КорАналитикаДоходов,
	|	ВтДанныеРасходы.АналитикаРасходов КАК КорАналитикаРасходов,
	|	NULL КАК КорАналитикаАктивовПассивов,
	|
	|	ВтДанныеРасходы.Сумма КАК Сумма,
	|	ВтДанныеРасходы.СуммаРегл КАК СуммаРегл,
	|
	|	&Валюта КАК Валюта,
	|	ВтДанныеРасходы.СуммаВВалюте КАК СуммаВВалюте
	|
	|ИЗ
	|	ВтДанныеРасходы КАК ВтДанныеРасходы
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеклассификацияРасходов)
	|	И ВтДанныеРасходы.Сумма + ВтДанныеРасходы.СуммаРегл <> 0
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВтДанныеДоходы.Дата КАК Период,
	|	ВтДанныеДоходы.НомерСтроки,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеклассификацияДоходов) КАК ХозяйственнаяОперация,
	|	ВтДанныеДоходы.Организация КАК Организация,
	|
	|	ВтДанныеДоходы.КорПодразделение КАК Подразделение,
	|	ВтДанныеДоходы.КорНаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВтДанныеДоходы.КорСтатьяДоходов КАК Статья,
	|	ВтДанныеДоходы.КорАналитикаДоходов КАК АналитикаДоходов,
	|	NULL КАК АналитикаРасходов,
	|	NULL КАК АналитикаАктивовПассивов,
	|
	|	ВтДанныеДоходы.Подразделение КАК КорПодразделение,
	|	ВтДанныеДоходы.НаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	ВтДанныеДоходы.СтатьяДоходов КАК КорСтатья,
	|	ВтДанныеДоходы.АналитикаДоходов КАК КорАналитикаДоходов,
	|	NULL КАК КорАналитикаРасходов,
	|	NULL КАК КорАналитикаАктивовПассивов,
	|
	|	ВтДанныеДоходы.Сумма КАК Сумма,
	|	ВтДанныеДоходы.СуммаРегл КАК СуммаРегл,
	|
	|	&Валюта КАК Валюта,
	|	ВтДанныеДоходы.СуммаВВалюте КАК СуммаВВалюте
	|
	|ИЗ
	|	ВтДанныеДоходы КАК ВтДанныеДоходы
	|ГДЕ
	|	&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеклассификацияДоходов)
	|	И ВтДанныеДоходы.Сумма + ВтДанныеДоходы.СуммаРегл <> 0
	|	
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

//++ НЕ УТ

Функция ТекстЗапросаТаблицаОтражениеДокументовВРеглУчете(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОтражениеДокументовВРеглУчете";
	
	Если НЕ ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтДанныеРасходы", ТекстыЗапроса) Тогда
		ТекстЗапросаВтДанныеРасходы(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	Если Не ПроведениеСерверУТ.ЕстьТаблицаЗапроса("ВтДанныеДоходы", ТекстыЗапроса) Тогда
		ТекстЗапросаВтДанныеДоходы(Запрос, ТекстыЗапроса);
	КонецЕсли; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Период КАК Период,
	|	Расходы.Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(Расходы.Дата, ДЕНЬ) КАК ДатаОтражения
	|ИЗ
	|	ВтДанныеРасходы КАК Расходы
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&Период КАК Период,
	|	Доходы.Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(Доходы.Дата, ДЕНЬ) КАК ДатаОтражения
	|ИЗ
	|	ВтДанныеДоходы КАК Доходы
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

//-- НЕ УТ

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	Возврат;
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Операция.Ссылка
	|ИЗ
	|	Документ.ПрочиеДоходыРасходы КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПрочиеДоходыРасходы.ПрочиеРасходы КАК ТабличнаяЧасть
	|		ПО Операция.Ссылка = ТабличнаяЧасть.Ссылка
	|ГДЕ
	|	ТабличнаяЧасть.СтатьяАктивовПассивов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЦелевоеФинансирование)
	|	И ТабличнаяЧасть.КорНаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	И ТабличнаяЧасть.АналитикаАктивовПассивов ССЫЛКА Справочник.НаправленияДеятельности
	|	И ТабличнаяЧасть.АналитикаАктивовПассивов <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	И Операция.Проведен
	|	И НЕ Операция.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Операция.Ссылка
	|ИЗ
	|	Документ.ПрочиеДоходыРасходы КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПрочиеДоходыРасходы.ПрочиеДоходы КАК ТабличнаяЧасть
	|		ПО Операция.Ссылка = ТабличнаяЧасть.Ссылка
	|ГДЕ
	|	ТабличнаяЧасть.СтатьяАктивовПассивов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЦелевоеФинансирование)
	|	И ТабличнаяЧасть.КорНаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	И ТабличнаяЧасть.АналитикаАктивовПассивов ССЫЛКА Справочник.НаправленияДеятельности
	|	И ТабличнаяЧасть.АналитикаАктивовПассивов <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	И Операция.Проведен
	|	И НЕ Операция.ПометкаУдаления";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "Документ.ПрочиеДоходыРасходы";
	МетаданныеОбъекта = Метаданные.Документы.ДвижениеПрочихАктивовПассивов;
	
	МВТ = Новый МенеджерВременныхТаблиц;
	
	ПараметрыВыборкиДляОбработки = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ПараметрыВыборкиДляОбработки.ИмяВременнойТаблицы = "втСсылокДляОбработки";
	Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуСсылокДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта, МВТ, ПараметрыВыборкиДляОбработки);
	Если Не Результат.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Если Не Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МВТ;
	#Область ТекстЗапроса
	Запрос.Текст=
	"ВЫБРАТЬ
	|	втСсылокДляОбработки.Ссылка
	|ИЗ
	|	втСсылокДляОбработки КАК втСсылокДляОбработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	""ПрочиеРасходы"" КАК ИмяТабличнойЧасти,
	|	ТабличнаяЧасть.НомерСтроки - 1 КАК ИндексСтроки,
	|	ТабличнаяЧасть.АналитикаАктивовПассивов КАК КорНаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(Перечисление.СтатьиБезАналитики.ПустаяСсылка) КАК АналитикаАктивовПассивов
	|ИЗ
	|	Документ.ПрочиеДоходыРасходы КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПрочиеДоходыРасходы.ПрочиеРасходы КАК ТабличнаяЧасть
	|		ПО Операция.Ссылка = ТабличнаяЧасть.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСсылокДляОбработки КАК втСсылокДляОбработки
	|		ПО (ТабличнаяЧасть.Ссылка = втСсылокДляОбработки.Ссылка)
	|ГДЕ
	|	ТабличнаяЧасть.СтатьяАктивовПассивов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЦелевоеФинансирование)
	|	И ТабличнаяЧасть.КорНаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	И ТабличнаяЧасть.АналитикаАктивовПассивов ССЫЛКА Справочник.НаправленияДеятельности
	|	И ТабличнаяЧасть.АналитикаАктивовПассивов <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Операция.Ссылка,
	|	""ПрочиеДоходы"",
	|	ТабличнаяЧасть.НомерСтроки - 1,
	|	ТабличнаяЧасть.АналитикаАктивовПассивов КАК КорНаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(Перечисление.СтатьиБезАналитики.ПустаяСсылка) КАК АналитикаАктивовПассивов
	|ИЗ
	|	Документ.ПрочиеДоходыРасходы КАК Операция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПрочиеДоходыРасходы.ПрочиеДоходы КАК ТабличнаяЧасть
	|		ПО Операция.Ссылка = ТабличнаяЧасть.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСсылокДляОбработки КАК втСсылокДляОбработки
	|		ПО (ТабличнаяЧасть.Ссылка = втСсылокДляОбработки.Ссылка)
	|ГДЕ
	|	ТабличнаяЧасть.СтатьяАктивовПассивов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ЦелевоеФинансирование)
	|	И ТабличнаяЧасть.КорНаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	И ТабличнаяЧасть.АналитикаАктивовПассивов ССЫЛКА Справочник.НаправленияДеятельности
	|	И ТабличнаяЧасть.АналитикаАктивовПассивов <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)";
	#КонецОбласти
	
	Пакет = Запрос.ВыполнитьПакет();
	
	Выборка = Пакет[Пакет.Количество()-2].Выбрать();
	ВыборкаСтрок = Пакет[Пакет.Количество()-1].Выбрать();
	СтруктураПоискаСтрок = Новый Структура("Ссылка", Неопределено);
	
	ОбработаноОбъектов = 0;
	ПроблемныхОбъектов = 0;
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			Блокировка.Заблокировать();
		Исключение
			ОтменитьТранзакцию();
			ТекстСообщения = НСтр("ru = 'Не удалось заблокировать движение прочих активов и пассивов: %1 по причине: %2'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.Ссылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				МетаданныеОбъекта,
				Выборка.Ссылка,
				ТекстСообщения);
			Продолжить;
		КонецПопытки;
		
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ВыборкаСтрок.Сбросить();
		ЗаполнитьЗначенияСвойств(СтруктураПоискаСтрок, Выборка);
		Пока ВыборкаСтрок.НайтиСледующий(СтруктураПоискаСтрок) Цикл
			ЗаполнитьЗначенияСвойств(ДокументОбъект[ВыборкаСтрок.ИмяТабличнойЧасти][ВыборкаСтрок.ИндексСтроки], ВыборкаСтрок);;
		КонецЦикла;
		
		ОбработаноОбъектов = ОбработаноОбъектов + 1;
		
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ДокументОбъект, Ложь, Ложь, РежимЗаписиДокумента.Запись);
			ЗафиксироватьТранзакцию();
		Исключение
			
			ОтменитьТранзакцию();
			
			ОбработаноОбъектов = ОбработаноОбъектов - 1;
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ТекстСообщения = НСтр("ru = 'Не удалось заполнить направление деятельности в документе ""%1"" по причине: %2'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.Ссылка, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				МетаданныеОбъекта,
				Выборка.Ссылка,
				ТекстСообщения);
			
			Продолжить;
			
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбработаноОбъектов = 0 И ПроблемныхОбъектов > 0 Тогда
		
		ТекстСообщения = НСтр("ru = 'Не выполнено заполнение направление деятельности в нескольких документах (пропущены): %1'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
		
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее


#КонецОбласти

#КонецОбласти

#КонецЕсли
