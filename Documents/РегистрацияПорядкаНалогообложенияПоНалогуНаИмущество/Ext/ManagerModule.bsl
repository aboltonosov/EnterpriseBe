
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает необходимость указания КБК по коду вида имущества
//
// Параметры:
// 	 КодВидаИмущества - Строка  - Код вида имущества
// 
// Возвращаемое значение:
// 	Результат - Булево - Признак необходимости указания КБК
//
Функция ТребуетсяУказаниеКБК(КодВидаИмущества) Экспорт
	
	Результат = Ложь;
	
	Если КодВидаИмущества = "06" ИЛИ КодВидаИмущества = "08" ИЛИ КодВидаИмущества = "10" Тогда
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт

	ТекстыЗапроса = Новый СписокЗначений;
	
	ПолноеИмяДокумента = "Документ.РегистрацияПорядкаНалогообложенияПоНалогуНаИмущество";
	
	ТекстыЗапросаВременныхТаблиц = Новый Соответствие;
	ПереопределениеРасчетаПараметров = Новый Структура;
		
	Если ИмяРегистра = "СпособыОтраженияРасходовПоИмущественнымНалогам" Тогда
		
		ТекстЗапроса = СпособыОтраженияРасходовПоИмущественнымНалогам(ТекстыЗапроса, Неопределено);
		СинонимТаблицыДокумента = "ТабличнаяЧастьДокумента";
		
	Иначе
		ТекстИсключения = НСтр("ru = 'В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	
	Результат.ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(ТекстЗапроса,
																								ПолноеИмяДокумента,
																								СинонимТаблицыДокумента,
																								ПереопределениеРасчетаПараметров,
																								ТекстыЗапросаВременныхТаблиц);

	Возврат Результат;
	
КонецФункции

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	Возврат ИсточникиДанных;
	
КонецФункции

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства);
	
	ТекстыЗапроса = Новый СписокЗначений;
	СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам(ТекстыЗапроса, Регистры);
	СпособыОтраженияРасходовПоИмущественнымНалогам(ТекстыЗапроса, Регистры);
	
	ПроведениеСерверУТ.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства)
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Операция.Ссылка,
	|	Операция.Номер,
	|	Операция.Дата КАК Период,
	|	Операция.Организация,
	|	Операция.ВидОбъектаНалогаНаИмущество,
	|	Операция.ПостановкаНаУчетВНалоговомОргане,
	|	Операция.НалоговыйОрган,
	|	Операция.КодПоОКАТО,
	|	Операция.КодПоОКТМО,
	|	Операция.ПорядокНалогообложения,
	|	Операция.НалоговаяСтавка,
	|	Операция.КодНалоговойЛьготыОсвобождениеОтНалогообложения,
	|	Операция.СниженнаяНалоговаяСтавка,
	|	Операция.КодВидаИмущества,
	|	Операция.КБК,
	|	Операция.НалоговаяБаза,
	|	Операция.ИспользуетсяТолькоВДеятельностиОблагаемойЕНВД,
	|	Операция.ДвижимоеИмуществоПодлежащееНалогообложению,
	|	Операция.УказаныСпособыОтражениеРасходов
	|ИЗ
	|	Документ.РегистрацияПорядкаНалогообложенияПоНалогуНаИмущество КАК Операция
	|ГДЕ
	|	Операция.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("КодЛьготыДвижимоеИмуществоПринятоеКУчетуПосле2013", "2010257");
	
КонецПроцедуры

Процедура ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ()
	
	ВходящиеДанные = Новый Соответствие;
	
	ЗакрытиеМесяцаУТВызовСервера.ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ВходящиеДанные, НСтр("ru = 'Отмена регистрации земельных участков'"));
	
КонецПроцедуры

Процедура СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СтавкиНалогаНаИмуществоПоОтдельнымОсновнымСредствам";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	Текст = 
	"ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТабличнаяЧастьДокумента.ОсновноеСредство КАК ОсновноеСредство,
	|	&КодПоОКТМО КАК КодПоОКТМО,
	|	&КодПоОКАТО КАК КодПоОКАТО,
	|	
	|	&ПостановкаНаУчетВНалоговомОргане КАК ПостановкаНаУчетВНалоговомОргане,
	|	&НалоговыйОрган КАК НалоговыйОрган,
	|	&ПорядокНалогообложения КАК ПорядокНалогообложения,
	|	&НалоговаяСтавка КАК НалоговаяСтавка,
	|	ВЫБОР КОГДА &ВидОбъектаНалогаНаИмущество = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовНалогаНаИмущество.ДвижимоеИмуществоПринятоеС2013)
	|				И &ПорядокНалогообложения = ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.ОсвобождаетсяОтНалогообложения) 
	|		ТОГДА &КодЛьготыДвижимоеИмуществоПринятоеКУчетуПосле2013
	|		ИНАЧЕ &КодНалоговойЛьготыОсвобождениеОтНалогообложения 
	|	КОНЕЦ КАК КодНалоговойЛьготыОсвобождениеОтНалогообложения,
	|	&СниженнаяНалоговаяСтавка КАК СниженнаяНалоговаяСтавка,
	|	&КодВидаИмущества КАК КодВидаИмущества,
	|	&КБК КАК КБК,
	|	ТабличнаяЧастьДокумента.ДоляСтоимостиЧислитель КАК ДоляСтоимостиЧислитель,
	|	ТабличнаяЧастьДокумента.ДоляСтоимостиЗнаменатель КАК ДоляСтоимостиЗнаменатель,
	|	&НалоговаяБаза КАК НалоговаяБаза,
	|	ТабличнаяЧастьДокумента.КадастроваяСтоимость КАК КадастроваяСтоимость,
	|	ТабличнаяЧастьДокумента.НеоблагаемаяКадастроваяСтоимость КАК НеоблагаемаяКадастроваяСтоимость,
	|	ТабличнаяЧастьДокумента.КадастровыйНомер КАК КадастровыйНомер,
	|	ТабличнаяЧастьДокумента.КадастровыйНомерПомещения КАК КадастровыйНомерПомещения,
	|	ТабличнаяЧастьДокумента.УсловныйНомер КАК УсловныйНомер,
	|	ТабличнаяЧастьДокумента.ДатаРегистрацииПраваСобственности КАК ДатаРегистрацииПраваСобственности,
	|	ТабличнаяЧастьДокумента.ДатаПрекращенияПраваСобственности КАК ДатаПрекращенияПраваСобственности,
	|	ТабличнаяЧастьДокумента.ДоляВПравеОбщейСобственностиЧислитель КАК ДоляВПравеОбщейСобственностиЧислитель,
	|	ТабличнаяЧастьДокумента.ДоляВПравеОбщейСобственностиЗнаменатель КАК ДоляВПравеОбщейСобственностиЗнаменатель,
	|	ТабличнаяЧастьДокумента.ДоляПлощадиЧислитель КАК ДоляПлощадиЧислитель,
	|	ТабличнаяЧастьДокумента.ДоляПлощадиЗнаменатель КАК ДоляПлощадиЗнаменатель,
	|	&ИспользуетсяТолькоВДеятельностиОблагаемойЕНВД КАК ИспользуетсяТолькоВДеятельностиОблагаемойЕНВД,
	|	&ВидОбъектаНалогаНаИмущество = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовНалогаНаИмущество.ДвижимоеИмуществоПринятоеС2013)
	|		И &ПорядокНалогообложения <> ЗНАЧЕНИЕ(Перечисление.ПорядокНалогообложенияИмущества.ОсвобождаетсяОтНалогообложения) КАК ДвижимоеИмуществоПодлежащееНалогообложению
	|	
	|ИЗ
	|	Документ.РегистрацияПорядкаНалогообложенияПоНалогуНаИмущество.ОС КАК ТабличнаяЧастьДокумента
	|ГДЕ
	|	ТабличнаяЧастьДокумента.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра);
	
КонецПроцедуры

Функция СпособыОтраженияРасходовПоИмущественнымНалогам(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СпособыОтраженияРасходовПоИмущественнымНалогам";
	
	Если Не ПроведениеСерверУТ.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли;
	
	Текст = 
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	
	|	ТабличнаяЧастьДокумента.ОсновноеСредство КАК ОсновноеСредство,
	|	&Организация КАК Организация,
	|	
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
	|	ИСТИНА КАК СпособОтраженияРасходовЗаданДокументом,
	|	&Ссылка КАК СпособОтраженияРасходов
	|	
	|ИЗ
	|	Документ.РегистрацияПорядкаНалогообложенияПоНалогуНаИмущество.ОС КАК ТабличнаяЧастьДокумента
	|ГДЕ
	|	ТабличнаяЧастьДокумента.Ссылка = &Ссылка
	|	И &УказаныСпособыОтражениеРасходов";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра);
	
	Возврат Текст;
	
КонецФункции

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	
	
КонецПроцедуры

#КонецОбласти

#Область СтандартныеПодсистемы

Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
	Возврат; //В дальнейшем будет добавлен код настроек версионирования
	
КонецПроцедуры

Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании) Экспорт
	
	Возврат; //В дальнейшем будет добавлен код команд
	
КонецПроцедуры

Процедура ДобавитьКомандыОтчетов(КомандыОтчетов) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуДвиженияДокумента(КомандыОтчетов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт

	ПолноеИмяОбъекта = "Документ.РегистрацияПорядкаНалогообложенияПоНалогуНаИмущество";
	
	Выборка = ОбновлениеИнформационнойБазы.ВыбратьСсылкиДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта);
 
 	Пока Выборка.Следующий() Цикл
 		
 		НачатьТранзакцию();
		
 		Попытка
			
 			Блокировка = Новый БлокировкаДанных;
 			ЭлементБлокировки = Блокировка.Добавить("Документ.РегистрацияПорядкаНалогообложенияПоНалогуНаИмущество");
 			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
 			Блокировка.Заблокировать();
			
			ДанныеОбъекта = Выборка.Ссылка.ПолучитьОбъект();
			Если ДанныеОбъекта = Неопределено Тогда
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Ссылка);
				Продолжить;
			КонецЕсли;
			
			ДанныеОбъекта.УказаныСпособыОтражениеРасходов = (ДанныеОбъекта.ОтражениеРасходов.Количество() <> 0);
			Если ЗначениеЗаполнено(ДанныеОбъекта.УдалитьВидИмущества) Тогда
				ДанныеОбъекта.КодВидаИмущества = "03";
				Если ДанныеОбъекта.НалоговаяБаза = Перечисления.НалоговаяБазаПоНалогуНаИмущество.КадастроваяСтоимость Тогда
					ДанныеОбъекта.КодВидаИмущества = "11";
				ИначеЕсли ДанныеОбъекта.УдалитьВидИмущества = Перечисления.УдалитьВидыИмущества.ВходитВСоставЕСГС Тогда
					ДанныеОбъекта.КодВидаИмущества = "01";
				ИначеЕсли ДанныеОбъекта.УдалитьВидИмущества = Перечисления.УдалитьВидыИмущества.НаходитсяНаТерриторииДругогоГосударства Тогда
					ДанныеОбъекта.КодВидаИмущества = "04";
				ИначеЕсли ДанныеОбъекта.УдалитьВидИмущества = Перечисления.УдалитьВидыИмущества.ИспользуетсяВОЭЗКалининградскойОбласти Тогда
					ДанныеОбъекта.КодВидаИмущества = "05";
				КонецЕсли;
				ДанныеОбъекта.УдалитьВидИмущества = Неопределено;
			КонецЕсли;
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(ДанныеОбъекта);
	
			ЗафиксироватьТранзакцию();
			
 		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru = 'Не удалось обработать объект: %Ссылка% по причине: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Выборка.Ссылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
									УровеньЖурналаРегистрации.Предупреждение,
									Выборка.Ссылка.Метаданные(),
									Выборка.Ссылка,
									ТекстСообщения);
									
 		КонецПопытки;
 
 	КонецЦикла;
 		
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Ссылка
	|ИЗ
	|	Документ.РегистрацияПорядкаНалогообложенияПоНалогуНаИмущество КАК ДанныеДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РегистрацияПорядкаНалогообложенияПоНалогуНаИмущество.ОтражениеРасходов КАК ТаблицаОтражениеРасходов
	|		ПО (ТаблицаОтражениеРасходов.Ссылка = ДанныеДокумента.Ссылка)
	|ГДЕ
	|	НЕ ДанныеДокумента.УказаныСпособыОтражениеРасходов
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеДокумента.Ссылка
	|ИЗ
	|	Документ.РегистрацияПорядкаНалогообложенияПоНалогуНаИмущество КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.УдалитьВидИмущества <> ЗНАЧЕНИЕ(Перечисление.УдалитьВидыИмущества.ПустаяСсылка)
	|";
	СписокСсылок = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, СписокСсылок);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
