
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьТаблицуТоваров();

	Если ЗначениеЗаполнено(Параметры.Организация) Тогда
		Элементы.ТаблицаТоваровОрганизация.Видимость = Ложь;
	КонецЕсли; 
	Если ЗначениеЗаполнено(Параметры.Подразделение) Тогда
		Элементы.ТаблицаТоваровПодразделение.Видимость = Ложь;
	КонецЕсли; 
	Если ЗначениеЗаполнено(Параметры.Переработчик) Тогда
		Элементы.ТаблицаТоваровПереработчик.Видимость = Ложь;
	КонецЕсли; 
	
	ЗаказыСервер.УстановитьПризнакиПрисутствияСтрокиВДокументе(
		ТаблицаТоваров,
		"ЗаказПереработчику",
		Параметры.МассивКодовСтрок);
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	ТекстВопроса = НСтр("ru = 'Данные были изменены. Перенести изменения в документ?'");
	Оповещение = Новый ОписаниеОповещения("ПеренестиТоварыВДокументИЗакрыть", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы, ТекстВопроса);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТаблицаТоваровВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элементы.ТаблицаТоваров.ТекущиеДанные <> Неопределено Тогда
		
		Если Поле.Имя = "ТаблицаТоваровЗаказПереработчику" Тогда
			ПоказатьЗначение(Неопределено, Элементы.ТаблицаТоваров.ТекущиеДанные.ЗаказПереработчику);
			
		ИначеЕсли Поле.Имя = "ТаблицаТоваровСделка" Тогда
			ПоказатьЗначение(Неопределено, Элементы.ТаблицаТоваров.ТекущиеДанные.Сделка);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеренестиВДокументВыполнить()
	
	ПеренестиТоварыВДокумент();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьТоварыВыполнить()
	
	ВыбратьВсеТоварыНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьТоварыВыполнить()
	
	ВыбратьВсеТоварыНаСервере(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	#Область СтандартноеОформление
	НоменклатураСервер.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(ЭтаФорма, 
																			 "ТаблицаТоваровХарактеристика",
																		     "ТаблицаТоваров.ХарактеристикиИспользуются");
	НоменклатураСервер.УстановитьУсловноеОформлениеЕдиницИзмерения(ЭтаФорма, 
																   "ТаблицаТоваровНоменклатураЕдиницаИзмерения", 
                                                                   "ТаблицаТоваров.Упаковка");
	#КонецОбласти
	
	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаТоваров.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаТоваров.ПрисутствуетВДокументе");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Gray);

	
КонецПроцедуры

&НаСервере
Процедура ВыбратьВсеТоварыНаСервере(ЗначениеВыбора = Истина)
	
	Для Каждого СтрокаТаблицы Из ТаблицаТоваров.НайтиСтроки(Новый Структура("СтрокаВыбрана", Не ЗначениеВыбора)) Цикл
		
		СтрокаТаблицы.СтрокаВыбрана = ЗначениеВыбора;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПоместитьТоварыВХранилище()
	
	Возврат ПоместитьВоВременноеХранилище(ТаблицаТоваров.Выгрузить(Новый Структура("СтрокаВыбрана", Истина)));
	
КонецФункции

&НаСервере
Процедура ЗаполнитьТаблицуТоваров()

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗаказыПоставщикам.ЗаказПоставщику     КАК Распоряжение,
	|	ЗаказыПоставщикам.Склад               КАК Склад,
	|	ЗаказыПоставщикам.КодСтроки           КАК КодСтроки,
	|	ЗаказыПоставщикам.Номенклатура        КАК Номенклатура,
	|	ЗаказыПоставщикам.Характеристика      КАК Характеристика,
	|	ЗаказыПоставщикам.КОформлениюОстаток  КАК Заказано
	|ПОМЕСТИТЬ ЗаказыПоставщикам
	|ИЗ
	|	РегистрНакопления.ЗаказыПоставщикам.Остатки(
	|			,
	|			ЗаказПоставщику ССЫЛКА Документ.ЗаказПереработчику
	|				И (Склад В ИЕРАРХИИ (&Склад) ИЛИ &Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	|			) КАК ЗаказыПоставщикам
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаказыПоставщикам.ЗаказПоставщику     КАК Распоряжение,
	|	ЗаказыПоставщикам.Склад               КАК Склад,
	|	ЗаказыПоставщикам.КодСтроки           КАК КодСтроки,
	|	ЗаказыПоставщикам.Номенклатура        КАК Номенклатура,
	|	ЗаказыПоставщикам.Характеристика      КАК Характеристика,
	|	ЗаказыПоставщикам.КОформлению         КАК Заказано
	|ИЗ
	|	РегистрНакопления.ЗаказыПоставщикам КАК ЗаказыПоставщикам
	|	
	|ГДЕ
	|	ЗаказыПоставщикам.Регистратор = &Регистратор
	|	И ЗаказыПоставщикам.Активность
	|	И (ЗаказыПоставщикам.Склад В ИЕРАРХИИ (&Склад) ИЛИ &Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Распоряжение,
	|	КодСтроки
	|;
	|
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказыПоставщикам.Распоряжение           КАК ЗаказПереработчику,
	|	ДокЗаказПереработчику.Номер              КАК НомерРаспоряжения,
	|	ДокЗаказПереработчику.Дата               КАК ДатаРаспоряжения,
	|	ДокЗаказПереработчику.Организация        КАК Организация,
	|	ДокЗаказПереработчику.Партнер            КАК Переработчик,
	|	ЗаказыПоставщикам.Склад                  КАК Склад,
	|	ЗаказыПоставщикам.КодСтроки              КАК КодСтроки,
	|	ЗаказыПоставщикам.Номенклатура           КАК Номенклатура,
	|	ЗаказыПоставщикам.Характеристика         КАК Характеристика,
	|	ЕСТЬNULL(ТаблицаПродукция.Назначение, ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)) КАК Назначение,
	|	ЕСТЬNULL(ТаблицаПродукция.Упаковка, НЕОПРЕДЕЛЕНО) КАК Упаковка,
	|
	|	ВЫБОР
	|		КОГДА ДокЗаказПереработчику.ПереработкаПоЗаказу
	|			ТОГДА ТаблицаПродукцияУслуги.Распоряжение
	|		ИНАЧЕ ДокЗаказПереработчику.Ссылка
	|	КОНЕЦ                                    КАК Распоряжение,
	|
	|	ЗаказыПоставщикам.Заказано               КАК Количество,
	|	ЗаказыПоставщикам.Заказано / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки1, 1) КАК КоличествоУпаковок,
	|	ЛОЖЬ КАК ВозвратныйОтход,
	|	ВЫБОР
	|		КОГДА ЗаказыПоставщикам.Номенклатура.ИспользованиеХарактеристик В (
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры), 
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры), 
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры))
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ХарактеристикиИспользуются,
	|	ДокЗаказПереработчику.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ИЗ
	|	ЗаказыПоставщикам КАК ЗаказыПоставщикам
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПереработчику КАК ДокЗаказПереработчику
	|	ПО (ДокЗаказПереработчику.Ссылка = ЗаказыПоставщикам.Распоряжение)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПереработчику.Продукция КАК ТаблицаПродукция
	|	ПО (ТаблицаПродукция.Ссылка = ЗаказыПоставщикам.Распоряжение)
	|		И (ТаблицаПродукция.КодСтроки = ЗаказыПоставщикам.КодСтроки)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПереработчику.Услуги КАК ТаблицаПродукцияУслуги
	|	ПО (ТаблицаПродукцияУслуги.Ссылка = ТаблицаПродукция.Ссылка)
	|		И (ТаблицаПродукцияУслуги.НомерГруппыЗатрат = ТаблицаПродукция.НомерГруппыЗатрат)
	|
	|ГДЕ
	|	НЕ ТаблицаПродукция.Ссылка ЕСТЬ NULL
	|	И (&Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ИЛИ ДокЗаказПереработчику.Подразделение = &Подразделение)
	|	И (&Переработчик = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|			ИЛИ ДокЗаказПереработчику.Партнер = &Переработчик)
	|	И (&Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ИЛИ ДокЗаказПереработчику.Организация = &Организация)
	|	И (&НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			ИЛИ ДокЗаказПереработчику.НаправлениеДеятельности = &НаправлениеДеятельности)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаказыПоставщикам.Распоряжение           КАК ЗаказПереработчику,
	|	ДокЗаказПереработчику.Номер              КАК НомерРаспоряжения,
	|	ДокЗаказПереработчику.Дата               КАК ДатаРаспоряжения,
	|	ДокЗаказПереработчику.Организация        КАК Организация,
	|	ДокЗаказПереработчику.Партнер            КАК Переработчик,
	|	ЗаказыПоставщикам.Склад                  КАК Склад,
	|	ЗаказыПоставщикам.КодСтроки              КАК КодСтроки,
	|	ЗаказыПоставщикам.Номенклатура           КАК Номенклатура,
	|	ЗаказыПоставщикам.Характеристика         КАК Характеристика,
	|	ЕСТЬNULL(ТаблицаОтходы.Назначение, ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)) КАК Назначение,
	|	ЕСТЬNULL(ТаблицаОтходы.Упаковка, НЕОПРЕДЕЛЕНО) КАК Упаковка,
	|
	|	ВЫБОР
	|		КОГДА ДокЗаказПереработчику.ПереработкаПоЗаказу
	|			ТОГДА ТаблицаОтходыУслуги.Распоряжение
	|		ИНАЧЕ ДокЗаказПереработчику.Ссылка
	|	КОНЕЦ                                    КАК Распоряжение,
	|
	|	ЗаказыПоставщикам.Заказано               КАК Количество,
	|	ЗаказыПоставщикам.Заказано / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки2, 1) КАК КоличествоУпаковок,
	|	ИСТИНА КАК ВозвратныйОтход,
	|	ВЫБОР
	|		КОГДА ЗаказыПоставщикам.Номенклатура.ИспользованиеХарактеристик В (
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры), 
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры), 
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры))
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ХарактеристикиИспользуются,
	|	ДокЗаказПереработчику.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ИЗ
	|	ЗаказыПоставщикам КАК ЗаказыПоставщикам
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПереработчику КАК ДокЗаказПереработчику
	|	ПО (ДокЗаказПереработчику.Ссылка = ЗаказыПоставщикам.Распоряжение)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПереработчику.ВозвратныеОтходы КАК ТаблицаОтходы
	|	ПО (ТаблицаОтходы.Ссылка = ЗаказыПоставщикам.Распоряжение)
	|		И (ТаблицаОтходы.КодСтроки = ЗаказыПоставщикам.КодСтроки)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПереработчику.Услуги КАК ТаблицаОтходыУслуги
	|	ПО (ТаблицаОтходыУслуги.Ссылка = ТаблицаОтходы.Ссылка)
	|		И (ТаблицаОтходыУслуги.НомерГруппыЗатрат = ТаблицаОтходы.НомерГруппыЗатрат)
	|
	|ГДЕ
	|	НЕ ТаблицаОтходы.Ссылка ЕСТЬ NULL
	|	И (&Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ИЛИ ДокЗаказПереработчику.Подразделение = &Подразделение)
	|	И (&Переработчик = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|			ИЛИ ДокЗаказПереработчику.Партнер = &Переработчик)
	|	И (&Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ИЛИ ДокЗаказПереработчику.Организация = &Организация)
	|	И (&НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			ИЛИ ДокЗаказПереработчику.НаправлениеДеятельности = &НаправлениеДеятельности)
	|
	//++ НЕ УТКА
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаказыПоставщикам.Распоряжение           КАК ЗаказПереработчику,
	|	ДокЗаказПереработчику.Номер              КАК НомерРаспоряжения,
	|	ДокЗаказПереработчику.Дата               КАК ДатаРаспоряжения,
	|	ДокЗаказПереработчику.Организация        КАК Организация,
	|	ДокЗаказПереработчику.Партнер            КАК Переработчик,
	|	ЗаказыПоставщикам.Склад                  КАК Склад,
	|	ЗаказыПоставщикам.КодСтроки              КАК КодСтроки,
	|	ЗаказыПоставщикам.Номенклатура           КАК Номенклатура,
	|	ЗаказыПоставщикам.Характеристика         КАК Характеристика,
	|	ЕСТЬNULL(ТаблицаПродукция.Назначение, ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)) КАК Назначение,
	|	ЕСТЬNULL(ТаблицаПродукция.Упаковка, НЕОПРЕДЕЛЕНО) КАК Упаковка,
	|
	|	ДокЗаказПереработчику.Ссылка             КАК Распоряжение,
	|
	|	ЗаказыПоставщикам.Заказано               КАК Количество,
	|	ЗаказыПоставщикам.Заказано / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки1, 1) КАК КоличествоУпаковок,
	|	ЛОЖЬ КАК ВозвратныйОтход,
	|	ВЫБОР
	|		КОГДА ЗаказыПоставщикам.Номенклатура.ИспользованиеХарактеристик В (
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры), 
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры), 
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры))
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ХарактеристикиИспользуются,
	|	ДокЗаказПереработчику.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ИЗ
	|	ЗаказыПоставщикам КАК ЗаказыПоставщикам
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПереработчику КАК ДокЗаказПереработчику
	|	ПО (ДокЗаказПереработчику.Ссылка = ЗаказыПоставщикам.Распоряжение)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПереработчику.Услуги КАК ТаблицаУслуги
	|	ПО (ТаблицаУслуги.Ссылка = ЗаказыПоставщикам.Распоряжение)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК ТаблицаПродукция
	|	ПО (ТаблицаПродукция.Ссылка = ТаблицаУслуги.Распоряжение)
	|		И (ТаблицаПродукция.КодСтроки = ЗаказыПоставщикам.КодСтроки)
	|
	|ГДЕ
	|	НЕ ТаблицаПродукция.Ссылка ЕСТЬ NULL
	|	И (&Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ИЛИ ДокЗаказПереработчику.Подразделение = &Подразделение)
	|	И (&Переработчик = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|			ИЛИ ДокЗаказПереработчику.Партнер = &Переработчик)
	|	И (&Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ИЛИ ДокЗаказПереработчику.Организация = &Организация)
	|	И (&НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			ИЛИ ДокЗаказПереработчику.НаправлениеДеятельности = &НаправлениеДеятельности)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаказыПоставщикам.Распоряжение           КАК ЗаказПереработчику,
	|	ДокЗаказПереработчику.Номер              КАК НомерРаспоряжения,
	|	ДокЗаказПереработчику.Дата               КАК ДатаРаспоряжения,
	|	ДокЗаказПереработчику.Организация        КАК Организация,
	|	ДокЗаказПереработчику.Партнер            КАК Переработчик,
	|	ЗаказыПоставщикам.Склад                  КАК Склад,
	|	ЗаказыПоставщикам.КодСтроки              КАК КодСтроки,
	|	ЗаказыПоставщикам.Номенклатура           КАК Номенклатура,
	|	ЗаказыПоставщикам.Характеристика         КАК Характеристика,
	|	ЕСТЬNULL(ТаблицаОтходы.Назначение, ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)) КАК Назначение,
	|	ЕСТЬNULL(ТаблицаОтходы.Упаковка, НЕОПРЕДЕЛЕНО) КАК Упаковка,
	|
	|	ДокЗаказПереработчику.Ссылка             КАК Распоряжение,
	|
	|	ЗаказыПоставщикам.Заказано               КАК Количество,
	|	ЗаказыПоставщикам.Заказано / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки2, 1) КАК КоличествоУпаковок,
	|	ИСТИНА КАК ВозвратныйОтход,
	|	ВЫБОР
	|		КОГДА ЗаказыПоставщикам.Номенклатура.ИспользованиеХарактеристик В (
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры), 
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры), 
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры))
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ХарактеристикиИспользуются,
	|	ДокЗаказПереработчику.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ИЗ
	|	ЗаказыПоставщикам КАК ЗаказыПоставщикам
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПереработчику КАК ДокЗаказПереработчику
	|	ПО (ДокЗаказПереработчику.Ссылка = ЗаказыПоставщикам.Распоряжение)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПереработчику.Услуги КАК ТаблицаУслуги
	|	ПО (ТаблицаУслуги.Ссылка = ЗаказыПоставщикам.Распоряжение)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ПобочныеИзделия КАК ТаблицаОтходы
	|	ПО (ТаблицаОтходы.Ссылка = ТаблицаУслуги.Распоряжение)
	|		И (ТаблицаОтходы.КодСтроки = ЗаказыПоставщикам.КодСтроки)
	|
	|ГДЕ
	|	НЕ ТаблицаОтходы.Ссылка ЕСТЬ NULL
	|	И (&Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|			ИЛИ ДокЗаказПереработчику.Подразделение = &Подразделение)
	|	И (&Переработчик = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|			ИЛИ ДокЗаказПереработчику.Партнер = &Переработчик)
	|	И (&Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ИЛИ ДокЗаказПереработчику.Организация = &Организация)
	|	И (&НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			ИЛИ ДокЗаказПереработчику.НаправлениеДеятельности = &НаправлениеДеятельности)
	|
	//-- НЕ УТКА
	|УПОРЯДОЧИТЬ ПО
	|	ДатаРаспоряжения,
	|	НомерРаспоряжения,
	|	ЗаказПереработчику";
	
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковки1",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ТаблицаПродукция.Упаковка",
			"ТаблицаПродукция.Номенклатура"));
		
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковки2",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ТаблицаОтходы.Упаковка",
			"ТаблицаОтходы.Номенклатура"));
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Организация",   Параметры.Организация);
	Запрос.УстановитьПараметр("Подразделение", Параметры.Подразделение);
	Запрос.УстановитьПараметр("Переработчик",  Параметры.Переработчик);
	Запрос.УстановитьПараметр("Склад",         Параметры.Склад);
	Запрос.УстановитьПараметр("Регистратор",   Параметры.Ссылка);
	Запрос.УстановитьПараметр("НаправлениеДеятельности", Параметры.НаправлениеДеятельности);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДанныеСтроки = ТаблицаТоваров.Добавить();
		ЗаполнитьЗначенияСвойств(ДанныеСтроки, Выборка);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиТоварыВДокумент()
	
	СтруктураПоиска = Новый Структура("СтрокаВыбрана", Истина);
 	СписокСтрок = ТаблицаТоваров.НайтиСтроки(СтруктураПоиска);
	
	Если СписокСтрок.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Необходимо выбрать строки.'"));
		Возврат;
	КонецЕсли;
	
	// Снятие модифицированности, т.к. перед закрытием признак проверяется.
	Модифицированность = Ложь;
	
	ДанныеЗаполнения = ДанныеЗаполнения(СписокСтрок);
	
	Если ДанныеЗаполнения <> Неопределено Тогда
		ОповеститьОВыборе(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиТоварыВДокументИЗакрыть(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ПеренестиТоварыВДокумент();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ДанныеЗаполнения(СписокСтрок)

	Организация = Неопределено;
	Переработчик = Неопределено;
	НаправлениеДеятельности = Неопределено;
	
	Для каждого ТекущиеДанные Из СписокСтрок Цикл
		
		Если Организация <> Неопределено 
			И Организация <> ТекущиеДанные.Организация Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Для оформления документа необходимо выбрать заказы одной организации.'"));
			Возврат Неопределено;
		ИначеЕсли Переработчик <> Неопределено 
			И Переработчик <> ТекущиеДанные.Переработчик Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Для оформления документа необходимо выбрать заказы одного переработчика.'"));
			Возврат Неопределено;
		ИначеЕсли НаправлениеДеятельности <> Неопределено 
			И НаправлениеДеятельности <> ТекущиеДанные.НаправлениеДеятельности Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Для оформления документа необходимо выбрать заказы с одинаковым направлением деятельности.'"));
			Возврат Неопределено;
		КонецЕсли;
		
		Переработчик = ТекущиеДанные.Переработчик;
		Организация = ТекущиеДанные.Организация;
		НаправлениеДеятельности = ТекущиеДанные.НаправлениеДеятельности;
		
	КонецЦикла;
	
	ОчиститьСообщения();
	
	РеквизитыШапки = Новый Структура;
	РеквизитыШапки.Вставить("Организация", Организация);
	РеквизитыШапки.Вставить("Партнер", Переработчик);
	РеквизитыШапки.Вставить("НаправлениеДеятельности", НаправлениеДеятельности);
	
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("АдресТоваровВХранилище", ПоместитьТоварыВХранилище());
	ДанныеЗаполнения.Вставить("РеквизитыШапки", РеквизитыШапки);
	
	Возврат ДанныеЗаполнения;

КонецФункции

#КонецОбласти

#КонецОбласти
