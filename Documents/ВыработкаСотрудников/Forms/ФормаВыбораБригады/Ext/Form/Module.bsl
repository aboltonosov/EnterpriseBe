
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Параметры.Свойство("СписокРаспоряжений") Тогда
		ВызватьИсключение НСтр("ru='Обработка не предназначена для непосредственного использования.'");
	КонецЕсли;
	
	СписокРаспоряжений.ЗагрузитьЗначения(Параметры.СписокРаспоряжений);
	ВидНаряда =	Параметры.ВидНаряда;
	Организация =	Параметры.Организация;
	Подразделение =	Параметры.Подразделение;
	
	Элементы.ОтборРабочийЦентр.Видимость = ПоказыватьОтборПоРабочемуЦентру();
	
	ПараметрыОтбора = ХранилищеНастроекДанныхФорм.Загрузить("ФормаВыбораБригады", "ПараметрыОтбора");
	Если ЗначениеЗаполнено(ПараметрыОтбора) И ВидНаряда = Перечисления.ВидыБригадныхНарядов.Производство Тогда
		ЗаполнитьЗначенияСвойств(ЭтаФорма, ПараметрыОтбора);
	КонецЕсли;
	
	//++ НЕ УТКА
	ОпределитьУправлениеМаршрутнымиЛистами();
	
	ЗаполнитьСписокБригад();
	//-- НЕ УТКА
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РабочийЦентрПриИзменении(Элемент)
	
	//++ НЕ УТКА
	РабочийЦентрПриИзмененииВызовСервера();
	//-- НЕ УТКА
	
	Возврат; // в КА обработчик пустой
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокБригад

&НаКлиенте
Процедура СписокБригадВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВыбратьИЗакрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаВыбратьИЗакрыть(Команда)
	
	ВыбратьИЗакрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//++ НЕ УТКА

&НаСервере
Процедура РабочийЦентрПриИзмененииВызовСервера()

	ПараметрыОтбора = Новый Структура("ОтборРабочийЦентр");
	ЗаполнитьЗначенияСвойств(ПараметрыОтбора, ЭтаФорма);
	ХранилищеНастроекДанныхФорм.Сохранить("ФормаВыбораБригады", "ПараметрыОтбора", ПараметрыОтбора);
	
	ЗаполнитьСписокБригад();

КонецПроцедуры

&НаСервере
Процедура ОпределитьУправлениеМаршрутнымиЛистами()

	Если НЕ Подразделение.Пустая() Тогда
		ПараметрыПодразделения = ПроизводствоСервер.ПараметрыПроизводственногоПодразделения(Подразделение);
		УправлениеМаршрутнымиЛистами = ПараметрыПодразделения.УправлениеМаршрутнымиЛистами;
	Иначе
		УправлениеМаршрутнымиЛистами = Перечисления.УправлениеМаршрутнымиЛистами.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокБригад()
	
	СтруктураОтбора = Документы.ВыработкаСотрудников.ТрудозатратыКОформлениюОтборПоУмолчанию();
	СтруктураОтбора.Распоряжения = СписокРаспоряжений.ВыгрузитьЗначения();
	СтруктураОтбора.РабочийЦентр = ОтборРабочийЦентр;
	СтруктураОтбора.Организация  = Организация;
	
	ТаблицаТрудозатратыКОформлению = Документы.ВыработкаСотрудников.ТрудозатратыКОформлению(СтруктураОтбора);
	
	ТаблицаТрудозатратыКОформлению.Свернуть("Бригада,Автораспределение");
	ТаблицаТрудозатратыКОформлению.Сортировать("Бригада");
	
	СписокБригад.Загрузить(ТаблицаТрудозатратыКОформлению);
	
КонецПроцедуры

//-- НЕ УТКА

&НаКлиенте
Процедура ВыбратьИЗакрыть()
	
	ТекущиеДанные = Элементы.СписокБригад.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Распоряжения",       СписокРаспоряжений.ВыгрузитьЗначения());
	ЗначенияЗаполнения.Вставить("Организация",        Организация);
	ЗначенияЗаполнения.Вставить("Подразделение",      Подразделение);
	ЗначенияЗаполнения.Вставить("ВидНаряда",          ВидНаряда);
	ЗначенияЗаполнения.Вставить("Бригада",            ТекущиеДанные.Бригада);
	ЗначенияЗаполнения.Вставить("Автораспределение",  ТекущиеДанные.Автораспределение);
	ЗначенияЗаполнения.Вставить("ПоРаспоряжениям",    Истина);
	ЗначенияЗаполнения.Вставить("ЗаполнятьСотрудников", Истина);
	
	//++ НЕ УТКА
	Если НЕ ОтборРабочийЦентр.Пустая() Тогда
		ЗначенияЗаполнения.Вставить("РабочийЦентр", ОтборРабочийЦентр);
	КонецЕсли;
	//-- НЕ УТКА
	
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Документ.ВыработкаСотрудников.ФормаОбъекта", ПараметрыФормы);
	
	Закрыть();
	
КонецПроцедуры

&НаСервере
Функция ПоказыватьОтборПоРабочемуЦентру()
	
	Результат = Ложь;
	
	//++ НЕ УТКА
	
	Для каждого КлючИЗначение Из СписокРаспоряжений Цикл
		
		Если ТипЗнч(КлючИЗначение.Значение) = Тип("ДокументСсылка.МаршрутныйЛистПроизводства") Тогда
			
			Результат = Истина;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	//-- НЕ УТКА
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
