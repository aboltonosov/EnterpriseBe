#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры.ДанныеЗаполнения);
	
	КоличествоПоказатель = ПолучитьПоказательКоличества(Количество);
	
	СписокКодовПоставки = КонтролируемыеСделкиПовтИсп.ПолучитьКодыУсловийПоставки();
	Элементы.КодУсловийПоставки.СписокВыбора.Очистить();
	Для Каждого Код Из СписокКодовПоставки Цикл
		НовыйКод = Элементы.КодУсловийПоставки.СписокВыбора.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйКод, Код);
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КоличествоПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Цена) Тогда
		Стоимость = Цена * Количество;
	ИначеЕсли ЗначениеЗаполнено(Стоимость) Тогда 
		Цена = ?(ЗначениеЗаполнено(Количество), Стоимость / Количество, 0);
	КонецЕсли;
	КоличествоПоказатель = ПолучитьПоказательКоличества(Количество);
КонецПроцедуры

&НаКлиенте
Процедура ЦенаПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Количество) Тогда 
		Стоимость = Цена * Количество;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СтоимостьПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Количество) Тогда 
		Цена = Стоимость / Количество;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	Если ПроверитьЗаполнение() Тогда
		Модифицированность = Ложь;
		СтруктураЗакрытия = ВернутьСтруктуруЗакрытия();
		Закрыть(СтруктураЗакрытия);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ВернутьСтруктуруЗакрытия()
	
	Структура = Новый Структура();
	
	Для Каждого Реквизит Из ЭтаФорма.ПолучитьРеквизиты() Цикл
		Структура.Вставить(Реквизит.Имя, ЭтаФорма[Реквизит.Имя]);
	КонецЦикла;
	
	Возврат Структура;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьПоказательКоличества(Количество)
	
	Если Количество > 0 И Количество < 1 Тогда
		Возврат 1;
	Иначе
		Возврат Окр(Количество, 0);
	КонецЕсли;
	
КонецФункции

#КонецОбласти