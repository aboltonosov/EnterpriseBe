// СтандартныеПодсистемы.РаботаСКонтрагентами
&НаКлиенте
Перем ПроверкаКонтрагентовПараметрыОбработчикаОжидания Экспорт;

&НаКлиенте
Перем ФормаДлительнойОперации Экспорт;
// Конец СтандартныеПодсистемы.РаботаСКонтрагентами

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();

	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	// СтандартныеПодсистемы.РаботаСКонтрагентами
	Если Объект.ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиПоАвансамКлиентов 
		Или Объект.ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиПоАвансамПоставщикам
		ИЛИ Объект.ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиЗадолженностиПередПоставщиками Тогда
		ПроверкаКонтрагентов.ПриСозданииНаСервереДокумент(ЭтотОбъект, Параметры);
		ПроверкаКонтрагентовВызовСервераПереопределяемыйУТ.ФормаДокументаПриСозданииНаСервере(ЭтотОбъект);
		ПроверкаКонтрагентовВключена = ПроверкаКонтрагентов.ПроверкаКонтрагентовВключена();
		Элементы.АвансыКлиентовЭтоНекорректныйКонтрагент.Видимость    = ПроверкаКонтрагентовВключена;
		Элементы.АвансыПоставщикамЭтоНекорректныйКонтрагент.Видимость = ПроверкаКонтрагентовВключена;
	КонецЕсли;
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами
	
	ЗаполнитьСпискиВыбораТипаРасчетов();
	ЗаполнитьТипОбъектаРасчетов();
	УстановитьПараметрыВыбораПартнера();
	УстановитьВидимость();

	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	ИспользоватьНесколькоВалют = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВалют");

	УстановитьЗаголовок();
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		ПриСозданииЧтенииНаСервере();
	КонецЕсли;

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

	ВводНаОсновании.ПриСозданииНаСервере(ЭтаФорма, Элементы.ПодменюСоздатьНаОсновании);
	
	МенюОтчеты.ПриСозданииНаСервере(ЭтаФорма, Элементы.ПодменюОтчеты);

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// Обработчик механизма "ДатыЗапретаИзменения"
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
	ПриСозданииЧтенииНаСервере();

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	ЗаполнитьТипОбъектаРасчетов();
	УстановитьЗаголовок();
	
	// СтандартныеПодсистемы.РаботаСКонтрагентами
	Если Объект.ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиПоАвансамКлиентов
		Или Объект.ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиПоАвансамПоставщикам Тогда
		ПроверкаКонтрагентов.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)

	ПараметрыЗаписи.Вставить("ТипОперации", Объект.ТипОперации);
	Оповестить("Запись_ВводОстатков", ПараметрыЗаписи, Объект.Ссылка);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.РаботаСКонтрагентами
	Если Объект.ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыОперацийВводаОстатков.ОстаткиПоАвансамКлиентов") 
		Или Объект.ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыОперацийВводаОстатков.ОстаткиПоАвансамПоставщикам") Тогда
		ПроверкаКонтрагентовКлиент.ПриОткрытииДокумент(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.РаботаСКонтрагентами
	Если Объект.ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыОперацийВводаОстатков.ОстаткиПоАвансамКлиентов")
		Или Объект.ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыОперацийВводаОстатков.ОстаткиПоАвансамПоставщикам") Тогда
		ПроверкаКонтрагентовКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.РаботаСКонтрагентами
	Если Объект.ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыОперацийВводаОстатков.ОстаткиПоАвансамКлиентов")
		Или Объект.ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыОперацийВводаОстатков.ОстаткиПоАвансамПоставщикам") Тогда
		ПроверкаКонтрагентов.ПередЗаписьюНаСервереДокумент(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.РаботаСКонтрагентами
	Если Объект.ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыОперацийВводаОстатков.ОстаткиПоАвансамКлиентов")
		Или Объект.ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыОперацийВводаОстатков.ОстаткиПоАвансамПоставщикам") Тогда
		ПроверкаКонтрагентов.ПриЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)

	Объект.Префикс = СформироватьПрефикс(Объект.Организация);

КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение <> Объект.Организация Тогда
		ОчиститьНомерДатуОбъектовРасчета();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнятьНомераПриИзменении(Элемент)

	Элементы.Префикс.Доступность = Объект.ДополнятьНомера;
	Если НЕ ЗначениеЗаполнено(Объект.Префикс) И Объект.ДополнятьНомера Тогда
		Объект.Префикс = СформироватьПрефикс(Объект.Организация);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	// СтандартныеПодсистемы.РаботаСКонтрагентами
	Если Объект.ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыОперацийВводаОстатков.ОстаткиПоАвансамКлиентов")
		Или Объект.ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыОперацийВводаОстатков.ОстаткиПоАвансамПоставщикам") Тогда
		ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Объект.Дата);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура РазделУчетаПриИзменении(Элемент)
	
	НачОстаткиРеглУчета = Объект.РазделУчета = ПредопределенноеЗначение("Перечисление.РазделыВводаНачальныхОстатков.РегламентированныйУчет");
	Элементы.ЗадолженностьКлиентовЗаполнитьПоОстаткам.Видимость = НачОстаткиРеглУчета;
	Элементы.ЗадолженностьПоставщикамЗаполнитьПоОстаткам.Видимость = НачОстаткиРеглУчета;
	Элементы.АвансыКлиентовЗаполнитьПоОстаткам.Видимость = НачОстаткиРеглУчета;
	Элементы.АвансыПоставщикамЗаполнитьПоОстаткам.Видимость = НачОстаткиРеглУчета;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовПодвалаФормы

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЗадолженностьКлиентов

&НаКлиенте
Процедура ЗадолженностьКлиентовПартнерПриИзменении(Элемент)

	ТекущиеДанные = Элементы.ЗадолженностьКлиентов.ТекущиеДанные;
	ТекущаяСтрокаИдентификатор = ТекущиеДанные.ПолучитьИдентификатор();
	ПартнерПриИзменении(ТекущиеДанные.Партнер, ТекущиеДанные.Контрагент, ТекущаяСтрокаИдентификатор);

КонецПроцедуры

&НаКлиенте
Процедура ЗадолженностьКлиентовКонтрагентПриИзменении(Элемент)

	ТекущиеДанные = Элементы.ЗадолженностьКлиентов.ТекущиеДанные;
	ТекущаяСтрокаИдентификатор = ТекущиеДанные.ПолучитьИдентификатор();
	ПроверитьДокументРасчетовСервер(ТекущаяСтрокаИдентификатор);

КонецПроцедуры

&НаКлиенте
Процедура ЗадолженностьКлиентовТипРасчетовОткрытие(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	РасчетныйДокумент = Элементы.ЗадолженностьКлиентов.ТекущиеДанные.РасчетныйДокумент;
	Если ЗначениеЗаполнено(РасчетныйДокумент) и ТипЗнч(РасчетныйДокумент) <> Тип("ПеречислениеСсылка.ТипыДоговоров") Тогда
		ПоказатьЗначение(Неопределено, РасчетныйДокумент);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗадолженностьКлиентовТипРасчетовОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	СтрокаТаблицы = Элементы.ЗадолженностьКлиентов.ТекущиеДанные;
	ТипРасчетовОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка, СтрокаТаблицы);

КонецПроцедуры

&НаКлиенте
Процедура ЗадолженностьКлиентовНомерРасчетногоДокументаПриИзменении(Элемент)

	ТекущиеДанные = Элементы.ЗадолженностьКлиентов.ТекущиеДанные;
	НомерРасчетногоДокументаПриИзменении(ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура ЗадолженностьКлиентовДатаРасчетногоДокументаПриИзменении(Элемент)

	ТекущиеДанные = Элементы.ЗадолженностьКлиентов.ТекущиеДанные;
	ДатаРасчетногоДокументаПриИзменении(ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура ЗадолженностьКлиентовСуммаПриИзменении(Элемент)

	ТекущиеДанные = Элементы.ЗадолженностьКлиентов.ТекущиеДанные;
	СуммаПриИзменении(ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура ЗадолженностьКлиентовВалютаПриИзменении(Элемент)

	ТекущиеДанные = Элементы.ЗадолженностьКлиентов.ТекущиеДанные;
	ВалютаПриИзменении(ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура ЗадолженностьКлиентовПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)

	ЗаполнитьВалютуВзаиморасчетовВСтроке(Элементы.ЗадолженностьКлиентов.ТекущиеДанные);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЗадолженностьПередПоставщиками

&НаКлиенте
Процедура ЗадолженностьПоставщикамПартнерПриИзменении(Элемент)

	ТекущиеДанные = Элементы.ЗадолженностьПоставщикам.ТекущиеДанные;
	ТекущаяСтрокаИдентификатор = ТекущиеДанные.ПолучитьИдентификатор();
	ПартнерПриИзменении(ТекущиеДанные.Партнер, ТекущиеДанные.Контрагент, ТекущаяСтрокаИдентификатор);

КонецПроцедуры

&НаКлиенте
Процедура ЗадолженностьПоставщикамКонтрагентПриИзменении(Элемент)

	ТекущиеДанные = Элементы.ЗадолженностьПоставщикам.ТекущиеДанные;
	ТекущаяСтрокаИдентификатор = ТекущиеДанные.ПолучитьИдентификатор();
	ПроверитьДокументРасчетовСервер(ТекущаяСтрокаИдентификатор);

КонецПроцедуры

&НаКлиенте
Процедура ЗадолженностьПоставщикамТипРасчетовОткрытие(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	РасчетныйДокумент = Элементы.ЗадолженностьПоставщикам.ТекущиеДанные.РасчетныйДокумент;
	Если ЗначениеЗаполнено(РасчетныйДокумент) и ТипЗнч(РасчетныйДокумент) <> Тип("ПеречислениеСсылка.ТипыДоговоров") Тогда
		ПоказатьЗначение(Неопределено, РасчетныйДокумент);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗадолженностьПоставщикамТипРасчетовОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	СтрокаТаблицы = Элементы.ЗадолженностьПоставщикам.ТекущиеДанные;
	ТипРасчетовОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка, СтрокаТаблицы);

КонецПроцедуры

&НаКлиенте
Процедура ЗадолженностьПоставщикамНомерРасчетногоДокументаПриИзменении(Элемент)

	ТекущиеДанные = Элементы.ЗадолженностьПоставщикам.ТекущиеДанные;
	НомерРасчетногоДокументаПриИзменении(ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура ЗадолженностьПоставщикамДатаРасчетногоДокументаПриИзменении(Элемент)

	ТекущиеДанные = Элементы.ЗадолженностьПоставщикам.ТекущиеДанные;
	ДатаРасчетногоДокументаПриИзменении(ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура ЗадолженностьПоставщикамСуммаПриИзменении(Элемент)

	ТекущиеДанные = Элементы.ЗадолженностьПоставщикам.ТекущиеДанные;
	СуммаПриИзменении(ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура ЗадолженностьПоставщикамВалютаПриИзменении(Элемент)

	ТекущиеДанные = Элементы.ЗадолженностьПоставщикам.ТекущиеДанные;
	ВалютаПриИзменении(ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура ЗадолженностьПоставщикамПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)

	ЗаполнитьВалютуВзаиморасчетовВСтроке(Элементы.ЗадолженностьПоставщикам.ТекущиеДанные);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыАвансыКлиентов

&НаКлиенте
Процедура АвансыКлиентовПартнерПриИзменении(Элемент)

	ТекущиеДанные = Элементы.АвансыКлиентов.ТекущиеДанные;
	ТекущаяСтрокаИдентификатор = ТекущиеДанные.ПолучитьИдентификатор();
	ПартнерПриИзменении(ТекущиеДанные.Партнер, ТекущиеДанные.Контрагент, ТекущаяСтрокаИдентификатор);
	
	// СтандартныеПодсистемы.РаботаСКонтрагентами
	Если Объект.ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыОперацийВводаОстатков.ОстаткиПоАвансамКлиентов")
		Или Объект.ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыОперацийВводаОстатков.ОстаткиПоАвансамПоставщикам") Тогда
		ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Объект.РасчетыСПартнерами);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами

КонецПроцедуры

&НаКлиенте
Процедура АвансыКлиентовКонтрагентПриИзменении(Элемент)

	ТекущиеДанные = Элементы.АвансыКлиентов.ТекущиеДанные;
	ТекущаяСтрокаИдентификатор = ТекущиеДанные.ПолучитьИдентификатор();
	ПроверитьДокументРасчетовСервер(ТекущаяСтрокаИдентификатор);
	
	// СтандартныеПодсистемы.РаботаСКонтрагентами
	Если Объект.ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыОперацийВводаОстатков.ОстаткиПоАвансамКлиентов")
		Или Объект.ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыОперацийВводаОстатков.ОстаткиПоАвансамПоставщикам") Тогда
		ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Объект.РасчетыСПартнерами);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами

КонецПроцедуры

&НаКлиенте
Процедура АвансыКлиентовТипРасчетовОткрытие(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	РасчетныйДокумент = Элементы.АвансыКлиентов.ТекущиеДанные.РасчетныйДокумент;
	Если ЗначениеЗаполнено(РасчетныйДокумент) и ТипЗнч(РасчетныйДокумент) <> Тип("ПеречислениеСсылка.ТипыДоговоров") Тогда
		ПоказатьЗначение(Неопределено, РасчетныйДокумент);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура АвансыКлиентовТипРасчетовОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	СтрокаТаблицы = Элементы.АвансыКлиентов.ТекущиеДанные;
	ТипРасчетовОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка, СтрокаТаблицы);

КонецПроцедуры

&НаКлиенте
Процедура АвансыКлиентовНомерРасчетногоДокументаПриИзменении(Элемент)

	ТекущиеДанные = Элементы.АвансыКлиентов.ТекущиеДанные;
	НомерРасчетногоДокументаПриИзменении(ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура АвансыКлиентовДатаРасчетногоДокументаПриИзменении(Элемент)

	ТекущиеДанные = Элементы.АвансыКлиентов.ТекущиеДанные;
	ДатаРасчетногоДокументаПриИзменении(ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура АвансыКлиентовСуммаПриИзменении(Элемент)

	ТекущиеДанные = Элементы.АвансыКлиентов.ТекущиеДанные;
	СуммаПриИзменении(ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура АвансыКлиентовВалютаПриИзменении(Элемент)

	ТекущиеДанные = Элементы.АвансыКлиентов.ТекущиеДанные;
	ВалютаПриИзменении(ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура АвансыКлиентовПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)

	ЗаполнитьВалютуВзаиморасчетовВСтроке(Элементы.АвансыКлиентов.ТекущиеДанные);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыАвансыПоставщикам

&НаКлиенте
Процедура АвансыПоставщикамПартнерПриИзменении(Элемент)

	ТекущиеДанные = Элементы.АвансыПоставщикам.ТекущиеДанные;
	ТекущаяСтрокаИдентификатор = ТекущиеДанные.ПолучитьИдентификатор();
	ПартнерПриИзменении(ТекущиеДанные.Партнер, ТекущиеДанные.Контрагент, ТекущаяСтрокаИдентификатор);
	
	// СтандартныеПодсистемы.РаботаСКонтрагентами
	Если Объект.ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыОперацийВводаОстатков.ОстаткиПоАвансамКлиентов")
		Или Объект.ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыОперацийВводаОстатков.ОстаткиПоАвансамПоставщикам") Тогда
		ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Объект.РасчетыСПартнерами);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами

КонецПроцедуры

&НаКлиенте
Процедура АвансыПоставщикамКонтрагентПриИзменении(Элемент)

	ТекущиеДанные = Элементы.АвансыПоставщикам.ТекущиеДанные;
	ТекущаяСтрокаИдентификатор = ТекущиеДанные.ПолучитьИдентификатор();
	ПроверитьДокументРасчетовСервер(ТекущаяСтрокаИдентификатор);
	
	// СтандартныеПодсистемы.РаботаСКонтрагентами
	Если Объект.ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыОперацийВводаОстатков.ОстаткиПоАвансамКлиентов")
		Или Объект.ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыОперацийВводаОстатков.ОстаткиПоАвансамПоставщикам") Тогда
		ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Объект.РасчетыСПартнерами);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами

КонецПроцедуры

&НаКлиенте
Процедура АвансыПоставщикамТипРасчетовОткрытие(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	РасчетныйДокумент = Элементы.АвансыПоставщикам.ТекущиеДанные.РасчетныйДокумент;
	Если ЗначениеЗаполнено(РасчетныйДокумент) и ТипЗнч(РасчетныйДокумент) <> Тип("ПеречислениеСсылка.ТипыДоговоров") Тогда
		ПоказатьЗначение(Неопределено, РасчетныйДокумент);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура АвансыПоставщикамТипРасчетовОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	СтрокаТаблицы = Элементы.АвансыПоставщикам.ТекущиеДанные;
	ТипРасчетовОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка, СтрокаТаблицы);

КонецПроцедуры

&НаКлиенте
Процедура АвансыПоставщикамНомерРасчетногоДокументаПриИзменении(Элемент)

	ТекущиеДанные = Элементы.АвансыПоставщикам.ТекущиеДанные;
	НомерРасчетногоДокументаПриИзменении(ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура АвансыПоставщикамДатаРасчетногоДокументаПриИзменении(Элемент)

	ТекущиеДанные = Элементы.АвансыПоставщикам.ТекущиеДанные;
	ДатаРасчетногоДокументаПриИзменении(ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура АвансыПоставщикамСуммаПриИзменении(Элемент)

	ТекущиеДанные = Элементы.АвансыПоставщикам.ТекущиеДанные;
	СуммаПриИзменении(ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура АвансыПоставщикамВалютаПриИзменении(Элемент)

	ТекущиеДанные = Элементы.АвансыПоставщикам.ТекущиеДанные;
	ВалютаПриИзменении(ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура АвансыПоставщикамПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)

	ЗаполнитьВалютуВзаиморасчетовВСтроке(Элементы.АвансыПоставщикам.ТекущиеДанные);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуОтчет(Команда)
	
	МенюОтчетыКлиент.ВыполнитьПодключаемуюКомандуОтчет(Команда, ЭтаФорма, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуСоздатьНаОсновании(Команда)
	
	ВводНаОснованииКлиент.ВыполнитьПодключаемуюКомандуСоздатьНаОсновании(Команда, ЭтаФорма, Объект);
	
КонецПроцедуры


&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОстаткам(Команда)
	//++ НЕ УТ
	Если Объект.РасчетыСПартнерами.Количество() Тогда
		ОповещениеВопросЗаполнитьПоОстаткам = Новый ОписаниеОповещения("ОбработчикОповещенияВопросЗаполнитьПоОстаткам", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'При заполнении текущие данные табличной части будут очищены. Продолжить?'");
		ПоказатьВопрос(ОповещениеВопросЗаполнитьПоОстаткам, ТекстВопроса, РежимДиалогаВопрос.ДаНет,,,  НСтр("ru = 'Ввод остатков регл. учета по данным оперативного'"));
	Иначе
		ЗаполнитьПоОстаткамНаСервере();
	КонецЕсли;
	//-- НЕ УТ
	Возврат;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы["ЗадолженностьКлиентовНомерРасчетногоДокумента"].Имя);
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы["ЗадолженностьКлиентовДатаРасчетногоДокумента"].Имя);
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы["ЗадолженностьПоставщикамНомерРасчетногоДокумента"].Имя);
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы["ЗадолженностьПоставщикамДатаРасчетногоДокумента"].Имя);
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы["АвансыКлиентовНомерРасчетногоДокумента"].Имя);
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы["АвансыКлиентовДатаРасчетногоДокумента"].Имя);
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы["АвансыПоставщикамНомерРасчетногоДокумента"].Имя);
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы["АвансыПоставщикамДатаРасчетногоДокумента"].Имя);
	
	ГруппаОтбора = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(Элемент.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(ГруппаОтбора, "Объект.РасчетыСПартнерами.ТипРасчетов", НСтр("ru = 'Договор с контрагентом'"));
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(ГруппаОтбора, "Объект.РасчетыСПартнерами.РасчетныйДокумент", Неопределено);
	//++ НЕ УТ
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(ГруппаОтбора, "Объект.РазделУчета", Перечисления.РазделыВводаНачальныхОстатков.РегламентированныйУчет);
	//-- НЕ УТ
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы["ЗадолженностьКлиентовТипРасчетов"].Имя);
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы["ЗадолженностьПоставщикамТипРасчетов"].Имя);
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы["АвансыКлиентовТипРасчетов"].Имя);
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы["АвансыПоставщикамТипРасчетов"].Имя);
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "Объект.РасчетыСПартнерами.РасчетныйДокумент", Неопределено);

	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не указан'"));

	//

	Элемент = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы["ЗадолженностьКлиентовНомерРасчетногоДокумента"].Имя);
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы["ЗадолженностьКлиентовДатаРасчетногоДокумента"].Имя);
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы["ЗадолженностьПоставщикамНомерРасчетногоДокумента"].Имя);
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы["ЗадолженностьПоставщикамДатаРасчетногоДокумента"].Имя);
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы["АвансыКлиентовНомерРасчетногоДокумента"].Имя);
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы["АвансыКлиентовДатаРасчетногоДокумента"].Имя);
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы["АвансыПоставщикамНомерРасчетногоДокумента"].Имя);
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы["АвансыПоставщикамДатаРасчетногоДокумента"].Имя);
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "Объект.РасчетыСПартнерами.РасчетныйДокумент", Неопределено);

	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	//++ НЕ УТ
	//Снятие отметки незаполненного при разделе учета "Регламентированный"
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы["ЗадолженностьКлиентовДатаПлатежа"].Имя);
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(Элемент.Поля, Элементы["ЗадолженностьПоставщикамДатаПлатежа"].Имя);
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Элемент.Отбор, "Объект.РазделУчета", Перечисления.РазделыВводаНачальныхОстатков.РегламентированныйУчет);
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	//-- НЕ УТ

КонецПроцедуры

&НаСервере
Процедура ПриСозданииЧтенииНаСервере()

	Элементы.Префикс.Доступность = Объект.ДополнятьНомера;

КонецПроцедуры

&НаСервере
Процедура УстановитьВидимость()

	ТипРасчетногоДокумента = Неопределено;
	УстановитьТипДокументаРасчетов(ТипРасчетногоДокумента);
	Если ТипРасчетногоДокумента <> Неопределено Тогда
		МетаданныеРасчетногоДокумента = ТипРасчетногоДокумента.Метаданные();
	КонецЕсли;
	Элементы.ГруппаЗадолженностьКлиентов.Видимость				= Объект.ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиЗадолженностиКлиентов;
	Элементы.ГруппаЗадолженностьПередПоставщиками.Видимость		= Объект.ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиЗадолженностиПередПоставщиками;
	Элементы.ГруппаАвансыКлиентов.Видимость						= Объект.ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиПоАвансамКлиентов;
	Элементы.ГруппаАвансыПоставщикам.Видимость					= Объект.ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиПоАвансамПоставщикам;

	НачОстаткиРеглУчета = Объект.РазделУчета = ПредопределенноеЗначение("Перечисление.РазделыВводаНачальныхОстатков.РегламентированныйУчет");
	Элементы.ЗадолженностьКлиентовЗаполнитьПоОстаткам.Видимость = НачОстаткиРеглУчета;
	Элементы.ЗадолженностьПоставщикамЗаполнитьПоОстаткам.Видимость = НачОстаткиРеглУчета;
	Элементы.АвансыКлиентовЗаполнитьПоОстаткам.Видимость = НачОстаткиРеглУчета;
	Элементы.АвансыПоставщикамЗаполнитьПоОстаткам.Видимость = НачОстаткиРеглУчета;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовок()
	
	АвтоЗаголовок = Ложь;
	Заголовок = Документы.ВводОстатков.ЗаголовокДокументаПоТипуОперации(Объект.Ссылка,
																						  Объект.Номер,
																						  Объект.Дата,
																						  Объект.ТипОперации);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСпискиВыбораТипаРасчетов()

	Если Объект.ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиПоАвансамКлиентов Тогда
		СписокВыбора = Элементы.АвансыКлиентовТипРасчетов.СписокВыбора;
		СписокВыбора.Очистить();
		СписокВыбора.Добавить("Не указан", НСтр("ru = 'Не указан'"));
		СписокВыбора.Добавить("Выбор", НСтр("ru = 'Выбрать объект расчетов'"));
		Если ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСКлиентами") Тогда
			СписокВыбора.Добавить(Перечисления.ТипыДоговоров.СПокупателем, НСтр("ru = 'Договор с покупателем'")); 
			Если ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриПродажах") Тогда
				СписокВыбора.Добавить(Перечисления.ТипыДоговоров.СКомиссионером, НСтр("ru = 'Договор с комиссионером'")); 
			КонецЕсли;
		КонецЕсли;
		Если ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыКлиентов") Тогда
			СписокВыбора.Добавить(Документы.ЗаказКлиента.ПустаяСсылка(), НСтр("ru = 'Заказ клиента'"));
		КонецЕсли;
		Если ПолучитьФункциональнуюОпцию("ИспользоватьЗаявкиНаВозвратТоваровОтКлиентов") Тогда
			СписокВыбора.Добавить(Документы.ЗаявкаНаВозвратТоваровОтКлиента.ПустаяСсылка(), НСтр("ru = 'Заявка на возврат товаров от клиента'"));
		КонецЕсли;
//++ НЕ УТКА
		Если ПолучитьФункциональнуюОпцию("ИспользоватьПроизводствоИзДавальческогоСырья") Тогда
				СписокВыбора.Добавить(Перечисления.ТипыДоговоров.СДавальцем, НСтр("ru = 'Договор с давальцем'"));
				СписокВыбора.Добавить(Документы.ОтчетДавальцу.ПустаяСсылка(), НСтр("ru = 'Отчет давальцу'"));
		КонецЕсли;
//-- НЕ УТКА
		СписокВыбора.Добавить(Документы.РеализацияТоваровУслуг.ПустаяСсылка(), НСтр("ru = 'Реализация товаров и услуг'"));
		СписокВыбора.Добавить(Документы.РеализацияУслугПрочихАктивов.ПустаяСсылка(), НСтр("ru = 'Реализация услуг и прочих активов'"));
	ИначеЕсли Объект.ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиЗадолженностиКлиентов Тогда
		СписокВыбора = Элементы.ЗадолженностьКлиентовТипРасчетов.СписокВыбора;
		СписокВыбора.Очистить();
		СписокВыбора.Добавить("Не указан", НСтр("ru = 'Не указан'"));
		СписокВыбора.Добавить("Выбор", НСтр("ru = 'Выбрать объект расчетов'"));
		Если ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСКлиентами") Тогда
			СписокВыбора.Добавить(Перечисления.ТипыДоговоров.СПокупателем, НСтр("ru = 'Договор с покупателем'")); 
			Если ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриПродажах") Тогда
				СписокВыбора.Добавить(Перечисления.ТипыДоговоров.СКомиссионером, НСтр("ru = 'Договор с комиссионером'")); 
				СписокВыбора.Добавить(Документы.ОтчетКомиссионера.ПустаяСсылка(), НСтр("ru = 'Отчет комиссионера о продажах'"));
				СписокВыбора.Добавить(Документы.ОтчетКомиссионераОСписании.ПустаяСсылка(), НСтр("ru = 'Отчет комиссионера о списании'"));
			КонецЕсли;
		КонецЕсли;
		СписокВыбора.Добавить(Документы.АктВыполненныхРабот.ПустаяСсылка(), НСтр("ru = 'Акт выполненных работ'"));
		Если ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыКлиентов") Тогда
			СписокВыбора.Добавить(Документы.ЗаказКлиента.ПустаяСсылка(), НСтр("ru = 'Заказ клиента'"));
		КонецЕсли;
		Если ПолучитьФункциональнуюОпцию("ИспользоватьЗаявкиНаВозвратТоваровОтКлиентов") Тогда
			СписокВыбора.Добавить(Документы.ЗаявкаНаВозвратТоваровОтКлиента.ПустаяСсылка(), НСтр("ru = 'Заявка на возврат товаров от клиента'"));
		КонецЕсли;
		Если ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриЗакупках") Тогда
			СписокВыбора.Добавить(Документы.ОтчетКомитенту.ПустаяСсылка(), НСтр("ru = 'Отчет комитенту о продажах'"));
		КонецЕсли;
//++ НЕ УТКА
		Если ПолучитьФункциональнуюОпцию("ИспользоватьПроизводствоИзДавальческогоСырья") Тогда
			СписокВыбора.Добавить(Перечисления.ТипыДоговоров.СДавальцем, НСтр("ru = 'Договор с давальцем'"));
			СписокВыбора.Добавить(Документы.ОтчетДавальцу.ПустаяСсылка(), НСтр("ru = 'Отчет давальцу'"));
		КонецЕсли;
//-- НЕ УТКА
		СписокВыбора.Добавить(Документы.РеализацияТоваровУслуг.ПустаяСсылка(), НСтр("ru = 'Реализация товаров и услуг'"));
		СписокВыбора.Добавить(Документы.РеализацияУслугПрочихАктивов.ПустаяСсылка(), НСтр("ru = 'Реализация услуг и прочих активов'"));
	ИначеЕсли Объект.ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиПоАвансамПоставщикам Тогда
		СписокВыбора = Элементы.АвансыПоставщикамТипРасчетов.СписокВыбора;
		СписокВыбора.Очистить();
		СписокВыбора.Добавить("Не указан", НСтр("ru = 'Не указан'"));
		СписокВыбора.Добавить("Выбор", НСтр("ru = 'Выбрать объект расчетов'"));
		СписокВыбора.Добавить(Перечисления.ТипыДоговоров.СПоставщиком, НСтр("ru = 'Договор с поставщиком'")); 
		Если ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриЗакупках") Тогда
			СписокВыбора.Добавить(Перечисления.ТипыДоговоров.СКомитентом, НСтр("ru = 'Договор с комитентом'"));
		КонецЕсли;
		Если ПолучитьФункциональнуюОпцию("ИспользоватьИмпортныеЗакупки") Тогда
			СписокВыбора.Добавить(Перечисления.ТипыДоговоров.Импорт, НСтр("ru = 'Договор импорта'"));
		КонецЕсли;
		Если ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыПоставщикам") Тогда
			СписокВыбора.Добавить(Документы.ЗаказПоставщику.ПустаяСсылка(), НСтр("ru = 'Заказ поставщику'"));
		КонецЕсли;
		СписокВыбора.Добавить(Документы.ПоступлениеТоваровУслуг.ПустаяСсылка(), НСтр("ru = 'Поступление товаров и услуг'"));
		СписокВыбора.Добавить(Документы.ПоступлениеУслугПрочихАктивов.ПустаяСсылка(), НСтр("ru = 'Поступление услуг и прочих активов'"));
	ИначеЕсли Объект.ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиЗадолженностиПередПоставщиками Тогда
		СписокВыбора = Элементы.ЗадолженностьПоставщикамТипРасчетов.СписокВыбора;
		СписокВыбора.Очистить();
		СписокВыбора.Добавить("Не указан", НСтр("ru = 'Не указан'"));
		СписокВыбора.Добавить("Выбор", НСтр("ru = 'Выбрать объект расчетов'"));
		СписокВыбора.Добавить(Перечисления.ТипыДоговоров.СПоставщиком, НСтр("ru = 'Договор с поставщиком'"));
		Если ПолучитьФункциональнуюОпцию("ИспользоватьИмпортныеЗакупки") Тогда
			СписокВыбора.Добавить(Перечисления.ТипыДоговоров.Импорт, НСтр("ru = 'Договор импорта'"));
		КонецЕсли;
		Если ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриЗакупках") Тогда
			СписокВыбора.Добавить(Перечисления.ТипыДоговоров.СКомитентом, НСтр("ru = 'Договор с комитентом'"));
			СписокВыбора.Добавить(Документы.ОтчетКомитенту.ПустаяСсылка(), НСтр("ru = 'Отчет комитенту о продажах'"));
			СписокВыбора.Добавить(Документы.ОтчетКомитентуОСписании.ПустаяСсылка(), НСтр("ru = 'Отчет комитенту о списании'"));
		КонецЕсли;
		Если ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыПоставщикам") Тогда
			СписокВыбора.Добавить(Документы.ЗаказПоставщику.ПустаяСсылка(), НСтр("ru = 'Заказ поставщику'"));
		КонецЕсли;
		Если ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриПродажах") Тогда
			СписокВыбора.Добавить(Документы.ОтчетКомиссионера.ПустаяСсылка(), НСтр("ru = 'Отчет комиссионера о продажах'"));
		КонецЕсли;
		СписокВыбора.Добавить(Документы.ПоступлениеТоваровУслуг.ПустаяСсылка(), НСтр("ru = 'Поступление товаров и услуг'"));
		СписокВыбора.Добавить(Документы.ПоступлениеУслугПрочихАктивов.ПустаяСсылка(), НСтр("ru = 'Поступление услуг и прочих активов'"));
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТипОбъектаРасчетов(ИдентификаторСтроки = Неопределено)

	Если НЕ ПравоДоступа("Чтение",Метаданные.Справочники.ДоговорыКонтрагентов) Тогда
		Возврат;
	КонецЕсли;

	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ТаблицаРасчетов.НомерСтроки,
	|	ТаблицаРасчетов.РасчетныйДокумент
	|ПОМЕСТИТЬ ВТСтрокиСДоговором
	|ИЗ
	|	&ТаблицаРасчетов КАК ТаблицаРасчетов
	|ГДЕ
	|	ТаблицаРасчетов.РасчетныйДокумент ССЫЛКА Справочник.ДоговорыКонтрагентов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСтрокиСДоговором.НомерСтроки,
	|	ДоговорыКонтрагентов.ТипДоговора
	|ИЗ
	|	ВТСтрокиСДоговором КАК ВТСтрокиСДоговором
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО ВТСтрокиСДоговором.РасчетныйДокумент = ДоговорыКонтрагентов.Ссылка
	|");
		
	Если НЕ ЗначениеЗаполнено(ИдентификаторСтроки) Тогда
		Запрос.УстановитьПараметр("ТаблицаРасчетов", Объект.РасчетыСПартнерами.Выгрузить());
	Иначе
		МассивСтрок = Новый Массив;
		ОбрабатываемаяСтрока = Объект.РасчетыСПартнерами.НайтиПоИдентификатору(ИдентификаторСтроки);
		МассивСтрок.Добавить(ОбрабатываемаяСтрока);
		Запрос.УстановитьПараметр("ТаблицаРасчетов", Объект.РасчетыСПартнерами.Выгрузить(МассивСтрок));
	КонецЕсли;

	ТЗСтрокиСДоговором = Запрос.Выполнить().Выгрузить();

	Для Каждого СтрокаТаблицы Из Объект.РасчетыСПартнерами Цикл
		Если ЗначениеЗаполнено(ИдентификаторСтроки) И СтрокаТаблицы <> ОбрабатываемаяСтрока Тогда
			Продолжить;
		КонецЕсли;
		Если ТипЗнч(СтрокаТаблицы.РасчетныйДокумент) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
			СтрокаСДоговором = ТЗСтрокиСДоговором.Найти(СтрокаТаблицы.НомерСтроки, "НомерСтроки");
			Если СтрокаСДоговором <> Неопределено Тогда
				ТипДоговора = СтрокаСДоговором.ТипДоговора;
			Иначе
				ТипДоговора = Неопределено;
			КонецЕсли;
			Если ТипДоговора = Перечисления.ТипыДоговоров.СПокупателем Тогда
				СтрокаТаблицы.ТипРасчетов = НСтр("ru = 'Договор с покупателем'");
			ИначеЕсли ТипДоговора = Перечисления.ТипыДоговоров.СДавальцем Тогда
				СтрокаТаблицы.ТипРасчетов = НСтр("ru = 'Договор с давальцем'");
			ИначеЕсли ТипДоговора = Перечисления.ТипыДоговоров.СКомиссионером Тогда
				СтрокаТаблицы.ТипРасчетов = НСтр("ru = 'Договор с комиссионером'");
			ИначеЕсли ТипДоговора = Перечисления.ТипыДоговоров.СПоставщиком Тогда
				СтрокаТаблицы.ТипРасчетов = НСтр("ru = 'Договор с поставщиком'");
			ИначеЕсли ТипДоговора = Перечисления.ТипыДоговоров.СКомитентом Тогда
				СтрокаТаблицы.ТипРасчетов = НСтр("ru = 'Договор с комитентом'");
			ИначеЕсли ТипДоговора = Перечисления.ТипыДоговоров.Импорт Тогда
				СтрокаТаблицы.ТипРасчетов = НСтр("ru = 'Договор импорта'");
			Иначе
				ХозяйственнаяОперацияДоговора = Документы.ВводОстатков.ХозяйственнаяОперацияДоговораПоТипуОперации(Объект.ТипОперации);
				Если ХозяйственнаяОперацияДоговора = Перечисления.ХозяйственныеОперации.РеализацияКлиенту Тогда
					СтрокаТаблицы.ТипРасчетов = НСтр("ru = 'Договор с покупателем'");
				ИначеЕсли ХозяйственнаяОперацияДоговора = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика Тогда
					СтрокаТаблицы.ТипРасчетов = НСтр("ru = 'Договор с поставщиком'");
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли ТипЗнч(СтрокаТаблицы.РасчетныйДокумент) = Тип("ПеречислениеСсылка.ТипыДоговоров") Тогда
			Если СтрокаТаблицы.РасчетныйДокумент = Перечисления.ТипыДоговоров.СПокупателем Тогда
				СтрокаТаблицы.ТипРасчетов = НСтр("ru = 'Договор с покупателем'");
			ИначеЕсли СтрокаТаблицы.РасчетныйДокумент = Перечисления.ТипыДоговоров.СКомиссионером Тогда
				СтрокаТаблицы.ТипРасчетов = НСтр("ru = 'Договор с комиссионером'");
			ИначеЕсли СтрокаТаблицы.РасчетныйДокумент = Перечисления.ТипыДоговоров.СПоставщиком Тогда
				СтрокаТаблицы.ТипРасчетов = НСтр("ru = 'Договор с поставщиком'");
			ИначеЕсли СтрокаТаблицы.РасчетныйДокумент = Перечисления.ТипыДоговоров.СКомитентом Тогда
				СтрокаТаблицы.ТипРасчетов = НСтр("ru = 'Договор с комитентом'");
			ИначеЕсли СтрокаТаблицы.РасчетныйДокумент = Перечисления.ТипыДоговоров.Импорт Тогда
				СтрокаТаблицы.ТипРасчетов = НСтр("ru = 'Договор импорта'");
			КонецЕсли;
		Иначе
			СтрокаТаблицы.ТипРасчетов = Строка(ТипЗнч(СтрокаТаблицы.РасчетныйДокумент));
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ДополнитьНомер(Номер)

	Если Не Объект.ДополнятьНомера
		Или Не ЗначениеЗаполнено(Номер) Тогда
		Возврат;
	КонецЕсли;
	Результат = СокрЛП(Номер);
	Разделитель = ?(ПустаяСтрока(Объект.Префикс), "", "-");
	Начало = СокрЛП(Объект.Префикс) + Разделитель;
	Пока СтрДлина(Результат) < 11 - СтрДлина(Начало) Цикл
		Результат = "0" + Результат;
	КонецЦикла;
	Если СтрДлина(Результат) <= 11 - СтрДлина(Начало) Тогда
		Результат = Начало + Результат;
	КонецЕсли;
	Номер = Результат;

КонецПроцедуры

&НаСервере
Процедура ПодобратьДокументРасчетовСервер(ТекущаяСтрокаИдентификатор)

	ИмяТЧ = Документы.ВводОстатков.ИмяТабличнойЧастиРасчетовПоТипуОперации(Объект.ТипОперации);
	ТекущиеДанные = Объект[ИмяТЧ].НайтиПоИдентификатору(ТекущаяСтрокаИдентификатор);
	Документы.ВводОстатков.ПодобратьДокументРасчетов(Объект, ТекущиеДанные);

КонецПроцедуры

&НаСервере
Процедура ПартнерПриИзменении(Партнер, Контрагент, ИдентификаторСтроки)
	
	ПартнерПриИзмененииСервер(Партнер, Контрагент);
	ПроверитьДокументРасчетовСервер(ИдентификаторСтроки);
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьДокументРасчетовСервер(ТекущаяСтрокаИдентификатор)

	ИмяТЧ = Документы.ВводОстатков.ИмяТабличнойЧастиРасчетовПоТипуОперации(Объект.ТипОперации);
	ТекущиеДанные = Объект[ИмяТЧ].НайтиПоИдентификатору(ТекущаяСтрокаИдентификатор);
	Если Не ЗначениеЗаполнено(ТекущиеДанные.РасчетныйДокумент) Тогда
		Возврат;
	КонецЕсли;
	Реквизиты = Новый Структура("Организация,Номер,Дата,Ссылка,Представление, Партнер, Контрагент");
	Если ТипЗнч(ТекущиеДанные.РасчетныйДокумент) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
		Реквизиты.Вставить("Валюта","ВалютаВзаиморасчетов");
	Иначе
		Реквизиты.Вставить("Валюта");
	КонецЕсли;
	Если ТипЗнч(ТекущиеДанные.РасчетныйДокумент) <> Тип("ПеречислениеСсылка.ТипыДоговоров") Тогда
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТекущиеДанные.РасчетныйДокумент,Реквизиты);
		Если ЗначениеЗаполнено(ЗначенияРеквизитов.Организация)
		 И ЗначенияРеквизитов.Организация <> Объект.Организация Тогда
			ТекстСообщения = НСтр("ru='В документе ""%РасчетныйДокумент%"" организация отличается от указанной во вводе остатков'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения,"%РасчетныйДокумент%",ЗначенияРеквизитов.Представление);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			ТекущиеДанные.РасчетныйДокумент = Неопределено;
			УстановитьТипДокументаРасчетов(ТекущиеДанные.РасчетныйДокумент);
			Возврат;
		КонецЕсли;

		Если ЗначениеЗаполнено(ТекущиеДанные.Партнер)
		 И ЗначениеЗаполнено(ЗначенияРеквизитов.Партнер)
		 И ЗначенияРеквизитов.Партнер <> ТекущиеДанные.Партнер Тогда
			ТекстСообщения = НСтр("ru='В %Объект% ""%РасчетныйДокумент%"" партнер отличается от указанного во вводе остатков'");
			Если ТипЗнч(ТекущиеДанные.РасчетныйДокумент) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
				ТекстСообщения = СтрЗаменить(ТекстСообщения,"%Объект%", НСтр("ru='договоре'"));
			Иначе
				ТекстСообщения = СтрЗаменить(ТекстСообщения,"%Объект%", НСтр("ru='документе'"));
			КонецЕсли;
			ТекстСообщения = СтрЗаменить(ТекстСообщения,"%РасчетныйДокумент%",ЗначенияРеквизитов.Представление);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			ТекущиеДанные.РасчетныйДокумент = Неопределено;
			ТекущиеДанные.НомерРасчетногоДокумента = "";
			ТекущиеДанные.ДатаРасчетногоДокумента = "";
			УстановитьТипДокументаРасчетов(ТекущиеДанные.РасчетныйДокумент);
			Возврат;
		КонецЕсли;

		Если ЗначениеЗаполнено(ТекущиеДанные.Контрагент)
		 И ЗначениеЗаполнено(ЗначенияРеквизитов.Контрагент)
		 И ЗначенияРеквизитов.Контрагент <> ТекущиеДанные.Контрагент Тогда
			ТекстСообщения = НСтр("ru='В %Объект% ""%РасчетныйДокумент%"" контрагент отличается от указанного во вводе остатков'");
			Если ТипЗнч(ТекущиеДанные.РасчетныйДокумент) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
				ТекстСообщения = СтрЗаменить(ТекстСообщения,"%Объект%", НСтр("ru='договоре'"));
			Иначе
				ТекстСообщения = СтрЗаменить(ТекстСообщения,"%Объект%", НСтр("ru='документе'"));
			КонецЕсли;
			ТекстСообщения = СтрЗаменить(ТекстСообщения,"%РасчетныйДокумент%",ЗначенияРеквизитов.Представление);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			ТекущиеДанные.РасчетныйДокумент = Неопределено;
			ТекущиеДанные.НомерРасчетногоДокумента = "";
			ТекущиеДанные.ДатаРасчетногоДокумента = "";
			УстановитьТипДокументаРасчетов(ТекущиеДанные.РасчетныйДокумент);
			Возврат;
		КонецЕсли;
		ТекущиеДанные.Контрагент = ЗначенияРеквизитов.Контрагент;
		ТекущиеДанные.Партнер = ЗначенияРеквизитов.Партнер;
		ТекущиеДанные.РасчетныйДокумент			= ЗначенияРеквизитов.Ссылка;
		ТекущиеДанные.ДатаРасчетногоДокумента	= ЗначенияРеквизитов.Дата;
		ТекущиеДанные.НомерРасчетногоДокумента	= ЗначенияРеквизитов.Номер;
		ТекущиеДанные.ВалютаВзаиморасчетов = ЗначенияРеквизитов.Валюта;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьТипДокументаРасчетов(РасчетныйДокумент)

	Если ЗначениеЗаполнено(РасчетныйДокумент) Тогда
		Возврат;
	КонецЕсли;
	РасчетныйДокумент = Документы.ВводОстатков.ТипДокументаРасчетовПоТипуОперации(Объект.ТипОперации);

КонецПроцедуры

&НаКлиенте
Процедура НомерРасчетногоДокументаПриИзменении(ТекущиеДанные)

	ДополнитьНомер(ТекущиеДанные.НомерРасчетногоДокумента);
	Если Не (ЗначениеЗаполнено(ТекущиеДанные.НомерРасчетногоДокумента)
		И ЗначениеЗаполнено(ТекущиеДанные.ДатаРасчетногоДокумента))
		И ТипЗнч(ТекущиеДанные.РасчетныйДокумент) <> Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
		Возврат;
	КонецЕсли;
	ТекущаяСтрокаИдентификатор = ТекущиеДанные.ПолучитьИдентификатор();
	ПодобратьДокументРасчетовСервер(ТекущаяСтрокаИдентификатор);

Конецпроцедуры

&НаКлиенте
Процедура ДатаРасчетногоДокументаПриИзменении(ТекущиеДанные)

	Если Не (ЗначениеЗаполнено(ТекущиеДанные.НомерРасчетногоДокумента)
		И ЗначениеЗаполнено(ТекущиеДанные.ДатаРасчетногоДокумента))
		И ТипЗнч(ТекущиеДанные.РасчетныйДокумент) <> Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
		Возврат;
	КонецЕсли;
	ТекущаяСтрокаИдентификатор = ТекущиеДанные.ПолучитьИдентификатор();
	ПодобратьДокументРасчетовСервер(ТекущаяСтрокаИдентификатор);

КонецПроцедуры

&НаСервереБезКонтекста
Функция РассчитатьСуммуРегл(Сумма, Валюта,ВалютаРегламентированногоУчета, ДатаДокумента)

	Если Валюта = ВалютаРегламентированногоУчета Тогда
		СуммаРегл = Сумма;
	Иначе
		КоэффициентПересчета = РаботаСКурсамиВалютУТ.ПолучитьКоэффициентПересчетаИзВалютыВВалюту(Валюта, ВалютаРегламентированногоУчета, ДатаДокумента);
		СуммаРегл = Окр(Сумма * КоэффициентПересчета, 2, 1);
	КонецЕсли;
	Возврат СуммаРегл;

КонецФункции

&НаКлиенте
Процедура ВалютаПриИзменении(ТекущиеДанные)

	Если ТекущиеДанные.Сумма <> 0 Тогда
		СуммаПриИзменении(ТекущиеДанные);
	КонецЕсли;

Конецпроцедуры

&НаКлиенте
Процедура СуммаПриИзменении(ТекущиеДанные)

	Если ТекущиеДанные.ВалютаВзаиморасчетов = ВалютаРегламентированногоУчета Тогда
		ТекущиеДанные.СуммаРегл = ТекущиеДанные.Сумма;
	Иначе
		ТекущиеДанные.СуммаРегл = РассчитатьСуммуРегл(ТекущиеДанные.Сумма, ТекущиеДанные.ВалютаВзаиморасчетов, ВалютаРегламентированногоУчета, Объект.Дата);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПартнерПриИзмененииСервер(Партнер, Контрагент)

	ПартнерыИКонтрагенты.ЗаполнитьКонтрагентаПартнераПоУмолчанию(Партнер, Контрагент);

КонецПроцедуры

&НаКлиенте
Процедура ТипРасчетовОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка, СтрокаТаблицы)

	СтандартнаяОбработка = Ложь;
	
	Если ВыбранноеЗначение = "Выбор" Тогда
		
		ЗначенияОтбора = Новый Структура;
		ЗначенияОтбора.Вставить("Организация", Объект.Организация);
		ЗначенияОтбора.Вставить("Партнер",     СтрокаТаблицы.Партнер);
		ЗначенияОтбора.Вставить("Контрагент",  СтрокаТаблицы.Контрагент);
		
		НастройкиВыбора = ФинансыКлиент.ПараметрыВыбораДокументаРасчетов();
		НастройкиВыбора.РедактируемыйДокумент = Объект.Ссылка;
		
		Если Объект.ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыОперацийВводаОстатков.ОстаткиЗадолженностиКлиентов")
			Или Объект.ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыОперацийВводаОстатков.ОстаткиПоАвансамКлиентов") Тогда
				НастройкиВыбора.ЭтоРасчетыСКлиентами = Истина;
		КонецЕсли;
		
		Если Объект.ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыОперацийВводаОстатков.ОстаткиЗадолженностиКлиентов")
			Или Объект.ТипОперации = ПредопределенноеЗначение("Перечисление.ТипыОперацийВводаОстатков.ОстаткиЗадолженностиПередПоставщиками") Тогда
				НастройкиВыбора.ЗапретитьДоговорыПоДокументам = Истина;
		КонецЕсли;
		
		ФинансыКлиент.ДокументРасчетовНачалоВыбора(ЗначенияОтбора, НастройкиВыбора, Элемент, СтандартнаяОбработка);
		
	ИначеЕсли ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
		СтрокаТаблицы.ДатаРасчетногоДокумента = ВыбранноеЗначение.Дата;
		СтрокаТаблицы.НомерРасчетногоДокумента = ВыбранноеЗначение.Номер;
		СтрокаТаблицы.РасчетныйДокумент = ВыбранноеЗначение.Заказ;
		СтрокаТаблицы.ВалютаВзаиморасчетов = ВыбранноеЗначение.ВалютаВзаиморасчетов;
		ЗаполнитьТипОбъектаРасчетов(СтрокаТаблицы.ПолучитьИдентификатор());
	Иначе
		Если ТипЗнч(СтрокаТаблицы.РасчетныйДокумент) <> ТипЗнч(ВыбранноеЗначение)
			 ИЛИ (ТипЗнч(СтрокаТаблицы.РасчетныйДокумент) = Тип("ПеречислениеСсылка.ТипыДоговоров")
				  И СтрокаТаблицы.РасчетныйДокумент <> ВыбранноеЗначение) Тогда
			Если ВыбранноеЗначение = НСтр("ru = 'Не указан'") Тогда
				СтрокаТаблицы.РасчетныйДокумент = Неопределено;
				СтрокаТаблицы.ТипРасчетов = ВыбранноеЗначение;
				СтрокаТаблицы.ДатаРасчетногоДокумента = Дата(1,1,1);
				СтрокаТаблицы.НомерРасчетногоДокумента = "";
			Иначе
				СтрокаТаблицы.РасчетныйДокумент = ВыбранноеЗначение;
				Если ВыбранноеЗначение = ПредопределенноеЗначение("Перечисление.ТипыДоговоров.СПокупателем") Тогда
					СтрокаТаблицы.ТипРасчетов = НСтр("ru = 'Договор с покупателем'");
				//++ НЕ УТКА
				ИначеЕсли ВыбранноеЗначение = ПредопределенноеЗначение("Перечисление.ТипыДоговоров.СДавальцем") Тогда
					СтрокаТаблицы.ТипРасчетов = НСтр("ru = 'Договор с давальцем'");
				//-- НЕ УТКА
				ИначеЕсли ВыбранноеЗначение = ПредопределенноеЗначение("Перечисление.ТипыДоговоров.СКомиссионером") Тогда
					СтрокаТаблицы.ТипРасчетов = НСтр("ru = 'Договор с комиссионером'");
				ИначеЕсли ВыбранноеЗначение = ПредопределенноеЗначение("Перечисление.ТипыДоговоров.СПоставщиком") Тогда
					СтрокаТаблицы.ТипРасчетов = НСтр("ru = 'Договор с поставщиком'");
				ИначеЕсли ВыбранноеЗначение = ПредопределенноеЗначение("Перечисление.ТипыДоговоров.СКомитентом") Тогда
					СтрокаТаблицы.ТипРасчетов = НСтр("ru = 'Договор с комитентом'");
				ИначеЕсли ВыбранноеЗначение = ПредопределенноеЗначение("Перечисление.ТипыДоговоров.Импорт") Тогда
					СтрокаТаблицы.ТипРасчетов = НСтр("ru = 'Договор импорта'");
				Иначе
					СтрокаТаблицы.ТипРасчетов = Строка(ТипЗнч(ВыбранноеЗначение));
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыВыбораПартнера()

	Если Объект.ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиЗадолженностиКлиентов
		ИЛИ Объект.ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиПоАвансамКлиентов Тогда
		МассивНастроек = Новый Массив;
		МассивНастроек.Добавить(Истина);
		ПараметрВыбора = Новый ПараметрВыбора("Отбор.Клиент",Новый ФиксированныйМассив(МассивНастроек));
		МассивПараметров = Новый Массив;
		МассивПараметров.Добавить(ПараметрВыбора);
		Элементы.ЗадолженностьКлиентовПартнер.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
		Элементы.АвансыКлиентовПартнер.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	ИначеЕсли Объект.ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиЗадолженностиПередПоставщиками
		ИЛИ Объект.ТипОперации = Перечисления.ТипыОперацийВводаОстатков.ОстаткиПоАвансамПоставщикам Тогда
		МассивНастроек = Новый Массив;
		МассивНастроек.Добавить(Истина);
		ПараметрВыбора = Новый ПараметрВыбора("Отбор.Поставщик",Новый ФиксированныйМассив(МассивНастроек));
		МассивПараметров = Новый Массив;
		МассивПараметров.Добавить(ПараметрВыбора);
		Элементы.ЗадолженностьПоставщикамПартнер.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
		Элементы.АвансыПоставщикамПартнер.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция СформироватьПрефикс(Организация)

	Возврат Документы.ВводОстатков.УстановитьПрефикс(Организация);

КонецФункции

&НаКлиенте
Процедура ЗаполнитьВалютуВзаиморасчетовВСтроке(Строка)

	Если НЕ ЗначениеЗаполнено(Строка.ВалютаВзаиморасчетов) И Не ИспользоватьНесколькоВалют Тогда
		Строка.ВалютаВзаиморасчетов = ВалютаРегламентированногоУчета;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОчиститьНомерДатуОбъектовРасчета()
	
	Для Каждого Элемент Из Объект.РасчетыСПартнерами Цикл
		Элемент.НомерРасчетногоДокумента = "";
		Элемент.ДатаРасчетногоДокумента = Дата(1,1,1);
	КонецЦикла;
	
КонецПроцедуры

#Область РаботаСКонтрагентами

&НаКлиенте
Процедура Подключаемый_ПоказатьПредложениеИспользоватьПроверкуКонтрагентов()
	ПроверкаКонтрагентовКлиент.ПредложитьВключитьПроверкуКонтрагентов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработатьРезультатПроверкиКонтрагентов()
	ПроверкаКонтрагентовКлиент.ОбработатьРезультатПроверкиКонтрагентовВДокументе(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ОтобразитьРезультатПроверкиКонтрагента() Экспорт
	ПроверкаКонтрагентов.ОтобразитьРезультатПроверкиКонтрагентаВДокументе(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ПроверитьКонтрагентовФоновоеЗадание(ПараметрыФоновогоЗадания) Экспорт	
	ПроверкаКонтрагентов.ПроверитьКонтрагентовВДокументеФоновоеЗадание(ЭтотОбъект, ПараметрыФоновогоЗадания);
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьКонтрагентов(Команда)
	
	// СтандартныеПодсистемы.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПроверитьКонтрагентовВДокументеПоКнопке(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.РаботаСКонтрагентами
	
КонецПроцедуры

#КонецОбласти

//++ НЕ УТ
&НаСервере
Процедура ЗаполнитьПоОстаткамНаСервере()
	
	ТабЧасть = РеквизитФормыВЗначение("Объект.РасчетыСПартнерами");
	ТабЧасть.Очистить();
	КлючевыеПоля = Документы.ВводОстатков.КлючевыеПоляРеглУчетаПоТипуОперации(Объект.ТипОперации);
	Отбор = Новый Структура;
	Для каждого КлючевоеПоле из КлючевыеПоля Цикл
		Если Не КлючевоеПоле = "Организация" И ЗначениеЗаполнено(Объект[КлючевоеПоле]) Тогда
			Отбор.Вставить(КлючевоеПоле, Объект[КлючевоеПоле]);
		КонецЕсли;
	КонецЦикла;
	
	Остатки = Документы.ВводОстатков.ОстаткиПоТипуОперации(Объект.Дата, Объект.ТипОперации, Объект.Организация, Отбор);
	
	Выборка = Остатки.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Стр = ТабЧасть.Добавить();
		ЗаполнитьЗначенияСвойств(Стр, Выборка);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОповещенияВопросЗаполнитьПоОстаткам(РезультатВопроса, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗаполнитьПоОстаткамНаСервере();		
	КонецЕсли;
	
КонецПроцедуры
//-- НЕ УТ

#КонецОбласти
