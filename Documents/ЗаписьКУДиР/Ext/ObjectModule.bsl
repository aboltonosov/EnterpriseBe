#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
    ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	Дата          = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
	Если Не ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если ДанныеЗаполнения.Свойство("Организация") Тогда
		Организация = ДанныеЗаполнения.Организация;
	КонецЕсли;
	
	Если ДанныеЗаполнения.Свойство("Дата") Тогда
		Дата = ДанныеЗаполнения.Дата;
	КонецЕсли;
	
	Ответственный = Пользователи.ТекущийПользователь();
	Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	
	Если ДанныеЗаполнения.Свойство("Строки") Тогда
		
		Для каждого СтрСтроки из ДанныеЗаполнения.Строки Цикл
			
			Стр = Строки.Добавить();
			Если СтрСтроки.Свойство("Сумма") Тогда
				Стр.Графа4 = СтрСтроки.Сумма;
				Стр.Графа5 = СтрСтроки.Сумма;
				Стр.Графа6 = СтрСтроки.Сумма;
				Стр.Графа7 = СтрСтроки.Сумма;
			Иначе
				ЗаполнитьЗначенияСвойств(Стр, СтрСтроки, "Графа4, Графа5, Графа6, Графа7, НДС, ДоходЕНВД, РасходЕНВД");
			КонецЕсли;
			Если СтрСтроки.Свойство("РеквизитыПервичногоДокумента") Тогда
				Стр.ДатаНомер = СтрСтроки.РеквизитыПервичногоДокумента;
			Иначе
				Стр.ДатаНомер = УчетУСНСервер.РеквизитыПервичногоДокумента(СтрСтроки.Документ);
			КонецЕсли;
			Если СтрСтроки.Свойство("Содержание") Тогда
				Стр.Содержание = СтрСтроки.Содержание;
			Иначе
				Стр.Содержание = "";
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(Стр, СтрСтроки, "ДокументВозникновенияРасходов, ВидРасходов, СтатьяРасходов");
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ДанныеЗаполнения.Свойство("Строки2") Тогда
		
		Для каждого СтрСтроки из ДанныеЗаполнения.Строки2 Цикл
			
			Стр = Строки2.Добавить();
			ЗаполнитьЗначенияСвойств(Стр, СтрСтроки);
			Стр.Графа13_СуммаРасходовЗаКвартал = - Стр.Графа13_СуммаРасходовЗаКвартал;
			
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	// Инициализация дополнительных свойств для проведения документа
	ПроведениеСерверУТ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	// Инициализация данных документа
	Документы.ЗаписьКУДиР.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	ПроведениеСерверУТ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	Если УчетнаяПолитика.ПрименяетсяУСН(ЭтотОбъект.Организация, ЭтотОбъект.Дата) Тогда
			
		УчетУСНСервер.ОтразитьДоходКУДиР(ДополнительныеСвойства, Движения, Отказ);
		УчетУСНСервер.ОтразитьРаздел2КУДиР(ДополнительныеСвойства, Движения, Отказ);

	КонецЕсли;
	ПроведениеСерверУТ.СформироватьЗаписиРегистровЗаданий(ЭтотОбъект);

КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	ПроведениеСерверУТ.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	ПроведениеСерверУТ.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	ПроведениеСерверУТ.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	ПроведениеСерверУТ.СформироватьЗаписиРегистровЗаданий(ЭтотОбъект);
	ПроведениеСерверУТ.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	Если НЕ УчетнаяПолитика.ПрименяетсяУСН(Организация, Дата) Тогда
			
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Организация %1 не применяет упрощенную систему налогообложения'"),
			Организация);
				
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Организация", "Объект", Отказ);
			
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли