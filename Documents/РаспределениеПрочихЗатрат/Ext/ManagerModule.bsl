#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет список команд создания на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании) Экспорт



КонецПроцедуры

// Заполняет список команд создания на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании
//
// Возвращаемое значение:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСоздатьНаОсновании) Экспорт

	 
	Если ПравоДоступа("Добавление", Метаданные.Документы.РаспределениеПрочихЗатрат) Тогда
		КомандаСоздатьНаОсновании = КомандыСоздатьНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Идентификатор = Метаданные.Документы.РаспределениеПрочихЗатрат.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ВводНаОсновании.ПредставлениеОбъекта(Метаданные.Документы.РаспределениеПрочихЗатрат);
		КомандаСоздатьНаОсновании.ПроверкаПроведенияПередСозданиемНаОсновании = Истина;
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьПроизводство,ИспользоватьУчетПрочихДоходовРасходов";
	

		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуДвиженияДокумента(КомандыОтчетов);
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуБазаРаспределенияПрочихПроизводственныхРасходов(КомандыОтчетов);
	
КонецПроцедуры

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//   Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
#Область РаспределениеРасходов //(Дт 20 :: Кт 25, 26)
	ТекстРаспределениеРасходов = "
	|ВЫБРАТЬ //// Распределение расходов (Дт 20 :: Кт 25, 26)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Строки.Сумма КАК Сумма,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Производство) КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	Строки.МестоУчетаДт КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	Строки.СтатьяРасходов КАК СубконтоДт1,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоДт2,
	|	Строки.ГруппаПродукции КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СуммаНУ КАК СуммаНУДт,
	|	Строки.СуммаПР КАК СуммаПРДт,
	|	Строки.СуммаВР КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|	Операция.СтатьяРасходов КАК АналитикаУчетаКт,
	|	Операция.Подразделение КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	Операция.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	Операция.СтатьяРасходов КАК СубконтоКт1,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоКт2,
	|	ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка) КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СуммаНУ КАК СуммаНУКт,
	|	Строки.СуммаПР КАК СуммаПРКт,
	|	Строки.СуммаВР КАК СуммаВРКт,
	|	""Распределение расходов"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РаспределениеПрочихЗатрат КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ПрочиеРасходыНЗП КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.ПорядокОтраженияРасходов КАК Счета
	|	ПО 
	|		НЕ Счета.СчетУчета = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|		И Счета.Организация = Операция.Организация
	|		И Счета.Подразделение = Операция.Подразделение
	|		И Счета.СтатьяРасходов = Операция.СтатьяРасходов
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|	ПО
	|		СтатьиРасходов.Ссылка = Операция.СтатьяРасходов
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.ПорядокОтраженияРасходов КАК КорСчета
	|	ПО 
	|		КорСчета.Организация = Операция.Организация
	|		И КорСчета.Подразделение = Строки.Подразделение
	|		И КорСчета.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|	
	|ГДЕ
	|	(Строки.Сумма <> 0 ИЛИ Строки.СуммаНУ <> 0 ИЛИ Строки.СуммаВР <> 0 ИЛИ Строки.СуммаПР <> 0)
	|	И ((ВЫБОР 
	|			КОГДА ЕСТЬNULL(Счета.СчетУчета, СтатьиРасходов.СчетУчета) <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|				ТОГДА ЕСТЬNULL(Счета.СчетУчета, СтатьиРасходов.СчетУчета)
	|			ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОбщепроизводственныеРасходы)
	|		КОНЕЦ) <> ЕСТЬNULL(КорСчета.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОсновноеПроизводство))
	|	    ИЛИ Операция.Подразделение <> Строки.Подразделение
	|	    ИЛИ Операция.НаправлениеДеятельности <> Строки.НаправлениеДеятельности
	|	    ИЛИ (&АналитическийУчетПоГруппамПродукции И Строки.ГруппаПродукции <> ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ //// Распределение расходов (Дт 25, 26 :: Кт 25, 26)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ПрочиеРасходы.СуммаРегл КАК Сумма,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	ПрочиеРасходы.СтатьяРасходов КАК АналитикаУчетаДт,
	|	ПрочиеРасходы.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	ПрочиеРасходы.Подразделение КАК ПодразделениеДт,
	|	ПрочиеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	ПрочиеРасходы.СтатьяРасходов КАК СубконтоДт1,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоДт2,
	|	ПрочиеРасходы.АналитикаРасходов КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ПрочиеРасходы.СуммаРегл - ПрочиеРасходы.ПостояннаяРазница - ПрочиеРасходы.ВременнаяРазница КАК СуммаНУДт,
	|	ПрочиеРасходы.ПостояннаяРазница КАК СуммаПРДт,
	|	ПрочиеРасходы.ВременнаяРазница КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|	Операция.СтатьяРасходов КАК АналитикаУчетаКт,
	|	Операция.Подразделение КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	Операция.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	Операция.СтатьяРасходов КАК СубконтоКт1,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ПрочиеРасходы.СуммаРегл - ПрочиеРасходы.ПостояннаяРазница - ПрочиеРасходы.ВременнаяРазница КАК СуммаНУКт,
	|	ПрочиеРасходы.ПостояннаяРазница КАК СуммаПРКт,
	|	ПрочиеРасходы.ВременнаяРазница КАК СуммаВРКт,
	|	""Распределение расходов"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РаспределениеПрочихЗатрат КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходы КАК ПрочиеРасходы
	|	ПО Операция.Ссылка = ПрочиеРасходы.ДокументДвижения
	|	   И ПрочиеРасходы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	   И (ПрочиеРасходы.СуммаРегл <> 0 ИЛИ ПрочиеРасходы.ВременнаяРазница <> 0 ИЛИ ПрочиеРасходы.ПостояннаяРазница <> 0)
	|";
#КонецОбласти

#Область СписаниеНаСтатьиАктивовПассивов //(Дт ХХ.ХХ :: Кт 20)
	ТекстСписаниеНаСтатьиАктивовПассивов = "
	|ВЫБРАТЬ //// Списание на прочие активы (Дт ХХ.ХХ :: Кт 20)
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	ТаблицаСписание.НомерСтроки КАК ИдентификаторСтроки,
	|
	|	Строки.СуммаРегл КАК Сумма,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	ТаблицаСписание.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ТаблицаСписание.СчетУчета КАК СчетДт,
	|	ТаблицаСписание.Субконто1 КАК СубконтоДт1,
	|	ТаблицаСписание.Субконто2 КАК СубконтоДт2,
	|	ТаблицаСписание.Субконто3 КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СуммаРегл - Строки.ПостояннаяРазница - Строки.ВременнаяРазница КАК СуммаНУДт,
	|	Строки.ПостояннаяРазница КАК СуммаПРДт,
	|	Строки.ВременнаяРазница КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|	Операция.СтатьяРасходов КАК АналитикаУчетаКт,
	|	Строки.Подразделение КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	Строки.СтатьяРасходов КАК СубконтоКт1,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоКт2,
	|	Строки.ГруппаПродукции КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СуммаРегл - Строки.ПостояннаяРазница - Строки.ВременнаяРазница КАК СуммаНУКт,
	|	Строки.ПостояннаяРазница КАК СуммаПРКт,
	|	Строки.ВременнаяРазница КАК СуммаВРКт,
	|	""Списание на прочие активы"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РаспределениеПрочихЗатрат КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ПрочиеРасходы КАК Строки
	|	ПО
	|		Операция.Ссылка = Строки.Регистратор
	|		И Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РаспределениеПрочихЗатрат.Списание КАК ТаблицаСписание
	|	ПО
	|		Операция.Ссылка = ТаблицаСписание.Ссылка
	|		И Строки.ИдентификаторСтроки = ТаблицаСписание.ИдентификаторСтроки
	|
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ТаблицаСписание.СтатьяРасходов) = ТИП(ПланВидовХарактеристик.СтатьиАктивовПассивов)
	|";
#КонецОбласти
	
	Возврат ТекстРаспределениеРасходов
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстСписаниеНаСтатьиАктивовПассивов
		+ "";
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц, 
// необходимых для отражения в регламентированном учете
//
// Возвращаемое значение:
//   Строка - Текст запроса временной таблицы.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Движения.ДокументДвижения     КАК Ссылка,
	|	(ВЫБОР
	|		КОГДА НЕ ЗаказыПереработчику.Партнер ЕСТЬ NULL ТОГДА ЗаказыПереработчику.Партнер
	|		КОГДА НЕ ОтчетПереработчика.Партнер ЕСТЬ NULL ТОГДА ОтчетПереработчика.Партнер
	|		ИНАЧЕ Движения.Подразделение КОНЕЦ) КАК МестоУчетаДт,
	|	Движения.Подразделение        КАК Подразделение,
	|	Движения.СтатьяРасходов       КАК СтатьяРасходов,
	|	Движения.ГруппаПродукции      КАК ГруппаПродукции,
	|	ВЫБОР
	|		КОГДА НЕ ВыпускБезЗаказа.Назначение.НаправлениеДеятельности ЕСТЬ NULL
	|			ТОГДА ВыпускБезЗаказа.Назначение.НаправлениеДеятельности
	//++ НЕ УТКА
	|		КОГДА НЕ ПродукцияЗаказаНаПроизводство.Назначение.НаправлениеДеятельности ЕСТЬ NULL
	|			ТОГДА ЕСТЬNULL(ПродукцияЗаказаНаПроизводство.Назначение.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
	|		КОГДА НЕ ЭтапПроизводства2_2.НаправлениеДеятельности ЕСТЬ NULL
	|			ТОГДА ЭтапПроизводства2_2.НаправлениеДеятельности
	|		КОГДА НЕ ПроизводствоБезЗаказа.Ссылка ЕСТЬ NULL
	|			ТОГДА Движения.АналитикаУчетаПартийПроизводства.Назначение.НаправлениеДеятельности
	//-- НЕ УТКА
	|		КОГДА НЕ ОтчетПереработчика.НаправлениеДеятельности ЕСТЬ NULL
	|			ТОГДА ОтчетПереработчика.НаправлениеДеятельности
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	КОНЕЦ                         КАК НаправлениеДеятельности,
	|	СУММА(Движения.СтоимостьРегл) КАК Сумма,
	|	СУММА(Движения.СтоимостьРегл - Движения.ПостояннаяРазница - Движения.ВременнаяРазница) КАК СуммаНУ,
	|	СУММА(Движения.ПостояннаяРазница)   КАК СуммаПР,
	|	СУММА(Движения.ВременнаяРазница)    КАК СуммаВР
	|ПОМЕСТИТЬ ПрочиеРасходыНЗП
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходыНезавершенногоПроизводства КАК Движения
	|		ПО ДокументыКОтражению.Ссылка = Движения.ДокументДвижения
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПереработчику КАК ЗаказыПереработчику
	|		ПО ЗаказыПереработчику.Ссылка = Движения.ЗаказНаПроизводство
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетПереработчика КАК ОтчетПереработчика
	|		ПО ОтчетПереработчика.Ссылка = Движения.ДокументВыпуска
	|			ИЛИ ОтчетПереработчика.Ссылка = Движения.ПартияПроизводства
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции.Товары КАК ВыпускБезЗаказа
	|		ПО (ВыпускБезЗаказа.Ссылка = Движения.ДокументВыпуска ИЛИ ВыпускБезЗаказа.Ссылка = Движения.ЗаказНаПроизводство)
	|		И ВыпускБезЗаказа.КодСтроки = Движения.КодСтрокиПродукция
	|		И НЕ ВыпускБезЗаказа.Ссылка.ВыпускПоРаспоряжениям
	//++ НЕ УТКА
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК ПродукцияЗаказаНаПроизводство
	|		ПО ПродукцияЗаказаНаПроизводство.Ссылка = Движения.ЗаказНаПроизводство
	|		И ПродукцияЗаказаНаПроизводство.КодСтроки = Движения.КодСтрокиПродукция
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства2_2
	|		ПО ЭтапПроизводства2_2.Ссылка = Движения.ПартияПроизводства
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа КАК ПроизводствоБезЗаказа
	|		ПО ПроизводствоБезЗаказа.Ссылка = Движения.ПартияПроизводства
	//-- НЕ УТКА
	|
	|СГРУППИРОВАТЬ ПО
	|	(ВЫБОР
	|		КОГДА НЕ ЗаказыПереработчику.Партнер ЕСТЬ NULL ТОГДА ЗаказыПереработчику.Партнер
	|		КОГДА НЕ ОтчетПереработчика.Партнер ЕСТЬ NULL ТОГДА ОтчетПереработчика.Партнер
	|		ИНАЧЕ Движения.Подразделение КОНЕЦ),
	|	Движения.ДокументДвижения,
	|	Движения.Подразделение,
	|	Движения.СтатьяРасходов,
	|	ВЫБОР
	|		КОГДА НЕ ВыпускБезЗаказа.Назначение.НаправлениеДеятельности ЕСТЬ NULL
	|			ТОГДА ВыпускБезЗаказа.Назначение.НаправлениеДеятельности
	//++ НЕ УТКА
	|		КОГДА НЕ ПродукцияЗаказаНаПроизводство.Назначение.НаправлениеДеятельности ЕСТЬ NULL
	|			ТОГДА ЕСТЬNULL(ПродукцияЗаказаНаПроизводство.Назначение.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
	|		КОГДА НЕ ЭтапПроизводства2_2.НаправлениеДеятельности ЕСТЬ NULL
	|			ТОГДА ЭтапПроизводства2_2.НаправлениеДеятельности
	|		КОГДА НЕ ПроизводствоБезЗаказа.Ссылка ЕСТЬ NULL
	|			ТОГДА Движения.АналитикаУчетаПартийПроизводства.Назначение.НаправлениеДеятельности
	//-- НЕ УТКА
	|		КОГДА НЕ ОтчетПереработчика.НаправлениеДеятельности ЕСТЬ NULL
	|			ТОГДА ОтчетПереработчика.НаправлениеДеятельности
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|	КОНЕЦ,
	|	Движения.ГруппаПродукции
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция возвращает список статей, по которым необходимо выполнить распределение.
//
// Параметры;
//		Период - Дата - Период выборки.
//		МассивОрганизаций - Массив - Список организаций по которым анализируются статьи.
//		СписокПодразделений - Массив - Список подразделений по которым анализируются статьи.
//		Состояние - ПеречислениеСсылка.СостоянияРаспределенияРасходов - Фильтр по состоянию.
//		ТолькоПостоянныеРазницы - Булево - Отбор только постоянных разниц.
//
//	Возвращаемое значение:
//		ТаблицаЗначений - Таблица статей к распределению.
//
Функция СтатьиКРаспределению(Период, МассивОрганизаций, СписокПодразделений, Состояние, ТолькоПостоянныеРазницы = Ложь) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапросаДанныеДляРаспределения();
	Запрос.УстановитьПараметр("НачалоПериода",        НачалоМесяца(Период));
	Запрос.УстановитьПараметр("КонецПериода",         КонецМесяца(Период));
	Запрос.УстановитьПараметр("ГраницаДатаОкончания", Новый Граница(КонецМесяца(Период), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("СписокПодразделений",  СписокПодразделений);
	Запрос.УстановитьПараметр("МассивОрганизаций",    МассивОрганизаций);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям",   Ложь);
	Запрос.УстановитьПараметр("ПоВсемПодразделениям", Ложь);
	Запрос.УстановитьПараметр("ФильтрПоСостоянию",    Состояние);
	Запрос.УстановитьПараметр("ТолькоПостоянныеРазницыПрошлыхПериодов", ТолькоПостоянныеРазницы);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Функция возвращает все поля статьи расходов.
// Параметры:
//	СтатьяРасходов - ПланВидовХарактеристик.СтатьиРасходов - Статья, у которой необходимо получить поля.
// Возвращаемое значение:
//	ВыборкаИзРезультатаЗапроса - Поля статьи расходов.
Функция ПоляСтатьиРасходов(Знач СтатьяРасходов) Экспорт

	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	СтатьиРасходов.СпособРаспределенияНаПроизводственныеЗатраты КАК ВариантРаспределения,
	|	СтатьиРасходов.ПравилоРаспределенияПоЭтапамПроизводства КАК ПравилоРаспределенияПоЭтапам,
	|	СтатьиРасходов.ПравилоРаспределенияПоПодразделениям КАК ПравилоРаспределенияПоПодразделениям,
	|	СтатьиРасходов.СтатьяКалькуляции КАК СтатьяКалькуляции
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|ГДЕ
	|	СтатьиРасходов.Ссылка = &СтатьяРасходов
	|");

	Запрос.УстановитьПараметр("СтатьяРасходов", СтатьяРасходов);

	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	Возврат Выборка;
КонецФункции

// Функция возвращает настройки распределения для статьи расходов.
// Параметры:
//	СтатьяРасходов - ПланВидовХарактеристик.СтатьиРасходов - Статья, у которой необходимо получить поля.
// Возвращаемое значение:
//	Массив - Массив параметров распределения.
Функция ПолучитьНастройкиРаспределенияСтатьиРасходов(Знач СтатьяРасходов) Экспорт

	Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	СР.СпособРаспределенияНаПроизводственныеЗатраты КАК ВариантРаспределения,
		|	СР.ПравилоРаспределенияПоЭтапамПроизводства КАК ПравилоРаспределенияПоЭтапам,
		|	СР.ПравилоРаспределенияПоПодразделениям КАК ПравилоРаспределенияПоПодразделениям,
		|	ЕСТЬNULL(ПП.БазаРаспределения, НЕОПРЕДЕЛЕНО) КАК БазаРаспределенияПоПодразделениям,
		|	ЕСТЬNULL(ПЭ.БазаРаспределения, НЕОПРЕДЕЛЕНО) КАК БазаРаспределенияПоЭтапам,
		|	ЕСТЬNULL(ПП.НаправлениеРаспределения, НЕОПРЕДЕЛЕНО) КАК НаправлениеРаспределения,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ПЭ.СтатьяКалькуляции, &ПустаяСтатьяКалькуляции) <> &ПустаяСтатьяКалькуляции
		|			ТОГДА ПЭ.СтатьяКалькуляции
		|		ИНАЧЕ СР.СтатьяКалькуляции
		|	КОНЕЦ КАК СтатьяКалькуляции
		|ИЗ
		|	ПланВидовХарактеристик.СтатьиРасходов КАК СР
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПравилаРаспределенияРасходов КАК ПП
		|	ПО ПП.Ссылка = СР.ПравилоРаспределенияПоПодразделениям
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПравилаРаспределенияРасходов КАК ПЭ
		|	ПО ПЭ.Ссылка = СР.ПравилоРаспределенияПоЭтапамПроизводства
		|ГДЕ
		|	СР.Ссылка = &Ссылка
		|;
		|
		|//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПП.Подразделение
		|ИЗ
		|	Справочник.ПравилаРаспределенияРасходов.ОтборПоПодразделениям КАК ПП
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СР
		|	ПО СР.ПравилоРаспределенияПоПодразделениям = ПП.Ссылка
		|		И СР.Ссылка = &Ссылка
		|;
		|
		|//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПП.Материал
		|ИЗ
		|	Справочник.ПравилаРаспределенияРасходов.ОтборПоМатериалам КАК ПП
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СР
		|	ПО СР.ПравилоРаспределенияПоЭтапамПроизводства = ПП.Ссылка
		|		И СР.Ссылка = &Ссылка
		|;
		|
		|//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПП.ВидРабот
		|ИЗ
		|	Справочник.ПравилаРаспределенияРасходов.ОтборПоВидамРабот КАК ПП
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СР
		|	ПО СР.ПравилоРаспределенияПоЭтапамПроизводства = ПП.Ссылка
		|		И СР.Ссылка = &Ссылка
		|;
		|
		|//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПП.ГруппаПродукции
		|ИЗ
		|	Справочник.ПравилаРаспределенияРасходов.ОтборПоГруппамПродукции КАК ПП
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СР
		|	ПО СР.ПравилоРаспределенияПоЭтапамПроизводства = ПП.Ссылка
		|		И СР.Ссылка = &Ссылка
		|");
	
	Запрос.УстановитьПараметр("Ссылка", СтатьяРасходов);
	Запрос.УстановитьПараметр("ПустаяСтатьяКалькуляции", Справочники.СтатьиКалькуляции.ПустаяСсылка());
	Результат = Запрос.ВыполнитьПакет();
	МассивРезультат = Новый Массив(11);
	ДанныеДокумента = Результат[0].Выгрузить()[0];
	МассивРезультат[0] = ДанныеДокумента.ВариантРаспределения;
	МассивРезультат[1] = ДанныеДокумента.ПравилоРаспределенияПоЭтапам;
	МассивРезультат[2] = ДанныеДокумента.ПравилоРаспределенияПоПодразделениям;
	МассивРезультат[3] = ДанныеДокумента.БазаРаспределенияПоПодразделениям;
	МассивРезультат[4] = ДанныеДокумента.БазаРаспределенияПоЭтапам;
	МассивРезультат[5] = ДанныеДокумента.НаправлениеРаспределения;
	МассивРезультат[6] = ДанныеДокумента.СтатьяКалькуляции;
	ОтборПоПодразделениям = Новый СписокЗначений;
	ОтборПоМатериалам = Новый СписокЗначений;
	ОтборПоВидамРабот = Новый СписокЗначений;
	ОтборПоГруппамПродукции = Новый СписокЗначений;
	ОтборПоПодразделениям.ЗагрузитьЗначения(Результат[1].Выгрузить().ВыгрузитьКолонку("Подразделение"));
	ОтборПоМатериалам.ЗагрузитьЗначения(Результат[2].Выгрузить().ВыгрузитьКолонку("Материал"));
	ОтборПоВидамРабот.ЗагрузитьЗначения(Результат[3].Выгрузить().ВыгрузитьКолонку("ВидРабот"));
	ОтборПоГруппамПродукции.ЗагрузитьЗначения(Результат[4].Выгрузить().ВыгрузитьКолонку("ГруппаПродукции"));
	МассивРезультат[7] = ОтборПоПодразделениям;
	МассивРезультат[8] = ОтборПоМатериалам;
	МассивРезультат[9] = ОтборПоВидамРабот;
	МассивРезультат[10] = ОтборПоГруппамПродукции;
	
	Возврат МассивРезультат;
	
КонецФункции

// Метод формирует документ "Распределение прочих затрат" по заданным условиям.
// Параметры:
//	 ПараметрыЗаполнения - Структура - Параметры заполнения документа.
// Возвращаемое значение:
//	Структура - Содержит сведения о документе и состоянии распределения.
Функция СформироватьДокумент(ПараметрыЗаполнения) Экспорт
	
	ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(НСтр("ru='Распределение прочих затрат'"));
	
	ПараметрыПередачи = Новый Структура();
	
	Если НЕ(ПараметрыЗаполнения.ВариантРаспределения = Перечисления.СпособыРаспределенияСтатейРасходов.ПоПодразделениямВручнуюПоЭтапамПоПравилу
				ИЛИ ПараметрыЗаполнения.ВариантРаспределения = Перечисления.СпособыРаспределенияСтатейРасходов.ПоЭтапамВручнуюПоВсемПодразделениям)
			И ПараметрыЗаполнения.СтатьяКалькуляции = Справочники.СтатьиКалькуляции.ПустаяСсылка() Тогда
			
			ПараметрыПередачи.Вставить("ОписаниеОшибки", НСтр("ru = 'Нет данных для заполнения статьи калькуляции в документе распределения'"));
			Возврат ПараметрыПередачи;
			
	КонецЕсли;
	
	Если ПараметрыЗаполнения.БазаРаспределенияПоЭтапам = Перечисления.ТипыБазыРаспределенияРасходов.КоличествоРаботУказанныхВидов
		И ПараметрыЗаполнения.ОтборПоВидамРабот.Количество() = 0 Тогда
			
			ПараметрыПередачи.Вставить("ОписаниеОшибки", НСтр("ru = 'В правиле распределения по этапам не указаны виды работ'"));
			Возврат ПараметрыПередачи;
			
	КонецЕсли;
	
	Если (ПараметрыЗаполнения.БазаРаспределенияПоЭтапам = Перечисления.ТипыБазыРаспределенияРасходов.КоличествоУказанныхМатериалов
			Или ПараметрыЗаполнения.БазаРаспределенияПоЭтапам = Перечисления.ТипыБазыРаспределенияРасходов.ВесУказанныхМатериалов
			Или ПараметрыЗаполнения.БазаРаспределенияПоЭтапам = Перечисления.ТипыБазыРаспределенияРасходов.ОбъемУказанныхМатериалов)
		И ПараметрыЗаполнения.ОтборПоМатериалам.Количество() = 0 Тогда
			
			ПараметрыПередачи.Вставить("ОписаниеОшибки", НСтр("ru = 'В правиле распределения по этапам не указаны материалы'"));
			Возврат ПараметрыПередачи;
			
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПараметрыЗаполнения.Документ) Тогда
		Док = Документы.РаспределениеПрочихЗатрат.СоздатьДокумент();
	Иначе
		Док = ПараметрыЗаполнения.Документ.ПолучитьОбъект();
		Док.ПоБазе.Очистить();
		Док.Вручную.Очистить();
		Док.ВыпускиБезРаспоряжения.Очистить();
		Док.Списание.Очистить();
		Док.ОтборПоМатериалам.Очистить();
		Док.ОтборПоВидамРабот.Очистить();
		Док.ОтборПоГруппамПродукции.Очистить();
		Док.ОтборПоПодразделениям.Очистить();
	КонецЕсли;
	Док.Заполнить(ПараметрыЗаполнения);
	
	Если ПараметрыЗаполнения.ВариантРаспределения = Перечисления.СпособыРаспределенияСтатейРасходов.ПоПодразделениямИЭтапамПоПравилам
		ИЛИ ПараметрыЗаполнения.ВариантРаспределения = Перечисления.СпособыРаспределенияСтатейРасходов.ПоЭтапамПоПравилуВДанномПодразделении
		ИЛИ ПараметрыЗаполнения.ВариантРаспределения = Перечисления.СпособыРаспределенияСтатейРасходов.ПоЭтапамПоПравилуПоВсемПодразделениям Тогда
		Док.ДоляСтоимостиПоПравилам = 100;
	КонецЕсли;
	
	Попытка
		Док.Записать(РежимЗаписиДокумента.Проведение);
	Исключение
		ЗаписьЖурналаРегистрации(Док.Метаданные().Имя, 
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ПараметрыПередачи.Вставить("ОписаниеОшибки", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат ПараметрыПередачи;
		
	КонецПопытки;
	
	Если Док.ПоБазе.Итог("ДоляСтоимости") + Док.ДоляСтоимостиПоПравилам > 0 Тогда
		ПараметрыПередачи.Вставить("Состояние", Перечисления.СостоянияРаспределенияРасходов.ГотовоКРаспределениюПоБазе);
	Иначе
		ПараметрыПередачи.Вставить("Состояние", Перечисления.СостоянияРаспределенияРасходов.ТребуетсяНастройкаРаспределения);
	КонецЕсли;
	ПараметрыПередачи.Вставить("Документ", Док.Ссылка);
		
	Возврат ПараметрыПередачи;
	
КонецФункции

// Функция возвращает текст запроса для получения данных о прочих расходах.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстЗапросаПоступилоПрочихРасходов() Экспорт
	
	ТекстЗапроса = "
		|ВЫБРАТЬ
		|	Организации.Ссылка КАК Организация
		|ПОМЕСТИТЬ СписокОрганизаций
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	Организации.Ссылка В(&МассивОрганизаций)
		|	И НЕ &ПоВсемОрганизациям
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Организации.Ссылка
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	&ПоВсемОрганизациям
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Подразделения.Ссылка КАК Подразделение
		|ПОМЕСТИТЬ СписокПодразделений
		|ИЗ
		|	Справочник.СтруктураПредприятия КАК Подразделения
		|ГДЕ
		|	Подразделения.Ссылка В(&СписокПодразделений)
		|	И НЕ &ПоВсемПодразделениям
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Подразделения.Ссылка
		|ИЗ
		|	Справочник.СтруктураПредприятия КАК Подразделения
		|ГДЕ
		|	&ПоВсемПодразделениям
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПрочиеРасходыОбороты.Организация КАК Организация,
		|	ПрочиеРасходыОбороты.Подразделение КАК Подразделение,
		|	ПрочиеРасходыОбороты.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	ПрочиеРасходыОбороты.СтатьяРасходов КАК СтатьяРасходов,
		|	ПрочиеРасходыОбороты.АналитикаРасходов КАК АналитикаРасходов,
		|	СУММА(ВЫБОР
		|			КОГДА ПрочиеРасходыОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|				ТОГДА -1
		|			ИНАЧЕ 1
		|		КОНЕЦ * ПрочиеРасходыОбороты.Сумма) КАК СуммаУпрПриход,
		|	СУММА(ВЫБОР
		|			КОГДА ПрочиеРасходыОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|				ТОГДА -1
		|			ИНАЧЕ 1
		|		КОНЕЦ * ПрочиеРасходыОбороты.СуммаБезНДС) КАК СуммаУпрБезНДСПриход,
		|	СУММА(ВЫБОР
		|			КОГДА ПрочиеРасходыОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|				ТОГДА -1
		|			ИНАЧЕ 1
		|		КОНЕЦ * ПрочиеРасходыОбороты.СуммаРегл) КАК СуммаРеглПриход,
		|	СУММА(ВЫБОР
		|			КОГДА ПрочиеРасходыОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|				ТОГДА -1
		|			ИНАЧЕ 1
		|		КОНЕЦ * ПрочиеРасходыОбороты.ПостояннаяРазница) КАК ПостояннаяРазницаПриход,
		|	СУММА(ВЫБОР
		|			КОГДА ПрочиеРасходыОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|				ТОГДА -1
		|			ИНАЧЕ 1
		|		КОНЕЦ * ПрочиеРасходыОбороты.ВременнаяРазница) КАК ВременнаяРазницаПриход
		|ПОМЕСТИТЬ ВТПриход
		|ИЗ
		|	РегистрНакопления.ПрочиеРасходы КАК ПрочиеРасходыОбороты
		|ГДЕ
		|	ПрочиеРасходыОбороты.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ПрочиеРасходыОбороты.Активность
		|	И НЕ ПрочиеРасходыОбороты.РасчетСебестоимости
		|	И ПрочиеРасходыОбороты.СтатьяРасходов.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
		|	И ПрочиеРасходыОбороты.Организация В
		|			(ВЫБРАТЬ
		|				СписокОрганизаций.Организация
		|			ИЗ
		|				СписокОрганизаций КАК СписокОрганизаций)
		|	И ПрочиеРасходыОбороты.Подразделение В
		|			(ВЫБРАТЬ
		|				СписокПодразделений.Подразделение
		|			ИЗ
		|				СписокПодразделений КАК СписокПодразделений)
		|	И НЕ(ПрочиеРасходыОбороты.Регистратор ССЫЛКА Документ.РаспределениеПрочихЗатрат
		|				ИЛИ ПрочиеРасходыОбороты.Регистратор ССЫЛКА Документ.РасчетСебестоимостиТоваров
		|				ИЛИ ВЫБОР
		|					КОГДА ПрочиеРасходыОбороты.Регистратор ССЫЛКА Документ.РегламентнаяОперация
		|						ТОГДА ПрочиеРасходыОбороты.Регистратор.ТипОперации <> ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РасчетНалогаНаИмущество)
		|								И ПрочиеРасходыОбороты.Регистратор.ТипОперации <> ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РасчетТранспортногоНалога)
		|								И ПрочиеРасходыОбороты.Регистратор.ТипОперации <> ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РасчетЗемельногоНалога)
		|								И ПрочиеРасходыОбороты.Регистратор.ТипОперации <> ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.ПризнаниеВНУЛизинговыхПлатежей)
		|					ИНАЧЕ ЛОЖЬ
		|				КОНЕЦ)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПрочиеРасходыОбороты.Организация,
		|	ПрочиеРасходыОбороты.Подразделение,
		|	ПрочиеРасходыОбороты.НаправлениеДеятельности,
		|	ПрочиеРасходыОбороты.СтатьяРасходов,
		|	ПрочиеРасходыОбороты.АналитикаРасходов
		|
		|ИМЕЮЩИЕ
		|	НЕ(СУММА(ВЫБОР
		|					КОГДА ПрочиеРасходыОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|						ТОГДА -1
		|					ИНАЧЕ 1
		|				КОНЕЦ * ПрочиеРасходыОбороты.Сумма) = 0
		|			И СУММА(ВЫБОР
		|					КОГДА ПрочиеРасходыОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|						ТОГДА -1
		|					ИНАЧЕ 1
		|				КОНЕЦ * ПрочиеРасходыОбороты.СуммаБезНДС) = 0
		|			И СУММА(ВЫБОР
		|					КОГДА ПрочиеРасходыОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|						ТОГДА -1
		|					ИНАЧЕ 1
		|				КОНЕЦ * ПрочиеРасходыОбороты.СуммаРегл) = 0
		|			И СУММА(ВЫБОР
		|					КОГДА ПрочиеРасходыОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|						ТОГДА -1
		|					ИНАЧЕ 1
		|				КОНЕЦ * ПрочиеРасходыОбороты.ПостояннаяРазница) = 0
		|			И СУММА(ВЫБОР
		|					КОГДА ПрочиеРасходыОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|						ТОГДА -1
		|					ИНАЧЕ 1
		|				КОНЕЦ * ПрочиеРасходыОбороты.ВременнаяРазница) = 0)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПрочиеРасходыОстатки.Организация,
		|	ПрочиеРасходыОстатки.Подразделение,
		|	ПрочиеРасходыОстатки.НаправлениеДеятельности,
		|	ПрочиеРасходыОстатки.СтатьяРасходов,
		|	ПрочиеРасходыОстатки.АналитикаРасходов,
		|	ПрочиеРасходыОстатки.СуммаОстаток,
		|	ПрочиеРасходыОстатки.СуммаБезНДСОстаток,
		|	ПрочиеРасходыОстатки.СуммаРеглОстаток,
		|	ПрочиеРасходыОстатки.ПостояннаяРазницаОстаток,
		|	ПрочиеРасходыОстатки.ВременнаяРазницаОстаток
		|ИЗ
		|	РегистрНакопления.ПрочиеРасходы.Остатки(
		|			&НачалоПериода,
		|			Подразделение В
		|				(ВЫБРАТЬ
		|					СписокПодразделений.Подразделение
		|				ИЗ
		|					СписокПодразделений КАК СписокПодразделений)
		|			И СтатьяРасходов.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)) КАК ПрочиеРасходыОстатки
		|ГДЕ
		|	ПрочиеРасходыОстатки.Организация В
		|		(ВЫБРАТЬ
		|			СписокОрганизаций.Организация
		|		ИЗ
		|			СписокОрганизаций КАК СписокОрганизаций)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СебестоимостьТоваров.Организация,
		|	СебестоимостьТоваров.Подразделение,
		|	ЕСТЬNULL(СебестоимостьТоваров.АналитикаУчетаНоменклатуры.Назначение.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
		|	СебестоимостьТоваров.СтатьяРасходовСписания,
		|	СебестоимостьТоваров.АналитикаРасходов,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
		|		ПО СебестоимостьТоваров.СтатьяРасходовСписания = СтатьиРасходов.Ссылка
		|			И (СтатьиРасходов.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты))
		|ГДЕ
		|	СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И СебестоимостьТоваров.Активность
		|	И (СебестоимостьТоваров.Регистратор ССЫЛКА Документ.ВнутреннееПотреблениеТоваров
		|			ИЛИ СебестоимостьТоваров.Регистратор ССЫЛКА Документ.ВыпускПродукции
		|			ИЛИ СебестоимостьТоваров.Регистратор ССЫЛКА Документ.РаспределениеПроизводственныхЗатрат
		//++ НЕ УТКА
		|			ИЛИ СебестоимостьТоваров.Регистратор ССЫЛКА Документ.ПроизводствоБезЗаказа
		|			ИЛИ СебестоимостьТоваров.Регистратор ССЫЛКА Документ.ЭтапПроизводства2_2
		//-- НЕ УТКА
		|			ИЛИ СебестоимостьТоваров.Регистратор ССЫЛКА Документ.ПрочееОприходованиеТоваров
		|			ИЛИ СебестоимостьТоваров.Регистратор ССЫЛКА Документ.СписаниеНедостачТоваров)
		|	И СебестоимостьТоваров.Организация В
		|			(ВЫБРАТЬ
		|				СписокОрганизаций.Организация
		|			ИЗ
		|				СписокОрганизаций КАК СписокОрганизаций)
		|	И СебестоимостьТоваров.Подразделение В
		|			(ВЫБРАТЬ
		|				СписокПодразделений.Подразделение
		|			ИЗ
		|				СписокПодразделений КАК СписокПодразделений)
		|	И НЕ СебестоимостьТоваров.РасчетСебестоимости
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Затраты.Организация,
		|	Затраты.Подразделение,
		|	ЕСТЬNULL(Затраты.Назначение.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)),
		|	Затраты.СтатьяРасходов,
		|	Затраты.АналитикаРасходов,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0
		|ИЗ
		|	РегистрНакопления.МатериалыИРаботыВПроизводстве КАК Затраты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
		|		ПО Затраты.СтатьяРасходов = СтатьиРасходов.Ссылка
		|			И (СтатьиРасходов.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты))
		|ГДЕ
		|	Затраты.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И Затраты.Активность
		|	И Затраты.Регистратор ССЫЛКА Документ.РаспределениеПроизводственныхЗатрат
		|	И Затраты.Организация В
		|			(ВЫБРАТЬ
		|				СписокОрганизаций.Организация
		|			ИЗ
		|				СписокОрганизаций КАК СписокОрганизаций)
		|	И Затраты.Подразделение В
		|			(ВЫБРАТЬ
		|				СписокПодразделений.Подразделение
		|			ИЗ
		|				СписокПодразделений КАК СписокПодразделений)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПриход.Организация,
		|	ВТПриход.Подразделение,
		|	ВТПриход.НаправлениеДеятельности,
		|	ВТПриход.СтатьяРасходов,
		|	ВТПриход.АналитикаРасходов,
		|	СУММА(ВТПриход.СуммаУпрПриход) КАК ПоступилоУпр,
		|	СУММА(ВТПриход.СуммаУпрБезНДСПриход) КАК ПоступилоУпрБезНДС,
		|	СУММА(ВТПриход.СуммаРеглПриход) КАК ПоступилоРегл,
		|	СУММА(ВТПриход.ПостояннаяРазницаПриход) КАК ПоступилоПостояннаяРазница,
		|	СУММА(ВТПриход.ВременнаяРазницаПриход) КАК ПоступилоВременнаяРазница
		|ПОМЕСТИТЬ ВТПоступило
		|ИЗ
		|	ВТПриход КАК ВТПриход
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТПриход.Организация,
		|	ВТПриход.Подразделение,
		|	ВТПриход.НаправлениеДеятельности,
		|	ВТПриход.СтатьяРасходов,
		|	ВТПриход.АналитикаРасходов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция возвращает текст запроса для получения данных для рабочего места по распределению расходов.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстЗапросаДанныеДляРаспределения() Экспорт
	
	// Распределяемые документы.
	ТекстЗапроса = ТекстЗапросаПоступилоПрочихРасходов() + "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.Регистратор
		|ПОМЕСТИТЬ ВТРегистраторыРаспределения
		|ИЗ
		|	РегистрНакопления.ПрочиеРасходы КАК ДД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат КАК Док
		|		ПО ДД.Регистратор = Док.Ссылка
		|			И (Док.Подразделение В
		|				(ВЫБРАТЬ
		|					СписокПодразделений.Подразделение
		|				ИЗ
		|					СписокПодразделений КАК СписокПодразделений))
		|ГДЕ
		|	ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ДД.Организация В
		|			(ВЫБРАТЬ
		|				СписокОрганизаций.Организация
		|			ИЗ
		|				СписокОрганизаций КАК СписокОрганизаций)
		|	И ДД.Активность
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РаспределениеПрочихЗатрат.Ссылка КАК Документ,
		|	ВЫБОР
		|		КОГДА ВТРегистраторыРаспределения.Регистратор ЕСТЬ NULL 
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ЕстьДвиженияРаспределения,
		|	РаспределениеПрочихЗатрат.Организация КАК Организация,
		|	РаспределениеПрочихЗатрат.Подразделение КАК Подразделение,
		|	РаспределениеПрочихЗатрат.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	РаспределениеПрочихЗатрат.СтатьяРасходов КАК СтатьяРасходов,
		|	РаспределениеПрочихЗатрат.АналитикаРасходов КАК АналитикаРасходов,
		|	СУММА(ЕСТЬNULL(РаспределениеПрочихЗатратВручную.ДоляСтоимости, 0) + ЕСТЬNULL(РаспределениеПрочихЗатратНаПартииПроизводства.ДоляСтоимости, 0) + ЕСТЬNULL(РаспределениеПрочихЗатратПоБазе.ДоляСтоимости, 0) + ЕСТЬNULL(РаспределениеПрочихЗатратВыпускиБезРаспоряжения.ДоляСтоимости, 0) + ЕСТЬNULL(РаспределениеПрочихЗатратСписание.ДоляСтоимости, 0)) + РаспределениеПрочихЗатрат.ДоляСтоимостиПоПравилам КАК СуммаДолейСтоимости
		|ПОМЕСТИТЬ ВТДокументы
		|ИЗ
		|	Документ.РаспределениеПрочихЗатрат КАК РаспределениеПрочихЗатрат
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат.Вручную КАК РаспределениеПрочихЗатратВручную
		|		ПО РаспределениеПрочихЗатрат.Ссылка = РаспределениеПрочихЗатратВручную.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат.ПартииПроизводства КАК РаспределениеПрочихЗатратНаПартииПроизводства
		|		ПО РаспределениеПрочихЗатрат.Ссылка = РаспределениеПрочихЗатратНаПартииПроизводства.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат.ПоБазе КАК РаспределениеПрочихЗатратПоБазе
		|		ПО РаспределениеПрочихЗатрат.Ссылка = РаспределениеПрочихЗатратПоБазе.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат.ВыпускиБезРаспоряжения КАК РаспределениеПрочихЗатратВыпускиБезРаспоряжения
		|		ПО РаспределениеПрочихЗатрат.Ссылка = РаспределениеПрочихЗатратВыпускиБезРаспоряжения.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат.Списание КАК РаспределениеПрочихЗатратСписание
		|		ПО РаспределениеПрочихЗатрат.Ссылка = РаспределениеПрочихЗатратСписание.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРегистраторыРаспределения КАК ВТРегистраторыРаспределения
		|		ПО РаспределениеПрочихЗатрат.Ссылка = ВТРегистраторыРаспределения.Регистратор
		|ГДЕ
		|	РаспределениеПрочихЗатрат.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И РаспределениеПрочихЗатрат.Организация В
		|			(ВЫБРАТЬ
		|				СписокОрганизаций.Организация
		|			ИЗ
		|				СписокОрганизаций КАК СписокОрганизаций)
		|	И РаспределениеПрочихЗатрат.Подразделение В
		|			(ВЫБРАТЬ
		|				СписокПодразделений.Подразделение
		|			ИЗ
		|				СписокПодразделений КАК СписокПодразделений)
		|	И РаспределениеПрочихЗатрат.Проведен
		|	И РаспределениеПрочихЗатрат.СтатьяРасходов.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
		|
		|СГРУППИРОВАТЬ ПО
		|	РаспределениеПрочихЗатрат.Ссылка,
		|	РаспределениеПрочихЗатрат.Организация,
		|	РаспределениеПрочихЗатрат.Подразделение,
		|	РаспределениеПрочихЗатрат.НаправлениеДеятельности,
		|	РаспределениеПрочихЗатрат.СтатьяРасходов,
		|	РаспределениеПрочихЗатрат.АналитикаРасходов,
		|	ВЫБОР
		|		КОГДА ВТРегистраторыРаспределения.Регистратор ЕСТЬ NULL 
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ,
		|	РаспределениеПрочихЗатрат.ДоляСтоимостиПоПравилам
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////";
	
	// Данные для таблиц к распределению.
	ТекстЗапроса = ТекстЗапроса + "
	|ВЫБРАТЬ
	|	НеРаспределенные.Организация,
	|	НеРаспределенные.Подразделение,
	|	НеРаспределенные.СтатьяРасходов,
	|	НеРаспределенные.АналитикаРасходов,
	|	НеРаспределенные.НаправлениеДеятельности
	|ПОМЕСТИТЬ НеРаспределенные
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы.Остатки(
	|		&ГраницаДатаОкончания,
	|		СтатьяРасходов.ВариантРаспределенияРасходов = 
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
	|		И Подразделение В (ВЫБРАТЬ
	|								СписокПодразделений.Подразделение
	|							ИЗ
	|								СписокПодразделений КАК СписокПодразделений)
	|	) КАК НеРаспределенные
	|ГДЕ
	//	ПР не включаются в проверку так как суммы ПР могут быть на конец периода
	|	Организация В
	|		(ВЫБРАТЬ
	|			СписокОрганизаций.Организация
	|		ИЗ
	|			СписокОрганизаций КАК СписокОрганизаций)
	|	И (НеРаспределенные.СуммаОстаток <> 0
	|	ИЛИ НеРаспределенные.СуммаБезНДСОстаток <> 0
	|	ИЛИ НеРаспределенные.СуммаРеглОстаток <> 0
	|	ИЛИ НеРаспределенные.ВременнаяРазницаОстаток <> 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Задания.Организация,
	|	МИНИМУМ(Задания.Месяц) КАК Месяц
	|ПОМЕСТИТЬ Задания
	|ИЗ
	|	РегистрСведений.ЗаданияКРасчетуСебестоимости КАК Задания
	|ГДЕ
	|	Задания.Организация В (ВЫБРАТЬ
	|								СписокОрганизаций.Организация
	|							ИЗ
	|								СписокОрганизаций КАК СписокОрганизаций)
	|СГРУППИРОВАТЬ ПО
	|	Задания.Организация
	|;
	|
	|ВЫБРАТЬ
	|	ВТПоступило.Организация КАК Организация,
	|	ВТПоступило.Подразделение КАК Подразделение,
	|	ВТПоступило.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВТПоступило.СтатьяРасходов КАК СтатьяРасходов,
	|	ВТПоступило.АналитикаРасходов КАК АналитикаРасходов,
	|	ВТДокументы.Документ КАК Документ,
	|	ВТПоступило.ПоступилоУпр КАК ПоступилоУпр,
	|	ВТПоступило.ПоступилоУпрБезНДС КАК ПоступилоУпрБезНДС,
	|	ВТПоступило.ПоступилоРегл КАК ПоступилоРегл,
	|	ВТПоступило.ПоступилоПостояннаяРазница КАК ПоступилоПостояннаяРазница,
	|	ВТПоступило.ПоступилоВременнаяРазница КАК ПоступилоВременнаяРазница,
	|	ВЫБОР
	// если месяц закрыт и расходов нет, то состояние Распределено 
	|		КОГДА (Задания.Месяц > &КонецПериода ИЛИ Задания.Месяц ЕСТЬ NULL) И НеРаспределенные.СтатьяРасходов ЕСТЬ NULL 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.Распределено)
	// если не настроено распределение долей, и есть остаток к распределению, то требуется настройка
	|		КОГДА ЕСТЬNULL(ВТДокументы.СуммаДолейСтоимости, 0) = 0 И НЕ НеРаспределенные.СтатьяРасходов ЕСТЬ NULL
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ТребуетсяНастройкаРаспределения)
	// если распределение долей есть, документ распределения сделал движения, но есть остаток к распределеню, то есть ошибка
	|		КОГДА ЕСТЬNULL(ВТДокументы.СуммаДолейСтоимости, 0) > 0 И ЕСТЬNULL(ВТДокументы.ЕстьДвиженияРаспределения, ЛОЖЬ) И НЕ НеРаспределенные.СтатьяРасходов ЕСТЬ NULL
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ОшибкаРаспределения)
	// если распределение долей есть, документ распределения сделал движения и статья полностью распределена то расходы распределены
	|		КОГДА ЕСТЬNULL(ВТДокументы.СуммаДолейСтоимости, 0) > 0 И ЕСТЬNULL(ВТДокументы.ЕстьДвиженияРаспределения, ЛОЖЬ) И НеРаспределенные.СтатьяРасходов ЕСТЬ NULL 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.Распределено)
	// если распределение долей есть, документ распределения не сделал движения, то расходы готовы к распределению
	|		КОГДА ЕСТЬNULL(ВТДокументы.СуммаДолейСтоимости, 0) > 0 И НЕ ЕСТЬNULL(ВТДокументы.ЕстьДвиженияРаспределения, ЛОЖЬ)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ГотовоКРаспределениюПоБазе)
	// не классифицированный случай
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ОшибкаРаспределения)
	|	КОНЕЦ КАК Состояние
	|ПОМЕСТИТЬ ВТДанныеДляРаспределения
	|ИЗ
	|	ВТПоступило КАК ВТПоступило
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДокументы КАК ВТДокументы
	|		ПО	ВТПоступило.СтатьяРасходов = ВТДокументы.СтатьяРасходов
	|			И ВТПоступило.АналитикаРасходов = ВТДокументы.АналитикаРасходов
	|			И ВТПоступило.Подразделение		= ВТДокументы.Подразделение
	|			И ВТПоступило.НаправлениеДеятельности	= ВТДокументы.НаправлениеДеятельности
	|			И ВТПоступило.Организация		= ВТДокументы.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ НеРаспределенные КАК НеРаспределенные
	|		ПО	ВТПоступило.СтатьяРасходов = НеРаспределенные.СтатьяРасходов
	|			И ВТПоступило.АналитикаРасходов = НеРаспределенные.АналитикаРасходов
	|			И ВТПоступило.Подразделение		= НеРаспределенные.Подразделение
	|			И ВТПоступило.Организация		= НеРаспределенные.Организация
	|			И ВТПоступило.НаправлениеДеятельности		= НеРаспределенные.НаправлениеДеятельности
	|		ЛЕВОЕ СОЕДИНЕНИЕ Задания КАК Задания
	|		ПО ВТПоступило.Организация = Задания.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДанныеДляЗагрузки.Организация КАК Организация,
	|	ВТДанныеДляЗагрузки.Подразделение КАК Подразделение,
	|	ВТДанныеДляЗагрузки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВТДанныеДляЗагрузки.СтатьяРасходов КАК СтатьяРасходов,
	|	ВТДанныеДляЗагрузки.АналитикаРасходов КАК АналитикаРасходов,
	|	ВТДанныеДляЗагрузки.Документ,
	|	ВТДанныеДляЗагрузки.Состояние
	|ИЗ
	|	ВТДанныеДляРаспределения КАК ВТДанныеДляЗагрузки
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ФильтрПоСостоянию = ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|			ИНАЧЕ ВТДанныеДляЗагрузки.Состояние = &ФильтрПоСостоянию
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация,
	|	Подразделение,
	|	НаправлениеДеятельности,
	|	СтатьяРасходов,
	|	АналитикаРасходов
	|;
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция возвращает статью калькуляции для документа распределения по данным правила распределения по этапам
// и распределяемой статьи расходов.
//
//	Параметры:
//		СтатьяРасходов - ПланВидовХарактеристикСсылка.СтатьиРасходов - статья расходов,
//		ПравилоРаспределенияПоЭтапам - СправочникСсылка.ПравилаРаспределенияРасходов - Правило распределения по этапам.
//	
//	Возвращаемое значение:
//		СправочникСсылка.СтатьиКалькуляции - статья калькуляции.
//
Функция ПолучитьСтатьюКалькуляцииДляДокумента(СтатьяРасходов, ПравилоРаспределенияПоЭтапам) Экспорт
	
	Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	Правило.СтатьяКалькуляции КАК СтатьяКалькуляции
		|ИЗ
		|	Справочник.ПравилаРаспределенияРасходов КАК Правило
		|ГДЕ
		|	Правило.Ссылка = &Правило
		|	И Правило.СтатьяКалькуляции <> ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Статья.СтатьяКалькуляции
		|ИЗ
		|	ПланВидовХарактеристик.СтатьиРасходов КАК Статья
		|ГДЕ
		|	Статья.Ссылка = &Статья
		|	И Статья.СтатьяКалькуляции <> ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
		|");
	
	Запрос.УстановитьПараметр("Правило", ПравилоРаспределенияПоЭтапам);
	Запрос.УстановитьПараметр("Статья", СтатьяРасходов);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.СтатьяКалькуляции;
	
КонецФункции

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	Возврат;
КонецПроцедуры

// Заполняет таблицу реквизитов, зависимых от варианта распределения.
//
// Параметры:
//	ВариантРаспределения - ПеречислениеСсылка.СпособыРаспределенияСтатейРасходов - вариант распределения статьи
//	МассивВсехРеквизитов - Массив - реквизиты, которые не зависят от хозяйственной операции
//	МассивРеквизитовОперации - Массив - реквизиты, которые зависят от хозяйственной операции
//
Процедура ИменаЭлементовФормыПоВариантуРаспределения(ВариантРаспределения, МассивВсехРеквизитов, МассивРеквизитовОперации) Экспорт
	
	НастройкиПроизводства = ПроизводствоСервер.НастройкиПодсистемыПроизводство();
	ПартионныйУчетВерсии22 = ПолучитьФункциональнуюОпцию("ПартионныйУчетВерсии22");
	
	МассивВсехРеквизитов = Новый Массив;
	
	МассивВсехРеквизитов.Добавить("ПоПодразделениямПоПравилу");
	МассивВсехРеквизитов.Добавить("ПоПодразделениямПоВручную");
	МассивВсехРеквизитов.Добавить("ПоЭтапамПоПравилу");
	МассивВсехРеквизитов.Добавить("ПоЭтапамВручную");
	МассивВсехРеквизитов.Добавить("ПоПартиямПроизводства");
	МассивВсехРеквизитов.Добавить("НаВыпускиБезРаспоряжений");
	МассивВсехРеквизитов.Добавить("ДоляСтоимостиПоЭтапамПоПравилу");
	МассивВсехРеквизитов.Добавить("СтатьяКалькуляции");
	МассивВсехРеквизитов.Добавить("ГруппаПоБазеОтборПоВидамПродукции");
	МассивВсехРеквизитов.Добавить("ДанныеДляБазыРаспределения");
	
	МассивРеквизитовОперации = Новый Массив;
	Если ВариантРаспределения = Перечисления.СпособыРаспределенияСтатейРасходов.ПоПодразделениямИЭтапамПоПравилам Тогда
		МассивРеквизитовОперации.Добавить("ПоПодразделениямПоПравилу");
		МассивРеквизитовОперации.Добавить("ПоЭтапамПоПравилу");
		МассивРеквизитовОперации.Добавить("СтатьяКалькуляции");
		
		Если Не ПартионныйУчетВерсии22 Тогда
			МассивРеквизитовОперации.Добавить("ДанныеДляБазыРаспределения");
		КонецЕсли;
		
	ИначеЕсли ВариантРаспределения = Перечисления.СпособыРаспределенияСтатейРасходов.ПоПодразделениямВручнуюПоЭтапамПоПравилу Тогда
		МассивРеквизитовОперации.Добавить("ПоПодразделениямПоВручную");
		МассивРеквизитовОперации.Добавить("ПоЭтапамПоПравилу");
		
		Если Не ПартионныйУчетВерсии22 Тогда
			МассивРеквизитовОперации.Добавить("ДанныеДляБазыРаспределения");
		КонецЕсли;
		
	ИначеЕсли ВариантРаспределения = Перечисления.СпособыРаспределенияСтатейРасходов.ПоЭтапамПоПравилуВДанномПодразделении Тогда
		МассивРеквизитовОперации.Добавить("ПоЭтапамПоПравилу");
		МассивРеквизитовОперации.Добавить("ДоляСтоимостиПоЭтапамПоПравилу");
		МассивРеквизитовОперации.Добавить("СтатьяКалькуляции");
		
		Если Не ПартионныйУчетВерсии22 Тогда
			МассивРеквизитовОперации.Добавить("ДанныеДляБазыРаспределения");
		КонецЕсли;
		
	ИначеЕсли ВариантРаспределения = Перечисления.СпособыРаспределенияСтатейРасходов.ПоЭтапамПоПравилуПоВсемПодразделениям Тогда
		МассивРеквизитовОперации.Добавить("ПоЭтапамПоПравилу");
		МассивРеквизитовОперации.Добавить("ДоляСтоимостиПоЭтапамПоПравилу");
		МассивРеквизитовОперации.Добавить("СтатьяКалькуляции");
		
		Если Не ПартионныйУчетВерсии22 Тогда
			МассивРеквизитовОперации.Добавить("ДанныеДляБазыРаспределения");
		КонецЕсли;
		
	ИначеЕсли ВариантРаспределения = Перечисления.СпособыРаспределенияСтатейРасходов.ПоЭтапамВручнуюПоВсемПодразделениям Тогда
		
		Если НастройкиПроизводства.ИспользуетсяПроизводство22 Тогда
			МассивРеквизитовОперации.Добавить("ПоПартиямПроизводства");
		КонецЕсли;
		
		Если НастройкиПроизводства.ИспользуетсяПроизводство21 Тогда
//++ НЕ УТКА
			МассивРеквизитовОперации.Добавить("ПоЭтапамВручную");
//-- НЕ УТКА
			МассивРеквизитовОперации.Добавить("НаВыпускиБезРаспоряжений");
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("АналитическийУчетПоГруппамПродукции") Тогда
		МассивРеквизитовОперации.Добавить("ГруппаПоБазеОтборПоВидамПродукции");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

#КонецОбласти

#Область БлокировкаПриОбновленииИБ

Процедура ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ПредставлениеОперации)
	
	ВходящиеДанные = Новый Соответствие;
	
	ВходящиеДанные.Вставить(Метаданные.Документы.РаспределениеПрочихЗатрат);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.МатериалыИРаботыВПроизводстве);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПрочиеРасходы);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПрочиеРасходыНезавершенногоПроизводства);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.СебестоимостьТоваров);
	
	ЗакрытиеМесяцаУТВызовСервера.ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ВходящиеДанные, ПредставлениеОперации);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
