#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Заполняет список команд создания на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании) Экспорт


	ВводНаОснованииПереопределяемый.ДобавитьКомандуСоздатьНаОснованииБизнесПроцессЗадание(КомандыСоздатьНаОсновании);


КонецПроцедуры

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСоздатьНаОсновании) Экспорт

	 
	Если ПравоДоступа("Добавление", Метаданные.Документы.ЛистКассовойКниги) Тогда
		КомандаСоздатьНаОсновании = КомандыСоздатьНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Идентификатор = Метаданные.Документы.ЛистКассовойКниги.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ВводНаОсновании.ПредставлениеОбъекта(Метаданные.Документы.ЛистКассовойКниги);
		КомандаСоздатьНаОсновании.ПроверкаПроведенияПередСозданиемНаОсновании = Истина;
		
	

		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов) Экспорт

	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);

	Возврат; //В дальнейшем будет добавлен код команд

КонецПроцедуры

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

	// Лист кассовой книги
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ЛистКассовойКниги";
	КомандаПечати.Представление = НСтр("ru = 'Лист кассовой книги'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	
	// 4D:ERP для Беларуси, ВладимирР, 06.02.2015 14:03:12 
	// Задача №9059, Кассовая книга в валюте
	// {
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ЛистВалютнойКассовойКниги_Локализация";
	КомандаПечати.Представление = НСтр("ru = 'Лист валютной кассовой книги'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	// }
	// 4D

	// Титульный лист и последняя страница
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Обложка";
	КомандаПечати.Представление = НСтр("ru = 'Титульный лист и последняя страница'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;

КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЛистКассовойКниги") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ЛистКассовойКниги",
			НСтр("ru='Лист кассовой книги'"),
			СформироватьПечатнуюФормуЛистаКассовойКниги(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
		
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Обложка") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"Обложка",
		НСтр("ru='Титульный лист кассовой книги'"),
		СформироватьПечатнуюФормуОбложкиИПоследнегоЛистаКассовойКниги(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
	// 4D:ERP для Беларуси, ВладимирР, 06.02.2015 14:03:12 
	// Задача №9059, Кассовая книга в валюте
	// {
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЛистВалютнойКассовойКниги_Локализация") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
		КоллекцияПечатныхФорм,
		"ЛистВалютнойКассовойКниги_Локализация",
		НСтр("ru='Лист валютной кассовой книги'"),
		СформироватьПечатнуюФормуЛистаВалютнойКассовойКниги_Локализация(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	// }
	// 4D
	
КонецПроцедуры

Функция ПолучитьКоличествоЛистовКассовойКнигиЗаПериодПоОрганизации(Организация, КассоваяКнига, ДатаНач, ДатаКон)
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ КассоваяКнигаДокументы.НомерЛиста) КАК КоличествоЛистов
	|ИЗ
	|	Документ.ЛистКассовойКниги КАК ДанныеДокумента
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.ЛистКассовойКниги.КассовыеОрдера КАК КассоваяКнигаДокументы
	|	ПО	
	|		ДанныеДокумента.Ссылка = КассоваяКнигаДокументы.Ссылка
	|ГДЕ
	|	ДанныеДокумента.Дата МЕЖДУ &ДатаНач И &ДатаКон
	|	И ДанныеДокумента.Организация = &Организация
	|	И ДанныеДокумента.КассоваяКнига = &КассоваяКнига
	|	И ДанныеДокумента.Проведен
	|");
	
	Запрос.УстановитьПараметр("ДатаНач",       ДатаНач);
	Запрос.УстановитьПараметр("ДатаКон",       ДатаКон);
	Запрос.УстановитьПараметр("Организация",   Организация);
	Запрос.УстановитьПараметр("КассоваяКнига", КассоваяКнига);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.КоличествоЛистов;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции

Процедура ДобавитьОбороты(РабочаяТаблица, Валюта, ВалПриход, Приход, ВалРасход, Расход)
	
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();

	СтрокаТаблицы = РабочаяТаблица.Найти(Валюта, "Валюта");
	
	Если СтрокаТаблицы = Неопределено Тогда
		
		НоваяСтрока = РабочаяТаблица.Добавить();
		
		НоваяСтрока.Валюта     = Валюта;
		Если НоваяСтрока.Валюта = Справочники.Валюты.ПустаяСсылка() Тогда 
			НоваяСтрока.Валюта = ВалютаРегламентированногоУчета;
		КонецЕсли;
		НоваяСтрока.ВалютаНаименованиеПолное = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НоваяСтрока.Валюта, "НаименованиеПолное");

		НоваяСтрока.ВалОстаток = 0;
		НоваяСтрока.Остаток    = 0;
		НоваяСтрока.ВалПриход  = ВалПриход;
		НоваяСтрока.Приход     = Приход;
		НоваяСтрока.ВалРасход  = ВалРасход;
		НоваяСтрока.Расход     = Расход;
		
	Иначе
		
		СтрокаТаблицы.ВалПриход = СтрокаТаблицы.ВалПриход + ?(ВалПриход=null,0,ВалПриход);
		СтрокаТаблицы.Приход    = СтрокаТаблицы.Приход    + ?(Приход=null,0,Приход);
		СтрокаТаблицы.ВалРасход = СтрокаТаблицы.ВалРасход + ?(ВалРасход=null,0,ВалРасход);
		СтрокаТаблицы.Расход    = СтрокаТаблицы.Расход    + ?(Расход=null,0,Расход);
		
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьПечатнуюФормуОбложкиИПоследнегоЛистаКассовойКниги(МассивОбъектов, ОбъектыПечати)
	
	// Печать обложки и титульного листа
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ОбложкаКассовойКниги";
	МакетОбложка 	  = УправлениеПечатью.МакетПечатнойФормы("Документ.ЛистКассовойКниги.ПФ_MXL_Обложка");
	ОбластьОбложка 	  = МакетОбложка.ПолучитьОбласть("Обложка");
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(ДокументКассоваяКнига.Дата, ГОД) КАК Год,
	|	ДокументКассоваяКнига.Организация КАК Организация,
	|	ДокументКассоваяКнига.КассоваяКнига КАК КассоваяКнига,
	|	ВЫРАЗИТЬ(ДокументКассоваяКнига.КассоваяКнига.СтруктурноеПодразделение КАК СТРОКА(1000)) КАК НаименованиеПодразделения,
	|	ВЫРАЗИТЬ(ТаблицаОтветственныеЛица.РуководительНаименование КАК СТРОКА(1000)) КАК Руководитель,
	|	ВЫРАЗИТЬ(ТаблицаОтветственныеЛица.РуководительДолжность КАК СТРОКА(1000)) КАК ДолжностьРуководителя,
	|	ВЫРАЗИТЬ(ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК СТРОКА(1000)) КАК ГлавныйБухгалтер,
	|	ДокументКассоваяКнига.Организация.КодПоОКПО КАК КодПоОКПО,
	|	ПОДСТРОКА(ДокументКассоваяКнига.Организация.НаименованиеПолное, 1, 200) КАК НаименованиеОрганизации
	|ИЗ
	|	Документ.ЛистКассовойКниги КАК ДокументКассоваяКнига
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО ДокументКассоваяКнига.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|ГДЕ
	|	ДокументКассоваяКнига.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Результат = Запрос.Выполнить();
	Выборка   = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		КоличествоЛистов = ПолучитьКоличествоЛистовКассовойКнигиЗаПериодПоОрганизации(
			Выборка.Организация, Выборка.КассоваяКнига,
			НачалоГода(Выборка.Год),
			КонецГода(Выборка.Год));
		
		ОбластьОбложка.Параметры.НазваниеОрганизации   = Выборка.НаименованиеОрганизации;
		ОбластьОбложка.Параметры.НазваниеПодразделения = Выборка.НаименованиеПодразделения;
		ОбластьОбложка.Параметры.НадписьОбложка        = " на "+Формат(Выборка.Год, "ДФ=yyyy") + " г.";
		ОбластьОбложка.Параметры.КодОКПО               = Выборка.КодПоОКПО;
		
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		ТабличныйДокумент.Вывести(ОбластьОбложка);
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		// Последний лист кассовой книги
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		ОбластьПослДеньГода = МакетОбложка.ПолучитьОбласть("ПослДеньГода");
		
		ОбластьПослДеньГода.Параметры.ГлБухгалтер           = СокрЛП(Выборка.ГлавныйБухгалтер);
		ОбластьПослДеньГода.Параметры.Руководитель          = СокрЛП(Выборка.Руководитель);
		ОбластьПослДеньГода.Параметры.РуководительДолжность = СокрЛП(Выборка.ДолжностьРуководителя);
		ОбластьПослДеньГода.Параметры.ЛистовЗаГод           = КоличествоЛистов;
		
		ТабличныйДокумент.Вывести(ОбластьПослДеньГода);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(
			ТабличныйДокумент,
			НомерСтрокиНачало,
			ОбъектыПечати,
			Неопределено); // Ссылка
		
	КонецЦикла;
	
	ТабличныйДокумент.ОтображатьСетку     = Ложь;
	ТабличныйДокумент.ОтображатьЗаголовки = Ложь;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция СформироватьПечатнуюФормуЛистаКассовойКниги(МассивОбъектов, ОбъектыПечати, ПараметрыПечати = Неопределено)
	
	// 4D:ERP для Беларуси, ВладимирР, 03.08.2015 16:48:38 
	// Права на кассовую книгу, №10138
	// {
	УстановитьПривилегированныйРежим(Истина); // TODO
	// }
	// 4D
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЛистКассовойКниги";
	ВыводитьОснования = Ложь;
	
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	ВалютаРеглУчетаНаименованиеПолное = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВалютаРегламентированногоУчета, "НаименованиеПолное");
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДокументКассоваяКнига.Ссылка КАК Ссылка,
	|	ДокументКассоваяКнига.ТипЛиста КАК ТипЛиста,
	|	ДокументКассоваяКнига.КоличествоДокументовНаЛисте КАК КоличествоДокументовНаЛисте,
	|	ДокументКассоваяКнига.Дата КАК Дата,
	|	ДокументКассоваяКнига.Организация КАК Организация,
	|	ДокументКассоваяКнига.КассоваяКнига КАК КассоваяКнига,
	|	ДокументКассоваяКнига.Ответственный.ФизическоеЛицо КАК Ответственный,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер
	|ИЗ
	|	Документ.ЛистКассовойКниги КАК ДокументКассоваяКнига
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО ДокументКассоваяКнига.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|ГДЕ
	|	ДокументКассоваяКнига.Ссылка В(&МассивДокументов)
	|	И ДокументКассоваяКнига.Проведен
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата,
	|	Ссылка";
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	
	Результат = Запрос.Выполнить();
	ВыборкаПоДокументамКассоваяКнига = Результат.Выбрать();
	
	Пока ВыборкаПоДокументамКассоваяКнига.Следующий() Цикл
		
		ЛистКассовойКниги = ВыборкаПоДокументамКассоваяКнига.Ссылка;
		
		ДатаНач = НачалоДня(ВыборкаПоДокументамКассоваяКнига.Дата);
		ДатаКон = КонецДня(ВыборкаПоДокументамКассоваяКнига.Дата);
		ГраницаКон = Новый Граница(ДатаКон, ВидГраницы.Включая);
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Организация = ВыборкаПоДокументамКассоваяКнига.Организация;
		КассоваяКнига = ВыборкаПоДокументамКассоваяКнига.КассоваяКнига;
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ЛистКассовойКниги.ПФ_MXL_ЛистКассовойКниги");
		
		ОбластьВалОстатокОтчет              = Макет.ПолучитьОбласть("ВалОстаток|Отчет");
		ОбластьПодвалОтчет                  = Макет.ПолучитьОбласть("Подвал|Отчет");
		ОбластьВТомЧислеОтчет               = Макет.ПолучитьОбласть("ВТомЧисле|Отчет");
		ОбластьОстатокОтчет                 = Макет.ПолучитьОбласть("Остаток|Отчет");
		ОбластьЛистовЗаМесяцОтчет           = Макет.ПолучитьОбласть("ЛистовЗаМесяц|Отчет");
		ОбластьЛистовЗаГодОтчет             = Макет.ПолучитьОбласть("ЛистовЗаГод|Отчет");
		ОбластьВкладнойЛистОтчет            = Макет.ПолучитьОбласть("ВкладнойЛист|Отчет");
		ОбластьВкладнойЛист373ПОтчет        = Макет.ПолучитьОбласть("ВкладнойЛист373П|Отчет");
		ОбластьОтчетКассираОтчет            = Макет.ПолучитьОбласть("ОтчетКассира|Отчет");
		ОбластьОтчетКассира373ПОтчет        = Макет.ПолучитьОбласть("ОтчетКассира373П|Отчет");
		ОбластьШапкаОтчет                   = Макет.ПолучитьОбласть("Шапка|Отчет");
		ОбластьОстатокНаНДОтчет             = Макет.ПолучитьОбласть("ОстатокНаНД|Отчет");
		ОбластьКурсовыеРазницыОтчет         = Макет.ПолучитьОбласть("КурсовыеРазницы|Отчет");
		ОбластьКурсовыеРазницыПоВалютеОтчет = Макет.ПолучитьОбласть("КурсовыеРазницыПоВалюте|Отчет");
		ОбластьПереносОтчет                 = Макет.ПолучитьОбласть("Перенос|Отчет");
		ОбластьСтрокаВалШирокаяОтчет        = Макет.ПолучитьОбласть("СтрокаВалШирокая|Отчет");
		ОбластьСтрокаВалОтчет               = Макет.ПолучитьОбласть("СтрокаВал|Отчет");
		ОбластьСтрокаШирокаяОтчет           = Макет.ПолучитьОбласть("СтрокаШирокая|Отчет");
		ОбластьСтрокаОтчет                  = Макет.ПолучитьОбласть("Строка|Отчет");
		ОбластьОборотОтчет                  = Макет.ПолучитьОбласть("Оборот|Отчет");
		ОбластьОборотРубОтчет               = Макет.ПолучитьОбласть("ОборотРуб|Отчет");
		ОбластьОборотВалОтчет               = Макет.ПолучитьОбласть("ОборотВал|Отчет");
		ОбластьКурсоваяРазницаОтчет         = Макет.ПолучитьОбласть("КурсоваяРазница|Отчет");
		ОбластьКурсоваяРазницаПоВалютеОтчет = Макет.ПолучитьОбласть("КурсоваяРазницаПоВалюте|Отчет");
		ОбластьКонечныйОстатокОтчет         = Макет.ПолучитьОбласть("КонечныйОстаток|Отчет");
		
		ЗапросПоИтогам = Новый Запрос;
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	Кассы.Ссылка КАК Касса
		|ПОМЕСТИТЬ Кассы
		|ИЗ
		|	Справочник.Кассы КАК Кассы
		|ГДЕ
		|	Кассы.Владелец = &Организация
		|	И Кассы.КассоваяКнига = &КассоваяКнига
		|;
		|////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ
		|	ДенежныеСредстваНаличныеОстатки.Касса.ВалютаДенежныхСредств КАК Валюта,
		|	ДенежныеСредстваНаличныеОстатки.Касса.ВалютаДенежныхСредств.НаименованиеПолное КАК ВалютаНаименованиеПолное,
		|	СУММА(ДенежныеСредстваНаличныеОстатки.СуммаРеглОстаток) КАК СуммаНачальныйОстатокДт,
		|	СУММА(ВЫБОР КОГДА ДенежныеСредстваНаличныеОстатки.Касса.ВалютаДенежныхСредств = &ВалютаРеглУчета ТОГДА
		|		0
		|	ИНАЧЕ
		|		ДенежныеСредстваНаличныеОстатки.СуммаОстаток
		|	КОНЕЦ) КАК ВалютнаяСуммаНачальныйОстатокДт
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваНаличные.Остатки(
		|		&ДатаНач,
		|		Организация = &Организация
		|			И Касса В (ВЫБРАТЬ Кассы.Касса ИЗ Кассы)) КАК ДенежныеСредстваНаличныеОстатки
		|СГРУППИРОВАТЬ ПО
		|	ДенежныеСредстваНаличныеОстатки.Касса.ВалютаДенежныхСредств
		|УПОРЯДОЧИТЬ ПО
		|	Валюта
		|АВТОУПОРЯДОЧИВАНИЕ
		|;
		|////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ
		|	ДенежныеСредстваНаличныеОстатки.Касса.ВалютаДенежныхСредств КАК Валюта,
		|	ДенежныеСредстваНаличныеОстатки.Касса.ВалютаДенежныхСредств.НаименованиеПолное КАК ВалютаНаименованиеПолное,
		|	СУММА(ДенежныеСредстваНаличныеОстатки.СуммаРеглОстаток) КАК СуммаОстаток,
		|	СУММА(ВЫБОР КОГДА ДенежныеСредстваНаличныеОстатки.Касса.ВалютаДенежныхСредств = &ВалютаРеглУчета ТОГДА
		|		0
		|	ИНАЧЕ
		|		ДенежныеСредстваНаличныеОстатки.СуммаОстаток
		|	КОНЕЦ) КАК ВалютнаяСуммаОстаток
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваНаличные.Остатки(
		|		&ГраницаКон,
		|		Организация = &Организация
		|			И Касса В (ВЫБРАТЬ Кассы.Касса ИЗ Кассы)) КАК ДенежныеСредстваНаличныеОстатки
		|СГРУППИРОВАТЬ ПО
		|	ДенежныеСредстваНаличныеОстатки.Касса.ВалютаДенежныхСредств
		|УПОРЯДОЧИТЬ ПО
		|	Валюта
		|АВТОУПОРЯДОЧИВАНИЕ
		|;
		|////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ
		|	ДенежныеСредства.Касса.ВалютаДенежныхСредств КАК Валюта,
		|	ЕСТЬNULL(СУММА(
		|		ВЫБОР КОГДА ДенежныеСредства.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
		|			ДенежныеСредства.Сумма
		|		КОГДА ДенежныеСредства.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
		|			-ДенежныеСредства.Сумма
		|		КОНЕЦ
		|	), 0) КАК СуммаИнвентаризации,
		|	ЕСТЬNULL(СУММА(
		|		ВЫБОР КОГДА ДенежныеСредства.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
		|			ДенежныеСредства.СуммаРегл
		|		КОГДА ДенежныеСредства.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
		|			-ДенежныеСредства.СуммаРегл
		|		КОНЕЦ
		|	), 0) КАК СуммаИнвентаризацииРегл
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваНаличные КАК ДенежныеСредства
		|
		|ГДЕ
		|	ДенежныеСредства.Период МЕЖДУ &ДатаНач И &ДатаКон
		|	И Организация = &Организация
		|	И Касса В (ВЫБРАТЬ Кассы.Касса ИЗ Кассы)
		|	И ДенежныеСредства.ХозяйственнаяОперация В
		|		(ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтражениеИзлишкаПриИнвентаризацииДенежныхСредств),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтражениеНедостачиПриИнвентаризацииДенежныхСредств))
		|
		|СГРУППИРОВАТЬ ПО
		|	ДенежныеСредства.Касса.ВалютаДенежныхСредств
		|";
		
		ЗапросПоИтогам.Текст = ТекстЗапроса;
		
		ЗапросПоИтогам.УстановитьПараметр("ДатаНач", ДатаНач);
		ЗапросПоИтогам.УстановитьПараметр("ДатаКон", ДатаКон);
		ЗапросПоИтогам.УстановитьПараметр("ГраницаКон", ГраницаКон);
		ЗапросПоИтогам.УстановитьПараметр("Организация", Организация);
		ЗапросПоИтогам.УстановитьПараметр("КассоваяКнига", КассоваяКнига);
		ЗапросПоИтогам.УстановитьПараметр("ВалютаРеглУчета", ВалютаРегламентированногоУчета);
		
		МассивРезультатов = ЗапросПоИтогам.ВыполнитьПакет();
		РезультатЗапросаПоИтогамНаНачало = МассивРезультатов[1];
		РезультатЗапросаПоИтогам = МассивРезультатов[2];
		РезультатЗапросаПоИнвентаризации = МассивРезультатов[3];
		
		ЗапросПоКурсовымРазницам = Новый Запрос;
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДенежныеСредства.Касса.ВалютаДенежныхСредств КАК Валюта,
		|	ДенежныеСредства.Касса.ВалютаДенежныхСредств.НаименованиеПолное КАК ВалютаНаименованиеПолное,
		|	СУММА(
		|		ВЫБОР КОГДА ДенежныеСредства.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
		|			ДенежныеСредства.СуммаРегл
		|		КОГДА ДенежныеСредства.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
		|			-ДенежныеСредства.СуммаРегл
		|		КОНЕЦ
		|	) КАК СуммаКурсовойРазницы
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваНаличные КАК ДенежныеСредства
		|
		|ГДЕ
		|	ДенежныеСредства.Период МЕЖДУ &ДатаНач И &ДатаКон
		|	И ДенежныеСредства.Организация = &Организация
		|	И ДенежныеСредства.Касса.КассоваяКнига = &КассоваяКнига
		|	И ДенежныеСредства.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереоценкаДенежныхСредств)
		|	И ДенежныеСредства.СуммаРегл <> 0
		|	
		|СГРУППИРОВАТЬ ПО
		|	ДенежныеСредства.Касса.ВалютаДенежныхСредств
		|
		|УПОРЯДОЧИТЬ ПО
		|	Валюта
		|ИТОГИ
		|	СУММА(СуммаКурсовойРазницы)
		|ПО
		|	ОБЩИЕ,
		|	Валюта
		|АВТОУПОРЯДОЧИВАНИЕ";

		ЗапросПоКурсовымРазницам.Текст = ТекстЗапроса;
		
		ЗапросПоКурсовымРазницам.УстановитьПараметр("ДатаНач",ДатаНач);
		ЗапросПоКурсовымРазницам.УстановитьПараметр("ДатаКон", ДатаКон);
		ЗапросПоКурсовымРазницам.УстановитьПараметр("Организация",Организация);
		ЗапросПоКурсовымРазницам.УстановитьПараметр("КассоваяКнига", КассоваяКнига);
		РезультатЗапросаПоКурсовымРазницам = ЗапросПоКурсовымРазницам.Выполнить();
		
		ДатаНач = НачалоДня(ВыборкаПоДокументамКассоваяКнига.Дата); 
		
		ЗапросПоДокументам = Новый Запрос("
		|ВЫБРАТЬ
		|	КассоваяКнигаДокументы.Документ КАК Документ,
		|	НАЧАЛОПЕРИОДА(КассоваяКнигаДокументы.Документ.Дата, ДЕНЬ) КАК День,
		|	КассоваяКнигаДокументы.Документ.Дата КАК ДатаДок,
		|	КассоваяКнигаДокументы.Документ.Номер КАК НомерДок,
		|	КассоваяКнигаДокументы.Документ.Валюта КАК Валюта,
		|	ПОДСТРОКА(КассоваяКнигаДокументы.Документ.Основание, 1, 200) КАК Основание,
		|	ВЫБОР
		|		КОГДА КассоваяКнигаДокументы.Документ ССЫЛКА Документ.ПриходныйКассовыйОрдер
		|			ТОГДА КассоваяКнигаДокументы.Документ.ПринятоОт
		|		ИНАЧЕ КассоваяКнигаДокументы.Документ.Выдать
		|	КОНЕЦ КАК ТекстДок,
		|	ЕСТЬNULL(КассоваяКнигаДокументы.Приход, 0) КАК Приход,
		|	ЕСТЬNULL(КассоваяКнигаДокументы.Расход, 0) КАК Расход,
		|	КассоваяКнигаДокументы.КорреспондирующийСчет КАК Счет,
		|	КассоваяКнигаДокументы.НомерЛиста КАК НомерЛиста,
		|	КассоваяКнигаДокументы.НомерСтроки КАК НомерСтроки,
		|	ДанныеДокумента.ТипЛиста КАК ТипЛиста
		|ИЗ
		|	Документ.ЛистКассовойКниги КАК ДанныеДокумента
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		Документ.ЛистКассовойКниги.КассовыеОрдера КАК КассоваяКнигаДокументы
		|	ПО
		|		ДанныеДокумента.Ссылка = КассоваяКнигаДокументы.Ссылка
		|ГДЕ
		|	ДанныеДокумента.Дата МЕЖДУ &ДатаНач И &ДатаКон
		|	И ДанныеДокумента.Организация = &Организация
		|	И ДанныеДокумента.Ссылка = &ЛистКассовойКниги
		|	И ДанныеДокумента.Проведен
		|	И ТИПЗНАЧЕНИЯ(КассоваяКнигаДокументы.Документ) В (ТИП(Документ.ПриходныйКассовыйОрдер), ТИП(Документ.РасходныйКассовыйОрдер))
		|
		|УПОРЯДОЧИТЬ ПО
		|	День,
		|	НомерЛиста,
		|	НомерСтроки
		|ИТОГИ
		|	СУММА(Приход),
		|	СУММА(Расход),
		|	МИНИМУМ(НомерЛиста)
		|ПО
		|	День,
		|	НомерСтроки,
		|	Документ,
		|	Счет
		|");
		ЗапросПоДокументам.УстановитьПараметр("ДатаНач", ДатаНач);
		ЗапросПоДокументам.УстановитьПараметр("ДатаКон", ДатаКон);
		ЗапросПоДокументам.УстановитьПараметр("Организация", Организация);
		ЗапросПоДокументам.УстановитьПараметр("ЛистКассовойКниги", ВыборкаПоДокументамКассоваяКнига.Ссылка);
		РезультатЗапросаПоДокументам = ЗапросПоДокументам.Выполнить();
		
		//++ НЕ УТ
		ЗапросПроводок = Новый Запрос;
		ЗапросПроводок.Текст = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	КассовыеОрдера.Документ КАК Документ,
		|	ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(КассовыеОрдера.Документ) = ТИП(Документ.ПриходныйКассовыйОрдер) ТОГДА
		|		ВЫБОР КОГДА Хозрасчетный.СчетКт В ИЕРАРХИИ(
		|			ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками),
		|			ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПокупателямиИЗаказчиками))
		|		ТОГДА
		|			Хозрасчетный.СчетКт.Родитель.Код
		|		ИНАЧЕ
		|			Хозрасчетный.СчетКт.Код
		|		КОНЕЦ
		|	КОГДА ТИПЗНАЧЕНИЯ(КассовыеОрдера.Документ) = ТИП(Документ.РасходныйКассовыйОрдер) ТОГДА
		|		ВЫБОР КОГДА Хозрасчетный.СчетДт В ИЕРАРХИИ(
		|			ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПоставщикамиИПодрядчиками),
		|			ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСПокупателямиИЗаказчиками))
		|		ТОГДА
		|			Хозрасчетный.СчетДт.Родитель.Код
		|		ИНАЧЕ
		|			Хозрасчетный.СчетДт.Код
		|		КОНЕЦ
		|	КОНЕЦ КАК Счет
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
		|		Документ.ЛистКассовойКниги.КассовыеОрдера КАК КассовыеОрдера
		|	ПО
		|		КассовыеОрдера.Документ = Хозрасчетный.Регистратор
		|	
		|ГДЕ
		|	КассовыеОрдера.Ссылка = &ЛистКассовойКниги
		|	И НЕ Хозрасчетный.СчетКт.Забалансовый
		|УПОРЯДОЧИТЬ ПО
		|	Счет
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОтражениеДокументовВРеглУчете.Регистратор КАК Ссылка,
		|	ОтражениеДокументовВРеглУчете.Комментарий КАК Комментарий
		|ИЗ
		|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
		|ГДЕ
		|	ОтражениеДокументовВРеглУчете.Регистратор В (&МассивНеОтраженныхДокументов)
		|";
		
		ЗапросПроводок.УстановитьПараметр("ЛистКассовойКниги", ВыборкаПоДокументамКассоваяКнига.Ссылка);
		
		МассивНеОтраженныхДокументов = Неопределено;
		Если ТипЗнч(ПараметрыПечати) = Тип("Структура") Тогда
			ПараметрыПечати.Свойство("ДокументыБезПроводок", МассивНеОтраженныхДокументов);
		КонецЕсли;
		ЗапросПроводок.УстановитьПараметр("МассивНеОтраженныхДокументов", МассивНеОтраженныхДокументов);
		
		Результат = ЗапросПроводок.ВыполнитьПакет();
		ВыборкаСчетовУчета = Результат[0].Выбрать();
		ВыборкаОшибок = Результат[1].Выбрать();
		//-- НЕ УТ
		
		ТаблицаДокументы = РезультатЗапросаПоДокументам.Выгрузить();
		ТаблицаДокументы.Очистить();
		ТаблицаДокументы.Колонки.Добавить("СтрокаСчет");
		ТаблицаДокументы.Колонки.Добавить("Валютный");
		ТаблицаДокументы.Колонки.Добавить("ВидДокумента");
		
		ТипЧисло = ОбщегоНазначенияУТ.ПолучитьОписаниеТиповЧисла(15,2);
		
		РабочаяТаблица = Новый ТаблицаЗначений;
		РабочаяТаблица.Колонки.Добавить("Валюта",Новый ОписаниеТипов("СправочникСсылка.Валюты"));
		РабочаяТаблица.Колонки.Добавить("ВалютаНаименованиеПолное");
		РабочаяТаблица.Колонки.Добавить("ВалОстаток",ТипЧисло);
		РабочаяТаблица.Колонки.Добавить("Остаток",   ТипЧисло);
		РабочаяТаблица.Колонки.Добавить("ВалПриход", ТипЧисло);
		РабочаяТаблица.Колонки.Добавить("Приход",    ТипЧисло);
		РабочаяТаблица.Колонки.Добавить("ВалРасход", ТипЧисло);
		РабочаяТаблица.Колонки.Добавить("Расход",    ТипЧисло);
		РабочаяТаблица.Индексы.Добавить("Валюта");
		
		ВыборкаПоВалюте = РезультатЗапросаПоИтогамНаНачало.Выбрать();
		
		Пока ВыборкаПоВалюте.Следующий() Цикл
			Строка = РабочаяТаблица.Добавить();
			Если ВыборкаПоВалюте.Валюта = ВалютаРегламентированногоУчета Тогда 
				Строка.Валюта = Справочники.Валюты.ПустаяСсылка();
			Иначе
				Строка.Валюта = ВыборкаПоВалюте.Валюта;
				Строка.ВалютаНаименованиеПолное = ВыборкаПоВалюте.ВалютаНаименованиеПолное
			КонецЕсли;
			Строка.Остаток    = ВыборкаПоВалюте.СуммаНачальныйОстатокДт;
			Строка.ВалОстаток = ВыборкаПоВалюте.ВалютнаяСуммаНачальныйОстатокДт;
		КонецЦикла;
		
		РабочаяТаблица.Свернуть("Валюта, ВалютаНаименованиеПолное", "ВалОстаток, Остаток, ВалПриход, Приход, ВалРасход, Расход");
		РабочаяТаблица.Сортировать("Валюта");
		
		ТаблицаОстатков = Новый ТаблицаЗначений;
		ТаблицаОстатков.Колонки.Добавить("Валюта",Новый ОписаниеТипов("СправочникСсылка.Валюты"));
		ТаблицаОстатков.Колонки.Добавить("ВалютаНаименованиеПолное");
		ТаблицаОстатков.Колонки.Добавить("ВалОстаток",ТипЧисло);
		ТаблицаОстатков.Колонки.Добавить("Остаток",   ТипЧисло);
		ТаблицаОстатков.Индексы.Добавить("Валюта");
		
		Выборка = РезультатЗапросаПоИтогам.Выбрать();
		ВыборкаИнвентаризации = РезультатЗапросаПоИнвентаризации.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Строка = ТаблицаОстатков.Добавить();
			Если Выборка.Валюта = ВалютаРегламентированногоУчета Тогда 
				Строка.Валюта = Справочники.Валюты.ПустаяСсылка();
			Иначе
				Строка.Валюта = Выборка.Валюта;
				Строка.ВалютаНаименованиеПолное = Выборка.ВалютаНаименованиеПолное;
			КонецЕсли;
			
			Строка.Остаток    = Выборка.СуммаОстаток;
			Строка.ВалОстаток = Выборка.ВалютнаяСуммаОстаток;
			
			Если ВыборкаИнвентаризации.НайтиСледующий(Выборка.Валюта) Тогда
				Строка.Остаток = Строка.Остаток - ВыборкаИнвентаризации.СуммаИнвентаризацииРегл;
				Строка.ВалОстаток = Строка.ВалОстаток - ВыборкаИнвентаризации.СуммаИнвентаризации;
				ВыборкаИнвентаризации.Сбросить();
			КонецЕсли;
		КонецЦикла;
		
		ТаблицаОстатков.Свернуть("Валюта, ВалютаНаименованиеПолное", "ВалОстаток, Остаток");
		ТаблицаОстатков.Сортировать("Валюта");
		
		ВыборкаДокументовПоДням = РезультатЗапросаПоДокументам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам,"День");
		
		ВыборкаДокументовПоДням.Следующий();
		ПоДокументам = Истина;
		
		ВывестиПодвал = Ложь;
		
		СведенияОбОрганизации = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(Организация, КонецДня(ДатаНач));
		
		ТипПКО = Тип("ДокументСсылка.ПриходныйКассовыйОрдер");
		ТипРКО = Тип("ДокументСсылка.РасходныйКассовыйОрдер");
		
		Пока ПоДокументам Цикл
			
			ДатаЛиста = ДатаНач;
			
			КоличествоПриходныхДокументов = 0;
			КоличествоРасходныхДокументов = 0;
			
			СчетКурсовыхРазниц = "91";
			ДатаДействияПриказа373П = Дата('20120101');
			
			Остаток = РабочаяТаблица.Итог("Остаток");
			Если ДатаЛиста >= ДатаНач Тогда
				
				Если ДатаЛиста < ДатаДействияПриказа373П Тогда
				
					ЗаголовокЛиста = НСтр("ru='КАССА за'") + " " + Формат(ДатаЛиста, "ДЛФ=D");
					ОбластьВкладнойЛистОтчет.Параметры.ЗаголовокЛиста = ЗаголовокЛиста;
					ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, 
						Макет, ОбластьВкладнойЛистОтчет, ВыборкаПоДокументамКассоваяКнига.Ссылка);
					
					ТабличныйДокумент.Вывести(ОбластьВкладнойЛистОтчет);
					
					ОбластьОтчетКассираОтчет.Параметры.ЗаголовокЛиста = ЗаголовокЛиста;
					ТабличныйДокумент.Присоединить(ОбластьОтчетКассираОтчет);
					
					// Номер первого листа документа "Кассовая книга"
					ОбластьШапкаОтчет.Параметры.ТекстНомерЛиста = "Лист " + ВыборкаДокументовПоДням.НомерЛиста;
					
				Иначе
					
					ЗаголовокЛиста = НСтр("ru='КАССА за'") + " " + Формат(ДатаЛиста, "ДЛФ=DD");
					ТекстНомерЛиста = НСтр("ru='Лист'") + " " + ВыборкаДокументовПоДням.НомерЛиста;
					
					ОбластьВкладнойЛист373ПОтчет.Параметры.ЗаголовокЛиста = ЗаголовокЛиста;
					ОбластьВкладнойЛист373ПОтчет.Параметры.ТекстНомерЛиста = ТекстНомерЛиста;
					ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, 
						Макет, ОбластьВкладнойЛист373ПОтчет, ВыборкаПоДокументамКассоваяКнига.Ссылка);
						
					ТабличныйДокумент.Вывести(ОбластьВкладнойЛист373ПОтчет);
					
					ОбластьОтчетКассира373ПОтчет.Параметры.ЗаголовокЛиста = ЗаголовокЛиста;
					ОбластьОтчетКассира373ПОтчет.Параметры.ТекстНомерЛиста = ТекстНомерЛиста;
					ТабличныйДокумент.Присоединить(ОбластьОтчетКассира373ПОтчет);
					
				КонецЕсли;
				
				ТабличныйДокумент.Вывести(ОбластьШапкаОтчет);
				ТабличныйДокумент.Присоединить(ОбластьШапкаОтчет);
				
				ОбластьОстатокНаНДОтчет.Параметры.ОстатокНачало = Остаток;
				ТабличныйДокумент.Вывести(ОбластьОстатокНаНДОтчет);
				ТабличныйДокумент.Присоединить(ОбластьОстатокНаНДОтчет);
				
			КонецЕсли;
			
	//		Высота каждой строки = 13 пунктов
	//		На стандартную страницу помещается 59 строк
	
	//		Высоты секций в строках:
	//		Шапка = 5 стр
	//		ВкладнойЛист = 3 стр
	//		ОтчетКассира = 3 стр
	//		ОстатокНаНачало = 1 стр
	//		ВТомЧисле = 1 стр
	//		Остаток = 1 стр
	//		ВалОстаток = 2 стр
	//		КурсовыеРазницы = 2 стр
	//		КурсовыеРазницыПоВалюте = 1 стр
	//		Строка = 2 стр
	//		СтрокаШирокая = 4 стр
	//		СтрокаВал = 2 стр
	//		СтрокаВалШирокая = 4 стр
	//		Перенос = 1 стр
	//		Оборот = 1 стр
	//		ОборотРуб = 1 стр
	//		ОборотВал = 2 стр
	//		КурсоваяРазница = 1 стр
	//		КурсоваяРазницаПоВалюте = 1 стр
	//		КонечныйОстаток = 1 стр
	//		Подвал = 11 стр
	//		ЛистовЗаМесяц = 1 стр
	//		ЛистовЗаГод = 1 стр
	
	
			Если (ДатаЛиста >= ДатаНач) и (Остаток > 0) Тогда
				ТабличныйДокумент.Вывести(ОбластьВТомЧислеОтчет);
				ТабличныйДокумент.Присоединить(ОбластьВТомЧислеОтчет);
			КонецЕсли;
			
			Для Каждого Строка ИЗ РабочаяТаблица Цикл
		
				Валюта     = Строка.Валюта;
				РубОстаток = Строка.Остаток;
				ВалОстаток = Строка.ВалОстаток;
				ВалютаНаименованиеПолное = Строка.ВалютаНаименованиеПолное;
			
				Если (Строка.Валюта = ВалютаРегламентированногоУчета) или
					(Строка.Валюта = Справочники.Валюты.ПустаяСсылка())Тогда
			
					Если ДатаЛиста >= ДатаНач Тогда
				
						ОбластьОстатокОтчет.Параметры.ВалютаРеглУчета = НСтр("ru='национальная валюта ('")
							+ ВалютаРеглУчетаНаименованиеПолное + "):";
						ОбластьОстатокОтчет.Параметры.РеглОстаток     = РубОстаток;
				
						ТабличныйДокумент.Вывести(ОбластьОстатокОтчет);
						ТабличныйДокумент.Присоединить(ОбластьОстатокОтчет);
						
					КонецЕсли;
				Иначе
					Если (РубОстаток <> 0) Или (ВалОстаток <> 0) Тогда
				
						Если ДатаЛиста >= ДатаНач Тогда
					
							ОбластьВалОстатокОтчет.Параметры.ВалютаВалУчета = НСтр("ru='иностранная валюта ('")
								+ ВалютаНаименованиеПолное + "):";
							ОбластьВалОстатокОтчет.Параметры.ВалОстатокРегл = РубОстаток;
							ОбластьВалОстатокОтчет.Параметры.ВалОстатокВал  = Формат(ВалОстаток,"ЧЦ=15; ЧДЦ=2; ЧРД==")+" "
								+ Строка(Валюта.Наименование);
					
							ТабличныйДокумент.Вывести(ОбластьВалОстатокОтчет);
							ТабличныйДокумент.Присоединить(ОбластьВалОстатокОтчет);
					
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			ТаблицаДокументы.Очистить();
			Валюты = Новый СписокЗначений;
			
			Если ВыборкаДокументовПоДням.День = ДатаЛиста Тогда
				
				ВыборкаНомеровСтрок = ВыборкаДокументовПоДням.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "НомерСтроки");
				
				Пока ВыборкаНомеровСтрок.Следующий() Цикл
				
					ВыборкаДокументов = ВыборкаНомеровСтрок.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Документ");
					
					Пока ВыборкаДокументов.Следующий() Цикл
						
						НоваяСтрока = ТаблицаДокументы.Добавить();
						НоваяСтрока.ВидДокумента = ?(ТипЗнч(ВыборкаДокументов.Документ) = ТипПКО, "ПриходныйОрдер", "РасходныйОрдер");
						НоваяСтрока.Документ     = ВыборкаДокументов.Документ;
						НоваяСтрока.ДатаДок      = ВыборкаДокументов.ДатаДок;
						НоваяСтрока.День         = ВыборкаДокументов.День;
						НоваяСтрока.НомерДок     = ВыборкаДокументов.НомерДок;
						НоваяСтрока.Валюта       = ВыборкаДокументов.Валюта;
						НоваяСтрока.Приход       = ВыборкаДокументов.Приход;
						НоваяСтрока.Расход       = ВыборкаДокументов.Расход;
						НоваяСтрока.Основание    = ВыборкаДокументов.Основание;
						НоваяСтрока.ТекстДок     = ВыборкаДокументов.ТекстДок;
						НоваяСтрока.НомерЛиста   = ВыборкаДокументов.НомерЛиста;
						
						НоваяСтрока.Валютный = ВыборкаДокументов.Валюта <> ВалютаРегламентированногоУчета;
						Если НоваяСтрока.Валютный И Валюты.НайтиПоЗначению(ВыборкаДокументов.Валюта) = Неопределено Тогда
							Валюты.Добавить(ВыборкаДокументов.Валюта);
						КонецЕсли;
						
						ВыборкаСчетов = ВыборкаДокументов.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Счет");
						СписокСчетов  = Новый ТаблицаЗначений;
						СписокСчетов.Колонки.Добавить("Счет");
						Пока ВыборкаСчетов.Следующий() Цикл
							
							СтрокаТаблицыСчетов      = СписокСчетов.Добавить();
							СтрокаТаблицыСчетов.Счет = ВыборкаСчетов.Счет;
							
						КонецЦикла;
						
						СписокСчетов.Свернуть("Счет");
						
						СтрокаСчет = "";
						Для Каждого СтрокаТаблицыСчетов Из СписокСчетов Цикл
							СтрокаСчет = СтрокаСчет + СтрокаТаблицыСчетов.Счет + Символы.ПС;
						КонецЦикла;
						
						//++ НЕ УТ
						СтрокаСчет = "";
						СтруктураПоиска = Новый Структура("Документ", ВыборкаДокументов.Документ);
						Пока ВыборкаСчетовУчета.НайтиСледующий(СтруктураПоиска) Цикл
							СтрокаСчет = СтрокаСчет + ВыборкаСчетовУчета.Счет + Символы.ПС;
						КонецЦикла;
						ВыборкаСчетовУчета.Сбросить();
						//-- НЕ УТ
						
						НоваяСтрока.СтрокаСчет = СтрокаСчет;
						
					КонецЦикла;
				КонецЦикла;
				
			КонецЕсли;
			
			СумПриход    = 0;
			СумРасход    = 0;
			ПерваяСтрока = 1;
			
			
			// Для простоты настройки печатной формы примем следующие соглашения:
			//	-	высота строк в таблице печатной формы задана жестко,
			//		тогда известно, сколько строк помещается на странице;
			// СтрокНаСтранице = 58;
			//	-	высота шапки и подвала задана жестко и кратна высоте строк таблицы,
			//		тогда можно указать, сколько строк занимают шапка и подвал
			//		в пересчете на строки таблицы;
			//	-	Количество строк шапки определено при выводе валюты в шапку;
			//	-	Количество строк подвала определим позже по количеству валют;
			//	-	для нормальной работы алгоритма необходимо, чтобы шапка и подвал
			//		могли поместиться на одной странице + хотя бы одна строка таблицы:
			//		СтрокНаСтранице >= СтрокШапки + СтрокПодвала + 1
			//	-	если подвал не помещается на странице, он переносится на другую
			//		страницу с последней строкой; исключение составляет случай,
			//		когда в таблице всего одна строка.
			
			ПредыдущийНомерЛиста = ВыборкаДокументовПоДням.НомерЛиста;
			
			Для Каждого Документ Из ТаблицаДокументы Цикл
				
				Если Документ.ВидДокумента = "ПриходныйОрдер" или Документ.ВидДокумента = "РасходныйОрдер" Тогда
					ЕстьВалюта = Документ.Валютный;
				Иначе
					ЕстьВалюта = Ложь;
				КонецЕсли;
				
				Если ЕстьВалюта Тогда
					Валюта          = Документ.Валюта;
					СтруктураКурсов = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Валюта,Документ.ДатаДок);
					
					Курс      = СтруктураКурсов.Курс;
					Кратность = СтруктураКурсов.Кратность;
					Кратность = ?(Кратность = 0, 1, Кратность);
				Иначе
					Валюта    = Справочники.Валюты.ПустаяСсылка();
					Курс      = 1;
					Кратность = 1;
				КонецЕсли;
					
				Если Документ.ВидДокумента = "РасходныйОрдер" Тогда
					Клиент = НСтр("ru='Выдано'") + " " + СокрЛП(Документ.ТекстДок) + ?(ВыводитьОснования = 1, " " 
						+ СокрЛП(Документ.Основание), "");
					КоличествоРасходныхДокументов = КоличествоРасходныхДокументов + 1;
					ВалРасход = Документ.Расход;
					Расход    = Окр(?(Документ.Расход=null,0,Документ.Расход)*Курс/Кратность,2,1);
					ВалПриход = 0;
					Приход    = 0;
				Иначе
					Клиент = НСтр("ru='Принято от'") + " " + СокрЛП(Документ.ТекстДок) + ?(ВыводитьОснования = 1, " " 
						+ СокрЛП(Документ.Основание), "");
					КоличествоПриходныхДокументов = КоличествоПриходныхДокументов + 1;
					ВалПриход = Документ.Приход;
					Приход    = Окр(?(Документ.Приход=null,0,Документ.Приход)*Курс/Кратность,2,1);
					ВалРасход = 0;
					Расход    = 0;
				КонецЕсли;
				
				НомерДокПечатнойФормы = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(
					Документ.НомерДок,
					Ложь,
					Истина);
				КоррСчет = Документ.СтрокаСчет;
				
				// Начинаем новую страницу
				Если ПредыдущийНомерЛиста <> Документ.НомерЛиста Тогда
					
					ПриходЗаДень = РабочаяТаблица.Итог("Приход");
					РасходЗаДень = РабочаяТаблица.Итог("Расход");
					
					Если ДатаЛиста >= ДатаНач Тогда
						
						ОбластьПереносОтчет.Параметры.ПриходЗаДень = ПриходЗаДень;
						ОбластьПереносОтчет.Параметры.РасходЗаДень = РасходЗаДень;
						ТабличныйДокумент.Вывести(ОбластьПереносОтчет);
						ТабличныйДокумент.Присоединить(ОбластьПереносОтчет);
						
						ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
						ОбластьШапкаОтчет.Параметры.ТекстНомерЛиста = НСтр("ru='Лист'") + " " + Документ.НомерЛиста;
						ТабличныйДокумент.Вывести(ОбластьШапкаОтчет);
						ТабличныйДокумент.Присоединить(ОбластьШапкаОтчет);
						
					КонецЕсли;
					
					ПредыдущийНомерЛиста = Документ.НомерЛиста;
					
				КонецЕсли;
				
				Если ДатаЛиста >= ДатаНач Тогда
					Если ЕстьВалюта Тогда
						Если ВыводитьОснования Тогда
							
							ОбластьСтрокаВалШирокаяОтчет.Параметры.НомерДокПечатнойФормы = НомерДокПечатнойФормы;
							ОбластьСтрокаВалШирокаяОтчет.Параметры.Контрагент = Клиент;
							ОбластьСтрокаВалШирокаяОтчет.Параметры.КоррСчет   = КоррСчет;
							ОбластьСтрокаВалШирокаяОтчет.Параметры.Приход     = Приход;
							ОбластьСтрокаВалШирокаяОтчет.Параметры.Расход     = Расход;
							
							Если Число(ВалПриход) > 0 Тогда 
								ОбластьСтрокаВалШирокаяОтчет.Параметры.ВалПриход  = Формат(ВалПриход,"ЧЦ=15; ЧДЦ=2; ЧРД==")+" "
									+ Строка(Валюта.Наименование);
							Иначе
								ОбластьСтрокаВалШирокаяОтчет.Параметры.ВалПриход = "";
							КонецЕсли;
							
							Если Число(ВалРасход) > 0 Тогда
								ОбластьСтрокаВалШирокаяОтчет.Параметры.ВалРасход  = Формат(ВалРасход,"ЧЦ=15; ЧДЦ=2; ЧРД==")+" "
									+ Строка(Валюта.Наименование);
							Иначе
								ОбластьСтрокаВалШирокаяОтчет.Параметры.ВалРасход = "";
							КонецЕсли;
							
							ОбластьСтрокаВалШирокаяОтчет.Параметры.Документ   = Документ.Документ;
							
							ТабличныйДокумент.Вывести(ОбластьСтрокаВалШирокаяОтчет);
							ТабличныйДокумент.Присоединить(ОбластьСтрокаВалШирокаяОтчет);
							
						Иначе
							
							ОбластьСтрокаВалОтчет.Параметры.НомерДокПечатнойФормы = НомерДокПечатнойФормы;
							ОбластьСтрокаВалОтчет.Параметры.Контрагент = Клиент;
							ОбластьСтрокаВалОтчет.Параметры.КоррСчет   = КоррСчет;
							ОбластьСтрокаВалОтчет.Параметры.Приход     = Приход;
							ОбластьСтрокаВалОтчет.Параметры.Расход     = Расход;
							
							Если Число(ВалПриход) > 0 Тогда 
								ОбластьСтрокаВалОтчет.Параметры.ВалПриход  = Формат(ВалПриход,"ЧЦ=15; ЧДЦ=2; ЧРД==")+" "
									+ Строка(Валюта.Наименование);
							Иначе
								ОбластьСтрокаВалОтчет.Параметры.ВалПриход = "";
							КонецЕсли;
							
							Если Число(ВалРасход) > 0 Тогда
								ОбластьСтрокаВалОтчет.Параметры.ВалРасход  = Формат(ВалРасход,"ЧЦ=15; ЧДЦ=2; ЧРД==")+" "
									+ Строка(Валюта.Наименование);
							Иначе
								ОбластьСтрокаВалОтчет.Параметры.ВалРасход = "";
							КонецЕсли;
							
							ОбластьСтрокаВалОтчет.Параметры.Документ   = Документ.Документ;
							
							ТабличныйДокумент.Вывести(ОбластьСтрокаВалОтчет);
							ТабличныйДокумент.Присоединить(ОбластьСтрокаВалОтчет);
							
						КонецЕсли;
						
					Иначе
						Если ВыводитьОснования Тогда
							
							ОбластьСтрокаШирокаяОтчет.Параметры.НомерДокПечатнойФормы = НомерДокПечатнойФормы;
							ОбластьСтрокаШирокаяОтчет.Параметры.Контрагент = Клиент;
							ОбластьСтрокаШирокаяОтчет.Параметры.КоррСчет   = КоррСчет;
							ОбластьСтрокаШирокаяОтчет.Параметры.Приход     = Приход;
							ОбластьСтрокаШирокаяОтчет.Параметры.Расход     = Расход;
							ОбластьСтрокаШирокаяОтчет.Параметры.Документ   = Документ.Документ;
							
							ТабличныйДокумент.Вывести(ОбластьСтрокаШирокаяОтчет);
							ТабличныйДокумент.Присоединить(ОбластьСтрокаШирокаяОтчет);
							
						Иначе
							
							ОбластьСтрокаОтчет.Параметры.НомерДокПечатнойФормы = НомерДокПечатнойФормы;
							ОбластьСтрокаОтчет.Параметры.Контрагент = Клиент;
							ОбластьСтрокаОтчет.Параметры.КоррСчет   = КоррСчет;
							ОбластьСтрокаОтчет.Параметры.Приход     = Приход;
							ОбластьСтрокаОтчет.Параметры.Расход     = Расход;
							ОбластьСтрокаОтчет.Параметры.Документ   = Документ.Документ;
							
							ТабличныйДокумент.Вывести(ОбластьСтрокаОтчет);
							ТабличныйДокумент.Присоединить(ОбластьСтрокаОтчет);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
				ДобавитьОбороты(РабочаяТаблица, Валюта, ВалПриход, Приход, ВалРасход, Расход);
				
			КонецЦикла;
			
			ВыборкаПоКурсовымРазницам = РезультатЗапросаПоКурсовымРазницам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам,"Валюта");
			Пока ВыборкаПоКурсовымРазницам.Следующий() Цикл
				
				Если (ВыборкаПоКурсовымРазницам.Валюта = Справочники.Валюты.ПустаяСсылка())
					ИЛИ ПустаяСтрока(ВыборкаПоКурсовымРазницам.Валюта) Тогда
					Продолжить;
				КонецЕсли;
				
				Если ПерваяСтрока = 1 Тогда
					ТабличныйДокумент.Вывести(ОбластьКурсовыеРазницыОтчет);
					ТабличныйДокумент.Присоединить(ОбластьКурсовыеРазницыОтчет);
					ПерваяСтрока = 0;
				КонецЕсли;
				ОбластьКурсовыеРазницыПоВалютеОтчет.Параметры.НадписьКРВалюта = НСтр("ru='по'") + " " + ВыборкаПоКурсовымРазницам.Валюта;
				ОбластьКурсовыеРазницыПоВалютеОтчет.Параметры.КоррСчет = "";
				Если ВыборкаПоКурсовымРазницам.СуммаКурсовойРазницы > 0 Тогда
					Приход = ВыборкаПоКурсовымРазницам.СуммаКурсовойРазницы;
					Расход = 0;
				Иначе
					Приход = 0;
					Расход = -ВыборкаПоКурсовымРазницам.СуммаКурсовойРазницы;
				КонецЕсли;
				
				ОбластьКурсовыеРазницыПоВалютеОтчет.Параметры.Приход = Приход;
				ОбластьКурсовыеРазницыПоВалютеОтчет.Параметры.Расход = Расход;
				
				
				ТабличныйДокумент.Вывести(ОбластьКурсовыеРазницыПоВалютеОтчет);
				ТабличныйДокумент.Присоединить(ОбластьКурсовыеРазницыПоВалютеОтчет);
				
			КонецЦикла;
			
			РабочаяТаблица.Свернуть("Валюта, ВалютаНаименованиеПолное", "ВалОстаток, Остаток, ВалПриход, Приход, ВалРасход, Расход");
			
			ПриходЗаДень = РабочаяТаблица.Итог("Приход");
			РасходЗаДень = РабочаяТаблица.Итог("Расход");
			Если ДатаЛиста >= ДатаНач Тогда
				
				ОбластьОборотОтчет.Параметры.ПриходЗаДень = ПриходЗаДень;
				ОбластьОборотОтчет.Параметры.РасходЗаДень = РасходЗаДень;
				
				ТабличныйДокумент.Вывести(ОбластьОборотОтчет);
				ТабличныйДокумент.Присоединить(ОбластьОборотОтчет);
				
				Для Каждого Строка ИЗ РабочаяТаблица Цикл
					Валюта = Строка.Валюта;
					ВалютаНаименованиеПолное = Строка.ВалютаНаименованиеПолное;
					ВалПриходЗаДень = Строка.ВалПриход;
					РубПриходЗаДень = Строка.Приход;
					ВалРасходЗаДень = Строка.ВалРасход;
					РубРасходЗаДень = Строка.Расход;
					Если (Строка.Валюта = ВалютаРегламентированногоУчета) или
						(Строка.Валюта = Справочники.Валюты.ПустаяСсылка()) Тогда
						
						ОбластьОборотРубОтчет.Параметры.РеглПриходЗаДень = РубПриходЗаДень;
						ОбластьОборотРубОтчет.Параметры.РеглРасходЗаДень = РубРасходЗаДень;
						ОбластьОборотРубОтчет.Параметры.ВалютаРеглУчета  = 
							НСтр("ru='национальная валюта ('") + ВалютаРеглУчетаНаименованиеПолное + "):";
						
						ТабличныйДокумент.Вывести(ОбластьОборотРубОтчет);
						ТабличныйДокумент.Присоединить(ОбластьОборотРубОтчет);
					Иначе
						Если (ВалПриходЗаДень <> 0) Или (РубПриходЗаДень <> 0) Или 
							(ВалРасходЗаДень <> 0) Или (РубРасходЗаДень <> 0) Тогда
							
							ОбластьОборотВалОтчет.Параметры.ВалютаВалУчета      = НСтр("ru='иностранная валюта ('") + 
								ВалютаНаименованиеПолное + "):";
							ОбластьОборотВалОтчет.Параметры.ВалПриходЗаДеньРегл = РубПриходЗаДень;
							ОбластьОборотВалОтчет.Параметры.ВалРасходЗаДеньРегл = РубРасходЗаДень;
							
							Если Число(ВалПриход) > 0 Тогда 
								ОбластьСтрокаВалОтчет.Параметры.ВалПриход  = Формат(ВалПриход,"ЧЦ=15; ЧДЦ=2; ЧРД==")+" "
									+ Строка(Валюта.Наименование);
							Иначе
								ОбластьСтрокаВалОтчет.Параметры.ВалПриход = "";
							КонецЕсли;
							
							Если Число(ВалПриходЗаДень) > 0 Тогда
								ОбластьОборотВалОтчет.Параметры.ВалПриходЗаДеньВал  = Формат(ВалПриходЗаДень,"ЧЦ=15; ЧДЦ=2; ЧРД==")+" "+Валюта;
							Иначе
								ОбластьОборотВалОтчет.Параметры.ВалПриходЗаДеньВал = "";
							КонецЕсли;
							
							Если Число(ВалРасходЗаДень) > 0 Тогда
								ОбластьОборотВалОтчет.Параметры.ВалРасходЗаДеньВал  = Формат(ВалРасходЗаДень,"ЧЦ=15; ЧДЦ=2; ЧРД==")+" "+Валюта;
							Иначе
								ОбластьОборотВалОтчет.Параметры.ВалРасходЗаДеньВал = "";
							КонецЕсли;
							
							ТабличныйДокумент.Вывести(ОбластьОборотВалОтчет);
							ТабличныйДокумент.Присоединить(ОбластьОборотВалОтчет);
							
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
			ПерваяСтрока = Истина;
			Остаток = ТаблицаОстатков.Итог("Остаток");
			
			Если ДатаЛиста >= ДатаНач Тогда
				
				ОбластьКонечныйОстатокОтчет.Параметры.ОстатокКонец = Остаток;
				
				ТабличныйДокумент.Вывести(ОбластьКонечныйОстатокОтчет);
				ТабличныйДокумент.Присоединить(ОбластьКонечныйОстатокОтчет);
				
				Если Остаток > 0 Тогда
					ТабличныйДокумент.Вывести(ОбластьВТомЧислеОтчет);
					ТабличныйДокумент.Присоединить(ОбластьВТомЧислеОтчет);
				КонецЕсли;
					
				Для Каждого Строка ИЗ ТаблицаОстатков Цикл
					
					Валюта     = Строка.Валюта;
					РубОстаток = Строка.Остаток;
					ВалОстаток = Строка.ВалОстаток;
					ВалютаНаименованиеПолное = Строка.ВалютаНаименованиеПолное;
					
					Если (Строка.Валюта = ВалютаРегламентированногоУчета) или
						(Строка.Валюта = Справочники.Валюты.ПустаяСсылка())Тогда
						
						Если ДатаЛиста >= ДатаНач Тогда
							
							ОбластьОстатокОтчет.Параметры.ВалютаРеглУчета = НСтр("ru='национальная валюта ('")+
								ВалютаРеглУчетаНаименованиеПолное + "):";
							ОбластьОстатокОтчет.Параметры.РеглОстаток     = РубОстаток;
							
							ТабличныйДокумент.Вывести(ОбластьОстатокОтчет);
							ТабличныйДокумент.Присоединить(ОбластьОстатокОтчет);
							
						КонецЕсли;
						
					Иначе
						
						Если (РубОстаток <> 0) Или (ВалОстаток <> 0) Тогда
							
							Если ДатаЛиста >= ДатаНач Тогда
								
								ОбластьВалОстатокОтчет.Параметры.ВалютаВалУчета = НСтр("ru='иностранная валюта ('")+
									ВалютаНаименованиеПолное + "):";
								ОбластьВалОстатокОтчет.Параметры.ВалОстатокРегл = РубОстаток;
								ОбластьВалОстатокОтчет.Параметры.ВалОстатокВал  = Формат(ВалОстаток,"ЧЦ=15; ЧДЦ=2; ЧРД==")+" "+
									Строка(Валюта.Наименование);
								
								ТабличныйДокумент.Вывести(ОбластьВалОстатокОтчет);
								ТабличныйДокумент.Присоединить(ОбластьВалОстатокОтчет);
								
							КонецЕсли;
							
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЦикла;

				ОбластьПодвалОтчет.Параметры.НадписьКолПриходныхРасходных=?(КоличествоПриходныхДокументов>0, 
				ЧислоПрописью(КоличествоПриходныхДокументов,"НД=Ложь",",,,,,,,,0")," - ")+ НСтр("ru='приходных и'") + " "+
				?(КоличествоРасходныхДокументов>0,ЧислоПрописью(КоличествоРасходныхДокументов,"НД=Ложь",",,,,,,,,0")," - ")
				+ НСтр("ru='расходных получил.'");
				
				ОбластьПодвалОтчет.Параметры.ГлБухгалтер = ВыборкаПоДокументамКассоваяКнига.ГлавныйБухгалтер;
				ОбластьПодвалОтчет.Параметры.Кассир = 
					ФизическиеЛицаУТ.ФамилияИнициалыФизЛица(
						ВыборкаПоДокументамКассоваяКнига.Ответственный,
						ВыборкаПоДокументамКассоваяКнига.Дата);
				
				ТабличныйДокумент.Вывести(ОбластьПодвалОтчет);
				ТабличныйДокумент.Присоединить(ОбластьПодвалОтчет);
			КонецЕсли;
			
			ПоДокументам = ВыборкаДокументовПоДням.Следующий();
			
			ВывестиПодвал = Истина;
			
		КонецЦикла;
		
		Если ВывестиПодвал Тогда
			
			Если ВыборкаПоДокументамКассоваяКнига.ТипЛиста = Перечисления.ТипыЛистовКассовойКниги.ПоследнийВМесяце Тогда
				
				КоличествоЛистов = ПолучитьКоличествоЛистовКассовойКнигиЗаПериодПоОрганизации(
					ВыборкаПоДокументамКассоваяКнига.Организация, ВыборкаПоДокументамКассоваяКнига.КассоваяКнига,
					НачалоМесяца(ВыборкаПоДокументамКассоваяКнига.Дата),
					КонецМесяца(ВыборкаПоДокументамКассоваяКнига.Дата));
				
				ОбластьЛистовЗаМесяцОтчет.Параметры.НадписьЛистовЗаМесяц = НСтр("ru='Количество листов кассовой книги за месяц:'") + " " +
					КоличествоЛистов;
				ТабличныйДокумент.Вывести(ОбластьЛистовЗаМесяцОтчет);
				ТабличныйДокумент.Присоединить(ОбластьЛистовЗаМесяцОтчет);
				
			КонецЕсли;
			
			Если ВыборкаПоДокументамКассоваяКнига.ТипЛиста = Перечисления.ТипыЛистовКассовойКниги.ПоследнийВГоду Тогда
				
				КоличествоЛистов = ПолучитьКоличествоЛистовКассовойКнигиЗаПериодПоОрганизации(
					ВыборкаПоДокументамКассоваяКнига.Организация, ВыборкаПоДокументамКассоваяКнига.КассоваяКнига,
					НачалоГода(ВыборкаПоДокументамКассоваяКнига.Дата),
					КонецГода(ВыборкаПоДокументамКассоваяКнига.Дата));
				
				ОбластьЛистовЗаГодОтчет.Параметры.НадписьЛистовЗаГод = НСтр("ru='Количество листов кассовой книги за год:'") + " " + 
					КоличествоЛистов;
				ТабличныйДокумент.Вывести(ОбластьЛистовЗаГодОтчет);
				ТабличныйДокумент.Присоединить(ОбластьЛистовЗаГодОтчет);
				
			КонецЕсли;
			
		КонецЕсли;
		
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(
			ТабличныйДокумент,
			НомерСтрокиНачало,
			ОбъектыПечати,
			ВыборкаПоДокументамКассоваяКнига.Ссылка);
		
	КонецЦикла; // Цикл по выделенным документам
	
	ТабличныйДокумент.ТолькоПросмотр = Истина;
	Возврат ТабличныйДокумент;
	
КонецФункции

// 4D:ERP для Беларуси, ВладимирР, 06.02.2015 14:03:12 
// Задача №9059, Кассовая книга в валюте
// {
Функция СформироватьИнициалыФамилиюФИзЛица(Объект)
	ФИО = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СокрЛП(Объект.Наименование), " ");
	
	КоличествоПодстрок = ФИО.Количество();
	Фамилия = ?(КоличествоПодстрок > 0, ФИО[0], "");
	Имя = ?(КоличествоПодстрок > 1, ФИО[1], "");
	Отчество = ?(КоличествоПодстрок > 2, ФИО[2], "");
	
	Возврат ?(Не ПустаяСтрока(Имя), Лев(Имя,1) + ".", "")
	+ ?(Не ПустаяСтрока(Отчество), Лев(Отчество,1)+".", "")
	+ ?(Не ПустаяСтрока(Фамилия), " " + Фамилия, "");
КонецФункции

Функция СформироватьПечатнуюФормуЛистаВалютнойКассовойКниги_Локализация(МассивОбъектов, ОбъектыПечати, ПараметрыПечати = Неопределено)
	УстановитьПривилегированныйРежим(Истина); // TODO
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ЛистКассовойКниги.ПФ_MXL_ЛистВалютнойКассовойКниги_Локализация");
	
	МассивСчетов = Новый Массив;
	МассивСчетов.Добавить(ПланыСчетов.Хозрасчетный.КассаОрганизацииВал);
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	ПриходРасход = Новый ТаблицаЗначений;
	ТипЧисло = ОбщегоНазначенияБПКлиентСервер.ПолучитьОписаниеТиповЧисла(15, 2);
	ПриходРасход.Колонки.Добавить("Валюта", Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	ПриходРасход.Колонки.Добавить("ПриходЗаДень", ТипЧисло);
	ПриходРасход.Колонки.Добавить("ПриходПоСтранице", ТипЧисло);
	ПриходРасход.Колонки.Добавить("РасходЗаДень", ТипЧисло);
	ПриходРасход.Колонки.Добавить("РасходПоСтранице", ТипЧисло);
	ПриходРасход.Колонки.Добавить("КурсЗаДень", ТипЧисло);
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЛистВалютнойКассовойКниги_Локализация";
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ОтветственныеЛицаСервер.СформироватьВременнуюТаблицуОтветственныхЛицДокументов(МассивОбъектов, МенеджерВременныхТаблиц);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЛистКассовойКнигиКассовыеОрдера.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТКассовыеЛисты
	|ИЗ
	|	Документ.ЛистКассовойКниги.КассовыеОрдера КАК ЛистКассовойКнигиКассовыеОрдера
	|ГДЕ
	|	ЛистКассовойКнигиКассовыеОрдера.Ссылка В(&МассивДокументов)
	|	И ЛистКассовойКнигиКассовыеОрдера.Документ.Валюта <> &ВалютаРегламентированногоУчета
	|	И ЛистКассовойКнигиКассовыеОрдера.Ссылка.Проведен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументЛистКассовойКниги.Ссылка КАК Ссылка,
	|	ДокументЛистКассовойКниги.ТипЛиста КАК ТипЛиста,
	|	ДокументЛистКассовойКниги.КоличествоДокументовНаЛисте КАК КоличествоДокументовНаЛисте,
	|	ДокументЛистКассовойКниги.Дата КАК Дата,
	|	ДокументЛистКассовойКниги.Организация КАК Организация,
	|	ДокументЛистКассовойКниги.КассоваяКнига КАК КассоваяКнига,
	|	ДокументЛистКассовойКниги.Ответственный.ФизическоеЛицо КАК Ответственный,
	|	ТаблицаОтветственныеЛица.ГлавныйБухгалтерНаименование КАК ГлавныйБухгалтер
	|ПОМЕСТИТЬ ТЗКассовыеДокументы
	|ИЗ
	|	Документ.ЛистКассовойКниги КАК ДокументЛистКассовойКниги
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОтветственныеЛица КАК ТаблицаОтветственныеЛица
	|		ПО ДокументЛистКассовойКниги.Ссылка = ТаблицаОтветственныеЛица.Ссылка
	|ГДЕ
	|	ДокументЛистКассовойКниги.Ссылка В
	|			(ВЫБРАТЬ
	|				ВТКассовыеЛисты.Ссылка
	|			ИЗ
	|				ВТКассовыеЛисты КАК ВТКассовыеЛисты)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТЗКассовыеДокументы.Ссылка КАК Ссылка,
	|	ТЗКассовыеДокументы.ТипЛиста КАК ТипЛиста,
	|	ТЗКассовыеДокументы.КоличествоДокументовНаЛисте КАК КоличествоДокументовНаЛисте,
	|	ТЗКассовыеДокументы.Дата КАК Дата,
	|	ТЗКассовыеДокументы.Организация КАК Организация,
	|	ТЗКассовыеДокументы.КассоваяКнига КАК КассоваяКнига,
	|	ТЗКассовыеДокументы.Ответственный КАК Ответственный,
	|	ТЗКассовыеДокументы.ГлавныйБухгалтер КАК ГлавныйБухгалтер
	|ИЗ
	|	ТЗКассовыеДокументы КАК ТЗКассовыеДокументы";
	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", ВалютаРегламентированногоУчета);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Новый ТабличныйДокумент;
	КонецЕсли;
	
	ЛистовЗаГод = 0;
	ЛистовЗаМесяц = 0;
	ЭтоПерваяСтраница = Истина;
	
	ВыборкаПоДокументамКассоваяКнига = Результат.Выбрать();
	Пока ВыборкаПоДокументамКассоваяКнига.Следующий() Цикл
		Если ЭтоПерваяСтраница Тогда
			ЭтоПерваяСтраница = Ложь;
		Иначе
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ОстаткиВалютыНаНачало = Новый Массив;
		СписокВалют = Новый Массив;
		
		НомерЛиста = ЛистовЗаГод + 1;
		ЛистовЗаГод = ЛистовЗаГод + 1;
		ЛистовЗаМесяц = ЛистовЗаМесяц + 1;
		
		КоличествоОбычныхСтрок = 0;
		КоличествоПриходныхДокументов = 0;
		КоличествоРасходныхДокументов = 0;
		КоличествоСтрокШапки = 5;
		КоличествоСтрокПодвала = 17;
		
		ПереносСтраницы = Ложь;
		Документ = Неопределено;
		
		ОбластьШапкаЗаголовок = Макет.ПолучитьОбласть("Шапка|Заголовок");
		ОбластьОстатокНаНачалоДняЗаголовок = Макет.ПолучитьОбласть("ОстатокНаНачалоДня|Заголовок");
		ОбластьСтрокаЗаголовок = Макет.ПолучитьОбласть("Строка|Заголовок");
		ОбластьИтогоПоСтраницеЗаголовок = Макет.ПолучитьОбласть("ИтогоПоСтранице|Заголовок");
		ОбластьИтогоЗаДеньЗаголовок = Макет.ПолучитьОбласть("ИтогоЗаДень|Заголовок");
		ОбластьОстатокНаКонецДняЗаголовок = Макет.ПолучитьОбласть("ОстатокНаКонецДня|Заголовок");
		ОбластьКурсЗаголовок = Макет.ПолучитьОбласть("Курс|Заголовок");
		ОбластьЭквивалентЗаголовок = Макет.ПолучитьОбласть("Эквивалент|Заголовок");
		ОбластьШапкаПриход = Макет.ПолучитьОбласть("Шапка|Приход");
		ОбластьОстатокНаНачалоДняПриход = Макет.ПолучитьОбласть("ОстатокНаНачалоДня|Приход");
		ОбластьСтрокаПриход = Макет.ПолучитьОбласть("Строка|Приход");
		ОбластьИтогоПоСтраницеПриход = Макет.ПолучитьОбласть("ИтогоПоСтранице|Приход");
		ОбластьИтогоЗаДеньПриход = Макет.ПолучитьОбласть("ИтогоЗаДень|Приход");
		ОбластьОстатокНаКонецДняПриход = Макет.ПолучитьОбласть("ОстатокНаКонецДня|Приход");
		ОбластьКурсПриход = Макет.ПолучитьОбласть("Курс|Приход");
		ОбластьЭквивалентПриход = Макет.ПолучитьОбласть("Эквивалент|Приход");
		ОбластьШапкаРасход = Макет.ПолучитьОбласть("Шапка|Расход");
		ОбластьОстатокНаНачалоДняРасход = Макет.ПолучитьОбласть("ОстатокНаНачалоДня|Расход");
		ОбластьСтрокаРасход = Макет.ПолучитьОбласть("Строка|Расход");
		ОбластьИтогоПоСтраницеРасход = Макет.ПолучитьОбласть("ИтогоПоСтранице|Расход");
		ОбластьИтогоЗаДеньРасход = Макет.ПолучитьОбласть("ИтогоЗаДень|Расход");
		ОбластьОстатокНаКонецДняРасход = Макет.ПолучитьОбласть("ОстатокНаКонецДня|Расход");
		ОбластьКурсРасход = Макет.ПолучитьОбласть("Курс|Расход");
		ОбластьЭквивалентРасход = Макет.ПолучитьОбласть("Эквивалент|Расход");
		ОбластьШапкаПриходРасход = Макет.ПолучитьОбласть("ШапкаПриход");
		ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
		ОбластьВкладнойЛист = Макет.ПолучитьОбласть("ВкладнойЛист");
		ОбластьЛистовЗаМесяц = Макет.ПолучитьОбласть("ЛистовЗаМесяц");
		ОбластьЛистовЗаГод = Макет.ПолучитьОбласть("ЛистовЗаГод");
		ОбластьНомерЛиста = Макет.ПолучитьОбласть("НомерЛиста");
		
		ДатаНач = НачалоДня(ВыборкаПоДокументамКассоваяКнига.Дата);
		ДатаКон = КонецДня(ВыборкаПоДокументамКассоваяКнига.Дата);
		ГраницаКон = Новый Граница(ДатаКон, ВидГраницы.Включая);
		
		НомерСтолбецВалюты = 4;
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;
		
		ЗапросПоИтогам = Новый Запрос;
		ЗапросПоИтогам.Текст =
		"ВЫБРАТЬ
		|	Кассы.Ссылка КАК Касса
		|ПОМЕСТИТЬ Кассы
		|ИЗ
		|	Справочник.Кассы КАК Кассы
		|ГДЕ
		|	Кассы.Владелец = &Организация
		|	И Кассы.КассоваяКнига = &КассоваяКнига
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДенежныеСредстваНаличныеОстаткиИОбороты.Период КАК Период,
		|	ДенежныеСредстваНаличныеОстаткиИОбороты.Касса.ВалютаДенежныхСредств КАК Валюта,
		|	ДенежныеСредстваНаличныеОстаткиИОбороты.СуммаРеглНачальныйОстаток КАК СуммаНачальныйОстатокДт,
		|	ДенежныеСредстваНаличныеОстаткиИОбороты.СуммаНачальныйОстаток КАК ВалютнаяСуммаНачальныйОстатокДт,
		|	ДенежныеСредстваНаличныеОстаткиИОбороты.СуммаРеглКонечныйОстаток КАК СуммаКонечныйОстатокДт,
		|	ДенежныеСредстваНаличныеОстаткиИОбороты.СуммаКонечныйОстаток КАК ВалютнаяСуммаКонечныйОстатокДт,
		|	ДенежныеСредстваНаличныеОстаткиИОбороты.СуммаПриход КАК СуммаОборотДт,
		|	ДенежныеСредстваНаличныеОстаткиИОбороты.СуммаРасход КАК СуммаОборотКт,
		|	ДенежныеСредстваНаличныеОстаткиИОбороты.Регистратор КАК Регистратор,
		|	ДенежныеСредстваНаличныеОстаткиИОбороты.Регистратор.Дата КАК Дата,
		|	ДенежныеСредстваНаличныеОстаткиИОбороты.Регистратор.Номер КАК Номер,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ДенежныеСредстваНаличныеОстаткиИОбороты.Регистратор) = ТИП(Документ.ПриходныйКассовыйОрдер)
		|			ТОГДА ДенежныеСредстваНаличныеОстаткиИОбороты.Регистратор.НомерИсходящегоДокумента
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК НомерИсходящегоДокумента,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ДенежныеСредстваНаличныеОстаткиИОбороты.Регистратор) = ТИП(Документ.ПриходныйКассовыйОрдер)
		|			ТОГДА ДенежныеСредстваНаличныеОстаткиИОбороты.Регистратор.СерияИсходящегоДокумента
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК СерияИсходящегоДокумента,
		|	ПРЕДСТАВЛЕНИЕ(ДенежныеСредстваНаличныеОстаткиИОбороты.Регистратор.Основание) КАК Основание,
		|	ПРЕДСТАВЛЕНИЕ(ДенежныеСредстваНаличныеОстаткиИОбороты.Регистратор) КАК ТекстДок,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ДенежныеСредстваНаличныеОстаткиИОбороты.Регистратор) = ТИП(Документ.РасходныйКассовыйОрдер)
		|			ТОГДА Проводки.СчетДт
		|		ИНАЧЕ Проводки.СчетКт
		|	КОНЕЦ КАК Счет,
		|	ПРЕДСТАВЛЕНИЕ(ВЫБОР
		|			КОГДА ТИПЗНАЧЕНИЯ(ДенежныеСредстваНаличныеОстаткиИОбороты.Регистратор) = ТИП(Документ.РасходныйКассовыйОрдер)
		|				ТОГДА Проводки.СчетДт
		|			ИНАЧЕ Проводки.СчетКт
		|		КОНЕЦ) КАК СчетПр
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваНаличные.ОстаткиИОбороты(
		|			&ДатаНач,
		|			&ГраницаКон,
		|			Регистратор,
		|			Движения,
		|			Организация = &Организация
		|				И Касса В
		|					(ВЫБРАТЬ
		|						Кассы.Касса
		|					ИЗ
		|						Кассы)) КАК ДенежныеСредстваНаличныеОстаткиИОбороты
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			Хозрасчетный.СчетДт КАК СчетДт,
		|			Хозрасчетный.СчетКт КАК СчетКт,
		|			Хозрасчетный.Регистратор КАК Регистратор
		|		ИЗ
		|			РегистрБухгалтерии.Хозрасчетный КАК Хозрасчетный
		|		ГДЕ
		|			Хозрасчетный.Организация = &Организация
		|			И Хозрасчетный.Активность = ИСТИНА
		|			И Хозрасчетный.Период МЕЖДУ &ДатаНач И &ДатаКон
		|			И (Хозрасчетный.СчетДт В ИЕРАРХИИ (&Счет)
		|						И (НЕ Хозрасчетный.СчетДт.Валютный
		|							ИЛИ Хозрасчетный.ВалютнаяСуммаДт <> 0)
		|					ИЛИ Хозрасчетный.СчетКт В ИЕРАРХИИ (&Счет)
		|						И (НЕ Хозрасчетный.СчетКт.Валютный
		|							ИЛИ Хозрасчетный.ВалютнаяСуммаКт <> 0))) КАК Проводки
		|		ПО ДенежныеСредстваНаличныеОстаткиИОбороты.Регистратор = Проводки.Регистратор
		|ГДЕ
		|	(ТИПЗНАЧЕНИЯ(ДенежныеСредстваНаличныеОстаткиИОбороты.Регистратор) = ТИП(Документ.ПриходныйКассовыйОрдер)
		|			ИЛИ ТИПЗНАЧЕНИЯ(ДенежныеСредстваНаличныеОстаткиИОбороты.Регистратор) = ТИП(Документ.РасходныйКассовыйОрдер))
		|
		|УПОРЯДОЧИТЬ ПО
		|	Период
		|ИТОГИ
		|	СУММА(СуммаНачальныйОстатокДт),
		|	СУММА(ВалютнаяСуммаНачальныйОстатокДт),
		|	СУММА(СуммаКонечныйОстатокДт),
		|	СУММА(ВалютнаяСуммаКонечныйОстатокДт),
		|	СУММА(СуммаОборотДт),
		|	СУММА(СуммаОборотКт)
		|ПО
		|	Валюта";
		ЗапросПоИтогам.УстановитьПараметр("ДатаНач", ДатаНач);
		ЗапросПоИтогам.УстановитьПараметр("ДатаКон", ДатаКон);
		ЗапросПоИтогам.УстановитьПараметр("ГраницаКон", ГраницаКон);
		ЗапросПоИтогам.УстановитьПараметр("Организация", ВыборкаПоДокументамКассоваяКнига.Организация);
		ЗапросПоИтогам.УстановитьПараметр("КассоваяКнига", ВыборкаПоДокументамКассоваяКнига.КассоваяКнига);
		ЗапросПоИтогам.УстановитьПараметр("Счет", МассивСчетов);
		
		Результат = ЗапросПоИтогам.Выполнить();
		Если Результат.Пустой() Тогда
			Продолжить;
		КонецЕсли;
		
		ПриходРасход.Очистить();
		
		ВыборкаПоВалюте = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Валюта");
		Пока ВыборкаПоВалюте.Следующий() Цикл
			Если ЗначениеЗаполнено(ВыборкаПоВалюте.Валюта) Тогда
				Строка = ПриходРасход.Добавить();
				Строка.Валюта = ВыборкаПоВалюте.Валюта;
				
				СписокВалют.Добавить(ВыборкаПоВалюте.Валюта);
				ОстаткиВалютыНаНачало.Добавить(ВыборкаПоВалюте.ВалютнаяСуммаНачальныйОстатокДт);
			КонецЕсли;
		КонецЦикла;
		
		// {
		// Шапка
		Шапка = Новый ТабличныйДокумент;
		ШапкаДоп1 = Новый ТабличныйДокумент;
		ШапкаДоп2 = Новый ТабличныйДокумент;
		ШапкаДоп3 = Новый ТабличныйДокумент;
		Шапка.Вывести(ОбластьШапкаЗаголовок);
		
		Для Номер = 0 По СписокВалют.Количество() - 1 Цикл
			ШапкаДоп1.Присоединить(ОбластьШапкаПриходРасход);
		КонецЦикла;
		
		ШапкаДоп1.Область(1, 1, 1, СписокВалют.Количество()).Объединить();
		ШапкаДоп1.Область(1, 1, 1, СписокВалют.Количество()).Текст = "Приход";
		ШапкаДоп2.Присоединить(ШапкаДоп1);
		ШапкаДоп1.Область(1, 1, 1, СписокВалют.Количество()).Текст = "Расход";
		ШапкаДоп2.Присоединить(ШапкаДоп1);
		
		ШапкаДоп1.Область(1, 1, 1, СписокВалют.Количество()).Текст = "Виды валют";
		ШапкаДоп3.Присоединить(ШапкаДоп1);
		ШапкаДоп3.Присоединить(ШапкаДоп1);
		ШапкаДоп2.Вывести(ШапкаДоп3);
		
		Для каждого СтрокаВалюты Из СписокВалют Цикл
			ОбластьШапкаПриход.Параметры.НаимнованиеВалюты = СокрЛП(СтрокаВалюты);
			ОбластьШапкаПриход.Параметры.НомерСтолбца = НомерСтолбецВалюты;
			
			Если НомерСтолбецВалюты = 4 Тогда
				ШапкаДоп2.Вывести(ОбластьШапкаПриход);
			Иначе
				ШапкаДоп2.Присоединить(ОбластьШапкаПриход);
			КонецЕсли;
			
			НомерСтолбецВалюты = НомерСтолбецВалюты + 1;
		КонецЦикла;
		
		Для каждого СтрокаВалюты Из СписокВалют Цикл
			ОбластьШапкаРасход.Параметры.НаимнованиеВалюты = СокрЛП(СтрокаВалюты);
			ОбластьШапкаРасход.Параметры.НомерСтолбца = НомерСтолбецВалюты;
			
			НомерСтолбецВалюты = НомерСтолбецВалюты + 1;
			ШапкаДоп2.Присоединить(ОбластьШапкаРасход);
		КонецЦикла;
		Шапка.Присоединить(ШапкаДоп2);
		// Шапка
		// }
		
		ОбластьВкладнойЛист.Параметры.ЗаголовокЛиста = "КАССА за " + Формат(ДатаНач, "ДФ=dd.MM.yyyy");
		ОбластьНомерЛиста.Параметры.ТекстНомерЛиста = "Лист " + НомерЛиста;
		ТабДокумент.Вывести(ОбластьВкладнойЛист);
		ТабДокумент.Вывести(ОбластьНомерЛиста);
		ТабДокумент.Вывести(Шапка);
		ТабДокумент.Вывести(ОбластьОстатокНаНачалоДняЗаголовок);
		
		Для каждого ЗначениеОстатка Из ОстаткиВалютыНаНачало Цикл
			ОбластьОстатокНаНачалоДняПриход.Параметры.ОстатокНаНачалоДня = Формат(ЗначениеОстатка, "ЧЦ=15; ЧДЦ=2; ЧРД=,");
			ТабДокумент.Присоединить(ОбластьОстатокНаНачалоДняПриход);
		КонецЦикла;
		
		Для каждого ЗначениеОстатка Из ОстаткиВалютыНаНачало Цикл
			ТабДокумент.Присоединить(ОбластьОстатокНаНачалоДняРасход);
		КонецЦикла;
		
		КоличествоСтрокШапки = КоличествоСтрокШапки + 4 + 1;
		//4 - количество строк Вкладной лист
		//1 - строка на начало дня
		
		Для каждого Строка Из ПриходРасход Цикл
			СтруктураЗаДень = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Строка.Валюта, ДатаКон);
			Строка.КурсЗаДень = СтруктураЗаДень.Курс;
		КонецЦикла;
		
		Документ = Результат.Выбрать(ОбходРезультатаЗапроса.Прямой);
		Пока Документ.Следующий() Цикл
			Если Не Документ.ТипЗаписи() = ТипЗаписиЗапроса.ДетальнаяЗапись Тогда
				Продолжить;
			КонецЕсли;
			
			СтруктураКурсов = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Документ.Валюта, Документ.Период);
			Курс = СтруктураКурсов.Курс;
			Кратность = ?(СтруктураКурсов.Кратность = 0, 1, СтруктураКурсов.Кратность);
			
			Если ТипЗнч(Документ.Регистратор) = Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
				Клиент = "Выдано " + СокрЛП(Документ.ТекстДок) + " " + СокрЛП(Документ.Основание);
				КоличествоРасходныхДокументов = КоличествоРасходныхДокументов + 1;
				Расход = Документ.СуммаОборотКт;
				Приход = 0;
			КонецЕсли;
			
			Если ТипЗнч(Документ.Регистратор) = Тип("ДокументСсылка.ПриходныйКассовыйОрдер") Тогда
				Клиент = "Принято от " + СокрЛП(Документ.ТекстДок) + " " + СокрЛП(Документ.Основание);
				КоличествоПриходныхДокументов = КоличествоПриходныхДокументов + 1;
				Приход = Документ.СуммаОборотДт;
				Расход = 0;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Документ.НомерИсходящегоДокумента) Или ЗначениеЗаполнено(Документ.СерияИсходящегоДокумента) Тогда
				ОбластьСтрокаЗаголовок.Параметры.НомерДокПечатнойФормы = Документ.СерияИсходящегоДокумента + Документ.НомерИсходящегоДокумента;
			Иначе
				ОбластьСтрокаЗаголовок.Параметры.НомерДокПечатнойФормы = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Документ.Номер, Истина, Истина);
			КонецЕсли;
			
			ОбластьСтрокаЗаголовок.Параметры.КоррСчет = Документ.СчетПр;
			ОбластьСтрокаЗаголовок.Параметры.Клиент = Клиент;
			ЗаполнитьЗначенияСвойств(ОбластьСтрокаЗаголовок.Параметры, Документ);
			ТабДокумент.Вывести(ОбластьСтрокаЗаголовок);
			
			ВысотаСтроки = 1;
			
			Для каждого ЭлементВалюты Из СписокВалют Цикл
				Если Документ.Валюта = ЭлементВалюты Тогда
					ОбластьСтрокаПриход.Параметры.ВалПриход = Формат(Приход, "ЧЦ=15; ЧДЦ=2; ЧРД=,");
					ЗаполнитьЗначенияСвойств(ОбластьСтрокаПриход.Параметры, Документ);
					ТабДокумент.Присоединить(ОбластьСтрокаПриход);
				Иначе
					ОбластьСтрокаПриход.Параметры.ВалПриход = 0;
					ЗаполнитьЗначенияСвойств(ОбластьСтрокаПриход.Параметры, Документ);
					ТабДокумент.Присоединить(ОбластьСтрокаПриход);
				КонецЕсли;
			КонецЦикла;
			
			Для каждого ЭлементВалюты Из СписокВалют Цикл
				Если Документ.Валюта = ЭлементВалюты Тогда
					ОбластьСтрокаРасход.Параметры.ВалРасход = Формат(Расход, "ЧЦ=15; ЧДЦ=2; ЧРД=,");
					ЗаполнитьЗначенияСвойств(ОбластьСтрокаРасход.Параметры, Документ);
					ТабДокумент.Присоединить(ОбластьСтрокаРасход);
				Иначе
					ОбластьСтрокаРасход.Параметры.ВалРасход = 0;
					ЗаполнитьЗначенияСвойств(ОбластьСтрокаРасход.Параметры, Документ);
					ТабДокумент.Присоединить(ОбластьСтрокаРасход);
				КонецЕсли;
			КонецЦикла;
			
			КоличествоОбычныхСтрок = КоличествоОбычныхСтрок + 1;
			
			Для каждого СтрокаПрихода Из ПриходРасход Цикл
				Если Документ.Валюта = СтрокаПрихода.Валюта Тогда
					СтрокаПрихода.ПриходЗаДень = СтрокаПрихода.ПриходЗаДень + Приход;
					СтрокаПрихода.ПриходПоСтранице = СтрокаПрихода.ПриходПоСтранице + Приход;
					СтрокаПрихода.РасходЗаДень = СтрокаПрихода.РасходЗаДень + Расход;
					СтрокаПрихода.РасходПоСтранице = СтрокаПрихода.РасходПоСтранице + Расход;
				КонецЕсли;
			КонецЦикла;
			
			Если Не ПереносСтраницы И (КоличествоСтрокШапки + КоличествоСтрокПодвала + КоличествоОбычныхСтрок + 2 >= 58) Тогда
				ПереносСтраницы = Истина;
			ИначеЕсли ПереносСтраницы И (КоличествоСтрокШапки + КоличествоОбычныхСтрок + 2 >= 57) Тогда
				КоличествоОбычныхСтрок = 0;
				ТабДокумент.Вывести(ОбластьИтогоПоСтраницеЗаголовок);
				
				Для каждого Строка Из ПриходРасход Цикл
					ОбластьИтогоПоСтраницеПриход.Параметры.ИтогоПоСтранице = Формат(Строка.ПриходПоСтранице, "ЧЦ=15; ЧДЦ=2; ЧРД=,");
					ТабДокумент.Присоединить(ОбластьИтогоПоСтраницеПриход);
				КонецЦикла;
				
				Для каждого Строка Из ПриходРасход Цикл
					ОбластьИтогоПоСтраницеРасход.Параметры.ИтогоПоСтранице = Формат(Строка.РасходПоСтранице, "ЧЦ=15; ЧДЦ=2; ЧРД=,");
					ТабДокумент.Присоединить(ОбластьИтогоПоСтраницеРасход);
				КонецЦикла;
				
				ТабДокумент.ВывестиГорИзонтальныйРазделительСтраниц();
				
				Для каждого Строка Из ПриходРасход Цикл
					Строка.ПриходПоСтранице = 0;
					Строка.РасходПоСтранице = 0;
				КонецЦикла;
				
				ПереносСтраницы = Ложь;
				НомерЛиста = ЛистовЗаГод + 1;
				ЛистовЗаГод = ЛистовЗаГод + 1;
				ЛистовЗаМесяц = ЛистовЗаМесяц + 1;
				ОбластьНомерЛиста.Параметры.ТекстНомерЛиста = "Лист " + НомерЛиста;
				ТабДокумент.Вывести(ОбластьНомерЛиста);
				ТабДокумент.Вывести(Шапка);
			КонецЕсли;
		КонецЦикла;
		
		ОстаткиНаКонецДня = Новый Массив;
		Для каждого Стр Из ПриходРасход Цикл
			ОстаткиНаКонецДня.Добавить(0);
		КонецЦикла;
		
		ТабДокумент.Вывести(ОбластьИтогоПоСтраницеЗаголовок);
		
		Для каждого Строка Из ПриходРасход Цикл
			ОбластьИтогоПоСтраницеПриход.Параметры.ИтогоПоСтранице = Формат(Строка.ПриходПоСтранице, "ЧЦ=15; ЧДЦ=2; ЧРД=,");
			ТабДокумент.Присоединить(ОбластьИтогоПоСтраницеПриход);
		КонецЦикла;
		
		Для каждого Строка Из ПриходРасход Цикл
			ОбластьИтогоПоСтраницеРасход.Параметры.ИтогоПоСтранице = Формат(Строка.РасходПоСтранице, "ЧЦ=15; ЧДЦ=2; ЧРД=,");
			ТабДокумент.Присоединить(ОбластьИтогоПоСтраницеРасход);
		КонецЦикла;
		
		ТабДокумент.Вывести(ОбластьИтогоЗаДеньЗаголовок);
		
		Для каждого Строка Из ПриходРасход Цикл
			ОбластьИтогоЗаДеньПриход.Параметры.ИтогоЗаДень = Формат(Строка.ПриходЗаДень, "ЧЦ=15; ЧДЦ=2; ЧРД=,");
			ТабДокумент.Присоединить(ОбластьИтогоЗаДеньПриход);
		КонецЦикла;
		
		Для каждого Строка Из ПриходРасход Цикл
			ОбластьИтогоЗаДеньРасход.Параметры.ИтогоЗаДень = Формат(Строка.РасходЗаДень, "ЧЦ=15; ЧДЦ=2; ЧРД=,");
			ТабДокумент.Присоединить(ОбластьИтогоЗаДеньРасход);
		КонецЦикла;
		
		ТабДокумент.Вывести(ОбластьОстатокНаКонецДняЗаголовок);
		Для НомерСтроки = 0 По ПриходРасход.Количество() - 1 Цикл
			ОстаткиНаКонецДня[НомерСтроки] = ОстаткиНаКонецДня[НомерСтроки] + ПриходРасход[НомерСтроки].ПриходЗаДень - ПриходРасход[НомерСтроки].РасходЗаДень + ОстаткиВалютыНаНачало[НомерСтроки];
			ОбластьОстатокНаКонецДняПриход.Параметры.ОстатокНаКонецДня = Формат(ОстаткиНаКонецДня[НомерСтроки], "ЧЦ=15; ЧДЦ=2; ЧРД=,");
			ТабДокумент.Присоединить(ОбластьОстатокНаКонецДняПриход);
			ОстаткиНаКонецДня[НомерСтроки] = ПриходРасход[НомерСтроки].ПриходЗаДень - ПриходРасход[НомерСтроки].РасходЗаДень + ОстаткиВалютыНаНачало[НомерСтроки];
			ОстаткиВалютыНаНачало[НомерСтроки] = ОстаткиНаКонецДня[НомерСтроки];
		КонецЦикла;
		
		Для каждого Стр Из ПриходРасход Цикл
			ТабДокумент.Присоединить(ОбластьОстатокНаКонецДняРасход);
		КонецЦикла;
		
		ТабДокумент.Вывести(ОбластьКурсЗаголовок);
		Для каждого Строка Из ПриходРасход Цикл
			Если Строка.ПриходЗаДень <> 0 Или Строка.РасходЗаДень <> 0 Тогда
				ОбластьКурсПриход.Параметры.КурсВалют = Строка.КурсЗаДень;
			Иначе
				ОбластьКурсПриход.Параметры.КурсВалют = "";
			КонецЕсли;
			
			ТабДокумент.Присоединить(ОбластьКурсПриход);
		КонецЦикла;
		
		Для каждого Стр Из ПриходРасход Цикл
			ТабДокумент.Присоединить(ОбластьКурсРасход);
		КонецЦикла;
		
		ТабДокумент.Вывести(ОбластьЭквивалентЗаголовок);
		
		Для СтрокаНомер = 0 По ПриходРасход.Количество() - 1 Цикл
			КурсВалют = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ПриходРасход[СтрокаНомер].Валюта, Документ.Период).Курс;
			ОбластьЭквивалентПриход.Параметры.ЭквивалентВРБ = Формат(ПриходРасход[СтрокаНомер].ПриходЗаДень * КурсВалют, "ЧЦ=15; ЧДЦ=0; ЧРД=,");
			ТабДокумент.Присоединить(ОбластьЭквивалентПриход);
		КонецЦикла;
		
		Для СтрокаНомер = 0 По ПриходРасход.Количество() - 1 Цикл
			КурсВалют = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ПриходРасход[СтрокаНомер].Валюта, Документ.Период).Курс;
			ОбластьЭквивалентРасход.Параметры.ЭквивалентВРБ = Формат(ПриходРасход[СтрокаНомер].РасходЗаДень * КурсВалют, "ЧЦ=15; ЧДЦ=0; ЧРД=,");
			ТабДокумент.Присоединить(ОбластьЭквивалентРасход);
		КонецЦикла;
		
		// печать подвала
		ОбластьПодвал.Параметры.КоличествоПриходныхРасходных = ?(КоличествоПриходныхДокументов > 0, ЧислоПрописью(КоличествоПриходныхДокументов, "НД=Ложь", ",,,,,,,,0"), " - ") + "приходных и " +
			?(КоличествоРасходныхДокументов > 0, ЧислоПрописью(КоличествоРасходныхДокументов, "НД=Ложь", ",,,,,,,,0"), " - ") + "расходных получил.";
			
		Руководители = ОтветственныеЛицаБП.ОтветственныеЛица(ВыборкаПоДокументамКассоваяКнига.Организация, КонецДня(ВыборкаПоДокументамКассоваяКнига.Дата));
		ОбластьПодвал.Параметры.ГлБухгалтер = СформироватьИнициалыФамилиюФИзЛица(Руководители.ГлавныйБухгалтер);
		ОбластьПодвал.Параметры.Кассир = СформироватьИнициалыФамилиюФИзЛица(Руководители.Кассир);
		ТабДокумент.Вывести(ОбластьПодвал);
	КонецЦикла;
	
	// {
	// Правая сторона
	ТабДокументВыходной = Новый ТабличныйДокумент;
	ТабДокументВыходной.Присоединить(ТабДокумент);
	
	Пока Не (ТабДокументВыходной.НайтиТекст("Копия") = Неопределено) Цикл
		Область1 = ТабДокументВыходной.НайтиТекст("Копия").Имя;
		ИскомаяОбласть = Область1 + ":" + Область1;
		ТабДокументВыходной.Область(ИскомаяОбласть).Текст = "";
	КонецЦикла;
	
	ТабДокументВыходной.ВывестиВертикальныйРазделительСтраниц();
	
	Пока Не (ТабДокумент.НайтиТекст("Вкладной лист кассовой книги") = Неопределено) Цикл
		Область1 = ТабДокумент.НайтиТекст("Вкладной лист кассовой книги").Имя;
		ИскомаяОбласть = Область1 + ":" + Область1;
		ТабДокумент.Область(ИскомаяОбласть).Текст = "Отчет кассира";
	КонецЦикла;
	
	ТабДокументВыходной.Присоединить(ТабДокумент);
	ТабДокументВыходной.ТолькоПросмотр = Истина;
	// Правая сторона
	// }
	
	УстановитьПривилегированныйРежим(Ложь); // TODO
	
	Возврат ТабДокументВыходной;
КонецФункции
// }
// 4D
#КонецОбласти

#Область ОбновлениеИнформационнойБазы

#КонецОбласти

#КонецОбласти

#КонецЕсли
