#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет список команд создания на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании) Экспорт



КонецПроцедуры

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСоздатьНаОсновании) Экспорт

	 
	Если ПравоДоступа("Добавление", Метаданные.Документы.РегламентнаяОперация) Тогда
		КомандаСоздатьНаОсновании = КомандыСоздатьНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Идентификатор = Метаданные.Документы.РегламентнаяОперация.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ВводНаОсновании.ПредставлениеОбъекта(Метаданные.Документы.РегламентнаяОперация);
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьРеглУчет";
		КомандаСоздатьНаОсновании.ПроверкаПроведенияПередСозданиемНаОсновании = Истина;
		
	

		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов) Экспорт

	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуДвиженияДокумента(КомандыОтчетов);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПроводкиРеглУчета

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	ТекстЗапроса - Строка - Текст запроса отражения в регл. учете.
//
Функция ТекстОтраженияВРеглУчете() Экспорт
	
	#Область СписаниеКосвенныхРасходовОСНО
	ТекстСписаниеКосвенныхРасходовОСНО = "
	|ВЫБРАТЬ 
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	КосвенныеРасходы.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	КосвенныеРасходы.СуммаРеглОСНО КАК Сумма,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СписаниеРасходовОСНО) КАК ВидСчетаДт,
	|	КосвенныеРасходы.СтатьяРасходов КАК АналитикаУчетаДт,
	|	КосвенныеРасходы.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	КосвенныеРасходы.Подразделение КАК ПодразделениеДт,
	|	КосвенныеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	КосвенныеРасходы.СтатьяРасходов КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	КосвенныеРасходы.СуммаРеглОСНО - КосвенныеРасходы.ПостояннаяРазницаОСНО - КосвенныеРасходы.ВременнаяРазницаОСНО КАК СуммаНУДт,
	|	КосвенныеРасходы.ПостояннаяРазницаОСНО КАК СуммаПРДт,
	|	КосвенныеРасходы.ВременнаяРазницаОСНО КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|	КосвенныеРасходы.СтатьяРасходов КАК АналитикаУчетаКт,
	|	КосвенныеРасходы.Подразделение КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	КосвенныеРасходы.Подразделение КАК ПодразделениеКт,
	|	КосвенныеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	КосвенныеРасходы.СтатьяРасходов КАК СубконтоКт1,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	КосвенныеРасходы.СуммаРеглОСНО - КосвенныеРасходы.ПостояннаяРазницаОСНО - КосвенныеРасходы.ВременнаяРазницаОСНО КАК СуммаНУКт,
	|	КосвенныеРасходы.ПостояннаяРазницаОСНО КАК СуммаПРКт,
	|	КосвенныеРасходы.ВременнаяРазницаОСНО КАК СуммаВРКт,
	|	""Списание косвенных расходов (ОСНО)"" КАК Содержание
	|
	|ИЗ
	|	Документ.РегламентнаяОперация КАК Операция
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		КосвенныеРасходы КАК КосвенныеРасходы
	|	ПО
	|		Операция.Ссылка = КосвенныеРасходы.Регистратор
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.СписаниеКосвенныхРасходов)
	|	И (КосвенныеРасходы.СуммаРеглОСНО <> 0 
	|		ИЛИ КосвенныеРасходы.ПостояннаяРазницаОСНО <> 0
	|		ИЛИ КосвенныеРасходы.ВременнаяРазницаОСНО <> 0)
	|";
	#КонецОбласти
	
	#Область СписаниеКосвенныхРасходовЕНВД
	ТекстСписаниеКосвенныхРасходовЕНВД = "
	|ВЫБРАТЬ 
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	КосвенныеРасходы.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	КосвенныеРасходы.СуммаРеглЕНВД КАК Сумма,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.СписаниеРасходовЕНВД) КАК ВидСчетаДт,
	|	КосвенныеРасходы.СтатьяРасходов КАК АналитикаУчетаДт,
	|	КосвенныеРасходы.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	КосвенныеРасходы.Подразделение КАК ПодразделениеДт,
	|	КосвенныеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	КосвенныеРасходы.СтатьяРасходов КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	КосвенныеРасходы.СуммаРеглЕНВД - КосвенныеРасходы.ПостояннаяРазницаЕНВД - КосвенныеРасходы.ВременнаяРазницаЕНВД КАК СуммаНУДт,
	|	КосвенныеРасходы.ПостояннаяРазницаЕНВД КАК СуммаПРДт,
	|	КосвенныеРасходы.ВременнаяРазницаЕНВД КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|	КосвенныеРасходы.СтатьяРасходов КАК АналитикаУчетаКт,
	|	КосвенныеРасходы.Подразделение КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	КосвенныеРасходы.Подразделение КАК ПодразделениеКт,
	|	КосвенныеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	КосвенныеРасходы.СтатьяРасходов КАК СубконтоКт1,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	КосвенныеРасходы.СуммаРеглЕНВД - КосвенныеРасходы.ПостояннаяРазницаЕНВД - КосвенныеРасходы.ВременнаяРазницаЕНВД КАК СуммаНУКт,
	|	КосвенныеРасходы.ПостояннаяРазницаЕНВД КАК СуммаПРКт,
	|	КосвенныеРасходы.ВременнаяРазницаЕНВД КАК СуммаВРКт,
	|	""Списание косвенных расходов (ЕНВД)"" КАК Содержание
	|
	|ИЗ
	|	Документ.РегламентнаяОперация КАК Операция
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		КосвенныеРасходы КАК КосвенныеРасходы
	|	ПО
	|		Операция.Ссылка = КосвенныеРасходы.Регистратор
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.СписаниеКосвенныхРасходов)
	|	И (КосвенныеРасходы.СуммаРеглЕНВД <> 0 
	|		ИЛИ КосвенныеРасходы.ПостояннаяРазницаЕНВД <> 0
	|		ИЛИ КосвенныеРасходы.ВременнаяРазницаЕНВД <> 0)
	|";
	#КонецОбласти

	#Область НачислениеИмущественныхНалогов
	ТекстНачислениеИмущественныхНалогов = "
	|ВЫБРАТЬ 
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	РасходыПоНалогу.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	РасходыПоНалогу.СуммаРегл КАК Сумма,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	РасходыПоНалогу.СтатьяРасходов КАК АналитикаУчетаДт,
	|	РасходыПоНалогу.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	РасходыПоНалогу.Подразделение КАК ПодразделениеДт,
	|	РасходыПоНалогу.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	РасходыПоНалогу.СтатьяРасходов КАК СубконтоДт1,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	РасходыПоНалогу.СуммаРегл - РасходыПоНалогу.ПостояннаяРазница - РасходыПоНалогу.ВременнаяРазница КАК СуммаНУДт,
	|	РасходыПоНалогу.ПостояннаяРазница КАК СуммаПРДт,
	|	РасходыПоНалогу.ВременнаяРазница КАК СуммаВРДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	РасходыПоНалогу.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|
	// 4D:ERP для Беларуси, Екатерина, 14.12.2015 18:13:45 
	// , №10763
	// {
	|	ВЫБОР КОГДА Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РасчетНалогаНаИмущество) 
	|					ИЛИ Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РасчетТранспортногоНалога) ТОГДА
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеНалогиИСборы)
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ЗемельныйНалог)
	|	КОНЕЦ КАК СчетКт,
	// }
	// 4D
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВГосБюджет.Налог) КАК СубконтоКт1,
	|	РасходыПоНалогу.РегистрацияВНалоговомОргане КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	РасходыПоНалогу.СуммаРегл - РасходыПоНалогу.ПостояннаяРазница - РасходыПоНалогу.ВременнаяРазница КАК СуммаНУКт,
	|	РасходыПоНалогу.ПостояннаяРазница КАК СуммаПРКт,
	|	РасходыПоНалогу.ВременнаяРазница КАК СуммаВРКт,
	|	""Начисление имущественных налогов"" КАК Содержание
	|
	|ИЗ
	|	Документ.РегламентнаяОперация КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходы КАК РасходыПоНалогу
	|		ПО Операция.Ссылка = РасходыПоНалогу.Регистратор
	|
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РасчетНалогаНаИмущество),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РасчетТранспортногоНалога),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РасчетЗемельногоНалога))
	|";
	#КонецОбласти
	
	#Область НачислениеТорговогоСбора
	ТекстНачислениеТорговогоСбора = "
	|ВЫБРАТЬ 
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	КосвенныеРасходы.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	КосвенныеРасходы.СуммаРегл КАК Сумма,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	КосвенныеРасходы.СтатьяРасходов КАК АналитикаУчетаДт,
	|	КосвенныеРасходы.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	КосвенныеРасходы.Подразделение КАК ПодразделениеДт,
	|	КосвенныеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	КосвенныеРасходы.СтатьяРасходов КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	КосвенныеРасходы.СуммаРегл - КосвенныеРасходы.ПостояннаяРазница - КосвенныеРасходы.ВременнаяРазница КАК СуммаНУДт,
	|	КосвенныеРасходы.ПостояннаяРазница КАК СуммаПРДт,
	|	КосвенныеРасходы.ВременнаяРазница КАК СуммаВРДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	КосвенныеРасходы.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТорговыйСбор) КАК СчетКт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВГосБюджет.Налог) КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	КосвенныеРасходы.СуммаРегл - КосвенныеРасходы.ПостояннаяРазница - КосвенныеРасходы.ВременнаяРазница КАК СуммаНУКт,
	|	КосвенныеРасходы.ПостояннаяРазница КАК СуммаПРКт,
	|	КосвенныеРасходы.ВременнаяРазница КАК СуммаВРКт,
	|	""Начисление торгового сбора"" КАК Содержание
	|
	|ИЗ
	|	Документ.РегламентнаяОперация КАК Операция
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходы КАК КосвенныеРасходы
	|		ПО Операция.Ссылка = КосвенныеРасходы.Регистратор
	|
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РасчетТорговогоСбора)
	|";
	#КонецОбласти
	
#Область РегистрацияПрочихРасходов
	ТекстРегистрацияПрочихРасходов = "
	|ВЫБРАТЬ // Прочие расходы (Дт <25, 26, 44, 20, 23> :: Кт <разные>)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|	
	|	0 КАК Сумма,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Строки.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.ПодразделениеЗатрат КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельностиЗатрат КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.СтатьяРасходов КАК СубконтоДт1,
	|	Строки.АналитикаРасходов КАК СубконтоДт2, 
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА 
	|			ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный) 
	|		ИНАЧЕ 
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) 
	|	КОНЕЦ КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.СуммаПлатежаНУ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	-Строки.СуммаПлатежаНУ КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.КорректировкаСтоимостиАрендованногоИмущества) КАК СчетКт,
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СуммаПлатежаНУ КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	-Строки.СуммаПлатежаНУ КАК СуммаВРКт,
	|	""Прочие расходы"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РегламентнаяОперация КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтРасходыПоАренднымПлатежам КАК Строки
	|	ПО
	|		ИСТИНА
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО
	|		Строки.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|	
	|ГДЕ
	|	Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.ПризнаниеВНУЛизинговыхПлатежей)
	|	И Строки.СуммаПлатежаНУ <> 0
	|
	|";
#КонецОбласти

#Область КорректировкиАмортизацииПоАренднымПлатежамНУ
	ТекстКорректировкиАмортизацииПоАренднымПлатежамНУ = "
	|ВЫБРАТЬ // Прочие расходы (Дт <25, 26, 44, 20, 23> :: Кт <амортизация>)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	"""" КАК ИдентификаторСтроки,
	|	
	|	0 КАК Сумма,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Строки.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.ПодразделениеЗатрат КАК ПодразделениеДт,
	|	Строки.НаправлениеДеятельностиЗатрат КАК НаправлениеДеятельностиДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Строки.СтатьяРасходов КАК СубконтоДт1,
	|	Строки.АналитикаРасходов КАК СубконтоДт2, 
	|	ВЫБОР КОГДА СтатьиСтроительства.Ссылка ЕСТЬ НЕ NULL ТОГДА 
	|			ЗНАЧЕНИЕ(Перечисление.СпособыСтроительства.Подрядный) 
	|		ИНАЧЕ 
	|			ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) 
	|	КОНЕЦ КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	Строки.КорректировкаАмортизацииНУ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	-Строки.КорректировкаАмортизацииНУ КАК СуммаВРДт,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПрочиеАктивыПассивы) КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	Строки.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.КорректировкаСтоимостиАрендованногоИмущества) КАК СчетКт,
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.КорректировкаАмортизацииНУ КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	-Строки.КорректировкаАмортизацииНУ КАК СуммаВРКт,
	|	""Коррректировка расходов по амортизации на величину превышения над лизинговыми платежами"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РегламентнаяОперация КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтКорректировкаАмортизации КАК Строки
	|	ПО
	|		ИСТИНА
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО
	|		Строки.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|	
	|ГДЕ
	|	Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.ПризнаниеВНУЛизинговыхПлатежей)
	|	И Строки.КорректировкаАмортизацииНУ <> 0
	|
	|";
#КонецОбласти

	// 4D:ERP для Беларуси, Екатерина, 30.03.2016 17:22:43 
	// Локализация плана счетов, №8969
	// {
	ТекстНачислениеТорговогоСбора = "";
	
	Возврат 
		ТекстСписаниеКосвенныхРасходовОСНО 
		+ "	ОБЪЕДИНИТЬ ВСЕ " + ТекстНачислениеИмущественныхНалогов;
	// }
	// 4D
		
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц, 
// необходимых для отражения в регламентированном учете
//
// Возвращаемое значение:
//	ТекстЗапроса - Строка - Текст запроса cоздания временных таблиц.
//
Функция ТекстЗапросаВТОтраженияВРеглУчете() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Операция.Ссылка КАК Регистратор,
	|	КосвенныеРасходы.Организация КАК Организация,
	|	КосвенныеРасходы.Подразделение КАК Подразделение,
	|	КосвенныеРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|	КосвенныеРасходы.АналитикаРасходов КАК АналитикаРасходов,
	|	КосвенныеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ВЫБОР 
	|		КОГДА НЕ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.УчетнаяПолитика.ПрименяетсяЕНВД, ЛОЖЬ) 
	|			  ИЛИ СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения)
	|			ТОГДА КосвенныеРасходы.СуммаРегл
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения)
	|			ТОГДА 0
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
	|			ТОГДА КосвенныеРасходы.СуммаРегл - ВЫРАЗИТЬ(КосвенныеРасходы.СуммаРегл * Доли.ДоляЕНВД КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК СуммаРеглОСНО,
	|	ВЫБОР 
	|		КОГДА НЕ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.УчетнаяПолитика.ПрименяетсяЕНВД, ЛОЖЬ) 
	|			  ИЛИ СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения)
	|			ТОГДА 0
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения)
	|			ТОГДА КосвенныеРасходы.СуммаРегл
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
	|			ТОГДА ВЫРАЗИТЬ(КосвенныеРасходы.СуммаРегл * Доли.ДоляЕНВД КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК СуммаРеглЕНВД,
	|	
	|	ВЫБОР 
	|		КОГДА НЕ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.УчетнаяПолитика.ПрименяетсяЕНВД, ЛОЖЬ)
	|			  ИЛИ СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения)
	|			ТОГДА КосвенныеРасходы.ПостояннаяРазница
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения)
	|			ТОГДА 0
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
	|			ТОГДА КосвенныеРасходы.ПостояннаяРазница - ВЫРАЗИТЬ(КосвенныеРасходы.ПостояннаяРазница * Доли.ДоляЕНВД КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК ПостояннаяРазницаОСНО,
	|	ВЫБОР 
	|		КОГДА НЕ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.УчетнаяПолитика.ПрименяетсяЕНВД, ЛОЖЬ) 
	|			  ИЛИ СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения)
	|			ТОГДА 0
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения)
	|			ТОГДА КосвенныеРасходы.ПостояннаяРазница
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
	|			ТОГДА ВЫРАЗИТЬ(КосвенныеРасходы.ПостояннаяРазница * Доли.ДоляЕНВД КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК ПостояннаяРазницаЕНВД,
	|	
	|	ВЫБОР 
	|		КОГДА НЕ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.УчетнаяПолитика.ПрименяетсяЕНВД, ЛОЖЬ)
	|			  ИЛИ СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения)
	|			ТОГДА КосвенныеРасходы.ВременнаяРазница
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения)
	|			ТОГДА 0
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
	|			ТОГДА КосвенныеРасходы.ВременнаяРазница - ВЫРАЗИТЬ(КосвенныеРасходы.ВременнаяРазница * Доли.ДоляЕНВД КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК ВременнаяРазницаОСНО,
	|	ВЫБОР 
	|		КОГДА НЕ ЕСТЬNULL(УчетнаяПолитикаОрганизаций.УчетнаяПолитика.ПрименяетсяЕНВД, ЛОЖЬ) 
	|			  ИЛИ СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсновнаяСистемаНалогообложения)
	|			ТОГДА 0
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.ОсобыйПорядокНалогообложения)
	|			ТОГДА КосвенныеРасходы.ВременнаяРазница
	|		КОГДА СтатьиРасходов.ВидДеятельностиДляНалоговогоУчетаЗатрат = ЗНАЧЕНИЕ(Перечисление.ВидыДеятельностиДляНалоговогоУчетаЗатрат.РаспределяемыеЗатраты)
	|			ТОГДА ВЫРАЗИТЬ(КосвенныеРасходы.ВременнаяРазница * Доли.ДоляЕНВД КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК ВременнаяРазницаЕНВД
	|
	|ПОМЕСТИТЬ КосвенныеРасходы 
	|ИЗ
	|	Документ.РегламентнаяОперация КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ПрочиеРасходы КАК КосвенныеРасходы
	|	ПО
	|		Операция.Ссылка = КосвенныеРасходы.Регистратор
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|	ПО
	|		КосвенныеРасходы.СтатьяРасходов = СтатьиРасходов.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.ПорядокОтраженияРасходов КАК ПорядокОтраженияРасходов
	|	ПО 
	|		КосвенныеРасходы.Организация = ПорядокОтраженияРасходов.Организация
	|		И КосвенныеРасходы.СтатьяРасходов = ПорядокОтраженияРасходов.СтатьяРасходов
	|		И КосвенныеРасходы.Подразделение = ПорядокОтраженияРасходов.Подразделение
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		УчетнаяПолитикаОрганизаций КАК УчетнаяПолитикаОрганизаций
	|	ПО
	|		Операция.Ссылка = УчетнаяПолитикаОрганизаций.Ссылка
	|		И Операция.Организация = УчетнаяПолитикаОрганизаций.Организация
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ДолиСписанияКосвенныхРасходов КАК Доли
	|	ПО
	|		Операция.Ссылка = Доли.Регистратор
	|		
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.СписаниеКосвенныхРасходов)
	|	И НЕ ВЫБОР
	|			КОГДА ЕСТЬNULL(ПорядокОтраженияРасходов.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)) <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|				ТОГДА ПорядокОтраженияРасходов.СчетУчета
	|			ИНАЧЕ СтатьиРасходов.СчетУчета
	|		КОНЕЦ В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеДоходыИРасходы), 
	|						  ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.Продажи))
	|;
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Создает и проводит документы "Регламентная операция" за указанный месяц.
//
//	Параметры:
//		Период - Дата - Начало месяца, в котором необходимо создать документы.
//		МассивОпераций - Массив - Содержит массив типов регламентных операций (значений перечисления "ТипыРегламентныхОпераций")
//		Организация - Массив, Неопределено - Список организаций по которым формируются документы. Если список пустой,
//												то документы формируются по всем организациям.
//		Отказ - Булево - Используется при вызове из формы закрытия месяца. При установке в "Истина" - дальнейшие операции
//							выполняться не будут.
//		ПроверятьНеобходимостьРаспределения - Булево - Признак того, что операцию выполнять только в случае если требуется. В противном случае выполнятся безусловно.
//		ПроводитьДокументы - Булево - Признак того, что созданные документы необходимо провести.
//	
// Возвращаемое значение:
//		РезультатРасчета - Структура - структура, содержащая:
//			* МассивДокументов - Массив - массив ссылок на созданные/измененные документы
//			* ТекстОшибки - Строка - текст описание ошибки; заполнен при Отказ = Истина
//
Функция РассчитатьРегламентныеОперации(Период, МассивОпераций, Организация = Неопределено, Отказ = Ложь, 
	
	ПроверятьНеобходимостьРаспределения = Ложь,	ПроводитьДокументы = Истина) Экспорт
	
	РезультатРасчета = Новый Структура;
	РезультатРасчета.Вставить("МассивДокументов", Новый Массив);
	РезультатРасчета.Вставить("ТекстОшибки", "");
	
	Если ПроверятьНеобходимостьРаспределения Тогда
		Состояние = ЗакрытиеМесяцаУТВызовСервера.СостояниеРасчетДолейСписанияКосвенныхРасходов(Организация, Период);
		Если Состояние = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется Тогда
			Возврат РезультатРасчета;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(Организация) = Тип("Массив") Тогда
		МассивОрганизаций = Организация;
	ИначеЕсли ТипЗнч(Организация) = Тип("СписокЗначений") Тогда
		МассивОрганизаций = Организация.ВыгрузитьЗначения();
	Иначе
		МассивОрганизаций = Новый Массив;
		Если ЗначениеЗаполнено(Организация) Тогда
			МассивОрганизаций.Добавить(Организация);
		КонецЕсли;
	КонецЕсли;
	
	ТаблицаОпераций = Новый ТаблицаЗначений;
	ТаблицаОпераций.Колонки.Добавить("ТипОперации", Новый ОписаниеТипов("ПеречислениеСсылка.ТипыРегламентныхОпераций"));
	ТаблицаОпераций.Колонки.Добавить("Порядок", ОбщегоНазначения.ОписаниеТипаЧисло(1));
	Для Сч = 1 По МассивОпераций.Количество() Цикл
		СтрокаОперации = ТаблицаОпераций.Добавить();
		СтрокаОперации.ТипОперации = МассивОпераций[Сч-1];
		СтрокаОперации.Порядок = Сч;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаОпераций.Порядок      КАК Порядок,
	|	ТаблицаОпераций.ТипОперации  КАК ТипОперации
	|ПОМЕСТИТЬ ВтТипыОпераций
	|ИЗ &ТаблицаОпераций КАК ТаблицаОпераций
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеСправочника.Ссылка КАК Ссылка,
	|	ДанныеСправочника.ОбособленноеПодразделение КАК ОбособленноеПодразделение
	|ПОМЕСТИТЬ ВтОрганизации
	|ИЗ
	|	Справочник.Организации КАК ДанныеСправочника
	|ГДЕ
	|	(ДанныеСправочника.Ссылка В (&МассивОрганизаций)
	|			ИЛИ &ПоВсемОрганизациям)
	|	И (ДанныеСправочника.Ссылка <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|			ИЛИ &УчитыватьУпрОрганизацию)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Организации.Ссылка                    КАК Организация,
	|	Организации.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
	|	ТипыРегламентныхОпераций.ТипОперации  КАК ТипОперации,
	|	ТипыРегламентныхОпераций.Порядок      КАК Порядок
	|ПОМЕСТИТЬ ВтТипыОперацийПоОрганизациям
	|ИЗ
	|	ВтОрганизации КАК Организации,
	|	ВтТипыОпераций КАК ТипыРегламентныхОпераций
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТипыОпераций.Организация,
	|	ТипыОпераций.ОбособленноеПодразделение,
	|	ТипыОпераций.ТипОперации,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(УчетныеПолитики.УчетнаяПолитика.СистемаНалогообложения, &ОСНО) = &ОСНО
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОрганизацияНаОСНО,
	|	ЕСТЬNULL(УчетныеПолитики.ПрименяетсяУСНДоходыМинусРасходы, ЛОЖЬ) КАК ПрименяетсяУСНДоходыМинусРасходы,
	|	МАКСИМУМ(ЕСТЬNULL(РегламентнаяОперацияПроведена.Ссылка, РегламентнаяОперацияЗаписана.Ссылка)) КАК Ссылка
	|ИЗ
	|	ВтТипыОперацийПоОрганизациям КАК ТипыОпераций
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.РегламентнаяОперация КАК РегламентнаяОперацияПроведена
	|	ПО
	|		ТипыОпераций.Организация = РегламентнаяОперацияПроведена.Организация
	|		И ТипыОпераций.ТипОперации = РегламентнаяОперацияПроведена.ТипОперации
	|		И РегламентнаяОперацияПроведена.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|		И РегламентнаяОперацияПроведена.Проведен
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.РегламентнаяОперация КАК РегламентнаяОперацияЗаписана
	|	ПО
	|		ТипыОпераций.Организация = РегламентнаяОперацияЗаписана.Организация
	|		И ТипыОпераций.ТипОперации = РегламентнаяОперацияЗаписана.ТипОперации
	|		И РегламентнаяОперацияЗаписана.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|		И НЕ РегламентнаяОперацияЗаписана.ПометкаУдаления
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(
	|				&ДатаОкончания,
	|				Организация В (&МассивОрганизаций)
	|					ИЛИ &ПоВсемОрганизациям) КАК УчетныеПолитики
	|		ПО ТипыОпераций.Организация = УчетныеПолитики.Организация
	|	
	|СГРУППИРОВАТЬ ПО
	|	ТипыОпераций.Организация,
	|	ТипыОпераций.ОбособленноеПодразделение,
	|	ТипыОпераций.ТипОперации,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(УчетныеПолитики.УчетнаяПолитика.СистемаНалогообложения, &ОСНО) = &ОСНО
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ЕСТЬNULL(УчетныеПолитики.ПрименяетсяУСНДоходыМинусРасходы, ЛОЖЬ),
	|	ТипыОпераций.Порядок
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТипыОпераций.Порядок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегламентнаяОперация.Ссылка КАК Ссылка,
	|	РегламентнаяОперация.Организация КАК Организация
	|ИЗ
	|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтОрганизации КАК Организации
	|	ПО
	|		РегламентнаяОперация.Организация = Организации.Ссылка 
	|ГДЕ
	|	РегламентнаяОперация.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И РегламентнаяОперация.Проведен
	|	И РегламентнаяОперация.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.ЗакрытиеГода)
	|
	|";

	Запрос.УстановитьПараметр("ДатаНачала", 			 НачалоМесяца(Период));
	Запрос.УстановитьПараметр("ДатаОкончания", 			 КонецМесяца(Период));
	Запрос.УстановитьПараметр("УчитыватьУпрОрганизацию", ПолучитьФункциональнуюОпцию("ИспользоватьУправленческуюОрганизацию"));
	Запрос.УстановитьПараметр("ОСНО", 					 Перечисления.СистемыНалогообложения.Общая);
	Запрос.УстановитьПараметр("ТаблицаОпераций", 		 ТаблицаОпераций);
	Запрос.УстановитьПараметр("МассивОрганизаций", 		 МассивОрганизаций);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", 	 НЕ ЗначениеЗаполнено(МассивОрганизаций));
	
	Результат = Запрос.ВыполнитьПакет();

	ТаблицаОпераций     = Результат[3].Выгрузить();
	ТаблицаЗакрытияГода = Результат[4].Выгрузить();
	ИмяСобытия		= НСтр("ru = 'РегламентнаяОперация'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	
	Для Каждого Строка Из ТаблицаОпераций Цикл
		
		Если Строка.Организация = Справочники.Организации.УправленческаяОрганизация
				И (Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.ФормированиеФинансовогоРезультата
				ИЛИ Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетНалогаНаПрибыль
				ИЛИ Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.ЗакрытиеГода) Тогда
			Продолжить;
		ИначеЕсли Строка.ОбособленноеПодразделение
				И (Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.ФормированиеФинансовогоРезультата
				ИЛИ Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетНалогаНаПрибыль
				ИЛИ Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетДолейСписанияКосвенныхРасходов) Тогда
			Продолжить; // Эти операции выполняются только для головной организации
		ИначеЕсли НЕ Строка.ОрганизацияНаОСНО 
				И Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетНалогаНаПрибыль Тогда   
			Продолжить; // Расчет налога на прибыль выполняем только для организация на ОСНО
		ИначеЕсли Строка.ОрганизацияНаОСНО 
				И (Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.ПризнаниеРасходовПриУСН
				ИЛИ Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетНалогаУСН) Тогда   
			Продолжить; // Эти операции выполняются только для организаций не на ОСНО
		КонецЕсли;
			
		Если Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.СписаниеКосвенныхРасходов
			ИЛИ Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.ФормированиеФинансовогоРезультата
			ИЛИ Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетНалогаНаПрибыль Тогда
			
			// Отменим проведение закрытия года, т.к. они оперируют одними данными
			СтруктураПоиска = Новый Структура("Организация", Строка.Организация);
			ЗакрытияГода = ТаблицаЗакрытияГода.НайтиСтроки(СтруктураПоиска);
			Для каждого ЗакрытиеГода Из ЗакрытияГода Цикл
				ЗакрытиеГодаДокумент = ЗакрытиеГода.Ссылка.ПолучитьОбъект();
				РезультатРасчета.МассивДокументов.Добавить(ЗакрытиеГода.Ссылка);
				Попытка 
					// Проверим, что документ находится в доступном для изменения периоде.
					ОписаниеОшибкиДаты = "";
					Если ДатыЗапретаИзменения.ИзменениеЗапрещено(ЗакрытиеГодаДокумент, , ОписаниеОшибкиДаты) Тогда
						ОписаниеОшибкиДаты = ПротоколРасчетаПартийИСебестоимости.ПредставлениеМногострочногоТекста(ОписаниеОшибкиДаты);
						ВызватьИсключение ОписаниеОшибкиДаты;
					КонецЕсли;
					ЗакрытиеГодаДокумент.Записать(РежимЗаписиДокумента.ОтменаПроведения);
					
				Исключение
					Отказ = Истина;
					ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					РезультатРасчета.ТекстОшибки = СокрЛП(РезультатРасчета.ТекстОшибки + Символы.ПС + ТекстОшибки);
					
					ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
					Возврат РезультатРасчета;
				КонецПопытки;
			КонецЦикла;
		КонецЕсли;
		
		Если Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетНалогаНаИмущество Тогда
			
			УплачиваютсяАвансы = РасчетИмущественныхНалогов.УплачиваютсяАвансыПоНалогуНаИмущество(
									Строка.Организация, Период);
			
			НеобходимРасчетНалогаНаИмущество = 
				Месяц(Период) = 12 
				ИЛИ (Месяц(Период)%3 = 0 И УплачиваютсяАвансы);
			
			Если Не НеобходимРасчетНалогаНаИмущество Тогда
				Продолжить;
			КонецЕсли;
			
		ИначеЕсли Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетТранспортногоНалога Тогда
			
			УплачиваютсяАвансы = РасчетИмущественныхНалогов.УплачиваютсяАвансыПоТранспортномуНалогу(
									Строка.Организация, Период);
			
			НеобходимРасчетТранспортногоНалога = 
				Месяц(Период) = 12 
				ИЛИ (Месяц(Период)%3 = 0 И УплачиваютсяАвансы);
			
			Если Не НеобходимРасчетТранспортногоНалога Тогда
				Продолжить;
			КонецЕсли;
			
		ИначеЕсли Строка.ТипОперации = Перечисления.ТипыРегламентныхОпераций.РасчетЗемельногоНалога Тогда
			
			УплачиваютсяАвансы = РасчетИмущественныхНалогов.УплачиваютсяАвансыПоЗемельномуНалогу(
									Строка.Организация, Период);
			
			НеобходимРасчетЗемельногоНалога = 
				Месяц(Период) = 12 
				ИЛИ (Месяц(Период)%3 = 0 И УплачиваютсяАвансы);
			
			Если Не НеобходимРасчетЗемельногоНалога Тогда
				Продолжить;
			КонецЕсли;
			
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Строка.Ссылка) Тогда
			РеглОперация = Документы.РегламентнаяОперация.СоздатьДокумент();
			РеглОперация.Дата = КонецМесяца(Период);
			РеглОперация.ТипОперации = Строка.ТипОперации;
			РеглОперация.Организация = Строка.Организация;
		Иначе
			РеглОперация = Строка.Ссылка.ПолучитьОбъект();
		КонецЕсли;
		
		// Проверим, что документ находится в доступном для изменения периоде.
		ОписаниеОшибкиДаты = "";
		
		Если ДатыЗапретаИзменения.ИзменениеЗапрещено(РеглОперация, , ОписаниеОшибкиДаты) Тогда
			Отказ = Истина;
			ТекстОшибки = ПротоколРасчетаПартийИСебестоимости.ПредставлениеМногострочногоТекста(ОписаниеОшибкиДаты);
			РезультатРасчета.ТекстОшибки = СокрЛП(РезультатРасчета.ТекстОшибки + Символы.ПС + ТекстОшибки);
			
			ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
			Возврат РезультатРасчета;
		КонецЕсли;
		
		Если РеглОперация.Проведен Тогда
			РеглОперация.ДополнительныеСвойства.Вставить("ОчисткаДляПоследующегоПроведения",Истина);
			Попытка 
				РеглОперация.Записать(РежимЗаписиДокумента.ОтменаПроведения)
			Исключение
				Отказ = Истина;
				ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				РезультатРасчета.ТекстОшибки = СокрЛП(РезультатРасчета.ТекстОшибки + Символы.ПС + ТекстОшибки);
				
				ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
				Возврат РезультатРасчета;
			КонецПопытки;
		КонецЕсли;
		
		РеглОперация.ДополнительныеСвойства.Вставить("ВыводитьСообщенияВЖурналРегистрации",Истина);
		РеглОперация.Записать(РежимЗаписиДокумента.Запись);
		РезультатРасчета.МассивДокументов.Добавить(РеглОперация.Ссылка);
		
		Если ПроводитьДокументы Тогда
			Если РеглОперация.ПроверитьЗаполнение() Тогда
				Попытка
					РеглОперация.Записать(РежимЗаписиДокумента.Проведение);
				Исключение
					Отказ = Истина;
					ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					РезультатРасчета.ТекстОшибки = СокрЛП(РезультатРасчета.ТекстОшибки + Символы.ПС + ТекстОшибки);
					
					ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
				КонецПопытки;
			Иначе
				Отказ = Истина;
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Не заполнены обязательные реквизиты документа %1'"),
					СокрЛП(РеглОперация.Ссылка));
				РезультатРасчета.ТекстОшибки = СокрЛП(РезультатРасчета.ТекстОшибки + Символы.ПС + ТекстОшибки);
				
				ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РезультатРасчета;
	
КонецФункции

// Возвращает текст запроса определения необходимости расчета долей списания косвенных расходов
//
// Возвращаемое значение:
// 	ТекстЗапроса - Строка - Текст запроса.
//
Функция ТекстРасчетаДолейСписанияКосвенныхРасходов() Экспорт
	Возврат "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.Ссылка,
	|	ДД.ГоловнаяОрганизация
	|ПОМЕСТИТЬ ОрганизацииИОбособленныеПодразделения
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЕСТЬNULL(ОбособПодр.Ссылка, ДД.Ссылка) КАК Ссылка,
	|		ДД.Ссылка КАК ГоловнаяОрганизация
	|	ИЗ
	|		Справочник.Организации КАК ДД
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОбособПодр
	|		ПО 
	|			ДД.Ссылка = ОбособПодр.ГоловнаяОрганизация
	|			И ДД.Ссылка <> ОбособПодр.Ссылка
	|	
	|	ГДЕ
	|		НЕ ДД.ОбособленноеПодразделение
	|		И ЕСТЬNULL(ОбособПодр.ОбособленноеПодразделение, ИСТИНА)
	|		И ДД.Ссылка В (&СписокОрганизаций)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ДД.Ссылка КАК Ссылка, // Добавим ссылки на сами головные организации
	|		ДД.Ссылка КАК ГоловнаяОрганизация
	|	ИЗ
	|		Справочник.Организации КАК ДД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОбособПодр
	|		ПО ДД.Ссылка = ОбособПодр.ГоловнаяОрганизация
	|	ГДЕ
	|		НЕ ДД.ОбособленноеПодразделение
	|		И ОбособПодр.ОбособленноеПодразделение
	|		И ДД.Ссылка В (&СписокОрганизаций)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ДД.Ссылка КАК Ссылка, // Добавим ссылки на филиалы
	|		ДД.ГоловнаяОрганизация КАК ГоловнаяОрганизация
	|	ИЗ
	|		Справочник.Организации КАК ДД
	|	ГДЕ
	|		ДД.ОбособленноеПодразделение
	|		И ДД.Ссылка В (&СписокОрганизаций)
	|	) КАК ДД
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Организация КАК Ссылка
	|ПОМЕСТИТЬ ОрганизацииФормированияРезервовПоСомнительнымДолгам
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаОрганизаций.СрезПоследних(&ДатаНачала, Организация В (&СписокОрганизаций)) КАК Т
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УчетныеПолитикиОрганизаций КАК СпрУчетнаяПолитика
	|		ПО Т.УчетнаяПолитика = СпрУчетнаяПолитика.Ссылка
	|		И (СпрУчетнаяПолитика.ПрименяетсяПБУ18
	|			ИЛИ СпрУчетнаяПолитика.ФормироватьРезервыПоСомнительнымДолгамБУ
	|			ИЛИ СпрУчетнаяПолитика.ФормироватьРезервыПоСомнительнымДолгамНУ)
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтатьиРасходов.Ссылка
	|ПОМЕСТИТЬ ВТКосвенныеРасходы
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|ГДЕ
	|	(СтатьиРасходов.ВариантРаспределенияРасходов = 
	|		 ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности)
	|	 ИЛИ СтатьиРасходов.ВариантРаспределенияРасходов = 
	|		 ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
	|	)
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация КАК Ссылка
	|ПОМЕСТИТЬ ВТОрганизацииСДвижениями
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы.ОстаткиИОбороты(
	|			&ДатаНачала,
	|			&ДатаОкончания,,,
	|			Организация В (
	|						ВЫБРАТЬ РАЗЛИЧНЫЕ
	|							ОрганизацииИОбособленныеПодразделения.Ссылка КАК Ссылка 
	|						ИЗ
	|							ОрганизацииИОбособленныеПодразделения КАК ОрганизацииИОбособленныеПодразделения)
	|	) КАК ДанныеРегистра
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКосвенныеРасходы КАК ВТКосвенныеРасходы
	|		ПО ДанныеРегистра.СтатьяРасходов = ВТКосвенныеРасходы.Ссылка
	|ГДЕ
	|	(ДанныеРегистра.СуммаРеглНачальныйОстаток 
	|		- ДанныеРегистра.ПостояннаяРазницаНачальныйОстаток
	|		- ДанныеРегистра.ВременнаяРазницаНачальныйОстаток) <> 0
	|	ИЛИ
	|	(ДанныеРегистра.СуммаРеглПриход
	|		- ДанныеРегистра.ПостояннаяРазницаПриход
	|		- ДанныеРегистра.ВременнаяРазницаПриход) <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АналитикаУчетаПоПартнерам.Организация
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоДокументам КАК Расчеты
	|ГДЕ
	|	Период <= &ДатаОкончания
	|	И АналитикаУчетаПоПартнерам.Организация В (ВЫБРАТЬ Т.Ссылка ИЗ ОрганизацииФормированияРезервовПоСомнительнымДолгам КАК Т)
	|	И Активность
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Проводки.Организация
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&Граница,
	|				Счет = ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РезервыПоСомнительнымДолгам),
	|				&ТипыСубконто,
	|				Организация В (ВЫБРАТЬ Т.Ссылка ИЗ ОрганизацииФормированияРезервовПоСомнительнымДолгам КАК Т) И Субконто3 <> НЕОПРЕДЕЛЕНО) КАК Проводки
	|ГДЕ
	|	(СуммаОстаток <> 0 ИЛИ СуммаНУОстаток <> 0 ИЛИ СуммаПРОстаток <> 0 ИЛИ СуммаВРОстаток <> 0)
	|СГРУППИРОВАТЬ ПО
	|	Организация, Субконто1, Субконто2, Субконто3 // чтобы получить правильные итоги в разрезе субконто
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	*
	|ИЗ ВТОрганизацииСДвижениями
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Организации.Ссылка.ГоловнаяОрганизация
	|ИЗ
	|	ВТОрганизацииСДвижениями КАК Организации
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДолиСписанияКосвенныхРасходов КАК ДанныеРегистра
	|	ПО Организации.Ссылка.ГоловнаяОрганизация = ДанныеРегистра.Организация 
	|		И ДанныеРегистра.ПериодРасчета МЕЖДУ &ДатаНачала И &ДатаОкончания
	|ГДЕ
	|	ДанныеРегистра.Организация ЕСТЬ NULL
	|";
	
КонецФункции

#КонецОбласти


// Подготовка параметров выполнения операций документа

Функция ТекстЗапросаРеквизиты()

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РегламентнаяОперация.Ссылка КАК Регистратор,
	|	РегламентнаяОперация.Дата КАК Период,
	|	РегламентнаяОперация.Организация КАК Организация
	|ИЗ
	|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
	|ГДЕ
	|	РегламентнаяОперация.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Расчет долей списания косвенных расходов

Функция ПодготовитьДанныеРасчетаДолейСписанияКосвенныхРасходов(СтруктураШапки, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", СтруктураШапки.Ссылка);
	
	Запрос.Текст = ТекстЗапросаРеквизиты();
	Результат    = Запрос.ВыполнитьПакет();
	НомераТаблиц = Новый Структура;
	
	НомераТаблиц.Вставить("ТаблицаРеквизитыРасчетДолей", НомераТаблиц.Количество());
	
	Возврат ТаблицыПроведения(НомераТаблиц, Результат);
	
КонецФункции

// Списание косвенных расходов

Функция ПодготовитьДанныеСписанияКосвенныхРасходов(СтруктураШапки, Отказ) Экспорт
	
	// Отменим выполнение расчета налога на прибыль, которое могло уже быть сделано в этом месяце. При списании косвенных расходов мы не должны учитывать движения расчета налога.
	ОтменитьВыполнениеРасчетаНалогаНаПрибыль(СтруктураШапки, Отказ);
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ДоляСписанияТР",          СтруктураШапки.ДоляСписанияТранспортныхРасходов);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация",   Перечисления.ХозяйственныеОперации.СписаниеКосвенныхРасходов);
	Запрос.УстановитьПараметр("Ссылка",                  СтруктураШапки.Ссылка);
	Запрос.УстановитьПараметр("НачДата",                 СтруктураШапки.НачДата);
	Запрос.УстановитьПараметр("КонДата",                 СтруктураШапки.КонДата);
	Запрос.УстановитьПараметр("Граница",                 СтруктураШапки.КонГраница);
	Запрос.УстановитьПараметр("Организация",             СтруктураШапки.Организация);
	Запрос.УстановитьПараметр("ВидыРасходовНормируемые", Перечисления.ВидыРасходовНУ.НормируемыеРасходы());
	Запрос.УстановитьПараметр("ЗакрытиеГода",            Ложь);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаСписаниеКосвенныхРасходов(НомераТаблиц);
	Результат    = Запрос.ВыполнитьПакет();
	
	Возврат ТаблицыПроведения(НомераТаблиц, Результат);
	
КонецФункции

Функция ТекстЗапросаСписаниеКосвенныхРасходов(НомераТаблиц)
	
	// Временные таблицы
	НомераТаблиц.Вставить("ВТ_СтатьиРасходов", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВТ_СписаниеРасходов", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаПрочиеРасходы", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаДвиженияДоходыРасходыПрочиеАктивыПассивы", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтатьиРасходов.Ссылка КАК Ссылка,
	|	ВЫБОР КОГДА СтатьиРасходов.ВидРасходов В (&ВидыРасходовНормируемые) ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК Нормируемые,
	|	ВЫБОР КОГДА СтатьиРасходов.ВидРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ТранспортныеРасходы) ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК Транспортные,
	|	ВЫБОР КОГДА СтатьиРасходов.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты) ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК Производственные,
	|	СтатьиРасходов.КосвенныеЗатратыНУ КАК КосвенныеЗатратыНУ,
	//	Доля списания транспортных расходов
	|	ВЫБОР КОГДА СтатьиРасходов.ВидРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ТранспортныеРасходы) ТОГДА
	|		&ДоляСписанияТР
	//	Нормируемые расходы в конце года списываются полностью
	|	КОГДА &ЗакрытиеГода ТОГДА
	|		1
	//	Доли списания нормируемых расходов
	|	КОГДА СтатьиРасходов.ВидРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.РасходыНаРекламуНормируемые) ТОГДА
	|		ЕСТЬNULL(ДолиСписания.ДоляРасходовНаРекламу, 0)
	|	КОГДА СтатьиРасходов.ВидРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ПредставительскиеРасходы) ТОГДА
	|		ЕСТЬNULL(ДолиСписания.ДоляПредставительскихРасходов, 0)
	|	КОГДА СтатьиРасходов.ВидРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ДобровольноеЛичноеСтрахование) ТОГДА
	|		ЕСТЬNULL(ДолиСписания.ДоляРасходовНаДобровольноеМедицинскоеСтрахование, 0)
	|	КОГДА СтатьиРасходов.ВидРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ДобровольноеСтрахованиеПоДоговорамДолгосрочногоСтрахованияЖизниРаботников) ТОГДА
	|		ЕСТЬNULL(ДолиСписания.ДоляРасходовНаДобровольноеСтрахованиеЖизни, 0)
	|	КОГДА СтатьиРасходов.ВидРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.ДобровольноеЛичноеСтрахованиеНаСлучайСмертиИлиУтратыРаботоспособности) ТОГДА
	|		ЕСТЬNULL(ДолиСписания.ДоляРасходовНаДобровольноеСтрахованиеОтНесчастныхСлучаев, 0)
	|	КОГДА СтатьиРасходов.ВидРасходов = ЗНАЧЕНИЕ(Перечисление.ВидыРасходовНУ.РасходыНаВозмещениеЗатратРаботниковПоУплатеПроцентов) ТОГДА
	|		ЕСТЬNULL(ДолиСписания.ДоляРасходовНаВозмещениеПроцентовРаботникам, 0)
	|	ИНАЧЕ
	|		1
	|	КОНЕЦ КАК ДоляСписания
	|ПОМЕСТИТЬ СтатьиРасходов
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДолиСписанияКосвенныхРасходов КАК ДолиСписания
	|		ПО (СтатьиРасходов.ВариантРаспределенияРасходов <>
	|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты))
	|			И ДолиСписания.Организация = &Организация
	|			И ДолиСписания.ПериодРасчета МЕЖДУ &НачДата И &КонДата
	|			И ДолиСписания.Регистратор <> &Ссылка
	|ГДЕ
	|	НЕ СтатьиРасходов.ВариантРаспределенияРасходов В (
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаРасходыБудущихПериодов), 
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров),
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НеРаспределять),
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&КонДата                               КАК Период,
	|	&ХозяйственнаяОперация                 КАК ХозяйственнаяОперация,
	|	ПрочиеРасходы.Организация              КАК Организация,
	|	ПрочиеРасходы.Подразделение            КАК Подразделение,
	|	ПрочиеРасходы.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	ПрочиеРасходы.СтатьяРасходов           КАК СтатьяРасходов,
	|	ПрочиеРасходы.АналитикаРасходов        КАК АналитикаРасходов,
	|	ВЫБОР КОГДА СтатьиРасходов.Транспортные ТОГДА
	|		ПрочиеРасходы.СуммаРеглОстаток * СтатьиРасходов.ДоляСписания
	|	ИНАЧЕ
	|		ПрочиеРасходы.СуммаРеглОстаток
	|	КОНЕЦ                                  КАК СуммаРегл,
	|	ВЫБОР КОГДА СтатьиРасходов.Транспортные ТОГДА
	|		ПрочиеРасходы.ПостояннаяРазницаОстаток * СтатьиРасходов.ДоляСписания
	|	КОГДА (НЕ &ЗакрытиеГода И СтатьиРасходов.Нормируемые) ТОГДА
	//		  Рассчитаем сумму нормируемых расходов по НУ к списанию: БУ - НУ(в части отнесения затрат на 90 счет)
	|		  (СуммаРеглОстаток - ВременнаяРазницаОстаток)                                                           // Остаток БУ
	|		- (СуммаРеглОстаток - ПостояннаяРазницаОстаток - ВременнаяРазницаОстаток) * СтатьиРасходов.ДоляСписания  // Остаток НУ, который мы можем отнести на затраты (90 счет)
	|	ИНАЧЕ
	//		Закрытие года или не нормируемые расходы
	|		ПрочиеРасходы.ПостояннаяРазницаОстаток
	|	КОНЕЦ                                  КАК ПостояннаяРазница,
	|	ВЫБОР КОГДА СтатьиРасходов.Транспортные ТОГДА
	|		ПрочиеРасходы.ВременнаяРазницаОстаток * СтатьиРасходов.ДоляСписания
	|	КОГДА (НЕ &ЗакрытиеГода И СтатьиРасходов.Нормируемые) ТОГДА
	|		ПрочиеРасходы.ВременнаяРазницаОстаток
	|	ИНАЧЕ
	//		Закрытие года или не нормируемые расходы
	|		ПрочиеРасходы.ВременнаяРазницаОстаток
	|	КОНЕЦ                                  КАК ВременнаяРазница
	|ПОМЕСТИТЬ СписаниеРасходов
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы.Остатки(
	|			&Граница,
	|			Организация = &Организация
	|				И СтатьяРасходов В (ВЫБРАТЬ Т.Ссылка ИЗ СтатьиРасходов КАК Т ГДЕ НЕ Т.Производственные)) КАК ПрочиеРасходы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтатьиРасходов КАК СтатьиРасходов
	|		ПО ПрочиеРасходы.СтатьяРасходов = СтатьиРасходов.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&КонДата КАК Период,
	|	&ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ПрочиеРасходы.Организация,
	|	ПрочиеРасходы.Подразделение,
	|	ПрочиеРасходы.НаправлениеДеятельности,
	|	ПрочиеРасходы.СтатьяРасходов,
	|	ПрочиеРасходы.АналитикаРасходов,
	|	ВЫБОР КОГДА СтатьиРасходов.Транспортные ТОГДА
	|		ПрочиеРасходы.СуммаРеглОстаток * СтатьиРасходов.ДоляСписания
	|	КОГДА &ЗакрытиеГода ТОГДА
	|		ПрочиеРасходы.СуммаРеглОстаток
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаРегл,
	|	ВЫБОР КОГДА СтатьиРасходов.Транспортные ТОГДА
	|		ПрочиеРасходы.СуммаРеглОстаток * СтатьиРасходов.ДоляСписания
	|	КОГДА &ЗакрытиеГода ТОГДА
	|		ПрочиеРасходы.ПостояннаяРазницаОстаток
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК ПостояннаяРазница,
	|	ВЫБОР КОГДА СтатьиРасходов.Транспортные ТОГДА
	|		ПрочиеРасходы.ВременнаяРазницаОстаток * СтатьиРасходов.ДоляСписания
	|	ИНАЧЕ
	|		ПрочиеРасходы.ВременнаяРазницаОстаток
	|	КОНЕЦ КАК ВременнаяРазница
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы.Остатки(
	|			&Граница,
	|			Организация = &Организация
	|				И СтатьяРасходов В (ВЫБРАТЬ Т.Ссылка ИЗ СтатьиРасходов КАК Т ГДЕ Т.Производственные И Т.КосвенныеЗатратыНУ)) КАК ПрочиеРасходы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ СтатьиРасходов КАК СтатьиРасходов
	|		ПО ПрочиеРасходы.СтатьяРасходов = СтатьиРасходов.Ссылка
	|ГДЕ
	//   Косвенные производственные расходы должны быть распределены на этапе распределения производственных расходов
	//   документом "Распределение прочих затрат", сумма регл и сумма ПР тоже должны быть распределены там,
	//   на этот этап может только прийти сумма ВР (Исключением является закрытие года, которое "подчищает" все остатки)
	|	(НЕ ПрочиеРасходы.ВременнаяРазницаОстаток = 0
	|			ИЛИ СтатьиРасходов.Транспортные
	|			ИЛИ &ЗакрытиеГода)
	|;
	|
	|
	|ВЫБРАТЬ
	|	СписаниеРасходов.Период                   КАК Период,
	|	СписаниеРасходов.ХозяйственнаяОперация    КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)    КАК ВидДвижения,
	|	СписаниеРасходов.Организация              КАК Организация,
	|	СписаниеРасходов.Подразделение            КАК Подразделение,
	|	СписаниеРасходов.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	СписаниеРасходов.СтатьяРасходов           КАК СтатьяРасходов,
	|	СписаниеРасходов.АналитикаРасходов        КАК АналитикаРасходов,
	|	СписаниеРасходов.СуммаРегл                КАК СуммаРегл,
	|	СписаниеРасходов.ПостояннаяРазница        КАК ПостояннаяРазница,
	|	СписаниеРасходов.ВременнаяРазница         КАК ВременнаяРазница
	|ИЗ
	|	СписаниеРасходов КАК СписаниеРасходов
	|ГДЕ
	|	СписаниеРасходов.СуммаРегл <> 0
	|	ИЛИ СписаниеРасходов.ПостояннаяРазница <> 0
	|	ИЛИ СписаниеРасходов.ВременнаяРазница <> 0
	|;
	|
	|ВЫБРАТЬ
	|	СписаниеРасходов.Период                   КАК Период,
	|	СписаниеРасходов.ХозяйственнаяОперация    КАК ХозяйственнаяОперация,
	|	СписаниеРасходов.Организация              КАК Организация,
	|	СписаниеРасходов.Подразделение            КАК Подразделение,
	|	СписаниеРасходов.НаправлениеДеятельности  КАК НаправлениеДеятельности,
	|	СписаниеРасходов.СтатьяРасходов           КАК Статья,
	|	СписаниеРасходов.АналитикаРасходов        КАК АналитикаРасходов,
	|	СписаниеРасходов.Подразделение            КАК КорПодразделение,
	|	СписаниеРасходов.НаправлениеДеятельности  КАК КорНаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПрибылиИУбытки) КАК КорСтатья,
	|	НЕОПРЕДЕЛЕНО                              КАК КорАналитикаАктивовПассивов,
	|	СписаниеРасходов.СуммаРегл                КАК СуммаРегл
	|ИЗ
	|	СписаниеРасходов КАК СписаниеРасходов
	|ГДЕ
	|	СписаниеРасходов.СуммаРегл <> 0
	|";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Процедура ОтменитьВыполнениеРасчетаНалогаНаПрибыль(СтруктураШапки, Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РегламентнаяОперация.Ссылка
	|ИЗ
	|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
	|ГДЕ
	|	РегламентнаяОперация.Проведен
	|	И РегламентнаяОперация.Дата МЕЖДУ &НачДата И &КонДата
	|	И РегламентнаяОперация.Организация = &Организация
	|	И РегламентнаяОперация.ТипОперации = ЗНАЧЕНИЕ(Перечисление.ТипыРегламентныхОпераций.РасчетНалогаНаПрибыль)";
	
	Запрос.УстановитьПараметр("НачДата", СтруктураШапки.НачДата);
	Запрос.УстановитьПараметр("КонДата", СтруктураШапки.КонДата);
	Запрос.УстановитьПараметр("Организация", СтруктураШапки.Организация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		НачатьТранзакцию();
		
		Попытка
			
			ЗаблокироватьДанныеДляРедактирования(Выборка.Ссылка);
			
			РасчетНалога = Выборка.Ссылка.ПолучитьОбъект();
			
			РасчетНалога.Записать(РежимЗаписиДокумента.ОтменаПроведения);
			ЗафиксироватьТранзакцию();
		Исключение
			
			ОтменитьТранзакцию();
			
			Отказ = Истина;
			
			ТекстСообщения = НСтр("ru = 'Не удалось отменить проведение расчета налога на прибыль по причине: %Причина%'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Списание косвенных расходов'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Предупреждение,
				Выборка.Ссылка, , ТекстСообщения);
				
			Возврат;
				
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

// Формирование финансового результата

Функция ПодготовитьПараметрыЗакрытиеСчетов90_91(СтруктураШапки, Отказ) Экспорт
	
	ПараметрыПроведения = Новый Структура;
	
	Запрос     = Новый Запрос;
	Содержание = НСтр("ru = 'Определение финансовых результатов'");
	Запрос.УстановитьПараметр("Ссылка",          СтруктураШапки.Ссылка);
	Запрос.УстановитьПараметр("КоэффициентЕНВД", НалоговыйУчет.КоэффициентРаспределенияРасходовПоВидамДеятельности(СтруктураШапки.Организация, СтруктураШапки.КонДата));
	Запрос.УстановитьПараметр("Содержание",      Содержание);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаЗакрытиеСчетов90_91(НомераТаблиц);
	Результат    = Запрос.ВыполнитьПакет();

	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;
	
КонецФункции // ПодготовитьПараметрыЗакрытиеСчетов90_91()

Функция ТекстЗапросаЗакрытиеСчетов90_91(НомераТаблиц)
	
	// Временные таблицы
	НомераТаблиц.Вставить("ТаблицаРеквизитыЗакрытие90_91", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РегламентнаяОперация.Ссылка КАК Регистратор,
	|	РегламентнаяОперация.Организация КАК Организация,
	|	РегламентнаяОперация.Дата КАК Период,
	|	НАЧАЛОПЕРИОДА(РегламентнаяОперация.Дата, МЕСЯЦ) КАК НачДата,
	|	КОНЕЦПЕРИОДА(РегламентнаяОперация.Дата, МЕСЯЦ) КАК КонДата,
	|	&КоэффициентЕНВД КАК КоэффициентЕНВД,
	|	&Содержание КАК Содержание
	|ИЗ
	|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
	|ГДЕ
	|	РегламентнаяОперация.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции // ТекстЗапросаЗакрытиеСчетов90_91()

Функция ТекстЗапросаОкруглениеНДС(НомераТаблиц)
	
	// Временные таблицы
	НомераТаблиц.Вставить("ТаблицаРеквизитыОкруглениеНДС", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РегламентнаяОперация.Ссылка КАК Регистратор,
	|	РегламентнаяОперация.Организация КАК Организация,
	|	РегламентнаяОперация.Дата КАК Период,
	|	НАЧАЛОПЕРИОДА(РегламентнаяОперация.Дата, КВАРТАЛ) КАК НачДата,
	|	КОНЕЦПЕРИОДА(РегламентнаяОперация.Дата, КВАРТАЛ) КАК КонДата,
	|	&Содержание КАК Содержание
	|ИЗ
	|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
	|ГДЕ
	|	РегламентнаяОперация.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции // ТекстЗапросаОкруглениеНДС()

Функция ПодготовитьПараметрыОкругленияНДС(СтруктураШапки) Экспорт
	
	ПараметрыПроведения = Новый Структура;
	
	Запрос     = Новый Запрос;
	Содержание = НСтр("ru = 'Отклонение при округлениии до рублей'");
	Запрос.УстановитьПараметр("Ссылка",          СтруктураШапки.Ссылка);
	Запрос.УстановитьПараметр("Содержание",      Содержание);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаОкруглениеНДС(НомераТаблиц);
	Результат    = Запрос.ВыполнитьПакет();

	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;
	
КонецФункции // ПодготовитьПараметрыОкругленияНДС()

// Расчет налога на прибыль

Функция ПодготовитьДанныеРасчетаНалогаНаПрибыль(СтруктураШапки, Отказ) Экспорт
	
#Область ДопПараметрыШапки // Для передачи "наверх!" - в ПроведениеПересчетаОНАиОНОКаждыйМесяц()
	СтруктураШапки.Вставить(
		"ПоддержкаПБУ18",
		УчетнаяПолитика.ПоддержкаПБУ18(СтруктураШапки.Организация, СтруктураШапки.КонДата));
	СтруктураШапки.Вставить(
		"СтавкаНалогаНаПрибыль",
		БухгалтерскийУчетПереопределяемый.ПолучитьСтавкуНалогаНаПрибыль(СтруктураШапки));
#Конецобласти
	
	Запрос = Новый Запрос;
	
	РегистрацияВНалоговомОргане = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		СтруктураШапки.Организация,
		"РегистрацияВНалоговомОргане").РегистрацияВНалоговомОргане;
	КоэффициентЕНВД = НалоговыйУчет.КоэффициентРаспределенияРасходовПоВидамДеятельности(
		СтруктураШапки.Организация,
		СтруктураШапки.КонДата);
	
	Запрос.УстановитьПараметр("Ссылка",                      СтруктураШапки.Ссылка);
	Запрос.УстановитьПараметр("СтавкаНалогаНаПрибыль",       СтруктураШапки.СтавкаНалогаНаПрибыль);
	Запрос.УстановитьПараметр("КоэффициентЕНВД",             КоэффициентЕНВД);
	Запрос.УстановитьПараметр("Содержание",                  НСтр("ru = 'Налог на прибыль'"));
	Запрос.УстановитьПараметр("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаРасчетНалога(НомераТаблиц);
	Результат    = Запрос.ВыполнитьПакет();
	
	Возврат ТаблицыПроведения(НомераТаблиц, Результат);
	
КонецФункции

Функция ТекстЗапросаРасчетНалога(НомераТаблиц)
	
	// Временные таблицы
	НомераТаблиц.Вставить("ТаблицаРеквизитыРасчетНалога", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РегламентнаяОперация.Ссылка КАК Регистратор,
	|	РегламентнаяОперация.Организация КАК Организация,
	|	РегламентнаяОперация.Дата КАК Дата,
	|	РегламентнаяОперация.Дата КАК Период,
	|	НАЧАЛОПЕРИОДА(РегламентнаяОперация.Дата, ГОД) КАК НачалоГода,
	|	НАЧАЛОПЕРИОДА(РегламентнаяОперация.Дата, МЕСЯЦ) КАК НачДата,
	|	КОНЕЦПЕРИОДА(РегламентнаяОперация.Дата, МЕСЯЦ) КАК КонДата,
	|	&РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	&КоэффициентЕНВД КАК КоэффициентЕНВД,
	|	&СтавкаНалогаНаПрибыль КАК СтавкаНалогаНаПрибыль,
	|	&Содержание КАК Содержание
	|ИЗ
	|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
	|ГДЕ
	|	РегламентнаяОперация.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

// Закрытие года

Функция ПодготовитьДанныеЗакрытияГода(СтруктураШапки, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	
	СодержаниеРеформация   = НСтр("ru = 'Реформация баланса'");;
	СодержаниеЗакрытиеГода = НСтр("ru = 'Закрытие года'");
	
	Период                 = СтруктураШапки.Дата;
	СтруктураШапки.Дата    = КонецМесяца(СтруктураШапки.Дата) + 1;
	НоваяСтавка            = БухгалтерскийУчетПереопределяемый.ПолучитьСтавкуНалогаНаПрибыль(СтруктураШапки) * 100;
	
	СтруктураШапки.Дата    = Период;
	СтараяСтавка           = БухгалтерскийУчетПереопределяемый.ПолучитьСтавкуНалогаНаПрибыль(СтруктураШапки) * 100;
	
	Запрос.УстановитьПараметр("Ссылка",                  СтруктураШапки.Ссылка);
	Запрос.УстановитьПараметр("НоваяСтавка",             НоваяСтавка);
	Запрос.УстановитьПараметр("СтараяСтавка",            СтараяСтавка);
	Запрос.УстановитьПараметр("Предприниматель",         СтруктураШапки.Предприниматель);
	Запрос.УстановитьПараметр("СодержаниеРеформация",    СодержаниеРеформация);
	Запрос.УстановитьПараметр("СодержаниеЗакрытиеГода",  СодержаниеЗакрытиеГода);
	Запрос.УстановитьПараметр("ЗакрытиеГода",            Истина);
	Запрос.УстановитьПараметр("ДоляСписанияТР",          0);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация",   Перечисления.ХозяйственныеОперации.ЗакрытиеМесяца);
	Запрос.УстановитьПараметр("НачДата",                 СтруктураШапки.НачДата);
	Запрос.УстановитьПараметр("КонДата",                 СтруктураШапки.КонДата);
	Запрос.УстановитьПараметр("Граница",                 СтруктураШапки.КонГраница);
	Запрос.УстановитьПараметр("Организация",             СтруктураШапки.Организация);
	Запрос.УстановитьПараметр("ВидыРасходовНормируемые", Перечисления.ВидыРасходовНУ.НормируемыеРасходы());
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаЗакрытиеГода(НомераТаблиц)
	             + ТекстЗапросаСписаниеКосвенныхРасходов(НомераТаблиц);
	Результат    = Запрос.ВыполнитьПакет();
	
	Возврат ТаблицыПроведения(НомераТаблиц, Результат);
	
КонецФункции

Функция ТекстЗапросаЗакрытиеГода(НомераТаблиц)
	
	// Временная таблица
	НомераТаблиц.Вставить("ТаблицаРеквизитыЗакрытиеГода", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РегламентнаяОперация.Ссылка КАК Регистратор,
	|	РегламентнаяОперация.Организация КАК Организация,
	|	РегламентнаяОперация.Дата КАК Период,
	|	НАЧАЛОПЕРИОДА(РегламентнаяОперация.Дата, ГОД) КАК НачалоГода,
	|	&НачДата КАК НачДата,
	|	&КонДата КАК КонДата,
	|	&НоваяСтавка КАК НоваяСтавка,
	|	&СтараяСтавка КАК СтараяСтавка,
	|	&Предприниматель КАК Предприниматель,
	|	&СодержаниеРеформация КАК СодержаниеРеформация,
	|	&СодержаниеЗакрытиеГода КАК СодержаниеЗакрытиеГода
	|ИЗ
	|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
	|ГДЕ
	|	РегламентнаяОперация.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

// Прочее

Функция ТаблицыПроведения(НомераТаблиц, Результат)
	
	ТаблицыПроведения = Новый Структура;
	
	Для Каждого НомерТаблицы Из НомераТаблиц Цикл
		Если НРег(Лев(НомерТаблицы.Ключ, 7)) = "таблица" Тогда
			ТаблицыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТаблицыПроведения;
	
КонецФункции

// Расчет резервов по сомнительным долгам
 
Функция ПодготовитьПараметрыРезервыПоСомнительнымДолгам(СтруктураШапки, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ГоловноеПодразделение = ОбщегоНазначенияБПВызовСервераПовтИсп.ГоловнаяОрганизация(СтруктураШапки.Организация);
	Если ЗначениеЗаполнено(ГоловноеПодразделение) Тогда
		ОрганизацияДляУчетнойПолитики = ГоловноеПодразделение;
	Иначе
		ОрганизацияДляУчетнойПолитики = СтруктураШапки.Организация;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Ссылка",              СтруктураШапки.Ссылка);
	Запрос.УстановитьПараметр("ДатаНачала",             НачалоМесяца(СтруктураШапки.Дата));
	Запрос.УстановитьПараметр("ДатаОкончания",             КонецМесяца(СтруктураШапки.Дата));
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ОрганизацияДляУчетнойПолитики);
	СписокОрганизаций = Новый СписокЗначений;
	СписокОрганизаций.Добавить(ОрганизацияДляУчетнойПолитики);
	Запрос.УстановитьПараметр("СписокОрганизаций", СписокОрганизаций);
	Запрос.УстановитьПараметр("Граница", Новый Граница(КонецМесяца(СтруктураШапки.Дата), ВидГраницы.Включая));
	ТипыСубконто = Новый Массив;
	ТипыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	ТипыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Договоры);
	ТипыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ДокументыРасчетовСКонтрагентами);
	Запрос.УстановитьПараметр("ТипыСубконто", ТипыСубконто);
	
	УстановитьПривилегированныйРежим(Истина);

	НомераТаблиц = Новый Структура;
	Запрос.Текст = ПолучитьТаблицуДолейСписанияКосвенныхРасходов(НомераТаблиц)
				 + ТекстЗапросаРезервыПоСомнительнымДолгам(НомераТаблиц);
	Результат    = Запрос.ВыполнитьПакет();
	ПараметрыПроведения = ТаблицыПроведения(НомераТаблиц, Результат);
	
	Если ПараметрыПроведения.ТаблицаДолейКосвенныхРасходов.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Нет данных о долях списания косвенных расходов
			|Не обнаружена регламентная операция ""Расчет долей списания косвенных расходов""
			|Выполните указанную операцию'");
		
		БухгалтерскийУчетПереопределяемый.СообщитьОбОшибкеРегОперацииСНавигацией(ТекстСообщения, , Отказ, СтруктураШапки.Ссылка);
	КонецЕсли;
	
	Возврат ПараметрыПроведения;
	
КонецФункции

Функция ТекстЗапросаРезервыПоСомнительнымДолгам(НомераТаблиц)
	
	// Временные таблицы
	НомераТаблиц.Вставить("ТаблицаРеквизитыРезервыПоСомнительнымДолгам", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РегламентнаяОперация.Ссылка КАК Регистратор,
	|	РегламентнаяОперация.Организация КАК Организация,
	|	РегламентнаяОперация.Дата КАК Период,
	|	КОНЕЦПЕРИОДА(РегламентнаяОперация.Дата, МЕСЯЦ) КАК КонДата
	|ИЗ
	|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
	|ГДЕ
	|	РегламентнаяОперация.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ПолучитьТаблицуДолейСписанияКосвенныхРасходов(НомераТаблиц)
	
	// Временная таблица
	НомераТаблиц.Вставить("ТаблицаДолейКосвенныхРасходов", НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ОбщиеДоли.ДоляРасходовНаРекламу,
	|	ОбщиеДоли.ДоляПредставительскихРасходов,
	|	ОбщиеДоли.ДоляРасходовНаДобровольноеМедицинскоеСтрахование,
	|	ОбщиеДоли.ДоляРасходовНаДобровольноеСтрахованиеЖизни,
	|	ОбщиеДоли.ДоляРасходовНаВозмещениеПроцентовРаботникам,
	|	ОбщиеДоли.ДоляЕНВД,
	|	ОбщиеДоли.ДоляТранспортныхРасходов,
	|	ОбщиеДоли.ДоляРасходовНаДобровольноеСтрахованиеОтНесчастныхСлучаев,
	|	ОбщиеДоли.ДоляРезервовПоСомнительнымДолгамНУ
	|ИЗ
	|	(ВЫБРАТЬ
	|		СУММА(Доли.ДоляРасходовНаРекламу) КАК ДоляРасходовНаРекламу,
	|		СУММА(Доли.ДоляПредставительскихРасходов) КАК ДоляПредставительскихРасходов,
	|		СУММА(Доли.ДоляРасходовНаДобровольноеМедицинскоеСтрахование) КАК ДоляРасходовНаДобровольноеМедицинскоеСтрахование,
	|		СУММА(Доли.ДоляРасходовНаДобровольноеСтрахованиеЖизни) КАК ДоляРасходовНаДобровольноеСтрахованиеЖизни,
	|		СУММА(Доли.ДоляРасходовНаВозмещениеПроцентовРаботникам) КАК ДоляРасходовНаВозмещениеПроцентовРаботникам,
	|		СУММА(Доли.ДоляЕНВД) КАК ДоляЕНВД,
	|		СУММА(Доли.ДоляТранспортныхРасходов) КАК ДоляТранспортныхРасходов,
	|		СУММА(Доли.ДоляРасходовНаДобровольноеСтрахованиеОтНесчастныхСлучаев) КАК ДоляРасходовНаДобровольноеСтрахованиеОтНесчастныхСлучаев,
	|		СУММА(Доли.ДоляРезервовПоСомнительнымДолгамНУ) КАК ДоляРезервовПоСомнительнымДолгамНУ
	|	ИЗ
	|		РегистрСведений.ДолиСписанияКосвенныхРасходов КАК Доли
	|	ГДЕ
	|		Доли.Организация = &ГоловнаяОрганизация
	|		И Доли.ПериодРасчета МЕЖДУ &ДатаНачала И &ДатаОкончания) КАК ОбщиеДоли
	|ГДЕ
	|	ОбщиеДоли.ДоляРасходовНаРекламу ЕСТЬ НЕ NULL";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

// Расчет налога на имущество

Функция ПодготовитьПараметрыРасчетИмущественныхНалогов(СтруктураШапки, Отказ) Экспорт
	
	ПараметрыПроведения = Новый Структура;

	Если СтруктураШапки.ВидНалога = Перечисления.ВидыИмущественныхНалогов.НалогНаИмущество Тогда
		
		НачалоПериода = НачалоГода(СтруктураШапки.НачДата);
		ИмяРегистраРасчетНалогов = "РасчетНалогаНаИмущество";
		
	Иначе
		НачалоПериода = ?(КонецГода(СтруктураШапки.КонДата) = КонецКвартала(СтруктураШапки.КонДата),
						НачалоГода(СтруктураШапки.НачДата),
						НачалоКвартала(СтруктураШапки.НачДата));
						
		Если СтруктураШапки.ВидНалога = Перечисления.ВидыИмущественныхНалогов.ТранспортныйНалог Тогда
			ИмяРегистраРасчетНалогов = "РасчетТранспортногоНалога";	
		ИначеЕсли СтруктураШапки.ВидНалога = Перечисления.ВидыИмущественныхНалогов.ЗемельныйНалог Тогда
			ИмяРегистраРасчетНалогов = "РасчетЗемельногоНалога";	
		КонецЕсли;
			
	КонецЕсли;
	
	Содержание  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Начислен %1 за %2'"),
		Строка(СтруктураШапки.ВидНалога),
		ПредставлениеПериода(НачалоПериода, СтруктураШапки.КонДата, "ФП = Истина"));
	
	Запрос = Новый Запрос;
		
	Запрос.УстановитьПараметр("Ссылка", СтруктураШапки.Ссылка);
	Запрос.УстановитьПараметр("Содержание", Содержание);
	Запрос.УстановитьПараметр("ВидНалога", СтруктураШапки.ВидНалога);
	Запрос.УстановитьПараметр("ИмяРегистраРасчетНалогов", ИмяРегистраРасчетНалогов);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаРасчетИмущественныхНалогов(НомераТаблиц);
	Результат = Запрос.ВыполнитьПакет();
	
	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;
	
КонецФункции

Функция ТекстЗапросаРасчетИмущественныхНалогов(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаРеквизитыРасчетИмущественныхНалогов", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РегламентнаяОперация.Ссылка КАК Регистратор,
	|	РегламентнаяОперация.Организация КАК Организация,
	|	РегламентнаяОперация.Дата КАК Период,
	|	&ВидНалога КАК ВидНалога,
	|	&Содержание КАК Содержание,
	|	&ИмяРегистраРасчетНалогов КАК ИмяРегистраРасчетНалогов
	|ИЗ
	|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
	|ГДЕ
	|	РегламентнаяОперация.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

// Расчет курсовых разниц

Процедура РасчетКурсовыхРазниц(ПараметрыРасчета, Отказ) Экспорт
	
	Период = КонецМесяца(ПараметрыРасчета.Дата);
	Запрос = Новый Запрос(ТекстЗапросаРасчетКурсовыхРазниц());
	Запрос.УстановитьПараметр("Организация", ПараметрыРасчета.Организация);
	Запрос.УстановитьПараметр("ГраницаОстатков", Новый Граница(Период, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("НаДату", Период);
	
	ПроводкиДокумента = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
	ПроводкиДокумента.Отбор.Регистратор.Установить(ПараметрыРасчета.Ссылка);
	
	ВидСубконтоСтатьиДДС = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиДвиженияДенежныхСредств;
	СтатьяДДСПрибыль = Справочники.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыПрибыль;
	СтатьяДДСУбыток = Справочники.СтатьиДвиженияДенежныхСредств.КурсовыеРазницыУбыток;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Проводка = ПроводкиДокумента.Добавить();
		Проводка.Активность = Истина;
		Проводка.Период = Период;
		Проводка.Организация = ПараметрыРасчета.Организация;
		
		СуммаРазницы = Макс(Выборка.КурсоваяРазница, -Выборка.КурсоваяРазница);
		Если Выборка.КурсоваяРазница > 0 Тогда// отразим прибыль
			Счет = "Дт";
			Кор = "Кт";
			КорСчет = Выборка.СчетДоходов;
			КорСубконто = Выборка.СтатьяДоходов;
			КорВидСубконто = Выборка.ВидСубконтоДоходыРасходы;
			СтатьяДДС = СтатьяДДСПрибыль;
			
		Иначе// отразим убытки
			Счет = "Кт";
			Кор = "Дт";
			КорСчет = Выборка.СчетРасходов;
			КорСубконто = Выборка.СтатьяРасходов;
			КорВидСубконто = Выборка.ВидСубконтоДоходыРасходы;
			СтатьяДДС = СтатьяДДСУбыток;
			
		КонецЕсли;
		
		// заполним счет и аналитику возникновения курсовой разницы
		Проводка["Счет"+Счет] = Выборка.Счет;
		Проводка["Валюта"+Счет] = Выборка.Валюта;
		Проводка["Подразделение"+Счет] = Выборка.Подразделение;
		Проводка["НаправлениеДеятельности"+Счет] = Выборка.НаправлениеДеятельности;
		ВидыСубконто = Выборка.ВидыСубконтоСчета.Выгрузить();
		Для Каждого стр Из ВидыСубконто Цикл
			Проводка["Субконто" + Счет][стр.ВидСубконто] = Выборка["Субконто"+стр.НомерСтроки];
			Если стр.ВидСубконто = ВидСубконтоСтатьиДДС И стр.ТолькоОбороты Тогда
				Проводка["Субконто" + Счет][стр.ВидСубконто] = СтатьяДДС;
			КонецЕсли;
		КонецЦикла;
		
		// заполним счет учета прибыли\убытка от курсовой разницы
		Проводка["Счет" + Кор] = КорСчет;
		Проводка["Подразделение"+Кор] = Выборка.Подразделение;
		Проводка["НаправлениеДеятельности"+Кор] = Выборка.НаправлениеДеятельности;
		Проводка["Субконто" + Кор][КорВидСубконто] = КорСубконто;
		
		Проводка.Сумма = СуммаРазницы;
		Если Выборка.НалоговыйУчет Тогда
			Проводка["СуммаНУ"+Счет] = СуммаРазницы;
		КонецЕсли;
		Проводка["СуммаНУ"+Кор] = СуммаРазницы;
		
	КонецЦикла;// по валютным остаткам
	
	ПроводкиДокумента.Записать();
	
КонецПроцедуры

Функция ТекстЗапросаРасчетКурсовыхРазниц()
	
	Возврат
	"ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Период,
	|	КурсыВалютСрезПоследних.Валюта,
	|	КурсыВалютСрезПоследних.Курс,
	|	КурсыВалютСрезПоследних.Кратность
	|ПОМЕСТИТЬ втКурсы
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&НаДату, ) КАК КурсыВалютСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Остатки.Счет,
	|	Остатки.Организация,
	|	Остатки.Подразделение,
	|	Остатки.НаправлениеДеятельности,
	|	Остатки.Счет.ВидыСубконто КАК ВидыСубконтоСчета,
	|	Остатки.Счет.Валютный КАК СчетВалютный,
	|	Остатки.Счет.НалоговыйУчет КАК НалоговыйУчет,
	|	Остатки.Субконто1,
	|	Остатки.Субконто2,
	|	Остатки.Субконто3,
	|	Остатки.Валюта,
	|	Остатки.ВалютнаяСуммаОстаток КАК ОстатокВалюты,
	|	Остатки.СуммаОстаток КАК ОстатокРегл,
	|	Остатки.ВалютнаяСуммаОстаток * втКурсы.Курс / втКурсы.Кратность КАК ОстатокПоКурсу,
	|	Остатки.ВалютнаяСуммаОстаток * втКурсы.Курс / втКурсы.Кратность - Остатки.СуммаОстаток КАК АбсолютнаяРазница,
	|	ВЫРАЗИТЬ(Остатки.ВалютнаяСуммаОстаток * втКурсы.Курс / втКурсы.Кратность КАК ЧИСЛО(15,2)) - Остатки.СуммаОстаток КАК КурсоваяРазница,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеДоходы) КАК СчетДоходов,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.КурсовыеРазницы) КАК СтатьяДоходов,
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПрочиеРасходы) КАК СчетРасходов,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.КурсовыеРазницы) КАК СтатьяРасходов,
	|	
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИрасходы) КАК ВидСубконтоДоходыРасходы
	|
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ГраницаОстатков,
	|			Счет.Валютный И НЕ Счет.ИсключитьИзПереоценкиПоПлануСчетов,
	|			,
	|			Организация = &Организация) КАК Остатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втКурсы КАК втКурсы
	|		ПО Остатки.Валюта = втКурсы.Валюта
	|ГДЕ
	|	ВЫРАЗИТЬ(Остатки.ВалютнаяСуммаОстаток * втКурсы.Курс / втКурсы.Кратность КАК ЧИСЛО(15,2)) <> Остатки.СуммаОстаток";
	
КонецФункции

// Расчет торгового сбора

Функция ПодготовитьПараметрыРасчетТорговогоСбора(СтруктураШапки, Отказ) Экспорт
	
	ПараметрыПроведения = Новый Структура;
	
	НачалоПериода = НачалоКвартала(СтруктураШапки.НачДата);
	КонецПериода  = КонецКвартала(СтруктураШапки.КонДата);
	
	Содержание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Начислен торговый сбор за %1'"),
		ПредставлениеПериода(НачалоПериода, КонецПериода, "ФП = Истина"));
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", СтруктураШапки.Ссылка);
	Запрос.УстановитьПараметр("Содержание", Содержание);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаРасчетТорговогоСбора(НомераТаблиц);
	Результат    = Запрос.ВыполнитьПакет();
	
	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;
	
КонецФункции

Функция ТекстЗапросаРасчетТорговогоСбора(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаРеквизитыРасчетТорговогоСбора", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РегламентнаяОперация.Ссылка                       КАК Регистратор,
	|	РегламентнаяОперация.Организация                  КАК Организация,
	|	РегламентнаяОперация.Дата                         КАК Период,
	|	НАЧАЛОПЕРИОДА(РегламентнаяОперация.Дата, КВАРТАЛ) КАК НачДата,
	|	КОНЕЦПЕРИОДА(РегламентнаяОперация.Дата, КВАРТАЛ)  КАК КонДата,
	|	&Содержание                                       КАК Содержание
	|ИЗ
	|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
	|ГДЕ
	|	РегламентнаяОперация.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

// Начисление налога УСН

Функция ПодготовитьПараметрыНачисленияНалогаУСН(СтруктураШапки, Отказ) Экспорт

	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", СтруктураШапки.Ссылка);
	
	Запрос.УстановитьПараметр("ПрименяетсяУСН",
		УчетнаяПолитика.ПрименяетсяУСН(СтруктураШапки.Организация, СтруктураШапки.КонДата));
	Запрос.УстановитьПараметр("ПрименяетсяУСНДоходы",
		УчетнаяПолитика.ПрименяетсяУСНДоходы(СтруктураШапки.Организация, СтруктураШапки.КонДата));
	Запрос.УстановитьПараметр("ПрименяетсяУСНДоходыМинусРасходы",
		УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходы(СтруктураШапки.Организация, СтруктураШапки.КонДата));
	Запрос.УстановитьПараметр("СтавкаНалогаУСН",
		УчетнаяПолитика.СтавкаНалогаУСН(СтруктураШапки.Организация, СтруктураШапки.КонДата));
		
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаНачислениеНалогаУСН(НомераТаблиц);
	Результат = Запрос.ВыполнитьПакет();
	
	Возврат ТаблицыПроведения(НомераТаблиц, Результат);

КонецФункции

Функция ТекстЗапросаНачислениеНалогаУСН(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаРеквизитыНалогУСН", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РегламентнаяОперация.Ссылка КАК Регистратор,
	|	РегламентнаяОперация.Организация КАК Организация,
	|	РегламентнаяОперация.Дата КАК Период,
	|	НАЧАЛОПЕРИОДА(РегламентнаяОперация.Дата, ГОД) КАК НачалоГода,
	|	КОНЕЦПЕРИОДА(РегламентнаяОперация.Дата, КВАРТАЛ) КАК КонДата,
	|	&ПрименяетсяУСН КАК ПрименяетсяУСН,
	|	&ПрименяетсяУСНДоходы КАК ПрименяетсяУСНДоходы,
	|	&ПрименяетсяУСНДоходыМинусРасходы КАК ПрименяетсяУСНДоходыМинусРасходы,
	|	&СтавкаНалогаУСН,
	|	ВЫБОР
	|		КОГДА НАЧАЛОПЕРИОДА(РегламентнаяОперация.Дата, ГОД) = НАЧАЛОПЕРИОДА(РегламентнаяОперация.Дата, КВАРТАЛ)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоНачалоГода,
	|	ВЫБОР
	|		КОГДА КОНЕЦПЕРИОДА(РегламентнаяОперация.Дата, ГОД) = КОНЕЦПЕРИОДА(РегламентнаяОперация.Дата, КВАРТАЛ)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоКонецГода
	|ИЗ
	|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
	|ГДЕ
	|	РегламентнаяОперация.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

// Признание расходов на приобретение ОС по договору лизинга

Функция ПодготовитьПараметрыПризнаниеВНУЛизинговыхПлатежей(СтруктураШапки) Экспорт

	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	
	Содержание = "Лизинговые платежи";
	
	Запрос.УстановитьПараметр("Ссылка",     СтруктураШапки.Ссылка);
	Запрос.УстановитьПараметр("Организация",СтруктураШапки.Организация);
	Запрос.УстановитьПараметр("КонДата",    СтруктураШапки.КонДата);
	Запрос.УстановитьПараметр("Содержание", Содержание);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаРеквизитыАрендныеПлатежи(НомераТаблиц)
					+ ТекстЗапросаТаблицаОС(НомераТаблиц);
	Результат    = Запрос.ВыполнитьПакет();

	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;
	
КонецФункции

Функция ТекстЗапросаРеквизитыАрендныеПлатежи(НомераТаблиц)
	
	НомераТаблиц.Вставить("АрендныеПлатежи", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РегламентнаяОперация.Ссылка КАК Регистратор,
	|	РегламентнаяОперация.Организация КАК Организация,
	|	РегламентнаяОперация.Дата КАК Период,
	|	РегламентнаяОперация.Дата КАК ДатаРасчета,
	|	ИСТИНА КАК ВыдаватьСообщения,
	|	"""" КАК ИмяСписка,
	|	&Содержание КАК Содержание
	|ИЗ
	|	Документ.РегламентнаяОперация КАК РегламентнаяОперация
	|ГДЕ
	|	РегламентнаяОперация.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаТаблицаОС(НомераТаблиц)
	
	НомераТаблиц.Вставить("ТаблицаОС", НомераТаблиц.Количество() + 2);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СостоянияОСОрганизаций.ОсновноеСредство КАК ОсновноеСредство,
	|	СостоянияОСОрганизаций.Организация КАК Организация,
	|	МАКСИМУМ(СостоянияОСОрганизаций.ДатаСостояния) КАК ДатаСостояния
	|ПОМЕСТИТЬ ДатыПоследнихСостояний
	|ИЗ
	|	РегистрСведений.СостоянияОСОрганизаций КАК СостоянияОСОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВводОстатковВнеоборотныхАктивов КАК ДокВводОстатков
	|		ПО ДокВводОстатков.Ссылка = СостоянияОСОрганизаций.Регистратор
	|ГДЕ
	|	(СостоянияОСОрганизаций.ДатаСостояния < &КонДата
	|			И НЕ СостоянияОСОрганизаций.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов
	|		ИЛИ СостоянияОСОрганизаций.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов
	|			И КОНЕЦПЕРИОДА(ДокВводОстатков.Дата, МЕСЯЦ) < &КонДата)
	|	И СостоянияОСОрганизаций.Организация = &Организация
	|
	|СГРУППИРОВАТЬ ПО
	|	СостоянияОСОрганизаций.ОсновноеСредство,
	|	СостоянияОСОрганизаций.Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СостоянияОСОрганизаций.ОсновноеСредство КАК ОсновноеСредство
	|ПОМЕСТИТЬ НеСнятыеСУчета
	|ИЗ
	|	ДатыПоследнихСостояний КАК ДатыПоследнихСостояний
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияОСОрганизаций КАК СостоянияОСОрганизаций
	|		ПО ДатыПоследнихСостояний.ОсновноеСредство = СостоянияОСОрганизаций.ОсновноеСредство
	|			И ДатыПоследнихСостояний.Организация = СостоянияОСОрганизаций.Организация
	|			И ДатыПоследнихСостояний.ДатаСостояния = СостоянияОСОрганизаций.ДатаСостояния
	|ГДЕ
	|	СостоянияОСОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 КАК НомерСтроки,
	|	СведенияБУ.ОсновноеСредство КАК ОсновноеСредство
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(
	|			&КонДата,
	|			Организация = &Организация
	|				И ОсновноеСредство В
	|					(ВЫБРАТЬ
	|						НеСнятыеСУчета.ОсновноеСредство
	|					ИЗ
	|						НеСнятыеСУчета)
	|				И ДоговорЛизинга <> ЗНАЧЕНИЕ(Справочник.ДоговорыЛизинга.ПустаяСсылка)) КАК СведенияБУ";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБПВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

#КонецОбласти

#КонецЕсли
