
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Актив", НСтр("ru = 'А'"));
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Пассив", НСтр("ru = 'П'"));
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "АктивПассив", НСтр("ru = 'АП'"));
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ТолькоОбороты", " ("+НСтр("ru = 'об'")+")");
	
	Параметры.Свойство("ТолькоРазрешенные", ТолькоРазрешенные);
	
	Если Параметры.Свойство("ВидыСубконто") Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"Ссылка",
			ПланыСчетов.Международный.СчетаУчетаСВидомСубконто(Параметры.ВидыСубконто),
			ВидСравненияКомпоновкиДанных.ВСписке,,
			Истина);
		
	КонецЕсли;
	
	Элементы.УчетПоНаправлениямДеятельности.Видимость = Справочники.НаправленияДеятельности.ИспользуетсяУчетПоНаправлениям();
	ОтображениеСписком = Параметры.Свойство("ВидыСубконто") ИЛИ Параметры.Отбор.Количество();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ОтображениеСписком Тогда
		Элементы.Список.Отображение = ОтображениеТаблицы.Список;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	Если ТолькоРазрешенные Тогда
		Если ТипЗнч(Значение) = Тип("ПланСчетовСсылка.Международный") Тогда
			СтандартнаяОбработка = НЕ Элемент.ДанныеСтроки(Значение).ЗапретитьИспользоватьВПроводках;
			
		ИначеЕсли ТипЗнч(Значение) = Тип("Массив") Тогда
			Индекс = 0;
			Пока Индекс <= Значение.ВГраница() Цикл
				Счет = Значение[Индекс];
				Если Элемент.ДанныеСтроки(Счет).ЗапретитьИспользоватьВПроводках Тогда
					Значение.Удалить(Индекс);
					Продолжить;
				КонецЕсли;
				Индекс = Индекс + 1;
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;// ТолькоРазрешенные
	
КонецПроцедуры

#КонецОбласти