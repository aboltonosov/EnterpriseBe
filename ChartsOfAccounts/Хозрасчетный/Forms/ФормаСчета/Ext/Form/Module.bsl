#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.Печать
	
	ЗапрещенныйСчет      = ЭтоЗапрещенныйСчет(Объект.Ссылка);
	
	Элементы.ВидыСубконтоДобавить.Доступность = НЕ ЗапрещенныйСчет;
	Элементы.ВидыСубконтоИзменить.Доступность = НЕ ЗапрещенныйСчет;
	Элементы.ВидыСубконтоУдалить.Доступность  = НЕ ЗапрещенныйСчет;
	
	Кнопка = Элементы.ВидыСубконто.КонтекстноеМеню.ПодчиненныеЭлементы.Найти("ВидыСубконтоКонтекстноеМенюДобавить");
	Если Кнопка <> Неопределено Тогда
		Кнопка.Доступность = НЕ ЗапрещенныйСчет;
	КонецЕсли;
	Кнопка = Элементы.ВидыСубконто.КонтекстноеМеню.ПодчиненныеЭлементы.Найти("ВидыСубконтоКонтекстноеМенюИзменить");
	Если Кнопка <> Неопределено Тогда
		Кнопка.Доступность = НЕ ЗапрещенныйСчет;
	КонецЕсли;
	Кнопка = Элементы.ВидыСубконто.КонтекстноеМеню.ПодчиненныеЭлементы.Найти("ВидыСубконтоКонтекстноеМенюУдалить");
	Если Кнопка <> Неопределено Тогда
		Кнопка.Доступность = НЕ ЗапрещенныйСчет;
	КонецЕсли;
	
	Элементы.Родитель.ТолькоПросмотр = Объект.Предопределенный;
	
	Элементы.Вид.Доступность            = НЕ Объект.Предопределенный;
	Элементы.Забалансовый.Доступность   = НЕ Объект.Предопределенный;
	Элементы.Количественный.Доступность = НЕ Объект.Предопределенный;
	Элементы.Валютный.Доступность       = НЕ Объект.Предопределенный;
	Элементы.НалоговыйУчет.Доступность  = НЕ Объект.Предопределенный;
	Элементы.УчетПоПодразделениям.Доступность = НЕ Объект.Предопределенный;
	Элементы.УчетПоНаправлениямДеятельности.Доступность = НЕ Объект.Предопределенный;
	Элементы.УчетПоНаправлениямДеятельности.Видимость   = НЕ Объект.Забалансовый;
	
	Если Параметры.Ключ.Пустая() 
		И Параметры.ЗначениеКопирования.Пустая() Тогда
		Объект.Валютный       = Ложь;
		Объект.Количественный = Ложь;
	КонецЕсли;
	
	Если НЕ Параметры.ЗначениеКопирования.Пустая() ИЛИ Объект.Ссылка.Пустая() Тогда
		Элементы.ИсключитьИзПереоценкиПоПлануСчетов.Видимость = Объект.Валютный;
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Элементы.ИсключитьИзПереоценкиПоПлануСчетов.Видимость = Объект.Валютный;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ОбновитьПовторноИспользуемыеЗначения();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КодПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	// Если задан субсчет, то в его коде должна быть точка
	Код      = Объект.Код;
	Родитель = Объект.Родитель;
	
	Если Найти(Код, ".") > 0 Тогда
		
		//Найдем код родителя, для этого найдем последнюю точку в коде счета
		ПозицияТочки = СтрДлина(Код);
		
		Пока Сред(Код, ПозицияТочки, 1) <> "." Цикл
			
			ПозицияТочки = ПозицияТочки - 1;
			
		КонецЦикла;
		
		КодРодителя    = Лев(Код, ПозицияТочки - 1);
		РодительПоКоду = НайтиРодителя(КодРодителя);
		
		Если НЕ ЗначениеЗаполнено(РодительПоКоду) Тогда
			
			ПоказатьПредупреждение( , СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='План счетов не содержит счета с кодом %1'"),
				КодРодителя));
			
		ИначеЕсли РодительПоКоду <> Объект.Ссылка Тогда
			
			СвойстваТекущегоРодителя = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Объект.Родитель);
			
			Объект.Родитель       = РодительПоКоду;
			Объект.Вид            = СвойстваТекущегоРодителя.Вид;
			Объект.Забалансовый   = СвойстваТекущегоРодителя.Забалансовый;
			Объект.Количественный = СвойстваТекущегоРодителя.Количественный;
			Объект.Валютный       = СвойстваТекущегоРодителя.Валютный;
			
			Элементы.ВидыСубконтоВалютный.Видимость       = Объект.Валютный;
			Элементы.ВидыСубконтоКоличественный.Видимость = Объект.Количественный;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Объект.КодБыстрогоВыбора = СокрЛП(СтрЗаменить(Код, ".", ""));
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура РодительПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ВалютныйПриИзменении(Элемент)
	
	ВалютныйПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура КоличественныйПриИзменении(Элемент)
	
	Элементы.ВидыСубконтоКоличественный.Видимость = Объект.Количественный;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗабалансовыйПриИзменении(Элемент)
	
	Элементы.УчетПоНаправлениямДеятельности.Видимость = НЕ Объект.Забалансовый;
	Если Объект.Забалансовый Тогда
		Объект.УчетПоНаправлениямДеятельности = Ложь;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ВидыСубконтоПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если ЗапрещенныйСчет Тогда
		ПредупреждениеОНевозможностиИзмененияСоставаВидовСубконто(Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидыСубконтоПередНачаломИзменения(Элемент, Отказ)
	
	Если ЗапрещенныйСчет Тогда
		ПредупреждениеОНевозможностиИзмененияСоставаВидовСубконто(Отказ);
	КонецЕсли;
	
	Если Элемент.ТекущиеДанные.Предопределенное Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидыСубконтоПередУдалением(Элемент, Отказ)
	
	Если ЗапрещенныйСчет Тогда
		ПредупреждениеОНевозможностиИзмененияСоставаВидовСубконто(Отказ);
	КонецЕсли;
	
	Если Элемент.ТекущиеДанные.Предопределенное Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидыСубконтоПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.Суммовой       = Истина;
		Элемент.ТекущиеДанные.Валютный       = Истина;
		Элемент.ТекущиеДанные.Количественный = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ БСП

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтаФорма, Объект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьПараметрыСчета(Счет)
	
	ПараметрыСчета = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(Хозрасчетный.УчетПоПодразделениям), ЛОЖЬ) КАК УчетПоПодразделениям,
	|	ЕСТЬNULL(МАКСИМУМ(Хозрасчетный.УчетПоНаправлениямДеятельности), ЛОЖЬ) КАК УчетПоНаправлениямДеятельности,
	|	ЕСТЬNULL(МАКСИМУМ(Хозрасчетный.НалоговыйУчет), ЛОЖЬ) КАК НалоговыйУчет
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Родитель В ИЕРАРХИИ(&Родитель)";
	Запрос.УстановитьПараметр("Родитель", Счет);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		ПараметрыСчета.Вставить("УчетПоПодразделениям", Истина);
		ПараметрыСчета.Вставить("УчетПоНаправлениямДеятельности", Ложь);
		ПараметрыСчета.Вставить("НалоговыйУчет"       , Истина);
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ПараметрыСчета.Вставить("УчетПоПодразделениям", НЕ Выборка.УчетПоПодразделениям);
		ПараметрыСчета.Вставить("УчетПоНаправлениямДеятельности", НЕ Выборка.УчетПоНаправлениямДеятельности);
		ПараметрыСчета.Вставить("НалоговыйУчет"       , НЕ Выборка.НалоговыйУчет);
	КонецЕсли;
	
	Возврат ПараметрыСчета;
	
КонецФункции

&НаСервереБезКонтекста
Функция НайтиРодителя(КодРодителя)
	
	Возврат ПланыСчетов.Хозрасчетный.НайтиПоКоду(КодРодителя);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьЗапрещенныеСчета()
	
	// 4D:ERP для Беларуси, Екатерина, 04.08.2015 16:31:10 
	// Локализация плана счетов, №8969
	// {
	ЗапрещенныеСчета = Новый Массив;
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Хозрасчетный.ОборудованиеКУстановке);              // 07
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Хозрасчетный.ПриобретениеОбъектовОсновныхСредств); // 08.04
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Хозрасчетный.Полуфабрикаты);                       // 10.02
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Хозрасчетный.Товары);                              // 41
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Хозрасчетный.ГотоваяПродукция);                    // 43
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Хозрасчетный.Материалы);                           // 10
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Хозрасчетный.ТоварыОтгруженные);                   // 45
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Хозрасчетный.ТоварыПринятыеНаКомиссию);            // 004
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыСПерсоналомПоОплатеТруда);     // 70
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Хозрасчетный.РасчетыПоДепонированнымСуммам);       // 76.04
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Хозрасчетный.РасходыНаОплатуТрудаБудущихПериодов); // 97.01
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Хозрасчетный.Касса);                               // 50
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Хозрасчетный.КассаОрганизации);                    // 50.01
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Хозрасчетный.ОперационнаяКасса);                   // 50.02
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Хозрасчетный.КассаОрганизацииВал);                 // 50.21
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Хозрасчетный.РасчетныеСчета);                      // 51
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Хозрасчетный.ВалютныеСчета);                       // 52
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Хозрасчетный.СпециальныеСчета);                    // 55
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Хозрасчетный.Аккредитивы);                         // 55.01
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Хозрасчетный.СчетаВДрагоценныхМеталлах);           // 55.02
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Хозрасчетный.ДепозитныеСчета);                     // 55.03
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Хозрасчетный.ПрочиеСпециальныеСчета);              // 55.04
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Хозрасчетный.АккредитивыВал);                      // 55.21
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Хозрасчетный.ДепозитныеСчетаВал);                  // 55.23
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Хозрасчетный.ПрочиеСпециальныеСчетаВал);           // 55.24
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Хозрасчетный.ПереводыВПути);                       // 57.01
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Хозрасчетный.ПереводыВПутиВал);                    // 57.21
	// }
	// 4D
	
	Возврат Новый ФиксированныйМассив(ЗапрещенныеСчета);
	
КонецФункции

&НаКлиенте
Процедура ПредупреждениеОНевозможностиИзмененияСоставаВидовСубконто(Отказ)
	
	ПоказатьПредупреждение( , НСтр("ru = 'Состав видов субконто на этом счете определяется настройкой параметров учета.'"));
	Отказ = Истина;
	
КонецПроцедуры // ПредупреждениеОНевозможностиИзмененияСоставаВидовСубконто()

&НаСервереБезКонтекста
Функция ЭтоЗапрещенныйСчет(Счет)

	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка"          , Счет);
	Запрос.УстановитьПараметр("СписокСчетов"    , ПолучитьЗапрещенныеСчета());
	Запрос.УстановитьПараметр("СписокИсключений", ПланыСчетов.Хозрасчетный.ПолучитьСчетаИсключения());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Ссылка
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Ссылка = &Ссылка
	|	И Хозрасчетный.Ссылка В ИЕРАРХИИ(&СписокСчетов)
	|	И (НЕ Хозрасчетный.Ссылка В ИЕРАРХИИ (&СписокИсключений))";
	ЗапрещенныйСчет = НЕ Запрос.Выполнить().Пустой();
	
	Возврат ЗапрещенныйСчет;

КонецФункции // ЭтоЗапрещенныйСчет()

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Объект   = Форма.Объект;
	Элементы = Форма.Элементы;
	Если НЕ Объект.Предопределенный Тогда
		Если ЗначениеЗаполнено(Объект.Родитель) Тогда
			СвойстваТекущегоРодителя = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Объект.Родитель);
			Элементы.УчетПоПодразделениям.Доступность = СвойстваТекущегоРодителя.УчетПоПодразделениям;
			Элементы.УчетПоНаправлениямДеятельности.Доступность = СвойстваТекущегоРодителя.УчетПоНаправлениямДеятельности;
			Объект.УчетПоНаправлениямДеятельности = СвойстваТекущегоРодителя.УчетПоНаправлениямДеятельности;
			Элементы.НалоговыйУчет.Доступность        = СвойстваТекущегоРодителя.НалоговыйУчет;
		Иначе
			Если НЕ Форма.Параметры.Ключ.Пустая() Тогда
				ПараметрыСчета = ПолучитьПараметрыСчета(Объект.Ссылка);
				Элементы.УчетПоПодразделениям.Доступность = ПараметрыСчета.УчетПоПодразделениям;
				Элементы.УчетПоНаправлениямДеятельности.Доступность = ПараметрыСчета.УчетПоНаправлениямДеятельности;
				Элементы.НалоговыйУчет.Доступность        = ПараметрыСчета.НалоговыйУчет;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // УправлениеФормой()

&НаСервере
Процедура ВалютныйПриИзмененииСервер()
	
	Элементы.ВидыСубконтоВалютный.Видимость = Объект.Валютный;
	Элементы.ИсключитьИзПереоценкиПоПлануСчетов.Видимость = Объект.Валютный;
	
КонецПроцедуры

#КонецОбласти