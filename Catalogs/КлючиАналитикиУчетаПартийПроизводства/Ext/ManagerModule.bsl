#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Процедура устанавливает пометку на удаление для найденных элементов справочника.
//
// Параметры:
//	СтруктураПараметров - Структура - Параметры выбора элементов справочника
//	ПометкаУдаления - Булево - Признак установки пометки на удаление.
//
Процедура УстановитьПометкуУдаления(СтруктураПараметров, ПометкаУдаления) Экспорт

	УстановитьПривилегированныйРежим(Истина);

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Таблица.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.КлючиАналитикиУчетаПартийПроизводства КАК Таблица
	|ГДЕ
	|	Таблица.ПометкаУдаления <> &ПометкаУдаления
	|";
	Запрос = Новый Запрос;
	
	Если СтруктураПараметров.Свойство("Номенклатура") Тогда

		Запрос.УстановитьПараметр("Номенклатура", СтруктураПараметров.Номенклатура);
		ТекстЗапроса = ТекстЗапроса + " И Таблица.Номенклатура = &Номенклатура";

	КонецЕсли;
	
	Если СтруктураПараметров.Свойство("Характеристика") Тогда

		Запрос.УстановитьПараметр("Характеристика", СтруктураПараметров.Характеристика);
		ТекстЗапроса = ТекстЗапроса + " И Таблица.Характеристика = &Характеристика";

	КонецЕсли;
	
	Если СтруктураПараметров.Свойство("Спецификация") Тогда

		Запрос.УстановитьПараметр("Спецификация", СтруктураПараметров.Спецификация);
		ТекстЗапроса = ТекстЗапроса + " И Таблица.Спецификация = &Спецификация";

	КонецЕсли;
	
	Если СтруктураПараметров.Свойство("Назначение") Тогда

		Запрос.УстановитьПараметр("Назначение", СтруктураПараметров.Назначение);
		ТекстЗапроса = ТекстЗапроса + " И Таблица.Назначение = &Назначение";

	КонецЕсли;

	Запрос.УстановитьПараметр("ПометкаУдаления", ПометкаУдаления);

	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл

		Выборка.Ссылка.ПолучитьОбъект().УстановитьПометкуУдаления(ПометкаУдаления);

	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗаменаДублейКлючейАналитики

Процедура ЗаменитьДублиКлючейАналитики() Экспорт
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ДанныеСправочника.Ссылка КАК Ссылка,
	|	ДанныеСправочника.ПометкаУдаления КАК ПометкаУдаления,
	|	Аналитика.КлючАналитики КАК КлючАналитики
	|ИЗ
	|	Справочник.КлючиАналитикиУчетаПартийПроизводства КАК ДанныеСправочника
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПартийПроизводства КАК ДанныеРегистра
	|	ПО
	|		ДанныеСправочника.Ссылка = ДанныеРегистра.КлючАналитики
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПартийПроизводства КАК Аналитика
	|	ПО
	|		ДанныеСправочника.Номенклатура		= Аналитика.Номенклатура
	|		И ДанныеСправочника.Характеристика	= Аналитика.Характеристика
	|		И ДанныеСправочника.Спецификация	= Аналитика.Спецификация
	|		И ДанныеСправочника.Назначение		= Аналитика.Назначение
	|ГДЕ
	|	ДанныеРегистра.КлючАналитики ЕСТЬ NULL
	|");
	
	// Сформируем соответствие ключей аналитики.
	СоответствиеАналитик = Новый Соответствие;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
	
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			СоответствиеАналитик.Вставить(Выборка.Ссылка, Выборка.КлючАналитики);
			
			Если Не Выборка.ПометкаУдаления Тогда
				
				СправочникОбъект = Выборка.Ссылка.ПолучитьОбъект();
				СправочникОбъект.УстановитьПометкуУдаления(Истина, Ложь);
				
			КонецЕсли;

		КонецЦикла;
		
		ОбщегоНазначенияУТ.ЗаменитьСсылки(СоответствиеАналитик);
		
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли