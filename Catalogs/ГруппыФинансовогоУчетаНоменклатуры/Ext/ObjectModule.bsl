#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
//++ НЕ УТ
	СчетУчетаВыручки91 = БухгалтерскийУчетПовтИсп.СчетВИерархии(
							СчетУчетаВыручкиОтПродаж, 
							ПланыСчетов.Хозрасчетный.ПрочиеДоходыИРасходы);
	Если НЕ СчетУчетаВыручки91 Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СтатьяДоходовРегл");
	КонецЕсли;
//-- НЕ УТ
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если Отказ ИЛИ ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	//++ НЕ УТ
	Если ПолучитьФункциональнуюОпцию("ИспользоватьРеглУчет") Тогда
		ВидыСчетаДляОчистки = Новый Массив;
		Если ЗначениеЗаполнено(СчетУчетаНаСкладе) Тогда
			ВидыСчетаДляОчистки.Добавить(Перечисления.ВидыСчетовРеглУчета.НаСкладе);
		КонецЕсли;
		Если ЗначениеЗаполнено(СчетУчетаВыручкиОтПродаж) Тогда
			ВидыСчетаДляОчистки.Добавить(Перечисления.ВидыСчетовРеглУчета.ВыручкаОтПродаж);
			ВидыСчетаДляОчистки.Добавить(Перечисления.ВидыСчетовРеглУчета.ВыручкаОтПродажКомиссионера);
		КонецЕсли;
		Если ЗначениеЗаполнено(СчетУчетаНДСПриПродаже) Тогда
			ВидыСчетаДляОчистки.Добавить(Перечисления.ВидыСчетовРеглУчета.НДСПриПродаже);
			ВидыСчетаДляОчистки.Добавить(Перечисления.ВидыСчетовРеглУчета.НДСПриПродажеКомиссионера);
		КонецЕсли;
		Если ЗначениеЗаполнено(СчетУчетаСебестоимостиПродаж) Тогда
			ВидыСчетаДляОчистки.Добавить(Перечисления.ВидыСчетовРеглУчета.СебестоимостьПродаж);
			ВидыСчетаДляОчистки.Добавить(Перечисления.ВидыСчетовРеглУчета.СебестоимостьПродажКомиссионера);
		КонецЕсли;
		Если ЗначениеЗаполнено(СчетУчетаВПути) Тогда
			ВидыСчетаДляОчистки.Добавить(Перечисления.ВидыСчетовРеглУчета.РеализацияБезПереходаПраваСобственности);
		КонецЕсли;
		Если ЗначениеЗаполнено(СчетУчетаНДСВПути) Тогда
			ВидыСчетаДляОчистки.Добавить(Перечисления.ВидыСчетовРеглУчета.НДСПриОтгрузкеБезПереходаПраваСобственности);
		КонецЕсли;
		Если ЗначениеЗаполнено(СчетУчетаПередачиНаКомиссию) Тогда
			ВидыСчетаДляОчистки.Добавить(Перечисления.ВидыСчетовРеглУчета.ПередачаНаКомиссию);
		КонецЕсли;
		
		Если ВидыСчетаДляОчистки.Количество() Тогда
			РегистрыСведений.СчетаРеглУчетаТребующиеНастройки.ОчиститьПриЗаписиАналитикиУчета(Ссылка, ВидыСчетаДляОчистки);
		КонецЕсли;
	КонецЕсли;
	//-- НЕ УТ
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
