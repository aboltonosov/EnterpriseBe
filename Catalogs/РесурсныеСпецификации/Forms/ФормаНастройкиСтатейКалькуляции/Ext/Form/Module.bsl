// 4D:ERP для Беларуси
// {
// Форма изменена
// }
// 4D

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьРеквизитыФормы(Параметры);
	
	УстановитьВидимостьИДоступность();
	
	Элементы.ФормаОК.Доступность = Не ТолькоПросмотр;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ОповещениеЗакрытия = Новый ОписаниеОповещения("ВопросПередЗакрытием", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		
		ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(ОповещениеЗакрытия, Отказ, ЗавершениеРаботы, ТекстВопроса,
			ТекстПредупреждения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если РежимУчетаВзносовПоСтатьямКалькуляции = 0 Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СтатьяКалькуляцииВзносов");
		МассивНепроверяемыхРеквизитов.Добавить("СтатьяКалькуляцииБелгосстрах");
		// 4D:ERP для Беларуси, АлексейЧ, 05.12.2017 10:07:07 
		// Локализовать настройку "Статьи калькуляции для взносов в фонды"< перенос из 2.1.3>, №15952
		// {
		// Действие не требуется
		// }
		// 4D
		МассивНепроверяемыхРеквизитов.Добавить("СтатьяКалькуляцииФСЗН");
	ИначеЕсли РежимУчетаВзносовПоСтатьямКалькуляции = 1 Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СтатьяКалькуляцииБелгосстрах");
		// 4D:ERP для Беларуси, АлексейЧ, 05.12.2017 10:07:07 
		// Локализовать настройку "Статьи калькуляции для взносов в фонды"< перенос из 2.1.3>, №15952
		// {
		// Действие не требуется
		// }
		// 4D
		МассивНепроверяемыхРеквизитов.Добавить("СтатьяКалькуляцииФСЗН");
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("СтатьяКалькуляцииВзносов");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура УчетаВзносовПоСтатьямКалькуляцииПриИзменении(Элемент)
	
	СтатьяКалькуляцииВзносов = ПредопределенноеЗначение("Справочник.СтатьиКалькуляции.ПустаяСсылка");
	СтатьяКалькуляцииБелгосстрах = ПредопределенноеЗначение("Справочник.СтатьиКалькуляции.ПустаяСсылка");
	// 4D:ERP для Беларуси, АлексейЧ, 05.12.2017 10:07:07 
	// Локализовать настройку "Статьи калькуляции для взносов в фонды"< перенос из 2.1.3>, №15952
	// {
	// Действие не требуется
	// }
	// 4D
	СтатьяКалькуляцииФСЗН = ПредопределенноеЗначение("Справочник.СтатьиКалькуляции.ПустаяСсылка");
	
	УстановитьВидимостьИДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если Модифицированность Тогда
		ЗавершитьРедактирование();
	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Модифицированность = Ложь;
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьРеквизитыФормы(СтруктураПараметров)
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, СтруктураПараметров.СтатьиКалькуляции);
	
	Если Не ЗначениеЗаполнено(СтатьяКалькуляцииБелгосстрах) Тогда
		РежимУчетаВзносовПоСтатьямКалькуляции = 0;
		
	// 4D:ERP для Беларуси, АлексейЧ, 05.12.2017 10:07:07 
	// Локализовать настройку "Статьи калькуляции для взносов в фонды"< перенос из 2.1.3>, №15952
	// {
	ИначеЕсли СтатьяКалькуляцииБелгосстрах = СтатьяКалькуляцииФСЗН Тогда 
	// }
	// 4D
		
		СтатьяКалькуляцииВзносов = СтатьяКалькуляцииБелгосстрах;
		РежимУчетаВзносовПоСтатьямКалькуляции = 1;
		
	Иначе
		РежимУчетаВзносовПоСтатьямКалькуляции = 2;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьИДоступность()
	
	Элементы.СтатьяКалькуляцииВзносов.Доступность = РежимУчетаВзносовПоСтатьямКалькуляции = 1;
	Элементы.ГруппаФонды.Доступность = РежимУчетаВзносовПоСтатьямКалькуляции = 2;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗакрытием(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗавершитьРедактирование();
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьРедактирование()
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Ложь;
	
	Если РежимУчетаВзносовПоСтатьямКалькуляции = 1 Тогда
		СтатьяКалькуляцииБелгосстрах = СтатьяКалькуляцииВзносов;
		СтатьяКалькуляцииФСЗН = СтатьяКалькуляцииВзносов;
		// 4D:ERP для Беларуси, АлексейЧ, 05.12.2017 10:07:07 
		// Локализовать настройку "Статьи калькуляции для взносов в фонды"< перенос из 2.1.3>, №15952
		// {
		// Действие не требуется
		// }
		// 4D
	КонецЕсли;
	
	// 4D:ERP для Беларуси, АлексейЧ, 05.12.2017 10:07:07 
	// Локализовать настройку "Статьи калькуляции для взносов в фонды"< перенос из 2.1.3>, №15952
	// {
	СтатьиКалькуляции = Новый Структура("СтатьяКалькуляцииБелгосстрах, СтатьяКалькуляцииФСЗН");
	// }
	// 4D
	
	ЗаполнитьЗначенияСвойств(СтатьиКалькуляции, ЭтаФорма);
	
	ЗначениеВыбора = Новый Структура;
	ЗначениеВыбора.Вставить("СтатьиКалькуляции", СтатьиКалькуляции);
	
	ОповеститьОВыборе(ЗначениеВыбора);
	
КонецПроцедуры

#КонецОбласти