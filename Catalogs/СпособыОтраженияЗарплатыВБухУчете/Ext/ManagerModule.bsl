#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает признак использования статей расходов устаревшего формата в способе отражения зарплаты.
//	Параметры:
//		Объект - СправочникОбъект.СпособыОтраженияЗарплатыВБухУчете - проверяемый способ отражения
//	Возвращаемое значение:
//		Булево - ИСТИНА, если в способе используются статьи расходов с вариантом распределения На прочие активы.
//
Функция ОтражениеПрочихОпераций(Объект) Экспорт
	
	ЭтоОтражениеПрочихОпераций = Ложь;
	
	МассивСтатей = Новый Массив;
	
	Если ТипЗнч(Объект.СтатьяРасходов) = Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов")
		И ЗначениеЗаполнено(Объект.СтатьяРасходов) Тогда
		МассивСтатей.Добавить(Объект.СтатьяРасходов);
	КонецЕсли;
	
	Если ТипЗнч(Объект.СтатьяРасходовЕНВД) = Тип("ПланВидовХарактеристикСсылка.СтатьиРасходов")
		И ЗначениеЗаполнено(Объект.СтатьяРасходовЕНВД) Тогда
		МассивСтатей.Добавить(Объект.СтатьяРасходовЕНВД);
	КонецЕсли;
	
	ПараметрыСтатей = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивСтатей, "ВариантРаспределенияРасходов, ВидЦенностиНДС");
	
	Для Каждого Статья Из ПараметрыСтатей Цикл
		
		Если Статья.Значение.ВариантРаспределенияРасходов = Перечисления.ВариантыРаспределенияРасходов.НаПрочиеАктивы
			И Статья.Значение.ВидЦенностиНДС = Перечисления.ВидыЦенностей.ПрочиеРаботыИУслуги Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// Возвращает текст запроса для получения параметров способа отражения.
//	Параметры:
//		ИмяВременнойТаблицы - Строка - имя таблицы, если требуется помещения результата во временную таблицу.
//	Возвращаемое значение:
//		Строка - текст запроса.
//
Функция ТекстЗапросаДанныхСпособов(ИмяВременнойТаблицы = "") Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	СписокСпособов.СпособОтражения,
	|	ЛОЖЬ КАК ОблагаетсяЕНВД,
	|	СпособОтраженияЗарплата.СтатьяРасходов КАК СтатьяРасходов,
	|	СпособОтраженияЗарплата.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СпособОтраженияЗарплата.СтатьяРасходов.ВариантРаспределенияРасходов КАК ВариантРаспределения,
	|	СпособОтраженияЗарплата.СтатьяРасходов.ПринятиеКналоговомуУчету КАК ПринятиеКналоговомуУчету,
	|	СпособОтраженияЗарплата.АналитикаРасходов КАК АналитикаРасходов,
	|	СпособОтраженияЗарплата.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	СпособОтраженияЗарплата.СчетУчета КАК СчетУчета,
	|	СпособОтраженияЗарплата.Субконто1 КАК Субконто1,
	|	СпособОтраженияЗарплата.Субконто2 КАК Субконто2,
	|	СпособОтраженияЗарплата.Субконто3 КАК Субконто3,
	
	|	СпособОтраженияЗарплата.СтатьяРасходовВзносов КАК СтатьяРасходовВзносов,
	|	СпособОтраженияЗарплата.СтатьяРасходовВзносов.ВариантРаспределенияРасходов КАК ВариантРаспределенияВзносов,
	|	СпособОтраженияЗарплата.СтатьяРасходовВзносов.ПринятиеКналоговомуУчету КАК ПринятиеКналоговомуУчетуВзносов,
	|	СпособОтраженияЗарплата.АналитикаРасходовВзносов КАК АналитикаРасходовВзносов,
	|	СпособОтраженияЗарплата.АналитикаАктивовПассивовВзносов КАК АналитикаАктивовПассивовВзносов,
	|	СпособОтраженияЗарплата.СчетУчетаВзносов КАК СчетУчетаВзносов,
	|	СпособОтраженияЗарплата.Субконто1Взносов КАК Субконто1Взносов,
	|	СпособОтраженияЗарплата.Субконто2Взносов КАК Субконто2Взносов,
	|	СпособОтраженияЗарплата.Субконто3Взносов КАК Субконто3Взносов
	
	|%ПоместитьВоВременнуюТаблицу%
	|ИЗ
	|	ВтСписокСпособовОтражения КАК СписокСпособов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияЗарплатыВБухУчете КАК СпособОтраженияЗарплата
	|		ПО СписокСпособов.СпособОтражения = СпособОтраженияЗарплата.Ссылка
	|ГДЕ
	|	СписокСпособов.СпособОтражения <> Значение(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокСпособов.СпособОтражения,
	|	ИСТИНА,
	|	СпособОтраженияЗарплата.СтатьяРасходовЕНВД,
	|	СпособОтраженияЗарплата.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СпособОтраженияЗарплата.СтатьяРасходовЕНВД.ВариантРаспределенияРасходов,
	|	СпособОтраженияЗарплата.СтатьяРасходовЕНВД.ПринятиеКналоговомуУчету,
	|	СпособОтраженияЗарплата.АналитикаРасходовЕНВД,
	|	СпособОтраженияЗарплата.АналитикаАктивовПассивовЕНВД,
	|	СпособОтраженияЗарплата.СчетУчетаЕНВД,
	|	СпособОтраженияЗарплата.Субконто1ЕНВД,
	|	СпособОтраженияЗарплата.Субконто2ЕНВД,
	|	СпособОтраженияЗарплата.Субконто3ЕНВД,
	
	|	СпособОтраженияЗарплата.СтатьяРасходовВзносовЕНВД,
	|	СпособОтраженияЗарплата.СтатьяРасходовВзносовЕНВД.ВариантРаспределенияРасходов,
	|	СпособОтраженияЗарплата.СтатьяРасходовВзносовЕНВД.ПринятиеКналоговомуУчету,
	|	СпособОтраженияЗарплата.АналитикаРасходовВзносовЕНВД,
	|	СпособОтраженияЗарплата.АналитикаАктивовПассивовВзносовЕНВД,
	|	СпособОтраженияЗарплата.СчетУчетаВзносовЕНВД,
	|	СпособОтраженияЗарплата.Субконто1ВзносовЕНВД,
	|	СпособОтраженияЗарплата.Субконто2ВзносовЕНВД,
	|	СпособОтраженияЗарплата.Субконто3ВзносовЕНВД
	
	|ИЗ
	|	ВтСписокСпособовОтражения КАК СписокСпособов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияЗарплатыВБухУчете КАК СпособОтраженияЗарплата
	|		ПО СписокСпособов.СпособОтражения = СпособОтраженияЗарплата.Ссылка
	|ГДЕ
	|	СписокСпособов.СпособОтражения <> Значение(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка)";
	
	Если ЗначениеЗаполнено(ИмяВременнойТаблицы) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ПоместитьВоВременнуюТаблицу%", "ПОМЕСТИТЬ " + ИмяВременнойТаблицы);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "%ПоместитьВоВременнуюТаблицу%", "");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает массив блокируемых реквизитов объекта.
//	Возвращаемое значение:
//		Массив - массив реквизитов.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	Результат = Новый Массив;
	
	Результат.Добавить("СтатьяРасходов");
	Результат.Добавить("АналитикаРасходов");
	Результат.Добавить("АналитикаАктивовПассивов");
	Результат.Добавить("РежимУчетаВзносов");
	Результат.Добавить("СтатьяРасходовВзносов");
	Результат.Добавить("АналитикаРасходовВзносов");
	Результат.Добавить("АналитикаАктивовПассивовВзносов");
	Результат.Добавить("ОсобыйСоответствуетОсновному");
	Результат.Добавить("СтатьяРасходовЕНВД");
	Результат.Добавить("АналитикаРасходовЕНВД");
	Результат.Добавить("АналитикаАктивовПассивовЕНВД");
	Результат.Добавить("РежимУчетаВзносовЕНВД");
	Результат.Добавить("СтатьяРасходовВзносовЕНВД");
	Результат.Добавить("АналитикаРасходовВзносовЕНВД");
	Результат.Добавить("АналитикаАктивовПассивовВзносовЕНВД");
	Результат.Добавить("НаправлениеДеятельности");
	
	Возврат Результат;
	
КонецФункции

// Проверяет использование объекта в документах Отражение зарплаты в финансовом учете и Начисление оценочных обязательств.
//	Параметры:
//		Параметры - Структура - структура, содержащая ссылку на проверяемый объект.
//		АдресХранилища - Строка - адрес, по которому следует поместить результат проверки.
//
Процедура ПроверитьИспользованиеОбъекта(Параметры, АдресХранилища) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1
	|	Зарплата.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленнаяЗарплатаИВзносы КАК Зарплата
	|ГДЕ
	|	Зарплата.Ссылка.Проведен
	|	И Зарплата.СпособОтраженияЗарплатыВБухучете = &Объект
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1
	|	Резервы.Ссылка
	|ИЗ
	|	Документ.НачислениеОценочныхОбязательствПоОтпускам.ОценочныеОбязательства КАК Резервы
	|ГДЕ
	|	Резервы.Ссылка.Проведен
	|	И Резервы.СпособОтраженияЗарплатыВБухучете = &Объект");
	
	Запрос.УстановитьПараметр("Объект", Параметры.Объект);
	Результат = Запрос.Выполнить();
	
	ПоместитьВоВременноеХранилище(Не Результат.Пустой(), АдресХранилища);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// Обработчик обновления УП 2.1.3
// В способах отражения зарплаты заполняет статьи расходов для учета взносов по данным неиспользуемых табличных частей.
//
Процедура ЗаполнитьСтатьиРасходовВзносовОбъекта() Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	СпособыОтражения.Ссылка КАК СпособОтражения
	|ИЗ
	|	Справочник.СпособыОтраженияЗарплатыВБухУчете КАК СпособыОтражения
	|ГДЕ
	|	(СпособыОтражения.СтатьяРасходовВзносов = НЕОПРЕДЕЛЕНО
	|			ИЛИ СпособыОтражения.СтатьяРасходовВзносов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			ИЛИ СпособыОтражения.СтатьяРасходовВзносов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиАктивовПассивов.ПустаяСсылка))");
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СпрОбъект = Выборка.СпособОтражения.ПолучитьОбъект();
		
		Если СпрОбъект.РежимУчетаВзносов = 0 Или СпрОбъект.УдалитьВзносы.Количество() = 0 Тогда
			
			СпрОбъект.СтатьяРасходовВзносов = СпрОбъект.СтатьяРасходов;
			СпрОбъект.АналитикаРасходовВзносов = СпрОбъект.АналитикаРасходов;
			СпрОбъект.АналитикаАктивовПассивовВзносов = СпрОбъект.АналитикаАктивовПассивов;
			СпрОбъект.СчетУчетаВзносов = СпрОбъект.СчетУчета;
			СпрОбъект.Субконто1Взносов = СпрОбъект.Субконто1;
			СпрОбъект.Субконто2Взносов = СпрОбъект.Субконто2;
			СпрОбъект.Субконто3Взносов = СпрОбъект.Субконто3;
			
		Иначе
			
			Строка = СпрОбъект.УдалитьВзносы[0];
			
			СпрОбъект.СтатьяРасходовВзносов = Строка.СтатьяРасходов;
			СпрОбъект.АналитикаРасходовВзносов = Строка.АналитикаРасходов;
			СпрОбъект.АналитикаАктивовПассивовВзносов = Строка.АналитикаАктивовПассивов;
			СпрОбъект.СчетУчетаВзносов = Строка.СчетУчета;
			СпрОбъект.Субконто1Взносов = Строка.Субконто1;
			СпрОбъект.Субконто2Взносов = Строка.Субконто2;
			СпрОбъект.Субконто3Взносов = Строка.Субконто3;
			
		КонецЕсли;
		
		Если СпрОбъект.ОсобыйСоответствуетОсновному Тогда
			
			СпрОбъект.СтатьяРасходовВзносовЕНВД = СпрОбъект.СтатьяРасходов;
			СпрОбъект.АналитикаРасходовВзносовЕНВД = СпрОбъект.АналитикаРасходов;
			СпрОбъект.АналитикаАктивовПассивовВзносовЕНВД = СпрОбъект.АналитикаАктивовПассивов;
			СпрОбъект.СчетУчетаВзносовЕНВД = СпрОбъект.СчетУчета;
			СпрОбъект.Субконто1ВзносовЕНВД = СпрОбъект.Субконто1;
			СпрОбъект.Субконто2ВзносовЕНВД = СпрОбъект.Субконто2;
			СпрОбъект.Субконто3ВзносовЕНВД = СпрОбъект.Субконто3;
			
		ИначеЕсли СпрОбъект.РежимУчетаВзносовЕНВД = 0 Или СпрОбъект.УдалитьВзносыЕНВД.Количество() = 0 Тогда
			
			СпрОбъект.СтатьяРасходовВзносовЕНВД = СпрОбъект.СтатьяРасходовЕНВД;
			СпрОбъект.АналитикаРасходовВзносовЕНВД = СпрОбъект.АналитикаРасходовЕНВД;
			СпрОбъект.АналитикаАктивовПассивовВзносовЕНВД = СпрОбъект.АналитикаАктивовПассивовЕНВД;
			СпрОбъект.СчетУчетаВзносовЕНВД = СпрОбъект.СчетУчетаЕНВД;
			СпрОбъект.Субконто1ВзносовЕНВД = СпрОбъект.Субконто1ЕНВД;
			СпрОбъект.Субконто2ВзносовЕНВД = СпрОбъект.Субконто2ЕНВД;
			СпрОбъект.Субконто3ВзносовЕНВД = СпрОбъект.Субконто3ЕНВД;
			
		Иначе
			
			Строка = СпрОбъект.УдалитьВзносыЕНВД[0];
			
			СпрОбъект.СтатьяРасходовВзносовЕНВД = Строка.СтатьяРасходов;
			СпрОбъект.АналитикаРасходовВзносовЕНВД = Строка.АналитикаРасходов;
			СпрОбъект.АналитикаАктивовПассивовВзносовЕНВД = Строка.АналитикаАктивовПассивов;
			СпрОбъект.СчетУчетаВзносовЕНВД = Строка.СчетУчета;
			СпрОбъект.Субконто1ВзносовЕНВД = Строка.Субконто1;
			СпрОбъект.Субконто2ВзносовЕНВД = Строка.Субконто2;
			СпрОбъект.Субконто3ВзносовЕНВД = Строка.Субконто3;
			
		КонецЕсли;
		
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(СпрОбъект);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли