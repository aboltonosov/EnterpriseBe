
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
	СтатьяРасходовЕНВД = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
	
	СтатьяРасходовВзносов = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
	СтатьяРасходовВзносовЕНВД = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	ПодбираемыеАналитики = Новый Массив;
	ПодбираемыеАналитики.Добавить(Тип("СправочникСсылка.ФизическиеЛица"));
	ПодбираемыеАналитики.Добавить(Тип("СправочникСсылка.Организации"));
	ПодбираемыеАналитики.Добавить(Тип("СправочникСсылка.СтруктураПредприятия"));
	ПодбираемыеАналитики.Добавить(Тип("ПеречислениеСсылка.ВидыОперацийПоЗарплате"));
	ПодбираемыеАналитики.Добавить(Тип("СправочникСсылка.ОбъектыЭксплуатации"));
	
	Если ПодбираемыеАналитики.Найти(ТипЗнч(АналитикаРасходов)) = Неопределено Тогда
		ПланыВидовХарактеристик.СтатьиРасходов.ПроверитьЗаполнениеАналитик(
			ЭтотОбъект, "СтатьяРасходов, АналитикаРасходов", МассивНепроверяемыхРеквизитов, Отказ);
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("АналитикаРасходов");
	КонецЕсли;
	
	Если РежимУчетаВзносов = 1 Тогда
		Если ПодбираемыеАналитики.Найти(ТипЗнч(АналитикаРасходовВзносов)) = Неопределено Тогда
			ПланыВидовХарактеристик.СтатьиРасходов.ПроверитьЗаполнениеАналитик(
			ЭтотОбъект, "СтатьяРасходовВзносов, АналитикаРасходовВзносов", МассивНепроверяемыхРеквизитов, Отказ);
		Иначе
			МассивНепроверяемыхРеквизитов.Добавить("АналитикаРасходовВзносов");
		КонецЕсли;
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("СтатьяРасходовВзносов");
		МассивНепроверяемыхРеквизитов.Добавить("АналитикаРасходовВзносов");
	КонецЕсли;
	
	Если Не ОсобыйСоответствуетОсновному Тогда
		
		Если ПодбираемыеАналитики.Найти(ТипЗнч(АналитикаРасходовЕНВД)) = Неопределено Тогда
			ПланыВидовХарактеристик.СтатьиРасходов.ПроверитьЗаполнениеАналитик(
			ЭтотОбъект, "СтатьяРасходовЕНВД, АналитикаРасходовЕНВД", МассивНепроверяемыхРеквизитов, Отказ);
		Иначе
			МассивНепроверяемыхРеквизитов.Добавить("АналитикаРасходовЕНВД");
		КонецЕсли;
		
		Если РежимУчетаВзносовЕНВД = 1 Тогда
			Если ПодбираемыеАналитики.Найти(ТипЗнч(АналитикаРасходовВзносовЕНВД)) = Неопределено Тогда
				ПланыВидовХарактеристик.СтатьиРасходов.ПроверитьЗаполнениеАналитик(
				ЭтотОбъект, "СтатьяРасходовВзносовЕНВД, АналитикаРасходовВзносовЕНВД", МассивНепроверяемыхРеквизитов, Отказ);
			Иначе
				МассивНепроверяемыхРеквизитов.Добавить("АналитикаРасходовВзносовЕНВД");
			КонецЕсли;
		Иначе
			МассивНепроверяемыхРеквизитов.Добавить("СтатьяРасходовВзносовЕНВД");
			МассивНепроверяемыхРеквизитов.Добавить("АналитикаРасходовВзносовЕНВД");
		КонецЕсли;
		
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("СтатьяРасходовЕНВД");
		МассивНепроверяемыхРеквизитов.Добавить("АналитикаРасходовЕНВД");
		МассивНепроверяемыхРеквизитов.Добавить("СтатьяРасходовВзносовЕНВД");
		МассивНепроверяемыхРеквизитов.Добавить("АналитикаРасходовВзносовЕНВД");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(
		ПроверяемыеРеквизиты,
		МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьПрочиеОперации = Справочники.СпособыОтраженияЗарплатыВБухУчете.ОтражениеПрочихОпераций(ЭтотОбъект);
	
	Если СтатьяРасходов = Неопределено Тогда
		СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
	КонецЕсли;
	ОписаниеТипов = Новый ОписаниеТипов(СтатьяРасходов.ТипЗначения);
	АналитикаРасходов = ОписаниеТипов.ПривестиЗначение(АналитикаРасходов);
	АналитикаАктивовПассивов = ОписаниеТипов.ПривестиЗначение(АналитикаАктивовПассивов);
	
	Если СтатьяРасходовЕНВД = Неопределено Тогда
		СтатьяРасходовЕНВД = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
	КонецЕсли;
	ОписаниеТипов = Новый ОписаниеТипов(СтатьяРасходовЕНВД.ТипЗначения);
	АналитикаРасходовЕНВД = ОписаниеТипов.ПривестиЗначение(АналитикаРасходовЕНВД);
	АналитикаАктивовПассивовЕНВД = ОписаниеТипов.ПривестиЗначение(АналитикаАктивовПассивовЕНВД);
	
	Если РежимУчетаВзносов = 0 Тогда
		СтатьяРасходовВзносов = СтатьяРасходов;
		АналитикаРасходовВзносов = АналитикаРасходов;
		АналитикаАктивовПассивовВзносов = АналитикаАктивовПассивов;
		СчетУчетаВзносов = СчетУчета;
		Субконто1Взносов = Субконто1;
		Субконто2Взносов = Субконто2;
		Субконто3Взносов = Субконто3;
	КонецЕсли;
	
	Если ОсобыйСоответствуетОсновному Тогда
		
		СтатьяРасходовЕНВД = СтатьяРасходов;
		АналитикаРасходовЕНВД = АналитикаРасходов;
		АналитикаАктивовПассивовЕНВД = АналитикаАктивовПассивов;
		СчетУчетаЕНВД = СчетУчета;
		Субконто1ЕНВД = Субконто1;
		Субконто2ЕНВД = Субконто2;
		Субконто3ЕНВД = Субконто3;
		
		СтатьяРасходовВзносовЕНВД = СтатьяРасходовВзносов;
		АналитикаРасходовВзносовЕНВД = АналитикаРасходовВзносов;
		АналитикаАктивовПассивовВзносовЕНВД = АналитикаАктивовПассивовВзносов;
		СчетУчетаВзносовЕНВД = СчетУчетаВзносов;
		Субконто1ВзносовЕНВД = Субконто1Взносов;
		Субконто2ВзносовЕНВД = Субконто2Взносов;
		Субконто3ВзносовЕНВД = Субконто3Взносов;
		
	ИначеЕсли РежимУчетаВзносовЕНВД = 0 Тогда
		
		СтатьяРасходовВзносовЕНВД = СтатьяРасходовЕНВД;
		АналитикаРасходовВзносовЕНВД = АналитикаРасходовЕНВД;
		АналитикаАктивовПассивовВзносовЕНВД = АналитикаАктивовПассивовЕНВД;
		СчетУчетаВзносовЕНВД = СчетУчетаЕНВД;
		Субконто1ВзносовЕНВД = Субконто1ЕНВД;
		Субконто2ВзносовЕНВД = Субконто2ЕНВД;
		Субконто3ВзносовЕНВД = Субконто3ЕНВД;
		
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#КонецЕсли
