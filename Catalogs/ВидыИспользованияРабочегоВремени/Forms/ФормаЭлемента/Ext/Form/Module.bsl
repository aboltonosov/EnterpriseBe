
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	УстановитьДоступностьЭлементов();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Если Объект.Ссылка = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыИспользованияРабочегоВремени.РабочееВремя") Тогда
		ТолькоПросмотр = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("Запись_ВидыИспользованияРабочегоВремени", Объект.Ссылка); 
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОсновноеВремяПриИзменении(Элемент)
	УстановитьСвойстваВидаВремениПоОсновномуВремени();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере 
Процедура УстановитьДоступностьЭлементов()
	
	ДоступносьЭлементов = Не Объект.Предопределенный;
	Элементы.ОсновноеВремя.Доступность = ДоступносьЭлементов;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыРасширеннаяПодсистемы") Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ОсновноеВремя",
			"Видимость",
			Истина);
			
		ИмяФОРаботаВХозрасчетнойОрганизации = "РаботаВХозрасчетнойОрганизации";
		ЗначениеФО = ПолучитьФункциональнуюОпцию(ИмяФОРаботаВХозрасчетнойОрганизации);
		Если ЗначениеФО <> Истина Тогда
			
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы,
				"БуквенныйКод",
				"Видимость",
				Ложь);
			
		КонецЕсли;
		
		ИмяФОРаботаВБюджетномУчреждении = "РаботаВБюджетномУчреждении";
		ЗначениеФО = ПолучитьФункциональнуюОпцию(ИмяФОРаботаВБюджетномУчреждении);
		Если ЗначениеФО = Истина Тогда
			
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы,
				"БуквКодБюджетный",
				"Видимость",
				Истина);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваВидаВремениПоОсновномуВремени()
	Если Объект.ОсновноеВремя.Пустая() Тогда
		Объект.РабочееВремя = Ложь;
		Объект.Целосменное = Ложь;
	Иначе
		СвойстваОсновногоВидаВремени = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ОсновноеВремя, "РабочееВремя, Целосменное");
		ЗаполнитьЗначенияСвойств(Объект, СвойстваОсновногоВидаВремени);
	КонецЕсли;		
КонецПроцедуры	

#КонецОбласти
