#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает реквизиты справочника, которые образуют естественный ключ
//  для элементов справочника.
//
// Возвращаемое значение:
//  Массив(Строка) - массив имен реквизитов, образующих естественный ключ.
//
Функция ПоляЕстественногоКлюча() Экспорт
	
	Результат = Новый Массив();
	
	Результат.Добавить("Код");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Обработчик обновления версии 3.0.46. Создает новый корневой элемент в справочнике и помещает в нее 
//  существующие корневые элементы, по формату относящиеся к ОКОФ ОК 013-94.
//
Процедура ОбновитьОКОФ_94() Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ОбщероссийскийКлассификаторОсновныхФондов.Ссылка
	|ИЗ
	|	Справочник.ОбщероссийскийКлассификаторОсновныхФондов КАК ОбщероссийскийКлассификаторОсновныхФондов
	|ГДЕ
	|	ОбщероссийскийКлассификаторОсновныхФондов.Родитель = ЗНАЧЕНИЕ(Справочник.ОбщероссийскийКлассификаторОсновныхФондов.ПустаяСсылка)
	|	И ПОДСТРОКА(ОбщероссийскийКлассификаторОсновныхФондов.Код, 3, 1) = "" ""
	|	И ОбщероссийскийКлассификаторОсновныхФондов.Код ПОДОБНО ""[0-9][0-9]_%""");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	КорневойЭлемент = Справочники.ОбщероссийскийКлассификаторОсновныхФондов.НайтиПоКоду("ОК 013-94");
	Если КорневойЭлемент.Пустая() Тогда
		ЭлементОбъект = Справочники.ОбщероссийскийКлассификаторОсновныхФондов.СоздатьЭлемент();
		ЭлементОбъект.Код = "ОК 013-94";
		ЭлементОбъект.Наименование = "Утвержден Постановлением Госстандарта РФ от 26 декабря 1994 г. N 359";
		ЭлементОбъект.НаименованиеГруппировки = "Утвержден Постановлением Госстандарта РФ от 26 декабря 1994 г. N 359. Срок действия 01.01.1996 - 01.01.2016";
		
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ЭлементОбъект);
		
		КорневойЭлемент = ЭлементОбъект.Ссылка;
	КонецЕсли;
	
	Пока Выборка.Следующий() Цикл
		ЭлементОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ЭлементОбъект.Родитель = КорневойЭлемент;
		
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ЭлементОбъект);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли