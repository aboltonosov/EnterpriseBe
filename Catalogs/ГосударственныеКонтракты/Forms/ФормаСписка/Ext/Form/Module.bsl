
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	УстановитьОтборСписка();
	
	// МенюОтчеты
	МенюОтчеты.ПриСозданииНаСервере(ЭтотОбъект, Элементы.ПодменюОтчеты);
	// Конец МенюОтчеты
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиЭлементовФормы

&НаКлиенте
Процедура ГоловнойИсполнительПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список,
		"ГоловнойИсполнительПредставление",
		ГоловнойИсполнитель,
		ВидСравненияКомпоновкиДанных.Содержит,
		,
		ЗначениеЗаполнено(ГоловнойИсполнитель));
	
КонецПроцедуры

&НаКлиенте
Процедура УполномоченныйБанкПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список,
		"УполномоченныйБанк",
		УполномоченныйБанк,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ЗначениеЗаполнено(УполномоченныйБанк));
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	УстановитьОтборСписка();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборСписка()
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаВыполняются Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "Состояние", Перечисления.СостоянияГосударственныхКонтрактов.Завершен, ВидСравненияКомпоновкиДанных.НеРавно);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "Состояние", Перечисления.СостоянияГосударственныхКонтрактов.Завершен, ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуОтчет(Команда)
	
	АктивныйСписок = Элементы.СписокВыполняются;
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЗавершены Тогда
		АктивныйСписок = Элементы.СписокЗавершены;
	КонецЕсли;
	МенюОтчетыКлиент.ВыполнитьПодключаемуюКомандуОтчет(Команда, ЭтотОбъект, АктивныйСписок);
	
КонецПроцедуры

#КонецОбласти

