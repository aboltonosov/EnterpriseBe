
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ЗначениеКопирования") Тогда
		МодельКопирования = Параметры.ЗначениеКопирования;
	КонецЕсли;


	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	
	Если Объект.Ссылка.Пустая() Тогда
		Объект.НачалоДействия = НачалоГода(ТекущаяДатаСеанса());
	КонецЕсли;
	
	Элементы.БюджетыПоОрганизациям.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций");
	Элементы.БюджетыПоПодразделениям.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьПодразделения");
	
	ЭтоУправлениеПредприятием = ПолучитьФункциональнуюОпцию("УправлениеПредприятием");
	ИспользоватьБюджетныйПроцесс = Ложь;
	//++ НЕ УТКА
	ИспользоватьБюджетныйПроцесс = ПолучитьФункциональнуюОпцию("ИспользоватьБюджетныйПроцесс");
	//-- НЕ УТКА
	
	Элементы.СтраницаБюджетныйПроцесс.Видимость = ЭтоУправлениеПредприятием И ИспользоватьБюджетныйПроцесс;
	
	Если Не ЭтоУправлениеПредприятием Тогда
		Элементы.ОсновнаяПанель.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	КонецЕсли;
	
	УстановитьДоступностьЭлементовФормы();

	АвтоматическиФормироватьЗадачи = Объект.АвтоматическиФормироватьЗадачи;
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

	Если Не Пользователи.ЭтоПолноправныйПользователь(, Истина) Тогда
		Элементы.НадписьРасписание.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОбновитьИнтерфейс();
	
	МодификацияКонфигурацииКлиентПереопределяемый.ПослеЗаписи(ЭтаФорма, ПараметрыЗаписи);
		
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	ТекущийОбъект.АвтоматическиФормироватьЗадачи = АвтоматическиФормироватьЗадачи;
	
	МодификацияКонфигурацииПереопределяемый.ПередЗаписьюНаСервере(ЭтаФорма, Отказ, ТекущийОбъект, ПараметрыЗаписи);

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	МодификацияКонфигурацииПереопределяемый.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	УстановитьДоступностьЭлементовФормы();
	// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	
	МодификацияКонфигурацииПереопределяемый.ПослеЗаписиНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не ЗавершениеРаботы = Истина И ЗначениеЗаполнено(МодельКопирования) 
		И ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Отказ = Истина;
		ТекстВопроса = НСтр("ru='Скопировать виды бюджетов, этапы процессов и лимиты расхода ДС?'");	
		ПоказатьВопрос(Новый ОписаниеОповещения("ВопросКопироватьВидыЭтапыМоделиЗавершение", ЭтотОбъект, Истина), ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ЗавершениеРаботы = Истина И ЗначениеЗаполнено(МодельКопирования) 
		И ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		ТекстВопроса = НСтр("ru='Скопировать виды бюджетов, этапы процессов и лимиты расхода ДС?'");	
		ПоказатьВопрос(Новый ОписаниеОповещения("ВопросКопироватьВидыЭтапыМоделиЗавершение", ЭтотОбъект, Ложь), ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ОбщегоНазначенияУТКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтаФорма,,Новый ОписаниеОповещения("ОповещениеОРазблокировке", ЭтаФорма));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИнтервал(Команда)
	
	ОбщегоНазначенияУТКлиент.РедактироватьПериод(Объект, 
		Новый Структура("ДатаНачала, ДатаОкончания", "НачалоДействия", "КонецДействия"));
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОповещениеОРазблокировке(Результат, ДополнительныеПараметры) Экспорт
	
	УстановитьДоступностьЭлементовФормы();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьЭлементовФормы()
	
	//Механизм блокировки заблокировал реквизиты
	Элементы.ФормироватьЗадачиАвтоматически.ТолькоПросмотр = Элементы.НачалоДействия.ТолькоПросмотр;
	Элементы.ЗапускатьДокументом.ТолькоПросмотр = Элементы.НачалоДействия.ТолькоПросмотр;
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьРасписаниеНажатие(Элемент)
	
	ПараметрыФормы = ПолучитьПараметрыФормыНастройкиРегламентногоЗадания();
	ОткрытьФорму("Обработка.РегламентныеИФоновыеЗадания.Форма.РегламентноеЗадание",
		ПараметрыФормы,ЭтаФорма,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьПараметрыФормыНастройкиРегламентногоЗадания()
	
	ПараметрыФормы = Неопределено;
	
	//++ НЕ УТКА
	
	РегламентноеЗадание = 
		РегламентныеЗаданияСервер.Задание(Метаданные.РегламентныеЗадания.ФормированиеБюджетныхЗадач);
	ПараметрыФормы = Новый Структура("Действие, Идентификатор","Изменить",РегламентноеЗадание.УникальныйИдентификатор);
	
	//-- НЕ УТКА
	
	Возврат ПараметрыФормы;
	
КонецФункции

&НаКлиенте
Процедура ВопросКопироватьВидыЭтапыМоделиЗавершение(РезультатВопроса, ЗакрыватьФорму = Ложь) Экспорт
		
	Если Не РезультатВопроса = КодВозвратаДиалога.Да Тогда
		МодельКопирования = Неопределено;
		Если ЗакрыватьФорму Тогда
			Закрыть();
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	СписокПараметров = Новый Структура(
		"МодельКопирования, МодельБюджетирования",  
		МодельКопирования, Объект.Ссылка);
		
	МодельКопирования = Неопределено;
		
	Оповещение = Новый ОписаниеОповещения("ПриЗакрытииФормыКопирования", ЭтаФорма, ЗакрыватьФорму);
    ОткрытьФорму("Справочник.МоделиБюджетирования.Форма.ФормаКопирования", СписокПараметров, ЭтаФорма, , , , Оповещение);

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытииФормыКопирования(Результат, ЗакрыватьФорму) Экспорт
	
	Если ЗакрыватьФорму Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
