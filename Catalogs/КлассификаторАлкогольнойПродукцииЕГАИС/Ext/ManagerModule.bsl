
#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	ИнтеграцияЕГАИСВызовСервера.ПриПолученииФормыСправочника(
		"КлассификаторАлкогольнойПродукцииЕГАИС",
		ВидФормы,
		Параметры,
		ВыбраннаяФорма,
		ДополнительнаяИнформация,
		СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Рассчитывает сопоставлены ли данные ЕГАИС с данными информационной базы
//
// Параметры:
//  Номенклатура	 - СправочникСсылка.Номенлатура - 
//  Характеристика	 - СправочникСсылка.ХарактеристикиНоменклатуры - 
//  ТаблицаУпаковки	 - ДанныеФормыКоллекция - 
// 
// Возвращаемое значение:
//   - Булево
//
Функция РассчитатьСопоставлено(Номенклатура, Характеристика, ТаблицаУпаковки) Экспорт
	
	УпаковкиСопоставлены = Истина;
	
	Если ГосударственныеИнформационныеСистемыПереопределяемый.ПризнакИспользованияУпаковок(Номенклатура) Тогда
		Если ТаблицаУпаковки.Количество() = 0 Тогда
			УпаковкиСопоставлены = Ложь;
		Иначе
			
			ТипыУпаковки = Метаданные.ОпределяемыеТипы.Упаковка.Тип.Типы();
			Для Каждого ТипУпаковки Из ТипыУпаковки Цикл
				ПустаяСсылкаНаУпаковку = Справочники[Метаданные.НайтиПоТипу(ТипУпаковки).Имя].ПустаяСсылка();
				Если ТаблицаУпаковки.Найти(ПустаяСсылкаНаУпаковку, "Упаковка") <> Неопределено Тогда
					УпаковкиСопоставлены = Ложь;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	
	Если ГосударственныеИнформационныеСистемыПереопределяемый.ПризнакИспользованияХарактеристик(Номенклатура) Тогда
		ХарактеристикиСопоставлены = ЗначениеЗаполнено(Номенклатура) И ЗначениеЗаполнено(Характеристика);
	Иначе
		ХарактеристикиСопоставлены = ЗначениеЗаполнено(Номенклатура);
	КонецЕсли;
	
	Возврат УпаковкиСопоставлены И ХарактеристикиСопоставлены;
	
КонецФункции

#КонецОбласти

#КонецЕсли