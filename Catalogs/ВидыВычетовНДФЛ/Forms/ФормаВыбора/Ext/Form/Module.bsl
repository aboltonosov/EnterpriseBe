// 4D:ERP для Беларуси
// {
// Форма изменена
// }
// 4D

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("СписокОтбора") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
			"Ссылка", Параметры.СписокОтбора, ВидСравненияКомпоновкиДанных.ВСписке, , Истина);
	ИначеЕсли Параметры.Свойство("МесяцНачисления") Или Параметры.Свойство("НалоговыйПериод") Тогда
		ВычетКДоходу = Неопределено;
		Параметры.Отбор.Свойство("ВычетКДоходу", ВычетКДоходу);
		Если ВычетКДоходу <> Истина Тогда
			МесяцНачисления = Неопределено;
			НалоговыйПериод = Неопределено;
			ГруппаВычета = Неопределено;
			Параметры.Свойство("МесяцНачисления", МесяцНачисления);
			Параметры.Свойство("НалоговыйПериод", НалоговыйПериод);
			Параметры.Отбор.Свойство("ГруппаВычета", ГруппаВычета);
			Если МесяцНачисления <> Неопределено И НалоговыйПериод = Неопределено Тогда
				НалоговыйПериод = Год(МесяцНачисления)
			КонецЕсли;
			Если НалоговыйПериод <> Неопределено Тогда
				СписокОтбора = Новый СписокЗначений; 
				ДоступныеВычеты = УчетНДФЛ.ВычетыНалогоплательщика(НалоговыйПериод, ГруппаВычета);
				ЗначенияРеквизитовВычетов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ДоступныеВычеты, "Код");
				Для каждого Вычет Из ДоступныеВычеты Цикл
					СписокОтбора.Добавить(Вычет, СокрЛП(ЗначенияРеквизитовВычетов.Получить(Вычет).Код));
				КонецЦикла;
				ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
					"Ссылка", СписокОтбора, ВидСравненияКомпоновкиДанных.ВСписке, , Истина);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ЗарплатаКадры.ПриСозданииНаСервереФормыСДинамическимСписком(ЭтотОбъект, "Список");
	
	// 4D:ERP для Беларуси, Юлия, 18.08.2017 14:59:50 
	// Редактировать справочник "Виды вычетов подоходного налога", № 16279 
	// {	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Наименование", "(не используется)", ВидСравненияКомпоновкиДанных.НеНачинаетсяС,,);
	// }
	// 4D
	
КонецПроцедуры

#Область ОбработчикиСобытийТаблицыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки)
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, Настройки);
КонецПроцедуры

&НаСервере
Процедура СписокПриОбновленииСоставаПользовательскихНастроекНаСервере(СтандартнаяОбработка)
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, , СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти
