//++ НЕ УТ
#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	ЗарплатаКадрыВызовСервера.ПодготовитьДанныеВыбораКлассификаторовСПорядкомРеквизитаДопУпорядочивания(ДанныеВыбора, Параметры, СтандартнаяОбработка, "Справочник.ВидыДокументовФизическихЛиц");
	
КонецПроцедуры

#КонецОбласти

// 4D:ERP для Беларуси, Яна, 15.06.2017 15:28:56 
// Коды элементов справочника "Виды документов физических лиц", №15167 
// {
#Область СлужебныеПроцедурыИФункции

Процедура НачальноеЗаполнение() Экспорт
	
	ЗаполнитьВидыДокументовФизическихЛицРБ();
	
КонецПроцедуры

// Выполняет заполнение справочника "ВидыДокументовФизическихЛиц".
//
Процедура ЗаполнитьВидыДокументовФизическихЛицРБ()
	
	// Заполнение реквизитов предопределенных элементов.

	ОписатьВидДокументаФизическихЛиц("СвидетельствоОРождении", "04", "Свидетельство о рождении (для граждан Республики Беларусь, не достигших шестнадцатилетнего возраста и не имеющих паспортов граждан Республики Беларусь");
	
	ОписатьВидДокументаФизическихЛиц("ПаспортРФ", "01", "Паспорт гражданина Республики Беларусь");
	
	ОписатьВидДокументаФизическихЛиц("ПаспортИностранногоГражданина", "03", "Национальный паспорт иностранного гражданина");
	
	ОписатьВидДокументаФизическихЛиц("УдостоверениеБеженца", "08", "Удостоверение беженца");

	ОписатьВидДокументаФизическихЛиц("ВидНаЖительство", "09", "Вид на жительство в Республике Беларусь");
	
	ОписатьВидДокументаФизическихЛиц("ИнойДокумент", "99", "Иной документ, удостоверяющий личность");
	
КонецПроцедуры

Процедура ОписатьВидДокументаФизическихЛиц(СсылкаИлиИмяПредопределенныхДанных = Неопределено,  Код = "", Наименование = "")

	Если ТипЗнч(СсылкаИлиИмяПредопределенныхДанных) = Тип("Строка") И Не ПустаяСтрока(СсылкаИлиИмяПредопределенныхДанных) Тогда
		
		СсылкаПредопределенного = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыДокументовФизическихЛиц." + СсылкаИлиИмяПредопределенныхДанных);
		Если ЗначениеЗаполнено(СсылкаПредопределенного) Тогда
			Элемент = СсылкаПредопределенного.ПолучитьОбъект();
		Иначе
			Элемент = Справочники.ВидыДокументовФизическихЛиц.СоздатьЭлемент();
			Элемент.ИмяПредопределенныхДанных = СсылкаИлиИмяПредопределенныхДанных;
		КонецЕсли;
		
	Иначе
		
		Если ЗначениеЗаполнено(СсылкаИлиИмяПредопределенныхДанных) Тогда
			СсылкаНаЭлемент = СсылкаИлиИмяПредопределенныхДанных;
		Иначе
			СсылкаНаЭлемент = Справочники.ВидыДокументовФизическихЛиц.НайтиПоРеквизиту("КодМВД", Код);
		КонецЕсли; 
		
		Если ЗначениеЗаполнено(СсылкаНаЭлемент) Тогда
			Элемент = СсылкаНаЭлемент.ПолучитьОбъект();
		Иначе
			Элемент = Справочники.ВидыДокументовФизическихЛиц.СоздатьЭлемент();
		КонецЕсли;
		
	КонецЕсли;

	Если ЗначениеЗаполнено(Код) Тогда
		Элемент.КодМВД = Код;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Наименование) Тогда
		Элемент.Наименование = Наименование;
	КонецЕсли;

	Элемент.ОбменДанными.Загрузка = Истина;
	Элемент.Записать();

КонецПроцедуры	

#КонецОбласти
//-- НЕ УТ
