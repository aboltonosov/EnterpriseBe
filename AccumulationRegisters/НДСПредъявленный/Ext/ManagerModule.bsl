#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

Функция ПолноеИмяРегистра()
	
	Возврат "РегистрНакопления.НДСПредъявленный";
	
КонецФункции

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НДСПредъявленный.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ВТ_ОбработанныеДокументы
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный КАК НДСПредъявленный
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Подзапрос.Регистратор КАК Регистратор
	|ИЗ
	|	(ВЫБРАТЬ
	|		Операция.Ссылка КАК Регистратор
	|	ИЗ
	|		Документ.ВводОстатков КАК Операция
	|	ГДЕ
	|		Операция.Проведен
	|		И Операция.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|		И Операция.ТипОперации В (ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиСобственныхТоваров))
	|		И Операция.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПоФактическомуИспользованию)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Операция.Ссылка
	|	ИЗ
	|		Документ.ВозвратТоваровМеждуОрганизациями КАК Операция
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровМеждуОрганизациями.ВидыЗапасов КАК Строки
	|			ПО Операция.Ссылка = Строки.Ссылка
	|	ГДЕ
	|		Операция.Проведен
	|		И Операция.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|		И Операция.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|		И Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровМеждуОрганизациями)
	|		И Строки.ВидЗапасовПолучателя.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Операция.Ссылка
	|	ИЗ
	|		Документ.ВозвратТоваровОтКлиента КАК Операция
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтКлиента.ВидыЗапасов КАК Строки
	|			ПО Операция.Ссылка = Строки.Ссылка
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|			ПО (Строки.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики)
	|	ГДЕ
	|		Операция.Проведен
	|		И Операция.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|		И Операция.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|		И Строки.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|		И Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратТоваровОтКлиента)
	|		И ВЫБОР
	|				КОГДА Операция.ВозвратПереданнойМногооборотнойТары
	|					ТОГДА Аналитика.Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Операция.Ссылка
	|	ИЗ
	|		Документ.ЗаявлениеОВвозеТоваров КАК Операция
	|	ГДЕ
	|		Операция.Проведен
	|		И Операция.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		Операция.Ссылка
	|	ИЗ
	|		Документ.ОтчетКомиссионера КАК Операция
	|	ГДЕ
	|		Операция.Проведен
	|		И Операция.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|		И Операция.СтавкаНДСВознаграждения <> 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Операция.Ссылка
	|	ИЗ
	|		Документ.ОтчетПоКомиссииМеждуОрганизациями КАК Операция
	|	ГДЕ
	|		Операция.Проведен
	|		И Операция.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|		И Операция.СтавкаНДСВознаграждения <> 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Операция.Ссылка
	|	ИЗ
	|		Документ.ПередачаТоваровМеждуОрганизациями КАК Операция
	|	ГДЕ
	|		Операция.Проведен
	|		И Операция.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|		И Операция.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|		И Операция.ПередачаПодДеятельность В (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС),
	|											  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт),
	|											  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг),
	|											  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров),
	|											  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию),
	|											  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПоФактическомуИспользованию))
	|		И Операция.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияТоваровВДругуюОрганизацию)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Операция.Ссылка
	|	ИЗ
	|		Документ.ПоступлениеТоваровУслуг КАК Операция
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровУслуг.Товары КАК Строки
	|			ПО (Строки.Ссылка = Операция.Ссылка)
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК КлючиАналитикаУчетаПартий
	|			ПО (Строки.УдалитьАналитикаУчетаПартий = КлючиАналитикаУчетаПартий.КлючАналитики)
	|	ГДЕ
	|		Операция.Проведен
	|		И Операция.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|		И КлючиАналитикаУчетаПартий.НалогообложениеНДС В (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС),
	|														  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт),
	|														  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг),
	|														  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров),
	|														  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПоФактическомуиспользованию),
	|														  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию))
	|		И Операция.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика),
	|											ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаРеглУчет),
	|											ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо))
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Операция.Ссылка
	|	ИЗ
	|		Документ.КорректировкаПоступления КАК Операция
	|	ГДЕ
	|		Операция.Проведен
	|		И Операция.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|		И Операция.ДокументОснование.ЗакупкаПодДеятельность В (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС),
	|														       ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт),
	|														       ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг),
	|														       ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров),
	|														       ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПоФактическомуиспользованию),
	|														       ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию))
	|
	//++ НЕ УТ
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Операция.Ссылка
	|	ИЗ
	|		Документ.ОтчетПереработчика КАК Операция
	|	ГДЕ
	|		Операция.Проведен
	|		И Операция.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|		И Операция.ЗакупкаПодДеятельность В (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС),
	|											 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт),
	|											 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг),
	|											 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров),
	|											 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПоФактическомуИспользованию),
	|											 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Операция.Ссылка
	|	ИЗ
	|		Документ.ПоступлениеУслугПоЛизингу КАК Операция
	|	ГДЕ
	|		Операция.Проведен
	|		И Операция.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|		И Операция.ЗакупкаПодДеятельность В (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС),
	|											 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт),
	|											 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг),
	|											 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров),
	|											 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПоФактическомуиспользованию),
	|											 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию))
	//-- НЕ УТ
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Операция.Ссылка
	|	ИЗ
	|		Документ.СчетФактураВыданный КАК Операция
	|	ГДЕ
	|		Операция.Проведен
	|		И Операция.Контрагент ССЫЛКА Справочник.Организации
	|		И Операция.Контрагент <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|		И Операция.Исправление
	|		И Операция.ДатаВыставления <> ДАТАВРЕМЯ(1,1,1)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Операция.Ссылка
	|	ИЗ
	|		Документ.СчетФактураНалоговыйАгент КАК Операция
	|	ГДЕ
	|		Операция.Проведен
	|		И Операция.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Операция.Ссылка
	|	ИЗ
	|		Документ.ТаможеннаяДекларацияИмпорт КАК Операция
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			Документ.ТаможеннаяДекларацияИмпорт.Разделы КАК РазделыДокумента
	|		ПО
	|			РазделыДокумента.Ссылка = Операция.Ссылка
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			Документ.ТаможеннаяДекларацияИмпорт.Товары КАК ТоварыДокумента
	|		ПО
	|			РазделыДокумента.Ссылка = ТоварыДокумента.Ссылка
	|			И РазделыДокумента.НомерРаздела = ТоварыДокумента.НомерРаздела
	|	
	|	ГДЕ
	|		Операция.Проведен
	|		И Операция.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|		И Операция.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыТаможенныхДеклараций.ВыпущеноСТаможни)
	|		И ТоварыДокумента.ЗакупкаПодДеятельность В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПоФактическомуИспользованию),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Операция.Ссылка
	|	ИЗ
	|		Документ.ПоступлениеУслугПрочихАктивов КАК Операция
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеУслугПрочихАктивов.Расходы КАК Строки
	|			ПО (Строки.Ссылка = Операция.Ссылка)
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК КлючиАналитикаУчетаПартий
	|			ПО (Строки.УдалитьАналитикаУчетаПартий = КлючиАналитикаУчетаПартий.КлючАналитики)
	|	ГДЕ
	|		Операция.Проведен
	|		И Операция.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|		И КлючиАналитикаУчетаПартий.НалогообложениеНДС В (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС),
	|														  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт),
	|														  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг),
	|														  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров),
	|														  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПоФактическомуиспользованию),
	|														  ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию))
	|		И Операция.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика),
	|											ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаЧерезПодотчетноеЛицо))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Партии.Регистратор
	|	ИЗ
	|		РегистрНакопления.ПартииТоваровОрганизаций КАК Партии
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикаПартий
	|			ПО (АналитикаПартий.КлючАналитики = Партии.АналитикаУчетаПартий)
	|	ГДЕ
	|		Партии.Активность
	|		И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Партии.НДСРегл <> 0
	|		И Партии.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|		И Партии.НалогообложениеНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|		И НЕ АналитикаУчетаПартий.НалогообложениеНДС ЕСТЬ NULL
	|		И ВЫБОР
	|			КОГДА Партии.АналитикаУчетаПартий.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|					ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|				ИНАЧЕ Партии.АналитикаУчетаПартий.НалогообложениеНДС
	|			КОНЕЦ <> ВЫБОР
	|				КОГДА Партии.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|					ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|				ИНАЧЕ Партии.НалогообложениеНДС
	|			КОНЕЦ
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НДСЗаписиКнигиПродаж.Регистратор
	|	ИЗ
	|		РегистрНакопления.НДСЗаписиКнигиПродаж КАК НДСЗаписиКнигиПродаж
	|	ГДЕ
	|		НДСЗаписиКнигиПродаж.Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.ВосстановлениеНДС)
	|		И НЕ(НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|					ИЛИ НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.КорректировкаРеализации
	|					ИЛИ НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.ЗаписьКнигиПродаж
	//++ НЕ УТ
	|					ИЛИ НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.ОперацияБух
	//-- НЕ УТ
	|					ИЛИ НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.СчетФактураПолученный
	|					ИЛИ НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.СчетФактураНалоговыйАгент
	|					ИЛИ НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.СчетФактураВыданныйАванс
	|					ИЛИ НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.СчетФактураКомиссионеру
	|					ИЛИ НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.СчетФактураНаНеподтвержденнуюРеализацию0
	|					ИЛИ НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.СчетФактураПолученныйАванс)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Партии.Регистратор
	|	ИЗ
	|		РегистрНакопления.ПартииЗатратНаВыпуск КАК Партии
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикаПартий
	|			ПО (АналитикаПартий.КлючАналитики = Партии.АналитикаУчетаПартий)
	|	ГДЕ
	|		Партии.Активность
	|		И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Партии.НДСРегл <> 0
	|		И ((Партии.НалогообложениеНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|				И НЕ АналитикаУчетаПартий.НалогообложениеНДС ЕСТЬ NULL
	|				И ВЫБОР
	|					КОГДА Партии.АналитикаУчетаПартий.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|					ИНАЧЕ Партии.АналитикаУчетаПартий.НалогообложениеНДС
	|				КОНЕЦ <> 
	|					ВЫБОР
	|						КОГДА Партии.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|							ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|					ИНАЧЕ Партии.НалогообложениеНДС
	|					КОНЕЦ)
	|			ИЛИ Партии.НалогообложениеНДС В (
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт),
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг),
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров)))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Партии.Регистратор
	|	ИЗ
	|		РегистрНакопления.ПартииРасходовНаСебестоимостьТоваров КАК Партии
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикаПартий
	|			ПО (АналитикаПартий.КлючАналитики = Партии.АналитикаУчетаПартий)
	|	ГДЕ
	|		Партии.Активность
	|		И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Партии.НДСРегл <> 0
	|		И ((Партии.НалогообложениеНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|				И НЕ АналитикаУчетаПартий.НалогообложениеНДС ЕСТЬ NULL
	|				И ВЫБОР
	|					КОГДА Партии.АналитикаУчетаПартий.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|					ИНАЧЕ Партии.АналитикаУчетаПартий.НалогообложениеНДС
	|				КОНЕЦ <> 
	|					ВЫБОР
	|						КОГДА Партии.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|							ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|					ИНАЧЕ Партии.НалогообложениеНДС
	|					КОНЕЦ)
	|			ИЛИ Партии.НалогообложениеНДС В (
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт),
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг),
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров)))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Партии.Регистратор
	|	ИЗ
	|		РегистрНакопления.ПартииПрочихРасходов КАК Партии
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|			ПО Партии.СтатьяРасходов = СтатьиРасходов.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиАналитикаУчетаНоменклатуры
	|			ПО Партии.АналитикаУчетаНоменклатуры = КлючиАналитикаУчетаНоменклатуры.КлючАналитики
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|			ПО (КлючиАналитикаУчетаНоменклатуры.Номенклатура = СпрНоменклатура.Ссылка)
	|	ГДЕ
	|		Партии.Активность
	|		И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Партии.НДСРегл <> 0
	|		И ((Партии.НалогообложениеНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|				И НЕ АналитикаУчетаПартий.НалогообложениеНДС ЕСТЬ NULL
	|				И ВЫБОР
	|					КОГДА Партии.АналитикаУчетаПартий.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|					ИНАЧЕ Партии.АналитикаУчетаПартий.НалогообложениеНДС
	|				КОНЕЦ <> 
	|					ВЫБОР
	|						КОГДА Партии.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|							ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|					ИНАЧЕ Партии.НалогообложениеНДС
	|					КОНЕЦ)
	|			ИЛИ Партии.НалогообложениеНДС В (
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт),
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг),
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров)))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Партии.Регистратор
	|	ИЗ
	|		РегистрНакопления.ПартииПроизводственныхЗатрат КАК Партии
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикаПартий
	|			ПО (АналитикаПартий.КлючАналитики = Партии.АналитикаУчетаПартий)
	|	ГДЕ
	|		Партии.Активность
	|		И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Партии.НДСРегл <> 0
	|		И Партии.НалогообложениеНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|		И НЕ АналитикаУчетаПартий.НалогообложениеНДС ЕСТЬ NULL
	|		И ((Партии.НалогообложениеНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|				И НЕ АналитикаУчетаПартий.НалогообложениеНДС ЕСТЬ NULL
	|				И ВЫБОР
	|					КОГДА Партии.АналитикаУчетаПартий.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|					ИНАЧЕ Партии.АналитикаУчетаПартий.НалогообложениеНДС
	|				КОНЕЦ <> 
	|					ВЫБОР
	|						КОГДА Партии.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|							ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|					ИНАЧЕ Партии.НалогообложениеНДС
	|					КОНЕЦ)
	|			ИЛИ Партии.НалогообложениеНДС В (
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт),
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг),
	|					ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров)))
	//++ НЕ УТКА
	|		И НЕ Партии.Регистратор ССЫЛКА Документ.МаршрутныйЛистПроизводства
	//-- НЕ УТКА
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НДСЗаписиКнигиПокупок.Регистратор
	|	ИЗ
	|		РегистрНакопления.НДСЗаписиКнигиПокупок КАК НДСЗаписиКнигиПокупок
	|	ГДЕ
	|		НЕ (НДСЗаписиКнигиПокупок.Регистратор ССЫЛКА Документ.СчетФактураВыданныйАванс
	//++ НЕ УТ
	|			 ИЛИ НДСЗаписиКнигиПокупок.Регистратор ССЫЛКА Документ.ОперацияБух
	//-- НЕ УТ
	|			 ИЛИ НДСЗаписиКнигиПокупок.Регистратор ССЫЛКА Документ.СчетФактураПолученныйАванс
	|			 ИЛИ НДСЗаписиКнигиПокупок.Регистратор ССЫЛКА Документ.ЗаписьКнигиПокупок)
	|		И ВЫБОР
	|			КОГДА НДСЗаписиКнигиПокупок.Регистратор ССЫЛКА Документ.СчетФактураВыданный
	|				ТОГДА НДСЗаписиКнигиПокупок.Поставщик ССЫЛКА Справочник.Организации
	|						И НДСЗаписиКнигиПокупок.Поставщик <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ) КАК Подзапрос
	|ГДЕ
	|	НЕ Подзапрос.Регистратор В
	|				(ВЫБРАТЬ
	|					ВТ_ОбработанныеДокументы.Регистратор
	|				ИЗ
	|					ВТ_ОбработанныеДокументы)";
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоДвижения = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = ПолноеИмяРегистра();
	
	СписокРегистраторов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, СписокРегистраторов, ДополнительныеПараметры);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСПредъявленный.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный КАК НДСПредъявленный
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(НДСПредъявленный.Регистратор) В (
	|					ТИП(Документ.СчетФактураПолученный),
	|					ТИП(Документ.СчетФактураВыданный))
	|	И НЕ НДСПредъявленный.РегламентнаяОперация
	|";
	
	СписокРегистраторов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, СписокРегистраторов, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	// 1. Проведение документов, формирующих только on-line движения по НДСПредъявленный
	Регистраторы = Новый Массив;
	Регистраторы.Добавить("Документ.ВводОстатков");
	Регистраторы.Добавить("Документ.ВозвратТоваровМеждуОрганизациями");
	Регистраторы.Добавить("Документ.ВозвратТоваровОтКлиента");
	Регистраторы.Добавить("Документ.ОтчетКомиссионера");
	//++ НЕ УТ
	Регистраторы.Добавить("Документ.ОтчетПереработчика");
	Регистраторы.Добавить("Документ.ПоступлениеУслугПоЛизингу");
	//-- НЕ УТ
	Регистраторы.Добавить("Документ.ОтчетПоКомиссииМеждуОрганизациями");
	Регистраторы.Добавить("Документ.СчетФактураНалоговыйАгент");
	
	ОбновлениеИнформационнойБазыУТ.ПерезаписатьДвиженияИзОчереди(Регистраторы, ПолноеИмяРегистра(), Параметры.Очередь);
	
	// 2. Формирование движений документов, формирующих on/off-line движения по НДСПредъявленный
	//    в части on-line-движений сохраняем в промежуточную ТЗ
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ВыборкаПоРегистраторам = ОбновлениеИнформационнойБазы.ВыбратьРегистраторыРегистраДляОбработки(Параметры.Очередь,
																							Неопределено,
																							ПолноеИмяРегистра(),
																							ДополнительныеПараметры);
	ДокументыКОбработке = Новый Массив;
	ДополнитьМассивДОкументовКОбработке(ДокументыКОбработке, Параметры.Очередь, ВыборкаПоРегистраторам);
	
	ДвиженияНДСПредъявленный = ПодготовитьПустуюТаблицуДвиженийНДСПредъявленный();
	СформироватьДвиженияДокументовИзОчереди(ДвиженияНДСПредъявленный, ДокументыКОбработке);

	// 3. Движения off-line
	СформироватьДвиженияДокументовОффЛайн(ДвиженияНДСПредъявленный, ДокументыКОбработке);
	
	// 4. Запись
	ЗаписатьДвиженияДокументовИзОчереди(ДвиженияНДСПредъявленный, Параметры.Очередь, ДокументыКОбработке);
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяРегистра());
	

КонецПроцедуры

Процедура СформироватьДвиженияДокументовИзОчереди(ДвиженияНДСПредъявленный, ДокументыКОбработке)
	
	ИмяРегистра = "НДСПредъявленный";
	ПолноеИмяРегистра = ПолноеИмяРегистра();
	
	Для Каждого Документ Из ДокументыКОбработке Цикл
		
		Если ТипЗнч(Документ) <> Тип("ДокументСсылка.ЗаявлениеОВвозеТоваров")
			И ТипЗнч(Документ) <> Тип("ДокументСсылка.ТаможеннаяДекларацияИмпорт")
			И ТипЗнч(Документ) <> Тип("ДокументСсылка.ПоступлениеТоваровУслуг")
			И ТипЗнч(Документ) <> Тип("ДокументСсылка.КорректировкаПоступления")
			И ТипЗнч(Документ) <> Тип("ДокументСсылка.ПоступлениеУслугПрочихАктивов")
			И ТипЗнч(Документ) <> Тип("ДокументСсылка.ПередачаТоваровМеждуОрганизациями") Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		МенеджерДокумента = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Документ);
		
		Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "Проведен") Тогда
			
			ДопСвойства = Новый Структура;
			ПроведениеСерверУТ.ИнициализироватьДополнительныеСвойстваДляПроведения(Документ, ДопСвойства);
			МенеджерДокумента.ИнициализироватьДанныеДокумента(Документ, ДопСвойства, ИмяРегистра);
			
			ДвиженияДокумента = ДопСвойства.ТаблицыДляДвижений.ТаблицаНДСПредъявленный;
			Если ДвиженияДокумента.Количество() = 0 Тогда
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Документ);
			КонецЕсли;
			Если ДвиженияДокумента.Колонки.Найти("Регистратор") = Неопределено Тогда
				ДвиженияДокумента.Колонки.Добавить("Регистратор");	
			КонецЕсли;
			ДвиженияДокумента.ЗаполнитьЗначения(Документ, "Регистратор");
			
			ОбщегоНазначенияУТ.ДобавитьСтрокиВТаблицу(ДвиженияНДСПредъявленный, ДвиженияДокумента);
			
			Если ТипЗнч(Документ) = Тип("ДокументСсылка.КорректировкаПоступления") Тогда
				
				Для каждого Строка Из ДвиженияДокумента Цикл
					
					// Сторнируем движения, чтобы не было незакрытого сальдо в регистре.
					// Движения отметим признаком РегламентнаяОперация = Истина.
					// Если будет инициирован перерасчет, то такие движения будут удалены.
					НоваяСтрока = ДвиженияНДСПредъявленный.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
					НоваяСтрока.СуммаБезНДС = -НоваяСтрока.СуммаБезНДС;
					НоваяСтрока.НДС         = -НоваяСтрока.НДС;
					НоваяСтрока.РегламентнаяОперация = Истина;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура СформироватьДвиженияДокументовОффЛайн(ДвиженияНДСПредъявленный, ДокументыКОбработке)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупок.Регистратор КАК Регистратор,
	|	НДСЗаписиКнигиПокупок.Период КАК Период,
	|	НДСЗаписиКнигиПокупок.Организация КАК Организация,
	|	НДСЗаписиКнигиПокупок.СчетФактура КАК СчетФактура,
	|	НДСЗаписиКнигиПокупок.Поставщик КАК Поставщик,
	|	НДСЗаписиКнигиПокупок.ВидЦенности КАК ВидЦенности,
	|	НДСЗаписиКнигиПокупок.СтавкаНДС КАК СтавкаНДС
	|ПОМЕСТИТЬ НДСЗаписиКнигиПокупокЭкспорт
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок КАК НДСЗаписиКнигиПокупок
	|ГДЕ
	|	НДСЗаписиКнигиПокупок.Событие В (ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПодтвержденаСтавка0), 
	|									 ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.НеПодтвержденаСтавка0),
	|									 ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету0))
	|	И НДСЗаписиКнигиПокупок.Регистратор В (&ДокументыКОбработке)
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСЗаписиКнигиПокупок.Регистратор,
	|	НДСЗаписиКнигиПокупок.Период,
	|	НДСЗаписиКнигиПокупок.Организация,
	|	НДСЗаписиКнигиПокупок.СчетФактура,
	|	НДСЗаписиКнигиПокупок.Поставщик,
	|	НДСЗаписиКнигиПокупок.ВидЦенности,
	|	НДСЗаписиКнигиПокупок.СтавкаНДС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НДСЗаписиКнигиПокупок.СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НДССостояниеРеализации0.ДокументРеализации КАК Ссылка
	|ПОМЕСТИТЬ РеализацииЭкспорт
	|ИЗ
	|	НДСЗаписиКнигиПокупокЭкспорт КАК НДСЗаписиКнигиПокупок
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.НДССостояниеРеализации0 КАК НДССостояниеРеализации0
	|	ПО 
	|		(КОНЕЦПЕРИОДА(НДССостояниеРеализации0.ДатаПодтверждения, КВАРТАЛ) = КОНЕЦПЕРИОДА(НДСЗаписиКнигиПокупок.Период, КВАРТАЛ))
	|		И (НДССостояниеРеализации0.Состояние В 
	|					(ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.ПодтвержденаРеализация0),
	|					 ЗНАЧЕНИЕ(Перечисление.НДССостоянияРеализация0.НЕПодтвержденаРеализация0)))
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументРеализации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродаж.Регистратор,
	|	НДСЗаписиКнигиПродаж.Период,
	|	НДСЗаписиКнигиПродаж.Организация,
	|	НДСЗаписиКнигиПродаж.СчетФактура,
	|	НДСЗаписиКнигиПродаж.Покупатель,
	|	НДСЗаписиКнигиПродаж.ВидЦенности,
	|	НДСЗаписиКнигиПродаж.СтавкаНДС,
	|	СУММА(НДСЗаписиКнигиПродаж.СуммаБезНДС) КАК СуммаБезНДС,
	|	СУММА(НДСЗаписиКнигиПродаж.НДС) КАК НДС
	|ПОМЕСТИТЬ НДСЗаписиКнигиПродаж
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж КАК НДСЗаписиКнигиПродаж
	|	
	|ГДЕ
	|	НДСЗаписиКнигиПродаж.Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.ВосстановлениеНДС)
	|	И НЕ(НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|				ИЛИ НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.ЗаписьКнигиПродаж
	//++ НЕ УТКА
	|				ИЛИ НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.ОперацияБух
	//-- НЕ УТКА
	|				ИЛИ НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.СчетФактураПолученный
	|				ИЛИ НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.СчетФактураНалоговыйАгент
	|				ИЛИ НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.СчетФактураВыданныйАванс
	|				ИЛИ НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.СчетФактураКомиссионеру
	|				ИЛИ НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.СчетФактураНаНеподтвержденнуюРеализацию0
	|				ИЛИ НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.СчетФактураПолученныйАванс)
	|	И НДСЗаписиКнигиПродаж.Регистратор В (&ДокументыКОбработке)
	|
	|СГРУППИРОВАТЬ ПО
	|	НДСЗаписиКнигиПродаж.Регистратор,
	|	НДСЗаписиКнигиПродаж.Период,
	|	НДСЗаписиКнигиПродаж.Организация,
	|	НДСЗаписиКнигиПродаж.СчетФактура,
	|	НДСЗаписиКнигиПродаж.Покупатель,
	|	НДСЗаписиКнигиПродаж.ВидЦенности,
	|	НДСЗаписиКнигиПродаж.СтавкаНДС
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Партии.МоментВремени КАК МоментВремени,
	|	Партии.Период КАК Период,
	|	Партии.Регистратор КАК Регистратор,
	|	Партии.Организация КАК Организация,
	|	Партии.АналитикаУчетаПартий.Контрагент КАК Контрагент,
	|	Партии.ДокументПоступления КАК СчетФактура,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.ДатаРегистратора, ДАТАВРЕМЯ(1,1,1)) КАК ДатаПоступления,
	|	ВЫБОР
	|		КОГДА Партии.ДокументПоступления ССЫЛКА Документ.СчетФактураНалоговыйАгент
	|			ТОГДА ВЫБОР
	|					КОГДА ВЫРАЗИТЬ(Партии.ДокументПоступления КАК Документ.СчетФактураНалоговыйАгент).ВидАгентскогоДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыАгентскихДоговоров.Аренда)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.НалоговыйАгентАренда)
	|					КОГДА ВЫРАЗИТЬ(Партии.ДокументПоступления КАК Документ.СчетФактураНалоговыйАгент).ВидАгентскогоДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыАгентскихДоговоров.РеализацияИмущества)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.НалоговыйАгентРеализацияИмущества)
	|					КОГДА ВЫРАЗИТЬ(Партии.ДокументПоступления КАК Документ.СчетФактураНалоговыйАгент).ВидАгентскогоДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыАгентскихДоговоров.Нерезидент)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.НалоговыйАгентИностранцы)
	|				КОНЕЦ
	|		КОГДА Партии.ДокументПоступления ССЫЛКА Документ.ТаможеннаяДекларацияИмпорт
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТаможенныеПлатежи)
	|		КОГДА Партии.ДокументПоступления ССЫЛКА Документ.ЗаявлениеОВвозеТоваров
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТаможенныйСоюз)
	|		ИНАЧЕ Партии.ВидЦенности
	|	КОНЕЦ КАК ВидЦенности,
	|	Партии.АналитикаУчетаПартий.СтавкаНДС КАК СтавкаНДС,
	|	ВЫБОР
	|		КОГДА Партии.АналитикаУчетаПартий.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|		ИНАЧЕ Партии.АналитикаУчетаПартий.НалогообложениеНДС
	|	КОНЕЦ КАК НалогообложениеФинПартии,
	|	ВЫБОР
	|		КОГДА Партии.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|		ИНАЧЕ Партии.НалогообложениеНДС
	|	КОНЕЦ КАК НалогообложениеПартии,
	|	Партии.ДокументРеализации КАК ДокументРеализации,
	|	СУММА(Партии.СтоимостьРегл) КАК СуммаБезНДС,
	|	СУММА(Партии.НДСРегл) КАК НДС
	|ПОМЕСТИТЬ Партии
	|ИЗ
	|	(ВЫБРАТЬ
	|		Партии.МоментВремени КАК МоментВремени,
	|		Партии.Период КАК Период,
	|		Партии.Регистратор КАК Регистратор,
	|		Партии.Организация КАК Организация,
	|		Партии.СтоимостьРегл КАК СтоимостьРегл,
	|		Партии.НДСРегл КАК НДСРегл,
	|		Партии.ДокументПоступления КАК ДокументПоступления,
	|		Партии.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|		ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары) КАК ВидЦенности,
	|		Партии.НалогообложениеНДС КАК НалогообложениеНДС,
	|		ВЫБОР 
	|			КОГДА ТИПЗНАЧЕНИЯ(Партии.Регистратор) В (
	|					ТИП(Документ.РеализацияТоваровУслуг), 
	|					ТИП(Документ.АктВыполненныхРабот))
	|				ТОГДА Партии.Регистратор
	|			ИНАЧЕ НЕОПРЕДЕЛЕНО
	|		КОНЕЦ КАК ДокументРеализации
	|	ИЗ
	|		РегистрНакопления.ПартииТоваровОрганизаций КАК Партии
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикаПартий
	|			ПО (АналитикаПартий.КлючАналитики = Партии.АналитикаУчетаПартий)
	|	ГДЕ
	|		Партии.Активность
	|		И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Партии.НДСРегл <> 0
	|		И Партии.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|		И Партии.НалогообложениеНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|		И Партии.Регистратор В (&ДокументыКОбработке)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Партии.МоментВремени,
	|		Партии.Период,
	|		Партии.Регистратор,
	|		Партии.Организация,
	|		Партии.СтоимостьРегл,
	|		Партии.НДСРегл,
	|		Партии.ДокументПоступления,
	|		Партии.АналитикаУчетаПартий,
	|		ВЫБОР
	|			КОГДА АналитикаНоменклатуры.Номенклатура.ТипНоменклатуры В 
	|										(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), 
	|										 ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|		КОНЕЦ,
	|		Партии.НалогообложениеНДС,
	|		ВЫБОР 
	|			КОГДА ТИПЗНАЧЕНИЯ(Партии.Регистратор) В (
	|					ТИП(Документ.РеализацияТоваровУслуг), 
	|					ТИП(Документ.АктВыполненныхРабот))
	|				ТОГДА Партии.Регистратор
	|			ИНАЧЕ НЕОПРЕДЕЛЕНО
	|		КОНЕЦ КАК ДокументРеализации
	|	ИЗ
	|		РегистрНакопления.ПартииЗатратНаВыпуск КАК Партии
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикаПартий
	|			ПО (АналитикаПартий.КлючАналитики = Партии.АналитикаУчетаПартий)
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаНоменклатуры
	|			ПО (АналитикаНоменклатуры.КлючАналитики = Партии.АналитикаУчетаНоменклатуры)
	|	ГДЕ
	|		Партии.Активность
	|		И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Партии.НДСРегл <> 0
	|		И Партии.НалогообложениеНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|		И Партии.Регистратор В (&ДокументыКОбработке)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Партии.МоментВремени,
	|		Партии.Период,
	|		Партии.Регистратор,
	|		Партии.Организация,
	|		ВЫБОР
	|			КОГДА Партии.ДокументПоступления ССЫЛКА Документ.ТаможеннаяДекларацияИмпорт
	|				ТОГДА ВЫБОР АналитикаПартий.СтавкаНДС
	|						КОГДА ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18)
	|							ТОГДА Партии.НДСРегл * 100 / 18
	|						КОГДА ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10)
	|							ТОГДА Партии.НДСРегл * 100 / 10
	|						ИНАЧЕ 0
	|					КОНЕЦ
	|			ИНАЧЕ Партии.СтоимостьРегл
	|		КОНЕЦ,
	|		Партии.НДСРегл,
	|		Партии.ДокументПоступленияРасходов,
	|		Партии.АналитикаУчетаПартий,
	|		ВЫБОР 
	|			КОГДА НЕ СтатьиРасходов.Ссылка ЕСТЬ NULL 
	|				ТОГДА СтатьиРасходов.ВидЦенностиНДС
	|			КОГДА АналитикаНоменклатуры.Номенклатура.ТипНоменклатуры В (
	|							ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|							ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|		КОНЕЦ КАК ВидЦенности,
	|		Партии.НалогообложениеНДС,
	|		ВЫБОР 
	|			КОГДА ТИПЗНАЧЕНИЯ(Партии.Регистратор) В (
	|					ТИП(Документ.РеализацияТоваровУслуг), 
	|					ТИП(Документ.АктВыполненныхРабот))
	|				ТОГДА Партии.Регистратор
	|			ИНАЧЕ НЕОПРЕДЕЛЕНО
	|		КОНЕЦ КАК ДокументРеализации
	|	ИЗ
	|		РегистрНакопления.ПартииРасходовНаСебестоимостьТоваров КАК Партии
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикаПартий
	|			ПО (АналитикаПартий.КлючАналитики = Партии.АналитикаУчетаПартий)
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаНоменклатуры
	|			ПО (АналитикаНоменклатуры.КлючАналитики = Партии.АналитикаУчетаНоменклатуры)
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|			ПО (Партии.СтатьяРасходов = СтатьиРасходов.Ссылка)
	|		
	|	ГДЕ
	|		Партии.Активность
	|		И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Партии.НДСРегл <> 0
	|		И Партии.НалогообложениеНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|		И Партии.Регистратор В (&ДокументыКОбработке)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Партии.МоментВремени,
	|		Партии.Период,
	|		Партии.Регистратор,
	|		Партии.Организация,
	|		Партии.СтоимостьРегл,
	|		Партии.НДСРегл,
	|		Партии.ДокументПоступленияРасходов,
	|		Партии.АналитикаУчетаПартий,
	|		ВЫБОР
	|			КОГДА НЕ СпрНоменклатура.ТипНоменклатуры ЕСТЬ NULL 
	|				ТОГДА ВЫБОР
	|						КОГДА СпрНоменклатура.ТипНоменклатуры В 
	|												(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), 
	|												 ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|							ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
	|						ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|					КОНЕЦ
	|			КОГДА НЕ СтатьиРасходов.Ссылка ЕСТЬ NULL 
	|				ТОГДА СтатьиРасходов.ВидЦенностиНДС
	//++ НЕ УТ
	|			КОГДА Партии.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|					И Партии.АналитикаАктивовПассивов ССЫЛКА Справочник.ОбъектыЭксплуатации
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ОС)
	|			КОГДА Партии.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|					И Партии.АналитикаАктивовПассивов ССЫЛКА Справочник.НематериальныеАктивы
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.НМА)
	//-- НЕ УТ
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|		КОНЕЦ,
	|		Партии.НалогообложениеНДС,
	|		Партии.ДокументРеализации КАК ДокументРеализации
	|	ИЗ
	|		РегистрНакопления.ПартииПрочихРасходов КАК Партии
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|			ПО Партии.СтатьяРасходов = СтатьиРасходов.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиАналитикаУчетаНоменклатуры
	|			ПО Партии.АналитикаУчетаНоменклатуры = КлючиАналитикаУчетаНоменклатуры.КлючАналитики
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|			ПО (КлючиАналитикаУчетаНоменклатуры.Номенклатура = СпрНоменклатура.Ссылка)
	|	ГДЕ
	|		Партии.Активность
	|		И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Партии.НДСРегл <> 0
	|		И Партии.НалогообложениеНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|		И Партии.Регистратор В (&ДокументыКОбработке)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Партии.МоментВремени,
	|		Партии.Период,
	|		Партии.Регистратор,
	|		Партии.Организация,
	|		Партии.СтоимостьРегл,
	|		Партии.НДСРегл,
	|		Партии.ДокументПоступления,
	|		Партии.АналитикаУчетаПартий,
	|		ВЫБОР
	|			КОГДА АналитикаНоменклатуры.Номенклатура.ТипНоменклатуры В 
	|									(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), 
	|									 ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|		КОНЕЦ,
	|		Партии.НалогообложениеНДС,
	|		ВЫБОР 
	|			КОГДА ТИПЗНАЧЕНИЯ(Партии.Регистратор) В (
	|					ТИП(Документ.РеализацияТоваровУслуг), 
	|					ТИП(Документ.АктВыполненныхРабот))
	|				ТОГДА Партии.Регистратор
	|			ИНАЧЕ НЕОПРЕДЕЛЕНО
	|		КОНЕЦ КАК ДокументРеализации
	|	ИЗ
	|		РегистрНакопления.ПартииПроизводственныхЗатрат КАК Партии
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикаПартий
	|			ПО (АналитикаПартий.КлючАналитики = Партии.АналитикаУчетаПартий)
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаНоменклатуры
	|			ПО (АналитикаНоменклатуры.КлючАналитики = Партии.АналитикаУчетаНоменклатуры)
	|	ГДЕ
	|		Партии.Активность
	|		И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Партии.НДСРегл <> 0
	|		И Партии.НалогообложениеНДС <> ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка)
	|		И Партии.Регистратор В (&ДокументыКОбработке)
	//++ НЕ УТКА
	|		И НЕ Партии.Регистратор ССЫЛКА Документ.МаршрутныйЛистПроизводства
	//-- НЕ УТКА
	|		) КАК Партии
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.АналитикаУчетаПартий КАК АналитикаПартий
	|	ПО 
	|		АналитикаПартий.КлючАналитики = Партии.АналитикаУчетаПартий
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|	ПО
	|		ДанныеПервичныхДокументов.Организация = Партии.Организация
	|		И ДанныеПервичныхДокументов.Документ = Партии.ДокументПоступления
	|ГДЕ
	|	НЕ Партии.АналитикаУчетаПартий.НалогообложениеНДС ЕСТЬ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	Партии.МоментВремени,
	|	Партии.Период,
	|	Партии.Регистратор,
	|	Партии.Организация,
	|	Партии.АналитикаУчетаПартий.Контрагент,
	|	Партии.ДокументПоступления,
	|	ЕСТЬNULL(ДанныеПервичныхДокументов.ДатаРегистратора, ДАТАВРЕМЯ(1,1,1)),
	|	Партии.ВидЦенности,
	|	Партии.АналитикаУчетаПартий.СтавкаНДС,
	|	ВЫБОР
	|		КОГДА Партии.АналитикаУчетаПартий.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|		ИНАЧЕ Партии.АналитикаУчетаПартий.НалогообложениеНДС
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Партии.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС)
	|		ИНАЧЕ Партии.НалогообложениеНДС
	|	КОНЕЦ,
	|	Партии.ДокументРеализации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Партии.ДокументРеализации КАК ДокументРеализации,
	|	Партии.ДокументПоступления КАК СчетФактура,
	|	ВЫБОР
	|		КОГДА Партии.ДокументПоступления ССЫЛКА Документ.СчетФактураНалоговыйАгент
	|			ТОГДА ВЫБОР
	|					КОГДА ВЫРАЗИТЬ(Партии.ДокументПоступления КАК Документ.СчетФактураНалоговыйАгент).ВидАгентскогоДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыАгентскихДоговоров.Аренда)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.НалоговыйАгентАренда)
	|					КОГДА ВЫРАЗИТЬ(Партии.ДокументПоступления КАК Документ.СчетФактураНалоговыйАгент).ВидАгентскогоДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыАгентскихДоговоров.РеализацияИмущества)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.НалоговыйАгентРеализацияИмущества)
	|					КОГДА ВЫРАЗИТЬ(Партии.ДокументПоступления КАК Документ.СчетФактураНалоговыйАгент).ВидАгентскогоДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыАгентскихДоговоров.Нерезидент)
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.НалоговыйАгентИностранцы)
	|				КОНЕЦ
	|		КОГДА Партии.ДокументПоступления ССЫЛКА Документ.ТаможеннаяДекларацияИмпорт
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТаможенныеПлатежи)
	|		КОГДА Партии.ДокументПоступления ССЫЛКА Документ.ЗаявлениеОВвозеТоваров
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ТаможенныйСоюз)
	|		ИНАЧЕ Партии.ВидЦенности
	|	КОНЕЦ КАК ВидЦенности,
	|	Партии.НалогообложениеНДС КАК НалогообложениеПартии,
	|	СУММА(Партии.СтоимостьРегл) КАК СуммаБезНДС,
	|	СУММА(Партии.НДСРегл) КАК НДС
	|ПОМЕСТИТЬ ПартииЭкспорт
	|ИЗ
	|	(ВЫБРАТЬ
	|		Партии.Организация,
	|		Партии.СтоимостьРегл КАК СтоимостьРегл,
	|		Партии.НДСРегл КАК НДСРегл,
	|		Партии.ДокументПоступления КАК ДокументПоступления,
	|		ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары) КАК ВидЦенности,
	|		Партии.НалогообложениеНДС КАК НалогообложениеНДС,
	|		Партии.Регистратор КАК ДокументРеализации
	|	ИЗ
	|		РегистрНакопления.ПартииТоваровОрганизаций КАК Партии
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикаПартий
	|			ПО (АналитикаПартий.КлючАналитики = Партии.АналитикаУчетаПартий)
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РеализацииЭкспорт КАК РеализацииЭкспорт
	|			ПО Партии.Регистратор = РеализацииЭкспорт.Ссылка
	|	ГДЕ
	|		Партии.Активность
	|		И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Партии.НДСРегл <> 0
	|		И Партии.ВидЗапасов.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Партии.Организация,
	|		Партии.СтоимостьРегл,
	|		Партии.НДСРегл,
	|		Партии.ДокументПоступления,
	|		ВЫБОР
	|			КОГДА АналитикаНоменклатуры.Номенклатура.ТипНоменклатуры В 
	|										(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), 
	|										 ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|		КОНЕЦ,
	|		Партии.НалогообложениеНДС,
	|		Партии.Регистратор КАК ДокументРеализации
	|	ИЗ
	|		РегистрНакопления.ПартииЗатратНаВыпуск КАК Партии
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикаПартий
	|			ПО (АналитикаПартий.КлючАналитики = Партии.АналитикаУчетаПартий)
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаНоменклатуры
	|			ПО (АналитикаНоменклатуры.КлючАналитики = Партии.АналитикаУчетаНоменклатуры)
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РеализацииЭкспорт КАК РеализацииЭкспорт
	|			ПО Партии.Регистратор = РеализацииЭкспорт.Ссылка
	|	ГДЕ
	|		Партии.Активность
	|		И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Партии.НДСРегл <> 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Партии.Организация,
	|		ВЫБОР
	|			КОГДА Партии.ДокументПоступления ССЫЛКА Документ.ТаможеннаяДекларацияИмпорт
	|				ТОГДА ВЫБОР АналитикаПартий.СтавкаНДС
	|						КОГДА ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС18)
	|							ТОГДА Партии.НДСРегл * 100 / 18
	|						КОГДА ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС10)
	|							ТОГДА Партии.НДСРегл * 100 / 10
	|						ИНАЧЕ 0
	|					КОНЕЦ
	|			ИНАЧЕ Партии.СтоимостьРегл
	|		КОНЕЦ,
	|		Партии.НДСРегл,
	|		Партии.ДокументПоступленияРасходов,
	|		ВЫБОР 
	|			КОГДА НЕ СтатьиРасходов.Ссылка ЕСТЬ NULL 
	|				ТОГДА СтатьиРасходов.ВидЦенностиНДС
	|			КОГДА АналитикаНоменклатуры.Номенклатура.ТипНоменклатуры В (
	|							ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|							ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|		КОНЕЦ КАК ВидЦенности,
	|		Партии.НалогообложениеНДС,
	|		Партии.Регистратор КАК ДокументРеализации
	|	ИЗ
	|		РегистрНакопления.ПартииРасходовНаСебестоимостьТоваров КАК Партии
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикаПартий
	|			ПО (АналитикаПартий.КлючАналитики = Партии.АналитикаУчетаПартий)
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаНоменклатуры
	|			ПО (АналитикаНоменклатуры.КлючАналитики = Партии.АналитикаУчетаНоменклатуры)
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|			ПО (Партии.СтатьяРасходов = СтатьиРасходов.Ссылка)
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РеализацииЭкспорт КАК РеализацииЭкспорт
	|			ПО Партии.Регистратор = РеализацииЭкспорт.Ссылка
	|		
	|	ГДЕ
	|		Партии.Активность
	|		И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Партии.НДСРегл <> 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Партии.Организация,
	|		Партии.СтоимостьРегл,
	|		Партии.НДСРегл,
	|		Партии.ДокументПоступленияРасходов,
	|		ВЫБОР
	|			КОГДА НЕ СпрНоменклатура.ТипНоменклатуры ЕСТЬ NULL 
	|				ТОГДА ВЫБОР
	|						КОГДА СпрНоменклатура.ТипНоменклатуры В 
	|												(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), 
	|												 ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|							ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
	|						ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|					КОНЕЦ
	|			КОГДА НЕ СтатьиРасходов.Ссылка ЕСТЬ NULL 
	|				ТОГДА СтатьиРасходов.ВидЦенностиНДС
	//++ НЕ УТ
	|			КОГДА Партии.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|					И Партии.АналитикаАктивовПассивов ССЫЛКА Справочник.ОбъектыЭксплуатации
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ОС)
	|			КОГДА Партии.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|					И Партии.АналитикаАктивовПассивов ССЫЛКА Справочник.НематериальныеАктивы
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.НМА)
	//-- НЕ УТ
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|		КОНЕЦ,
	|		Партии.НалогообложениеНДС,
	|		Партии.ДокументРеализации КАК ДокументРеализации
	|	ИЗ
	|		РегистрНакопления.ПартииПрочихРасходов КАК Партии
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|			ПО Партии.СтатьяРасходов = СтатьиРасходов.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиАналитикаУчетаНоменклатуры
	|			ПО Партии.АналитикаУчетаНоменклатуры = КлючиАналитикаУчетаНоменклатуры.КлючАналитики
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|			ПО (КлючиАналитикаУчетаНоменклатуры.Номенклатура = СпрНоменклатура.Ссылка)
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РеализацииЭкспорт КАК РеализацииЭкспорт
	|			ПО Партии.ДокументРеализации = РеализацииЭкспорт.Ссылка
	|	ГДЕ
	|		Партии.Активность
	|		И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Партии.НДСРегл <> 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Партии.Организация,
	|		Партии.СтоимостьРегл,
	|		Партии.НДСРегл,
	|		Партии.ДокументПоступления,
	|		ВЫБОР
	|			КОГДА АналитикаНоменклатуры.Номенклатура.ТипНоменклатуры В 
	|									(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар), 
	|									 ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|		КОНЕЦ,
	|		Партии.НалогообложениеНДС,
	|		Партии.Регистратор КАК ДокументРеализации
	|	ИЗ
	|		РегистрНакопления.ПартииПроизводственныхЗатрат КАК Партии
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПартий КАК АналитикаПартий
	|			ПО (АналитикаПартий.КлючАналитики = Партии.АналитикаУчетаПартий)
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаНоменклатуры
	|			ПО (АналитикаНоменклатуры.КлючАналитики = Партии.АналитикаУчетаНоменклатуры)
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РеализацииЭкспорт КАК РеализацииЭкспорт
	|			ПО Партии.Регистратор = РеализацииЭкспорт.Ссылка
	|	ГДЕ
	|		Партии.Активность
	|		И Партии.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Партии.НДСРегл <> 0
	|		) КАК Партии
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
	|	ПО
	|		ДанныеПервичныхДокументов.Организация = Партии.Организация
	|		И ДанныеПервичныхДокументов.Документ = Партии.ДокументПоступления
	|ГДЕ
	|	Партии.НалогообложениеНДС В (
	|			 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт), 
	|			 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг)) 
	|	ИЛИ (Партии.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров)
	|			И ЕСТЬNULL(ДанныеПервичныхДокументов.ДатаРегистратора, ДАТАВРЕМЯ(1,1,1)) < &ДатаНачалаДействия150ФЗ)
	|
	|СГРУППИРОВАТЬ ПО
	|	Партии.ДокументПоступления,
	|	Партии.НалогообложениеНДС,
	|	Партии.ВидЦенности,
	|	Партии.ДокументРеализации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// Включение в стоимость
	|ВЫБРАТЬ
	|	Партии.Регистратор КАК Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	Партии.Период КАК Период,
	|	Партии.Организация КАК Организация,
	|	Партии.СчетФактура КАК СчетФактура,
	|	Партии.Контрагент КАК Поставщик,
	|	Партии.ВидЦенности КАК ВидЦенности,
	|	Партии.СтавкаНДС КАК СтавкаНДС,
	|	Партии.НалогообложениеФинПартии КАК ВидДеятельностиНДС,
	|	NULL КАК ИсправленныйСчетФактура,
	|	NULL КАК РеализацияЭкспорт,
	|	Партии.СуммаБезНДС КАК СуммаБезНДС,
	|	Партии.НДС КАК НДС,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияНДСПредъявленный.ВключениеНДСВСтоимость) КАК Событие,
	|	Партии.НалогообложениеПартии КАК КорВидДеятельностиНДС,
	|	NULL КАК ИдентификаторСтроки,
	|	ИСТИНА КАК РегламентнаяОперация
	|ИЗ
	|	Партии КАК Партии
	|ГДЕ
	|	Партии.НалогообложениеФинПартии В 
	|							(ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС), 
	|							 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт), 
	|							 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров),
	|							 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг),
	|							 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПоФактическомуИспользованию), 
	|							 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию))
	|	И Партии.НалогообложениеПартии В 
	|							(ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС), 
	|							 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Восстановление
	|ВЫБРАТЬ
	|	Партии.Регистратор КАК Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	Партии.Период КАК Период,
	|	Партии.Организация КАК Организация,
	|	Партии.СчетФактура КАК СчетФактура,
	|	Партии.Контрагент КАК Поставщик,
	|	Партии.ВидЦенности КАК ВидЦенности,
	|	Партии.СтавкаНДС КАК СтавкаНДС,
	|	Партии.НалогообложениеФинПартии КАК ВидДеятельностиНДС,
	|	NULL КАК ИсправленныйСчетФактура,
	|	NULL КАК РеализацияЭкспорт,
	|	Партии.СуммаБезНДС КАК СуммаБезНДС,
	|	Партии.НДС КАК НДС,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияНДСПредъявленный.ВосстановлениеНДСПодВидДеятельности) КАК Событие,
	|	Неопределено КАК КорВидДеятельностиНДС,
	|	NULL КАК ИдентификаторСтроки,
	|	ИСТИНА КАК РегламентнаяОперация
	|ИЗ
	|	Партии КАК Партии
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ НДСЗаписиКнигиПродаж КАК НДСЗаписиКнигиПродаж
	|	ПО НДСЗаписиКнигиПродаж.Регистратор = Партии.Регистратор
	|		И Партии.Период = НДСЗаписиКнигиПродаж.Период
	|		И Партии.Организация = НДСЗаписиКнигиПродаж.Организация
	|		И Партии.СчетФактура = НДСЗаписиКнигиПродаж.СчетФактура
	|		И Партии.Контрагент = НДСЗаписиКнигиПродаж.Покупатель
	|		И Партии.СтавкаНДС = НДСЗаписиКнигиПродаж.СтавкаНДС
	|	
	|ГДЕ
	|	(Партии.НалогообложениеФинПартии = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|		ИЛИ (Партии.НалогообложениеФинПартии = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров)
	|			И Партии.ДатаПоступления  >= &ДатаНачалаДействия150ФЗ))
	|	И Партии.НалогообложениеФинПартии <> Партии.НалогообложениеПартии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Исключение из стоимости
	|ВЫБРАТЬ
	|	Партии.Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	Партии.Период,
	|	Партии.Организация,
	|	Партии.СчетФактура,
	|	Партии.Контрагент,
	|	Партии.ВидЦенности,
	|	Партии.СтавкаНДС,
	|	Партии.НалогообложениеПартии,
	|	NULL,
	|	NULL,
	|	Партии.СуммаБезНДС,
	|	Партии.НДС,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияНДСПредъявленный.ИсключениеНДСИзСтоимости),
	|	Партии.НалогообложениеФинПартии,
	|	NULL,
	|	ИСТИНА
	|ИЗ
	|	Партии КАК Партии
	|ГДЕ
	|	Партии.НалогообложениеФинПартии В 
	|							(ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС), 
	|							 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|	И Партии.НалогообложениеПартии В 
	|							(ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС), 
	|							 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт), 
	|							 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров),
	|							 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг),
	|							 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПоФактическомуИспользованию), 
	|							 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Изменение деятельности: общие случаи
	|ВЫБРАТЬ
	|	Партии.Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	Партии.Период,
	|	Партии.Организация,
	|	Партии.СчетФактура,
	|	Партии.Контрагент,
	|	Партии.ВидЦенности,
	|	Партии.СтавкаНДС,
	|	Партии.НалогообложениеФинПартии,
	|	NULL,
	|	NULL,
	|	Партии.СуммаБезНДС,
	|	Партии.НДС,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияНДСПредъявленный.ИзменениеВидаДеятельностиНДС),
	|	Партии.НалогообложениеПартии,
	|	NULL,
	|	ИСТИНА
	|ИЗ
	|	Партии КАК Партии
	|ГДЕ
	|	Партии.НалогообложениеФинПартии <> Партии.НалогообложениеПартии
	|	И Партии.НалогообложениеФинПартии В 
	|	                    (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС),
	|	                     ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт),
	|	                     ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг),
	|	                     ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПоФактическомуИспользованию),
	|	                     ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию))
	|	И Партии.НалогообложениеПартии В 
	|						(ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС),
	|						 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПоФактическомуИспользованию), 
	|						 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Партии.Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	Партии.Период,
	|	Партии.Организация,
	|	Партии.СчетФактура,
	|	Партии.Контрагент,
	|	Партии.ВидЦенности,
	|	Партии.СтавкаНДС,
	|	Партии.НалогообложениеПартии,
	|	NULL,
	|	NULL,
	|	Партии.СуммаБезНДС,
	|	Партии.НДС,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияНДСПредъявленный.ИзменениеВидаДеятельностиНДС),
	|	Партии.НалогообложениеФинПартии,
	|	NULL,
	|	ИСТИНА
	|ИЗ
	|	Партии КАК Партии
	|ГДЕ
	|	Партии.НалогообложениеФинПартии <> Партии.НалогообложениеПартии
	|	И Партии.НалогообложениеФинПартии В 
	|							(ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС), 
	|							 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт),
	|							 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг),
	|							 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПоФактическомуИспользованию), 
	|							 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию))
	|	И Партии.НалогообложениеПартии В 
	|							(ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС), 
	|							 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПоФактическомуИспользованию), 
	|							 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Изменение деятельности: ЭкспортНесырьевыхТоваров -> [ПродажаОблагаетсяНДС, ПоФактическомуИспользованию, ВводОСВЭксплуатацию]
	|ВЫБРАТЬ
	|	Партии.Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	Партии.Период,
	|	Партии.Организация,
	|	Партии.СчетФактура,
	|	Партии.Контрагент,
	|	Партии.ВидЦенности,
	|	Партии.СтавкаНДС,
	|	Партии.НалогообложениеФинПартии,
	|	NULL,
	|	NULL,
	|	Партии.СуммаБезНДС,
	|	Партии.НДС,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияНДСПредъявленный.ИзменениеВидаДеятельностиНДС),
	|	Партии.НалогообложениеПартии,
	|	NULL,
	|	ИСТИНА
	|ИЗ
	|	Партии КАК Партии
	|ГДЕ
	|	Партии.НалогообложениеФинПартии = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров)
	|	И (Партии.НалогообложениеПартии В (
	|					 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПоФактическомуИспользованию), 
	|					 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию))
	|		ИЛИ (Партии.НалогообложениеПартии = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|				И Партии.ДатаПоступления < &ДатаНачалаДействия150ФЗ))
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Партии.Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	Партии.Период,
	|	Партии.Организация,
	|	Партии.СчетФактура,
	|	Партии.Контрагент,
	|	Партии.ВидЦенности,
	|	Партии.СтавкаНДС,
	|	Партии.НалогообложениеПартии,
	|	NULL,
	|	NULL,
	|	Партии.СуммаБезНДС,
	|	Партии.НДС,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияНДСПредъявленный.ИзменениеВидаДеятельностиНДС),
	|	Партии.НалогообложениеФинПартии,
	|	NULL,
	|	ИСТИНА
	|ИЗ
	|	Партии КАК Партии
	|ГДЕ
	|	Партии.НалогообложениеФинПартии = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров)
	|	И (Партии.НалогообложениеПартии В (
	|					 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПоФактическомуИспользованию), 
	|					 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию))
	|		ИЛИ (Партии.НалогообложениеПартии = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|				И Партии.ДатаПоступления < &ДатаНачалаДействия150ФЗ))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Изменение деятельности: Любой -> [ПродажаНаЭкспорт, ЭкспортСырьевыхТоваровУслуг] 
	// + Реализаця на [ПродажаНаЭкспорт, ЭкспортСырьевыхТоваровУслуг]
	|ВЫБРАТЬ
	|	Партии.Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	Партии.Период,
	|	Партии.Организация,
	|	Партии.СчетФактура,
	|	Партии.Контрагент,
	|	Партии.ВидЦенности,
	|	Партии.СтавкаНДС,
	|	ВЫБОР 
	|		КОГДА Партии.НалогообложениеФинПартии В (
	|						ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС), 
	|						ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			ТОГДА Партии.НалогообложениеПартии
	|		ИНАЧЕ Партии.НалогообложениеФинПартии
	|	КОНЕЦ,
	|	NULL,
	|	НЕОПРЕДЕЛЕНО,
	|	Партии.СуммаБезНДС,
	|	Партии.НДС,
	|	ВЫБОР
	|		КОГДА Партии.ДокументРеализации <> НЕОПРЕДЕЛЕНО
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СобытияНДСПредъявленный.РеализацияПоСтавке0)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СобытияНДСПредъявленный.ИзменениеВидаДеятельностиНДС)
	|	КОНЕЦ,
	|	Партии.НалогообложениеПартии,
	|	NULL,
	|	ИСТИНА
	|ИЗ
	|	Партии КАК Партии
	|ГДЕ
	|	Партии.НалогообложениеПартии В (
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг))
	|	И (Партии.ДокументРеализации = НЕОПРЕДЕЛЕНО
	|			ИЛИ Партии.НалогообложениеФинПартии <> Партии.НалогообложениеПартии)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Партии.Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	Партии.Период,
	|	Партии.Организация,
	|	Партии.СчетФактура,
	|	Партии.Контрагент,
	|	Партии.ВидЦенности,
	|	Партии.СтавкаНДС,
	|	Партии.НалогообложениеПартии,
	|	NULL,
	|	Партии.ДокументРеализации,
	|	Партии.СуммаБезНДС,
	|	Партии.НДС,
	|	ВЫБОР
	|		КОГДА Партии.ДокументРеализации <> НЕОПРЕДЕЛЕНО
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СобытияНДСПредъявленный.РеализацияПоСтавке0)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СобытияНДСПредъявленный.ИзменениеВидаДеятельностиНДС)
	|	КОНЕЦ,
	|	Партии.НалогообложениеФинПартии,
	|	NULL,
	|	ИСТИНА
	|ИЗ
	|	Партии КАК Партии
	|ГДЕ
	|	Партии.НалогообложениеПартии В (
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг))
	|	И (Партии.ДокументРеализации <> НЕОПРЕДЕЛЕНО
	|		ИЛИ Партии.НалогообложениеФинПартии <> Партии.НалогообложениеПартии)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Изменение деятельности: Любой -> [ЭкспортНесырьевыхТоваров]
	// + Реализация партий до 01.07.2016 на [ЭкспортНесырьевыхТоваров]
	|ВЫБРАТЬ
	|	Партии.Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	Партии.Период,
	|	Партии.Организация,
	|	Партии.СчетФактура,
	|	Партии.Контрагент,
	|	Партии.ВидЦенности,
	|	Партии.СтавкаНДС,
	|	ВЫБОР 
	|		КОГДА Партии.НалогообложениеФинПартии В (
	|						ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС), 
	|						ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|			ТОГДА Партии.НалогообложениеПартии
	|		ИНАЧЕ Партии.НалогообложениеФинПартии
	|	КОНЕЦ,
	|	NULL,
	|	НЕОПРЕДЕЛЕНО,
	|	Партии.СуммаБезНДС,
	|	Партии.НДС,
	|	ВЫБОР
	|		КОГДА Партии.ДокументРеализации <> НЕОПРЕДЕЛЕНО И Партии.ДатаПоступления < &ДатаНачалаДействия150ФЗ
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СобытияНДСПредъявленный.РеализацияПоСтавке0)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СобытияНДСПредъявленный.ИзменениеВидаДеятельностиНДС)
	|	КОНЕЦ,
	|	Партии.НалогообложениеПартии,
	|	NULL,
	|	ИСТИНА
	|ИЗ
	|	Партии КАК Партии
	|ГДЕ
	|	Партии.НалогообложениеПартии = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров)
	|	И (Партии.НалогообложениеФинПартии В 
	|							(ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт),
	|							 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг),
	|							 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПоФактическомуИспользованию), 
	|							 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию),
	|							 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|							 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|		ИЛИ (Партии.НалогообложениеФинПартии = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|				И Партии.ДатаПоступления < &ДатаНачалаДействия150ФЗ)
	|		ИЛИ (Партии.ДокументРеализации <> НЕОПРЕДЕЛЕНО
	|				И Партии.ДатаПоступления < &ДатаНачалаДействия150ФЗ)) 
	|		
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Партии.Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	Партии.Период,
	|	Партии.Организация,
	|	Партии.СчетФактура,
	|	Партии.Контрагент,
	|	Партии.ВидЦенности,
	|	Партии.СтавкаНДС,
	|	Партии.НалогообложениеПартии,
	|	NULL,
	|	ВЫБОР
	|		КОГДА Партии.ДокументРеализации <> НЕОПРЕДЕЛЕНО И Партии.ДатаПоступления < &ДатаНачалаДействия150ФЗ
	|			ТОГДА Партии.ДокументРеализации
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	Партии.СуммаБезНДС,
	|	Партии.НДС,
	|	ВЫБОР
	|		КОГДА Партии.ДокументРеализации <> НЕОПРЕДЕЛЕНО И Партии.ДатаПоступления < &ДатаНачалаДействия150ФЗ
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СобытияНДСПредъявленный.РеализацияПоСтавке0)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СобытияНДСПредъявленный.ИзменениеВидаДеятельностиНДС)
	|	КОНЕЦ,
	|	Партии.НалогообложениеФинПартии,
	|	NULL,
	|	ИСТИНА
	|ИЗ
	|	Партии КАК Партии
	|ГДЕ
	|	Партии.НалогообложениеПартии = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров)
	|	И (Партии.НалогообложениеФинПартии В 
	|							(ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНаЭкспорт),
	|							 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортСырьевыхТоваровУслуг),
	|							 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПоФактическомуИспользованию), 
	|							 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ВводОСВЭксплуатацию),
	|							 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|							 ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД))
	|		ИЛИ (Партии.НалогообложениеФинПартии = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|				И Партии.ДатаПоступления < &ДатаНачалаДействия150ФЗ)
	|		ИЛИ (Партии.ДокументРеализации <> НЕОПРЕДЕЛЕНО
	|				И Партии.ДатаПоступления < &ДатаНачалаДействия150ФЗ))  
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Вычеты НДС на основании смен налогообложения
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупок.Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	НДСЗаписиКнигиПокупок.Период,
	|	НДСЗаписиКнигиПокупок.Организация,
	|	НДСЗаписиКнигиПокупок.СчетФактура,
	|	НДСЗаписиКнигиПокупок.Поставщик,
	|	Партии.ВидЦенности,
	|	НДСЗаписиКнигиПокупок.СтавкаНДС,
	|	Партии.НалогообложениеПартии,
	|	НДСЗаписиКнигиПокупок.ИсправленныйСчетФактура,
	|	NULL,
	|	Партии.СуммаБезНДС,
	|	Партии.НДС,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияНДСПредъявленный.НДСПринятКВычету),
	|	NULL,
	|	NULL,
	|	ИСТИНА
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок КАК НДСЗаписиКнигиПокупок
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Партии КАК Партии
	|	ПО
	|		НДСЗаписиКнигиПокупок.Регистратор = Партии.Регистратор
	|		И Партии.Период = НДСЗаписиКнигиПокупок.Период
	|		И Партии.Организация = НДСЗаписиКнигиПокупок.Организация
	|		И Партии.СчетФактура = НДСЗаписиКнигиПокупок.СчетФактура
	|		И Партии.СтавкаНДС = НДСЗаписиКнигиПокупок.СтавкаНДС
	|ГДЕ
	|	НДСЗаписиКнигиПокупок.Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету)
	|	И Партии.НалогообложениеПартии В (
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ЭкспортНесырьевыхТоваров),
	|			ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС))
	|	И НЕ (НДСЗаписиКнигиПокупок.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|				ИЛИ НДСЗаписиКнигиПокупок.Регистратор ССЫЛКА Документ.СчетФактураНалоговыйАгент
	|				ИЛИ НДСЗаписиКнигиПокупок.Регистратор ССЫЛКА Документ.ВозвратТоваровОтКлиента)
	|	И НДСЗаписиКнигиПокупок.НДС > 0
	|	И НДСЗаписиКнигиПокупок.Регистратор В (&ДокументыКОбработке)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Вычеты НДС по счетам-фактурам
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупок.Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	НДСЗаписиКнигиПокупок.Период,
	|	НДСЗаписиКнигиПокупок.Организация,
	|	НДСЗаписиКнигиПокупок.СчетФактура,
	|	НДСЗаписиКнигиПокупок.Поставщик,
	|	ВЫБОР
	|		КОГДА НДСЗаписиКнигиПокупок.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ОС)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.Товары)
	|		ИНАЧЕ НДСЗаписиКнигиПокупок.ВидЦенности
	|	КОНЕЦ,
	|	НДСЗаписиКнигиПокупок.СтавкаНДС,
	|	ВЫБОР 
	|		КОГДА НДСЗаписиКнигиПокупок.СчетФактура ССЫЛКА Документ.ВозвратТоваровОтКлиента 
	|			ТОГДА ВЫРАЗИТЬ(НДСЗаписиКнигиПокупок.СчетФактура КАК Документ.ВозвратТоваровОтКлиента).НалогообложениеНДС
	|		КОГДА НДСЗаписиКнигиПокупок.СчетФактура ССЫЛКА Документ.ПоступлениеТоваровУслуг 
	|			ТОГДА ВЫРАЗИТЬ(НДСЗаписиКнигиПокупок.СчетФактура КАК Документ.ПоступлениеТоваровУслуг).ЗакупкаПодДеятельность
	|		КОГДА НДСЗаписиКнигиПокупок.СчетФактура ССЫЛКА Документ.ОтчетКомиссионера 
	|			ТОГДА ВЫРАЗИТЬ(НДСЗаписиКнигиПокупок.СчетФактура КАК Документ.ОтчетКомиссионера).НалогообложениеНДС
	|		КОГДА НДСЗаписиКнигиПокупок.СчетФактура ССЫЛКА Документ.ВводОстатков 
	|			ТОГДА ВЫРАЗИТЬ(НДСЗаписиКнигиПокупок.СчетФактура КАК Документ.ВводОстатков).НалогообложениеНДС
	|		КОГДА НДСЗаписиКнигиПокупок.СчетФактура ССЫЛКА Документ.ОтчетПоКомиссииМеждуОрганизациями 
	|			ТОГДА ВЫРАЗИТЬ(НДСЗаписиКнигиПокупок.СчетФактура КАК Документ.ОтчетПоКомиссииМеждуОрганизациями).НалогообложениеНДС
	|		КОГДА НДСЗаписиКнигиПокупок.СчетФактура ССЫЛКА Документ.КорректировкаПоступления 
	|			ТОГДА ВЫРАЗИТЬ(НДСЗаписиКнигиПокупок.СчетФактура КАК Документ.КорректировкаПоступления).ДокументОснование.ЗакупкаПодДеятельность
	//++ НЕ УТКА
	|		КОГДА НДСЗаписиКнигиПокупок.СчетФактура ССЫЛКА Документ.ОтчетПереработчика 
	|			ТОГДА ВЫРАЗИТЬ(НДСЗаписиКнигиПокупок.СчетФактура КАК Документ.ОтчетПереработчика).ЗакупкаПодДеятельность
	|		КОГДА НДСЗаписиКнигиПокупок.СчетФактура ССЫЛКА Документ.ПоступлениеУслугПоЛизингу 
	|			ТОГДА ВЫРАЗИТЬ(НДСЗаписиКнигиПокупок.СчетФактура КАК Документ.ПоступлениеУслугПоЛизингу).ЗакупкаПодДеятельность
	//-- НЕ УТКА
	|		КОГДА НДСЗаписиКнигиПокупок.СчетФактура ССЫЛКА Документ.ПоступлениеУслугПрочихАктивов 
	|			ТОГДА ВЫРАЗИТЬ(НДСЗаписиКнигиПокупок.СчетФактура КАК Документ.ПоступлениеУслугПрочихАктивов).ЗакупкаПодДеятельность
	|		КОГДА НДСЗаписиКнигиПокупок.СчетФактура ССЫЛКА Документ.ВозвратТоваровМеждуОрганизациями 
	|			ТОГДА ВЫРАЗИТЬ(НДСЗаписиКнигиПокупок.СчетФактура КАК Документ.ВозвратТоваровМеждуОрганизациями).НалогообложениеНДС
	|		КОГДА НДСЗаписиКнигиПокупок.СчетФактура ССЫЛКА Документ.ПередачаТоваровМеждуОрганизациями 
	|			ТОГДА ВЫРАЗИТЬ(НДСЗаписиКнигиПокупок.СчетФактура КАК Документ.ПередачаТоваровМеждуОрганизациями).ПередачаПодДеятельность
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|	КОНЕЦ,
	|	НДСЗаписиКнигиПокупок.ИсправленныйСчетФактура,
	|	NULL,
	|	НДСЗаписиКнигиПокупок.СуммаБезНДС,
	|	НДСЗаписиКнигиПокупок.НДС,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияНДСПредъявленный.НДСПринятКВычету),
	|	NULL,
	|	NULL,
	|	ИСТИНА
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПокупок КАК НДСЗаписиКнигиПокупок
	|ГДЕ
	|	НДСЗаписиКнигиПокупок.Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПокупки.ПредъявленНДСКВычету)
	|	И (НДСЗаписиКнигиПокупок.Регистратор ССЫЛКА Документ.СчетФактураПолученный
	|		ИЛИ НДСЗаписиКнигиПокупок.Регистратор ССЫЛКА Документ.ТаможеннаяДекларацияИмпорт
	|		ИЛИ НДСЗаписиКнигиПокупок.Регистратор ССЫЛКА Документ.ЗаявлениеОВвозеТоваров)
	|	И НДСЗаписиКнигиПокупок.НДС > 0
	|	И НДСЗаписиКнигиПокупок.Регистратор В (&ДокументыКОбработке)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Восстановления НДС по записям с невыясненным источником происхождения
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПродаж.Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	НДСЗаписиКнигиПродаж.Период,
	|	НДСЗаписиКнигиПродаж.Организация,
	|	НДСЗаписиКнигиПродаж.СчетФактура,
	|	НДСЗаписиКнигиПродаж.Покупатель,
	|	НДСЗаписиКнигиПродаж.ВидЦенности,
	|	НДСЗаписиКнигиПродаж.СтавкаНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС),
	|	NULL,
	|	NULL,
	|	НДСЗаписиКнигиПродаж.СуммаБезНДС,
	|	НДСЗаписиКнигиПродаж.НДС,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияНДСПредъявленный.ВосстановлениеНДСПодВидДеятельности),
	|	NULL,
	|	NULL,
	|	ИСТИНА
	|ИЗ
	|	РегистрНакопления.НДСЗаписиКнигиПродаж КАК НДСЗаписиКнигиПродаж
	|	ЛЕВОЕ СОЕДИНЕНИЕ Партии КАК Партии
	|	ПО НДСЗаписиКнигиПродаж.Регистратор = Партии.Регистратор
	|				И Партии.Период = НДСЗаписиКнигиПродаж.Период
	|				И Партии.Организация = НДСЗаписиКнигиПродаж.Организация
	|				И Партии.СчетФактура = НДСЗаписиКнигиПродаж.СчетФактура
	|				И Партии.Контрагент = НДСЗаписиКнигиПродаж.Покупатель
	|				И Партии.СтавкаНДС = НДСЗаписиКнигиПродаж.СтавкаНДС
	|				И Партии.НалогообложениеФинПартии = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС)
	|				И Партии.НалогообложениеФинПартии <> Партии.НалогообложениеПартии
	|
	|ГДЕ
	|	Партии.Регистратор ЕСТЬ NULL // Отбираем записи с невыясненным источником происхождения
	|	И НДСЗаписиКнигиПродаж.Событие = ЗНАЧЕНИЕ(Перечисление.СобытияПоНДСПродажи.ВосстановлениеНДС)
	|	И НЕ(НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|				ИЛИ НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.КорректировкаРеализации
	|				ИЛИ НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.ЗаписьКнигиПродаж
	//++ НЕ УТКА
	|				ИЛИ НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.ОперацияБух
	//-- НЕ УТКА
	|				ИЛИ НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.СчетФактураПолученный
	|				ИЛИ НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.СчетФактураНалоговыйАгент
	|				ИЛИ НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.СчетФактураВыданныйАванс
	|				ИЛИ НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.СчетФактураКомиссионеру
	|				ИЛИ НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.СчетФактураНаНеподтвержденнуюРеализацию0
	|				ИЛИ НДСЗаписиКнигиПродаж.Регистратор ССЫЛКА Документ.СчетФактураПолученныйАванс)
	|	И НДСЗаписиКнигиПродаж.Регистратор В (&ДокументыКОбработке)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Вычеты НДС по подтвержденному экспорту
	|ВЫБРАТЬ
	|	НДСЗаписиКнигиПокупок.Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	НДСЗаписиКнигиПокупок.Период,
	|	НДСЗаписиКнигиПокупок.Организация,
	|	ПартииЭкспорт.СчетФактура,
	|	НДСЗаписиКнигиПокупок.Поставщик,
	|	ПартииЭкспорт.ВидЦенности,
	|	НДСЗаписиКнигиПокупок.СтавкаНДС,
	|	ПартииЭкспорт.НалогообложениеПартии,
	|	NULL,
	|	ПартииЭкспорт.ДокументРеализации,
	|	ПартииЭкспорт.СуммаБезНДС,
	|	ПартииЭкспорт.НДС,
	|	ЗНАЧЕНИЕ(Перечисление.СобытияНДСПредъявленный.НДСПринятКВычету),
	|	NULL,
	|	NULL,
	|	ИСТИНА
	|ИЗ
	|	НДСЗаписиКнигиПокупокЭкспорт КАК НДСЗаписиКнигиПокупок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПартииЭкспорт КАК ПартииЭкспорт
	|		ПО НДСЗаписиКнигиПокупок.СчетФактура = ПартииЭкспорт.СчетФактура
	|ГДЕ
	|	НДСЗаписиКнигиПокупок.Регистратор В (&ДокументыКОбработке)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Пометка движений по ИСФ в НДСПредъявленный признаком РегламентнаяОперация 
	|ВЫБРАТЬ
	|	НДСПредъявленный.Регистратор КАК Регистратор,
	|	НДСПредъявленный.ВидДвижения КАК ВидДвижения,
	|	НДСПредъявленный.Период КАК Период,
	|	НДСПредъявленный.Организация КАК Организация,
	|	НДСПредъявленный.СчетФактура КАК СчетФактура,
	|	НДСПредъявленный.Поставщик КАК Поставщик,
	|	НДСПредъявленный.ВидЦенности КАК ВидЦенности,
	|	НДСПредъявленный.СтавкаНДС КАК СтавкаНДС,
	|	НДСПредъявленный.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	|	НДСПредъявленный.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
	|	НДСПредъявленный.РеализацияЭкспорт КАК РеализацияЭкспорт,
	|	НДСПредъявленный.СуммаБезНДС КАК СуммаБезНДС,
	|	НДСПредъявленный.НДС КАК НДС,
	|	НДСПредъявленный.Событие КАК Событие,
	|	НДСПредъявленный.КорВидДеятельностиНДС КАК КорВидДеятельностиНДС,
	|	НДСПредъявленный.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ИСТИНА КАК РегламентнаяОперация
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный КАК НДСПредъявленный
	|
	|ГДЕ
	|	НДСПредъявленный.Регистратор В (&ДокументыКОбработке)
	|	И ТИПЗНАЧЕНИЯ(НДСПредъявленный.Регистратор) = ТИП(Документ.СчетФактураВыданный)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДСПредъявленный.Регистратор КАК Регистратор,
	|	НДСПредъявленный.ВидДвижения КАК ВидДвижения,
	|	НДСПредъявленный.Период КАК Период,
	|	НДСПредъявленный.Организация КАК Организация,
	|	НДСПредъявленный.СчетФактура КАК СчетФактура,
	|	НДСПредъявленный.Поставщик КАК Поставщик,
	|	НДСПредъявленный.ВидЦенности КАК ВидЦенности,
	|	НДСПредъявленный.СтавкаНДС КАК СтавкаНДС,
	|	НДСПредъявленный.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	|	НДСПредъявленный.ИсправленныйСчетФактура КАК ИсправленныйСчетФактура,
	|	НДСПредъявленный.РеализацияЭкспорт КАК РеализацияЭкспорт,
	|	НДСПредъявленный.СуммаБезНДС КАК СуммаБезНДС,
	|	НДСПредъявленный.НДС КАК НДС,
	|	НДСПредъявленный.Событие КАК Событие,
	|	НДСПредъявленный.КорВидДеятельностиНДС КАК КорВидДеятельностиНДС,
	|	НДСПредъявленный.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	ИСТИНА КАК РегламентнаяОперация
	|ИЗ
	|	РегистрНакопления.НДСПредъявленный КАК НДСПредъявленный
	|
	|ГДЕ
	|	НДСПредъявленный.Регистратор В (&ДокументыКОбработке)
	|	И ТИПЗНАЧЕНИЯ(НДСПредъявленный.Регистратор) = ТИП(Документ.СчетФактураПолученный)
	|	И НДСПредъявленный.Событие <> ЗНАЧЕНИЕ(Перечисление.СобытияНДСПредъявленный.НДСПринятКВычету)
	|";
	
	Запрос.УстановитьПараметр("ДокументыКОбработке", ДокументыКОбработке);
	Запрос.УстановитьПараметр("ДатаНачалаДействия150ФЗ", УчетНДСУТ.ДатаНачалаДействия150ФЗ());
	ДвиженияДокументов = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.Прямой);
	
	ОбщегоНазначенияУТ.ДобавитьСтрокиВТаблицу(ДвиженияНДСПредъявленный, ДвиженияДокументов);
	
КонецПроцедуры

Функция ПодготовитьПустуюТаблицуДвиженийНДСПредъявленный()
	
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("ВидДвижения");
	ТЗ.Колонки.Добавить("Регистратор");
	ТЗ.Колонки.Добавить("Период");
	ТЗ.Колонки.Добавить("Организация");
	ТЗ.Колонки.Добавить("СчетФактура");
	ТЗ.Колонки.Добавить("Поставщик");
	ТЗ.Колонки.Добавить("ВидЦенности");
	ТЗ.Колонки.Добавить("СтавкаНДС");
	ТЗ.Колонки.Добавить("ВидДеятельностиНДС");
	ТЗ.Колонки.Добавить("ИсправленныйСчетФактура");
	ТЗ.Колонки.Добавить("РеализацияЭкспорт");
	ТЗ.Колонки.Добавить("СуммаБезНДС");
	ТЗ.Колонки.Добавить("НДС");
	ТЗ.Колонки.Добавить("Событие");
	ТЗ.Колонки.Добавить("КорВидДеятельностиНДС");
	ТЗ.Колонки.Добавить("ИдентификаторСтроки");
	ТЗ.Колонки.Добавить("РегламентнаяОперация");
	Возврат ТЗ;
	
КонецФункции

Процедура ЗаписатьДвиженияДокументовИзОчереди(ДвиженияНДСПредъявленный, Очередь, Регистраторы)
	
	Для каждого Регистратор из Регистраторы Цикл
		МассивДвижений = ДвиженияНДСПредъявленный.НайтиСтроки(Новый Структура("Регистратор", Регистратор));
		
		НаборЗаписей = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмяРегистра()).СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
		
		НачатьТранзакцию();
		
		Попытка
			
			Для каждого Движение из МассивДвижений Цикл
				
				Если Движение.НДС = 0 Тогда
					Продолжить;
				КонецЕсли;

				Запись = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(Запись, Движение);
			КонецЦикла; 
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru = 'Не удалось перезаписать движения в регистр %ИмяРегистра% по документу %Ссылка% по причине: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Регистратор);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ИмяРегистра%", ПолноеИмяРегистра());
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
			Метаданные.НайтиПоПолномуИмени(Регистратор.Метаданные().ПолноеИмя()), Регистратор, ТекстСообщения);
			
			ВызватьИсключение;
			
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДополнитьМассивДОкументовКОбработке(ДокументыКОбработке, Очередь, ВыборкаПоРегистраторам) 
	
	Пока ВыборкаПоРегистраторам.Следующий() Цикл
		Если Не ЭтоДокументOnLine(ВыборкаПоРегистраторам.Регистратор) Тогда
			ДокументыКОбработке.Добавить(ВыборкаПоРегистраторам.Регистратор);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ЭтоДокументOnLine(Ссылка)
	
	// Документы, формирующие движения по регистру "НДС Предъявленный" он-лайн
	// обрабатываются в секции 1 процедуры "ОбработатьДанныеДляПереходаНаНовуюВерсию"
	Возврат 
			ТипЗнч(Ссылка) = Тип("ДокументСсылка.ВводОстатков") или
			ТипЗнч(Ссылка) = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями") или
			ТипЗнч(Ссылка) = Тип("ДокументСсылка.ВозвратТоваровОтКлиента") или
			ТипЗнч(Ссылка) = Тип("ДокументСсылка.ОтчетКомиссионера") или
//++ НЕ УТ
			ТипЗнч(Ссылка) = Тип("ДокументСсылка.ПоступлениеУслугПоЛизингу") или
			ТипЗнч(Ссылка) = Тип("ДокументСсылка.ОтчетПереработчика") или
//-- НЕ УТ
			ТипЗнч(Ссылка) = Тип("ДокументСсылка.ОтчетПоКомиссииМеждуОрганизациями") или
			ТипЗнч(Ссылка) = Тип("ДокументСсылка.СчетФактураНалоговыйАгент");

КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
