#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Регистратор = Отбор.Регистратор.Значение;
	
	ОтменаПроведения = ?(ДополнительныеСвойства.Свойство("РежимЗаписи"),
		ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения,
		Ложь);
		
	Если ТипЗнч(Регистратор) = Тип("ДокументСсылка.ПланПроизводства") 
		И НЕ ОтменаПроведения
		И ПланироватьПолуфабрикатыАвтоматически(Регистратор) Тогда
		
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
		
		ПрочитатьСтарыеИНовыеДвижения(МенеджерВременныхТаблиц, Регистратор);
		
		ДополнительныеСвойства.Вставить("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц);
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("МенеджерВременныхТаблиц") Тогда
		
		СформироватьОчередьДопланированияПолуфабрикатов(
			ДополнительныеСвойства.МенеджерВременныхТаблиц,
			Отбор.Регистратор.Значение);
			
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПланироватьПолуфабрикатыАвтоматически(ПланПроизводства)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		ПланПроизводства,
		"ВидПлана.ПланироватьПолуфабрикатыАвтоматически");
	
КонецФункции

Процедура ПрочитатьСтарыеИНовыеДвижения(МенеджерВременныхТаблиц, ПланПроизводства)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	СтарыеДвижения.ДатаВыпускаПолуфабриката КАК ДатаВыпускаПолуфабриката,
	|	СтарыеДвижения.ПланПроизводства КАК ПланПроизводства,
	|	СтарыеДвижения.ПодразделениеДиспетчер КАК ПодразделениеДиспетчер,
	|	СтарыеДвижения.СпецификацияПолуфабриката КАК СпецификацияПолуфабриката,
	|	СтарыеДвижения.Назначение КАК Назначение,
	|	СтарыеДвижения.Сценарий КАК Сценарий,
	|	СтарыеДвижения.Номенклатура КАК Номенклатура,
	|	СтарыеДвижения.Характеристика КАК Характеристика,
	|	СтарыеДвижения.Количество КАК Количество
	|ПОМЕСТИТЬ СтарыеДвижения
	|ИЗ
	|	РегистрНакопления.ПланыПотребленияМатериалов КАК СтарыеДвижения
	|ГДЕ
	|	СтарыеДвижения.Регистратор = &Ссылка
	|	И СтарыеДвижения.ПланПроизводства = &Ссылка
	|	И СтарыеДвижения.СпецификацияПолуфабриката <> ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НовыеДвижения.ДатаВыпускаПолуфабриката КАК ДатаВыпускаПолуфабриката,
	|	НовыеДвижения.ПланПроизводства КАК ПланПроизводства,
	|	НовыеДвижения.ПодразделениеДиспетчер КАК ПодразделениеДиспетчер,
	|	НовыеДвижения.СпецификацияПолуфабриката КАК СпецификацияПолуфабриката,
	|	НовыеДвижения.Назначение КАК Назначение,
	|	НовыеДвижения.Сценарий КАК Сценарий,
	|	НовыеДвижения.Номенклатура КАК Номенклатура,
	|	НовыеДвижения.Характеристика КАК Характеристика,
	|	НовыеДвижения.Количество КАК Количество
	|ПОМЕСТИТЬ НовыеДвижения
	|ИЗ
	|	&НовыеДвижения КАК НовыеДвижения
	|ГДЕ
	|	НовыеДвижения.ПланПроизводства = &Ссылка
	|	И НовыеДвижения.СпецификацияПолуфабриката <> ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)");
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ПланПроизводства);
	Запрос.УстановитьПараметр("НовыеДвижения", ЭтотОбъект);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СформироватьОчередьДопланированияПолуфабрикатов(МенеджерВременныхТаблиц, ПланПроизводства)
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОчередьРасчета.ДатаВыпуска,
	|	ОчередьРасчета.ПланПроизводства,
	|	ОчередьРасчета.Подразделение,
	|	ОчередьРасчета.Спецификация,
	|	ОчередьРасчета.Назначение,
	|	ОчередьРасчета.Сценарий,
	|	ОчередьРасчета.Номенклатура,
	|	ОчередьРасчета.Характеристика,
	|	ИСТИНА КАК ВыпускПолуфабрикатов
	|ИЗ
	|	(ВЫБРАТЬ
	|		НовыеДвижения.ПланПроизводства КАК ПланПроизводства,
	|		НовыеДвижения.Номенклатура КАК Номенклатура,
	|		НовыеДвижения.Характеристика КАК Характеристика,
	|		НовыеДвижения.СпецификацияПолуфабриката КАК Спецификация,
	|		НовыеДвижения.Количество КАК КоличествоРазность,
	|		НовыеДвижения.ДатаВыпускаПолуфабриката КАК ДатаВыпуска,
	|		НовыеДвижения.Сценарий КАК Сценарий,
	|		НовыеДвижения.ПодразделениеДиспетчер КАК Подразделение,
	|		НовыеДвижения.Назначение КАК Назначение
	|	ИЗ
	|		НовыеДвижения КАК НовыеДвижения
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СтарыеДвижения.ПланПроизводства,
	|		СтарыеДвижения.Номенклатура,
	|		СтарыеДвижения.Характеристика,
	|		СтарыеДвижения.СпецификацияПолуфабриката,
	|		-СтарыеДвижения.Количество,
	|		СтарыеДвижения.ДатаВыпускаПолуфабриката,
	|		СтарыеДвижения.Сценарий,
	|		СтарыеДвижения.ПодразделениеДиспетчер,
	|		СтарыеДвижения.Назначение
	|	ИЗ
	|		СтарыеДвижения КАК СтарыеДвижения) КАК ОчередьРасчета
	|
	|СГРУППИРОВАТЬ ПО
	|	ОчередьРасчета.ДатаВыпуска,
	|	ОчередьРасчета.ПланПроизводства,
	|	ОчередьРасчета.Подразделение,
	|	ОчередьРасчета.Спецификация,
	|	ОчередьРасчета.Назначение,
	|	ОчередьРасчета.Сценарий,
	|	ОчередьРасчета.Номенклатура,
	|	ОчередьРасчета.Характеристика
	|
	|ИМЕЮЩИЕ
	|	СУММА(ОчередьРасчета.КоличествоРазность) <> 0";
	
	ТаблицаОчередь = Запрос.Выполнить().Выгрузить();
	
	ДополнительныеСвойства.МенеджерВременныхТаблиц.Закрыть();
	
	РегистрыСведений.ОчередьРасчетаПланаПроизводства.ЗаписатьОчередь(ТаблицаОчередь, ПланПроизводства);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
