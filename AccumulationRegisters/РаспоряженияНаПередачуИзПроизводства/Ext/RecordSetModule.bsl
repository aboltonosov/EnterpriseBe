#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)

	Если ОбменДанными.Загрузка Или Не ПроведениеСерверУТ.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	БлокироватьДляИзменения = Истина;

	// Текущее состояние набора помещается во временную таблицу,
	// чтобы при записи получить изменение нового набора относительно текущего.

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Период              КАК Период,
	|	Таблица.КодСтроки           КАК КодСтроки,
	|	Таблица.Распоряжение        КАК Распоряжение,
	|	Таблица.Отправитель         КАК Отправитель,
	|	Таблица.Получатель          КАК Получатель,
	|	Таблица.Номенклатура        КАК Номенклатура,
	|	Таблица.Характеристика      КАК Характеристика,
	|	Таблица.Назначение          КАК Назначение,
	|	Таблица.Операция            КАК Операция,
	|	Таблица.ВидДвижения         КАК ВидДвижения,
	|	ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			Таблица.Количество
	|		ИНАЧЕ
	|			-Таблица.Количество
	|	КОНЕЦ                       КАК КоличествоПередЗаписью
	|ПОМЕСТИТЬ ДвиженияРаспоряженияНаПередачуИзПроизводстваПередЗаписью
	|ИЗ
	|	РегистрНакопления.РаспоряженияНаПередачуИзПроизводства КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый
	|";
	Запрос.Выполнить();

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)

	Если ОбменДанными.Загрузка Или Не ПроведениеСерверУТ.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ИспользоватьДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров =
		ПолучитьФункциональнуюОпцию("ИспользоватьДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров");
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МАКСИМУМ(ТаблицаИзменений.ДатаКонтроляПриЗаписи) КАК ДатаКонтроля,
	|	ТаблицаИзменений.Распоряжение                 КАК Распоряжение,
	|	ТаблицаИзменений.КодСтроки                    КАК КодСтроки,
	|	ТаблицаИзменений.Отправитель                  КАК Отправитель,
	|	ТаблицаИзменений.Получатель                   КАК Получатель,
	|	ТаблицаИзменений.Номенклатура                 КАК Номенклатура,
	|	ТаблицаИзменений.Характеристика               КАК Характеристика,
	|	ТаблицаИзменений.Назначение                   КАК Назначение,
	|	ТаблицаИзменений.Операция                     КАК Операция,
	|	СУММА(ТаблицаИзменений.КоличествоИзменение)   КАК КоличествоИзменение
	|ПОМЕСТИТЬ ДвиженияРаспоряженияНаПередачуИзПроизводстваИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДАТАВРЕМЯ(1,1,1)                КАК ДатаКонтроляПриЗаписи,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ)
	|			ИНАЧЕ КОНЕЦПЕРИОДА(Таблица.Период, МЕСЯЦ)
	|		КОНЕЦ                           КАК ДатаКонтроляПередЗаписью,
	|		Таблица.Распоряжение            КАК Распоряжение,
	|		Таблица.КодСтроки               КАК КодСтроки,
	|		Таблица.Отправитель             КАК Отправитель,
	|		Таблица.Получатель              КАК Получатель,
	|		Таблица.Номенклатура            КАК Номенклатура,
	|		Таблица.Характеристика          КАК Характеристика,
	|		Таблица.Назначение              КАК Назначение,
	|		Таблица.Операция                КАК Операция,
	|		Таблица.КоличествоПередЗаписью  КАК КоличествоИзменение
	|	ИЗ
	|		ДвиженияРаспоряженияНаПередачуИзПроизводстваПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ)
	|			ИНАЧЕ КОНЕЦПЕРИОДА(Таблица.Период, МЕСЯЦ)
	|		КОНЕЦ                           КАК ДатаКонтроляПриЗаписи,
	|		ДАТАВРЕМЯ(1,1,1)                КАК ДатаКонтроляПередЗаписью,
	|		Таблица.Распоряжение            КАК Распоряжение,
	|		Таблица.КодСтроки               КАК КодСтроки,
	|		Таблица.Отправитель             КАК Отправитель,
	|		Таблица.Получатель              КАК Получатель,
	|		Таблица.Номенклатура            КАК Номенклатура,
	|		Таблица.Характеристика          КАК Характеристика,
	|		Таблица.Назначение              КАК Назначение,
	|		Таблица.Операция                КАК Операция,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|				-Таблица.Количество
	|			ИНАЧЕ
	|				Таблица.Количество
	|		КОНЕЦ                           КАК КоличествоИзменение
	|	ИЗ
	|		РегистрНакопления.РаспоряженияНаПередачуИзПроизводства КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Распоряжение,
	|	ТаблицаИзменений.КодСтроки,
	|	ТаблицаИзменений.Отправитель,
	|	ТаблицаИзменений.Получатель,
	|	ТаблицаИзменений.Номенклатура,
	|	ТаблицаИзменений.Характеристика,
	|	ТаблицаИзменений.Назначение,
	|	ТаблицаИзменений.Операция
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.КоличествоИзменение) > 0
	|		ИЛИ МАКСИМУМ(ТаблицаИзменений.ДатаКонтроляПриЗаписи) <> МАКСИМУМ(ТаблицаИзменений.ДатаКонтроляПередЗаписью)
	|;
	|УНИЧТОЖИТЬ ДвиженияРаспоряженияНаПередачуИзПроизводстваПередЗаписью
	|";
	
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	Выборка.Следующий();
	
	// Новые изменения были помещены во временную таблицу.
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияРаспоряженияНаПередачуИзПроизводстваИзменение", Выборка.Количество > 0);
	
	Если ИспользоватьДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров Тогда
		
		Регистратор = Отбор.Регистратор.Значение;
		
		СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
		
		Если СтруктураВременныеТаблицы.ДвиженияРаспоряженияНаПередачуИзПроизводстваИзменение Тогда
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров",
				Константы.ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров.Получить());
			Запрос.УстановитьПараметр("МерныеТипыЕдиницИзмерений",
				Справочники.УпаковкиЕдиницыИзмерения.МерныеТипыЕдиницИзмерений());
			Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
			
			ТекстЗапроса = 
			"ВЫБРАТЬ
			|	Таблица.Отправитель,
			|	Таблица.Получатель,
			|	Таблица.Распоряжение,
			|	Таблица.Номенклатура,
			|	Таблица.Характеристика,
			|	Таблица.Назначение,
			|	Таблица.Операция,
			|	Таблица.КодСтроки
			|ПОМЕСТИТЬ ДвиженияРаспоряженияНаПередачуИзПроизводстваИзменениеМерныеТовары
			|ИЗ
			|	ДвиженияРаспоряженияНаПередачуИзПроизводстваИзменение КАК Таблица
			|ГДЕ 
			|	Таблица.Номенклатура.ЕдиницаИзмерения.ТипИзмеряемойВеличины В (&МерныеТипыЕдиницИзмерений)
			|СГРУППИРОВАТЬ ПО
			|	Таблица.Отправитель,
			|	Таблица.Получатель,
			|	Таблица.Распоряжение,
			|	Таблица.Номенклатура,
			|	Таблица.Характеристика,
			|	Таблица.Назначение,
			|	Таблица.Операция,
			|	Таблица.КодСтроки
			|;
			|////////////////////////////////////////////////////////////////////////////////
			|
			//Основная таблица
			|ВЫБРАТЬ
			|	Таблица.ВидДвижения    КАК ВидДвижения,
			|	Таблица.Отправитель    КАК Отправитель,
			|	Таблица.Получатель     КАК Получатель,
			|	Таблица.Распоряжение   КАК Распоряжение,
			|	Таблица.Номенклатура   КАК Номенклатура,
			|	Таблица.Характеристика КАК Характеристика,
			|	Таблица.Назначение     КАК Назначение,
			|	Таблица.Операция       КАК Операция,
			|	Таблица.КодСтроки      КАК КодСтроки,
			|	Таблица.Количество     КАК Количество
			|ПОМЕСТИТЬ ВТРаспоряженияНаПередачуИзПроизводства
			|ИЗ
			|	ДвиженияРаспоряженияНаПередачуИзПроизводстваИзменениеМерныеТовары КАК Изменения
			|	ЛЕВОЕ СОЕДИНЕНИЕ
			|		РегистрНакопления.РаспоряженияНаПередачуИзПроизводства КАК Таблица
			|		ПО Таблица.Отправитель       = Изменения.Отправитель
			|			И Таблица.Получатель     = Изменения.Получатель
			|			И Таблица.Распоряжение   = Изменения.Распоряжение
			|			И Таблица.Номенклатура   = Изменения.Номенклатура
			|			И Таблица.Характеристика = Изменения.Характеристика
			|			И Таблица.Назначение     = Изменения.Назначение
			|			И Таблица.Операция       = Изменения.Операция
			|			И Таблица.КодСтроки      = Изменения.КодСтроки
			|ГДЕ
			|	НЕ Изменения.Распоряжение ЕСТЬ NULL
			|;
			|////////////////////////////////////////////////////////////////////////////////
			|
			//Допустимые отклонения
			|ВЫБРАТЬ
			|	Таблица.Отправитель    КАК Отправитель,
			|	Таблица.Получатель     КАК Получатель,
			|	Таблица.Распоряжение   КАК Распоряжение,
			|	Таблица.Номенклатура   КАК Номенклатура,
			|	Таблица.Характеристика КАК Характеристика,
			|	Таблица.Назначение     КАК Назначение,
			|	Таблица.Операция       КАК Операция,
			|	Таблица.КодСтроки      КАК КодСтроки,
			|	СУММА(Таблица.Количество
			|		* (&ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров / 100)) КАК ДопустимоеОтклонение
			|ПОМЕСТИТЬ ВТДопустимыеОтклоненияРаспоряженияНаПередачуИзПроизводства
			|ИЗ
			|	ВТРаспоряженияНаПередачуИзПроизводства КАК Таблица
			|ГДЕ
			|	Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
			|СГРУППИРОВАТЬ ПО
			|	Таблица.Отправитель,
			|	Таблица.Получатель,
			|	Таблица.Распоряжение,
			|	Таблица.Номенклатура,
			|	Таблица.Характеристика,
			|	Таблица.Назначение,
			|	Таблица.Операция,
			|	Таблица.КодСтроки
			|ИНДЕКСИРОВАТЬ ПО
			|	Отправитель,
			|	Получатель,
			|	Распоряжение,
			|	Номенклатура,
			|	Характеристика,
			|	Назначение,
			|	Операция,
			|	КодСтроки
			|;
			|////////////////////////////////////////////////////////////////////////////////
			|
			//Остатки
			|ВЫБРАТЬ
			|	Таблица.Отправитель    КАК Отправитель,
			|	Таблица.Получатель     КАК Получатель,
			|	Таблица.Распоряжение   КАК Распоряжение,
			|	Таблица.Номенклатура   КАК Номенклатура,
			|	Таблица.Характеристика КАК Характеристика,
			|	Таблица.Назначение     КАК Назначение,
			|	Таблица.Операция       КАК Операция,
			|	Таблица.КодСтроки      КАК КодСтроки,
			|	СУММА(ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
			|			Таблица.Количество
			|		ИНАЧЕ
			|			-Таблица.Количество
			|	КОНЕЦ)                     КАК КоличествоОстаток
			|ПОМЕСТИТЬ ВТРаспоряженияНаПередачуИзПроизводстваОстатки
			|ИЗ
			|	ВТРаспоряженияНаПередачуИзПроизводства КАК Таблица
			|СГРУППИРОВАТЬ ПО
			|	Таблица.Отправитель,
			|	Таблица.Получатель,
			|	Таблица.Распоряжение,
			|	Таблица.Номенклатура,
			|	Таблица.Характеристика,
			|	Таблица.Назначение,
			|	Таблица.Операция,
			|	Таблица.КодСтроки
			|;
			|////////////////////////////////////////////////////////////////////////////////
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Таблица.Распоряжение КАК Распоряжение
			|ИЗ
			|	ВТРаспоряженияНаПередачуИзПроизводстваОстатки КАК Таблица
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
			|		ВТДопустимыеОтклоненияРаспоряженияНаПередачуИзПроизводства КАК ДопустимыеОтклонения
			|		ПО
			|			Таблица.Отправитель             = ДопустимыеОтклонения.Отправитель
			|			И Таблица.Получатель            = ДопустимыеОтклонения.Получатель
			|			И Таблица.Распоряжение          = ДопустимыеОтклонения.Распоряжение
			|			И Таблица.Номенклатура          = ДопустимыеОтклонения.Номенклатура
			|			И Таблица.Характеристика        = ДопустимыеОтклонения.Характеристика
			|			И Таблица.Назначение            = ДопустимыеОтклонения.Назначение
			|			И Таблица.Операция              = ДопустимыеОтклонения.Операция
			|			И Таблица.КодСтроки             = ДопустимыеОтклонения.КодСтроки
			|ГДЕ
			|	Таблица.КоличествоОстаток <= ДопустимыеОтклонения.ДопустимоеОтклонение
			|	И Таблица.КоличествоОстаток >= -ДопустимыеОтклонения.ДопустимоеОтклонение";
			
			Запрос.Текст = ТекстЗапроса;
			
			ВыборкаЗаказ = Запрос.Выполнить().Выбрать();
			
			Пока ВыборкаЗаказ.Следующий() Цикл
				
				РегистрыСведений.ОчередьЗаказовККорректировкеСтрокМерныхТоваров.ДобавитьЗаказВОчередь(ВыборкаЗаказ.Распоряжение);
				
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли