#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
	 ИЛИ УниверсальныеМеханизмыПартийИСебестоимости.ДвиженияЗаписываютсяРасчетомПартийИСебестоимости(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	УниверсальныеМеханизмыПартийИСебестоимости.СохранитьДвиженияСформированныеРасчетомПартийИСебестоимости(ЭтотОбъект, Замещение);
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено
	 ИЛИ НЕ ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ПартииРасходов.Период                       КАК Период,
	|	ПартииРасходов.Регистратор                  КАК Регистратор,
	|	ПартииРасходов.Подразделение                КАК Подразделение,
	|	ПартииРасходов.Организация                  КАК Организация,
	|	ПартииРасходов.СтатьяРасходов               КАК СтатьяРасходов,
	|	ПартииРасходов.АналитикаРасходов            КАК АналитикаРасходов,
	|	ПартииРасходов.ДокументПоступленияРасходов  КАК ДокументПоступленияРасходов,
	|	ПартииРасходов.АналитикаУчетаПартий         КАК АналитикаУчетаПартий,
	|	ПартииРасходов.АналитикаУчетаНоменклатуры   КАК АналитикаУчетаНоменклатуры,
	|	ПартииРасходов.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|	ПартииРасходов.ВидДеятельностиНДС           КАК ВидДеятельностиНДС,
	|	ПартииРасходов.СтоимостьРегл                КАК СтоимостьРегл,
	|	ПартииРасходов.НДСРегл                      КАК НДСРегл
	|
	|ПОМЕСТИТЬ ПартииПрочихРасходовПередЗаписью
	|ИЗ
	|	РегистрНакопления.ПартииПрочихРасходов КАК ПартииРасходов
	|ГДЕ
	|	ПартииРасходов.Регистратор = &Регистратор
	|	И ПартииРасходов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|");
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
	 ИЛИ НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
	 ИЛИ ПланыОбмена.ГлавныйУзел() <> Неопределено
	 ИЛИ УниверсальныеМеханизмыПартийИСебестоимости.ДвиженияЗаписываютсяРасчетомПартийИСебестоимости(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.Период                          КАК Период,
	|	Таблица.Регистратор                     КАК Регистратор,
	|	Таблица.Подразделение                   КАК Подразделение,
	|	Таблица.Организация                     КАК Организация,
	|	Таблица.СтатьяРасходов                  КАК СтатьяРасходов,
	|	Таблица.ВариантРаспределенияРасходов    КАК ВариантРаспределенияРасходов,
	|	Таблица.ВидЦенностиНДС                  КАК ВидЦенностиНДС,
	|	Таблица.АналитикаРасходов               КАК АналитикаРасходов,
	|	Таблица.ДокументПоступленияРасходов     КАК ДокументПоступленияРасходов,
	|	Таблица.АналитикаУчетаПартий            КАК АналитикаУчетаПартий,
	|	Таблица.АналитикаУчетаНоменклатуры      КАК АналитикаУчетаНоменклатуры,
	|	Таблица.НаправлениеДеятельности         КАК НаправлениеДеятельности,
	|	Таблица.ВидДеятельностиНДС              КАК ВидДеятельностиНДС,
	|	СУММА(Таблица.СтоимостьРегл)            КАК СтоимостьРегл,
	|	СУММА(Таблица.НДСРегл)                  КАК НДСРегл
	|ПОМЕСТИТЬ втПартийПрочихРасходовРазница
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Период                          КАК Период,
	|		Таблица.Регистратор                     КАК Регистратор,
	|		Таблица.Подразделение                   КАК Подразделение,
	|		Таблица.Организация                     КАК Организация,
	|		Таблица.СтатьяРасходов                  КАК СтатьяРасходов,
	|		СтатьиРасходов.ВариантРаспределенияРасходов КАК ВариантРаспределенияРасходов,
	|		СтатьиРасходов.ВидЦенностиНДС           КАК ВидЦенностиНДС,
	|		Таблица.АналитикаРасходов               КАК АналитикаРасходов,
	|		Таблица.ДокументПоступленияРасходов     КАК ДокументПоступленияРасходов,
	|		Таблица.АналитикаУчетаПартий            КАК АналитикаУчетаПартий,
	|		Таблица.АналитикаУчетаНоменклатуры      КАК АналитикаУчетаНоменклатуры,
	|		Таблица.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|		Таблица.ВидДеятельностиНДС           КАК ВидДеятельностиНДС,
	|		Таблица.СтоимостьРегл                   КАК СтоимостьРегл,
	|		Таблица.НДСРегл                         КАК НДСРегл
	|	ИЗ
	|		ПартииПрочихРасходовПередЗаписью КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|		ПО
	|			Таблица.СтатьяРасходов =  СтатьиРасходов.Ссылка
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПартииРасходов.Период                       КАК Период,
	|		ПартииРасходов.Регистратор                  КАК Регистратор,
	|		ПартииРасходов.Подразделение                КАК Подразделение,
	|		ПартииРасходов.Организация                  КАК Организация,
	|		ПартииРасходов.СтатьяРасходов               КАК СтатьяРасходов,
	|		СтатьиРасходов.ВариантРаспределенияРасходов КАК ВариантРаспределенияРасходов,
	|		СтатьиРасходов.ВидЦенностиНДС               КАК ВидЦенностиНДС,
	|		ПартииРасходов.АналитикаРасходов            КАК АналитикаРасходов,
	|		ПартииРасходов.ДокументПоступленияРасходов  КАК ДокументПоступленияРасходов,
	|		ПартииРасходов.АналитикаУчетаПартий         КАК АналитикаУчетаПартий,
	|		ПартииРасходов.АналитикаУчетаНоменклатуры   КАК АналитикаУчетаНоменклатуры,
	|		ПартииРасходов.НаправлениеДеятельности      КАК НаправлениеДеятельности,
	|		ПартииРасходов.ВидДеятельностиНДС           КАК ВидДеятельностиНДС,
	|		-ПартииРасходов.СтоимостьРегл               КАК СтоимостьРегл,
	|		-ПартииРасходов.НДСРегл                     КАК НДСРегл
	|	ИЗ
	|		РегистрНакопления.ПартииПрочихРасходов КАК ПартииРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|		ПО
	|			ПартииРасходов.СтатьяРасходов =  СтатьиРасходов.Ссылка
	|	ГДЕ
	|		ПартииРасходов.Регистратор = &Регистратор
	|		И ПартииРасходов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|
	|) КАК Таблица
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Период,
	|	Таблица.Регистратор,
	|	Таблица.Организация,
	|	Таблица.Подразделение,
	|	Таблица.СтатьяРасходов,
	|	Таблица.ВариантРаспределенияРасходов,
	|	Таблица.ВидЦенностиНДС,
	|	Таблица.АналитикаРасходов,
	|	Таблица.ДокументПоступленияРасходов,
	|	Таблица.АналитикаУчетаПартий,
	|	Таблица.АналитикаУчетаНоменклатуры,
	|	Таблица.НаправлениеДеятельности,
	|	Таблица.ВидДеятельностиНДС
	|;
	|
	|///////////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ) КАК МЕСЯЦ,
	|	Таблица.Организация                  КАК Организация,
	|	Таблица.Регистратор                  КАК Документ,
	|	ЛОЖЬ                                 КАК ИзмененыДанныеДляПартионногоУчетаВерсии21
	|ПОМЕСТИТЬ ПартииПрочихРасходовЗаданияКРасчетуСебестоимости
	|ИЗ
	|	втПартийПрочихРасходовРазница КАК Таблица
	|	
	|ГДЕ
	|	Таблица.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(Таблица.Период, КВАРТАЛ), МЕСЯЦ)    КАК Месяц,
	|	ЗНАЧЕНИЕ(Перечисление.ОперацииЗакрытияМесяца.РаспределениеНДС) КАК Операция,
	|	Таблица.Организация    КАК Организация,
	|	Таблица.Регистратор    КАК Документ
	|ПОМЕСТИТЬ ПартииПрочихРасходовЗаданияКЗакрытиюМесяца
	|ИЗ
	|	втПартийПрочихРасходовРазница КАК Таблица
	|ГДЕ
	|	ЕСТЬNULL(Таблица.ВариантРаспределенияРасходов, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|
	//++ НЕ УТ
	|
	|ОБЪЕДИНИТЬ
	|	
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(СостоянияОСОрганизаций.Период, МЕСЯЦ) >= НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ)
	|			ТОГДА НАЧАЛОПЕРИОДА(СостоянияОСОрганизаций.Период, МЕСЯЦ)
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ)
	|	КОНЕЦ КАК Месяц,
	|	ЗНАЧЕНИЕ(Перечисление.ОперацииЗакрытияМесяца.РаспределениеНДС) КАК Операция,
	|	Таблица.Организация    КАК Организация,
	|	Таблица.Регистратор    КАК Документ
	|ИЗ
	|	втПартийПрочихРасходовРазница КАК Таблица
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СостоянияОСОрганизаций КАК СостоянияОСОрганизаций
	|	ПО
	|		Таблица.АналитикаРасходов = СостоянияОСОрганизаций.ОсновноеСредство
	|		И Таблица.Организация = СостоянияОСОрганизаций.Организация
	|		И СостоянияОСОрганизаций.Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС
	|		И СостоянияОСОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияОС.ПринятоКУчету)
	|ГДЕ
	|	Таблица.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы) 
	|
	|ОБЪЕДИНИТЬ
	|	
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР 
	|		КОГДА НАЧАЛОПЕРИОДА(СостоянияНМАОрганизаций.Период, МЕСЯЦ) >= НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ)
	|			ТОГДА НАЧАЛОПЕРИОДА(СостоянияНМАОрганизаций.Период, МЕСЯЦ)
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ)
	|	КОНЕЦ КАК Месяц,
	|	ЗНАЧЕНИЕ(Перечисление.ОперацииЗакрытияМесяца.РаспределениеНДС) КАК Операция,
	|	Таблица.Организация    КАК Организация,
	|	Таблица.Регистратор    КАК Документ
	|ИЗ
	|	втПартийПрочихРасходовРазница КАК Таблица
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СостоянияНМАОрганизаций КАК СостоянияНМАОрганизаций
	|	ПО
	|		Таблица.АналитикаРасходов = СостоянияНМАОрганизаций.НематериальныйАктив
	|		И Таблица.Организация = СостоянияНМАОрганизаций.Организация
	|		И СостоянияНМАОрганизаций.Регистратор ССЫЛКА Документ.ПринятиеКУчетуНМА
	|		И СостоянияНМАОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.ПринятКУчету)
	|ГДЕ
	|	Таблица.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы) 
	//-- НЕ УТ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПартииПрочихРасходовПередЗаписью
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втПартийПрочихРасходовРазница
	|");
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
