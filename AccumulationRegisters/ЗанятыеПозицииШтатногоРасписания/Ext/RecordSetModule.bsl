#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Пропустить = Ложь;
	Если ЭтотОбъект.ДополнительныеСвойства.Свойство("ПропуститьОбновлениеУсловныхДвижений", Пропустить) И Пропустить Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ТекущийНабор", ЭтотОбъект);
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТекущийНабор.Период КАК Период,
		|	ТекущийНабор.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
		|	ТекущийНабор.КоличествоСтавок КАК КоличествоСтавок,
		|	ТекущийНабор.Сотрудник КАК Сотрудник,
		|	ТекущийНабор.УсловноеДвижение КАК УсловноеДвижение,
		|	ТекущийНабор.ДатаОтменыУсловногоДвижения КАК ДатаОтменыУсловногоДвижения,
		|	ТекущийНабор.ВидДвижения КАК ВидДвижения
		|ПОМЕСТИТЬ ВТТекущийНабор
		|ИЗ
		|	&ТекущийНабор КАК ТекущийНабор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПредыдущийНабор.Сотрудник КАК Сотрудник,
		|	ПредыдущийНабор.Период КАК Период,
		|	ПредыдущийНабор.ДатаОтменыУсловногоДвижения КАК ДатаОтменыУсловногоДвижения,
		|	ПредыдущийНабор.УсловноеДвижение КАК УсловноеДвижение
		|ПОМЕСТИТЬ ВТНаборРегистратора
		|ИЗ
		|	РегистрНакопления.ЗанятыеПозицииШтатногоРасписания КАК ПредыдущийНабор
		|ГДЕ
		|	ПредыдущийНабор.Регистратор = &Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПредыдущийНабор.Сотрудник КАК Сотрудник,
		|	ПредыдущийНабор.Период КАК Период,
		|	ПредыдущийНабор.ДатаОтменыУсловногоДвижения КАК ДатаОтменыУсловногоДвижения,
		|	ИСТИНА КАК УдаляемаяЗапись
		|ПОМЕСТИТЬ ВТИзмененныеПозиции
		|ИЗ
		|	ВТНаборРегистратора КАК ПредыдущийНабор
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТекущийНабор КАК ТекущийНабор
		|		ПО (ТекущийНабор.Период = ПредыдущийНабор.Период)
		|			И (ТекущийНабор.Сотрудник = ПредыдущийНабор.Сотрудник)
		|			И (ТекущийНабор.УсловноеДвижение = ПредыдущийНабор.УсловноеДвижение)
		|			И (ТекущийНабор.ДатаОтменыУсловногоДвижения = ПредыдущийНабор.ДатаОтменыУсловногоДвижения)
		|ГДЕ
		|	ТекущийНабор.Сотрудник ЕСТЬ NULL 
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТекущийНабор.Сотрудник,
		|	ТекущийНабор.Период,
		|	ТекущийНабор.ДатаОтменыУсловногоДвижения,
		|	ЛОЖЬ
		|ИЗ
		|	ВТТекущийНабор КАК ТекущийНабор";
	
	Запрос.Выполнить();
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДругиеНаборы.Регистратор КАК Регистратор,
		|	ДругиеНаборы.НомерСтроки,
		|	ДругиеНаборы.УсловноеКоличествоСтавок,
		|	ВЫБОР
		|		КОГДА ИзмененныеПозиции.УдаляемаяЗапись
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ОчисткаРесурса
		|ИЗ
		|	ВТИзмененныеПозиции КАК ИзмененныеПозиции
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЗанятыеПозицииШтатногоРасписания КАК ДругиеНаборы
		|		ПО (ДругиеНаборы.Период > ИзмененныеПозиции.Период)
		|			И (ДругиеНаборы.ДатаОтменыУсловногоДвижения < ИзмененныеПозиции.Период)
		|			И (ДругиеНаборы.Сотрудник = ИзмененныеПозиции.Сотрудник)
		|ГДЕ
		|	ДругиеНаборы.Регистратор <> &Регистратор
		|	И ДругиеНаборы.УсловноеДвижение
		|ИТОГИ ПО
		|	Регистратор";
	
	// Перепишем движения других регистраторов.
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Выборка.Следующий() Цикл
		
		НаборЗаписей = РегистрыНакопления.ЗанятыеПозицииШтатногоРасписания.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		НаборЗаписей.Прочитать();
		
		ЗаписатьНабор = Ложь;
		ВыборкаНомеровСтрок = Выборка.Выбрать();
		Пока ВыборкаНомеровСтрок.Следующий() Цикл
			
			Если ВыборкаНомеровСтрок.ОчисткаРесурса Тогда
				КоличествоСтавок = 0;
			Иначе
				КоличествоСтавок = ВыборкаНомеровСтрок.УсловноеКоличествоСтавок;
			КонецЕсли;
			
			Если НаборЗаписей[ВыборкаНомеровСтрок.НомерСтроки - 1].КоличествоСтавок <> КоличествоСтавок Тогда
				
				ЗаписатьНабор = Истина;
				НаборЗаписей[ВыборкаНомеровСтрок.НомерСтроки - 1].КоличествоСтавок = КоличествоСтавок;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если ЗаписатьНабор Тогда
			
			НаборЗаписей.ДополнительныеСвойства.Вставить("ПропуститьОбновлениеУсловныхДвижений", Истина);
			НаборЗаписей.ДополнительныеСвойства.Вставить("ОтключитьПроверкуДатыЗапретаИзменения", Истина);
			НаборЗаписей.Записать();
			
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТекущийНабор.Период КАК Период,
		|	ТекущийНабор.Сотрудник КАК Сотрудник
		|ИЗ
		|	ВТТекущийНабор КАК ТекущийНабор
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗанятыеПозицииШтатногоРасписания КАК ДругиеНаборы
		|		ПО (ДругиеНаборы.Период > ТекущийНабор.ДатаОтменыУсловногоДвижения)
		|			И (ДругиеНаборы.Период < ТекущийНабор.Период)
		|			И (ДругиеНаборы.Сотрудник = ТекущийНабор.Сотрудник)
		|ГДЕ
		|	ДругиеНаборы.Регистратор <> &Регистратор
		|	И НЕ ДругиеНаборы.УсловноеДвижение
		|	И ТекущийНабор.УсловноеДвижение";
	
	// Обнулим ресурс в текущем объекте.
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Для Каждого Строка Из ЭтотОбъект Цикл
			
			Если Строка.Сотрудник = Выборка.Сотрудник И Строка.Период = Выборка.Период И Строка.УсловноеДвижение Тогда
				Строка.КоличествоСтавок = 0;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли