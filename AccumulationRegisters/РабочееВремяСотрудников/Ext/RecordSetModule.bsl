#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("НаборЗаписей", ЭтотОбъект.Выгрузить());
	Запрос.УстановитьПараметр("Регистратор", ЭтотОбъект.Отбор.Регистратор.Значение);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НаборЗаписей.Сотрудник,
	|	НаборЗаписей.Период,
	|	НаборЗаписей.ВидУчетаВремени,
	|	НаборЗаписей.Дней,
	|	НаборЗаписей.Часов,
	|	НаборЗаписей.План,
	|	НаборЗаписей.Внутрисменное,
	|	НаборЗаписей.ВЦеломЗаПериод,
	|	НаборЗаписей.ВидВремениВытесняемый,
	|	НаборЗаписей.ИтоговыеДанные,
	|	НаборЗаписей.ПериодРегистрации,
	|	НаборЗаписей.Территория,
	|	НаборЗаписей.УсловияТруда
	|ПОМЕСТИТЬ ВТНовыйНаборЗаписей
	|ИЗ
	|	&НаборЗаписей КАК НаборЗаписей
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РабочееВремяСотрудников.Период,
	|	РабочееВремяСотрудников.Регистратор,
	|	РабочееВремяСотрудников.НомерСтроки,
	|	РабочееВремяСотрудников.Активность,
	|	РабочееВремяСотрудников.Сотрудник,
	|	РабочееВремяСотрудников.ВидУчетаВремени,
	|	РабочееВремяСотрудников.Дней,
	|	РабочееВремяСотрудников.Часов,
	|	РабочееВремяСотрудников.План,
	|	РабочееВремяСотрудников.Внутрисменное,
	|	РабочееВремяСотрудников.ВЦеломЗаПериод,
	|	РабочееВремяСотрудников.ВидВремениВытесняемый,
	|	РабочееВремяСотрудников.ИтоговыеДанные,
	|	РабочееВремяСотрудников.ПериодРегистрации,
	|	РабочееВремяСотрудников.Территория,
	|	РабочееВремяСотрудников.УсловияТруда
	|ПОМЕСТИТЬ ВТСтарыйНаборЗаписей
	|ИЗ
	|	РегистрНакопления.РабочееВремяСотрудников КАК РабочееВремяСотрудников
	|ГДЕ
	|	РабочееВремяСотрудников.Регистратор = &Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР
	|		КОГДА СтарыйНаборЗаписей.Сотрудник ЕСТЬ NULL
	|			ТОГДА НовыйНаборЗаписей.Сотрудник
	|		ИНАЧЕ СтарыйНаборЗаписей.Сотрудник
	|	КОНЕЦ КАК Сотрудник,
	|	ВЫБОР
	|		КОГДА СтарыйНаборЗаписей.Сотрудник ЕСТЬ NULL
	|			ТОГДА НАЧАЛОПЕРИОДА(НовыйНаборЗаписей.Период, МЕСЯЦ)
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(СтарыйНаборЗаписей.Период, МЕСЯЦ)
	|	КОНЕЦ КАК Месяц,
	|	ВЫБОР
	|		КОГДА СтарыйНаборЗаписей.Сотрудник ЕСТЬ NULL
	|			ТОГДА НовыйНаборЗаписей.ПериодРегистрации
	|		ИНАЧЕ СтарыйНаборЗаписей.ПериодРегистрации
	|	КОНЕЦ КАК ПериодРегистрации
	|ПОМЕСТИТЬ ВТПериодыСотрудников
	|ИЗ
	|	ВТНовыйНаборЗаписей КАК НовыйНаборЗаписей
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВТСтарыйНаборЗаписей КАК СтарыйНаборЗаписей
	|		ПО НовыйНаборЗаписей.Сотрудник = СтарыйНаборЗаписей.Сотрудник
	|			И НовыйНаборЗаписей.Период = СтарыйНаборЗаписей.Период
	|			И НовыйНаборЗаписей.ВидУчетаВремени = СтарыйНаборЗаписей.ВидУчетаВремени
	|			И НовыйНаборЗаписей.Дней = СтарыйНаборЗаписей.Дней
	|			И НовыйНаборЗаписей.Часов = СтарыйНаборЗаписей.Часов
	|			И НовыйНаборЗаписей.План = СтарыйНаборЗаписей.План
	|			И НовыйНаборЗаписей.Внутрисменное = СтарыйНаборЗаписей.Внутрисменное
	|			И НовыйНаборЗаписей.ВЦеломЗаПериод = СтарыйНаборЗаписей.ВЦеломЗаПериод
	|			И НовыйНаборЗаписей.ВидВремениВытесняемый = СтарыйНаборЗаписей.ВидВремениВытесняемый
	|			И НовыйНаборЗаписей.ИтоговыеДанные = СтарыйНаборЗаписей.ИтоговыеДанные
	|			И НовыйНаборЗаписей.ПериодРегистрации = СтарыйНаборЗаписей.ПериодРегистрации
	|			И НовыйНаборЗаписей.Территория = СтарыйНаборЗаписей.Территория
	|			И НовыйНаборЗаписей.УсловияТруда = СтарыйНаборЗаписей.УсловияТруда
	|ГДЕ
	|	(НовыйНаборЗаписей.Сотрудник ЕСТЬ NULL
	|			ИЛИ СтарыйНаборЗаписей.Сотрудник ЕСТЬ NULL)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПериодыСотрудников.Сотрудник,
	|	ПериодыСотрудников.Месяц,
	|	ГрафикиРаботыПоВидамВремени.ПериодРегистрации
	|ИЗ
	|	ВТПериодыСотрудников КАК ПериодыСотрудников
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикиРаботыПоВидамВремени КАК ГрафикиРаботыПоВидамВремени
	|		ПО ПериодыСотрудников.Сотрудник = ГрафикиРаботыПоВидамВремени.ГрафикРаботы
	|			И ПериодыСотрудников.Месяц = ГрафикиРаботыПоВидамВремени.Месяц
	|			И ПериодыСотрудников.ПериодРегистрации <= ГрафикиРаботыПоВидамВремени.ПериодРегистрации";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	НаборЗаписей = РегистрыСведений.ГрафикиРаботыПоВидамВремени.СоздатьНаборЗаписей();
	Пока Выборка.Следующий() Цикл
		НаборЗаписей.Отбор.ГрафикРаботы.Установить(Выборка.Сотрудник);
		НаборЗаписей.Отбор.Месяц.Установить(Выборка.Месяц);	
		НаборЗаписей.Отбор.ПериодРегистрации.Установить(Выборка.ПериодРегистрации);
		НаборЗаписей.Записать();
	КонецЦикла;	
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли