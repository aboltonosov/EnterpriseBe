#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция ДополнитьНаборЗаписейНачислениямиСовместителейИПодработок(НаборЗаписей, ДобавленныеЗаписи = НеОпределено, ДублироватьСовместителей = Истина, ДублироватьПодработки = Истина) Экспорт
	
	ЗаписиДобавлялись = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("НаборЗаписейНачисления", НаборЗаписей.Выгрузить());
	Запрос.УстановитьПараметр("ДублироватьСовместителей", ДублироватьСовместителей);
	Запрос.УстановитьПараметр("ДублироватьПодработки", ДублироватьПодработки);
	
	Запрос.Текст =
	
	"ВЫБРАТЬ
	|	Начисления.ПериодРегистрации,
	|	Начисления.Регистратор,
	|	Начисления.НомерСтроки,
	|	Начисления.ВидРасчета,
	|	Начисления.ПериодДействия,
	|	Начисления.ПериодДействияНачало,
	|	Начисления.ПериодДействияКонец,
	|	Начисления.БазовыйПериодНачало,
	|	Начисления.БазовыйПериодКонец,
	|	Начисления.Активность,
	|	Начисления.Сторно,
	|	Начисления.Сотрудник,
	|	Начисления.ФизическоеЛицо,
	|	Начисления.ГоловнаяОрганизация,
	|	Начисления.Результат,
	|	Начисления.ОтработаноДней,
	|	Начисления.ОтработаноЧасов,
	|	Начисления.РезультатВТомЧислеЗаСчетФБ,
	|	Начисления.ГрафикРаботы,
	|	Начисления.ВидУчетаВремени,
	|	Начисления.ВремяВЧасах,
	|	Начисления.ГрафикРаботыНорма,
	|	Начисления.ОбщийГрафик,
	|	Начисления.Организация,
	|	Начисления.ФиксСтрока,
	|	Начисления.ФиксЗаполнение,
	|	Начисления.ФиксРасчетВремени,
	|	Начисления.ФиксРасчет,
	|	Начисления.РасчетнаяБазаЗаЕдиницуНормыВремени,
	|	Начисления.ИдентификаторСтроки,
	|	Начисления.ПериодРегистрацииВремени,
	|	Начисления.ПериодРегистрацииНормыВремени,
	|	Начисления.ДоляРезультата
	|ПОМЕСТИТЬ ВТВсеНачисленияНабора
	|ИЗ
	|	&НаборЗаписейНачисления КАК Начисления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.Ссылка КАК ВидРасчета
	|ПОМЕСТИТЬ ВТКонтролируемыеНачисления
	|ИЗ
	|	ПланВидовРасчета.Начисления КАК Начисления
	|ГДЕ
	|	(ВЫБОР
	|				КОГДА &ДублироватьСовместителей
	|						ИЛИ &ДублироватьПодработки
	|					ТОГДА Начисления.ДублироватьДляВнутреннихСовместителейИПодработок
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ
	|			ИЛИ ВЫБОР
	|				КОГДА &ДублироватьПодработки
	|					ТОГДА Начисления.ДублироватьДляПодработок
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.ПериодРегистрации,
	|	Начисления.Регистратор,
	|	Начисления.НомерСтроки,
	|	Начисления.ВидРасчета,
	|	Начисления.ПериодДействия,
	|	Начисления.ПериодДействияНачало,
	|	Начисления.ПериодДействияКонец,
	|	Начисления.БазовыйПериодНачало,
	|	Начисления.БазовыйПериодКонец,
	|	Начисления.Активность,
	|	Начисления.Сторно,
	|	Начисления.Сотрудник,
	|	Начисления.ФизическоеЛицо,
	|	Начисления.ГоловнаяОрганизация,
	|	Начисления.Результат,
	|	Начисления.ОтработаноДней,
	|	Начисления.ОтработаноЧасов,
	|	Начисления.РезультатВТомЧислеЗаСчетФБ,
	|	Начисления.ГрафикРаботы,
	|	Начисления.ВидУчетаВремени,
	|	Начисления.ВремяВЧасах,
	|	Начисления.ГрафикРаботыНорма,
	|	Начисления.ОбщийГрафик,
	|	Начисления.Организация,
	|	Начисления.ФиксСтрока,
	|	Начисления.ФиксЗаполнение,
	|	Начисления.ФиксРасчетВремени,
	|	Начисления.ФиксРасчет,
	|	Начисления.РасчетнаяБазаЗаЕдиницуНормыВремени,
	|	Начисления.ИдентификаторСтроки,
	|	Начисления.ПериодРегистрацииВремени,
	|	Начисления.ПериодРегистрацииНормыВремени,
	|	Начисления.ДоляРезультата
	|ПОМЕСТИТЬ ВТНачисленияНабора
	|ИЗ
	|	ВТВсеНачисленияНабора КАК Начисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКонтролируемыеНачисления КАК КонтролируемыеНачисления
	|		ПО Начисления.ВидРасчета = КонтролируемыеНачисления.ВидРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(НачисленияНабора.ПериодДействияНачало) КАК НачалоПериода,
	|	МАКСИМУМ(НачисленияНабора.ПериодДействияКонец) КАК ОкончаниеПериода,
	|	НачисленияНабора.Организация
	|ИЗ
	|	ВТНачисленияНабора КАК НачисленияНабора
	|ГДЕ
	|	НачисленияНабора.Сотрудник = ВЫРАЗИТЬ(НачисленияНабора.Сотрудник КАК Справочник.Сотрудники).ГоловнойСотрудник
	|
	|СГРУППИРОВАТЬ ПО
	|	НачисленияНабора.Организация";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		УдалитьВременныеТаблицы = Ложь;
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если УдалитьВременныеТаблицы Тогда
				
				Запрос.Текст =
					"УНИЧТОЖИТЬ ВТФизическиеЛица
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|УНИЧТОЖИТЬ ВТСотрудникиОрганизации
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|УНИЧТОЖИТЬ ВТСотрудникиССовместителями
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|УНИЧТОЖИТЬ ВТНачисленияНабораСНачислениямиСовместителей
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|УНИЧТОЖИТЬ ВТСотрудникиСПодработками";
					
				Запрос.Выполнить();
				
			Иначе
				УдалитьВременныеТаблицы = Истина;
			КонецЕсли;
			
			Запрос.УстановитьПараметр("Организация", Выборка.Организация);
			Запрос.Текст =
				"ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	НачисленияНабора.ФизическоеЛицо
				|ПОМЕСТИТЬ ВТФизическиеЛица
				|ИЗ
				|	ВТНачисленияНабора КАК НачисленияНабора
				|ГДЕ
				|	НачисленияНабора.Сотрудник = НачисленияНабора.Сотрудник.ГоловнойСотрудник";
				
			Запрос.Выполнить();
			
			ПараметрыПолучения = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоВременнойТаблице();
			ЗаполнитьЗначенияСвойств(ПараметрыПолучения, Выборка);
			ПараметрыПолучения.КадровыеДанные = "ВидЗанятости,ГоловнойСотрудник";
			ПараметрыПолучения.ОтбиратьПоГоловнойОрганизации = Истина;
			ПараметрыПолучения.ПодработкиРаботниковПоТрудовымДоговорам = Истина;
			
			КадровыйУчет.СоздатьВТСотрудникиОрганизации(Запрос.МенеджерВременныхТаблиц, Ложь, ПараметрыПолучения);
			
			// Дублирование начислений внутренних совместителей
			Запрос.Текст =
			"ВЫБРАТЬ
			|	СотрудникиОрганизации.Сотрудник,
			|	СотрудникиОрганизацииСовместители.Сотрудник КАК Совместитель
			|ПОМЕСТИТЬ ВТСотрудникиССовместителями
			|ИЗ
			|	ВТСотрудникиОрганизации КАК СотрудникиОрганизации
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиОрганизации КАК СотрудникиОрганизацииСовместители
			|		ПО СотрудникиОрганизации.ФизическоеЛицо = СотрудникиОрганизацииСовместители.ФизическоеЛицо
			|			И (СотрудникиОрганизацииСовместители.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ВнутреннееСовместительство))
			|ГДЕ
			|	СотрудникиОрганизации.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ОсновноеМестоРаботы)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	НачисленияНабора.ПериодРегистрации,
			|	НачисленияНабора.Регистратор,
			|	НачисленияНабора.НомерСтроки,
			|	НачисленияНабора.ВидРасчета,
			|	НачисленияНабора.ПериодДействия,
			|	НачисленияНабора.ПериодДействияНачало,
			|	НачисленияНабора.ПериодДействияКонец,
			|	НачисленияНабора.БазовыйПериодНачало,
			|	НачисленияНабора.БазовыйПериодКонец,
			|	НачисленияНабора.Активность,
			|	НачисленияНабора.Сторно,
			|	НачисленияНабора.Сотрудник,
			|	НачисленияНабора.ФизическоеЛицо,
			|	НачисленияНабора.ГоловнаяОрганизация,
			|	НачисленияНабора.Результат,
			|	НачисленияНабора.ОтработаноДней,
			|	НачисленияНабора.ОтработаноЧасов,
			|	НачисленияНабора.РезультатВТомЧислеЗаСчетФБ,
			|	НачисленияНабора.ГрафикРаботы,
			|	НачисленияНабора.ВидУчетаВремени,
			|	НачисленияНабора.ВремяВЧасах,
			|	НачисленияНабора.ГрафикРаботыНорма,
			|	НачисленияНабора.ОбщийГрафик,
			|	НачисленияНабора.Организация,
			|	НачисленияНабора.ФиксСтрока,
			|	НачисленияНабора.ФиксЗаполнение,
			|	НачисленияНабора.ФиксРасчетВремени,
			|	НачисленияНабора.ФиксРасчет,
			|	НачисленияНабора.РасчетнаяБазаЗаЕдиницуНормыВремени,
			|	НачисленияНабора.ИдентификаторСтроки,
			|	НачисленияНабора.ПериодРегистрацииВремени,
			|	НачисленияНабора.ПериодРегистрацииНормыВремени,
			|	НачисленияНабора.ДоляРезультата,
			|	ЛОЖЬ КАК ДобавленноеНачислениеНабора
			|ПОМЕСТИТЬ ВТНачисленияНабораСНачислениямиСовместителей
			|ИЗ
			|	ВТНачисленияНабора КАК НачисленияНабора
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	НачисленияНабора.ПериодРегистрации,
			|	НачисленияНабора.Регистратор,
			|	НачисленияНабора.НомерСтроки,
			|	НачисленияНабора.ВидРасчета,
			|	НачисленияНабора.ПериодДействия,
			|	НачисленияНабора.ПериодДействияНачало,
			|	НачисленияНабора.ПериодДействияКонец,
			|	НачисленияНабора.БазовыйПериодНачало,
			|	НачисленияНабора.БазовыйПериодКонец,
			|	НачисленияНабора.Активность,
			|	НачисленияНабора.Сторно,
			|	СотрудникиССовместителями.Совместитель,
			|	НачисленияНабора.ФизическоеЛицо,
			|	НачисленияНабора.ГоловнаяОрганизация,
			|	НачисленияНабора.Результат,
			|	НачисленияНабора.ОтработаноДней,
			|	НачисленияНабора.ОтработаноЧасов,
			|	НачисленияНабора.РезультатВТомЧислеЗаСчетФБ,
			|	НачисленияНабора.ГрафикРаботы,
			|	НачисленияНабора.ВидУчетаВремени,
			|	НачисленияНабора.ВремяВЧасах,
			|	НачисленияНабора.ГрафикРаботыНорма,
			|	НачисленияНабора.ОбщийГрафик,
			|	НачисленияНабора.Организация,
			|	НачисленияНабора.ФиксСтрока,
			|	НачисленияНабора.ФиксЗаполнение,
			|	НачисленияНабора.ФиксРасчетВремени,
			|	НачисленияНабора.ФиксРасчет,
			|	НачисленияНабора.РасчетнаяБазаЗаЕдиницуНормыВремени,
			|	НачисленияНабора.ИдентификаторСтроки,
			|	НачисленияНабора.ПериодРегистрацииВремени,
			|	НачисленияНабора.ПериодРегистрацииНормыВремени,
			|	НачисленияНабора.ДоляРезультата,
			|	ИСТИНА
			|ИЗ
			|	ВТСотрудникиССовместителями КАК СотрудникиССовместителями
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачисленияНабора КАК НачисленияНабора
			|		ПО СотрудникиССовместителями.Сотрудник = НачисленияНабора.Сотрудник
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияНабора КАК НачисленияНабораСовместителей
			|		ПО СотрудникиССовместителями.Совместитель = НачисленияНабораСовместителей.Сотрудник
			|			И (НачисленияНабора.ВидРасчета = НачисленияНабораСовместителей.ВидРасчета)
			|			И (НачисленияНабора.ПериодДействияНачало = НачисленияНабораСовместителей.ПериодДействияНачало)
			|			И (НачисленияНабора.ПериодДействияКонец = НачисленияНабораСовместителей.ПериодДействияКонец)
			|ГДЕ
			|	НачисленияНабораСовместителей.Сотрудник ЕСТЬ NULL
			|	И ВЫРАЗИТЬ(НачисленияНабора.ВидРасчета КАК ПланВидовРасчета.Начисления).ДублироватьДляВнутреннихСовместителейИПодработок
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	НачисленияНабораСНачислениямиСовместителей.ПериодРегистрации,
			|	НачисленияНабораСНачислениямиСовместителей.Регистратор,
			|	НачисленияНабораСНачислениямиСовместителей.НомерСтроки,
			|	НачисленияНабораСНачислениямиСовместителей.ВидРасчета,
			|	НачисленияНабораСНачислениямиСовместителей.ПериодДействия,
			|	НачисленияНабораСНачислениямиСовместителей.ПериодДействияНачало,
			|	НачисленияНабораСНачислениямиСовместителей.ПериодДействияКонец,
			|	НачисленияНабораСНачислениямиСовместителей.БазовыйПериодНачало,
			|	НачисленияНабораСНачислениямиСовместителей.БазовыйПериодКонец,
			|	НачисленияНабораСНачислениямиСовместителей.Активность,
			|	НачисленияНабораСНачислениямиСовместителей.Сторно,
			|	НачисленияНабораСНачислениямиСовместителей.Сотрудник,
			|	НачисленияНабораСНачислениямиСовместителей.ФизическоеЛицо,
			|	НачисленияНабораСНачислениямиСовместителей.ГоловнаяОрганизация,
			|	0 КАК Результат,
			|	0 КАК ОтработаноДней,
			|	0 КАК ОтработаноЧасов,
			|	0 КАК РезультатВТомЧислеЗаСчетФБ,
			|	НачисленияНабораСНачислениямиСовместителей.ГрафикРаботы,
			|	НачисленияНабораСНачислениямиСовместителей.ВидУчетаВремени,
			|	НачисленияНабораСНачислениямиСовместителей.ВремяВЧасах,
			|	НачисленияНабораСНачислениямиСовместителей.ГрафикРаботыНорма,
			|	НачисленияНабораСНачислениямиСовместителей.Организация,
			|	НачисленияНабораСНачислениямиСовместителей.ФиксСтрока,
			|	НачисленияНабораСНачислениямиСовместителей.ФиксЗаполнение,
			|	НачисленияНабораСНачислениямиСовместителей.ФиксРасчетВремени,
			|	НачисленияНабораСНачислениямиСовместителей.ФиксРасчет,
			|	НачисленияНабораСНачислениямиСовместителей.РасчетнаяБазаЗаЕдиницуНормыВремени,
			|	НачисленияНабораСНачислениямиСовместителей.ИдентификаторСтроки,
			|	НачисленияНабораСНачислениямиСовместителей.ПериодРегистрацииВремени,
			|	НачисленияНабораСНачислениямиСовместителей.ДоляРезультата
			|ИЗ
			|	ВТНачисленияНабораСНачислениямиСовместителей КАК НачисленияНабораСНачислениямиСовместителей
			|ГДЕ
			|	НачисленияНабораСНачислениямиСовместителей.ДобавленноеНачислениеНабора";
				
			Если ДублироватьСовместителей Тогда
				Выборка = Запрос.Выполнить().Выбрать();
				Пока Выборка.Следующий() Цикл
					ЗаписиДобавлялись = Истина;
					НоваяЗапись = НаборЗаписей.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
					Если ДобавленныеЗаписи <> НеОпределено Тогда
						ДобавленныеЗаписи.Добавить(НоваяЗапись);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
			// Дублирование начислений внутренних подработок
			Запрос.Текст =
			"ВЫБРАТЬ
			|	СотрудникиОрганизации.Сотрудник,
			|	СотрудникиОрганизацииПодработки.Сотрудник КАК Подработка
			|ПОМЕСТИТЬ ВТСотрудникиСПодработками
			|ИЗ
			|	ВТСотрудникиОрганизации КАК СотрудникиОрганизации
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиОрганизации КАК СотрудникиОрганизацииПодработки
			|		ПО СотрудникиОрганизации.ФизическоеЛицо = СотрудникиОрганизацииПодработки.ФизическоеЛицо
			|			И СотрудникиОрганизации.Сотрудник = СотрудникиОрганизацииПодработки.ГоловнойСотрудник
			|			И (СотрудникиОрганизацииПодработки.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.Подработка))
			|ГДЕ
			|	СотрудникиОрганизации.ВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.Подработка)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	НачисленияНабора.ПериодРегистрации,
			|	НачисленияНабора.Регистратор,
			|	НачисленияНабора.НомерСтроки,
			|	НачисленияНабора.ВидРасчета,
			|	НачисленияНабора.ПериодДействия,
			|	НачисленияНабора.ПериодДействияНачало,
			|	НачисленияНабора.ПериодДействияКонец,
			|	НачисленияНабора.БазовыйПериодНачало,
			|	НачисленияНабора.БазовыйПериодКонец,
			|	НачисленияНабора.Активность,
			|	НачисленияНабора.Сторно,
			|	СотрудникиСПодработками.Подработка КАК Сотрудник,
			|	НачисленияНабора.ФизическоеЛицо,
			|	НачисленияНабора.ГоловнаяОрганизация,
			|	0 КАК Результат,
			|	0 КАК ОтработаноДней,
			|	0 КАК ОтработаноЧасов,
			|	0 КАК РезультатВТомЧислеЗаСчетФБ,
			|	НачисленияНабора.ГрафикРаботы,
			|	НачисленияНабора.ВидУчетаВремени,
			|	НачисленияНабора.ВремяВЧасах,
			|	НачисленияНабора.ГрафикРаботыНорма,
			|	НачисленияНабора.ОбщийГрафик,
			|	НачисленияНабора.Организация,
			|	НачисленияНабора.ФиксСтрока,
			|	НачисленияНабора.ФиксЗаполнение,
			|	НачисленияНабора.ФиксРасчетВремени,
			|	НачисленияНабора.ФиксРасчет,
			|	НачисленияНабора.РасчетнаяБазаЗаЕдиницуНормыВремени,
			|	НачисленияНабора.ИдентификаторСтроки,
			|	НачисленияНабора.ПериодРегистрацииВремени,
			|	НачисленияНабора.ПериодРегистрацииНормыВремени,
			|	НачисленияНабора.ДоляРезультата
			|ИЗ
			|	ВТСотрудникиСПодработками КАК СотрудникиСПодработками
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачисленияНабораСНачислениямиСовместителей КАК НачисленияНабора
			|		ПО СотрудникиСПодработками.Сотрудник = НачисленияНабора.Сотрудник
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияНабораСНачислениямиСовместителей КАК НачисленияНабораГоловныхСотрудников
			|		ПО СотрудникиСПодработками.Подработка = НачисленияНабораГоловныхСотрудников.Сотрудник
			|			И (НачисленияНабора.ВидРасчета = НачисленияНабораГоловныхСотрудников.ВидРасчета)
			|			И (НачисленияНабора.ПериодДействияНачало = НачисленияНабораГоловныхСотрудников.ПериодДействияНачало)
			|			И (НачисленияНабора.ПериодДействияКонец = НачисленияНабораГоловныхСотрудников.ПериодДействияКонец)
			|ГДЕ
			|	НачисленияНабораГоловныхСотрудников.Сотрудник ЕСТЬ NULL";
			
			Если ДублироватьПодработки Тогда
				Выборка = Запрос.Выполнить().Выбрать();
				Пока Выборка.Следующий() Цикл
					ЗаписиДобавлялись = Истина;
					НоваяЗапись = НаборЗаписей.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
					Если ДобавленныеЗаписи <> НеОпределено Тогда
						ДобавленныеЗаписи.Добавить(НоваяЗапись);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
		
	Возврат ЗаписиДобавлялись;
	
КонецФункции

Функция ЕстьДвиженияПоРегистратору(Регистратор) Экспорт 
	Если Не ЗначениеЗаполнено(Регистратор) Тогда
		Возврат Ложь;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК Поле1
	|ИЗ
	|	РегистрРасчета.Начисления КАК Начисления
	|ГДЕ
	|	Начисления.Регистратор = &Регистратор";
	
	Возврат Не Запрос.Выполнить().Пустой();
КонецФункции	

#КонецОбласти

#КонецЕсли